OBJECT Report 11012859 Process Collective-List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Process Collective-List;
               NOR=Prosesser samlingsfaktura;
               SVE=Behandla samlingsfaktura];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ServSetup.GET;
                   JobsSetup.GET;
                 END;

    OnPostReport=VAR
                   ServiceOrder@1100528604 : Record 11012823;
                   ServiceOrderExtension@1100528605 : Record 11071727;
                   JobLedgerEntry@1100528606 : Record 11072005;
                   InvoiceRange@1100528600 : Text[50];
                   CreditMemoRange@1100528601 : Text[50];
                   InvoiceProposalRange@1100529300 : Text[50];
                   InternalCounter@1100528602 : Integer;
                   DocumentNo@1100528603 : Code[20];
                 BEGIN
                   InternalCounter := InternalChargeMgt.GetNumInternalCharges;

                   IF InternalChargeMgt.GetNumInternalCharges > 0 THEN
                     InternalChargeMgt.PostBatch(1, TempGenJnlLine, TmpIcEntry, DocumentNo);  //DP00847.c

                   JobLedgerEntry.SETCURRENTKEY("Document No.");
                   JobLedgerEntry.SETRANGE("Document No.", DocumentNo);
                   JobLedgerEntry.SETFILTER(Comment, '<>%1', '');
                   JobLedgerEntry.SETRANGE("Service Order No.", '');
                   IF JobLedgerEntry.FINDSET THEN
                     REPEAT
                       IF STRLEN(JobLedgerEntry.Comment) <= MAXSTRLEN(ServiceOrder."No.") THEN
                         IF ServiceOrder.GET(JobLedgerEntry.Comment) THEN BEGIN
                           ServiceOrderExtension.GetServOrderExtension(ServiceOrder."No.");
                           IF ServiceOrderExtension."Master Project" = JobLedgerEntry."Job No." THEN BEGIN
                             JobLedgerEntry."Service Order No." := ServiceOrder."No.";
                             JobLedgerEntry."Service Contract No." := ServiceOrder."Service Contract No.";
                             JobLedgerEntry.Comment := '';
                             JobLedgerEntry.MODIFY;
                           END;
                         END;
                     UNTIL JobLedgerEntry.NEXT = 0;

                   IF (InvCounter = 0) AND (CreditMemoCounter = 0) AND (InternalCounter = 0) AND (InvProposalCounter = 0) THEN //DP00195.c
                     MESSAGE(Text005)
                   ELSE BEGIN
                     // 141210 **** ROT ****<<<
                     UpdateROTForSalesHeader;
                     // 141210 **** ROT ****>>>
                     IF FirstSalesHeaderNo <> '' THEN
                       IF FirstSalesHeaderNo <> LastSalesHeaderNo THEN
                         InvoiceRange := STRSUBSTNO('%1..%2', FirstSalesHeaderNo, LastSalesHeaderNo)
                       ELSE
                         InvoiceRange := FirstSalesHeaderNo;
                     IF FirstSalesCreditMemoNo <> '' THEN
                       IF FirstSalesCreditMemoNo <> LastSalesCreditMemoNo THEN
                         CreditMemoRange := STRSUBSTNO('%1..%2', FirstSalesCreditMemoNo, LastSalesCreditMemoNo)
                       ELSE
                         CreditMemoRange := FirstSalesCreditMemoNo;
                      //DP00195.sn
                      IF FirstInvoiceProposalNo <> '' THEN
                        IF FirstInvoiceProposalNo <> LastInvoiceProposalNo THEN
                          InvoiceProposalRange := STRSUBSTNO('%1..%2', FirstInvoiceProposalNo, LastInvoiceProposalNo)
                        ELSE
                          InvoiceProposalRange := FirstInvoiceProposalNo;
                      //DP00195.en

                     MESSAGE(
                       Text006,
                       InvCounter, InvoiceRange, CreditMemoCounter, CreditMemoRange, InvProposalCounter,InvoiceProposalRange, //DP00195.c
                       LineCounter, InternalCounter);
                   END;

                   OnAfterPostReport(FirstSalesHeaderNo, LastSalesHeaderNo, FirstSalesCreditMemoNo, LastSalesCreditMemoNo, LastServiceOrderNo, FirstInvoiceProposalNo, LastInvoiceProposalNo);
                 END;

  }
  DATASET
  {
    { 8464;    ;DataItem;                    ;
               DataItemTable=Table11071730;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               IF ReleaseCollectiveList THEN
                                 SETFILTER(Status, '<%1', Status::Processed)
                               ELSE
                                 SETRANGE(Status, Status::Released);
                             END;

               OnAfterGetRecord=VAR
                                  ContinueProcessing@1100528600 : Boolean;
                                  PlantLocation@1100529000 : Record 11012554;
                                BEGIN
                                  ContinueProcessing := TRUE;

                                  IF ((("Ending Date" = 0D) OR ("Ending Date" > TODAY)) AND
                                       ("Minimal Invoice Amount" > 0) AND (GetTotalInvoicePrice < "Minimal Invoice Amount")) OR
                                     (("Ending Date" > TODAY) AND ("Minimal Invoice Amount" = 0))
                                  THEN
                                    CurrReport.SKIP;

                                  InsertHeader := TRUE;
                                  //DP00195.sn
                                  IsPlantInternalCharge := FALSE;
                                  PlantProjectNo := '';
                                  CompanyPlantInvoice := '';

                                  IF "Service Collective-List"."Service Order No." <> '' THEN
                                    IF ServiceOrder.GET("Service Collective-List"."Service Order No.") THEN BEGIN
                                      IsPlantInternalCharge := ServiceOrder.IsPlantInternalCharge;
                                      IF IsPlantInternalCharge THEN
                                        ServiceOrder.CheckChargeToProject
                                      ELSE
                                        IF ServiceOrder."Plant Location" <> '' THEN BEGIN
                                          ServiceOrder.CALCFIELDS("Master Project");
                                          IF ServiceOrder."Master Project" = '' THEN BEGIN
                                            PlantLocation.CHANGECOMPANY(ServiceOrder."Plant Company");
                                            PlantLocation.GET(ServiceOrder."Plant Location");
                                            IF PlantLocation."Project No." <> '' THEN BEGIN
                                              PlantProjectNo := PlantLocation."Project No.";
                                              CompanyPlantInvoice := PlantLocation."Company Name";
                                              IF CompanyPlantInvoice = COMPANYNAME THEN
                                                CompanyPlantInvoice := '';
                                            END;
                                          END;
                                        END;
                                    END;
                                  //DP00195.en

                                  IF RecalculateCollectiveList THEN
                                    ForceRecalculate(TRUE);
                                  IF ReleaseCollectiveList AND (Status <> Status::Released) THEN BEGIN
                                    Status := Status::Released; // no validate
                                    MODIFY(TRUE);
                                  END;
                                  IF Status <> Status::Released THEN
                                    CurrReport.SKIP;
                                END;

               OnPostDataItem=VAR
                                SalesCalcDiscount@1100409000 : Codeunit 60;
                              BEGIN
                                ProcessInvoiceDiscountSurcharge(SalesHeaderRec."Document Date");
                                SalesCalcDiscount.CalculateIncDiscForHeader(SalesHeaderRec);
                              END;

               ReqFilterFields=No. }

    { 4731;1   ;DataItem;                    ;
               DataItemTable=Table11071931;
               DataItemTableView=SORTING(Collective List No.,Line No.)
                                 WHERE(Invoiced=CONST(No));
               OnAfterGetRecord=VAR
                                  ServiceContract@1100528600 : Record 11012812;
                                BEGIN
                                  ServiceContract.GET("Service Contract No.");
                                  ServiceContract.TESTFIELD("Bill-to Customer No. (Contr.)");
                                  IF InsertHeader THEN
                                    InsertSalesHeader(
                                      "Service Collective-List", ServiceContract."% Labor", ServiceContract."% to B Account", ServiceContract."Your Reference",
                                      '', 0D, "Service Collective-List"."Service Order No.", ServiceContract."No.",
                                      ServiceContract."Global Dimension 1 Code", ServiceContract."Bill-to Customer No. (Contr.)");
                                  InsertSCInvoicingSalesLine;

                                  Invoiced := TRUE;
                                  MODIFY;
                                END;

               DataItemLink=Collective List No.=FIELD(No.) }

    { 3589;1   ;DataItem;                    ;
               DataItemTable=Table11012825;
               DataItemTableView=SORTING(Collective List No.,Base Service Order No.,Service Order No.,Line No.,Invoiced,Chargeable)
                                 WHERE(Invoiced=CONST(No),
                                       Chargeable=CONST(Yes));
               OnPreDataItem=BEGIN
                               CASE SortingOrderType OF
                                 SortingOrderType::"Line No.":
                                   SETCURRENTKEY("Collective List No.", "Base Service Order No.", "Service Order No.", "Line No.");
                                 SortingOrderType::"Work Order - Cost Object":
                                   SETCURRENTKEY("Collective List No.", "Base Service Order No.", "Service Order No.", "Created by Work Order No.", "Cost Object");
                               END;
                               SalesHeaderRec.LOCKTABLE;
                               SalesLineRec.LOCKTABLE;

                               SETRANGE("Additional Cost");
                               SETRANGE("Charge to Plant Location");
                               IF IsPlantInternalCharge THEN
                                 SETRANGE("Charge to Plant Location", FALSE);
                               IF PlantProjectNo <> '' THEN
                                 SETRANGE("Additional Cost", FALSE);
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrServiceOrderCostPlusEntry := "Service Order Cost Plus Entry";
                                  SOCostPlusEntryOnAfterGetRecor("Service Order Cost Plus Entry");
                                END;

               OnPostDataItem=BEGIN
                                //DP00195
                                "Service Collective-List".CALCFIELDS("Invoice Price");
                                IF "Service Collective-List"."Invoice Price" = 0 THEN BEGIN
                                  "Service Collective-List".Status := "Service Collective-List".Status::Processed;
                                  "Service Collective-List".MODIFY;
                                END;
                                SalesHeaderRec.DetermineRemovalContribution;
                                UpdateInvoicedAndChargable("Service Order Cost Plus Entry");
                              END;

               DataItemLink=Collective List No.=FIELD(No.) }

    { 1100529300;1;DataItem;SO Cost Plus Entry Plant;
               DataItemTable=Table11012825;
               DataItemTableView=SORTING(Collective List No.,Base Service Order No.,Service Order No.,Line No.,Invoiced,Chargeable)
                                 WHERE(Invoiced=CONST(No),
                                       Chargeable=CONST(Yes));
               OnPreDataItem=BEGIN
                               SETRANGE("Additional Cost");
                               SETRANGE("Charge to Plant Location");
                               IF IsPlantInternalCharge THEN
                                 SETRANGE("Charge to Plant Location", TRUE);
                               IF PlantProjectNo <> '' THEN
                                 SETRANGE("Additional Cost", TRUE);

                               //DP00195
                               IF (PlantProjectNo = '') AND NOT IsPlantInternalCharge THEN
                                 CurrReport.BREAK;

                               CASE SortingOrderType OF
                                 SortingOrderType::"Line No.":
                                   SETCURRENTKEY("Collective List No.", "Base Service Order No.", "Service Order No.", "Line No.");
                                 SortingOrderType::"Work Order - Cost Object":
                                   SETCURRENTKEY("Collective List No.", "Base Service Order No.", "Service Order No.", "Created by Work Order No.", "Cost Object");
                               END;
                               IF NOT ISEMPTY THEN BEGIN
                                 SalesHeaderRec.LOCKTABLE;
                                 SalesLineRec.LOCKTABLE;
                                 LastServiceOrderNo := '';
                                 InsertHeader := TRUE;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  //DP00195
                                  CurrServiceOrderCostPlusEntry := "SO Cost Plus Entry Plant";
                                  SOCostPlusEntryOnAfterGetRecor("SO Cost Plus Entry Plant");
                                END;

               OnPostDataItem=BEGIN
                                //DP00195
                                "Service Collective-List".CALCFIELDS("Invoice Price");
                                IF "Service Collective-List"."Invoice Price" = 0 THEN BEGIN
                                  "Service Collective-List".Status := "Service Collective-List".Status::Processed;
                                  "Service Collective-List".MODIFY;
                                END;
                                SalesHeaderRec.DetermineRemovalContribution;
                                UpdateInvoicedAndChargable("SO Cost Plus Entry Plant");
                              END;

               DataItemLink=Collective List No.=FIELD(No.) }

    { 1100525000;1;DataItem;UsedForCommit    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnAfterGetRecord=BEGIN
                                  IF InternalChargeMgt.GetNumInternalCharges = 0 THEN //T007417
                                    COMMIT;
                                  CurrReport.SKIP;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100528600;;Container;
                  ContainerType=ContentArea }

      { 1100528601;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528602;2;Field  ;
                  CaptionML=[ENU=Sort by;
                             SVE=Sortera efter];
                  OptionCaptionML=[ENU=Line No.,Work Order No. - Cost Object;
                                   SVE=Radnr.,Arbetsordernr. kostnadsobjekt];
                  SourceExpr=SortingOrderType;
                  OnValidate=VAR
                               JobsSetup@1100528600 : Record 315;
                             BEGIN
                               IF SortingOrderType = SortingOrderType::"Work Order - Cost Object" THEN
                                 JobsSetup.TestIsFSAActive;
                             END;
                              }

      { 1100528603;2;Field  ;
                  CaptionML=[ENU=Recalculatie;
                             SVE=Omber„kna samlingsfaktura];
                  SourceExpr=RecalculateCollectiveList }

      { 1100528604;2;Field  ;
                  CaptionML=[ENU=Release;
                             NOR=Frigi;
                             SVE=Sl„pp];
                  SourceExpr=ReleaseCollectiveList;
                  OnValidate=BEGIN
                               IF ReleaseCollectiveList THEN
                                 RecalculateCollectiveList := TRUE;
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text005@11012023 : TextConst 'ENU=No Invoices Created.;NOR=Ingen fakturaer er opprettet.;SVE=Inga fakturor har skapats.';
      Text006@11012024 : TextConst 'ENU=%1 Invoices (%2), %3 Credit Memos (%4), %5 Invoices (%6), %7 Invoice Lines and %8 Internal Charge Lines created.;SVE="%1 fakturor (%2), %3 kreditnotor (%4), %5 fakturor (%6), %7 fakturarader och %8 interna avgiftsrader har skapats. "';
      ServSetup@1210190004 : Record 11012800;
      ServTypeRec@1210190001 : Record 11012814;
      ServContrRec@1100485004 : Record 11012812;
      SalesHeaderRec@1210190008 : Record 36;
      SalesLineRec@1210190009 : Record 37;
      CustRec@1210190003 : Record 18;
      ContractRec@1100528600 : Record 11012812;
      ServiceOrder@1100528608 : Record 11012823;
      TempGenJnlLine@1100409000 : TEMPORARY Record 81;
      TmpIcEntry@1100529000 : TEMPORARY Record 11012058;
      CurrServiceOrderCostPlusEntry@1100529301 : Record 11012825;
      JobsSetup@1100529603 : Record 315;
      CreditMemoCounter@1210190000 : Integer;
      InvCounter@11012011 : Integer;
      InvProposalCounter@1100529302 : Integer;
      LineCounter@11012013 : Integer;
      SalesLineNo@11012014 : Integer;
      NoSeriesMgt@11012016 : Codeunit 396;
      InternalChargeMgt@1100528609 : Codeunit 11012265;
      InsertHeader@11012017 : Boolean;
      MultipleDim1@1100485000 : Boolean;
      MultipleReferences@1100485002 : Boolean;
      MultipleOrderNos@1100485001 : Boolean;
      MultipleServOrders@1100485005 : Boolean;
      IsPlantInternalCharge@1100529600 : Boolean;
      FirstSalesHeaderNo@1100528601 : Code[20];
      LastSalesHeaderNo@1100528602 : Code[20];
      FirstSalesCreditMemoNo@1100528603 : Code[20];
      LastSalesCreditMemoNo@1100528604 : Code[20];
      LastServiceOrderNo@1100528605 : Code[20];
      FirstInvoiceProposalNo@1100529304 : Code[20];
      LastInvoiceProposalNo@1100529303 : Code[20];
      AmountLaborForRot@1101285000 : Decimal;
      PlantProjectNo@1100529602 : Code[20];
      SortingOrderType@1100528606 : 'Line No.,Work Order - Cost Object';
      ReleaseCollectiveList@1100528610 : Boolean;
      RecalculateCollectiveList@1100528607 : Boolean;
      CompanyPlantInvoice@1100529001 : Text;
      Text007@1100529601 : TextConst 'ENU=Posted from Service Order %1';

    PROCEDURE InsertSalesHeader@1(IServiceCollectiveList@1100528601 : Record 11071730;PercLabor@1100528600 : Decimal;PerctoBAccount@1100528602 : Decimal;YourReference@1100528603 : Text[50];OrderNoCustomer@1100528604 : Text[50];CommisionDateCustomer@1100528400 : Date;ServiceOrderNo@1100528605 : Code[20];ServiceContractNo@1100528606 : Code[20];GlobalDim1Code@1100528607 : Code[20];BilltoCustomerNo@1100528608 : Code[20]);
    VAR
      ServOrder@1100409000 : Record 11012823;
      Customer@1100527000 : Record 18;
    BEGIN
      // ************** ROT ***************************************<<
      IF (InvCounter > 0) THEN UpdateROTForSalesHeader;  // 141210
      // If there are invoices before this one, create rot info
      // ************** ROT ***************************************>>

      SalesHeaderRec.INIT;
      IF "Service Collective-List".GetTotalInvoiceAmountInclVAT < 0 THEN BEGIN
        SalesHeaderRec."Document Type" := SalesHeaderRec."Document Type"::"Credit Memo";
        CreditMemoCounter := CreditMemoCounter + 1;
      END ELSE BEGIN
        SalesHeaderRec."Document Type" := SalesHeaderRec."Document Type"::Invoice;
        InvCounter := InvCounter + 1;
      END;

      SalesHeaderRec."Service Invoice" := TRUE;
      SalesHeaderRec."Company Name" := CompanyPlantInvoice;
      SalesHeaderRec."No." := '';

      IF SalesHeaderRec."Document Type" <> SalesHeaderRec."Document Type"::"Invoice Proposal" THEN BEGIN   //DP00195
        IF ServTypeRec."Sales Invoice Nos." <> '' THEN BEGIN
          SalesHeaderRec."No." := NoSeriesMgt.GetNextNo(ServTypeRec."Sales Invoice Nos.", WORKDATE, TRUE);
          SalesHeaderRec."No. Series" := ServTypeRec."Sales Invoice Nos.";
        END;
        IF ServTypeRec."Posted Sales Invoice Nos." <> '' THEN
          SalesHeaderRec."Posting No. Series" := ServTypeRec."Posted Sales Invoice Nos.";
      END;
      SalesHeaderRec.INSERT(TRUE);

      IF SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::Invoice THEN BEGIN
        IF FirstSalesHeaderNo = '' THEN
          FirstSalesHeaderNo := SalesHeaderRec."No.";
        LastSalesHeaderNo := SalesHeaderRec."No.";
      END;

      IF SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo" THEN BEGIN
        IF FirstSalesCreditMemoNo = '' THEN
          FirstSalesCreditMemoNo := SalesHeaderRec."No.";
        LastSalesCreditMemoNo := SalesHeaderRec."No.";
      END;

      //DP00195.sn.
      IF SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Invoice Proposal" THEN BEGIN
        IF FirstInvoiceProposalNo = '' THEN
          FirstInvoiceProposalNo := SalesHeaderRec."No.";
        LastInvoiceProposalNo := SalesHeaderRec."No.";
      END;
      //DP00195.en

      SalesHeaderRec.VALIDATE("Sell-to Customer No.", IServiceCollectiveList."Customer No.");   //evdb.n
      SalesHeaderRec.SetHideValidationDialog(TRUE);
      SalesHeaderRec.VALIDATE("Bill-to Customer No.", IServiceCollectiveList."Bill-to Customer No.");
      SalesHeaderRec.VALIDATE("Alternative Bill-to Address", IServiceCollectiveList."Alternative Bill-to Address");
      SalesHeaderRec.VALIDATE("Bill-to Contact No.", IServiceCollectiveList."Bill-to Contact Person No.");

      SalesHeaderRec."% Labor" := PercLabor;
      SalesHeaderRec."% to B Account" := PerctoBAccount;
      SalesHeaderRec."Specific WKA Percentages" := (PercLabor <> 0) OR (PerctoBAccount <> 0);
      SalesHeaderRec."Principal Reference" := YourReference;
      SalesHeaderRec."Order No. Customer" := OrderNoCustomer;
      SalesHeaderRec."Commision Date Customer" := CommisionDateCustomer;
      IF ServOrder.GET(ServiceOrderNo) AND ServOrder.UseBilltoAsSelltoCustomer THEN
        SalesHeaderRec.SetSkipCheckCustomerNoOnServiceOrder(TRUE);
      SalesHeaderRec.VALIDATE("Service Order No.", ServiceOrderNo);
      SalesHeaderRec.SetSkipCheckCustomerNoOnServiceOrder(FALSE);

      IF ServContrRec.GET(ServiceContractNo) THEN BEGIN
        ServContrRec.TESTFIELD(Status, ServContrRec.Status::"Invoicing Allowed");
        IF ServContrRec."Contract Manager" <> '' THEN
          SalesHeaderRec.VALIDATE("Salesperson Code", ServContrRec."Contract Manager");  //db, 11-04-08 (voor kostenplaats valideren)
        IF ServiceOrderNo = '' THEN //mg, 06-02-13: C005416
          SalesHeaderRec."Invoice Text" := ServContrRec."Invoice Text Installments";
      END;

      SalesHeaderRec."Currency Code" := IServiceCollectiveList."Currency Code";
      SalesHeaderRec.UpdateCurrencyFactor;

      SalesHeaderRec.VALIDATE("Shortcut Dimension 1 Code", GlobalDim1Code);
      //DP00640.sn (SEPA-2)
      IF SalesHeaderRec."Service Order No." <> '' THEN BEGIN
        IF ServOrder.GET(SalesHeaderRec."Service Order No.") THEN BEGIN
          IF ServOrder."Payment Method Code (SO Inv.)" <> '' THEN
            SalesHeaderRec.VALIDATE("Payment Method Code", ServOrder."Payment Method Code (SO Inv.)");
          //C033734.sn
          IF ServOrder."Bill-to Contact Person" <> '' THEN
            SalesHeaderRec.VALIDATE("Bill-to Contact No.", ServOrder."Bill-to Contact Person");
          //C033734.en
          //>> 160618 ITERO.SB RAD-023 Added invoice text from service order
          IF ServOrder."Invoice Text" <> '' THEN
            SalesHeaderRec."Invoice Text" := ServOrder."Invoice Text";
          //<<
          //>>180903
          IF IServiceCollectiveList."Shortcut Dimension 2 Code" <> '' THEN
            SalesHeaderRec.VALIDATE("Shortcut Dimension 2 Code",IServiceCollectiveList."Shortcut Dimension 2 Code");
          //<<180903
        END;
      END;
      //DP00640.en (SEPA-2)
      SalesHeaderRec.GetDirectDebitMandateServiceOrProject(SalesHeaderRec."Service Order No.", '', '', ''); //DP00640
      IF Customer.GET(SalesHeaderRec."Sell-to Customer No.") THEN
        SalesHeaderRec."Calculate B Amounts based on" := Customer."Calculate B Amounts based on";
      IF IServiceCollectiveList."Principal Reference" <> '' THEN
        SalesHeaderRec."Principal Reference" := IServiceCollectiveList."Principal Reference";

      SalesHeaderRec.MODIFY(TRUE);
      SalesLineNo := 10000;
      InsertHeader := FALSE;

      MultipleReferences := FALSE;
      MultipleOrderNos := FALSE;
      MultipleDim1 := FALSE;
      MultipleServOrders := FALSE;

      CustRec.GET(BilltoCustomerNo);
    END;

    PROCEDURE InsertSalesLine@5(IServiceOrder@1100528600 : Record 11012823;ServiceOrderCostPlusEntry@1100529300 : Record 11012825);
    VAR
      DimValRec@1100485001 : Record 349;
      Currency@1100528602 : Record 4;
      ProjectTypeRec@1100529304 : Record 11012009;
      ProjRec@1100529305 : Record 11072003;
      SalesHeader2@1100528400 : Record 36;
      CustomerPostingGroup@1100529600 : Record 92;
      DimMgt@1100485000 : Codeunit 408;
      CostPlusAmnt@1210190001 : Decimal;
      CostPlusDisc@1210190000 : Decimal;
      UnitPrice@1100528900 : Decimal;
      Customer@1100527550 : Record 18;
      lvGeneralLedgerSetup@1101285000 : Record 98;
      lvDimensionValue@1101285001 : Record 349;
    BEGIN
      IF NOT Currency.GET(SalesHeaderRec."Currency Code") THEN
        Currency.InitRoundingPrecision;

      SalesLineRec.INIT;
      SalesLineRec.SuspendUpdateVATAmounts(TRUE);
      SalesLineRec."Document Type" := SalesHeaderRec."Document Type";
      SalesLineRec."Document No." := SalesHeaderRec."No.";
      SalesLineRec."Line No." := SalesLineNo;
      SalesLineRec."System-Created Entry" := TRUE;
      SalesLineRec."Service Invoice" := TRUE;
      SalesLineRec."Collective-List Line Type" := SalesLineRec."Collective-List Line Type"::"Cost Plus Entry";
      SalesLineRec."Collective List No." := "Service Collective-List"."No.";
      SalesLineRec.Type := SalesLineRec.Type::"G/L Account";
      SalesLineRec."Cost Plus Line No." := ServiceOrderCostPlusEntry."Line No.";
      SalesLineRec."Cost Type Cost Plus Line" := ServiceOrderCostPlusEntry."Cost Type";
      SalesLineRec."Cost Object Cost Plus Line" := ServiceOrderCostPlusEntry."Cost Object";
      SalesLineRec."Additional Cost (Service)" := ServiceOrderCostPlusEntry."Additional Cost";
      SalesLineRec.VALIDATE("Sell-to Customer No.", ServiceOrderCostPlusEntry."Customer No.");
      SalesLineRec.VALIDATE("Bill-to Customer No.", ServiceOrderCostPlusEntry."Bill-to Customer No.");
      SalesLineRec.VALIDATE("Service Order No.", ServiceOrderCostPlusEntry."Service Order No.");
      SalesLineRec.GetServTypeRevenueAccount;
      IF SalesLineRec."Shortcut Dimension 2 Code" = '' THEN //wrong account no. returned by previous function
        SalesLineRec.VALIDATE("No.", ServTypeRec.GetWipRevenueAcc(ServiceOrderCostPlusEntry."Cost Type",COMPANYNAME,SalesHeaderRec."Customer Posting Group"));

      //DP00195.sn
      IF ServiceOrder.GET(ServiceOrderCostPlusEntry."Service Order No.") THEN BEGIN
        IF PlantProjectNo <> '' THEN BEGIN
          IF ServiceOrderCostPlusEntry."Additional Cost" THEN BEGIN
            SalesLineRec."Job No." := PlantProjectNo;
            IF CompanyPlantInvoice = '' THEN BEGIN
              ProjRec.GET(PlantProjectNo);
              SalesLineRec."No." := ProjectTypeRec.GetWipAcc(ProjRec."Project Type",
                                                        ServiceOrderCostPlusEntry."Cost Type",
                                                        ProjRec."Project Status",
                                                        JobsSetup."Provisions at Closure",
                                                        COMPANYNAME,
                                                        ServiceOrderCostPlusEntry."Cost Type",'',
                                                        SalesHeaderRec."Sell-to Customer No.");
            END ELSE
              SalesLineRec.VALIDATE("No.", ServTypeRec.GetWipRevenueAcc(ServiceOrderCostPlusEntry."Cost Type",COMPANYNAME,SalesHeaderRec."Customer Posting Group"));
          END ELSE
            IF SalesHeaderRec."Customer Posting Group" <> '' THEN
              IF CustomerPostingGroup.GET(SalesHeaderRec."Customer Posting Group") THEN
                SalesLineRec.VALIDATE("No.", CustomerPostingGroup.GetReceivablesAccount);
        END ELSE BEGIN
          SalesLineRec.GetServTypeRevenueAccount;
          IF SalesLineRec."Shortcut Dimension 2 Code" = '' THEN //wrong account no. returned by previous function
            SalesLineRec.VALIDATE("No.", ServTypeRec.GetWipRevenueAcc(ServiceOrderCostPlusEntry."Cost Type",COMPANYNAME,SalesHeaderRec."Customer Posting Group"));
        END;
      END;
      //DP00195.en

      //db, 14-12-05 ivm verwijderingbijdrage artikel toekennen voor validatie aantal+eenheid
      SalesLineRec."Item No." := ServiceOrderCostPlusEntry."Item No.";
      SalesLineRec."Serial No." := ServiceOrderCostPlusEntry."Serial No.";
      SalesLineRec."Lot No." := ServiceOrderCostPlusEntry."Lot No.";
      SalesLineRec."Basic Item" := ServiceOrderCostPlusEntry."Basic Item";
      SalesLineRec."Trade Item" := ServiceOrderCostPlusEntry."Trade Item";
      SalesLineRec.Manufacturer := ServiceOrderCostPlusEntry.Manufacturer;
      SalesLineRec."Vendor (Trade Item)" := ServiceOrderCostPlusEntry."Vendor (Trade Item)";

      //db, 11-05-07 (melding 9036)
      SalesLineRec."Dimensional Factor" := ServiceOrderCostPlusEntry."Dimensional Factor";
      IF (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo") AND
        (ServiceOrderCostPlusEntry.Quantity < 0) THEN
        SalesLineRec.VALIDATE(Quantity, -1 * ServiceOrderCostPlusEntry.Quantity)
      ELSE
        SalesLineRec.VALIDATE(Quantity, ServiceOrderCostPlusEntry.Quantity);
      GetCostPlusAmountAndDiscount(CostPlusAmnt, CostPlusDisc);  //db, 18-04-11
      UnitPrice := CostPlusAmnt * (100-CostPlusDisc)/100;  //DP01339
      IF (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo") AND (CostPlusAmnt > 0) THEN
        SalesLineRec.VALIDATE(Quantity, -1 * ServiceOrderCostPlusEntry.Quantity);
      IF (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo") AND (CostPlusAmnt < 0) THEN
        SalesLineRec.VALIDATE("Unit Price", -1 * UnitPrice)  //DP01339
      ELSE
        SalesLineRec.VALIDATE("Unit Price", UnitPrice);  //DP01339
      //SalesLineRec.UpdateUnitPrice(0);  //DP01339
      //** 4PS02.sn
      //C041255.sn
      SalesLineRec."Gross Price (FCY)" := ServiceOrderCostPlusEntry."Gross Price";
      SalesLineRec."Gross Price (LCY)" := ServiceOrderCostPlusEntry."Gross Price (LCY)";
      SalesLineRec."Basic Price (FCY)" := ServiceOrderCostPlusEntry."Basic Price";
      SalesLineRec."Basic Price (LCY)" := ServiceOrderCostPlusEntry."Basic Price (LCY)";
      SalesLineRec."Sales Price (FCY)" := ServiceOrderCostPlusEntry."Sales Price";
      SalesLineRec."Sales Price (LCY)" := ServiceOrderCostPlusEntry."Sales Price (LCY)";
      SalesLineRec."Surcharge Amount (FCY)" := ServiceOrderCostPlusEntry."Surcharge Amount (FCY)";
      SalesLineRec."Surcharge Amount (LCY)" := ServiceOrderCostPlusEntry."Surcharge Amount (LCY)";
      //C041255.en
      SalesLineRec."Surcharge %" := ServiceOrderCostPlusEntry."Surcharge %";
      SalesLineRec."Purchase Discount % (Item)" := ServiceOrderCostPlusEntry."Purchase Discount % (Item)";
      SalesLineRec."Sales Discount % (Item)" := ServiceOrderCostPlusEntry."Sales Discount % (Item)";
      SalesLineRec."Sales Condition Present" := ServiceOrderCostPlusEntry."Sales Condition Present";  //db, 06-02-20
      SalesLineRec."Price Specification Leading" := TRUE;  //DP01339
      //** 4PS02.en

      SalesLineRec.Amount :=
        ROUND(SalesLineRec."Unit Price" * SalesLineRec.Quantity * SalesLineRec.DimensionalFactor, Currency."Amount Rounding Precision");

      //DP01339.so:
      //already done by validate UnitPrice; CostPlusAmnt NOT based on local currency
      //IF (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo") AND (CostPlusAmnt < 0) THEN
      //  SalesLineRec."Amount (LCY)" := -1 * CostPlusAmnt * SalesLineRec.Quantity
      //ELSE
      //  SalesLineRec."Amount (LCY)" := CostPlusAmnt * SalesLineRec.Quantity;
      //
      //Currency.InitRoundingPrecision;
      //SalesLineRec."Amount (LCY)"  := ROUND(SalesLineRec."Amount (LCY)", Currency."Amount Rounding Precision");
      //DP01339.eo

      SalesLineRec.VALIDATE("Unit of Measure Code", ServiceOrderCostPlusEntry."Unit of Measure");
      SalesLineRec."Sales Surcharge Overtime %" := ServiceOrderCostPlusEntry."Sales Surcharge Overtime %";
      SalesLineRec.VALIDATE(Text, ServiceOrderCostPlusEntry.Text);
      SalesLineRec.Description := ServiceOrderCostPlusEntry.Description;
      SalesLineRec."Description 2" := ServiceOrderCostPlusEntry."Description 2";
      IF SalesHeader2.GET(SalesLineRec."Document Type", SalesLineRec."Document No.") THEN
        IF (SalesHeader2."Language Code" <> '') AND
           (SalesLineRec."Item No." <> '')
        THEN
          SalesLineRec.GetItemTranslation;
      SalesLineRec."Employee No." := ServiceOrderCostPlusEntry."Employee No.";
      SalesLineRec."Source Document" := ServiceOrderCostPlusEntry."Source Document";

      IF SalesLineRec."Shortcut Dimension 2 Code" <> '' THEN BEGIN
        DimMgt.GetDimValueRec(2,SalesLineRec."Shortcut Dimension 2 Code",DimValRec,FALSE,'');
          SalesLineRec."Cost Component" := DimValRec."Cost Component";
      END;
      IF SalesLineRec."Cost Component" = '' THEN
        SalesLineRec."Cost Component" := ServiceOrderCostPlusEntry."Cost Component";

      SalesLineRec."Removal Contribution" := ServiceOrderCostPlusEntry."Removal Contribution";
      IF ServiceOrderCostPlusEntry."Removal Contribution" THEN
        SalesLineRec."Attached to Line No. (RC)" := GetAttachedtoLineNoRC(SalesHeaderRec, ServiceOrderCostPlusEntry);

      SalesLineRec."Principal Reference" := IServiceOrder."Your Reference";
      SalesLineRec."Order No. Customer" := IServiceOrder."Order No. Customer";
      SalesLineRec."Cost Entry No. Service Ledger" := ServiceOrderCostPlusEntry."Entry No. Service Ledger";

      IF ServiceOrderCostPlusEntry."Additional Cost" THEN  //db, 24-02-10
        SalesLineRec.VALIDATE("Shortcut Dimension 1 Code", IServiceOrder."Department Code (Other)")
      ELSE
        SalesLineRec.VALIDATE("Shortcut Dimension 1 Code", IServiceOrder."Global Dimension 1 Code");
      //SalesLineRec."Cost Price" :=  CostPlusEntryRec."Cost Price";  //diekus: uitgeschakeld
      SalesLineRec."Execution Date" := ServiceOrderCostPlusEntry."Posting Date";
      //kzwerver, 110615, sn, #24416
      SalesLineRec."Wage Component Cost Plus Line" := ServiceOrderCostPlusEntry."Wage Component";
      SalesLineRec."Hour Rate Cost Plus Line" := ServiceOrderCostPlusEntry."Hour Rate Code";
      //kzwerver, 110615, en, #24416
      SalesLineRec."Object No." := ServiceOrderCostPlusEntry."Object No.";

      //kzwerver, 111004, sn, #RfC Unit Price
      SalesLineRec."Service Price Book Code" := ServiceOrderCostPlusEntry."Price Book Code";
      SalesLineRec."SUP Index Date" := ServiceOrderCostPlusEntry."Unit Price Index Date";
      SalesLineRec."SUP Extention" := ServiceOrderCostPlusEntry."Unit Price Extention";
      SalesLineRec."SUP Production Date" := ServiceOrderCostPlusEntry."Production Recording Date";
      SalesLineRec."SUP Room" := ServiceOrderCostPlusEntry."Unit Price Room";
      SalesLineRec."Service Unit Price Code" := ServiceOrderCostPlusEntry."Unit Price Code";
      //kzwerver, 111004, en, #RfC Unit Price

      SalesLineRec."Work Order No." := ServiceOrderCostPlusEntry."Created by Work Order No.";
      //>> 200114 ORANGO.SB
      IF ServiceOrderCostPlusEntry."Work Order No." <> '' THEN
        SalesLineRec."Work Order No."  := ServiceOrderCostPlusEntry."Work Order No.";
      //<<
      SalesLineRec.SuspendUpdateVATAmounts(FALSE);
      IF SalesLineRec.Type <> SalesLineRec.Type::" " THEN
        SalesLineRec.UpdateAmounts;
      IF ServSetup."Cost Object Revenues" <> '' THEN BEGIN
        SalesLineRec.VALIDATE("Shortcut Dimension 2 Code", ServSetup."Cost Object Revenues");
        SalesLineRec.VALIDATE(Description, ServiceOrderCostPlusEntry.Description);
      END;
      SalesLineRec.GetItemTranslation;
      SalesLineRec.VALIDATE("VAT Prod. Posting Group", ServiceOrderCostPlusEntry."VAT Prod. Posting Group");
      SalesLineRec.VALIDATE("VAT Bus. Posting Group", CustRec."VAT Bus. Posting Group");
      IF IServiceOrder."VAT Bus. Posting Group" <> '' THEN
        SalesLineRec.VALIDATE("VAT Bus. Posting Group", IServiceOrder."VAT Bus. Posting Group");
      IF ServiceOrderCostPlusEntry."Bill-to Customer No." <> IServiceOrder."Bill-to Customer No." THEN
        IF Customer.GET(ServiceOrderCostPlusEntry."Bill-to Customer No.") THEN
          IF Customer."VAT Prod. Posting Group" <> '' THEN
            SalesLineRec.VALIDATE("VAT Bus. Posting Group", Customer."VAT Bus. Posting Group");
      SalesLineRec."Line Discount %" := ServiceOrderCostPlusEntry."Discount % (ServOrder)";

      SalesLineRec."Service Order Object No." := "Service Order Cost Plus Entry"."Service Order Object No.";
      SalesLineRec."Dsp Assignment Id" := "Service Order Cost Plus Entry"."Dsp Assignment Id";

      SalesLineRec.INSERT;

      // 141210 Calculation of ROT Amount <<<
      // *********************************************************************************************************************
      // 1. ROT CALCULATON FROM CostPlus
      // Cost Plus entry is added to a Sales Line. Now check if the entry is ROT-based.
      lvGeneralLedgerSetup.GET;
      IF (lvDimensionValue.GET(lvGeneralLedgerSetup."Global Dimension 2 Code", ServiceOrderCostPlusEntry."Cost Object")) THEN BEGIN
        IF lvDimensionValue."ROT-reduction" THEN
          AmountLaborForRot += SalesLineRec."Amount Including VAT";
      END;
      // *********************************************************************************************************************
      // 141210 Calculation of ROT Amount >>>



      IF SalesHeaderRec."Principal Reference" <> SalesLineRec."Principal Reference" THEN
        MultipleReferences := TRUE;
      IF SalesHeaderRec."Order No. Customer" <> SalesLineRec."Order No. Customer" THEN
        MultipleOrderNos := TRUE;
      IF SalesHeaderRec."Shortcut Dimension 1 Code" <> SalesLineRec."Shortcut Dimension 1 Code" THEN
        MultipleDim1 := TRUE;
      IF SalesHeaderRec."Service Order No." <> SalesLineRec."Service Order No." THEN
        MultipleServOrders := TRUE;

      SalesLineNo := SalesLineNo + 10000;
      LineCounter := LineCounter + 1;

      LinkDocumentsToSalesInvoice(IServiceOrder."No.", SalesHeaderRec);
    END;

    PROCEDURE InsertSCInvoicingSalesLine@1100528602();
    VAR
      DimensionValue@1100528602 : Record 349;
      DimensionManagement@1100528601 : Codeunit 408;
      MaintenanceInvoiceMgt@1100528600 : Codeunit 11012828;
    BEGIN
      SalesLineRec.INIT;
      SalesLineRec.SuspendUpdateVATAmounts(TRUE);
      SalesLineRec."Document Type" := SalesHeaderRec."Document Type";
      SalesLineRec."Document No." := SalesHeaderRec."No.";
      SalesLineRec."Line No." := SalesLineNo;
      SalesLineRec."System-Created Entry" := TRUE;
      SalesLineRec."Service Invoice" := TRUE;
      SalesLineRec."Collective-List Line Type" := "Collective-List SC Inv. Line".Type +1;
      SalesLineRec."Collective List No." := "Service Collective-List"."No.";

      SalesLineRec.Type := SalesLineRec.Type::"G/L Account";
      SalesLineRec.VALIDATE("Service Contract No.", "Collective-List SC Inv. Line"."Service Contract No.");
      SalesLineRec.VALIDATE("Object No.", "Collective-List SC Inv. Line"."Service Object No.");
      SalesLineRec.VALIDATE("Installment Line No.", "Collective-List SC Inv. Line"."Installment Line No.");
      SalesLineRec.VALIDATE("Service Location No.", "Collective-List SC Inv. Line"."Service Location No.");
      SalesLineRec.VALIDATE("Shortcut Dimension 2 Code", "Collective-List SC Inv. Line"."Cost Object");

      SalesLineRec."Currency Code" := SalesHeaderRec."Currency Code";

      SalesLineRec."Unit Price" := "Collective-List SC Inv. Line"."Unit Price";
      SalesLineRec.VALIDATE(Amount, "Collective-List SC Inv. Line".Amount);
      SalesLineRec."Amount (LCY)" :=
        MaintenanceInvoiceMgt.CalcAmountLCYFromAmount(
          SalesLineRec.Amount, "Collective-List SC Inv. Line"."Currency Code", "Collective-List SC Inv. Line"."Currency Date");

      IF (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo") AND (SalesLineRec."Amount (LCY)" > 0) THEN
        SalesLineRec.VALIDATE(Quantity, -1 * "Collective-List SC Inv. Line".Quantity)
      ELSE
        SalesLineRec.VALIDATE(Quantity, "Collective-List SC Inv. Line".Quantity);

      SalesLineRec.VALIDATE("VAT Prod. Posting Group", "Collective-List SC Inv. Line"."VAT Prod. Posting Group");
      SalesLineRec.Description := "Collective-List SC Inv. Line".Description;
      SalesLineRec."Description 2" := "Collective-List SC Inv. Line"."Description 2";
      IF SalesLineRec."Shortcut Dimension 2 Code" <> '' THEN BEGIN
        DimensionManagement.GetDimValueRec(2,SalesLineRec."Shortcut Dimension 2 Code",DimensionValue,FALSE,'');
          SalesLineRec."Cost Component" := DimensionValue."Cost Component";
      END;

      SalesLineRec.VALIDATE("Service Control Period Date", "Collective-List SC Inv. Line"."Service Control Period Date");
      SalesLineRec.VALIDATE("Unit of Measure", "Collective-List SC Inv. Line"."Unit of Measure");
      SalesLineRec."Coll.-List SC Inv. Line No." := "Collective-List SC Inv. Line"."Line No.";

      SalesLineRec.SuspendUpdateVATAmounts(FALSE);
      IF SalesLineRec.Type <> SalesLineRec.Type::" " THEN
        SalesLineRec.UpdateAmounts;
      SalesLineRec.INSERT;

      SalesLineNo := SalesLineNo + 10000;
      LineCounter := LineCounter + 1;
    END;

    PROCEDURE GetCostPlusAmountAndDiscount@1210190001(VAR CostPlusAmnt@1210190001 : Decimal;VAR CostPlusDisc@1210190002 : Decimal);
    VAR
      ServiceOrderExtension@1100528600 : Record 11071727;
      CostPlusAmnt2@1210190003 : Decimal;
    BEGIN
      //db, 18-04-11
      CostPlusDisc := CurrServiceOrderCostPlusEntry."Discount % (ServOrder)";
      CostPlusAmnt := CurrServiceOrderCostPlusEntry."Sales Price";
      IF NOT ServiceOrderExtension.GET(CurrServiceOrderCostPlusEntry."Service Order No.") THEN
        ServiceOrderExtension.INIT;
      IF ServiceOrderExtension."Item Price Cost Plus Entry" = ServiceOrderExtension."Item Price Cost Plus Entry"::GrossMin THEN
      BEGIN
        CostPlusAmnt2 := CostPlusAmnt * (1 - CostPlusDisc / 100);
        IF (CurrServiceOrderCostPlusEntry."Gross Price" > CostPlusAmnt2) AND
           (CurrServiceOrderCostPlusEntry."Gross Price" <> 0) THEN BEGIN
          CostPlusAmnt := CurrServiceOrderCostPlusEntry."Gross Price";
          CostPlusDisc := 100 * (CurrServiceOrderCostPlusEntry."Gross Price" - CostPlusAmnt2) /
            CurrServiceOrderCostPlusEntry."Gross Price";
        END ELSE BEGIN
          IF (CurrServiceOrderCostPlusEntry."Basic Price" > CostPlusAmnt2) AND
             (CurrServiceOrderCostPlusEntry."Basic Price" <> 0) THEN BEGIN
            CostPlusAmnt := CurrServiceOrderCostPlusEntry."Basic Price";
            CostPlusDisc := 100 * (CurrServiceOrderCostPlusEntry."Basic Price" - CostPlusAmnt2) /
              CurrServiceOrderCostPlusEntry."Basic Price";
          END;
        END;
      END;
    END;

    PROCEDURE GetAttachedtoLineNoRC@1100528600(ISalesHeader@1100528603 : Record 36;IServiceOrderCostPlusEntry@1100528600 : Record 11012825) : Decimal;
    VAR
      ServiceOrderCostPlusEntry@1100528601 : Record 11012825;
      SalesLine@1100528602 : Record 37;
    BEGIN
      IF NOT IServiceOrderCostPlusEntry."Removal Contribution" THEN
        EXIT;
      IF IServiceOrderCostPlusEntry."Attached to Line No. (RC)" = 0 THEN
        EXIT;
      IF NOT ServiceOrderCostPlusEntry.GET(
        IServiceOrderCostPlusEntry."Service Order No.", IServiceOrderCostPlusEntry."Attached to Line No. (RC)")
      THEN
        EXIT;

      SalesLine.SETRANGE("Document Type", ISalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.", ISalesHeader."No.");
      SalesLine.SETRANGE("Cost Plus Line No.", ServiceOrderCostPlusEntry."Line No.");
      IF SalesLine.FINDLAST THEN
        EXIT(SalesLine."Line No.");
    END;

    PROCEDURE DivideComment@1100409000(TextToDivide@1100409000 : Text[80];VAR Text1@1100409001 : Text[50];VAR Text2@1100409002 : Text[50]);
    VAR
      Pos@1100409003 : Integer;
    BEGIN
      IF STRLEN(TextToDivide) <= MAXSTRLEN(Text1) THEN BEGIN  //CALL C006639 sn
        Text1 := COPYSTR(TextToDivide, 1, MAXSTRLEN(Text1));
        Text2 := '';
      END ELSE BEGIN                                          //CALL C006639 en
        Pos := GetSplitPosition(TextToDivide, MAXSTRLEN(Text1));
        IF STRLEN(TextToDivide) - Pos <= MAXSTRLEN(Text2) THEN BEGIN
          Text1 := COPYSTR(TextToDivide, 1, Pos);
          Text2 := COPYSTR(TextToDivide, Pos +1, STRLEN(TextToDivide));
        END ELSE BEGIN
          Text1 := COPYSTR(TextToDivide, 1, MAXSTRLEN(Text1));
          Text2 := COPYSTR(TextToDivide, MAXSTRLEN(Text1) +1, 40);
        END;
      END;                                                   //CALL C006639 n
    END;

    PROCEDURE GetSplitPosition@1100409001(TextToDivide@1100409000 : Text[1024];MaxStrLength@1100409001 : Integer) Pos : Integer;
    VAR
      SplitText@1100409002 : Text[1024];
    BEGIN
      SplitText := ReverseText(COPYSTR(TextToDivide, 1, MaxStrLength));
      Pos := STRPOS(SplitText, ' ');
      IF Pos = 0 THEN
        Pos := STRPOS(SplitText, ';');
      IF Pos = 0 THEN
        Pos := STRPOS(SplitText, ',');
      IF Pos = 0 THEN
        Pos := MaxStrLength
      ELSE
        Pos := MaxStrLength - (Pos + (MaxStrLength - STRLEN(SplitText))) + 1; //CALL C006639 changed
      EXIT(Pos);
    END;

    PROCEDURE ReverseText@1100409002(Text1@1100409000 : Text[1024]) : Text[1024];
    VAR
      Length@1100409001 : Integer;
      Text2@1100409002 : Text[1024];
      i@1100409003 : Integer;
    BEGIN
      Length := STRLEN(Text1);
      Text2 := '';
      FOR i := 1 TO Length DO
        Text2[i] := Text1[Length - i + 1];
      EXIT(Text2);
    END;

    PROCEDURE LinkDocumentsToSalesInvoice@1100528700(ServiceOrderNo@1100528701 : Code[20];SalesHeader@1100528700 : Record 36);
    VAR
      DocsToBeLinkedtoSlsInv@1100525000 : Record 11229792;
      ServiceOrder@1100528703 : Record 11012823;
      ServiceOrderRecRef@1100528702 : RecordRef;
      DocumentProperties@1100528704 : Record 11012746;
      DocumentRelation@1100528706 : Record 11012407;
      DocumentLinkManagement@1100528705 : Codeunit 11012401;
    BEGIN
      IF NOT (SalesHeader."Document Type" IN [SalesHeader."Document Type"::Invoice, SalesHeader."Document Type"::"Credit Memo"]) THEN
        EXIT;

      DocsToBeLinkedtoSlsInv.SETRANGE("Customer No.", SalesHeader."Bill-to Customer No.");
      IF DocsToBeLinkedtoSlsInv.ISEMPTY THEN
        EXIT;

      ServiceOrder.GET(ServiceOrderNo);
      ServiceOrderRecRef.GETTABLE(ServiceOrder);

      DocumentLinkManagement.GetDocuments(DocumentProperties, ServiceOrderRecRef);
      DocumentProperties.MARKEDONLY(TRUE);
      IF DocumentProperties.FINDSET THEN
        REPEAT
          IF DocumentMustBeLinked(SalesHeader."Bill-to Customer No.", ServiceOrder, DocumentProperties, SalesHeader."Document Type") THEN BEGIN
            DocumentRelation.INIT;
            CASE SalesHeader."Document Type" OF
              SalesHeader."Document Type"::Invoice:
                DocumentRelation."Document Type" := DocumentRelation."Document Type"::"Sales Invoice";
              SalesHeader."Document Type"::"Credit Memo":
                DocumentRelation."Document Type" := DocumentRelation."Document Type"::"Sales Cr.Memo";
            END;
            DocumentRelation.VALIDATE("No.", SalesHeader."No.");
            DocumentRelation.VALIDATE("Related Document No.", DocumentProperties."No.");
            DocumentRelation."Send by E-Mail" := TRUE;
            IF DocumentProperties.FileCanBeConvertedToPDF THEN
              DocumentRelation.Print := TRUE;
            IF NOT DocumentRelation.FIND THEN
              DocumentRelation.INSERT(TRUE);
          END;
        UNTIL DocumentProperties.NEXT = 0;
    END;

    PROCEDURE DocumentMustBeLinked@1100528701(CustomerNo@1100528700 : Code[20];ServiceOrder@1100528703 : Record 11012823;DocumentProperties@1100528702 : Record 11012746;SalesDocumentType@1100528600 : Option) : Boolean;
    VAR
      DocsToBeLinkedtoSlsInv@1100528701 : Record 11229792;
    BEGIN
      DocsToBeLinkedtoSlsInv.SETRANGE("Customer No.", CustomerNo);
      DocsToBeLinkedtoSlsInv.SETFILTER("Source Type", '%1|%2',
        DocsToBeLinkedtoSlsInv."Source Type"::" ", ServiceOrder."Source Type" + 1);
      DocsToBeLinkedtoSlsInv.SETFILTER("Document Type", '%1|%2', '', DocumentProperties."Document Type");
      DocsToBeLinkedtoSlsInv.SETFILTER("Document Category", '%1|%2', '', DocumentProperties."Document Category");
      IF SalesDocumentType = SalesHeaderRec."Document Type"::"Credit Memo" THEN
        DocsToBeLinkedtoSlsInv.SETRANGE("Exclude Credit Memo", FALSE);
      EXIT(DocsToBeLinkedtoSlsInv.FINDFIRST);
    END;

    PROCEDURE SOCostPlusEntryOnAfterGetRecor@1100529901(VAR ServiceOrderCostPlusEntry@1100529903 : Record 11012825);
    VAR
      ServiceOrderExtension@1100529902 : Record 11071727;
      CostPlusAmnt@1100529901 : Decimal;
      CostPlusDisc@1100529900 : Decimal;
    BEGIN
      WITH ServiceOrderCostPlusEntry DO BEGIN
        IF LastServiceOrderNo <> "Service Order No." THEN BEGIN
          ServiceOrder.GET("Service Order No.");
          ServiceOrder.TESTFIELD("Service Type");
          ServiceOrder.TESTFIELD("Service Type (Other)");
          IF NOT IsPlantInternalCharge THEN
            ServiceOrder.TESTFIELD("Bill-to Customer No.");
          IF "Service Collective-List"."Invoice per Service Order" THEN
            InsertHeader := TRUE;
          IF InsertHeader THEN
            SalesHeaderRec.DetermineRemovalContribution;
          LastServiceOrderNo := "Service Order No.";
        END;
        IF "Charge to Plant Location" THEN
          ServiceOrder.TESTFIELD("Plant Location");

        ServiceOrderExtension.GetServOrderExtension("Service Order No.");
        IF ((ServiceOrderExtension."Master Project" = '') OR
           (ServSetup."Service to Project" = ServSetup."Service to Project"::"Transfer Cost and Revenue")) AND
           (NOT IsPlantInternalCharge)
        THEN BEGIN
          TESTFIELD("VAT Prod. Posting Group");
          TESTFIELD(Description);
          CALCFIELDS("Cost Type");
          IF "Additional Cost" THEN
            ServTypeRec.GET(ServiceOrder."Service Type (Other)")
          ELSE
            ServTypeRec.GET(ServiceOrder."Service Type");
          IF NOT ContractRec.GET(ServiceOrder."Service Contract No.") THEN
            ContractRec.INIT;

          IF InsertHeader THEN
            InsertSalesHeader(
              "Service Collective-List", ServiceOrder."% Labor", ServiceOrder."% to B Account", ServiceOrder."Your Reference",
              ServiceOrder."Order No. Customer", ServiceOrder."Commision Date", "Service Order No.",
              ServiceOrder."Service Contract No.", ServiceOrder."Global Dimension 1 Code", ServiceOrder."Bill-to Customer No.")
          ELSE BEGIN
            IF (SalesHeaderRec."Order No. Customer" <> '') AND (SalesHeaderRec."Order No. Customer" <> ServiceOrder."Order No. Customer") THEN BEGIN
              SalesHeaderRec."Order No. Customer" := '';
              SalesHeaderRec.MODIFY;
            END;
          END;
          InsertSalesLine(ServiceOrder,ServiceOrderCostPlusEntry);
        END ELSE BEGIN
          GetCostPlusAmountAndDiscount(CostPlusAmnt, CostPlusDisc);
          IF IsPlantInternalCharge THEN
            InternalChargeMgt.InsertTempGLEntry(
              2, ServiceOrder."No.", Description, STRSUBSTNO(Text007, ServiceOrder."No."),
              Quantity, CostPlusAmnt, "Cost Object", "Cost Component", '', "Unit of Measure",
              ServiceOrder."No.", TempGenJnlLine, TmpIcEntry, "Line No.", "Charge to Plant Location")
          ELSE
            InternalChargeMgt.InsertTempGLEntry(
              1, ServiceOrder."No.", Description, "Description 2", Quantity, CostPlusAmnt, "Cost Object", "Cost Component", '', "Unit of Measure",
              ServiceOrder."No.", TempGenJnlLine, TmpIcEntry, "Line No.", FALSE);  //DP00847.c
        END;
      END;
    END;

    PROCEDURE UpdateROTForSalesHeader@8();
    VAR
      lvRotInformation@1000 : Record 11128101;
      lvRotInformation2@1001 : Record 11128101;
    BEGIN
      // ************* ROT *******************************
      IF (SalesHeaderRec."Service Order No." <> '') AND (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::Invoice) THEN BEGIN
         lvRotInformation.RESET;
         lvRotInformation.SETRANGE(Type, lvRotInformation.Type::"Service Order");
         lvRotInformation.SETRANGE("Document No.", SalesHeaderRec."Service Order No.");
         IF lvRotInformation.FINDSET(FALSE) THEN BEGIN
            SalesHeaderRec."ROT/RUT reduction" := TRUE;
            SalesHeaderRec.MODIFY(FALSE);
            REPEAT
               lvRotInformation2 := lvRotInformation;
               lvRotInformation2.Type := lvRotInformation2.Type::Invoice;
               lvRotInformation2."Invoice No." := SalesHeaderRec."No.";
               lvRotInformation2."Invoice Relation" := lvRotInformation2."Invoice Relation"::"Service Order";
               lvRotInformation2.INSERT;
               //>> 151222 ITERO.SB Handle Extended ROT info for service order
               lvRotInformation2.CopyExtendedROTInformation(lvRotInformation); // 150320
               //<<
            UNTIL lvRotInformation.NEXT = 0;
            IF (AmountLaborForRot > 0) THEN BEGIN
               lvRotInformation2.ValidateLaborCost;
               lvRotInformation2.VALIDATE("Labour Cost", AmountLaborForRot);
               lvRotInformation2.MODIFY;
            END;
         END;
      END;

      AmountLaborForRot := 0; // Reset AmountLaborForRot;
    END;

    LOCAL PROCEDURE ProcessInvoiceDiscountSurcharge@1100528622(IInvoiceFromDate@1100528603 : Date);
    VAR
      Customer@1100528608 : Record 18;
      ServiceSalesDiscSurch@1100528602 : Record 11072237;
      DimensionValue@1100528607 : Record 349;
      CustomerPostingGroup@1100528609 : Record 92;
      MaintenanceInvoiceMgt@1100528600 : Codeunit 11012828;
      DimensionManagement@1100528605 : Codeunit 408;
      UnitPrice@1100528604 : Decimal;
      DiscountSurcharge@1100528606 : 'Discount,Surcharge';
      EmptyDateFormula@1100528601 : DateFormula;
    BEGIN
      IF NOT Customer.GET(SalesHeaderRec."Bill-to Customer No.") THEN
        EXIT;
      IF Customer."Customer Price Group" = '' THEN
        EXIT;

      FOR DiscountSurcharge := DiscountSurcharge::Discount TO DiscountSurcharge::Surcharge DO BEGIN
        ServiceSalesDiscSurch.SETRANGE(Type, ServiceSalesDiscSurch.Type::"Customer Group");
        ServiceSalesDiscSurch.SETRANGE(Code, Customer."Customer Price Group");
        ServiceSalesDiscSurch.SETRANGE("Object No.", '');
        ServiceSalesDiscSurch.SETRANGE("Discount/Surcharge", DiscountSurcharge);
        ServiceSalesDiscSurch.SETFILTER("Starting Date", '<>%1&<=%2', 0D, IInvoiceFromDate);
        ServiceSalesDiscSurch.SETFILTER("Ending Date", '%1|>%2', 0D, IInvoiceFromDate);
        ServiceSalesDiscSurch.SETRANGE(Source, ServiceSalesDiscSurch.Source::"Service Order");
        ServiceSalesDiscSurch.SETFILTER("Contract Group", '%1', '');
        ServiceSalesDiscSurch.SETFILTER("Invoice Period", '%1', EmptyDateFormula);
        IF ServiceSalesDiscSurch.FIND('+') THEN BEGIN
          SalesHeaderRec.TESTFIELD("Customer Posting Group");
          CustomerPostingGroup.GET(SalesHeaderRec."Customer Posting Group");
          REPEAT
            IF IsCorrectTransactionMode(ServiceSalesDiscSurch, SalesHeaderRec) THEN BEGIN
              IF ServiceSalesDiscSurch.Percentage <> 0 THEN BEGIN
                SalesLineRec.SETRANGE("Document Type", SalesHeaderRec."Document Type");
                SalesLineRec.SETRANGE("Document No.", SalesHeaderRec."No.");
                SalesLineRec.CALCSUMS("Line Amount");
                UnitPrice := SalesLineRec."Line Amount" * ServiceSalesDiscSurch.Percentage / 100;
              END ELSE BEGIN
                IF Customer."Currency Code" = '' THEN
                  UnitPrice := ServiceSalesDiscSurch.Amount
                ELSE
                  UnitPrice := MaintenanceInvoiceMgt.CalcAmountFromAmountLCY(
                    ServiceSalesDiscSurch.Amount, Customer."Currency Code", SalesHeaderRec."Document Date");
              END;
              IF UnitPrice <> 0 THEN BEGIN
                SalesLineRec.INIT;
                SalesLineRec.SuspendUpdateVATAmounts(TRUE);
                SalesLineRec."Document Type" := SalesHeaderRec."Document Type";
                SalesLineRec."Document No." := SalesHeaderRec."No.";
                SalesLineRec."Line No." := SalesLineNo;
                SalesLineRec."System-Created Entry" := TRUE;
                SalesLineRec."Service Invoice" := TRUE;
                SalesLineRec."Collective List No." := "Service Collective-List"."No.";

                SalesLineRec.Type := SalesLineRec.Type::"G/L Account";
                SalesLineRec.VALIDATE("No.", CustomerPostingGroup."Service Charge Acc.");
                SalesLineRec.VALIDATE("Shortcut Dimension 2 Code", SalesHeaderRec."Shortcut Dimension 2 Code");

                SalesLineRec."Currency Code" := SalesHeaderRec."Currency Code";

                IF ServiceSalesDiscSurch."Discount/Surcharge" = ServiceSalesDiscSurch."Discount/Surcharge"::Discount THEN
                  UnitPrice := -UnitPrice;

                SalesLineRec."Unit Price" := UnitPrice;
                SalesLineRec.VALIDATE(Amount, UnitPrice);
                SalesLineRec."Amount (LCY)" :=
                  MaintenanceInvoiceMgt.CalcAmountLCYFromAmount(
                    SalesLineRec.Amount, Customer."Currency Code", SalesHeaderRec."Document Date");

                IF (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo") AND (SalesLineRec."Amount (LCY)" > 0) THEN
                  SalesLineRec.VALIDATE(Quantity, -1)
                ELSE
                  SalesLineRec.VALIDATE(Quantity, 1);

                SalesLineRec.Description := ServiceSalesDiscSurch.Description;
                IF SalesLineRec."Shortcut Dimension 2 Code" <> '' THEN BEGIN
                  DimensionManagement.GetDimValueRec(2,SalesLineRec."Shortcut Dimension 2 Code",DimensionValue,FALSE,'');
                    SalesLineRec."Cost Component" := DimensionValue."Cost Component";
                END;

                SalesLineRec.SuspendUpdateVATAmounts(FALSE);
                IF SalesLineRec.Type <> SalesLineRec.Type::" " THEN
                  SalesLineRec.UpdateAmounts;

                SalesLineRec.INSERT;

                SalesLineNo += 10000;
                LineCounter += 1;
              END;
            END;
          UNTIL ServiceSalesDiscSurch.NEXT(-1) = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE IsCorrectTransactionMode@1100528615(IServiceSalesDiscSurch@1100528600 : Record 11072237;ISalesHeader@1100528601 : Record 36) : Boolean;
    BEGIN
      IF IServiceSalesDiscSurch."Transaction Mode Filter" = '' THEN
        EXIT(TRUE);
      ISalesHeader.SETRECFILTER;
      EXIT(NOT ISalesHeader.ISEMPTY);
    END;

    LOCAL PROCEDURE UpdateInvoicedAndChargable@1100525001(VAR IServiceOrderCostPlusEntry@1100525000 : Record 11012825);
    BEGIN
      IF NOT IServiceOrderCostPlusEntry.ISEMPTY THEN
        IF IServiceOrderCostPlusEntry.FINDSET(TRUE) THEN
          REPEAT
            IServiceOrderCostPlusEntry.Invoiced := TRUE;
            IServiceOrderCostPlusEntry.Chargeable := FALSE;
            IServiceOrderCostPlusEntry.MODIFY;
          UNTIL IServiceOrderCostPlusEntry.NEXT = 0;
    END;

    PROCEDURE SetDefaults@1100528601(ISortingOrderType@1100528600 : Option;IReleaseCollectiveList@1100528601 : Boolean;IRecalculateCollectiveList@1100528602 : Boolean);
    BEGIN
      SortingOrderType := ISortingOrderType;
      ReleaseCollectiveList := IReleaseCollectiveList;
      RecalculateCollectiveList := IRecalculateCollectiveList;
    END;

    [Business]
    PROCEDURE OnAfterPostReport@1100285100(FirstSalesHeaderNo@1100285106 : Code[20];LastSalesHeaderNo@1100285105 : Code[20];FirstSalesCreditMemoNo@1100285104 : Code[20];LastSalesCreditMemoNo@1100285103 : Code[20];LastServiceOrderNo@1100285102 : Code[20];FirstInvoiceProposalNo@1100285101 : Code[20];LastInvoiceProposalNo@1100285100 : Code[20]);
    BEGIN
    END;

    BEGIN
    {
      141210 ITERO.MH Added ROT calculation when creating Invoice
      151222 ITERO.SB Handle Extended ROT info for service order
      160618 ITERO.SB RAD-023 Added invoice text from service order
      180903 ORANGO.DL CostObject (global dim 2) from CostPlusEntry to SalesHead
      190416 ORANGO.SB Support #23895
      200114 ORANGO.SB Support #26911 SRFC-1010 Added field work order no.
    }
    END.
  }
  RDLDATA
  {
  }
}

