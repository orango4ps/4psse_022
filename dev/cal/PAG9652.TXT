OBJECT Page 9652 Report Layout Selection
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Report Layout Selection;
               NOR=Rapportoppsettsvalg;
               SVE=Val av rapportlayout];
    ApplicationArea=#Basic,#Suite;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table9651;
    PageType=List;
    UsageCategory=Administration;
    AdditionalSearchTermsML=[ENU=customization,document layout;
                             NOR=tilpassing,dokumentoppsett;
                             SVE=anpassning,dokumentlayout];
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 SelectedCompany := COMPANYNAME;
               END;

    OnFindRecord=BEGIN
                   IF NOT IsInitialized THEN
                     InitializeData;
                   EXIT(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       GetRec;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           GetRec;
                         END;

    ActionList=ACTIONS
    {
      { 3       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 4       ;1   ;Action    ;
                      Name=Customizations;
                      CaptionML=[ENU=Custom Layouts;
                                 NOR=Egendefinerte oppsett;
                                 SVE=Anpassade layouter];
                      ToolTipML=[ENU=View or edit the custom layouts that are available for a report.;
                                 NOR=Vis eller rediger de egendefinerte oppsettene som er tilgjengelige for en rapport.;
                                 SVE=Visa eller redigera anpassade layouter som „r tillg„ngliga f”r en rapport.];
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page 9650;
                      RunPageLink=Report ID=FIELD(Report ID);
                      Promoted=Yes;
                      Image=Report;
                      PromotedCategory=Process }
      { 9       ;1   ;Action    ;
                      Name=RunReport;
                      CaptionML=[ENU=Run Report;
                                 NOR=Kj›r rapport;
                                 SVE=K”r rapport];
                      ToolTipML=[ENU=Run a test report.;
                                 NOR=Kj›r en testrapport.;
                                 SVE=K”r en testrapport.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Report;
                      PromotedCategory=Report;
                      OnAction=BEGIN
                                 REPORT.RUN("Report ID");
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=BulkUpdate;
                      CaptionML=[ENU=Update All layouts;
                                 NOR=Oppdater alle oppsett;
                                 SVE=Uppdatera alla layouter];
                      ToolTipML=[ENU=Update specific report layouts or all custom report layouts that might be affected by dataset changes.;
                                 NOR=Oppdater bestemte rapportoppsett eller alle tilpassede rapportoppsett som kan bli p†virket av endringer i datasettet.;
                                 SVE=Uppdatera specifika rapportlayouter eller alla anpassade rapportlayouter som kan p†verkas av dataupps„ttnings„ndringar.];
                      ApplicationArea=#Basic,#Suite;
                      Image=UpdateXML;
                      OnAction=VAR
                                 DocumentReportMgt@1000 : Codeunit 9651;
                               BEGIN
                                 DocumentReportMgt.BulkUpgrade(FALSE);
                               END;
                                }
      { 16      ;1   ;Action    ;
                      Name=TestUpdate;
                      CaptionML=[ENU=Test Layout Updates;
                                 NOR=Test oppdateringer av oppsett;
                                 SVE=Testlayoutuppdateringar];
                      ToolTipML=[ENU=Check if there are any updates detected.;
                                 NOR=Kontroller om det finnes registrerte oppdateringer.;
                                 SVE=Kontrollera om n†gra uppdateringar g†r att identifiera.];
                      ApplicationArea=#Basic,#Suite;
                      Image=TestReport;
                      OnAction=VAR
                                 DocumentReportMgt@1000 : Codeunit 9651;
                               BEGIN
                                 DocumentReportMgt.BulkUpgrade(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 11  ;0   ;Container ;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                CaptionML=[ENU=Company;
                           NOR=Selskap;
                           SVE=F”retag];
                GroupType=Group }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Company Name;
                           NOR=Selskapsnavn;
                           SVE=F”retagsnamn];
                ToolTipML=[ENU=Specifies the name of the company that is used for the report.;
                           NOR=Angir navnet p† selskapet som brukes for rapporten.;
                           SVE=Anger namnet p† f”retaget som anv„nds f”r rapporten.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SelectedCompany;
                TableRelation=Company;
                Importance=Promoted;
                OnValidate=BEGIN
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 15  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the object ID of the report.;
                           NOR=Angir rapportens objekt-ID.;
                           SVE=Anger rapportens objekt-ID.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report ID";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the name of the report.;
                           NOR=Angir navnet p† rapporten.;
                           SVE=Anger rapportens namn.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report Name";
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Selected Layout;
                           NOR=Valgt oppsett;
                           SVE=Vald layout];
                ToolTipML=[ENU=Specifies the report layout that is currently used on the report.;
                           NOR=Angir rapportoppsettet som brukes i rapporten for ›yeblikket.;
                           SVE=Anger den rapportlayout som anv„nds i rapporten f”r n„rvarande.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Type;
                OnValidate=BEGIN
                             UpdateRec;
                             COMMIT;
                             LookupCustomLayout;
                           END;
                            }

    { 13  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the custom report layout.;
                           NOR=Angir det tilpassede rapportoppsettet.;
                           SVE=Anger den anpassade rapportlayouten.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Custom Report Layout Code";
                TableRelation="Custom Report Layout" WHERE (Report ID=FIELD(Report ID));
                Visible=FALSE;
                OnValidate=BEGIN
                             ReportLayoutSelection.VALIDATE("Custom Report Layout Code",ReportLayoutSelection."Custom Report Layout Code");
                             UpdateRec;
                           END;
                            }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Custom Layout Description;
                           NOR=Beskrivelse av egendefinert oppsett;
                           SVE=Anpassad layoutbeskrivning];
                ToolTipML=[ENU=Specifies the description for a custom layout that is used by the report when the Selected Layout field is set to Custom.;
                           NOR=Angir beskrivelsen for et egendefinert oppsett som brukes av rapporten n†r Egendefinert er angitt i feltet Valgt oppsett.;
                           SVE=Anger beskrivningen av en anpassad layout som anv„nds i rapporten n„r f„ltet Vald layout ges v„rdet Anpassad.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CustomLayoutDescription;
                OnValidate=VAR
                             CustomReportLayout2@1000 : Record 9650;
                           BEGIN
                             CustomReportLayout2.SETCURRENTKEY("Report ID","Company Name",Type);
                             CustomReportLayout2.SETRANGE("Report ID",ReportLayoutSelection."Report ID");
                             CustomReportLayout2.SETFILTER("Company Name",'%1|%2','',SelectedCompany);
                             CustomReportLayout2.SETFILTER(
                               Description,STRSUBSTNO('@*%1*',CustomLayoutDescription));
                             IF NOT CustomReportLayout2.FINDFIRST THEN
                               ERROR(CouldNotFindCustomReportLayoutErr,CustomLayoutDescription);

                             IF CustomReportLayout2.Code <> "Custom Report Layout Code" THEN BEGIN
                               VALIDATE("Custom Report Layout Code",CustomReportLayout2.Code);
                               UpdateRec;
                             END;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=BEGIN
                           LookupCustomLayout;
                         END;
                          }

    { 8   ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 7   ;1   ;Part      ;
                CaptionML=[ENU=Custom Layouts;
                           NOR=Egendefinerte oppsett;
                           SVE=Anpassade layouter];
                ApplicationArea=#Basic,#Suite;
                SubPageLink=Report ID=FIELD(Report ID);
                PagePartID=Page9653;
                PartType=Page;
                ShowFilter=No;
                UpdatePropagation=Both }

  }
  CODE
  {
    VAR
      ReportLayoutSelection@1000 : Record 9651;
      SelectedCompany@1004 : Text[30];
      WrongCompanyErr@1005 : TextConst 'ENU=You cannot select a layout that is specific to another company.;NOR=Du kan ikke velge et oppsett som er spesifikt for et annet selskap.;SVE=Du kan inte v„lja en layout som „r specifik f”r ett annat f”retag.';
      CustomLayoutDescription@1002 : Text;
      IsInitialized@1001 : Boolean;
      CouldNotFindCustomReportLayoutErr@1003 : TextConst '@@@=%1 Description of custom report layout;ENU=There is no custom report layout with %1 in the description.;NOR=Det finnes ingen egendefinerte rapportoppsett med %1 i beskrivelsen.;SVE=Det finns ingen anpassad rapportlayout med %1 i beskrivningen.';

    LOCAL PROCEDURE UpdateRec@18();
    BEGIN
      IF ReportLayoutSelection.GET("Report ID",SelectedCompany) THEN BEGIN
        ReportLayoutSelection := Rec;
        ReportLayoutSelection."Report Name" := '';
        ReportLayoutSelection."Company Name" := SelectedCompany;
        OnUpdateRecOnBeforeModify(ReportLayoutSelection,Rec,SelectedCompany);
        ReportLayoutSelection.MODIFY;
      END ELSE BEGIN
        CLEAR(ReportLayoutSelection);
        ReportLayoutSelection := Rec;
        ReportLayoutSelection."Report Name" := '';
        ReportLayoutSelection."Company Name" := SelectedCompany;
        OnUpdateRecOnBeforeInsert(ReportLayoutSelection,Rec,SelectedCompany);
        ReportLayoutSelection.INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetRec@2();
    BEGIN
      IF NOT GET("Report ID",'') THEN
        EXIT;
      IF NOT ReportLayoutSelection.GET("Report ID",SelectedCompany) THEN BEGIN
        ReportLayoutSelection.INIT;
        ReportLayoutSelection.Type := GetDefaultType("Report ID");
      END;
      Type := ReportLayoutSelection.Type;
      "Custom Report Layout Code" := ReportLayoutSelection."Custom Report Layout Code";
      CALCFIELDS("Report Layout Description");
      CustomLayoutDescription := "Report Layout Description";
      OnGetRecOnBeforeModify(Rec,SelectedCompany);
      MODIFY;
    END;

    LOCAL PROCEDURE LookupCustomLayout@5();
    BEGIN
      IF Type <> Type::"Custom Layout" THEN
        EXIT;

      IF NOT SelectReportLayout THEN
        EXIT;

      GetRec;
      IF (Type = Type::"Custom Layout") AND
         ("Custom Report Layout Code" = '')
      THEN BEGIN
        VALIDATE(Type,GetDefaultType("Report ID"));
        UpdateRec;
      END;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SelectReportLayout@3() : Boolean;
    VAR
      CustomReportLayout2@1000 : Record 9650;
      OK@1001 : Boolean;
    BEGIN
      CustomReportLayout2.FILTERGROUP(4);
      CustomReportLayout2.SETRANGE("Report ID","Report ID");
      CustomReportLayout2.FILTERGROUP(0);
      CustomReportLayout2.SETFILTER("Company Name",'%1|%2',SelectedCompany,'');
      OK := PAGE.RUNMODAL(PAGE::"Custom Report Layouts",CustomReportLayout2) = ACTION::LookupOK;
      IF OK THEN BEGIN
        IF CustomReportLayout2.FIND THEN BEGIN
          IF NOT (CustomReportLayout2."Company Name" IN [SelectedCompany,'']) THEN
            ERROR(WrongCompanyErr);
          "Custom Report Layout Code" := CustomReportLayout2.Code;
          Type := Type::"Custom Layout";
          UpdateRec;
        END
      END ELSE
        IF Type = Type::"Custom Layout" THEN
          IF CustomReportLayout2.ISEMPTY THEN BEGIN
            Type := GetDefaultType("Report ID");
            "Custom Report Layout Code" := '';
            UpdateRec;
          END;
      EXIT(OK);
    END;

    LOCAL PROCEDURE InitializeData@1();
    VAR
      ReportMetadata@1000 : Record 2000000139;
    BEGIN
      ReportMetadata.SETRANGE(ProcessingOnly,FALSE);
      IF ReportMetadata.FINDSET THEN
        REPEAT
          INIT;
          "Report ID" := ReportMetadata.ID;
          "Report Name" := ReportMetadata.Caption;
          INSERT;
        UNTIL ReportMetadata.NEXT = 0;
      IF FINDFIRST THEN;
      IsInitialized := TRUE;
    END;

    [Integration]
    LOCAL PROCEDURE OnGetRecOnBeforeModify@7(VAR ReportLayoutSelection@1000 : Record 9651;SelectedCompany@1001 : Text[30]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdateRecOnBeforeInsert@6(VAR ToReportLayoutSelection@1000 : Record 9651;FromReportLayoutSelection@1003 : Record 9651;SelectedCompany@1001 : Text[30]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdateRecOnBeforeModify@4(VAR ToReportLayoutSelection@1000 : Record 9651;FromReportLayoutSelection@1003 : Record 9651;SelectedCompany@1001 : Text[30]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

