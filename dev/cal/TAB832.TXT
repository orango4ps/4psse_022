OBJECT Table 832 Workflows Entries Buffer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    ReplicateData=No;
    CaptionML=[ENU=Workflows Entries Buffer;
               NOR=Buffer for arbeidsflytposter];
  }
  FIELDS
  {
    { 1   ;   ;Created by Application;Option      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Created by Application;
                                                              NOR=Opprettet av program];
                                                   OptionCaptionML=[ENU=Microsoft Flow,Dynamics 365,Dynamics NAV;
                                                                    NOR=Microsoft Flow,Dynamics 365,Dynamics NAV];
                                                   OptionString=Microsoft Flow,Dynamics 365,Dynamics NAV }
    { 2   ;   ;Entry No.           ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Entry No.;
                                                              NOR=Postnr.;
                                                              SVE=Lîpnr] }
    { 3   ;   ;Workflow Step Instance ID;GUID     ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Workflow Step Instance ID;
                                                              NOR=ID for forekomst for arbeidsflyttrinn;
                                                              SVE=Instans-ID fîr arbetsflîdessteg] }
    { 4   ;   ;Record ID           ;RecordID      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Record ID;
                                                              NOR=Post-ID;
                                                              SVE=Post-ID] }
    { 5   ;   ;Initiated By User ID;Code50        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Initiated By User ID;
                                                              NOR=Startet av bruker-ID] }
    { 6   ;   ;To Be Approved By User ID;Code50   ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=To Be Approved By User ID;
                                                              NOR=MÜ godkjennes av bruker-ID] }
    { 7   ;   ;Date-Time Initiated ;DateTime      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Date-Time Initiated;
                                                              NOR=Dato/-klokkeslett initiert] }
    { 8   ;   ;Last Date-Time Modified;DateTime   ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Last Date-Time Modified;
                                                              NOR=Dato/klokkeslett for siste endring;
                                                              SVE=éndrades senast (datum/tid)] }
    { 9   ;   ;Last Modified By User ID;Code50    ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Last Modified By User ID;
                                                              NOR=Sist endret av bruker-ID;
                                                              SVE=éndrades senast av anvÑndar-ID] }
    { 10  ;   ;Status              ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU="Created,Open,Canceled,Rejected,Approved, ";
                                                                    NOR="Opprettet,èpen,Annullert,Avvist,Godkjent, "];
                                                   OptionString=[Created,Open,Canceled,Rejected,Approved, ] }
    { 11  ;   ;Response            ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Response;
                                                              NOR=Svar;
                                                              SVE=Svar];
                                                   OptionCaptionML=[ENU="NotExpected,Pending,Cancel,Continue,Reject, ";
                                                                    NOR="Uventet,Venter,Avbryt,Fortsett,Avvis, "];
                                                   OptionString=[NotExpected,Pending,Cancel,Continue,Reject, ] }
    { 12  ;   ;Amount              ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Amount;
                                                              NOR=Belõp;
                                                              SVE=Belopp] }
    { 13  ;   ;Due Date            ;Date          ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Due Date;
                                                              NOR=Forfallsdato;
                                                              SVE=Fîrfallodatum] }
  }
  KEYS
  {
    {    ;Workflow Step Instance ID               ;Clustered=Yes }
    {    ;Entry No.                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE AddWorkflowWebhookEntry@4(WorkflowWebhookEntry@1000 : Record 467;VAR WorkflowsCounter@1001 : Integer);
    BEGIN
      IF NOT GET(WorkflowWebhookEntry."Workflow Step Instance ID") THEN BEGIN
        WorkflowsCounter := WorkflowsCounter + 1;
        INIT;
        "Created by Application" := "Created by Application"::"Microsoft Flow";
        "Entry No." := WorkflowWebhookEntry."Entry No.";
        "Workflow Step Instance ID" := WorkflowWebhookEntry."Workflow Step Instance ID";
        "Record ID" := WorkflowWebhookEntry."Record ID";
        "Initiated By User ID" := WorkflowWebhookEntry."Initiated By User ID";
        "Date-Time Initiated" := WorkflowWebhookEntry."Date-Time Initiated";
        "Last Date-Time Modified" := WorkflowWebhookEntry."Last Date-Time Modified";
        "Last Modified By User ID" := WorkflowWebhookEntry."Last Modified By User ID";
        Response := WorkflowWebhookEntry.Response;
        Status := Status::" ";
        INSERT;
      END;
    END;

    [External]
    PROCEDURE AddApprovalEntry@5(ApprovalEntry@1000 : Record 454;VAR WorkflowsCounter@1001 : Integer);
    VAR
      AzureAdMgt@1002 : Codeunit 6300;
    BEGIN
      IF NOT GET(ApprovalEntry."Workflow Step Instance ID") THEN BEGIN
        WorkflowsCounter := WorkflowsCounter + 1;
        INIT;
        IF AzureAdMgt.IsSaaS THEN
          "Created by Application" := "Created by Application"::"Dynamics 365"
        ELSE
          "Created by Application" := "Created by Application"::"Dynamics NAV";
        "Entry No." := WorkflowsCounter;
        "Workflow Step Instance ID" := ApprovalEntry."Workflow Step Instance ID";
        "Record ID" := ApprovalEntry."Record ID to Approve";
        "Initiated By User ID" := ApprovalEntry."Sender ID";
        "To Be Approved By User ID" := ApprovalEntry."Approver ID";
        "Date-Time Initiated" := ApprovalEntry."Date-Time Sent for Approval";
        "Last Date-Time Modified" := ApprovalEntry."Last Date-Time Modified";
        "Last Modified By User ID" := ApprovalEntry."Last Modified By User ID";
        Status := ApprovalEntry.Status;
        Response := Response::" ";
        "Due Date" := ApprovalEntry."Due Date";
        INSERT;
      END;
    END;

    [External]
    PROCEDURE RunWorkflowEntriesPage@1(RecordIDInput@1002 : RecordID;TableId@1008 : Integer;DocumentType@1007 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';DocumentNo@1000 : Code[20]);
    VAR
      ApprovalEntry@1006 : Record 454;
      WorkflowWebhookEntry@1001 : Record 467;
      Approvals@1004 : Page 832;
      WorkflowWebhookEntries@1003 : Page 830;
      ApprovalEntries@1005 : Page 658;
    BEGIN
      // if we are looking at a particular record, we want to see only record related workflow entries
      IF DocumentNo <> '' THEN BEGIN
        ApprovalEntry.SETRANGE("Record ID to Approve",RecordIDInput);
        WorkflowWebhookEntry.SETRANGE("Record ID",RecordIDInput);
        // if we have flows created by multiple applications, start generic page filtered for this RecordID
        IF ApprovalEntry.FINDFIRST AND WorkflowWebhookEntry.FINDFIRST THEN BEGIN
          Approvals.Setfilters(RecordIDInput);
          Approvals.RUN;
        END ELSE BEGIN
          // otherwise, open the page filtered for this record that corresponds to the type of the flow
          IF WorkflowWebhookEntry.FINDFIRST THEN BEGIN
            WorkflowWebhookEntries.Setfilters(RecordIDInput);
            WorkflowWebhookEntries.RUN;
            EXIT;
          END;

          IF ApprovalEntry.FINDFIRST THEN BEGIN
            ApprovalEntries.Setfilters(TableId,DocumentType,DocumentNo);
            ApprovalEntries.RUN;
            EXIT;
          END;

          // if no workflow exist, show (empty) joint workflows page
          Approvals.Setfilters(RecordIDInput);
          Approvals.RUN;
        END
      END ELSE
        // otherwise, open the page with all workflow entries
        Approvals.RUN;
    END;

    BEGIN
    END.
  }
}

