OBJECT Table 11124924 Plant App Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Plant App Setup;
  }
  FIELDS
  {
    { 10  ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              NOR=Prim‘rn›kkel;
                                                              SVE=Prim„rnyckel] }
    { 20  ;   ;Proxy URL           ;Text80        ;OnValidate=BEGIN
                                                                UrlEndsWithSlash;
                                                                CheckUrlStartsWithHttps;
                                                              END;

                                                   CaptionML=ENU=Proxy URL }
    { 30  ;   ;Use Generic User Management;Boolean;OnValidate=BEGIN
                                                                IF "Use Generic User Management" THEN BEGIN
                                                                  IF CONFIRM(TEXT002) THEN
                                                                    MigrateUserData()
                                                                  ELSE
                                                                    "Use Generic User Management" := FALSE;
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Use Generic User Management }
    { 40  ;   ;Intent Key Data     ;Text150       ;DataClassification=ToBeClassified;
                                                   CaptionML=ENG=Intent Key Data }
    { 50  ;   ;Scanner Type        ;Option        ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Scanner Type;
                                                   OptionCaptionML=ENU=Camera,Wedge,Intent;
                                                   OptionString=Camera,Wedge,Intent }
    { 60  ;   ;Show Lines of Other Pickers;Boolean;CaptionML=ENU=Show Lines of Other Order Pickers }
    { 70  ;   ;Check On Item Inventory;Boolean    ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Check On Item Inventory }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528000 : TextConst 'ENU=In production environments %1  must start with ''https://''.';
      TEXT002@1100528001 : TextConst 'ENU=Are you sure that you want to convert the Plant App users to Generic user management?';

    LOCAL PROCEDURE UrlEndsWithSlash@1100528000();
    BEGIN
      IF STRLEN("Proxy URL") > 1 THEN
        IF COPYSTR("Proxy URL", STRLEN("Proxy URL"), 1) <> '/' THEN
          "Proxy URL" := "Proxy URL" + '/'
    END;

    LOCAL PROCEDURE CheckUrlStartsWithHttps@1100528001();
    BEGIN
      IF STRLEN("Proxy URL") > 8 THEN
        IF COPYSTR("Proxy URL", 1, 8) <> 'https://' THEN
          MESSAGE(STRSUBSTNO(Text001, FIELDCAPTION("Proxy URL")));
    END;

    LOCAL PROCEDURE MigrateUserData@1100528002();
    VAR
      AppUser@1100528000 : Record 11130031;
      GAPAppUser@1100528001 : Record 11124975;
      ResourceWOP@1100528002 : Record 11229278;
      Employee@1100528003 : Record 5200;
    BEGIN
      AppUser.SETFILTER("Resource No.", '<>%1','');
      IF AppUser.FINDSET THEN BEGIN
        REPEAT
          IF ResourceWOP.GET(AppUser."Resource No.") THEN BEGIN
            IF ResourceWOP."Active in PLA" THEN BEGIN
              GAPAppUser.Username := AppUser.Username;
              GAPAppUser.Name := ResourceWOP.Name;
              IF Employee.GET(ResourceWOP."Source No.") THEN
                GAPAppUser.Email := Employee."E-Mail";

              IF NOT GAPAppUser.INSERT(TRUE) THEN
                GAPAppUser.MODIFY(TRUE);

              //do not validate because it is already hashed with md5
              GAPAppUser.Password := AppUser.Password;
              GAPAppUser.MODIFY(FALSE);

              ResourceWOP."Active in PLA" := FALSE;
              ResourceWOP.MODIFY;

              IF ResourceWOP."Active in CCA" = FALSE THEN
                AppUser.DELETE;
            END;
          END;
        UNTIL AppUser.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

