OBJECT Table 570 G/L Account Category
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Description;
    OnInsert=BEGIN
               CentralDataManagement.UpdateGLAccountCategory(Rec,1,xRec); //**4PS.n
             END;

    OnModify=BEGIN
               CentralDataManagement.UpdateGLAccountCategory(Rec,2,xRec); //**4PS.n
             END;

    OnDelete=VAR
               GLAccount@1000 : Record 15;
             BEGIN
               IF "System Generated" THEN
                 ERROR(CannotDeleteSystemGeneratedErr,Description);
               GLAccount.SETRANGE("Account Subcategory Entry No.","Entry No.");
               IF GLAccount.FINDFIRST THEN
                 ERROR(CategoryUsedOnAccountErr,TABLECAPTION,Description,GLAccount.TABLECAPTION,GLAccount."No.");

               CentralDataManagement.RemoveGLAccountCategory(Rec); //**4PS.n

               DeleteChildren("Entry No.");
             END;

    CaptionML=[ENU=G/L Account Category;
               NOR=Finanskontokategori;
               SVE=Redovisningskontokategori];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No.;
                                                              NOR=L›penummer;
                                                              SVE=L”pnr] }
    { 2   ;   ;Parent Entry No.    ;Integer       ;CaptionML=[ENU=Parent Entry No.;
                                                              NOR=Hovedoppf›ringsnummer;
                                                              SVE=™verordnat transaktionsnr] }
    { 3   ;   ;Sibling Sequence No.;Integer       ;CaptionML=[ENU=Sibling Sequence No.;
                                                              NOR=Sekvensnr. for likestilt;
                                                              SVE=Syskonsekvensnr] }
    { 4   ;   ;Presentation Order  ;Text100       ;CaptionML=[ENU=Presentation Order;
                                                              NOR=Presentasjonsrekkef›lge;
                                                              SVE=Presentationsordning] }
    { 5   ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              NOR=Innrykking;
                                                              SVE=Indrag] }
    { 6   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 7   ;   ;Account Category    ;Option        ;OnValidate=BEGIN
                                                                IF "Account Category" IN ["Account Category"::Income,"Account Category"::"Cost of Goods Sold","Account Category"::Expense]
                                                                THEN BEGIN
                                                                  "Income/Balance" := "Income/Balance"::"Income Statement";
                                                                  "Additional Report Definition" := "Additional Report Definition"::" ";
                                                                END ELSE
                                                                  "Income/Balance" := "Income/Balance"::"Balance Sheet";
                                                                IF Description = '' THEN
                                                                  Description := FORMAT("Account Category");
                                                                UpdatePresentationOrder;
                                                              END;

                                                   CaptionML=[ENU=Account Category;
                                                              NOR=Kontokategori;
                                                              SVE=Kontokategori];
                                                   OptionCaptionML=[ENU=,Assets,Liabilities,Equity,Income,Cost of Goods Sold,Expense;
                                                                    NOR=,Aktiva,Gjeld,Egenkapital,Inntekt,Solgte varers kost,Utgifter;
                                                                    SVE=,Tillg†ngar,Skulder,Eget kapital,Inkomst,Kostnad f”r s†lda varor,Utgift];
                                                   OptionString=,Assets,Liabilities,Equity,Income,Cost of Goods Sold,Expense;
                                                   BlankZero=Yes }
    { 8   ;   ;Income/Balance      ;Option        ;OnValidate=BEGIN
                                                                UpdatePresentationOrder;
                                                              END;

                                                   CaptionML=[ENU=Income/Balance;
                                                              NOR=Resultat/balanse;
                                                              SVE=Resultat/Saldo];
                                                   OptionCaptionML=[ENU=Income Statement,Balance Sheet;
                                                                    NOR=Resultat,Balanse;
                                                                    SVE=Resultatr„kning,Balansr„kning];
                                                   OptionString=Income Statement,Balance Sheet;
                                                   Editable=No }
    { 9   ;   ;Additional Report Definition;Option;OnValidate=BEGIN
                                                                IF "Additional Report Definition" <> "Additional Report Definition"::" " THEN
                                                                  TESTFIELD("Income/Balance","Income/Balance"::"Balance Sheet");
                                                              END;

                                                   CaptionML=[ENU=Additional Report Definition;
                                                              NOR=Definisjon av tilleggsrapport;
                                                              SVE=Alternativ rapportdefinition];
                                                   OptionCaptionML=[ENU=" ,Operating Activities,Investing Activities,Financing Activities,Cash Accounts,Retained Earnings,Distribution to Shareholders";
                                                                    NOR=" ,Driftsaktiviteter,Investeringsaktiviteter,Finansaktiviteter,Kontantkontoer,Fri egenkapital,Fordelt til aksjon‘rer";
                                                                    SVE=" ,R”relseaktiviteter,Investeringsaktiviteter,Finansieringsaktiviteter,Kassakonton,Balanserad vinst eller f”rlust,Utdelning till aktie„gare"];
                                                   OptionString=[ ,Operating Activities,Investing Activities,Financing Activities,Cash Accounts,Retained Earnings,Distribution to Shareholders] }
    { 11  ;   ;System Generated    ;Boolean       ;CaptionML=[ENU=System Generated;
                                                              NOR=Systemgenerert;
                                                              SVE=Systemgenererat] }
    { 12  ;   ;Has Children        ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("G/L Account Category" WHERE (Parent Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Has Children;
                                                              NOR=Har underordnede;
                                                              SVE=Har underordnade] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Presentation Order,Sibling Sequence No.  }
    {    ;Parent Entry No.                         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NewCategoryTxt@1001 : TextConst 'ENU=<Enter a Name>;NOR=<Angi et navn>;SVE=<Skriv ett namn>';
      CannotDeleteSystemGeneratedErr@1019 : TextConst '@@@="%1 = a category value, e.g. ""Assets""";ENU=%1 is a system generated category and cannot be deleted.;NOR=%1 er en systemgenerert kategori og kan ikke slettes.;SVE=%1 „r en systemgenererad kategori och kan inte tas bort.';
      NoAccountsInFilterErr@1002 : TextConst '@@@="%1 = either ''Balance Sheet'' or ''Income Statement''";ENU=There are no G/L Accounts in the filter of type %1.;NOR=Det finnes ingen finanskonti i filteret av typen %1.;SVE=Det finns inga redovisningskonton i filtret av typen %1.';
      CategoryUsedOnAccountErr@1003 : TextConst '@@@="%1=account category table name, %2=category description, %3=g/l account table name, %4=g/l account number.";ENU=You cannot delete %1 %2 because it is used in %3 %4.;NOR=Du kan ikke slette %1 %2 fordi den brukes i %3 %4.;SVE=Du kan inte ta bort %1 %2 eftersom det anv„nds i %3 %4.';
      CentralDataManagement@1100529200 : Codeunit 11012010;

    [External]
    PROCEDURE UpdatePresentationOrder@4();
    VAR
      GLAccountCategory@1000 : Record 570;
      PresentationOrder@1001 : Text;
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;
      GLAccountCategory := Rec;
      IF "Sibling Sequence No." = 0 THEN
        "Sibling Sequence No." := "Entry No." * 10000 MOD 2000000000;
      Indentation := 0;
      PresentationOrder := COPYSTR(FORMAT(1000000 + "Sibling Sequence No."),2);
      WHILE GLAccountCategory."Parent Entry No." <> 0 DO BEGIN
        Indentation += 1;
        GLAccountCategory.GET(GLAccountCategory."Parent Entry No.");
        PresentationOrder := COPYSTR(FORMAT(1000000 + GLAccountCategory."Sibling Sequence No."),2) + PresentationOrder;
      END;
      CASE "Account Category" OF
        "Account Category"::Assets:
          PresentationOrder := '0' + PresentationOrder;
        "Account Category"::Liabilities:
          PresentationOrder := '1' + PresentationOrder;
        "Account Category"::Equity:
          PresentationOrder := '2' + PresentationOrder;
        "Account Category"::Income:
          PresentationOrder := '3' + PresentationOrder;
        "Account Category"::"Cost of Goods Sold":
          PresentationOrder := '4' + PresentationOrder;
        "Account Category"::Expense:
          PresentationOrder := '5' + PresentationOrder;
      END;
      "Presentation Order" := COPYSTR(PresentationOrder,1,MAXSTRLEN("Presentation Order"));
      //MODIFY; //**4PS.o
      MODIFY(TRUE); //**4PS.n
    END;

    [External]
    PROCEDURE InitializeDataSet@1();
    BEGIN
      CODEUNIT.RUN(CODEUNIT::"G/L Account Category Mgt.");
    END;

    [External]
    PROCEDURE InsertRow@6() : Integer;
    VAR
      GLAccountCategoryMgt@1000 : Codeunit 570;
    BEGIN
      EXIT(GLAccountCategoryMgt.AddCategory("Entry No.","Parent Entry No.","Account Category",NewCategoryTxt,FALSE,0));
    END;

    LOCAL PROCEDURE Move@13(Steps@1002 : Integer);
    VAR
      GLAccountCategory@1000 : Record 570;
      SiblingOrder@1001 : Integer;
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;
      GLAccountCategory := Rec;
      GLAccountCategory.SETRANGE("Parent Entry No.","Parent Entry No.");
      GLAccountCategory.SETRANGE("Account Category","Account Category");
      GLAccountCategory.SETCURRENTKEY("Presentation Order","Sibling Sequence No.");
      IF GLAccountCategory.NEXT(Steps) = 0 THEN
        EXIT;
      SiblingOrder := "Sibling Sequence No.";
      "Sibling Sequence No." := GLAccountCategory."Sibling Sequence No.";
      GLAccountCategory."Sibling Sequence No." := SiblingOrder;
      GLAccountCategory.UpdatePresentationOrder;
      //GLAccountCategory.MODIFY; //**4PS.o
      GLAccountCategory.MODIFY(TRUE); //**4PS.n
      UpdatePresentationOrder;
      //MODIFY; //**4PS.o
      MODIFY(TRUE); //**4PS.n
      UpdateDescendants(Rec);
      UpdateDescendants(GLAccountCategory);
    END;

    [External]
    PROCEDURE MoveUp@11();
    BEGIN
      Move(-1);
    END;

    [External]
    PROCEDURE MoveDown@14();
    BEGIN
      Move(1);
    END;

    LOCAL PROCEDURE ChangeAncestor@10(ChangeToChild@1002 : Boolean);
    VAR
      GLAccountCategory@1000 : Record 570;
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;
      GLAccountCategory := Rec;
      IF ChangeToChild THEN BEGIN
        GLAccountCategory.SETRANGE("Parent Entry No.","Parent Entry No.");
        GLAccountCategory.SETRANGE(Indentation,Indentation);
        GLAccountCategory.SETCURRENTKEY("Presentation Order","Sibling Sequence No.");
        IF GLAccountCategory.NEXT(-1) = 0 THEN
          EXIT;
        "Parent Entry No." := GLAccountCategory."Entry No."
      END ELSE
        IF GLAccountCategory.GET("Parent Entry No.") THEN
          "Parent Entry No." := GLAccountCategory."Parent Entry No."
        ELSE
          EXIT;
      UpdatePresentationOrder;
      //MODIFY; //**4PS.o
      MODIFY(TRUE); //**4PS.n
      UpdateDescendants(Rec);
    END;

    LOCAL PROCEDURE UpdateDescendants@12(ParentGLAccountCategory@1000 : Record 570);
    VAR
      GLAccountCategory@1001 : Record 570;
    BEGIN
      IF ParentGLAccountCategory."Entry No." = 0 THEN
        EXIT;
      GLAccountCategory.SETRANGE("Parent Entry No.",ParentGLAccountCategory."Entry No.");
      IF GLAccountCategory.FINDSET THEN
        REPEAT
          GLAccountCategory."Income/Balance" := ParentGLAccountCategory."Income/Balance";
          GLAccountCategory."Account Category" := ParentGLAccountCategory."Account Category";
          GLAccountCategory.UpdatePresentationOrder;
          UpdateDescendants(GLAccountCategory);
        UNTIL GLAccountCategory.NEXT = 0;
    END;

    [External]
    PROCEDURE MakeChildOfPreviousSibling@15();
    BEGIN
      ChangeAncestor(TRUE);
    END;

    [External]
    PROCEDURE MakeSiblingOfParent@16();
    BEGIN
      ChangeAncestor(FALSE);
    END;

    [External]
    PROCEDURE DeleteRow@9();
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;
      DeleteChildren("Entry No.");
      DELETE(TRUE);
    END;

    LOCAL PROCEDURE DeleteChildren@7(ParentEntryNo@1001 : Integer);
    VAR
      GLAccountCategory@1000 : Record 570;
    BEGIN
      GLAccountCategory.SETRANGE("Parent Entry No.",ParentEntryNo);
      IF GLAccountCategory.FINDSET THEN
        REPEAT
          GLAccountCategory.DeleteRow;
        UNTIL GLAccountCategory.NEXT = 0;
    END;

    [External]
    PROCEDURE MapAccounts@8();
    BEGIN
    END;

    [External]
    PROCEDURE ValidateTotaling@5(NewTotaling@1001 : Text);
    VAR
      GLAccount@1000 : Record 15;
      OldTotaling@1002 : Text;
    BEGIN
      //**4PS.sn
      IF NOT CentralDataManagement.FinanceFormEditable THEN
        EXIT;
      //**4PS.en

      OldTotaling := GetTotaling;
      IF NewTotaling = OldTotaling THEN
        EXIT;
      IF NewTotaling <> '' THEN BEGIN
        GLAccount.SETFILTER("No.",NewTotaling);
        GLAccount.SETRANGE("Income/Balance","Income/Balance");
        GLAccount.LOCKTABLE;
        IF NOT GLAccount.FINDSET THEN
          ERROR(NoAccountsInFilterErr,"Income/Balance");
        IF OldTotaling <> '' THEN
          ClearGLAccountSubcategoryEntryNo(OldTotaling,"Income/Balance");
        REPEAT
          GLAccount.VALIDATE("Account Subcategory Entry No.","Entry No.");
          GLAccount.MODIFY(TRUE);
        UNTIL GLAccount.NEXT = 0;
      END ELSE
        ClearGLAccountSubcategoryEntryNo(OldTotaling,"Income/Balance");
    END;

    LOCAL PROCEDURE ClearGLAccountSubcategoryEntryNo@3(Filter@1000 : Text;IncomeBalance@1002 : Integer);
    VAR
      GLAccount@1001 : Record 15;
    BEGIN
      GLAccount.SETFILTER("No.",Filter);
      GLAccount.SETRANGE("Income/Balance",IncomeBalance);
      GLAccount.MODIFYALL("Account Subcategory Entry No.",0);
    END;

    [External]
    PROCEDURE LookupTotaling@22();
    VAR
      GLAccount@1001 : Record 15;
      GLAccList@1000 : Page 18;
      OldTotaling@1002 : Text;
    BEGIN
      //**4PS.sn
      IF NOT CentralDataManagement.FinanceFormEditable THEN
        EXIT;
      //**4PS.en

      GLAccount.SETRANGE("Income/Balance","Income/Balance");
      OldTotaling := GetTotaling;
      IF OldTotaling <> '' THEN BEGIN
        GLAccount.SETFILTER("No.",OldTotaling);
        IF GLAccount.FINDFIRST THEN
          GLAccList.SETRECORD(GLAccount);
        GLAccount.SETRANGE("No.");
      END;
      GLAccList.SETTABLEVIEW(GLAccount);
      GLAccList.LOOKUPMODE(TRUE);
      IF GLAccList.RUNMODAL = ACTION::LookupOK THEN
        ValidateTotaling(GLAccList.GetSelectionFilter);
    END;

    [External]
    PROCEDURE PositiveNormalBalance@19() : Boolean;
    BEGIN
      EXIT("Account Category" IN ["Account Category"::Expense,"Account Category"::Assets,"Account Category"::"Cost of Goods Sold"]);
    END;

    [External]
    PROCEDURE GetBalance@17() : Decimal;
    VAR
      GLEntry@1000 : Record 17;
      GLAccountCategory@1001 : Record 570;
      Balance@1002 : Decimal;
      TotalingStr@1004 : Text;
      IsHandled@1003 : Boolean;
    BEGIN
      CALCFIELDS("Has Children");
      IF "Has Children" THEN BEGIN
        GLAccountCategory.SETRANGE("Parent Entry No.","Entry No.");
        IF GLAccountCategory.FINDSET THEN
          REPEAT
            Balance += GLAccountCategory.GetBalance;
          UNTIL GLAccountCategory.NEXT = 0;
      END;
      TotalingStr := GetTotaling;
      IF TotalingStr = '' THEN
        EXIT(Balance);

      IsHandled := FALSE;
      OnGetBalanceOnAfterGetTotaling(Rec,TotalingStr,Balance,IsHandled);
      IF IsHandled THEN
        EXIT(Balance);

      GLEntry.SETFILTER("G/L Account No.",TotalingStr);
      GLEntry.CALCSUMS(Amount);
      EXIT(Balance + GLEntry.Amount);
    END;

    [External]
    PROCEDURE GetTotaling@2() : Text[250];
    VAR
      GLAccount@1000 : Record 15;
      SelectionFilterManagement@1001 : Codeunit 46;
      TotalingStr@1003 : Text[250];
      IsHandled@1002 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeGetTotaling(Rec,TotalingStr,IsHandled);
      IF IsHandled THEN
        EXIT(TotalingStr);

      GLAccount.SETRANGE("Account Subcategory Entry No.","Entry No.");
      EXIT(COPYSTR(SelectionFilterManagement.GetSelectionFilterForGLAccount(GLAccount),1,250));
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeGetTotaling@20(GLAccountCategory@1000 : Record 570;VAR TotallingStr@1001 : Text[250];VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGetBalanceOnAfterGetTotaling@18(VAR GLAccountCategory@1000 : Record 570;TotalingStr@1001 : Text;VAR Balance@1002 : Decimal;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

