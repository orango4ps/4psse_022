OBJECT Table 11229942 DPT Default Subsite Doc Type
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               DocumentSetupPeripheralApp@1100528304 : Record 11072451;
               DPTDefRespPersPermProject@1100528300 : Record 11072333;
               DPTDefAuthorityPermProject@1100528301 : Record 11072335;
               DPTDefRespPersPermServLoc@1100528302 : Record 11072341;
               DPTDefAuthorityPermServLoc@1100528303 : Record 11072342;
             BEGIN
               DPTDefRespPersPermProject.SETRANGE("Subsite Code", "Subsite Code");
               DPTDefRespPersPermProject.SETRANGE("Document Type", "Document Type");
               DPTDefRespPersPermProject.DELETEALL(TRUE);

               DPTDefAuthorityPermProject.SETRANGE("Subsite Code", "Subsite Code");
               DPTDefAuthorityPermProject.SETRANGE("Document Type", "Document Type");
               DPTDefAuthorityPermProject.DELETEALL(TRUE);

               DPTDefRespPersPermServLoc.SETRANGE("Subsite Code", "Subsite Code");
               DPTDefRespPersPermServLoc.SETRANGE("Document Type", "Document Type");
               DPTDefRespPersPermServLoc.DELETEALL(TRUE);

               DPTDefAuthorityPermServLoc.SETRANGE("Subsite Code", "Subsite Code");
               DPTDefAuthorityPermServLoc.SETRANGE("Document Type", "Document Type");
               DPTDefAuthorityPermServLoc.DELETEALL(TRUE);

               DocumentSetupPeripheralApp.SETRANGE("Document Portal Subsite", "Subsite Code");
               DocumentSetupPeripheralApp.SETRANGE("Document Type", "Document Type");
               DocumentSetupPeripheralApp.DELETEALL(TRUE);
             END;

    CaptionML=ENU=DPT Default Subsite Per Document Type;
  }
  FIELDS
  {
    { 10  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=" ,Project,Service Location";
                                                                    SVE=,Project,Serviceplats];
                                                   OptionString=[ ,Project,ServiceLocation];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Default Subsite".Code WHERE (Source Type=FIELD(Source Type),
                                                                                                   Code=FIELD(Subsite Code));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Subsite Description", "Subsite Structure");
                                                              END;

                                                   CaptionML=ENU=Subsite Code;
                                                   NotBlank=Yes }
    { 21  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite".Description WHERE (Source Type=FIELD(Source Type),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description }
    { 22  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite"."Subsite Structure" WHERE (Source Type=FIELD(Source Type),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 30  ;   ;Document Type       ;Code10        ;TableRelation="Document Type";
                                                   OnValidate=VAR
                                                                DPTInterface@1100527800 : Codeunit 11012451;
                                                              BEGIN
                                                                DPTInterface.TestDocumentTypeCode("Document Type");
                                                                CALCFIELDS("Document Type Description");
                                                              END;

                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   NotBlank=Yes }
    { 31  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 40  ;   ;Default Quote Subsite OLD;Boolean  ;OnValidate=BEGIN
                                                                CheckDefaultQuoteSubsite;
                                                              END;

                                                   CaptionML=ENU=Default Quote Subsite;
                                                   Editable=No }
    { 90  ;   ;Use Document CheckIn/-Out;Boolean  ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                UpdateSubsiteStructure;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out }
  }
  KEYS
  {
    {    ;Source Type,Subsite Code,Document Type  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=There can be only 1 %1.';
      Text001@1100528301 : TextConst 'ENU=You are about to create ALL Subsite - Document Type combinations.\\Are you sure?';
      Text002@1100525001 : TextConst 'ENU=%1 cann''t be equal to %2, because this is already the Quote Request %1.';

    PROCEDURE CheckDefaultQuoteSubsite@1100528300();
    VAR
      DPTDefaultSubsiteDocType@1100528301 : Record 11229942;
      DocumentTemplate@1100525000 : Record 11012401;
    BEGIN
      DPTDefaultSubsiteDocType.SETRANGE("Source Type", "Source Type");
      DPTDefaultSubsiteDocType.SETFILTER("Document Type", '<>%1', "Document Type");
      DPTDefaultSubsiteDocType.SETRANGE("Default Quote Subsite OLD", TRUE);
      IF (NOT DPTDefaultSubsiteDocType.ISEMPTY) THEN
        ERROR(Text000, FIELDCAPTION("Default Quote Subsite OLD"));

      //#C039464.c
      DocumentTemplate.SETRANGE(Type, DocumentTemplate.Type::"Purchase Quote");
      DocumentTemplate.SETRANGE("Document Type", "Document Type");
      IF (NOT DocumentTemplate.ISEMPTY) THEN
        ERROR(Text002, FIELDCAPTION("Document Type"), "Document Type");
    END;

    PROCEDURE CreateAllDocumentTypeLinks@1100528304(SourceType@1100528303 : Option);
    VAR
      DocumentType@1100528301 : Record 11012405;
      DPTDefaultSubsite@1100528300 : Record 11229941;
      DPTDefaultSubsiteDocType@1100528302 : Record 11229942;
    BEGIN
      IF (NOT CONFIRM(Text001, FALSE)) THEN
        EXIT;
      DPTDefaultSubsite.SETRANGE("Source Type", SourceType);
      DPTDefaultSubsite.SETRANGE("Quote Request Subsite", FALSE);
      IF (NOT DPTDefaultSubsite.FINDSET) THEN
        EXIT;

      REPEAT
        DocumentType.SETFILTER(Type, '<>%1', '');
        IF (DocumentType.FINDSET) THEN BEGIN
          REPEAT
            IF (NOT DPTDefaultSubsiteDocType.GET(DPTDefaultSubsite."Source Type", DPTDefaultSubsite.Code, DocumentType.Type)) THEN BEGIN
              DPTDefaultSubsiteDocType.INIT;
              DPTDefaultSubsiteDocType."Source Type" := DPTDefaultSubsite."Source Type";
              DPTDefaultSubsiteDocType."Subsite Code" := DPTDefaultSubsite.Code;
              DPTDefaultSubsiteDocType."Document Type" := DocumentType.Type;
              DPTDefaultSubsiteDocType."Use Document CheckIn/-Out" := DPTDefaultSubsite."Use Document CheckIn/-Out";
              DPTDefaultSubsiteDocType.INSERT(TRUE);
            END;
          UNTIL (DocumentType.NEXT = 0);
        END;
      UNTIL (DPTDefaultSubsite.NEXT = 0);
    END;

    PROCEDURE ToggleUseDocumentCheckInOut@1100528301(NewValue@1100528300 : Boolean);
    BEGIN
      IF (NOT FINDSET(TRUE)) THEN
        EXIT;

      REPEAT
        VALIDATE("Use Document CheckIn/-Out", NewValue);
        MODIFY(TRUE);
      UNTIL (NEXT = 0);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528302();
    BEGIN
    END;

    LOCAL PROCEDURE UpdateSubsiteStructure@1100528303();
    VAR
      DPTDefaultSubsiteDocType@1100528300 : Record 11229942;
    BEGIN
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure"<> "Subsite Structure"::OneDocLibrary) THEN
        EXIT;

      DPTDefaultSubsiteDocType.SETRANGE("Source Type", "Source Type");
      DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", "Subsite Code");
      DPTDefaultSubsiteDocType.SETFILTER("Document Type", '<>%1', "Document Type");
      DPTDefaultSubsiteDocType.MODIFYALL("Use Document CheckIn/-Out", "Use Document CheckIn/-Out");
    END;

    BEGIN
    END.
  }
}

