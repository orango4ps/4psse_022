OBJECT Codeunit 99000809 Planning Line Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 99000765=rm,
                TableData 99000763=r,
                TableData 99000771=r,
                TableData 99000772=r,
                TableData 5410=rd,
                TableData 99000829=rimd,
                TableData 99000830=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Phantomstcklistenstruktur von %1 hat mehr als 50 Ebenen.;ENU=BOM phantom structure for %1 is higher than 50 levels.;NLD=Phantomstructuur van stuklijst voor %1 bevat meer dan 50 niveaus.;NOR=Stykklistefantomstruktur for %1 er h›yere enn 50 niv†er.;SVE=Struktur med fiktiva artiklar f”r %1 „r mer „n 50 niv†er.';
      Text002@1002 : TextConst 'DEU=Es ist nicht gengend Platz vorhanden, um die tieferen Stcklistenebenen der Auftragsfertigungszeile einzufgen.;ENU=There is not enough space to insert lower level Make-to-Order lines.;NLD=Er is niet voldoende ruimte om Make-to-Orderregels van een lager niveau in te voegen.;NOR=Det er ikke nok plass til † sette inn lavniv†linjer for kundeordreproduksjon.;SVE=Det finns inte tillr„ckligt med utrymme f”r att infoga l„gre niv†ers Tillverka-Mot-Kundorder raderna.';
      Item@1003 : Record 27;
      SKU@1004 : Record 5700;
      ReqLine@1005 : Record 246;
      ProdBOMLine@1006 : ARRAY [50] OF Record 99000772;
      AsmBOMComp@1026 : ARRAY [50] OF Record 90;
      PlanningRtngLine2@1007 : Record 99000830;
      PlanningComponent@1008 : Record 99000829;
      TempPlanningComponent@1010 : TEMPORARY Record 99000829;
      TempPlanningErrorLog@1001 : TEMPORARY Record 5430;
      CalcPlanningRtngLine@1020 : Codeunit 99000810;
      UOMMgt@1011 : Codeunit 5402;
      CostCalcMgt@1012 : Codeunit 5836;
      PlanningRoutingMgt@1013 : Codeunit 99000808;
      VersionMgt@1014 : Codeunit 99000756;
      GetPlanningParameters@1015 : Codeunit 99000855;
      LeadTimeMgt@1016 : Codeunit 5404;
      CalendarMgt@1027 : Codeunit 99000755;
      LineSpacing@1018 : ARRAY [50] OF Integer;
      NextPlanningCompLineNo@1025 : Integer;
      Blocked@1019 : Boolean;
      PlanningResiliency@1017 : Boolean;
      Text010@1021 : TextConst 'DEU=%5 nicht definiert fr die Zeile mit %1 %2 fr %3 %4 oder eine Version davon.;ENU=The line with %1 %2 for %3 %4 or one of its versions, has no %5 defined.;NLD=Op de regel met %1 %2 voor %3 %4 of een van de versies hiervan is geen %5 gedefinieerd.;NOR=Linjen med %1 %2 for %3 %4 eller ‚n av dens versjoner har ingen %5 definert.;SVE=%5 har inte definierats f”r raden med %1 %2 f”r %3 %4 eller n†gon av dess versioner.';
      Text011@1022 : TextConst 'DEU=Fr %1 ist ''Neu berechnen'' auf False festgelegt.;ENU=%1 has recalculate set to false.;NLD=Voor %1 is opnieuw berekenen ingesteld op onwaar.;NOR=%1 har omregning satt til false.;SVE=%1 har omber„knats, anges till falskt.';
      Text012@1009 : TextConst 'DEU=Sie mssen %1 in %2 %3 angeben.;ENU=You must specify %1 in %2 %3.;NLD=Geef %1 op in %2 %3.;NOR=Du m† angi %1 i %2 %3.;SVE=Du m†ste ange %1 i %2 %3.';
      Text014@1023 : TextConst 'DEU=Fert.-Stcklistenkopf Nr. %1, verwendet von Artikel %2, weist Ebenen ber 50 in der Stcklistenstruktur auf.;ENU=Production BOM Header No. %1 used by Item %2 has BOM levels that exceed 50.;NLD=Productiestuklijstkopnr. %1 gebruikt door artikel %2 heeft meer dan 50 stuklijstniveaus.;NOR=Hodenr. %1 for produksjonsstykkliste, som ble brukt av vare %2, har stykklisteniv†er som overstiger 50.;SVE=Prod.strukturhuvudet nr %1 som anv„nds av artikeln %2 har niv†er som ”verstiger 50.';
      Text015@1024 : TextConst 'DEU=Es ist kein Platz zum Einfgen einer weiteren Zeile in die Arbeitsmappe vorhanden.;ENU=There is no more space to insert another line in the worksheet.;NLD=Er is niet voldoende ruimte voor nog een regel op het werkblad.;NOR=Det er ikke plass til † sette inn en ny linje i forslaget.;SVE=Det finns inte plats att infoga en ny rad i kalkylbladet.';

    LOCAL PROCEDURE TransferRouting@3(VAR ReqLine@1004 : Record 246);
    VAR
      RoutingHeader@1003 : Record 99000763;
      RoutingLine@1000 : Record 99000764;
      PlanningRoutingLine@1001 : Record 99000830;
      IsHandled@1005 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeTransferRouting(ReqLine,PlanningResiliency,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF ReqLine."Routing No." = '' THEN
        EXIT;

      RoutingHeader.GET(ReqLine."Routing No.");
      RoutingLine.SETRANGE("Routing No.",ReqLine."Routing No.");
      RoutingLine.SETRANGE("Version Code",ReqLine."Routing Version Code");
      IF RoutingLine.FIND('-') THEN
        REPEAT
          IF PlanningResiliency AND PlanningRoutingLine.Recalculate THEN
            TempPlanningErrorLog.SetError(
              STRSUBSTNO(Text011,PlanningRoutingLine.TABLECAPTION),
              DATABASE::"Routing Header",RoutingHeader.GETPOSITION);
          PlanningRoutingLine.TESTFIELD(Recalculate,FALSE);
          CheckRoutingLine(RoutingHeader,RoutingLine);
          TransferRoutingLine(PlanningRoutingLine,ReqLine,RoutingLine);
        UNTIL RoutingLine.NEXT = 0;

      OnAfterTransferRouting(ReqLine);
    END;

    LOCAL PROCEDURE TransferRoutingLine@26(VAR PlanningRoutingLine@1000 : Record 99000830;ReqLine@1001 : Record 246;RoutingLine@1002 : Record 99000764);
    BEGIN
      WITH PlanningRoutingLine DO BEGIN
        TransferFromReqLine(ReqLine);
        TransferFromRoutingLine(RoutingLine);

        CostCalcMgt.RoutingCostPerUnit(
          Type,"No.","Direct Unit Cost","Indirect Cost %","Overhead Rate","Unit Cost per","Unit Cost Calculation");
        VALIDATE("Direct Unit Cost");

        UpdateDatetime;
        OnAfterTransferRtngLine(ReqLine,RoutingLine,PlanningRoutingLine);
        INSERT;
      END;
    END;

    LOCAL PROCEDURE TransferBOM@4(ProdBOMNo@1000 : Code[20];Level@1001 : Integer;LineQtyPerUOM@1002 : Decimal;ItemQtyPerUOM@1010 : Decimal);
    VAR
      BOMHeader@1003 : Record 99000771;
      CompSKU@1004 : Record 5700;
      ReqQty@1007 : Decimal;
      IsHandled@1005 : Boolean;
      UpdateCondition@1006 : Boolean;
    BEGIN
      IF ReqLine."Production BOM No." = '' THEN
        EXIT;

      PlanningComponent.LOCKTABLE;

      IF Level > 50 THEN BEGIN
        IF PlanningResiliency THEN BEGIN
          BOMHeader.GET(ReqLine."Production BOM No.");
          TempPlanningErrorLog.SetError(
            STRSUBSTNO(Text014,ReqLine."Production BOM No.",ReqLine."No."),
            DATABASE::"Production BOM Header",BOMHeader.GETPOSITION);
        END;
        ERROR(
          Text000,
          ProdBOMNo);
      END;

      IF NextPlanningCompLineNo = 0 THEN BEGIN
        PlanningComponent.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
        PlanningComponent.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
        PlanningComponent.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
        IF PlanningComponent.FIND('+') THEN
          NextPlanningCompLineNo := PlanningComponent."Line No.";
        PlanningComponent.RESET;
      END;

      BOMHeader.GET(ProdBOMNo);

      ProdBOMLine[Level].SETRANGE("Production BOM No.",ProdBOMNo);
      IF Level > 1 THEN
        ProdBOMLine[Level].SETRANGE("Version Code",VersionMgt.GetBOMVersion(BOMHeader."No.",ReqLine."Starting Date",TRUE))
      ELSE
        ProdBOMLine[Level].SETRANGE("Version Code",ReqLine."Production BOM Version Code");
      ProdBOMLine[Level].SETFILTER("Starting Date",'%1|..%2',0D,ReqLine."Starting Date");
      ProdBOMLine[Level].SETFILTER("Ending Date",'%1|%2..',0D,ReqLine."Starting Date");
      OnTransferBOMOnAfterProdBOMLineSetFilters(ProdBOMLine[Level],ReqLine);
      IF ProdBOMLine[Level].FIND('-') THEN
        REPEAT
          IsHandled := FALSE;
          OnTransferBOMOnBeforeTransferPlanningComponent(ReqLine,ProdBOMLine[Level],Blocked,IsHandled);
          IF NOT IsHandled THEN BEGIN
            IF ProdBOMLine[Level]."Routing Link Code" <> '' THEN BEGIN
              PlanningRtngLine2.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
              PlanningRtngLine2.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
              PlanningRtngLine2.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
              PlanningRtngLine2.SETRANGE("Routing Link Code",ProdBOMLine[Level]."Routing Link Code");
              PlanningRtngLine2.FINDFIRST;
              ReqQty :=
                ProdBOMLine[Level].Quantity *
                (1 + ProdBOMLine[Level]."Scrap %" / 100) *
                (1 + PlanningRtngLine2."Scrap Factor % (Accumulated)") *
                (1 + ReqLine."Scrap %" / 100) *
                LineQtyPerUOM /
                ItemQtyPerUOM +
                PlanningRtngLine2."Fixed Scrap Qty. (Accum.)";
            END ELSE
              ReqQty :=
                ProdBOMLine[Level].Quantity *
                (1 + ProdBOMLine[Level]."Scrap %" / 100) *
                (1 + ReqLine."Scrap %" / 100) *
                LineQtyPerUOM /
                ItemQtyPerUOM;
            CASE ProdBOMLine[Level].Type OF
              ProdBOMLine[Level].Type::Item:
                BEGIN
                  IsHandled := FALSE;
                  UpdateCondition := ReqQty <> 0;
                  OnTransferBOMOnBeforeUpdatePlanningComp(ProdBOMLine[Level],UpdateCondition,IsHandled);
                  IF NOT IsHandled THEN
                    IF UpdateCondition THEN BEGIN
                      IF NOT IsPlannedComp(PlanningComponent,ReqLine,ProdBOMLine[Level]) THEN BEGIN
                        NextPlanningCompLineNo := NextPlanningCompLineNo + 10000;
                        CreatePlanningComponentFromProdBOM(
                          PlanningComponent,ReqLine,ProdBOMLine[Level],CompSKU,LineQtyPerUOM,ItemQtyPerUOM);
                      END ELSE BEGIN
                        PlanningComponent.RESET;
                        PlanningComponent.BlockDynamicTracking(Blocked);
                        PlanningComponent.VALIDATE(
                          "Quantity per",
                          PlanningComponent."Quantity per" + ProdBOMLine[Level]."Quantity per" * LineQtyPerUOM / ItemQtyPerUOM);
                        PlanningComponent.VALIDATE("Routing Link Code",ProdBOMLine[Level]."Routing Link Code");
                        OnBeforeModifyPlanningComponent(ReqLine,ProdBOMLine[Level],PlanningComponent,LineQtyPerUOM,ItemQtyPerUOM);
                        PlanningComponent.MODIFY;
                      END;

                      // A temporary list of Planning Components handled is sustained:
                      TempPlanningComponent := PlanningComponent;
                      IF NOT TempPlanningComponent.INSERT THEN
                        TempPlanningComponent.MODIFY;
                    END;
                END;
              ProdBOMLine[Level].Type::"Production BOM":
                BEGIN
                  OnTransferBOMOnBeforeTransferProductionBOM(ReqQty,ProdBOMLine[Level],LineQtyPerUOM,ItemQtyPerUOM);
                  TransferBOM(ProdBOMLine[Level]."No.",Level + 1,ReqQty,1);
                  ProdBOMLine[Level].SETRANGE("Production BOM No.",ProdBOMNo);
                  ProdBOMLine[Level].SETRANGE(
                    "Version Code",VersionMgt.GetBOMVersion(ProdBOMNo,ReqLine."Starting Date",TRUE));
                  ProdBOMLine[Level].SETFILTER("Starting Date",'%1|..%2',0D,ReqLine."Starting Date");
                  ProdBOMLine[Level].SETFILTER("Ending Date",'%1|%2..',0D,ReqLine."Starting Date");
                END;
            END;
          END;
        UNTIL ProdBOMLine[Level].NEXT = 0;
    END;

    LOCAL PROCEDURE TransferAsmBOM@13(ParentItemNo@1000 : Code[20];Level@1001 : Integer;Quantity@1002 : Decimal);
    VAR
      ParentItem@1003 : Record 27;
      CompSKU@1004 : Record 5700;
      Item2@1006 : Record 27;
      ReqQty@1007 : Decimal;
    BEGIN
      PlanningComponent.LOCKTABLE;

      IF Level > 50 THEN BEGIN
        IF PlanningResiliency THEN BEGIN
          Item.GET(ReqLine."No.");
          TempPlanningErrorLog.SetError(
            STRSUBSTNO(Text014,ReqLine."No.",ReqLine."No."),
            DATABASE::Item,Item.GETPOSITION);
        END;
        ERROR(
          Text000,
          ParentItemNo);
      END;

      IF NextPlanningCompLineNo = 0 THEN BEGIN
        PlanningComponent.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
        PlanningComponent.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
        PlanningComponent.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
        IF PlanningComponent.FIND('+') THEN
          NextPlanningCompLineNo := PlanningComponent."Line No.";
        PlanningComponent.RESET;
      END;

      ParentItem.GET(ParentItemNo);

      AsmBOMComp[Level].SETRANGE("Parent Item No.",ParentItemNo);
      IF AsmBOMComp[Level].FIND('-') THEN
        REPEAT
          ReqQty := Quantity * AsmBOMComp[Level]."Quantity per";
          CASE AsmBOMComp[Level].Type OF
            AsmBOMComp[Level].Type::Item:
              IF ReqQty <> 0 THEN BEGIN
                IF NOT IsPlannedAsmComp(PlanningComponent,ReqLine,AsmBOMComp[Level]) THEN BEGIN
                  NextPlanningCompLineNo := NextPlanningCompLineNo + 10000;

                  PlanningComponent.RESET;
                  PlanningComponent.INIT;
                  PlanningComponent.BlockDynamicTracking(Blocked);
                  PlanningComponent."Worksheet Template Name" := ReqLine."Worksheet Template Name";
                  PlanningComponent."Worksheet Batch Name" := ReqLine."Journal Batch Name";
                  PlanningComponent."Worksheet Line No." := ReqLine."Line No.";
                  PlanningComponent."Line No." := NextPlanningCompLineNo;
                  PlanningComponent.VALIDATE("Item No.",AsmBOMComp[Level]."No.");
                  PlanningComponent."Variant Code" := AsmBOMComp[Level]."Variant Code";
                  IF IsInventoryItem(AsmBOMComp[Level]."No.") THEN
                    PlanningComponent."Location Code" := SKU."Components at Location";
                  PlanningComponent.Description := COPYSTR(AsmBOMComp[Level].Description,1,MAXSTRLEN(PlanningComponent.Description));
                  PlanningComponent."Planning Line Origin" := ReqLine."Planning Line Origin";
                  PlanningComponent.VALIDATE("Unit of Measure Code",AsmBOMComp[Level]."Unit of Measure Code");
                  PlanningComponent."Quantity per" := Quantity * AsmBOMComp[Level]."Quantity per";
                  PlanningComponent.GetDefaultBin;
                  PlanningComponent.Quantity := AsmBOMComp[Level]."Quantity per";
                  PlanningComponent.Position := AsmBOMComp[Level].Position;
                  PlanningComponent."Position 2" := AsmBOMComp[Level]."Position 2";
                  PlanningComponent."Position 3" := AsmBOMComp[Level]."Position 3";
                  PlanningComponent."Lead-Time Offset" := AsmBOMComp[Level]."Lead-Time Offset";
                  PlanningComponent.VALIDATE("Routing Link Code");
                  PlanningComponent.VALIDATE("Scrap %",0);
                  PlanningComponent.VALIDATE("Calculation Formula",PlanningComponent."Calculation Formula"::" ");

                  GetPlanningParameters.AtSKU(
                    CompSKU,
                    PlanningComponent."Item No.",
                    PlanningComponent."Variant Code",
                    PlanningComponent."Location Code");
                  IF Item2.GET(PlanningComponent."Item No.") THEN
                    PlanningComponent.Critical := Item2.Critical;

                  PlanningComponent."Flushing Method" := CompSKU."Flushing Method";
                  PlanningComponent."Ref. Order Type" := ReqLine."Ref. Order Type";
                  PlanningComponent."Ref. Order Status" := ReqLine."Ref. Order Status";
                  PlanningComponent."Ref. Order No." := ReqLine."Ref. Order No.";
                  OnBeforeInsertAsmPlanningComponent(ReqLine,AsmBOMComp[Level],PlanningComponent);
                  PlanningComponent.INSERT;
                END ELSE BEGIN
                  PlanningComponent.RESET;
                  PlanningComponent.BlockDynamicTracking(Blocked);
                  PlanningComponent.VALIDATE(
                    "Quantity per",
                    PlanningComponent."Quantity per" +
                    Quantity *
                    AsmBOMComp[Level]."Quantity per");
                  PlanningComponent.VALIDATE("Routing Link Code",'');
                  PlanningComponent.MODIFY;
                END;

                // A temporary list of Planning Components handled is sustained:
                TempPlanningComponent := PlanningComponent;
                IF NOT TempPlanningComponent.INSERT THEN
                  TempPlanningComponent.MODIFY;
              END;
          END;
        UNTIL AsmBOMComp[Level].NEXT = 0;
    END;

    LOCAL PROCEDURE CalculateComponents@6();
    VAR
      PlanningAssignment@1000 : Record 99000850;
    BEGIN
      PlanningComponent.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      PlanningComponent.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
      PlanningComponent.SETRANGE("Worksheet Line No.",ReqLine."Line No.");

      IF PlanningComponent.FIND('-') THEN
        REPEAT
          PlanningComponent.BlockDynamicTracking(Blocked);
          PlanningComponent.SetRequisitionLine(ReqLine);
          PlanningComponent.VALIDATE("Routing Link Code");
          PlanningComponent.MODIFY;
          WITH PlanningComponent DO
            PlanningAssignment.ChkAssignOne("Item No.","Variant Code","Location Code","Due Date");
        UNTIL PlanningComponent.NEXT = 0;
    END;

    [External]
    PROCEDURE CalculateRoutingFromActual@11(PlanningRtngLine@1000 : Record 99000830;Direction@1001 : 'Forward,Backward';CalcStartEndDate@1002 : Boolean);
    BEGIN
      IF (ReqLine."Worksheet Template Name" <> PlanningRtngLine."Worksheet Template Name") OR
         (ReqLine."Journal Batch Name" <> PlanningRtngLine."Worksheet Batch Name") OR
         (ReqLine."Line No." <> PlanningRtngLine."Worksheet Line No.")
      THEN
        ReqLine.GET(
          PlanningRtngLine."Worksheet Template Name",
          PlanningRtngLine."Worksheet Batch Name",PlanningRtngLine."Worksheet Line No.");

      IF  PlanningRoutingMgt.NeedsCalculation(
           PlanningRtngLine."Worksheet Template Name",
           PlanningRtngLine."Worksheet Batch Name",
           PlanningRtngLine."Worksheet Line No.")
      THEN BEGIN
        PlanningRoutingMgt.Calculate(ReqLine);
        PlanningRtngLine.GET(
          PlanningRtngLine."Worksheet Template Name",
          PlanningRtngLine."Worksheet Batch Name",
          PlanningRtngLine."Worksheet Line No.",PlanningRtngLine."Operation No.");
      END;
      IF Direction = Direction::Forward THEN
        PlanningRtngLine.SETCURRENTKEY(
          "Worksheet Template Name",
          "Worksheet Batch Name",
          "Worksheet Line No.",
          "Sequence No.(Forward)")
      ELSE
        PlanningRtngLine.SETCURRENTKEY(
          "Worksheet Template Name",
          "Worksheet Batch Name",
          "Worksheet Line No.",
          "Sequence No.(Backward)");

      PlanningRtngLine.SETRANGE("Worksheet Template Name",PlanningRtngLine."Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name",PlanningRtngLine."Worksheet Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.",PlanningRtngLine."Worksheet Line No.");

      REPEAT
        IF CalcStartEndDate THEN
          IF ((Direction = Direction::Forward) AND (PlanningRtngLine."Previous Operation No." <> '')) OR
             ((Direction = Direction::Backward) AND (PlanningRtngLine."Next Operation No." <> ''))
          THEN BEGIN
            PlanningRtngLine."Starting Time" := 0T;
            PlanningRtngLine."Starting Date" := 0D;
            PlanningRtngLine."Ending Time" := 235959T;
            PlanningRtngLine."Ending Date" := CalendarMgt.GetMaxDate;
          END;
        CLEAR(CalcPlanningRtngLine);
        IF PlanningResiliency THEN
          CalcPlanningRtngLine.SetResiliencyOn(
            ReqLine."Worksheet Template Name",ReqLine."Journal Batch Name",ReqLine."No.");
        CalcPlanningRtngLine.CalculateRouteLine(PlanningRtngLine,Direction,CalcStartEndDate,ReqLine);
        CalcStartEndDate := TRUE;
      UNTIL PlanningRtngLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CalculateRouting@7(Direction@1000 : 'Forward,Backward');
    VAR
      PlanningRtngLine@1001 : Record 99000830;
      ValidateItems@1100528400 : Codeunit 11012033;
      LeadTime@1100528401 : Code[20];
    BEGIN
      IF PlanningRoutingMgt.NeedsCalculation(
           ReqLine."Worksheet Template Name",
           ReqLine."Journal Batch Name",
           ReqLine."Line No.")
      THEN
        PlanningRoutingMgt.Calculate(ReqLine);

      IF Direction = Direction::Forward THEN
        PlanningRtngLine.SETCURRENTKEY(
          "Worksheet Template Name",
          "Worksheet Batch Name",
          "Worksheet Line No.",
          "Sequence No.(Forward)")
      ELSE
        PlanningRtngLine.SETCURRENTKEY(
          "Worksheet Template Name",
          "Worksheet Batch Name",
          "Worksheet Line No.",
          "Sequence No.(Backward)");

      PlanningRtngLine.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
      IF NOT PlanningRtngLine.FINDFIRST THEN BEGIN
        //**4PS.sn
        IF (ReqLine."Trade Item" <> '') AND
           (ReqLine."Ref. Order Type" = ReqLine."Ref. Order Type"::Purchase)
        THEN
          LeadTime := ValidateItems.GetDeliveryTimeItem(
            '', ReqLine.Manufacturer, ReqLine."Basic Item", ReqLine."Vendor (Trade Item)",
            ReqLine."Trade Item", 0, '', ReqLine."Vendor No.");
        //**4PS.en
        IF Direction = Direction::Forward THEN
          //ReqLine.CalcEndingDate('') //**4PS.o
          ReqLine.CalcEndingDate(LeadTime) //**4PS.n
        ELSE
          //ReqLine.CalcStartingDate(''); //**4PS.o
          ReqLine.CalcStartingDate(LeadTime); //**4PS.n
        ReqLine.UpdateDatetime;
        OnCalculateRoutingOnAfterUpdateReqLine(ReqLine);
        EXIT;
      END;

      IF Direction = Direction::Forward THEN BEGIN
        PlanningRtngLine."Starting Date" := ReqLine."Starting Date";
        PlanningRtngLine."Starting Time" := ReqLine."Starting Time";
      END ELSE BEGIN
        PlanningRtngLine."Ending Date" := ReqLine."Ending Date";
        PlanningRtngLine."Ending Time" := ReqLine."Ending Time";
      END;
      CalculateRoutingFromActual(PlanningRtngLine,Direction,FALSE);

      CalculatePlanningLineDates(ReqLine);
    END;

    [External]
    PROCEDURE CalculatePlanningLineDates@12(VAR ReqLine2@1000 : Record 246);
    VAR
      PlanningRtngLine@1001 : Record 99000830;
      IsLineModified@1002 : Boolean;
    BEGIN
      PlanningRtngLine.SETRANGE("Worksheet Template Name",ReqLine2."Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name",ReqLine2."Journal Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.",ReqLine2."Line No.");
      PlanningRtngLine.SETFILTER("Next Operation No.",'%1','');

      IF PlanningRtngLine.FINDFIRST THEN BEGIN
        ReqLine2."Ending Date" := PlanningRtngLine."Ending Date";
        ReqLine2."Ending Time" := PlanningRtngLine."Ending Time";
        IsLineModified := TRUE;
      END;

      PlanningRtngLine.SETRANGE("Next Operation No.");
      PlanningRtngLine.SETFILTER("Previous Operation No.",'%1','');
      IF PlanningRtngLine.FINDFIRST THEN BEGIN
        ReqLine2."Starting Date" := PlanningRtngLine."Starting Date";
        ReqLine2."Starting Time" := PlanningRtngLine."Starting Time";
        ReqLine2."Order Date" := PlanningRtngLine."Starting Date";
        IsLineModified := TRUE;
      END;

      IF IsLineModified THEN BEGIN
        ReqLine2.UpdateDatetime;
        ReqLine2.MODIFY;
      END;
    END;

    [External]
    PROCEDURE Calculate@8(VAR ReqLine2@1000 : Record 246;Direction@1001 : 'Forward,Backward';CalcRouting@1002 : Boolean;CalcComponents@1003 : Boolean;PlanningLevel@1004 : Integer);
    VAR
      PlanningRtngLine@1005 : Record 99000830;
      ProdOrderCapNeed@1006 : Record 5410;
      IsHandled@1007 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCalculate(ReqLine2,Direction,CalcRouting,CalcComponents,PlanningLevel,IsHandled);
      IF IsHandled THEN
        EXIT;

      ReqLine := ReqLine2;
      IF ReqLine."Action Message" <> ReqLine."Action Message"::Cancel THEN
        ReqLine.TESTFIELD(Quantity);
      IF Direction = Direction::Backward THEN
        ReqLine.TESTFIELD("Ending Date")
      ELSE
        ReqLine.TESTFIELD("Starting Date");

      IF CalcRouting THEN BEGIN
        PlanningRtngLine.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
        PlanningRtngLine.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
        PlanningRtngLine.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
        PlanningRtngLine.DELETEALL;

        ProdOrderCapNeed.SETCURRENTKEY(
          "Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.");
        ProdOrderCapNeed.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
        ProdOrderCapNeed.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
        ProdOrderCapNeed.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
        ProdOrderCapNeed.DELETEALL;
        TransferRouting(ReqLine);
      END;

      IF CalcComponents THEN BEGIN
        PlanningComponent.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
        PlanningComponent.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
        PlanningComponent.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
        IF PlanningComponent.FIND('-') THEN
          REPEAT
            PlanningComponent.BlockDynamicTracking(Blocked);
            PlanningComponent.DELETE(TRUE);
          UNTIL PlanningComponent.NEXT = 0;
        IF ReqLine."Planning Level" = 0 THEN
          ReqLine.DeleteMultiLevel;
        IF (ReqLine."Replenishment System" = ReqLine."Replenishment System"::Assembly) OR
           ((ReqLine."Replenishment System" = ReqLine."Replenishment System"::"Prod. Order") AND (ReqLine."Production BOM No." <> ''))
        THEN BEGIN
          Item.GET(ReqLine."No.");
          GetPlanningParameters.AtSKU(SKU,ReqLine."No.",ReqLine."Variant Code",ReqLine."Location Code");

          IF ReqLine."Replenishment System" = ReqLine."Replenishment System"::Assembly THEN
            TransferAsmBOM(Item."No.",1,ReqLine."Qty. per Unit of Measure")
          ELSE BEGIN
            IsHandled := FALSE;
            OnCalculateOnBeforeTransferBOM(ReqLine,SKU,PlanningResiliency,IsHandled);
            IF NOT IsHandled THEN
              TransferBOM(
                ReqLine."Production BOM No.",1,ReqLine."Qty. per Unit of Measure",
                UOMMgt.GetQtyPerUnitOfMeasure(
                  Item,VersionMgt.GetBOMUnitOfMeasure(ReqLine."Production BOM No.",ReqLine."Production BOM Version Code")));
          END;
        END;
      END;
      Recalculate(ReqLine,Direction);
      ReqLine2 := ReqLine;
      IF CalcComponents AND
         (SKU."Manufacturing Policy" = SKU."Manufacturing Policy"::"Make-to-Order")
      THEN
        CheckMultiLevelStructure(ReqLine,CalcRouting,CalcComponents,PlanningLevel);
    END;

    LOCAL PROCEDURE CreatePlanningComponentFromProdBOM@24(VAR PlanningComponent@1000 : Record 99000829;ReqLine@1006 : Record 246;ProdBOMLine@1001 : Record 99000772;CompSKU@1005 : Record 5700;LineQtyPerUOM@1002 : Decimal;ItemQtyPerUOM@1003 : Decimal);
    VAR
      Item2@1004 : Record 27;
    BEGIN
      WITH PlanningComponent DO BEGIN
        RESET;
        INIT;
        BlockDynamicTracking(Blocked);
        "Worksheet Template Name" := ReqLine."Worksheet Template Name";
        "Worksheet Batch Name" := ReqLine."Journal Batch Name";
        "Worksheet Line No." := ReqLine."Line No.";
        "Line No." := NextPlanningCompLineNo;
        VALIDATE("Item No.",ProdBOMLine."No.");
        "Variant Code" := ProdBOMLine."Variant Code";
        IF IsInventoryItem(ProdBOMLine."No.") THEN
          "Location Code" := SKU."Components at Location";
        Description := ProdBOMLine.Description;
        "Planning Line Origin" := ReqLine."Planning Line Origin";
        VALIDATE("Unit of Measure Code",ProdBOMLine."Unit of Measure Code");
        "Quantity per" := ProdBOMLine."Quantity per" * LineQtyPerUOM / ItemQtyPerUOM;
        VALIDATE("Routing Link Code",ProdBOMLine."Routing Link Code");
        OnTransferBOMOnBeforeGetDefaultBin(PlanningComponent,ProdBOMLine);
        GetDefaultBin;
        Length := ProdBOMLine.Length;
        Width := ProdBOMLine.Width;
        Weight := ProdBOMLine.Weight;
        Depth := ProdBOMLine.Depth;
        Quantity := ProdBOMLine.Quantity;
        Position := ProdBOMLine.Position;
        "Position 2" := ProdBOMLine."Position 2";
        "Position 3" := ProdBOMLine."Position 3";
        "Lead-Time Offset" := ProdBOMLine."Lead-Time Offset";
        VALIDATE("Scrap %",ProdBOMLine."Scrap %");
        VALIDATE("Calculation Formula",ProdBOMLine."Calculation Formula");

        GetPlanningParameters.AtSKU(CompSKU,"Item No.","Variant Code","Location Code");
        IF Item2.GET("Item No.") THEN
          Critical := Item2.Critical;

        "Flushing Method" := CompSKU."Flushing Method";
        IF (SKU."Manufacturing Policy" = SKU."Manufacturing Policy"::"Make-to-Order") AND
           (CompSKU."Manufacturing Policy" = CompSKU."Manufacturing Policy"::"Make-to-Order") AND
           (CompSKU."Replenishment System" = CompSKU."Replenishment System"::"Prod. Order")
        THEN
          "Planning Level Code" := ReqLine."Planning Level" + 1;

        "Ref. Order Type" := ReqLine."Ref. Order Type";
        "Ref. Order Status" := ReqLine."Ref. Order Status";
        "Ref. Order No." := ReqLine."Ref. Order No.";
        OnBeforeInsertPlanningComponent(ReqLine,ProdBOMLine,PlanningComponent,LineQtyPerUOM,ItemQtyPerUOM);
        INSERT;
      END;
    END;

    [External]
    PROCEDURE Recalculate@9(VAR ReqLine2@1000 : Record 246;Direction@1001 : 'Forward,Backward');
    BEGIN
      RecalculateWithOptionalModify(ReqLine2,Direction,TRUE);
    END;

    [External]
    PROCEDURE RecalculateWithOptionalModify@15(VAR ReqLine2@1000 : Record 246;Direction@1001 : 'Forward,Backward';ModifyRec@1002 : Boolean);
    BEGIN
      OnBeforeRecalculateWithOptionalModify(ReqLine2,Direction);

      ReqLine := ReqLine2;

      CalculateRouting(Direction);
      IF ModifyRec THEN
        ReqLine.MODIFY(TRUE);
      CalculateComponents;
      IF ReqLine."Planning Level" > 0 THEN BEGIN
        IF Direction = Direction::Forward THEN
          ReqLine."Due Date" := ReqLine."Ending Date"
      END ELSE
        IF (ReqLine."Due Date" < ReqLine."Ending Date") OR
           (Direction = Direction::Forward)
        THEN
          ReqLine."Due Date" :=
            LeadTimeMgt.PlannedDueDate(
              ReqLine."No.",
              ReqLine."Location Code",
              ReqLine."Variant Code",
              ReqLine."Ending Date",
              ReqLine."Vendor No.",
              ReqLine."Ref. Order Type");
      ReqLine.UpdateDatetime;
      ReqLine2 := ReqLine;
    END;

    LOCAL PROCEDURE CheckRoutingLine@29(RoutingHeader@1000 : Record 99000763;RoutingLine@1001 : Record 99000764);
    VAR
      MachineCenter@1003 : Record 99000758;
    BEGIN
      IF PlanningResiliency AND (RoutingLine."No." = '') THEN BEGIN
        RoutingHeader.GET(RoutingLine."Routing No.");
        TempPlanningErrorLog.SetError(
          STRSUBSTNO(
            Text010,
            RoutingLine.FIELDCAPTION("Operation No."),RoutingLine."Operation No.",
            RoutingHeader.TABLECAPTION,RoutingHeader."No.",
            RoutingLine.FIELDCAPTION("No.")),
          DATABASE::"Routing Header",RoutingHeader.GETPOSITION);
      END;
      RoutingLine.TESTFIELD("No.");

      IF PlanningResiliency AND (RoutingLine."Work Center No." = '') THEN BEGIN
        MachineCenter.GET(RoutingLine."No.");
        TempPlanningErrorLog.SetError(
          STRSUBSTNO(
            Text012,
            MachineCenter.FIELDCAPTION("Work Center No."),
            MachineCenter.TABLECAPTION,
            MachineCenter."No."),
          DATABASE::"Machine Center",MachineCenter.GETPOSITION);
      END;
      RoutingLine.TESTFIELD("Work Center No.");
    END;

    [External]
    PROCEDURE CheckMultiLevelStructure@1(ReqLine2@1000 : Record 246;CalcRouting@1001 : Boolean;CalcComponents@1002 : Boolean;PlanningLevel@1003 : Integer);
    VAR
      ReqLine3@1004 : Record 246;
      Item3@1005 : Record 27;
      PlanningComp@1006 : Record 99000829;
      PlngComponentReserve@1009 : Codeunit 99000840;
      PlanningLineNo@1008 : Integer;
      NoOfComponents@1011 : Integer;
    BEGIN
      IF PlanningLevel < 0 THEN
        EXIT;

      IF NOT Item3.GET(ReqLine2."No.") THEN
        EXIT;
      IF Item3."Manufacturing Policy" <> Item3."Manufacturing Policy"::"Make-to-Order" THEN
        EXIT;

      PlanningLineNo := ReqLine2."Line No.";

      PlanningComp.SETRANGE("Worksheet Line No.",ReqLine2."Line No.");
      PlanningComp.SETFILTER("Item No.",'<>%1','');
      PlanningComp.SETFILTER("Expected Quantity",'<>0');
      PlanningComp.SETFILTER("Planning Level Code",'>0');
      NoOfComponents := PlanningComp.COUNT;
      IF PlanningLevel = 0 THEN BEGIN
        ReqLine3.RESET;
        ReqLine3.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
        ReqLine3.SETRANGE("Journal Batch Name",ReqLine."Journal Batch Name");
        ReqLine3 := ReqLine2;
        IF ReqLine3.FIND('>') THEN
          LineSpacing[1] := (ReqLine3."Line No." - ReqLine."Line No.") DIV (1 + NoOfComponents)
        ELSE
          LineSpacing[1] := 10000;
      END ELSE
        IF (PlanningLevel > 0) AND (PlanningLevel < 50) THEN
          LineSpacing[PlanningLevel + 1] := LineSpacing[PlanningLevel] DIV (1 + NoOfComponents);

      IF PlanningComp.FIND('-') THEN
        REPEAT
          IF LineSpacing[PlanningLevel + 1] = 0 THEN BEGIN
            IF PlanningResiliency THEN
              TempPlanningErrorLog.SetError(Text015,DATABASE::"Requisition Line",ReqLine.GETPOSITION);
            ERROR(Text002);
          END;
          ReqLine3.INIT;
          ReqLine3.BlockDynamicTracking(Blocked);
          ReqLine3."Worksheet Template Name" := ReqLine2."Worksheet Template Name";
          ReqLine3."Journal Batch Name" := ReqLine2."Journal Batch Name";
          PlanningLineNo := PlanningLineNo + LineSpacing[PlanningLevel + 1];
          ReqLine3."Line No." := PlanningLineNo;
          ReqLine3."Ref. Order Type" := ReqLine2."Ref. Order Type";
          ReqLine3."Ref. Order Status" := ReqLine2."Ref. Order Status";
          ReqLine3."Ref. Order No." := ReqLine2."Ref. Order No.";

          ReqLine3."Planning Line Origin" := ReqLine2."Planning Line Origin";
          ReqLine3.Level := ReqLine2.Level;
          ReqLine3."Demand Type" := ReqLine2."Demand Type";
          ReqLine3."Demand Subtype" := ReqLine2."Demand Subtype";
          ReqLine3."Demand Order No." := ReqLine2."Demand Order No.";
          ReqLine3."Demand Line No." := ReqLine2."Demand Line No.";
          ReqLine3."Demand Ref. No." := ReqLine2."Demand Ref. No.";
          ReqLine3."Demand Ref. No." := ReqLine2."Demand Ref. No.";
          ReqLine3."Demand Date" := ReqLine2."Demand Date";
          ReqLine3.Status := ReqLine2.Status;
          ReqLine3."User ID" := ReqLine2."User ID";

          ReqLine3.Type := ReqLine3.Type::Item;
          ReqLine3.VALIDATE("No.",PlanningComp."Item No.");
          ReqLine3."Action Message" := ReqLine2."Action Message";
          ReqLine3."Accept Action Message" := ReqLine2."Accept Action Message";
          ReqLine3.Description := PlanningComp.Description;
          ReqLine3."Variant Code" := PlanningComp."Variant Code";
          ReqLine3."Unit of Measure Code" := PlanningComp."Unit of Measure Code";
          ReqLine3."Location Code" := PlanningComp."Location Code";
          ReqLine3."Bin Code" := PlanningComp."Bin Code";
          ReqLine3."Ending Date" := PlanningComp."Due Date";
          ReqLine3.VALIDATE("Ending Time",PlanningComp."Due Time");
          ReqLine3."Due Date" := PlanningComp."Due Date";
          ReqLine3."Demand Date" := PlanningComp."Due Date";
          ReqLine3.VALIDATE(Quantity,PlanningComp."Expected Quantity");
          ReqLine3.VALIDATE("Needed Quantity",PlanningComp."Expected Quantity");
          ReqLine3.VALIDATE("Demand Quantity",PlanningComp."Expected Quantity");
          ReqLine3."Demand Qty. Available" := 0;

          ReqLine3."Planning Level" := PlanningLevel + 1;
          ReqLine3."Related to Planning Line" := ReqLine2."Line No.";
          ReqLine3."Order Promising ID" := ReqLine2."Order Promising ID";
          ReqLine3."Order Promising Line ID" := ReqLine2."Order Promising Line ID";
          OnCheckMultiLevelStructureOnBeforeInsertPlanningLine(ReqLine3,PlanningComp);
          InsertPlanningLine(ReqLine3);
          ReqLine3.Quantity :=
            ROUND(
              ReqLine3."Quantity (Base)" /
              ReqLine3."Qty. per Unit of Measure",UOMMgt.QtyRndPrecision);
          ReqLine3."Net Quantity (Base)" :=
            (ReqLine3.Quantity -
             ReqLine3."Original Quantity") *
            ReqLine3."Qty. per Unit of Measure";
          ReqLine3.MODIFY;
          PlngComponentReserve.BindToRequisition(
            PlanningComp,ReqLine3,PlanningComp."Expected Quantity",PlanningComp."Expected Quantity (Base)");
          PlanningComp."Supplied-by Line No." := ReqLine3."Line No.";
          PlanningComp.MODIFY;
          ReqLine3.VALIDATE("Production BOM No.");
          ReqLine3.VALIDATE("Routing No.");
          ReqLine3.MODIFY;
          Calculate(ReqLine3,1,CalcRouting,CalcComponents,PlanningLevel + 1);
          ReqLine3.MODIFY;
        UNTIL PlanningComp.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertPlanningLine@2(VAR ReqLine@1000 : Record 246);
    VAR
      ReqLine2@1001 : Record 246;
    BEGIN
      ReqLine2 := ReqLine;
      ReqLine2.SETCURRENTKEY("Worksheet Template Name","Journal Batch Name",Type,"No.");
      ReqLine2.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      ReqLine2.SETRANGE("Journal Batch Name",ReqLine."Journal Batch Name");
      ReqLine2.SETRANGE(Type,ReqLine.Type::Item);
      ReqLine2.SETRANGE("No.",ReqLine."No.");
      ReqLine2.SETRANGE("Variant Code",ReqLine."Variant Code");
      ReqLine2.SETRANGE("Ref. Order Type",ReqLine."Ref. Order Type");
      ReqLine2.SETRANGE("Ref. Order Status",ReqLine."Ref. Order Status");
      ReqLine2.SETRANGE("Ref. Order No.",ReqLine."Ref. Order No.");
      ReqLine2.SETFILTER("Planning Level",'>%1',0);

      IF ReqLine2.FINDFIRST THEN BEGIN
        ReqLine2.BlockDynamicTracking(Blocked);
        ReqLine2.VALIDATE(Quantity,ReqLine2.Quantity + ReqLine.Quantity);

        IF ReqLine2."Due Date" > ReqLine."Due Date" THEN
          ReqLine2."Due Date" := ReqLine."Due Date";

        IF ReqLine2."Ending Date" > ReqLine."Ending Date" THEN BEGIN
          ReqLine2."Ending Date" := ReqLine."Ending Date";
          ReqLine2."Ending Time" := ReqLine."Ending Time";
        END ELSE
          IF (ReqLine2."Ending Date" = ReqLine."Ending Date") AND
             (ReqLine2."Ending Time" > ReqLine."Ending Time")
          THEN
            ReqLine2."Ending Time" := ReqLine."Ending Time";

        IF ReqLine2."Planning Level" < ReqLine."Planning Level" THEN
          ReqLine2."Planning Level" := ReqLine."Planning Level";

        ReqLine2.MODIFY;
        ReqLine := ReqLine2;
      END ELSE
        ReqLine.INSERT;

      OnAfterInsertPlanningLine(ReqLine);
    END;

    [External]
    PROCEDURE BlockDynamicTracking@17(SetBlock@1000 : Boolean);
    BEGIN
      Blocked := SetBlock;
    END;

    [External]
    PROCEDURE GetPlanningCompList@5(VAR PlanningCompList@1000 : TEMPORARY Record 99000829);
    BEGIN
      // The procedure returns a list of the Planning Components handled.
      IF TempPlanningComponent.FIND('-') THEN
        REPEAT
          PlanningCompList := TempPlanningComponent;
          IF NOT PlanningCompList.INSERT THEN
            PlanningCompList.MODIFY;
          TempPlanningComponent.DELETE;
        UNTIL TempPlanningComponent.NEXT = 0;
    END;

    LOCAL PROCEDURE IsPlannedComp@10(VAR PlanningComp@1000 : Record 99000829;ReqLine@1001 : Record 246;ProdBOMLine@1002 : Record 99000772) : Boolean;
    VAR
      PlanningComp2@1003 : Record 99000829;
    BEGIN
      WITH PlanningComp DO BEGIN
        PlanningComp2 := PlanningComp;

        SETCURRENTKEY(
          "Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.","Item No.");
        SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
        SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
        SETRANGE("Worksheet Line No.",ReqLine."Line No.");
        SETRANGE("Item No.",ProdBOMLine."No.");
        IF FIND('-') THEN
          REPEAT
            IF IsPlannedCompFound(PlanningComp,ProdBOMLine) THEN
              EXIT(TRUE);
          UNTIL NEXT = 0;

        PlanningComp := PlanningComp2;
        EXIT(FALSE);
      END;
    END;

    LOCAL PROCEDURE IsPlannedCompFound@27(PlanningComp@1001 : Record 99000829;ProdBOMLine@1002 : Record 99000772) : Boolean;
    VAR
      IsFound@1000 : Boolean;
    BEGIN
      WITH PlanningComp DO BEGIN
        IsFound :=
          ("Variant Code" = ProdBOMLine."Variant Code") AND
          ("Routing Link Code" = ProdBOMLine."Routing Link Code") AND
          (Position = ProdBOMLine.Position) AND
          ("Position 2" = ProdBOMLine."Position 2") AND
          ("Position 3" = ProdBOMLine."Position 3") AND
          (Length = ProdBOMLine.Length) AND
          (Width = ProdBOMLine.Width) AND
          (Weight = ProdBOMLine.Weight) AND
          (Depth = ProdBOMLine.Depth) AND
          ("Unit of Measure Code" = ProdBOMLine."Unit of Measure Code");
        OnAfterIsPlannedCompFound(PlanningComp,ProdBOMLine,IsFound);
        EXIT(IsFound);
      END;
    END;

    LOCAL PROCEDURE IsPlannedAsmComp@14(VAR PlanningComp@1000 : Record 99000829;ReqLine@1001 : Record 246;AsmBOMComp@1002 : Record 90) : Boolean;
    VAR
      PlanningComp2@1003 : Record 99000829;
    BEGIN
      WITH PlanningComp DO BEGIN
        PlanningComp2 := PlanningComp;

        SETCURRENTKEY(
          "Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.","Item No.");
        SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
        SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
        SETRANGE("Worksheet Line No.",ReqLine."Line No.");
        SETRANGE("Item No.",AsmBOMComp."No.");
        IF FIND('-') THEN
          REPEAT
            IF IsPlannedAsmCompFound(PlanningComp,AsmBOMComp) THEN
              EXIT(TRUE);
          UNTIL NEXT = 0;

        PlanningComp := PlanningComp2;
        EXIT(FALSE);
      END;
    END;

    LOCAL PROCEDURE IsPlannedAsmCompFound@31(PlanningComp@1001 : Record 99000829;AsmBOMComp@1002 : Record 90) : Boolean;
    VAR
      IsFound@1000 : Boolean;
    BEGIN
      WITH PlanningComp DO BEGIN
        IsFound :=
          ("Variant Code" = AsmBOMComp."Variant Code") AND
          (Position = AsmBOMComp.Position) AND
          ("Position 2" = AsmBOMComp."Position 2") AND
          ("Position 3" = AsmBOMComp."Position 3") AND
          ("Unit of Measure Code" = AsmBOMComp."Unit of Measure Code");
        OnAfterIsPlannedAsmCompFound(PlanningComp,AsmBOMComp,IsFound);
        EXIT(IsFound);
      END;
    END;

    [External]
    PROCEDURE SetResiliencyOn@48(WkshTemplName@1001 : Code[10];JnlBatchName@1000 : Code[10];ItemNo@1002 : Code[20]);
    BEGIN
      PlanningResiliency := TRUE;
      TempPlanningErrorLog.SetJnlBatch(WkshTemplName,JnlBatchName,ItemNo);
    END;

    [External]
    PROCEDURE GetResiliencyError@47(VAR PlanningErrorLog@1000 : Record 5430) : Boolean;
    BEGIN
      TempPlanningComponent.DELETEALL;
      IF CalcPlanningRtngLine.GetResiliencyError(PlanningErrorLog) THEN
        EXIT(TRUE);
      EXIT(TempPlanningErrorLog.GetError(PlanningErrorLog));
    END;

    LOCAL PROCEDURE IsInventoryItem@21(ItemNo@1000 : Code[20]) : Boolean;
    VAR
      Item@1001 : Record 27;
    BEGIN
      Item.GET(ItemNo);
      EXIT(Item.IsInventoriableType);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertPlanningLine@19(VAR RequisitionLine@1000 : Record 246);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterIsPlannedCompFound@28(PlanningComp@1000 : Record 99000829;ProdBOMLine@1001 : Record 99000772;VAR IsFound@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterIsPlannedAsmCompFound@32(PlanningComp@1002 : Record 99000829;AsmBOMComp@1001 : Record 90;VAR IsFound@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransferRouting@16(VAR RequisitionLine@1000 : Record 246);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransferRtngLine@49(VAR ReqLine@1000 : Record 246;VAR RoutingLine@1001 : Record 99000764;VAR PlanningRoutingLine@1002 : Record 99000830);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnTransferBOMOnAfterProdBOMLineSetFilters@18(VAR ProdBOMLine@1000 : Record 99000772;RequisitionLine@1001 : Record 246);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnTransferBOMOnBeforeGetDefaultBin@53(VAR PlanningComponent@1000 : Record 99000829;VAR ProductionBOMLine@1001 : Record 99000772);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCalculate@20(VAR ReqLine2@1004 : Record 246;Direction@1003 : 'Forward,Backward';CalcRouting@1002 : Boolean;CalcComponents@1001 : Boolean;PlanningLevel@1000 : Integer;VAR IsHandled@1005 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertPlanningComponent@50(VAR ReqLine@1000 : Record 246;VAR ProductionBOMLine@1001 : Record 99000772;VAR PlanningComponent@1002 : Record 99000829;LineQtyPerUOM@1003 : Decimal;ItemQtyPerUOM@1004 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeModifyPlanningComponent@51(VAR ReqLine@1000 : Record 246;VAR ProductionBOMLine@1001 : Record 99000772;VAR PlanningComponent@1002 : Record 99000829;LineQtyPerUOM@1003 : Decimal;ItemQtyPerUOM@1004 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertAsmPlanningComponent@52(VAR ReqLine@1000 : Record 246;VAR BOMComponent@1001 : Record 90;VAR PlanningComponent@1002 : Record 99000829);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeRecalculateWithOptionalModify@35(VAR RequisitionLine@1000 : Record 246;Direction@1001 : 'Forward,Backward');
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeTransferRouting@23(VAR RequisitionLine@1000 : Record 246;PlanningResilency@1001 : Boolean;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCalculateOnBeforeTransferBOM@34(VAR RequisitionLine@1000 : Record 246;VAR StockkeepingUnit@1001 : Record 5700;PlanningResilency@1002 : Boolean;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCalculateRoutingOnAfterUpdateReqLine@30(VAR RequisitionLine@1000 : Record 246);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCheckMultiLevelStructureOnBeforeInsertPlanningLine@54(VAR ReqLine@1000 : Record 246;VAR PlanningComponent@1001 : Record 99000829);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnTransferBOMOnBeforeTransferPlanningComponent@22(VAR RequisitionLine@1000 : Record 246;VAR ProductionBOMLine@1001 : Record 99000772;Blocked@1002 : Boolean;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnTransferBOMOnBeforeTransferProductionBOM@33(VAR ReqQty@1000 : Decimal;ProductionBOMLine@1001 : Record 99000772;LineQtyPerUOM@1002 : Decimal;ItemQtyPerUOM@1003 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnTransferBOMOnBeforeUpdatePlanningComp@25(VAR ProductionBOMLine@1000 : Record 99000772;VAR UpdateCondition@1001 : Boolean;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

