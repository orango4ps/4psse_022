OBJECT Table 11012573 Plant Hours Revenues
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE(Week,Week);
               TESTFIELD("Plant Type");
               TESTFIELD("Plant No.");
             END;

    OnDelete=BEGIN
               PlantHourLineRec.SETRANGE(Year,Year);
               PlantHourLineRec.SETRANGE(Week,Week);
               PlantHourLineRec.SETRANGE("Plant Type","Plant Type");
               PlantHourLineRec.SETRANGE("Plant No.","Plant No.");
               PlantHourLineRec.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               VALIDATE(Week,Week);
               TESTFIELD("Plant Type");
               TESTFIELD("Plant No.");

               PlantHourLineRec.SETRANGE(Year,xRec.Year);
               PlantHourLineRec.SETRANGE(Week,xRec.Week);
               PlantHourLineRec.SETRANGE("Plant Type",xRec."Plant Type");
               PlantHourLineRec.SETRANGE("Plant No.",xRec."Plant No.");
               IF PlantHourLineRec.FIND('-') THEN
                 REPEAT
                   PlantHourLineRec.TESTFIELD(Status,PlantHourLineRec.Status::Open);
                   IF PlantHourLineRec.Origin = PlantHourLineRec.Origin::HourAcc THEN
                     PlantHourLineRec.FIELDERROR(Origin);
                 UNTIL PlantHourLineRec.NEXT = 0;

               IF PlantHourLineRec.FIND('-') THEN
                 REPEAT
                   PlantHourLineRec2.TRANSFERFIELDS(PlantHourLineRec);
                   PlantHourLineRec2.Year := Year;
                   PlantHourLineRec2.Week := Week;
                   PlantHourLineRec2."Plant Type" := "Plant Type";
                   PlantHourLineRec2."Plant No." := "Plant No.";
                   PlantHourLineRec2.INSERT;
                   PlantHourLineRec.DELETE;
                 UNTIL PlantHourLineRec.NEXT = 0;
             END;

    CaptionML=[ENU=Plant Hours Revenues;
               NOR=Maskintimer Inntekter;
               SVE=Maskintimmar intÑkter];
    LookupPageID=Page11012595;
  }
  FIELDS
  {
    { 20  ;   ;Year                ;Integer       ;OnValidate=BEGIN
                                                                Date2.RESET;
                                                                Date2.SETRANGE("Period Type",Date2."Period Type"::Year);
                                                                Date2.SETRANGE("Period No.",Year);
                                                                IF NOT Date2.FIND('-') THEN
                                                                  FIELDERROR(Year);

                                                                StartDateYear := Date2."Period Start";
                                                                YearEndDate := Date2."Period End";
                                                              END;

                                                   CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   MinValue=1 }
    { 30  ;   ;Week                ;Integer       ;OnValidate=BEGIN
                                                                IF Year = 0 THEN
                                                                  Year := DATE2DMY(TODAY, 3);
                                                                VALIDATE(Year,Year);
                                                                Date.RESET;
                                                                Date.SETRANGE("Period Type",Date."Period Type"::Week);
                                                                Date.SETRANGE("Period No.",Week);
                                                                Date.SETRANGE("Period Start",CALCDATE('<-3D>',StartDateYear),YearEndDate);
                                                                Date.SETRANGE("Period End",StartDateYear,CALCDATE('<+3D>',YearEndDate));
                                                                IF NOT Date.FIND('-') THEN
                                                                  FIELDERROR(Week);
                                                              END;

                                                   OnLookup=VAR
                                                              DateSelectionManagement@1100409000 : Codeunit 11130005;
                                                            BEGIN
                                                              DateSelectionManagement.GetWeekYear(Week, Year);
                                                              VALIDATE(Week);
                                                            END;

                                                   CaptionML=[ENU=Week;
                                                              NOR=Uke;
                                                              SVE=Vecka];
                                                   MinValue=1;
                                                   MaxValue=53 }
    { 40  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=BEGIN
                                                                IF "Plant No." <> '' THEN
                                                                  VALIDATE("Plant No.");
                                                              END;

                                                   OnLookup=VAR
                                                              PlantSetup@1100529900 : Record 11012550;
                                                              PlantType@1100529000 : Record 11012551;
                                                            BEGIN
                                                              PlantSetup.GET;
                                                              IF NOT PlantSetup."No Filter on Hr. Acc. Type" THEN BEGIN
                                                                PlantType.SETRANGE("Hour Accounting Type", PlantType."Hour Accounting Type"::Unmanned);
                                                                IF PlantType.ISEMPTY THEN  //C-028223 (Soft filter)
                                                                  PlantType.SETRANGE("Hour Accounting Type", PlantType."Hour Accounting Type"::Manned)
                                                                ELSE
                                                                  PlantType.SETFILTER("Hour Accounting Type", '%1|%2', PlantType."Hour Accounting Type"::Manned, PlantType."Hour Accounting Type"::Unmanned);
                                                              END;
                                                              PlantType.Code := "Plant Type";
                                                              IF PAGE.RUNMODAL(0, PlantType) = ACTION::LookupOK THEN
                                                                VALIDATE("Plant Type", PlantType.Code);
                                                            END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 50  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr];
                                                   NotBlank=Yes }
    { 60  ;   ;Plant Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=[ENU=Plant Description;
                                                              NOR=Maskinbeskrivelse;
                                                              SVE=Maskinbeskrivning];
                                                   Editable=No }
    { 70  ;   ;Open                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Plant Hours Revenues Line" WHERE (Year=FIELD(Year),
                                                                                                        Week=FIELD(Week),
                                                                                                        Plant Type=FIELD(Plant Type),
                                                                                                        Plant No.=FIELD(Plant No.),
                                                                                                        Status=CONST(Open)));
                                                   CaptionML=[ENU=Open;
                                                              NOR=èpne;
                                                              SVE=ôppen];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Year,Week,Plant Type,Plant No.          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantHourLineRec@11012000 : Record 11012574;
      PlantHourLineRec2@11012001 : Record 11012574;
      Date@11012004 : Record 2000000007;
      Date2@11012005 : Record 2000000007;
      StartDateYear@11012006 : Date;
      YearEndDate@11012007 : Date;

    BEGIN
    {
      07-10-2002 Cor Hofman: Copied from table "Hour Accounting" for "Plant Hour Accounting"
    }
    END.
  }
}

