OBJECT Codeunit 1415 Automatic Import of Bank Stmt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            BankAccount@1002 : Record 270;
            DataExchDef@1001 : Record 1222;
            DataExch@1003 : Record 1220;
            BankAccReconciliation@1004 : Record 273;
            DummyBankAccReconciliationLine@1005 : Record 274;
            RecRef@1000 : RecordRef;
            LastStatementNo@1006 : Code[20];
          BEGIN
            TESTFIELD("Record ID to Process");
            RecRef.GET("Record ID to Process");
            RecRef.SETTABLE(BankAccount);

            IF NOT BankAccount."Automatic Stmt. Import Enabled" THEN
              EXIT;

            BankAccount.GetDataExchDef(DataExchDef);

            DataExch."Related Record" := BankAccount.RECORDID;
            IF NOT DataExch.ImportFileContent(DataExchDef) THEN
              EXIT;

            BankAccount.LOCKTABLE;
            LastStatementNo := BankAccount."Last Statement No.";
            BankAccReconciliation.CreateNewBankPaymentAppBatch(BankAccount."No.",BankAccReconciliation);

            IF NOT BankAccReconciliation.ImportStatement(BankAccReconciliation,DataExch) THEN BEGIN
              DeleteBankAccReconciliation(BankAccReconciliation,BankAccount,LastStatementNo);
              EXIT;
            END;

            IF NOT DummyBankAccReconciliationLine.LinesExist(BankAccReconciliation) THEN BEGIN
              DeleteBankAccReconciliation(BankAccReconciliation,BankAccount,LastStatementNo);
              EXIT;
            END;

            COMMIT;
            BankAccReconciliation.ProcessStatement(BankAccReconciliation);
          END;

  }
  CODE
  {

    LOCAL PROCEDURE DeleteBankAccReconciliation@2(VAR BankAccReconciliation@1000 : Record 273;VAR BankAccount@1002 : Record 270;LastStatementNo@1001 : Code[20]);
    BEGIN
      BankAccReconciliation.DELETE;
      BankAccount.FIND;
      BankAccount."Last Statement No." := LastStatementNo;
      BankAccount.MODIFY;
      COMMIT;
    END;

    BEGIN
    END.
  }
}

