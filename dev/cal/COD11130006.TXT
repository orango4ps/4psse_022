OBJECT Codeunit 11130006 XMLport Status Dialog Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      StatusDialog@1100525000 : Dialog;
      Text000@1100525001 : TextConst 'DEU=Importiert ...;ENU=Importing ...;NLD=Importeren ...;NOR=Importererÿ...\\;SVE=Importerarÿ...\\';
      Counter@1100525002 : Integer;
      LastUpdateTime@1100525004 : Time;
      Text001@1100525005 : TextConst 'DEU=Exportiert ...;ENU=Exporting ...;NLD=Exporteren ...;NOR=Exporterer ...\\;SVE=Exporterar ...\\';
      Text002@1100525003 : TextConst 'DEU=#1# #2# #3# #4# #5# #6# #7# #8# #9# #10# #11# #12# #13#;ENU=#1# #2# #3# #4# #5# #6# #7# #8# #9# #10# #11# #12# #13#;NLD=#1# #2# #3# #4# #5# #6# #7# #8# #9# #10# #11# #12# #13#;NOR=#1# #2# #3# #4# #5# #6# #7# #8# #9# #10# #11# #12# #13#;SVE=#1# #2# #3# #4# #5# #6# #7# #8# #9# #10# #11# #12# #13#';
      Text003@1100525006 : TextConst 'DEU=#1#;ENU=#1#;NLD=#1#;NOR=#1#;SVE=#1#';

    PROCEDURE ShowImportStatus@1100525001(RecRef@1100525000 : RecordRef);
    BEGIN
      ShowStatus(Text000 + '\' + Text002, RecRef);
    END;

    PROCEDURE ShowExportStatus@1100525002(RecRef@1100525000 : RecordRef);
    BEGIN
      ShowStatus(Text001 + '\' + Text002, RecRef);
    END;

    LOCAL PROCEDURE ShowStatus@1100525000(MessageTxt@1100525001 : Text[1024];RecRef@1100525000 : RecordRef);
    VAR
      KeyRef@1100525003 : KeyRef;
      I@1100525004 : Integer;
    BEGIN
      IF Counter <= 0 THEN BEGIN
        StatusDialog.OPEN(MessageTxt);
        LastUpdateTime := TIME;
      END;

      IF (Counter MOD 100 = 0) OR
         (TIME >= LastUpdateTime + 1000)
      THEN BEGIN
        StatusDialog.UPDATE(1, RecRef.CAPTION);
        KeyRef := RecRef.KEYINDEX(RecRef.CURRENTKEYINDEX);
        FOR I := 1 TO 12 DO
          IF I <= KeyRef.FIELDCOUNT THEN
            StatusDialog.UPDATE(I + 1, STRSUBSTNO('%1', KeyRef.FIELDINDEX(I)))
          ELSE
            StatusDialog.UPDATE(I + 1, '');
        LastUpdateTime := TIME;
      END;

      Counter := Counter + 1;
    END;

    PROCEDURE ShowImportCounter@1100525004();
    BEGIN
      IF Counter <= 0 THEN BEGIN
        StatusDialog.OPEN(Text000 + '\' + Text003, Counter);
        LastUpdateTime := TIME;
      END;

      IF (Counter MOD 100 = 0) OR
         (TIME >= LastUpdateTime + 1000)
      THEN BEGIN
        StatusDialog.UPDATE(1, Counter);
        LastUpdateTime := TIME;
      END;

      Counter := Counter + 1;
    END;

    PROCEDURE CloseStatus@1100525003();
    BEGIN
      IF Counter > 0 THEN
        StatusDialog.CLOSE;
    END;

    BEGIN
    END.
  }
}

