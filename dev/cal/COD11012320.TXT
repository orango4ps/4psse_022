OBJECT Codeunit 11012320 Export Plant Loc. (Plant Mgt.)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            PlantLocation@1100525001 : Record 11012554;
            PlantLocations@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
            XMLDOMManagement4PS@1100525002 : Codeunit 11020220;
          BEGIN
            IF GUIALLOWED THEN
              Window.OPEN(Text000);

            UtilityFunctions.CreateOutFolder;

            DOMDocument := DOMDocument.XmlDocument;

            AppendElement(DOMDocument, 'plantlocations', '', PlantLocations);
            PlantLocations.SetAttribute('version', '1.0');
            PlantLocations.SetAttribute('exportdate', UtilityFunctions.FormatDateTime(CURRENTDATETIME));

            PlantLocation.SETCURRENTKEY("Customer No.");
            PlantLocation.SETFILTER("Customer No.", '<>%1', '');
            PlantLocation.SETRANGE(Depot, FALSE);
            PlantLocation.SETRANGE(Status, PlantLocation.Status::Production);

            IF PlantLocation.FINDSET THEN
              REPEAT
                IF GUIALLOWED THEN
                  Window.UPDATE;
                AppendPlantLocation(PlantLocations, PlantLocation);
              UNTIL PlantLocation.NEXT = 0;

            IF GUIALLOWED THEN
              XMLDOMManagement4PS.SaveXMLToClientFile(DOMDocument, FileName)
            ELSE
              DOMDocument.Save(FileName);

            IF GUIALLOWED THEN
              Window.CLOSE;
          END;

  }
  CODE
  {
    VAR
      UtilityFunctions@1100525003 : Codeunit 11012318;
      DOMDocument@1100525002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      Window@1100525000 : Dialog;
      Text000@1100525001 : TextConst 'DEU=Werkzeuglagerorte exportieren...;ENU=Exporting Plant Locations...;NLD=Materieellocaties exporteren...;SVE=Exporterar Maskinsplatser ...';

    PROCEDURE AppendPlantLocation@1100525000(ParentNode@1100525003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";PlantLocation@1100525002 : Record 11012554);
    VAR
      PlantLocation2@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      Dummy@1100525001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
    BEGIN
      AppendElement(ParentNode, 'plantlocation', '', PlantLocation2);
      AppendElement(PlantLocation2, 'code', PlantLocation.Code, Dummy);
      AppendElement(PlantLocation2, 'custno', PlantLocation."Customer No.", Dummy);
      AppendElement(PlantLocation2, 'description', PlantLocation.Description, Dummy);
      AppendElement(PlantLocation2, 'description2', PlantLocation."Description 2", Dummy);
      AppendElement(PlantLocation2, 'address', PlantLocation.Address, Dummy);
      AppendElement(PlantLocation2, 'address2', PlantLocation."Address 2", Dummy);
      AppendElement(PlantLocation2, 'postcode', PlantLocation."Post Code", Dummy);
      AppendElement(PlantLocation2, 'city', PlantLocation.City, Dummy);
      AppendElement(PlantLocation2, 'country', PlantLocation."Country/Region Code", Dummy);
      AppendElement(PlantLocation2, 'contact', PlantLocation.Contact, Dummy);
      AppendElement(PlantLocation2, 'phone', PlantLocation."Phone No.", Dummy);
      AppendElement(PlantLocation2, 'fax', PlantLocation."Fax No.", Dummy);
      AppendElement(PlantLocation2, 'blocked', UtilityFunctions.FormatBoolean(PlantLocation.Blocked), Dummy);
      AppendElement(PlantLocation2, 'discountpct', UtilityFunctions.FormatDecimal(PlantLocation."Discount %"), Dummy);
      AppendElement(PlantLocation2, 'startdate', UtilityFunctions.FormatDate(PlantLocation."Starting Date"), Dummy);
      AppendElement(PlantLocation2, 'enddate', UtilityFunctions.FormatDate(PlantLocation."Ending Date"), Dummy);
    END;

    PROCEDURE AppendElement@1100525001(ParentNode@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Name@1100525001 : Text[80];Value@1100525002 : Text[1024];VAR NewElement@1100525003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement");
    VAR
      TextNode@1100525004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlText";
    BEGIN
      NewElement := DOMDocument.CreateElement(Name);

      IF Value <> '' THEN BEGIN
        TextNode := DOMDocument.CreateTextNode(Value);
        NewElement.AppendChild(TextNode);
      END;

      ParentNode.AppendChild(NewElement);
    END;

    PROCEDURE FileName@1100525002() : Text[260];
    VAR
      PlantMgtFileExchangeSetup@1100525000 : Record 11125655;
    BEGIN
      PlantMgtFileExchangeSetup.GET;
      PlantMgtFileExchangeSetup.TESTFIELD("Export Path");

      EXIT(
        STRSUBSTNO('%1\plantlocations%2.xml', DELCHR(PlantMgtFileExchangeSetup."Export Path", '>', '\'),
          UtilityFunctions.FileNameDateTime));
    END;

    BEGIN
    END.
  }
}

