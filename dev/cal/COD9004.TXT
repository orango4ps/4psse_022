OBJECT Codeunit 9004 User Grp. Perm. Subscribers
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Table,9000,OnBeforeDeleteEvent)]
    PROCEDURE DeleteUserGroupPermissionSetsOnDeleteUserGroup@3(VAR Rec@1000 : Record 9000;RunTrigger@1001 : Boolean);
    VAR
      UserGroupPermissionSet@1002 : Record 9003;
    BEGIN
      UserGroupPermissionSet.SETRANGE("User Group Code",Rec.Code);
      UserGroupPermissionSet.DELETEALL(TRUE);
      Rec.FIND;
    END;

    [EventSubscriber(Table,9003,OnBeforeInsertEvent)]
    PROCEDURE AddUserGroupAccessControlOnInsertUserGroupPermissionSet@2(VAR Rec@1000 : Record 9003;RunTrigger@1001 : Boolean);
    VAR
      UserGroupAccessControl@1002 : Record 9002;
    BEGIN
      UserGroupAccessControl.AddUserGroupPermissionSet(Rec."User Group Code",Rec."Role ID",Rec."App ID",Rec.Scope);
    END;

    [EventSubscriber(Table,9003,OnBeforeDeleteEvent)]
    PROCEDURE RemoveUserGroupAccessControlOnDeleteUserGroupPermissionSet@8(VAR Rec@1000 : Record 9003;RunTrigger@1001 : Boolean);
    VAR
      UserGroupAccessControl@1002 : Record 9002;
    BEGIN
      UserGroupAccessControl.RemoveUserGroupPermissionSet(Rec."User Group Code",Rec."Role ID",Rec."App ID",Rec.Scope);
    END;

    [EventSubscriber(Table,9003,OnBeforeRenameEvent)]
    PROCEDURE ReAddUserGroupAccessControlOnRenameUserGroupPermissionSet@9(VAR Rec@1000 : Record 9003;VAR xRec@1001 : Record 9003;RunTrigger@1003 : Boolean);
    VAR
      UserGroupAccessControl@1002 : Record 9002;
    BEGIN
      UserGroupAccessControl.RemoveUserGroupPermissionSet(xRec."User Group Code",xRec."Role ID",xRec."App ID",xRec.Scope);
      UserGroupAccessControl.AddUserGroupPermissionSet(Rec."User Group Code",Rec."Role ID",Rec."App ID",Rec.Scope);
    END;

    BEGIN
    END.
  }
}

