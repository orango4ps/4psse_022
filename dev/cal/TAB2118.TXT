OBJECT Table 2118 O365 Email Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               IntegrationManagement@1001 : Codeunit 5150;
             BEGIN
               IF Email = '' THEN
                 ERROR(EmailAddressEmptyErr);

               Code := UPPERCASE(IntegrationManagement.GetIdWithoutBrackets(CREATEGUID));

               CheckAlreadyAdded;
             END;

    OnModify=BEGIN
               IF Email <> '' THEN
                 CheckAlreadyAdded
               ELSE
                 IF DELETE THEN; // If Email validate was called rec may have been deleted already
             END;

    OnRename=BEGIN
               CheckAlreadyAdded;
             END;

    ReplicateData=No;
    CaptionML=[ENU=O365 Email Setup;
               NOR=O365 E-postoppsett;
               SVE=O365 E-postinst„llning];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code80        ;OnValidate=VAR
                                                                GuidVar@1000 : GUID;
                                                              BEGIN
                                                                IF NOT EVALUATE(GuidVar,Code) THEN
                                                                  ERROR(CodeFormatErr);

                                                                IF ISNULLGUID(GuidVar) THEN
                                                                  ERROR(CodeEmptyErr);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 2   ;   ;Email               ;Text80        ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit 9520;
                                                              BEGIN
                                                                IF DELCHR(Email,'=',' ') = '' THEN
                                                                  IF DELETE THEN
                                                                    EXIT;

                                                                MailManagement.CheckValidEmailAddress(Email);
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Email;
                                                              NOR=E-post;
                                                              SVE=E-post] }
    { 3   ;   ;RecipientType       ;Option        ;CaptionML=[ENU=RecipientType;
                                                              NOR=Mottakertype;
                                                              SVE=Mottagartyp];
                                                   OptionCaptionML=[ENU=CC,BCC;
                                                                    NOR=Kopi,Blindkopi;
                                                                    SVE=CC,BCC];
                                                   OptionString=CC,BCC }
  }
  KEYS
  {
    {    ;Code,RecipientType                      ;Clustered=Yes }
    {    ;Email,RecipientType                      }
    {    ;Email                                    }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;RecipientType,Email                      }
  }
  CODE
  {
    VAR
      EmailAddressEmptyErr@1000 : TextConst 'ENU=An email address must be specified.;NOR=Du m† oppgi en e-postadresse.';
      ConfigAlreadyExistsErr@1001 : TextConst 'ENU=The email address has already been added.;NOR=E-postadressen er allerede lagt til.';
      CodeFormatErr@1002 : TextConst 'ENU=The code must be a GUID.;NOR=Koden m† v‘re en GUID.';
      CodeEmptyErr@1003 : TextConst 'ENU=The code must be not null.;NOR=Koden kan ikke v‘re null.';

    [External]
    PROCEDURE GetCCAddressesFromO365EmailSetup@1() Addresses : Text[250];
    BEGIN
      RESET;
      SETCURRENTKEY(Email,RecipientType);
      SETRANGE(RecipientType,RecipientType::CC);

      IF FINDSET THEN BEGIN
        REPEAT
          Addresses += Email + ';';
        UNTIL NEXT = 0;
      END;
      Addresses := DELCHR(Addresses,'>',';');
    END;

    [External]
    PROCEDURE GetBCCAddressesFromO365EmailSetup@3() Addresses : Text[250];
    BEGIN
      RESET;
      SETCURRENTKEY(Email,RecipientType);
      SETRANGE(RecipientType,RecipientType::BCC);
      IF FINDSET THEN BEGIN
        REPEAT
          Addresses += Email + ';';
        UNTIL NEXT = 0;
      END;
      Addresses := DELCHR(Addresses,'>',';');
    END;

    LOCAL PROCEDURE CheckAlreadyAdded@7();
    VAR
      O365EmailSetup@1000 : Record 2118;
    BEGIN
      O365EmailSetup.SETFILTER(Code,'<>%1',Code);
      SETCURRENTKEY(Email,RecipientType);
      O365EmailSetup.SETRANGE(Email,Email);
      O365EmailSetup.SETRANGE(RecipientType,RecipientType);
      IF NOT O365EmailSetup.ISEMPTY THEN
        ERROR(ConfigAlreadyExistsErr);
    END;

    BEGIN
    END.
  }
}

