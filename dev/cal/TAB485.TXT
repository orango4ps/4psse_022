OBJECT Table 485 Business Chart Buffer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ERROR(Text002,TABLECAPTION);
             END;

    ReplicateData=No;
    CaptionML=[ENU=Business Chart Buffer;
               NOR=Buffer for forretningsdiagram;
               SVE=Buffert fîr verksamhetsdiagram];
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=ID;
                                                              NOR=ID;
                                                              SVE=ID] }
    { 2   ;   ;Chart Type          ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Chart Type;
                                                              NOR=Diagramtype;
                                                              SVE=Diagramtyp];
                                                   OptionCaptionML=[ENU=Point,,Bubble,Line,,StepLine,,,,,Column,StackedColumn,StackedColumn100,Area,,StackedArea,StackedArea100,Pie,Doughnut,,,Range,,,,Radar,,,,,,,,Funnel;
                                                                    NOR=Punkt,,Boble,Linje,,StepLine,,,,,Kolonne,StackedColumn,StackedColumn100,Areal,,StackedArea,StackedArea100,Sektor,Hjul,,,OmrÜde,,,,Radar,,,,,,,,Trakt;
                                                                    SVE=Punkt,,Bubbla,Linje,,Steglinje,,,,,Stapel,Staplad stapel,Staplad stapel100,OmrÜde,,Staplad yta,Staplad yta100,Cirkel,Ring,,,Intervall,,,,PolÑr,,,,,,,,Tratt];
                                                   OptionString=Point,,Bubble,Line,,StepLine,,,,,Column,StackedColumn,StackedColumn100,Area,,StackedArea,StackedArea100,Pie,Doughnut,,,Range,,,,Radar,,,,,,,,Funnel }
    { 3   ;   ;Data Type           ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Data Type;
                                                              NOR=Datatype;
                                                              SVE=Datatyp];
                                                   OptionCaptionML=[ENU=String,Integer,Decimal,DateTime;
                                                                    NOR=Streng,Heltall,Desimal,Dato/klokkeslett;
                                                                    SVE=StrÑng,Heltal,Decimal,DatumTid];
                                                   OptionString=String,Integer,Decimal,DateTime }
    { 4   ;   ;XML                 ;BLOB          ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=XML;
                                                              NOR=XML;
                                                              SVE=XML] }
    { 5   ;   ;Drill-Down X Index  ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Drill-Down X Index;
                                                              NOR=Vis X-indeks;
                                                              SVE=Specificera X-index] }
    { 6   ;   ;Drill-Down Y Value  ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Drill-Down Y Value;
                                                              NOR=Vis Y-verdi;
                                                              SVE=Specificera Y-vÑrde] }
    { 7   ;   ;Drill-Down Measure Index;Integer   ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Drill-Down Measure Index;
                                                              NOR=Vis mÜlindeks;
                                                              SVE=Specificera mÜttindex] }
    { 8   ;   ;Period Length       ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Period Length;
                                                              NOR=Periodelengde;
                                                              SVE=Period];
                                                   OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period,None;
                                                                    NOR=Dag,Uke,MÜned,Kvartal,èr,Regnskapsperiode,Ingen;
                                                                    SVE=Dag,Vecka,MÜnad,Kvartal,èr,Bokfîringsperiod,Ingen];
                                                   OptionString=Day,Week,Month,Quarter,Year,Accounting Period,None }
    { 9   ;   ;Period Filter Start Date;Date      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Period Filter Start Date;
                                                              NOR=Startdato for periodefilter;
                                                              SVE=Startdatum fîr periodfilter] }
    { 10  ;   ;Period Filter End Date;Date        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Period Filter End Date;
                                                              NOR=Sluttdato for periodefilter;
                                                              SVE=Slutdatum fîr periodfilter] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TempBusChartMapToColumn@1001 : TEMPORARY Record 486;
      TempBusChartMapToMeasure@1000 : TEMPORARY Record 486;
      dotNetChartData@1012 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartData";
      dotNetDataTable@1013 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataTable";
      IsInitialized@1014 : Boolean;
      Text001@1002 : TextConst 'ENU=Data Type must be Integer or Decimal for Measure %1.;NOR=Datatypen mÜ vëre heltall eller desimal for mÜlet %1.;SVE=Datatypen mÜste vara Integer eller Decimal fîr mÜttet %1.';
      Text002@1007 : TextConst 'ENU=You cannot insert into table %1.;NOR=Du kan ikke sette inn i tabellen %1.;SVE=Det gÜr inte att infoga i tabellen %1.';
      Text003@1005 : TextConst 'ENU=You cannot add more than %1 measures.;NOR=Du kan ikke legge til mer enn %1 mÜl.;SVE=Det gÜr inte att lÑgga till mer Ñn %1 mÑtvÑrden.';

    [External]
    PROCEDURE Initialize@2();
    VAR
      dotNetCultureInfo@1000 : DotNet "'mscorlib'.System.Globalization.CultureInfo";
    BEGIN
      IF NOT IsInitialized THEN BEGIN
        dotNetDataTable := dotNetDataTable.DataTable('DataTable');
        dotNetCultureInfo := dotNetCultureInfo.CultureInfo(WINDOWSLANGUAGE);
        dotNetDataTable.Locale := dotNetCultureInfo.InvariantCulture;

        dotNetChartData := dotNetChartData.BusinessChartData;
        IsInitialized := TRUE;
      END;
      dotNetDataTable.Clear;
      dotNetDataTable.Columns.Clear;
      dotNetChartData.ClearMeasures;
      ClearMap(TempBusChartMapToColumn);
      ClearMap(TempBusChartMapToMeasure);
    END;

    LOCAL PROCEDURE ClearMap@16(VAR BusChartMap@1000 : Record 486);
    BEGIN
      BusChartMap.RESET;
      BusChartMap.DELETEALL;
    END;

    [External]
    PROCEDURE SetChartCondensed@30(Condensed@1000 : Boolean);
    BEGIN
      dotNetChartData.ShowChartCondensed := Condensed;
    END;

    [External]
    PROCEDURE SetXAxis@3(Caption@1000 : Text;Type@1001 : Option);
    BEGIN
      AddDataColumn(Caption,Type);
      dotNetChartData.XDimension := Caption;
    END;

    [External]
    PROCEDURE SetPeriodXAxis@38();
    VAR
      DataType@1000 : Option;
    BEGIN
      IF "Period Length" = "Period Length"::Day THEN
        DataType := "Data Type"::DateTime
      ELSE
        DataType := "Data Type"::String;
      SetXAxis(FORMAT("Period Length"),DataType);
    END;

    [External]
    PROCEDURE GetXCaption@35() : Text;
    BEGIN
      EXIT(dotNetChartData.XDimension);
    END;

    [External]
    PROCEDURE AddMeasure@4(Caption@1000 : Text;Value@1001 : Variant;ValueType@1004 : Option;ChartType@1002 : Integer);
    VAR
      DotNetChartType@1003 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
    BEGIN
      "Data Type" := ValueType;
      IF NOT ("Data Type" IN ["Data Type"::Integer,"Data Type"::Decimal]) THEN
        ERROR(Text001,Caption);
      AddDataColumn(Caption,ValueType);
      DotNetChartType := ChartType;
      dotNetChartData.AddMeasure(Caption,DotNetChartType);
      TempBusChartMapToMeasure.Add(
        COPYSTR(Caption,1,MAXSTRLEN(TempBusChartMapToMeasure.Name)),Value);
    END;

    [External]
    PROCEDURE GetMaxNumberOfMeasures@40() : Integer;
    VAR
      MaximumNumberOfColoursInChart@1000 : Integer;
    BEGIN
      MaximumNumberOfColoursInChart := 6;
      EXIT(MaximumNumberOfColoursInChart);
    END;

    [External]
    PROCEDURE RaiseErrorMaxNumberOfMeasuresExceeded@42();
    BEGIN
      ERROR(Text003,GetMaxNumberOfMeasures);
    END;

    [External]
    PROCEDURE AddColumn@9(Value@1002 : Variant);
    VAR
      Caption@1000 : Text;
    BEGIN
      ConvertDateToDateTime(Value);
      Caption := FORMAT(Value,0,9);
      AddColumnWithCaption(Value,Caption);
    END;

    [External]
    PROCEDURE AddPeriods@32(FromDate@1000 : Date;ToDate@1001 : Date);
    VAR
      ColumnNo@1002 : Integer;
      NumberOfPeriods@1003 : Integer;
    BEGIN
      NumberOfPeriods := CalcNumberOfPeriods(FromDate,ToDate);
      FOR ColumnNo := 1 TO NumberOfPeriods DO BEGIN
        ToDate := CalcToDate(FromDate);
        AddPeriodColumn(ToDate);
        FromDate := CALCDATE('<1D>',ToDate);
      END;
    END;

    [External]
    PROCEDURE AddPeriodColumn@34(Value@1000 : Variant);
    VAR
      Caption@1001 : Text;
    BEGIN
      ConvertDateToDateTime(Value);
      Caption := GetPeriodCaption(Value);
      IF "Period Length" = "Period Length"::Day THEN
        AddColumnWithCaption(Value,FORMAT(Value,0,9))
      ELSE
        AddColumnWithCaption(Value,Caption);
    END;

    LOCAL PROCEDURE AddColumnWithCaption@37(Value@1000 : Variant;Caption@1001 : Text);
    VAR
      dotNetDataRow@1002 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
    BEGIN
      dotNetDataRow := dotNetDataTable.NewRow;
      dotNetDataRow.Item(GetXCaption,Caption);
      dotNetDataTable.Rows.Add(dotNetDataRow);
      TempBusChartMapToColumn.Add(Caption,Value);
    END;

    LOCAL PROCEDURE ConvertDateToDateTime@36(VAR Value@1000 : Variant);
    BEGIN
      IF IsXAxisDateTime AND Value.ISDATE THEN
        Value := CREATEDATETIME(Value,120000T);
    END;

    LOCAL PROCEDURE AddDataColumn@5(Caption@1003 : Text;ValueType@1002 : Option);
    VAR
      dotNetDataColumn@1001 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataColumn";
      dotNetSystemType@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Type";
    BEGIN
      dotNetDataColumn := dotNetDataColumn.DataColumn(Caption);
      dotNetDataColumn.Caption := Caption;
      dotNetDataColumn.ColumnName(Caption);
      dotNetDataColumn.DataType(dotNetSystemType.GetType(GetSystemTypeName(ValueType)));
      dotNetDataTable.Columns.Add(dotNetDataColumn);
    END;

    LOCAL PROCEDURE GetSystemTypeName@6(Type@1000 : Option) : Text;
    BEGIN
      CASE Type OF
        "Data Type"::String:
          EXIT('System.String');
        "Data Type"::Integer:
          EXIT('System.Int32');
        "Data Type"::Decimal:
          EXIT('System.Decimal');
        "Data Type"::DateTime:
          EXIT('System.DateTime');
      END;
    END;

    [External]
    PROCEDURE SetValue@10(MeasureName@1001 : Text;XAxisIndex@1000 : Integer;Value@1002 : Variant);
    VAR
      dotNetDataRow@1003 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
    BEGIN
      dotNetDataRow := dotNetDataTable.Rows.Item(XAxisIndex);
      dotNetDataRow.Item(MeasureName,Value);
    END;

    [External]
    PROCEDURE SetValueByIndex@11(MeasureIndex@1001 : Integer;XAxisIndex@1000 : Integer;Value@1002 : Variant);
    VAR
      dotNetDataRow@1003 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
    BEGIN
      dotNetDataRow := dotNetDataTable.Rows.Item(XAxisIndex);
      dotNetDataRow.Item(TempBusChartMapToMeasure.GetName(MeasureIndex),Value);
    END;

    [External]
    PROCEDURE FindFirstMeasure@23(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    BEGIN
      TempBusChartMapToMeasure.RESET;
      Result := TempBusChartMapToMeasure.FINDSET;
      BusChartMap := TempBusChartMapToMeasure;
    END;

    [External]
    PROCEDURE NextMeasure@24(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    BEGIN
      Result := TempBusChartMapToMeasure.NEXT <> 0;
      BusChartMap := TempBusChartMapToMeasure;
    END;

    [External]
    PROCEDURE FindFirstColumn@21(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    BEGIN
      TempBusChartMapToColumn.RESET;
      Result := TempBusChartMapToColumn.FINDSET;
      BusChartMap := TempBusChartMapToColumn;
    END;

    [External]
    PROCEDURE FindMidColumn@29(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    VAR
      MidColumnIndex@1001 : Integer;
    BEGIN
      TempBusChartMapToColumn.RESET;
      TempBusChartMapToColumn.FINDLAST;
      MidColumnIndex := -ROUND(TempBusChartMapToColumn.COUNT DIV 2);
      Result := MidColumnIndex = TempBusChartMapToColumn.NEXT(MidColumnIndex);
      BusChartMap := TempBusChartMapToColumn;
    END;

    [External]
    PROCEDURE NextColumn@22(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    BEGIN
      Result := TempBusChartMapToColumn.NEXT <> 0;
      BusChartMap := TempBusChartMapToColumn;
    END;

    [External]
    PROCEDURE GetValue@14(MeasureName@1001 : Text;XAxisIndex@1000 : Integer;VAR Value@1002 : Variant);
    VAR
      dotNetDataRow@1003 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
    BEGIN
      dotNetDataRow := dotNetDataTable.Rows.Item(XAxisIndex);
      Value := dotNetDataRow.Item(MeasureName);
    END;

    [External]
    PROCEDURE GetXValue@7(XAxisIndex@1001 : Integer;VAR Value@1002 : Variant);
    BEGIN
      GetValue(GetXCaption,XAxisIndex,Value);
    END;

    [External]
    PROCEDURE GetXValueAsDate@19(XIndex@1000 : Integer) : Date;
    VAR
      Value@1001 : Variant;
    BEGIN
      IF IsXAxisDateTime THEN BEGIN
        GetXValue(XIndex,Value);
        EXIT(VARIANT2DATE(Value));
      END;
      TempBusChartMapToColumn.GET(XIndex);
      EXIT(TempBusChartMapToColumn.GetValueAsDate);
    END;

    [External]
    PROCEDURE GetMeasureValueString@8(MeasureIndex@1000 : Integer) : Text;
    BEGIN
      EXIT(TempBusChartMapToMeasure.GetValueString(MeasureIndex));
    END;

    [External]
    PROCEDURE GetMeasureName@41(MeasureIndex@1000 : Integer) : Text;
    BEGIN
      EXIT(TempBusChartMapToMeasure.GetName(MeasureIndex));
    END;

    [External]
    PROCEDURE GetCurrMeasureValueString@12() : Text;
    BEGIN
      EXIT(TempBusChartMapToMeasure.GetValueString("Drill-Down Measure Index"));
    END;

    [External]
    PROCEDURE Update@15(dotNetChartAddIn@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartAddIn");
    BEGIN
      dotNetChartData.DataTable := dotNetDataTable;
      dotNetChartAddIn.Update(dotNetChartData);
    END;

    PROCEDURE SetDrillDownIndexes@1(dotNetPoint@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    BEGIN
      SetDrillDownIndexesByCoordinate(dotNetPoint.Measures.GetValue(0),dotNetPoint.XValueString,dotNetPoint.YValues.GetValue(0));
    END;

    LOCAL PROCEDURE GetDateString@17(XValueString@1000 : Text[249]) : Text[249];
    VAR
      Days@1002 : Decimal;
      DateTime@1001 : DateTime;
    BEGIN
      IF EVALUATE(Days,XValueString,9) THEN BEGIN
        DateTime := CREATEDATETIME(DMY2DATE(30,12,1899) + ROUND(Days,1,'<'),120000T);
        EXIT(FORMAT(DateTime,0,9));
      END;
      EXIT(XValueString);
    END;

    [External]
    PROCEDURE IsXAxisDateTime@26() : Boolean;
    BEGIN
      EXIT(FORMAT(dotNetDataTable.Columns.Item(0).DataType) = 'System.DateTime')
    END;

    [Internal]
    PROCEDURE WriteToXML@48(VAR XMLDoc@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      XMLElement@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      OutStream@1005 : OutStream;
      InStream@1004 : InStream;
      XMLLine@1003 : Text[80];
      XMLText@1001 : Text;
    BEGIN
      XML.CREATEOUTSTREAM(OutStream);
      dotNetDataTable.WriteXml(OutStream);
      XML.CREATEINSTREAM(InStream);
      WHILE NOT InStream.EOS DO BEGIN
        InStream.READTEXT(XMLLine);
        XMLText := XMLText + XMLLine;
      END;
      XMLElement := XMLDoc.CreateElement('DataTable','test','');
      XMLElement.InnerXml(XMLText);
      XMLDoc.AppendChild(XMLElement);
    END;

    [External]
    PROCEDURE CalcFromDate@18(Date@1000 : Date) : Date;
    BEGIN
      EXIT(CalcPeriodDate(Date,TRUE));
    END;

    [External]
    PROCEDURE CalcToDate@13(Date@1000 : Date) : Date;
    BEGIN
      EXIT(CalcPeriodDate(Date,FALSE));
    END;

    LOCAL PROCEDURE CalcPeriodDate@39(Date@1000 : Date;CalcStartDate@1001 : Boolean) : Date;
    VAR
      Modificator@1002 : Text[1];
    BEGIN
      IF Date = 0D THEN
        EXIT(Date);

      CASE "Period Length" OF
        "Period Length"::Day:
          EXIT(Date);
        "Period Length"::Week,
        "Period Length"::Month,
        "Period Length"::Quarter,
        "Period Length"::Year:
          BEGIN
            IF CalcStartDate THEN
              Modificator := '-';
            EXIT(CALCDATE(STRSUBSTNO('<%1C%2>',Modificator,GetPeriodLength),Date));
          END;
      END;
    END;

    [External]
    PROCEDURE CalcNumberOfPeriods@28(FromDate@1000 : Date;ToDate@1001 : Date) : Integer;
    VAR
      NumberOfPeriods@1002 : Integer;
    BEGIN
      IF FromDate = ToDate THEN
        EXIT(1);
      IF ToDate < FromDate THEN
        SwapDates(FromDate,ToDate);

      CASE "Period Length" OF
        "Period Length"::Day:
          NumberOfPeriods := ToDate - FromDate;
        "Period Length"::Week:
          NumberOfPeriods := (CALCDATE('<-CW>',ToDate) - CALCDATE('<-CW>',FromDate)) DIV 7;
        "Period Length"::Month:
          NumberOfPeriods := DATE2DMY(ToDate,2) - DATE2DMY(FromDate,2) + GetNumberOfYears(FromDate,ToDate) * 12;
        "Period Length"::Quarter:
          NumberOfPeriods := GetQuarterIndex(ToDate) - GetQuarterIndex(FromDate) + GetNumberOfYears(FromDate,ToDate) * 4;
        "Period Length"::Year:
          NumberOfPeriods := GetNumberOfYears(FromDate,ToDate);
      END;
      EXIT(NumberOfPeriods + 1);
    END;

    LOCAL PROCEDURE SwapDates@27(VAR FromDate@1000 : Date;VAR ToDate@1001 : Date);
    VAR
      Date@1002 : Date;
    BEGIN
      Date := FromDate;
      FromDate := ToDate;
      ToDate := Date;
    END;

    LOCAL PROCEDURE GetNumberOfYears@25(FromDate@1000 : Date;ToDate@1001 : Date) : Integer;
    BEGIN
      EXIT(DATE2DMY(ToDate,3) - DATE2DMY(FromDate,3));
    END;

    LOCAL PROCEDURE GetQuarterIndex@20(Date@1000 : Date) : Integer;
    BEGIN
      EXIT((DATE2DMY(Date,2) - 1) DIV 3 + 1);
    END;

    [External]
    PROCEDURE GetPeriodLength@31() : Text[1];
    BEGIN
      CASE "Period Length" OF
        "Period Length"::Day:
          EXIT('D');
        "Period Length"::Week:
          EXIT('W');
        "Period Length"::Month:
          EXIT('M');
        "Period Length"::Quarter:
          EXIT('Q');
        "Period Length"::Year:
          EXIT('Y');
      END;
    END;

    [External]
    PROCEDURE GetPeriodCaption@33(Date@1000 : Variant) : Text;
    VAR
      PeriodFormManagement@1001 : Codeunit 359;
    BEGIN
      IF Date.ISDATETIME OR Date.ISDATE THEN
        EXIT(PeriodFormManagement.CreatePeriodFormat("Period Length",Date));
      EXIT(FORMAT(Date,0,9));
    END;

    [External]
    PROCEDURE GetPeriodFromMapColumn@43(ColumnIndex@1002 : Integer;VAR FromDate@1001 : Date;VAR ToDate@1000 : Date);
    BEGIN
      ToDate := GetXValueAsDate(ColumnIndex);
      FromDate := CalcFromDate(ToDate);
    END;

    [External]
    PROCEDURE InitializePeriodFilter@46(StartDate@1000 : Date;EndDate@1001 : Date);
    BEGIN
      "Period Filter Start Date" := StartDate;
      "Period Filter End Date" := EndDate;
    END;

    [External]
    PROCEDURE RecalculatePeriodFilter@44(VAR StartDate@1005 : Date;VAR EndDate@1004 : Date;MovePeriod@1000 : ' ,Next,Previous');
    VAR
      Calendar@1003 : Record 2000000007;
      PeriodFormMgt@1002 : Codeunit 359;
      SearchText@1001 : Text[3];
    BEGIN
      IF StartDate <> 0D THEN BEGIN
        Calendar.SETFILTER("Period Start",'%1..%2',StartDate,EndDate);
        IF NOT PeriodFormMgt.FindDate('+',Calendar,"Period Length") THEN
          PeriodFormMgt.FindDate('+',Calendar,Calendar."Period Type"::Date);
        Calendar.SETRANGE("Period Start");
      END;

      CASE MovePeriod OF
        MovePeriod::Next:
          SearchText := '>=';
        MovePeriod::Previous:
          SearchText := '<=';
        ELSE
          SearchText := '';
      END;

      PeriodFormMgt.FindDate(SearchText,Calendar,"Period Length");

      StartDate := Calendar."Period Start";
      EndDate := Calendar."Period End";
    END;

    [External]
    PROCEDURE SetDrillDownIndexesByCoordinate@45(MeasureName@1000 : Text[249];XValueString@1001 : Text[249];YValue@1002 : Decimal);
    BEGIN
      "Drill-Down Measure Index" := TempBusChartMapToMeasure.GetIndex(MeasureName);

      IF IsXAxisDateTime THEN
        XValueString := GetDateString(XValueString);

      "Drill-Down X Index" := TempBusChartMapToColumn.GetIndex(XValueString);
      "Drill-Down Y Value" := YValue;
    END;

    BEGIN
    END.
  }
}

