OBJECT Table 6086322 Expense Allocation Inbox
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.01,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Entry No.,Continia User ID,Description;
    OnInsert=VAR
               ExpenseInbox@1160040001 : Record 6086323;
               SplitAndAllocationMgt@1160040000 : Codeunit 6086301;
             BEGIN
               ExpenseInbox.GET("Inbox Entry No.");
               SplitAndAllocationMgt.CopyExpInboxDimToAllocationDim(ExpenseInbox,Rec);

               IF NOT ("Amount %" IN [0,100]) THEN
                 Modified := TRUE;
             END;

    OnModify=BEGIN
               Modified := TRUE;
             END;

    OnDelete=VAR
               EMAttendeeInbox@1160040001 : Record 6086325;
               EMDimInbox@1160040000 : Record 6086362;
             BEGIN
               EMDimInbox.SETRANGE("Table ID",DATABASE::"Expense Allocation Inbox");
               EMDimInbox.SETRANGE("Document Type",0);
               EMDimInbox.SETRANGE("Document No.",'');
               EMDimInbox.SETRANGE("Doc. Ref. No.","Entry No.");
               EMDimInbox.DELETEALL;

               EMAttendeeInbox.SETRANGE("Table ID",DATABASE::"Expense Allocation");
               EMAttendeeInbox.SETRANGE("Doc. Ref. No.","Entry No.");
               EMAttendeeInbox.DELETEALL;
             END;

    CaptionML=[DEU=Ausgabenzuweisung Posteingang;
               ENU=Expense Allocation Inbox;
               NLD=Uitgavetoewijzing postvak IN];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[DEU=Lfd. Nr.;
                                                              ENU=Entry No.;
                                                              NLD=Volgnr.;
                                                              SVE=Lîpnr] }
    { 2   ;   ;Table ID            ;Integer       ;CaptionML=[DEU=Tabellen-ID;
                                                              ENU=Table ID;
                                                              NLD=Tabel-id;
                                                              SVE=Tabell-ID] }
    { 3   ;   ;Inbox Entry No.     ;Integer       ;CaptionML=[DEU=Posteingang Seriennr.;
                                                              ENU=Inbox Entry No.;
                                                              NLD=Postvak IN volgnr.] }
    { 10  ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=BEGIN
                                                                ERROR(NotAllowed,FIELDCAPTION("Continia User ID"));
                                                                VALIDATE("Expense Type");
                                                              END;

                                                   CaptionML=[DEU=Continia-Benutzer-ID;
                                                              ENU=Continia User ID;
                                                              NLD=Continia gebruikers-id;
                                                              SVE=AnstÑllningsnr] }
    { 11  ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              SVE=Beskrivning] }
    { 12  ;   ;Description 2       ;Text50        ;CaptionML=[DEU=Beschreibung 2;
                                                              ENU=Description 2;
                                                              NLD=Omschrijving 2;
                                                              SVE=Beskrivning 2] }
    { 13  ;   ;Document Date       ;Date          ;CaptionML=[DEU=Belegdatum;
                                                              ENU=Document Date;
                                                              NLD=Documentdatum;
                                                              SVE=Dokumentdatum] }
    { 14  ;   ;Date Created        ;Date          ;CaptionML=[DEU=Erstellt am;
                                                              ENU=Date Created;
                                                              NLD=Datum aangemaakt;
                                                              SVE=Skapat datum] }
    { 15  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   OnValidate=BEGIN
                                                                VALIDATE("Expense Type");
                                                              END;

                                                   CaptionML=[DEU=LÑnder-/Regionscode;
                                                              ENU=Country/Region Code;
                                                              NLD=Land-/regiocode;
                                                              SVE=Lands-/regionkod];
                                                   NotBlank=Yes }
    { 16  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[DEU=WÑhrungscode;
                                                              ENU=Currency Code;
                                                              NLD=Valutacode;
                                                              SVE=Valutakod] }
    { 17  ;   ;No Refund           ;Boolean       ;CaptionML=[DEU=Keine Erstattung;
                                                              ENU=No Refund;
                                                              NLD=Geen restitutie] }
    { 18  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                UpdateAmount(FIELDNO(Amount));
                                                              END;

                                                   CaptionML=[DEU=Betrag;
                                                              ENU=Amount;
                                                              NLD=Bedrag;
                                                              SVE=Belopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 19  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=VAR
                                                                GLSetup@1160040001 : Record 98;
                                                                SplitAndAllocationMgt@1160040002 : Codeunit 6086301;
                                                              BEGIN
                                                                GLSetup.GET;
                                                                IF GLSetup."Global Dimension 1 Code" <> '' THEN
                                                                  SplitAndAllocationMgt.UpdateExpInboxAllocDim(
                                                                    DATABASE::"Expense Allocation Inbox","Entry No.",GLSetup."Global Dimension 1 Code","Global Dimension 1 Code",'','');
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 1;
                                                              ENU=Global Dimension 1 Code;
                                                              NLD=Globale dimensiecode 1;
                                                              SVE=Global dimension 1 kod];
                                                   CaptionClass='1,1,1' }
    { 20  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                GLSetup@1160040001 : Record 98;
                                                                SplitAndAllocationMgt@1160040002 : Codeunit 6086301;
                                                              BEGIN
                                                                GLSetup.GET;
                                                                IF GLSetup."Global Dimension 2 Code" <> '' THEN
                                                                  SplitAndAllocationMgt.UpdateExpInboxAllocDim(
                                                                    DATABASE::"Expense Allocation Inbox","Entry No.",GLSetup."Global Dimension 2 Code","Global Dimension 2 Code",'','');
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 2;
                                                              ENU=Global Dimension 2 Code;
                                                              NLD=Globale dimensiecode 2;
                                                              SVE=Global dimension 2 kod];
                                                   CaptionClass='1,1,2' }
    { 21  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=[DEU=Projektnr.;
                                                              ENU=Job No.;
                                                              NLD=Projectnr.;
                                                              SVE=Projekt Nr.] }
    { 22  ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   CaptionML=[DEU=Projektaufgabennr.;
                                                              ENU=Job Task No.;
                                                              NLD=Projecttaaknr.;
                                                              SVE=Projektaktivitetsnr] }
    { 23  ;   ;Billable            ;Boolean       ;OnValidate=BEGIN
                                                                IF Billable THEN
                                                                  "Job Line Type" := "Job Line Type"::Contract
                                                                ELSE
                                                                  "Job Line Type" := "Job Line Type"::" ";
                                                              END;

                                                   CaptionML=[DEU=Fakturierbar;
                                                              ENU=Billable;
                                                              NLD=Facturabel;
                                                              SVE=Fakturerbart] }
    { 24  ;   ;Job Line Type       ;Option        ;CaptionML=[DEU=Projektzeilenart;
                                                              ENU=Job Line Type;
                                                              NLD=Regelsoort project;
                                                              SVE=Projektradtyp];
                                                   OptionCaptionML=[DEU=" ,Budget,Fakturierbar,Budget und Fakturierbar";
                                                                    ENU=" ,Budget,Billable,Both Budget and Billable";
                                                                    NLD=" ,Budget,Factureerbaar,Budget en factureerbaar";
                                                                    SVE=" ,Budget,Fakturerbart,BÜde budget och fakturerbart"];
                                                   OptionString=[ ,Schedule,Contract,Both Schedule and Contract] }
    { 25  ;   ;Cash/Private Card   ;Boolean       ;CaptionML=[DEU=Cash/Privat-Karte;
                                                              ENU=Cash/Private Card;
                                                              NLD=Cash/privÇkaart];
                                                   Editable=No }
    { 26  ;   ;Expense Type        ;Code20        ;TableRelation="Expense Type";
                                                   CaptionML=[DEU=Ausgabenart;
                                                              ENU=Expense Type;
                                                              NLD=Uitgavetype] }
    { 27  ;   ;Modified            ;Boolean       ;CaptionML=[DEU=GeÑndert;
                                                              ENU=Modified;
                                                              NLD=Gewijzigd;
                                                              SVE=éndrad] }
    { 28  ;   ;Amount %            ;Decimal       ;OnValidate=BEGIN
                                                                UpdateAmount(FIELDNO("Amount %"));
                                                              END;

                                                   CaptionML=[DEU=Betrag %;
                                                              ENU=Amount %;
                                                              NLD=Bedrag %];
                                                   DecimalPlaces=0:5 }
    { 29  ;   ;Admin Comment       ;Text250       ;CaptionML=[DEU=Admin Bemerkung;
                                                              ENU=Admin Comment;
                                                              NLD=Admin Opmerking] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Table ID,Inbox Entry No.                ;SumIndexFields=Amount }
    {    ;Inbox Entry No.                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConfRemAmount@6086301 : TextConst 'DEU=Der Betrag in der Ausgabe (%1 %2) und der zugewiesene Gesamtbetrag (%1 %3) stimmen nicht Åberein. Mîchten Sie die Seite dennoch schlie·en?;ENU=The amount on the Expense (%1 %2) and the total allocated Amount (%1 %3) do not match. Do you want to close the page anyway?;NLD=Het bedrag op de uitgave (%1 %2) en het totaal toegewezen bedrag (%1 %3) komen niet overeen. Wilt u de pagina toch sluiten?';
      ConfZeroLines@6086300 : TextConst 'DEU=Es liegen Zuordnungen mit dem Betrag 0 vor. Mîchten Sie diese wirklich behalten?;ENU=Allocations with 0 amount exist. Are you sure you want to keep these?;NLD=Allocaties met bedrag 0 bestaan. Weet u zeker dat u deze wilt behouden?';
      NotAllowed@1160040000 : TextConst 'DEU=Sie dÅrfen %1 nicht Ñndern.;ENU=You are not allowed to change %1.;NLD=U mag %1 niet wijzigen.';
      RemAmountMissmatchErr@6086302 : TextConst 'DEU=Der Betrag auf der Ausgabe (%1 %2) und der zugewiesene Gesamtbetrag (%1 %3) stimmen nicht Åberein.;ENU=The amount on the Expense (%1 %2) and the total allocated Amount (%1 %3) do not match.;NLD=Het bedrag op de uitgave (%1 %2) en het totaal toegewezen bedrag (%1 %3) komen niet overeen.';
      Text002@1160040001 : TextConst 'DEU=%1 muss eingegeben werden, bevor Sie Dimensionen anzeigen kînnen.;ENU=The %1 must be inserted before you can show dimensions.;NLD=De %1 moet aangemaakt voordat u dimensies kunt tonen.';
      ZeroLinesErr@6086303 : TextConst 'DEU="Zuweisungen mit Betrag 0 sind nicht zulÑssig. ";ENU=Allocations with amount 0 are not allowed.;NLD=Toewijzingen met het bedrag 0 zijn niet toegestaan.';

    PROCEDURE GetLastEntryNo@1160040004() : Integer;
    VAR
      ExpAllocInbox@1160040000 : Record 6086322;
    BEGIN
      IF ExpAllocInbox.FINDLAST THEN
        EXIT(ExpAllocInbox."Entry No.")
      ELSE
        EXIT(1);
    END;

    LOCAL PROCEDURE UpdateAmount@1160040003(CalledByFieldNo@1160040001 : Integer);
    VAR
      Currency@1160040002 : Record 4;
      ExpInbox@1160040000 : Record 6086323;
    BEGIN
      ExpInbox.GET("Inbox Entry No.");
      Currency.InitRoundingPrecision;

      CASE CalledByFieldNo OF

        FIELDNO(Amount):
          IF ExpInbox.Amount = 0 THEN
            "Amount %" := 100
          ELSE
            "Amount %" := Amount / ExpInbox.Amount * 100;

        FIELDNO("Amount %"):
          Amount := ROUND("Amount %" / 100 * ExpInbox.Amount,Currency."Amount Rounding Precision");

      END;
    END;

    PROCEDURE ShowDimensions@1160040000(ReadOnly@1160040001 : Boolean);
    VAR
      ExpInbox@1160040002 : Record 6086323;
    BEGIN
      IF "Entry No." = 0 THEN
        ERROR(Text002,TABLECAPTION);

      ExpInbox.GET("Inbox Entry No.");
      DrillDownDimensions(NOT ReadOnly);
    END;

    LOCAL PROCEDURE DrillDownDimensions@1160040014(Editable@1160040006 : Boolean);
    VAR
      ExpInbox@1160040007 : Record 6086323;
      EMDimInbox@1160040000 : Record 6086362;
      TempEMDimInbox@1160040002 : TEMPORARY Record 6086362;
      ExpInboxDim@1160040001 : Page 6086330;
    BEGIN
      EMDimInbox.SETRANGE("Table ID",DATABASE::"Expense Allocation Inbox");
      EMDimInbox.SETRANGE("Document Type",0);
      EMDimInbox.SETRANGE("Document No.",'');
      EMDimInbox.SETRANGE("Doc. Ref. No.","Entry No.");

      ExpInbox.GET("Inbox Entry No.");

      TempEMDimInbox.DELETEALL;
      IF (NOT (ExpInbox.Status = ExpInbox.Status::Accepted)) AND Editable THEN BEGIN
        IF EMDimInbox.FINDSET THEN
          REPEAT
            TempEMDimInbox := EMDimInbox;
            TempEMDimInbox.INSERT;
          UNTIL EMDimInbox.NEXT = 0;

        TempEMDimInbox.SETRANGE("Table ID",DATABASE::"Expense Allocation Inbox");
        TempEMDimInbox.SETRANGE("Document Type",0);
        TempEMDimInbox.SETRANGE("Document No.",'');
        TempEMDimInbox.SETRANGE("Doc. Ref. No.","Entry No.");
        PAGE.RUNMODAL(PAGE::"Expense Inbox Dimension",TempEMDimInbox);

        IF EMDimInbox.EMDimInboxUpdated(TempEMDimInbox,DATABASE::"Expense Allocation Inbox",0,'',"Entry No.") THEN BEGIN
          EMDimInbox.DELETEALL(TRUE);

          IF TempEMDimInbox.FINDSET THEN
            REPEAT
              EMDimInbox := TempEMDimInbox;
              EMDimInbox.INSERT(TRUE);
            UNTIL TempEMDimInbox.NEXT = 0;
        END;
      END ELSE BEGIN
        ExpInboxDim.SETTABLEVIEW(EMDimInbox);
        ExpInboxDim.EDITABLE := FALSE;
        ExpInboxDim.RUNMODAL;
      END;
    END;

    PROCEDURE InitFromExpInbox@1160040001(VAR ExpAllInbox@1160040000 : Record 6086322;ExpInbox@1160040001 : Record 6086323);
    BEGIN
      ExpAllInbox."Entry No." := GetLastEntryNo + 1;
      ExpAllInbox."Table ID" := DATABASE::"Expense Inbox";
      ExpAllInbox."Inbox Entry No." := ExpInbox."Entry No.";
      ExpAllInbox."Continia User ID" := ExpInbox."Continia User ID";
      ExpAllInbox."Document Date" := ExpInbox."Document Date";
      ExpAllInbox."Date Created" := TODAY;
      ExpAllInbox."Cash/Private Card" := ExpInbox."Cash/Private Card";
      ExpAllInbox.Description := ExpInbox.Description;
      ExpAllInbox."Description 2" := ExpInbox."Description 2";
      ExpAllInbox."Country/Region Code" := ExpInbox."Country/Region Code";
      ExpAllInbox."Document Date" := ExpInbox."Document Date";
      ExpAllInbox."Currency Code" := ExpInbox."Currency Code";
      ExpAllInbox.Amount := Amount;
      ExpAllInbox."Expense Type" := ExpInbox."Expense Type";
      ExpAllInbox."Global Dimension 1 Code" := ExpInbox."Global Dimension 1 Code";
      ExpAllInbox."Global Dimension 2 Code" := ExpInbox."Global Dimension 2 Code";
      ExpAllInbox."Job No." := ExpInbox."Job No.";
      ExpAllInbox."Job Task No." := ExpInbox."Job Task No.";
      ExpAllInbox.Billable := ExpInbox.Billable;
    END;

    PROCEDURE DrillDownAttendees@1160040005();
    VAR
      ExpInbox@1160040002 : Record 6086323;
      ExpAttendeeInbox@1160040000 : Record 6086325;
      ExpAttendeesInbox@1160040001 : Page 6086326;
    BEGIN
      IF ExpInbox.GET("Inbox Entry No.") THEN BEGIN
        ExpAttendeeInbox.SETRANGE("Table ID",DATABASE::"Expense Allocation Inbox");
        ExpAttendeeInbox.SETRANGE("Doc. Ref. No.","Entry No.");
        ExpAttendeesInbox.SETTABLEVIEW(ExpAttendeeInbox);
        ExpAttendeesInbox.EDITABLE := NOT (ExpInbox.Status = ExpInbox.Status::Accepted);
        ExpAttendeesInbox.RUNMODAL;
      END;
    END;

    PROCEDURE GetAttendeesForDisplay@1160040002() DisplayTxt : Text[150];
    VAR
      ExpAttendeeInbox@1160040000 : Record 6086325;
    BEGIN
      EXIT(ExpAttendeeInbox.GetAttendeesForDisplay(DATABASE::"Expense Allocation Inbox","Entry No."));
    END;

    PROCEDURE ValidateAllocationConsistency@6086301(ShowDialog@6086300 : Boolean) : Boolean;
    VAR
      ExpenseAllocationInbox@6086301 : Record 6086322;
      ExpenseInbox@6086302 : Record 6086323;
    BEGIN
      ExpenseInbox.GET(Rec."Inbox Entry No.");

      ExpenseAllocationInbox.SETCURRENTKEY("Table ID","Inbox Entry No.");
      ExpenseAllocationInbox.SETRANGE("Table ID",DATABASE::"Expense Inbox");
      ExpenseAllocationInbox.SETRANGE("Inbox Entry No.",Rec."Inbox Entry No.");
      ExpenseAllocationInbox.CALCSUMS(Amount);
      IF (NOT ExpenseAllocationInbox.ISEMPTY) AND (ExpenseAllocationInbox.Amount <> ExpenseInbox.Amount) THEN
        IF ShowDialog THEN
          EXIT(
            CONFIRM(
              ConfRemAmount,FALSE,ExpenseInbox."Currency Code",ExpenseInbox.Amount,ExpenseInbox.Amount - ExpenseAllocationInbox.Amount))
        ELSE
          ERROR(
            RemAmountMissmatchErr,ExpenseInbox."Currency Code",ExpenseInbox.Amount,ExpenseInbox.Amount - ExpenseAllocationInbox.Amount);

      IF ZeroAmountLines THEN
        IF ShowDialog THEN
          EXIT(CONFIRM(ConfZeroLines,FALSE))
        ELSE
          ERROR(ZeroLinesErr);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ZeroAmountLines@1160040008() : Boolean;
    VAR
      ExpAllocInbox@1160040000 : Record 6086322;
    BEGIN
      ExpAllocInbox.SETCURRENTKEY("Table ID","Inbox Entry No.");
      ExpAllocInbox.SETRANGE("Table ID",DATABASE::"Expense Inbox");
      ExpAllocInbox.SETRANGE("Inbox Entry No.","Inbox Entry No.");
      ExpAllocInbox.SETRANGE(Amount,0);
      EXIT(NOT ExpAllocInbox.ISEMPTY);
    END;

    PROCEDURE DeleteAutoGeneratedEntries@1160040007();
    VAR
      ExpAllocInbox@1160040000 : Record 6086322;
    BEGIN
      ExpAllocInbox.SETCURRENTKEY("Table ID","Inbox Entry No.");
      ExpAllocInbox.SETRANGE("Table ID",DATABASE::"Expense Inbox");
      ExpAllocInbox.SETRANGE("Inbox Entry No.","Inbox Entry No.");
      ExpAllocInbox.SETRANGE(Modified,FALSE);
      ExpAllocInbox.DELETEALL(TRUE);
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
}

