OBJECT Report 1142 Export Cost Budget to Excel
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kostenbudget nach Excel exportieren;
               ENU=Export Cost Budget to Excel;
               NLD=Kostenbudget naar Excel exporteren;
               NOR=Eksporter kostbudsjett til Excel];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 3233;    ;DataItem;                    ;
               DataItemTable=Table1109;
               OnPreDataItem=BEGIN
                               IF GETRANGEMIN("Budget Name") <> GETRANGEMAX("Budget Name") THEN
                                 ERROR(Text001);

                               IF (StartDate = 0D) OR
                                  (NoOfPeriods = 0) OR
                                  (FORMAT(PeriodLength) = '')
                               THEN
                                 ERROR(Text002);

                               FOR i := 1 TO NoOfPeriods DO BEGIN
                                 IF i = 1 THEN
                                   TempPeriod."Period Start" := StartDate
                                 ELSE
                                   TempPeriod."Period Start" := CALCDATE(PeriodLength,TempPeriod."Period Start");
                                 TempPeriod."Period End" := CALCDATE(PeriodLength,TempPeriod."Period Start");
                                 TempPeriod."Period End" := CALCDATE('<-1D>',TempPeriod."Period End");
                                 TempPeriod."Period No." := i;
                                 TempPeriod.INSERT;
                               END;
                               SETRANGE(Date,StartDate,TempPeriod."Period End");
                               TempCostBudgetBuf2.DELETEALL;
                               TempExcelBuffer.DELETEALL;
                             END;

               OnAfterGetRecord=BEGIN
                                  CLEAR(TempCostBudgetBuf1);
                                  TempCostBudgetBuf1."Cost Type No." := "Cost Type No.";
                                  TempCostBudgetBuf1."Budget Name" := "Budget Name";
                                  TempCostBudgetBuf1.Date := CalcPeriodStart(Date);
                                  TempCostBudgetBuf1."Cost Center Code" := "Cost Center Code";
                                  TempCostBudgetBuf1."Cost Object Code" := "Cost Object Code";
                                  TempCostBudgetBuf1.Amount := Amount;

                                  TempCostBudgetBuf2 := TempCostBudgetBuf1;
                                  IF TempCostBudgetBuf2.FIND THEN BEGIN
                                    TempCostBudgetBuf2.Amount := TempCostBudgetBuf2.Amount + TempCostBudgetBuf1.Amount;
                                    TempCostBudgetBuf2.MODIFY;
                                  END ELSE
                                    TempCostBudgetBuf2.INSERT;
                                END;

               OnPostDataItem=VAR
                                CostType@1002 : Record 1103;
                                Window@1005 : Dialog;
                                RecNo@1004 : Integer;
                                TotalRecNo@1003 : Integer;
                                LastBudgetRowNo@1001 : Integer;
                              BEGIN
                                Window.OPEN(
                                  Text005 +
                                  '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
                                Window.UPDATE(1,0);
                                TotalRecNo := CostType.COUNT;
                                RecNo := 0;
                                CostBudgetName.INIT;

                                RowNo := 1;
                                EnterCell(RowNo,1,Text006,FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);
                                EnterCell(RowNo,2,'',FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);
                                EnterFilterInCell(GETFILTER("Budget Name"),FIELDCAPTION("Budget Name"));
                                EnterFilterInCell(GETFILTER("Cost Center Code"),FIELDCAPTION("Cost Center Code"));
                                EnterFilterInCell(GETFILTER("Cost Object Code"),FIELDCAPTION("Cost Object Code"));

                                CostAccSetup.GET;

                                RowNo := RowNo + 2;
                                HeaderRowNo := RowNo;
                                EnterCell(HeaderRowNo,1,FIELDCAPTION("Cost Type No."),FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);
                                EnterCell(HeaderRowNo,2,CostType.FIELDCAPTION(Name),FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);
                                EnterCell(HeaderRowNo,3,CostType.FIELDCAPTION("Cost Center Code"),FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);
                                EnterCell(HeaderRowNo,4,CostType.FIELDCAPTION("Cost Object Code"),FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);
                                i := 0;
                                ColNo := 4;
                                IF TempPeriod.FIND('-') THEN
                                  REPEAT
                                    ColNo := ColNo + 1;
                                    EnterCell(HeaderRowNo,ColNo,FORMAT(TempPeriod."Period Start"),FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Date);
                                  UNTIL TempPeriod.NEXT = 0;

                                COPYFILTER("Cost Type No.",CostType."No.");
                                IF CostType.FIND('-') THEN
                                  REPEAT
                                    RecNo := RecNo + 1;
                                    Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
                                    RowNo := RowNo + 1;
                                    EnterCostType(RowNo,CostType);
                                    IF (CostType.Totaling = '') OR (NOT IncludeTotalingFormulas) THEN BEGIN
                                      TempCostBudgetBuf2.SETRANGE("Cost Type No.",CostType."No.");
                                      IF TempCostBudgetBuf2.FIND('-') THEN BEGIN
                                        TempCostBudgetBuf1 := TempCostBudgetBuf2;
                                        EnterCCCO(RowNo,CostType);
                                        IF TempPeriod.FIND('-') THEN
                                          REPEAT
                                            IF (TempCostBudgetBuf2."Cost Object Code" <> TempCostBudgetBuf1."Cost Object Code") OR
                                               (TempCostBudgetBuf2."Cost Center Code" <> TempCostBudgetBuf1."Cost Center Code")
                                            THEN BEGIN
                                              RowNo := RowNo + 1;
                                              EnterCostType(RowNo,CostType);
                                              EnterCCCO(RowNo,CostType);
                                              TempCostBudgetBuf1 := TempCostBudgetBuf2;
                                            END;
                                            TempPeriod.GET(0,TempCostBudgetBuf2.Date);
                                            EnterCell(
                                              RowNo,4 + TempPeriod."Period No.",
                                              MatrixMgt.FormatValue(TempCostBudgetBuf2.Amount,RoundingFactor,FALSE),
                                              CostType.Type <> CostType.Type::"Cost Type",
                                              FALSE,'',TempExcelBuffer."Cell Type"::Number);
                                            TempPeriod.NEXT;
                                          UNTIL TempCostBudgetBuf2.NEXT = 0;
                                      END ELSE
                                        CLEAR(TempCostBudgetBuf2);
                                    END ELSE
                                      IF TempPeriod.FIND('-') THEN BEGIN
                                        REPEAT
                                          EnterFormula(
                                            RowNo,4 + TempPeriod."Period No.",
                                            CostType.Totaling,
                                            CostType.Type <> CostType.Type::"Cost Type",FALSE);
                                        UNTIL TempPeriod.NEXT = 0;
                                      END;
                                  UNTIL CostType.NEXT = 0;
                                IF IncludeTotalingFormulas THEN
                                  HasFormulaError := TempExcelBuffer.ExportBudgetFilterToFormula(TempExcelBuffer);
                                Window.CLOSE;
                                LastBudgetRowNo := RowNo;

                                RowNo := RowNo + 200; // Move way below the budget

                                IF HasFormulaError THEN
                                  IF NOT CONFIRM(STRSUBSTNO(Text007,TempExcelBuffer.GetExcelReference(7))) THEN
                                    CurrReport.BREAK;

                                TempExcelBuffer.CreateBook(ServerFileName,TempExcelBuffer.GetExcelReference(10));
                                TempExcelBuffer.SetCurrent(HeaderRowNo + 1,1);
                                TempExcelBuffer.StartRange;
                                TempExcelBuffer.SetCurrent(LastBudgetRowNo,1);
                                TempExcelBuffer.EndRange;
                                TempExcelBuffer.CreateRange(TempExcelBuffer.GetExcelReference(11));
                                IF TempPeriod.FIND('-') THEN BEGIN
                                  REPEAT
                                    TempExcelBuffer.SetCurrent(HeaderRowNo + 1,4 + TempPeriod."Period No.");
                                    TempExcelBuffer.StartRange;
                                    TempExcelBuffer.SetCurrent(LastBudgetRowNo,4 + TempPeriod."Period No.");
                                    TempExcelBuffer.EndRange;
                                    TempExcelBuffer.CreateRange(TempExcelBuffer.GetExcelReference(9) + '_' + FORMAT(TempPeriod."Period No."));
                                  UNTIL TempPeriod.NEXT = 0;
                                END;

                                TempExcelBuffer.WriteSheet(PADSTR(CostBudgetName.Name,30),COMPANYNAME,USERID);
                                TempExcelBuffer.CloseBook;
                                TempExcelBuffer.OpenExcel;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 7   ;0   ;Container ;
                  ContainerType=ContentArea }

      { 6   ;1   ;Group     ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=Startdatum;
                             ENU=Start Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das erste Datum an, das in dem nach Excel zu exportierenden Budget enthalten sein soll.;
                             ENU=Specifies the first date to be included in the budget to be exported to Excel.;
                             NLD=Hiermee wordt de begindatum opgegeven die moet worden opgenomen in het budget dat u naar Excel wilt exporteren.;
                             NOR=Angir den fõrste datoen som skal tas med i budsjettet som skal leses ut til Excel.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=StartDate }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Anzahl Perioden;
                             ENU=No. of Periods;
                             NLD=Aantal perioden;
                             NOR=Antall perioder;
                             SVE=Antal perioder];
                  ToolTipML=[DEU=Gibt die Anzahl der Buchhaltungsperioden an, die nach Excel exportiert werden sollen.;
                             ENU=Specifies the number of accounting periods to be exported to Excel.;
                             NLD=Hiermee wordt het aantal boekingsperioden opgegeven dat u naar Excel wilt exporteren.;
                             NOR=Angir hvor mange regnskapsperioder som skal leses ut til Excel.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=NoOfPeriods }

      { 4   ;2   ;Field     ;
                  CaptionML=[DEU=PeriodenlÑnge;
                             ENU=Period Length;
                             NLD=Periodelengte;
                             NOR=Periodelengde;
                             SVE=Period];
                  ToolTipML=[DEU=Gibt die LÑnge der Buchhaltungsperioden fÅr das Budget an, das nach Excel exportiert werden soll.;
                             ENU=Specifies the length of the accounting periods for the budget to be exported to Excel.;
                             NLD=Hiermee wordt de lengte van de boekingsperioden opgegeven voor het budget dat u naar Excel wilt exporteren.;
                             NOR=Angir lengden pÜ regnskapsperiodene som skal leses ut til Excel.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=PeriodLength }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Summenformeln Åbernehmen;
                             ENU=Include Totalling Formulas;
                             NLD=Samentellingformules opnemen;
                             NOR=Ta med sammentellingsformler];
                  ToolTipML=[DEU=Gibt an, wenn in Excel Summenformeln, basierend auf den Summenfeldern im Fenster "Kostenartenplan", erstellt werden sollen.;
                             ENU=Specifies if you want sum formulas to be created in Excel based on the totaling fields used in the Chart of Cost Types window.;
                             NLD=Hiermee wordt opgegeven of u in Excel totaalformules wilt maken op basis van de samentellingsvelden die worden gebruikt in het venster Kostensoortschema.;
                             NOR=Angir om du vil at det skal opprettes sammentellingsformler i Excel, som er basert pÜ sammentellingsfeltene som brukes i vinduet Diagram med kosttyper.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=IncludeTotalingFormulas }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempPeriod@1024 : TEMPORARY Record 2000000007;
      TempCostBudgetBuf1@1022 : TEMPORARY Record 1114;
      TempCostBudgetBuf2@1021 : TEMPORARY Record 1114;
      CostAccSetup@1019 : Record 1108;
      CostBudgetName@1017 : Record 1110;
      TempExcelBuffer@1016 : TEMPORARY Record 370;
      MatrixMgt@1004 : Codeunit 9200;
      ServerFileName@1005 : Text;
      StartDate@1013 : Date;
      PeriodLength@1012 : DateFormula;
      RoundingFactor@1000 : 'None,1,1000,1000000';
      NoOfPeriods@1011 : Integer;
      i@1009 : Integer;
      RowNo@1008 : Integer;
      ColNo@1007 : Integer;
      HeaderRowNo@1002 : Integer;
      Text001@1027 : TextConst 'DEU=Es kînnen nicht mehrere Budgets gemeinsam exportiert werden.;ENU=You can only export one budget at a time.;NLD=U kunt slechts ÇÇn budget tegelijkertijd exporteren.;NOR=Du kan bare eksportere ett budsjett om gangen.';
      Text002@1026 : TextConst 'DEU=Geben Sie das Startdatum, die Periodenanzahl und die PeriodenlÑnge an.;ENU=You must specify the starting date, number of periods, and period length.;NLD=U moet de begindatum, het aantal perioden en de periodelengte opgeven.;NOR=Du mÜ angi en startdato, antall perioder og periodelengde.';
      Text005@1020 : TextConst 'DEU=Daten werden analysiert ...\\;ENU=Analyzing Data...\\;NLD=Analyseren gegevens...\\;NOR=Analyserer data...\\;SVE=Analyserar data...\\';
      Text006@1018 : TextConst 'DEU=Filter exportieren;ENU=Export Filters;NLD=Filters exporteren;NOR=Eksporter filtre';
      Text007@1014 : TextConst 'DEU=Einige Filter kînnen nicht in Excel-Formeln konvertiert werden. In der Excel-Tabelle mÅssen %1 Fehler ÅberprÅft werden. Mîchten Sie die Excel-Tabelle erstellen?;ENU=Some filters cannot be converted into Excel formulas. You must verify %1 errors in the Excel worksheet. Do you want to create the Excel worksheet?;NLD=Sommige filters kunnen niet naar een Excel-formule geconverteerd worden. U moet %1 fouten in het Excel-werkblad controleren. Wilt u het Excel-werkblad maken?;NOR=Noen filtre kan ikke konverteres til Excel-formler. Du mÜ kontrollere %1 feil i Excel-regnearket. Vil du opprette Excel-regnearket?';
      IncludeTotalingFormulas@1001 : Boolean;
      HasFormulaError@1003 : Boolean;

    LOCAL PROCEDURE CalcPeriodStart@1(EntryDate@1000 : Date) : Date;
    BEGIN
      TempPeriod."Period Start" := EntryDate;
      TempPeriod.FIND('=<');
      EXIT(TempPeriod."Period Start");
    END;

    LOCAL PROCEDURE EnterCell@3(RowNo@1000 : Integer;ColumnNo@1001 : Integer;CellValue@1002 : Text[250];Bold@1003 : Boolean;UnderLine@1004 : Boolean;NumberFormat@1005 : Text[30];CellType@1006 : Option);
    BEGIN
      TempExcelBuffer.INIT;
      TempExcelBuffer.VALIDATE("Row No.",RowNo);
      TempExcelBuffer.VALIDATE("Column No.",ColumnNo);
      TempExcelBuffer."Cell Value as Text" := CellValue;
      TempExcelBuffer.Formula := '';
      TempExcelBuffer.Bold := Bold;
      TempExcelBuffer.Underline := UnderLine;
      TempExcelBuffer.NumberFormat := NumberFormat;
      TempExcelBuffer."Cell Type" := CellType;
      TempExcelBuffer.INSERT;
    END;

    LOCAL PROCEDURE EnterFilterInCell@5(Filter@1000 : Text[250];FieldName@1001 : Text[100]);
    BEGIN
      IF Filter <> '' THEN BEGIN
        RowNo := RowNo + 1;
        EnterCell(RowNo,1,FieldName,FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
        EnterCell(RowNo,2,Filter,FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
      END;
    END;

    LOCAL PROCEDURE EnterFormula@8(RowNo@1000 : Integer;ColumnNo@1001 : Integer;CellValue@1002 : Text[250];Bold@1005 : Boolean;UnderLine@1006 : Boolean);
    BEGIN
      TempExcelBuffer.INIT;
      TempExcelBuffer.VALIDATE("Row No.",RowNo);
      TempExcelBuffer.VALIDATE("Column No.",ColumnNo);
      TempExcelBuffer."Cell Value as Text" := '';
      TempExcelBuffer.Formula := CellValue; // is converted to formula later.
      TempExcelBuffer.Bold := Bold;
      TempExcelBuffer.Underline := UnderLine;
      TempExcelBuffer.INSERT;
    END;

    LOCAL PROCEDURE EnterCCCO@2(RowNo@1000 : Integer;VAR CostType@1002 : Record 1103);
    BEGIN
      EnterCell(
        RowNo,3,
        TempCostBudgetBuf2."Cost Center Code",CostType.Type <> CostType.Type::"Cost Type",FALSE,'',TempExcelBuffer."Cell Type"::Text);
      EnterCell(
        RowNo,4,
        TempCostBudgetBuf2."Cost Object Code",CostType.Type <> CostType.Type::"Cost Type",FALSE,'',TempExcelBuffer."Cell Type"::Text);
    END;

    LOCAL PROCEDURE EnterCostType@6(RowNo@1000 : Integer;VAR CostType@1001 : Record 1103);
    BEGIN
      EnterCell(RowNo,1,CostType."No.",CostType.Type <> CostType.Type::"Cost Type",FALSE,'',TempExcelBuffer."Cell Type"::Text);
      EnterCell(
        RowNo,2,COPYSTR(COPYSTR(PADSTR(' ',100),1,2 * CostType.Indentation + 1) + CostType.Name,2),
        CostType.Type <> CostType.Type::"Cost Type",FALSE,'',TempExcelBuffer."Cell Type"::Text);
    END;

    [External]
    PROCEDURE SetParameters@4(NewRoundingFactor@1000 : 'None,1,1000,1000000');
    BEGIN
      RoundingFactor := NewRoundingFactor;
    END;

    [External]
    PROCEDURE SetFileNameSilent@7(NewFileName@1000 : Text);
    BEGIN
      ServerFileName := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

