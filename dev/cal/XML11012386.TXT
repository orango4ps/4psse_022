OBJECT XMLport 11012386 Import GL Entries LOSA (CTB)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries LOSA;
               SVE=Importera redovisningstransaktioner LOSA];
    Direction=Import;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    IF HumanResourcesSetup."Salary Application" = HumanResourcesSetup."Salary Application"::"LOSA-CTB" THEN BEGIN
                      ImportType := ImportType::CTB;
                    END ELSE BEGIN
                      IF HumanResourcesSetup."Salary Application" = HumanResourcesSetup."Salary Application"::"LOSA-Cobra" THEN BEGIN
                        ImportType := ImportType::COBRA;
                      END ELSE BEGIN
                        ERROR(Text000, HumanResourcesSetup.TABLECAPTION);
                      END;
                    END;

                    HumanResourcesSetup.TESTFIELD("Exchange Salary Application");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");
                    HumanResourcesSetup.TESTFIELD("Pathname Salary Backup Files");
                    HumanResourcesSetup.TESTFIELD("Participant No. (Losa)");

                    currXMLport.FILENAME(HumanResourcesSetup."Pathname Files from Salary");
                  END;

    OnPreXMLport=VAR
                   LosaCobraParticipant@1100528500 : Record 11012385;
                   lvFileNameLen@1100525500 : Integer;
                   ParticipantFound@1100528501 : Boolean;
                 BEGIN
                   GenJnlLine.LOCKTABLE;
                   GenJnlLine.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   GenJnlLine.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF GenJnlLine.FIND('+') THEN
                     LineCounter := GenJnlLine."Line No." + 10000
                   ELSE
                     LineCounter := 10000;

                   FileMgt.SplitFileAbsPathName(currXMLport.FILENAME,DummyTxt,SelectedFileName);
                   HumanResourcesSetup.TESTFIELD("Participant No. (Losa)");

                   lvFileNameLen := STRLEN(SelectedFileName);
                   CASE TRUE OF
                     (ImportType = ImportType::CTB) AND (lvFileNameLen > 12):
                        ERROR(Text005);
                     (ImportType = ImportType::COBRA) AND (lvFileNameLen < 13):
                        ERROR(Text004);
                   END;

                   ParticipantFound := COPYSTR(SelectedFileName, 1, STRLEN(HumanResourcesSetup."Participant No. (Losa)")) = HumanResourcesSetup."Participant No. (Losa)";
                   IF NOT ParticipantFound THEN
                     IF LosaCobraParticipant.FINDSET THEN
                       REPEAT
                         ParticipantFound := COPYSTR(SelectedFileName, 1, STRLEN(LosaCobraParticipant."Participant No.")) = LosaCobraParticipant."Participant No.";
                       UNTIL (LosaCobraParticipant.NEXT = 0) OR ParticipantFound;

                   IF NOT ParticipantFound THEN
                     ERROR(
                       Text003,HumanResourcesSetup.FIELDCAPTION("Participant No. (Losa)"),HumanResourcesSetup."Participant No. (Losa)",
                       HumanResourcesSetup.TABLECAPTION,LosaCobraParticipant.TABLECAPTION);


                   JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template");
                   JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch");
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{77E5F554-CBF1-4E58-8010-02A32EDCD837}];  ;Root                ;Element ;Text     }

    { [{AE0A55D3-B2E0-42DD-B482-991E215DC6E4}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(DocNoCde);
                                                                              CLEAR(DebCredCde);
                                                                              CLEAR(PostDate);
                                                                              CLEAR(AccountCde);
                                                                              CLEAR(AmntTxt);
                                                                              CLEAR(QuantTxt);
                                                                              CLEAR(DescTxt);
                                                                              CLEAR(DoorgaveCde);
                                                                              CLEAR(AanvCde);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 RecRef@1100525500 : RecordRef;
                                                                                 length1@1100525000 : Integer;
                                                                               BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN
                                                                                   "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                   "Journal Batch Name" := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                   "Line No." := LineCounter;

                                                                                   RecRef.GETTABLE("Gen. Journal Line");
                                                                                   XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                   LineCounter := LineCounter + 10000;

                                                                                   IF (ImportType = ImportType::CTB) THEN BEGIN
                                                                                     AanvCde := COPYSTR(AanvCde,1,20);
                                                                                   END;
                                                                                   //"Document No." := DocNoCde;                                     //** CALL C028320.old
                                                                                   "Document No." := COPYSTR(DocNoCde,1,MAXSTRLEN("Document No."));  //** CALL C028320.new
                                                                                   "Account Type" := "Account Type"::"G/L Account";
                                                                                   EVALUATE("Posting Date", PostDate);
                                                                                   "Source Code"   := JnlTemplRec."Source Code";
                                                                                   IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                     "Reason Code" := JnlBatchRec."Reason Code"
                                                                                   ELSE
                                                                                     "Reason Code" := JnlTemplRec."Reason Code";

                                                                                   BedrijfIDCde := COPYSTR(AanvCde, 32,10);
                                                                                   AanvCde := COPYSTR(AanvCde, 1,31);
                                                                                   IF BedrijfIDCde <> '' THEN BEGIN
                                                                                     CompanyData.SETRANGE("Company ID", BedrijfIDCde);
                                                                                     IF CompanyData.FINDFIRST THEN
                                                                                       IF CompanyData."Company Name" <> COMPANYNAME THEN
                                                                                         "Receiving Company" := CompanyData."Company Name";
                                                                                   END;

                                                                                   AccountCde  := DELCHR(AccountCde, '>');     //** CALL C028320.new
                                                                                   DoorgaveCde := DELCHR(DoorgaveCde, '<>');   //** CALL C028320.new
                                                                                   IF (AccountCde = '') AND (DoorgaveCde = 'KP') THEN BEGIN
                                                                                     HumanResourcesSetup.TESTFIELD("Length G/L Account (Losa)");
                                                                                     "Account No." :=  COPYSTR(AanvCde,1,HumanResourcesSetup."Length G/L Account (Losa)");
                                                                                     //** CALL C028320.sn
                                                                                     length1 := STRLEN(AanvCde) -  HumanResourcesSetup."Length G/L Account (Losa)";
                                                                                     IF length1 > MAXSTRLEN("Shortcut Dimension 1 Code") THEN
                                                                                        length1 := MAXSTRLEN("Shortcut Dimension 1 Code");
                                                                                     IF length1 > 0 THEN
                                                                                       "Shortcut Dimension 1 Code" := COPYSTR(AanvCde, HumanResourcesSetup."Length G/L Account (Losa)"+1, length1)
                                                                                     //** CALL C028320.en
                                                                                     //** CALL C028320.so
                                                                                     //"Shortcut Dimension 1 Code" := COPYSTR(AanvCde,HumanResourcesSetup."Length G/L Account (Losa)"+1,
                                                                                     //                                       MAXSTRLEN(AanvCde) - HumanResourcesSetup."Length G/L Account (Losa)");
                                                                                     //** CALL C028320.eo
                                                                                   END ELSE BEGIN
                                                                                     IF DoorgaveCde = 'KP' THEN BEGIN
                                                                                       //"Account No." := AccountCde;                                      //** CALL C028320.old
                                                                                       "Account No." := COPYSTR(AccountCde, 1, MAXSTRLEN("Account No."));  //** CALL C028320.new
                                                                                       //"Shortcut Dimension 1 Code" := AanvCde;                           //** CALL C028320.old
                                                                                       "Shortcut Dimension 1 Code" := COPYSTR(AanvCde, 1, MAXSTRLEN("Shortcut Dimension 1 Code"));    //** CALL C028320.new
                                                                                     END;
                                                                                   END;

                                                                                   IF HumanResourcesSetup."Length Department (Losa)" > 0 THEN BEGIN
                                                                                     "Shortcut Dimension 1 Code" := DELCHR("Shortcut Dimension 1 Code", '>' , ' ');
                                                                                     IF HumanResourcesSetup."Length Department (Losa)" < STRLEN("Shortcut Dimension 1 Code") THEN
                                                                                       "Shortcut Dimension 1 Code" :=
                                                                                         COPYSTR(
                                                                                           "Shortcut Dimension 1 Code",
                                                                                           STRLEN("Shortcut Dimension 1 Code") + 1 - HumanResourcesSetup."Length Department (Losa)" ,
                                                                                           HumanResourcesSetup."Length Department (Losa)");
                                                                                   END;

                                                                                   VALIDATE("Account No.");
                                                                                   VALIDATE("Shortcut Dimension 1 Code");

                                                                                   IF (DoorgaveCde = 'PJ') OR (DoorgaveCde = 'WO') THEN BEGIN  //C039317
                                                                                     HumanResourcesSetup.TESTFIELD("Length Project (Extern)");
                                                                                     IF DoorgaveCde = 'WO' THEN BEGIN //C039317
                                                                                       "Service Order No." := COPYSTR(AanvCde,1,HumanResourcesSetup."Length Project (Extern)");
                                                                                       GetServiceCategory;
                                                                                     END ELSE
                                                                                       "Job No." := COPYSTR(AanvCde,1,HumanResourcesSetup."Length Project (Extern)");
                                                                                     //** CALL C028320.sn
                                                                                     length1 := STRLEN(AanvCde) -  HumanResourcesSetup."Length Project (Extern)";
                                                                                     IF length1 > MAXSTRLEN("Shortcut Dimension 2 Code") THEN
                                                                                        length1 := MAXSTRLEN("Shortcut Dimension 2 Code");
                                                                                     IF length1 > 0 THEN
                                                                                       "Shortcut Dimension 2 Code" := COPYSTR(AanvCde,HumanResourcesSetup."Length Project (Extern)"+1, length1);
                                                                                     //** CALL C028320.en
                                                                                     //** CALL C028320.so
                                                                                     //"Shortcut Dimension 2 Code" := COPYSTR(AanvCde,HumanResourcesSetup."Length Project (Extern)"+1,
                                                                                     //                                       MAXSTRLEN(AanvCde) - HumanResourcesSetup."Length Project (Extern)");
                                                                                     //** CALL C028320.eo
                                                                                     VALIDATE("Shortcut Dimension 2 Code");
                                                                                   END;

                                                                                   Quantity := ConvertTextToValue(QuantTxt);
                                                                                   //IF DebCredCde = 'C' THEN            //** CALL C028320.old
                                                                                   IF COPYSTR(DebCredCde,1,1) = 'C' THEN //** CALL C028320.new
                                                                                     Quantity := Quantity * -1;
                                                                                   VALIDATE(Quantity);
                                                                                   Amount := ConvertTextToValue(AmntTxt);
                                                                                   //IF DebCredCde = 'C' THEN            //** CALL C028320.old
                                                                                   IF COPYSTR(DebCredCde,1,1) = 'C' THEN //** CALL C028320.new
                                                                                     Amount := Amount * -1;
                                                                                   VALIDATE(Amount);
                                                                                   "System-Created Entry" := TRUE;
                                                                                   Description := COPYSTR(DescTxt,1,MAXSTRLEN(Description));
                                                                                   "Gen. Posting Type" := "Gen. Posting Type"::" ";
                                                                                   "Gen. Bus. Posting Group" := '';
                                                                                   "Gen. Prod. Posting Group"  := '';
                                                                                   "VAT Bus. Posting Group" := '';
                                                                                   VALIDATE("VAT Prod. Posting Group", '');
                                                                                   "Origin Salary Application" := TRUE;
                                                                                 END;
                                                                               END;
                                                                                }

    { [{569A044F-5361-44D8-8BE4-097458CC410E}];2 ;DocNoCde            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{8B1B66B6-CBFF-4795-BEA2-70D09B73915E}];2 ;Filler1             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{97BACC17-1C36-45BB-8081-A911FF782CDC}];2 ;DebCredCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{E9688C8A-90F4-44FB-954C-D5B4B5C46218}];2 ;PostDate            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{6E18C3BE-94D5-4FC9-A08D-B1C89F08486E}];2 ;Filler2             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{63B9D256-BA8E-423B-BDBC-88E63EFCAAEE}];2 ;AccountCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{B6080510-734B-4E2E-8201-717B7EEFAA99}];2 ;Filler3             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=26 }

    { [{D34EC409-A3B0-4A58-BD8A-8B45B1FCC2D9}];2 ;AmntTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{5BF084DA-B7E3-4163-871A-9C9DD00DF0AB}];2 ;Filler4             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=60 }

    { [{FC540A0E-CCE4-49B1-A320-81F9FAFD03CC}];2 ;QuantTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{F5131AB0-9D3B-4980-BB8C-B7B7D5DC0A9C}];2 ;Filler5             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=9 }

    { [{B2815861-BAF7-460E-90A5-C1133FF47644}];2 ;DescTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{B6EE96C8-8296-4C31-90A8-0CF22C59A166}];2 ;Filler6             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=51 }

    { [{96649CF6-5C72-49B8-93A7-C9C69AE1EDED}];2 ;DoorgaveCde         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{E2B7F608-1987-4233-B26A-E1F5C12A64C9}];2 ;AanvCde             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=42 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      HumanResourcesSetup@1100525520 : Record 5218;
      GenJnlLine@1100525519 : Record 81;
      JnlTemplRec@1100525518 : Record 80;
      JnlBatchRec@1100525517 : Record 232;
      CompanyData@1100525516 : Record 11020674;
      FileMgt@1100525512 : Codeunit 419;
      XMLportStatusDialogMgt@1100525000 : Codeunit 11130006;
      LineCounter@1100525515 : Integer;
      DummyTxt@1100525508 : Text[250];
      BedrijfIDCde@1100525504 : Code[4];
      OldFile@1100525503 : Text[250];
      NewFile@1100525502 : Text[250];
      SelectedFileName@1100525501 : Text[250];
      ImportType@1100525500 : 'CTB,COBRA';
      Text000@1100525511 : TextConst 'ENU=Check %1 to activate Losa as payroll application.;SVE=Kontrollera %1 f”r att aktivera Losa som l”neapplikation.';
      Text003@1100525507 : TextConst 'ENU=The Filename has to start with %1 (%2) of the %3 or table %4.';
      Text004@1100525506 : TextConst 'ENU=The filename is too short for a Losa (COBRA) import file.;SVE=Filnamnet „r f”r kort f”r en Losa (COBRA) importfil.';
      Text005@1100525505 : TextConst 'ENU=The filename is too long for a Losa (CTB) import file.;SVE=Filnamnet „r f”r kort f”r en Losa (CTB) importfil.';

    PROCEDURE ConvertTextToValue@1210190001(QuantityText@1210190000 : Text) Value@1210190003 : Decimal;
    VAR
      ValueQuantityText@1210190001 : Text;
    BEGIN
      IF QuantityText = '' THEN
        EXIT(0);

      ValueQuantityText := DELCHR(QuantityText,'=','.-');
      EVALUATE(Value,DELCHR(ValueQuantityText,'=','.'));
      Value := Value/100;
      IF STRPOS(QuantityText, '-') > 0 THEN
        Value := -Value;
    END;

    BEGIN
    END.
  }
}

