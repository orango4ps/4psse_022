OBJECT Page 11125037 Project Purchase Quote List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projekt Einkaufsanfrage šbersicht;
               ENU=Project Purchase Quote List;
               NLD=Project inkoopofferte overzicht;
               NOR=Liste over prosjektinnkj›pstilbud;
               SVE=Lista ”ver projektink”psofferter];
    SourceTable=Table38;
    SourceTableView=WHERE(Document Type=CONST(Quote));
    PageType=List;
    CardPageID=Purchase Quote;
    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
               END;

    OnAfterGetRecord=BEGIN
                       CalculateBalances;
                     END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100000999 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    ActionList=ACTIONS
    {
      { 1100528200;  ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100528201;1 ;Action    ;
                      CaptionML=[DEU=&Einkaufsanfragezeilen;
                                 ENU=&Purchase Quote Lines;
                                 NLD=&Inkoopofferteregels];
                      Promoted=Yes;
                      Image=PostedReceipts;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PurchOrderFactBoxMgt@1100528200 : Codeunit 11130003;
                                 PurchaseHeader@1100525000 : Record 38;
                               BEGIN
                                 IF ProjectNo <> '' THEN BEGIN
                                   PurchaseHeader.COPY(Rec);
                                   PurchaseHeader."Job No." := ProjectNo;
                                   PurchOrderFactBoxMgt.ShowPurchQuoteLines(PurchaseHeader, FALSE);
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100485000;1;Group  ;
                Editable=FALSE;
                GroupType=Repeater }

    { 1100485001;2;Field  ;
                Lookup=No;
                SourceExpr="No." }

    { 1100485003;2;Field  ;
                SourceExpr="Buy-from Vendor No." }

    { 1100485005;2;Field  ;
                SourceExpr="Buy-from Vendor Name" }

    { 1100485007;2;Field  ;
                SourceExpr="Job No." }

    { 1100525002;2;Field  ;
                SourceExpr="Service Order No." }

    { 1100525001;2;Field  ;
                SourceExpr="Order Date" }

    { 1100485015;2;Field  ;
                CaptionML=[DEU=Betrag;
                           ENU=Amount;
                           NLD=Bedrag;
                           NOR=Bel›p;
                           SVE=Belopp];
                SourceExpr=BalanceExclVAT;
                AutoFormatExpr="Currency Code" }

    { 1100525003;2;Field  ;
                SourceExpr="Currency Code" }

    { 1100485009;2;Field  ;
                CaptionML=[DEU=Betrag inkl. MwSt.;
                           ENU=Amount Including VAT;
                           NLD=Bedrag incl. btw;
                           NOR=Bel›p inkl. mva.;
                           SVE=Belopp inkl. moms];
                SourceExpr=BalanceInclVAT;
                AutoFormatExpr="Currency Code" }

    { 1100525004;2;Field  ;
                SourceExpr="Warranty Data present" }

    { 1100485017;2;Field  ;
                SourceExpr="Vendor Order No.";
                Visible=FALSE }

    { 1100485025;2;Field  ;
                SourceExpr="Sell-to Customer No.";
                Visible=FALSE }

    { 1100485019;2;Field  ;
                SourceExpr="Ship-to Code";
                Visible=FALSE }

    { 1100485021;2;Field  ;
                SourceExpr="Expected Receipt Date";
                Visible=FALSE }

    { 1100485027;2;Field  ;
                SourceExpr="Purchase Action";
                Visible=FALSE }

    { 1100485029;2;Field  ;
                SourceExpr="Posting Description";
                Visible=FALSE }

    { 1100525000;2;Field  ;
                SourceExpr=Status }

    { 1210190005;0;Container;
                ContainerType=FactBoxArea }

    { 1210190004;1;Part   ;
                SubPageLink=Document Type=FIELD(Document Type),
                            No.=FIELD(No.);
                PagePartID=Page11130218;
                PartType=Page }

    { 1210190001;1;Part   ;
                SubPageLink=Document Type=FIELD(Document Type),
                            No.=FIELD(No.);
                PagePartID=Page11130217;
                PartType=Page }

  }
  CODE
  {
    VAR
      ProjectNo@1100525003 : Code[20];
      ServiceOrderNo@1100525002 : Code[20];
      BalanceInclVAT@1100525001 : Decimal;
      BalanceExclVAT@1100525000 : Decimal;
      DPA_IsHardFilter@1100000999 : Boolean;

    PROCEDURE CalculateBalances@1100485090();
    VAR
      lvPurchLineRec@1100485000 : Record 39;
      lvProj@1100485003 : Code[20];
      lvServOrder@1100485002 : Code[20];
    BEGIN
      lvProj := ProjectNo;
      lvServOrder := ServiceOrderNo;

      lvPurchLineRec.SETRANGE("Document Type", "Document Type");
      lvPurchLineRec.SETRANGE("Document No.", "No.");
      IF (lvProj <> '') THEN BEGIN
        lvPurchLineRec.SETFILTER("Job No.", '<>%1', lvProj);
        IF NOT lvPurchLineRec.FINDFIRST THEN
          lvProj := '';
        lvPurchLineRec.SETRANGE("Job No.");  //* Reset filter
      END;
      IF (lvServOrder <> '') THEN BEGIN
        lvPurchLineRec.SETFILTER("Service Order No.", '<>%1', lvServOrder);
        IF NOT lvPurchLineRec.FINDFIRST THEN
          lvServOrder := '';
        lvPurchLineRec.SETRANGE("Service Order No.");  //* Reset filter
      END;

      IF (lvProj = '') AND (lvServOrder = '') THEN BEGIN
        lvPurchLineRec."Document Type" := "Document Type";
        lvPurchLineRec."Document No." := "No.";
        lvPurchLineRec.CALCFIELDS("Balance Incl. VAT", "Balance Excl. VAT");
        BalanceInclVAT := lvPurchLineRec."Balance Incl. VAT";
        BalanceExclVAT := lvPurchLineRec."Balance Excl. VAT";
      END ELSE BEGIN
        BalanceInclVAT := 0;
        BalanceExclVAT := 0;
        lvPurchLineRec.RESET;
        lvPurchLineRec.SETRANGE("Document Type", "Document Type");
        lvPurchLineRec.SETRANGE("Document No.", "No.");
        IF lvProj <> '' THEN
          lvPurchLineRec.SETRANGE("Job No.", lvProj);
        IF lvServOrder <> '' THEN
          lvPurchLineRec.SETRANGE("Service Order No.", lvServOrder);
        IF lvPurchLineRec.FINDSET THEN BEGIN
          REPEAT
            BalanceInclVAT := BalanceInclVAT + lvPurchLineRec."Amount Including VAT";
            BalanceExclVAT := BalanceExclVAT + lvPurchLineRec.Amount;
          UNTIL lvPurchLineRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE MarkPurchaseQuotes@1100485000(ProjectNo@1100485000 : Code[20]);
    BEGIN
      IF ProjectNo = '' THEN
        EXIT;

      MarkPurchaseQuotesOfProject(ProjectNo);
      MARKEDONLY(TRUE);
    END;

    PROCEDURE SetProject@1100525000(_ProjectNo@1100525000 : Code[20]);
    BEGIN
      ProjectNo := _ProjectNo;
    END;

    BEGIN
    END.
  }
}

