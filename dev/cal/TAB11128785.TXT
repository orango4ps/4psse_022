OBJECT Table 11128785 NEN2767/LTMP Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               NEN2767LTMPHeader@1100528601 : Record 11128784;
               NEN2767LTMPLine@1100528600 : Record 11128785;
               NEN2767Management@1100528602 : Codeunit 11012863;
             BEGIN
               "Service Order No." := '';
               "Try-out Quote" := '';

               IF "Line No." = 0 THEN BEGIN
                 NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
                 IF NEN2767LTMPLine.FINDLAST THEN
                   "Line No." := NEN2767LTMPLine."Line No.";
                 "Line No." += 1;
               END;
               IF NEN2767LTMPHeader.GET("Header Entry ID") THEN
                 NEN2767LTMPHeader.TESTFIELD("NEN2767 Version Code");
               CreateScenarioLines;

               IF NOT GetSameObjectOrElementLine(NEN2767LTMPLine) THEN
                 CopyServiceObjectPictureToElement
               ELSE
                 NEN2767Management.CopyElementPictureToLine(NEN2767LTMPLine, Rec);
             END;

    OnDelete=BEGIN
               DeleteScenarioLines;
               DeleteDocumentLinks;
             END;

    OnRename=VAR
               LTMPScenarioLine@1100528600 : Record 11128792;
               NewLTMPScenarioLine@1100528601 : Record 11128792;
               LTMPPolicyRiskValue@1100528602 : Record 11128789;
               NewLTMPPolicyRiskValue@1100528603 : Record 11128789;
               DocumentLink@1100528604 : Record 11012747;
             BEGIN
               IF "Structure Line Code" <> xRec."Structure Line Code" THEN BEGIN
                 LTMPScenarioLine.SETRANGE("Header Entry ID", "Header Entry ID");
                 LTMPScenarioLine.SETRANGE("Structure Line Code", xRec."Structure Line Code");
                 LTMPScenarioLine.SETRANGE("Line No.", "Line No.");
                 IF LTMPScenarioLine.FINDSET THEN
                   REPEAT
                     NewLTMPScenarioLine := LTMPScenarioLine;
                     NewLTMPScenarioLine."Structure Line Code" := "Structure Line Code";
                     NewLTMPScenarioLine.INSERT(TRUE);
                     LTMPScenarioLine.DELETE;
                   UNTIL LTMPScenarioLine.NEXT = 0;
                 LTMPPolicyRiskValue.SETRANGE("Header Entry ID", "Header Entry ID");
                 LTMPPolicyRiskValue.SETRANGE("Structure Line Code", xRec."Structure Line Code");
                 LTMPPolicyRiskValue.SETRANGE("Line No.", "Line No.");
                 IF LTMPPolicyRiskValue.FINDSET THEN
                   REPEAT
                     NewLTMPPolicyRiskValue := LTMPPolicyRiskValue;
                     NewLTMPPolicyRiskValue."Structure Line Code" := "Structure Line Code";
                     NewLTMPPolicyRiskValue.INSERT(TRUE);
                     LTMPPolicyRiskValue.DELETE;
                   UNTIL LTMPPolicyRiskValue.NEXT = 0;

                 UpdateStructure;

                 DocumentLink.SETRANGE("Record ID", xRec.RECORDID);
                 DocumentLink.SETRANGE("Internal Company", COMPANYNAME);
                 DocumentLink.MODIFYALL("Record ID", RECORDID);
               END;
             END;

    CaptionML=ENU=NEN2767/LTMP Line;
  }
  FIELDS
  {
    { 1   ;   ;Header Entry ID     ;Integer       ;TableRelation="NEN2767/LTMP Header";
                                                   OnValidate=VAR
                                                                NEN2767LTMPHeader@1100528600 : Record 11128784;
                                                              BEGIN
                                                                IF NEN2767LTMPHeader.GET("Header Entry ID") THEN BEGIN
                                                                  "Location Type" := NEN2767LTMPHeader."Location Type";
                                                                  "Survey No." := NEN2767LTMPHeader."Survey No.";
                                                                  "Location Code" := NEN2767LTMPHeader."Location Code";
                                                                  VALIDATE("NEN2767 Version Code", NEN2767LTMPHeader."NEN2767 Version Code");
                                                                  "Structure No." := NEN2767LTMPHeader."Structure No.";
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Header Entry ID;
                                                   Editable=No }
    { 5   ;   ;Structure No.       ;Integer       ;TableRelation=IF (Location Type=CONST(Service Location)) "Service Location Structure".No. WHERE (Service Location No.=FIELD(Location Code))
                                                                 ELSE IF (Location Type=CONST(Survey Location)) "Serv. Survey Loc. Structure"."Structure No." WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                                                                     Location Code=FIELD(Location Code));
                                                   CaptionML=[ENU=Structure No.;
                                                              NOR=Strukturnr;
                                                              SVE=Strukturnr];
                                                   Editable=No }
    { 6   ;   ;Structure Line Code ;Code20        ;TableRelation=IF (Location Type=CONST(Service Location)) "Service Loc. Structure Line".No. WHERE (Service Location No.=FIELD(Location Code),
                                                                                                                                                     Structure No.=FIELD(Structure No.))
                                                                                                                                                     ELSE IF (Location Type=CONST(Survey Location)) "Service Survey Structure Line".Code WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                                                                                                                                                Location Code=FIELD(Location Code),
                                                                                                                                                                                                                                                Structure No.=FIELD(Structure No.),
                                                                                                                                                                                                                                                Object Line=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF "Structure Line Code" <> xRec."Structure Line Code" THEN
                                                                  UpdateStructure;
                                                              END;

                                                   OnLookup=VAR
                                                              NEN2767LTMPHeader@1100528602 : Record 11128784;
                                                              NEN2767Management@1100528601 : Codeunit 11012863;
                                                            BEGIN
                                                              GetHeader(NEN2767LTMPHeader);
                                                              IF NEN2767Management.SelectStructureLineCode(NEN2767LTMPHeader, "Structure Line Code") THEN
                                                                VALIDATE("Structure Line Code");
                                                            END;

                                                   CaptionML=ENU=Structure Line Code }
    { 10  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Location Type       ;Option        ;CaptionML=ENU=Location Type;
                                                   OptionCaptionML=ENU=Unbound,Service Location,Survey Location;
                                                   OptionString=Unbound,Service Location,Survey Location;
                                                   Editable=No }
    { 30  ;   ;Location Code       ;Code20        ;TableRelation=IF (Location Type=CONST(Service Location)) "Service Location"
                                                                 ELSE IF (Location Type=CONST(Survey Location)) "Service Survey Location"."Location Code" WHERE (Survey No.=FIELD(Survey No.));
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Plasseringkode;
                                                              SVE=LagerstÑllekod];
                                                   Editable=No }
    { 40  ;   ;Survey No.          ;Code20        ;FieldClass=Normal;
                                                   TableRelation="Service Survey";
                                                   CaptionML=[ENU=Survey No.;
                                                              NOR=Kartlegging nr.;
                                                              SVE=KartlÑggningsnr];
                                                   Editable=No }
    { 50  ;   ;Measurement Date    ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("NEN2767/LTMP Header"."Measurement Date" WHERE (Entry ID=FIELD(Header Entry ID)));
                                                   CaptionML=[ENU=Measurement Date;
                                                              NOR=MÜlingsdato];
                                                   Editable=No }
    { 60  ;   ;Object No.          ;Code20        ;TableRelation=IF (Location Type=CONST(Service Location)) Service-Object
                                                                 ELSE IF (Location Type=CONST(Survey Location)) "Serv. Surv. Location Object"."Seq. No." WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                                                                Location Code=FIELD(Location Code));
                                                   OnValidate=VAR
                                                                ServiceObject@1100528600 : Record 11071691;
                                                                StandardObject@1100528604 : Record 11020334;
                                                                ObjectSpecificationMgt@1100528601 : Codeunit 11012848;
                                                                NEN2767Management@1100528603 : Codeunit 11012863;
                                                              BEGIN
                                                                IF "Object No." <> '' THEN BEGIN
                                                                  ServiceObject.GET("Object No.");
                                                                  IF "Structure Line Code" = '' THEN
                                                                    DetermineStructureLineCode
                                                                  ELSE
                                                                    CheckAndAddToStructure;
                                                                  VALIDATE("Element No.", ServiceObject."NEN2767 Element Code");
                                                                  IF NOT CopyDataFromSameLineWithSameObjectOrElement THEN BEGIN
                                                                    CALCFIELDS("Measurement Date", "Price Index Code");
                                                                    TESTFIELD("Location Type", "Location Type"::"Service Location");
                                                                    ServiceObject.CALCFIELDS("Brand Description", "Model Description", "Type Description");
                                                                    VALIDATE("Brand Code", ServiceObject."Brand Code");
                                                                    VALIDATE("Model Code", ServiceObject."Model Code");
                                                                    VALIDATE("Type Code", ServiceObject."Type Code");
                                                                    "Year of Manufacture" := ObjectSpecificationMgt.GetYearOfManufacture("Object No.");
                                                                    IF ServiceObject."Measure Quantity" <> 0 THEN
                                                                      Quantity := ServiceObject."Measure Quantity"
                                                                    ELSE
                                                                      Quantity := ServiceObject.Quantity;
                                                                    IF ServiceObject."Unit Of Measure" <> '' THEN
                                                                      "Unit of Measure" := ServiceObject."Unit Of Measure";
                                                                    "Replacement Value" := NEN2767Management.GetIndexedReplacementValue("Object No.", "Measurement Date", "Price Index Code");
                                                                    "Desired Score" := ServiceObject."Desired Condition Score";
                                                                    IF StandardObject.GET(ServiceObject."Standard Object No.") THEN BEGIN
                                                                      IF FORMAT(StandardObject."Theoretical Life Span") <> '' THEN
                                                                        "Theoretical Life Span" := StandardObject."Theoretical Life Span";
                                                                    END;
                                                                    CopyServiceObjectPictureToElement;
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              ObjectNoOnLookup;
                                                            END;

                                                   CaptionML=[ENU=Object No.;
                                                              NOR=Objektnr.;
                                                              SVE=Objektnr] }
    { 65  ;   ;Object Seq. No.     ;Integer       ;TableRelation="Serv. Surv. Location Object"."Seq. No." WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                 Location Code=FIELD(Location Code));
                                                   OnValidate=VAR
                                                                ServSurvLocationObject@1100528600 : Record 11071813;
                                                                StandardObject@1100528601 : Record 11020334;
                                                                NEN2767Element@1100528602 : Record 11128770;
                                                              BEGIN
                                                                IF "Object Seq. No." <> 0 THEN BEGIN
                                                                  TESTFIELD("Location Type", "Location Type"::"Survey Location");
                                                                  ServSurvLocationObject.GET("Survey No.", "Location Code", "Object Seq. No.");
                                                                  ServSurvLocationObject.TESTFIELD(Quantity, 1);
                                                                  "Year of Manufacture" := ServSurvLocationObject."Year of Manufacture";
                                                                  IF StandardObject.GET(ServSurvLocationObject."Standard Object No.") THEN BEGIN
                                                                    IF NEN2767Element.GET("NEN2767 Version Code", StandardObject."NEN2767 Element Code") THEN
                                                                      VALIDATE("Building Part Code", NEN2767Element."Building Part");
                                                                    VALIDATE("Element No.", StandardObject."NEN2767 Element Code");
                                                                    StandardObject.CALCFIELDS("Brand Description", "Model Description", "Type Description");
                                                                    VALIDATE("Brand Code", StandardObject."Brand Code");
                                                                    VALIDATE("Model Code", StandardObject."Model Code");
                                                                    VALIDATE("Type Code", StandardObject."Type Code");
                                                                  END;
                                                                  Quantity := ServSurvLocationObject."Quantity per Object";
                                                                  CopyDataFromSameLineWithSameObjectOrElement;
                                                                  IF "Structure Line Code" = '' THEN
                                                                    DetermineStructureLineCode
                                                                  ELSE
                                                                    CheckAndAddToStructure;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServSurvLocationObject@1100528602 : Record 11071813;
                                                              NEN2767LTMPHeader@1100528601 : Record 11128784;
                                                              NEN2767Management@1100528603 : Codeunit 11012863;
                                                              DummyCode@1100528600 : Code[20];
                                                            BEGIN
                                                              IF "Location Type" <> "Location Type"::"Survey Location" THEN
                                                                EXIT;
                                                              GetHeader(NEN2767LTMPHeader);
                                                              IF NOT NEN2767LTMPHeader."Use Existing Structure" THEN BEGIN
                                                                ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
                                                                ServSurvLocationObject.SETRANGE("Location Code", "Location Code");
                                                                IF "Object Seq. No." <> 0 THEN BEGIN
                                                                  ServSurvLocationObject.SETRANGE("Seq. No.", "Object Seq. No.");
                                                                  IF ServSurvLocationObject.FINDFIRST THEN;
                                                                  ServSurvLocationObject.SETRANGE("Seq. No.");
                                                                END;
                                                                IF PAGE.RUNMODAL(0, ServSurvLocationObject) = ACTION::LookupOK THEN
                                                                  VALIDATE("Object Seq. No.", ServSurvLocationObject."Seq. No.");
                                                              END ELSE BEGIN
                                                                IF NEN2767Management.SelectStructureLineCodeAndObjectNo(NEN2767LTMPHeader, "Structure Line Code", DummyCode, "Object Seq. No.") THEN
                                                                  VALIDATE("Object Seq. No.");
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Object Seq. No. }
    { 70  ;   ;Building Part Code  ;Code10        ;TableRelation="NEN2767 Building Part".Code WHERE (Version Code=FIELD(NEN2767 Version Code));
                                                   OnValidate=BEGIN
                                                                IF "Building Part Code" <> xRec."Building Part Code" THEN
                                                                  VALIDATE("Element No.", '');
                                                              END;

                                                   CaptionML=ENU=Building Part Code }
    { 80  ;   ;Element No.         ;Code20        ;TableRelation=IF (Building Part Code=CONST()) "NEN2767 Element".No. WHERE (Version Code=FIELD(NEN2767 Version Code))
                                                                 ELSE IF (Building Part Code=FILTER(<>'')) "NEN2767 Element".No. WHERE (Version Code=FIELD(NEN2767 Version Code),
                                                                                                                                        Building Part=FIELD(Building Part Code));
                                                   OnValidate=VAR
                                                                NEN2767Element@1100528600 : Record 11128770;
                                                                xNEN2767Element@1100528500 : Record 11128770;
                                                              BEGIN
                                                                IF "Element No." <> '' THEN BEGIN
                                                                  NEN2767Element.GET("NEN2767 Version Code", "Element No.");
                                                                  "Building Part Code" := NEN2767Element."Building Part";

                                                                  IF Description = '' THEN
                                                                    Description := NEN2767Element.Description
                                                                  //DP01529.sn T007897
                                                                  ELSE
                                                                    IF xNEN2767Element.GET(xRec."NEN2767 Version Code",xRec."Element No.") THEN
                                                                      IF Description = xNEN2767Element.Description THEN
                                                                        Description := NEN2767Element.Description;
                                                                  //DP01529.en
                                                                  CopyDataFromSameLineWithSameObjectOrElement;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Element No.;
                                                              NOR=Elementnr;
                                                              SVE=Elementnr] }
    { 85  ;   ;Description         ;Text100       ;OnValidate=BEGIN
                                                                UpdateOtherDataSameElementLines;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 90  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                UpdateOtherDataSameElementLines;
                                                                DetermineScore;
                                                                VALIDATE("Scale Percentage");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 100 ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=BEGIN
                                                                UpdateOtherDataSameElementLines;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=MÜleenhet;
                                                              SVE=MÜttenhet] }
    { 110 ;   ;Year of Manufacture ;Integer       ;OnValidate=BEGIN
                                                                UpdateOtherDataSameElementLines;
                                                                IF "Year of Manufacture" = 0 THEN
                                                                  TESTFIELD(Degradation, FALSE);
                                                                DetermineScore;
                                                              END;

                                                   CaptionML=[ENU=Year of Manufacture;
                                                              NOR=ProduksjonsÜr;
                                                              SVE=TillverkningsÜr];
                                                   BlankZero=Yes }
    { 120 ;   ;Brand Code          ;Code20        ;TableRelation="Service Object Brand";
                                                   OnValidate=VAR
                                                                ServiceObjectBrand@1100528600 : Record 11072195;
                                                              BEGIN
                                                                IF xRec."Brand Code" <> "Brand Code" THEN BEGIN
                                                                  VALIDATE("Model Code", '');
                                                                  IF ServiceObjectBrand.GET("Brand Code") THEN
                                                                    "Brand Description" := ServiceObjectBrand.Description;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Brand;
                                                              NOR=Varemerke;
                                                              SVE=MÑrke] }
    { 121 ;   ;Brand Description   ;Text30        ;OnValidate=BEGIN
                                                                IF "Brand Description" <> xRec."Brand Description" THEN
                                                                  TESTFIELD("Brand Code", '');
                                                              END;

                                                   CaptionML=[ENU=Brand Description;
                                                              SVE=MÑrke] }
    { 125 ;   ;Model Code          ;Code20        ;TableRelation="Service Object Model".Code WHERE (Brand Code=FIELD(Brand Code));
                                                   OnValidate=VAR
                                                                ServiceObjectModel@1100528600 : Record 11072196;
                                                              BEGIN
                                                                IF xRec."Model Code" <> "Model Code" THEN BEGIN
                                                                  VALIDATE("Type Code", '');
                                                                  IF ServiceObjectModel.GET("Brand Code", "Model Code") THEN
                                                                    "Model Description" := ServiceObjectModel.Description;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Model;
                                                              NOR=Modell;
                                                              SVE=Modell] }
    { 126 ;   ;Model Description   ;Text30        ;OnValidate=BEGIN
                                                                IF "Model Description" <> xRec."Model Description" THEN
                                                                  TESTFIELD("Model Code", '');
                                                              END;

                                                   CaptionML=[ENU=Model Description;
                                                              NOR=Modellbeskrivelse;
                                                              SVE=Modell] }
    { 128 ;   ;Type Code           ;Code20        ;TableRelation="Service Object Type".Code WHERE (Brand Code=FIELD(Brand Code),
                                                                                                   Model Code=FIELD(Model Code));
                                                   OnValidate=VAR
                                                                ServiceObjectType@1100528600 : Record 11072197;
                                                              BEGIN
                                                                IF "Type Code" <> xRec."Type Code" THEN
                                                                  IF ServiceObjectType.GET("Brand Code", "Model Code", "Type Code") THEN
                                                                    "Type Description" := ServiceObjectType.Description;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Type] }
    { 129 ;   ;Type Description    ;Text30        ;OnValidate=BEGIN
                                                                IF "Type Description" <> xRec."Type Description" THEN
                                                                  TESTFIELD("Type Code", '');
                                                              END;

                                                   CaptionML=[ENU=Type Description;
                                                              NOR=Typebeskrivelse;
                                                              SVE=Radtypbeskrivning] }
    { 140 ;   ;Comment             ;Text100       ;CaptionML=[ENU=Comment;
                                                              NOR=Kommentar;
                                                              SVE=Kommentar] }
    { 150 ;   ;Defect Code         ;Code10        ;TableRelation="NEN2767 Element Defect"."Defect Code" WHERE (Version Code=FIELD(NEN2767 Version Code),
                                                                                                               Element No.=FIELD(Element No.));
                                                   OnValidate=VAR
                                                                NEN2767Defect@1100528600 : Record 11128773;
                                                              BEGIN
                                                                IF "Defect Code" <> '' THEN BEGIN
                                                                  TESTFIELD("No Defects", FALSE);
                                                                  TESTFIELD(Degradation, FALSE);
                                                                  IF NEN2767Defect.GET("NEN2767 Version Code", "Defect Code") THEN BEGIN
                                                                    "Defect Description" := NEN2767Defect.Description;
                                                                    Interest := NEN2767Defect.Interest;
                                                                  END;
                                                                END ELSE
                                                                  Interest := Interest::" ";
                                                                DetermineScore;
                                                              END;

                                                   CaptionML=[ENU=Defect;
                                                              NOR=Defekt;
                                                              SVE=Trasig] }
    { 155 ;   ;Defect Position     ;Code10        ;CaptionML=ENU=Defect Position }
    { 160 ;   ;Defect Description  ;Text250       ;CaptionML=[ENU=Defect Description;
                                                              NOR=Defektbeskrivelse] }
    { 170 ;   ;Interest            ;Option        ;OnValidate=BEGIN
                                                                IF Interest <> Interest::" " THEN BEGIN
                                                                  TESTFIELD("No Defects", FALSE);
                                                                  TESTFIELD(Degradation, FALSE);
                                                                END ELSE
                                                                  VALIDATE(Intensity, Intensity::" ");
                                                                IF xRec.Interest <> Interest THEN
                                                                  TESTFIELD("Defect Code", '');
                                                              END;

                                                   CaptionML=[ENU=Interest;
                                                              NOR=Rente;
                                                              SVE=rÑnta];
                                                   OptionCaptionML=ENU="Severe,Serious,Minor, ";
                                                   OptionString=[Severe,Serious,Minor, ] }
    { 180 ;   ;Intensity           ;Option        ;OnValidate=BEGIN
                                                                IF Intensity <> Intensity::" " THEN BEGIN
                                                                  TESTFIELD("No Defects", FALSE);
                                                                  TESTFIELD(Degradation, FALSE);
                                                                END ELSE
                                                                  VALIDATE("Scale Percentage", 0);
                                                                DetermineScore;
                                                              END;

                                                   CaptionML=[ENU=Intensity;
                                                              NOR=Intensitet;
                                                              SVE=Intensitet];
                                                   OptionCaptionML=[ENU=" ,Initial,Advanced,Final";
                                                                    NOR=" ,Opprinnelig,Avansert,Endelig"];
                                                   OptionString=[ ,Initial,Advanced,Final] }
    { 190 ;   ;Scale Percentage    ;Decimal       ;OnValidate=VAR
                                                                NEN2767Management@1100528600 : Codeunit 11012863;
                                                              BEGIN
                                                                Scale := NEN2767Management.GetScaleByPercentage("Scale Percentage");
                                                                DetermineScore;
                                                                VALIDATE("Measure Quantity", Quantity * "Scale Percentage" / 100);
                                                              END;

                                                   CaptionML=ENU=Scale Percentage;
                                                   BlankZero=Yes }
    { 200 ;   ;Score (100%)        ;Option        ;CaptionML=ENU=Score (100%);
                                                   OptionCaptionML=[ENU=,1,2,3,4,5,6;
                                                                    NOR=,1,2,3,4,5,6;
                                                                    SVE=,1,2,3,4,5,6];
                                                   OptionString=,1,2,3,4,5,6;
                                                   Editable=No }
    { 210 ;   ;Correction Factor   ;Decimal       ;CaptionML=[ENU=Correction Factor;
                                                              NOR=Korreksjonsfaktor;
                                                              SVE=Korrigeringsfaktor];
                                                   Editable=No }
    { 220 ;   ;Scale Percentage (Correction);Decimal;
                                                   CaptionML=ENU=Scale Percentage (Correction);
                                                   Editable=No }
    { 230 ;   ;Score               ;Option        ;OnValidate=BEGIN
                                                                IF ("Starting Year (Needed)" = 0) AND (Score > 1) THEN
                                                                  "Starting Year (Needed)" := DATE2DMY(WORKDATE, 3);
                                                              END;

                                                   CaptionML=[ENU=Score;
                                                              NOR=Poengsum;
                                                              SVE=PoÑng];
                                                   OptionCaptionML=[ENU=,1,2,3,4,5,6;
                                                                    NOR=,1,2,3,4,5,6;
                                                                    SVE=,1,2,3,4,5,6];
                                                   OptionString=,1,2,3,4,5,6;
                                                   Editable=No }
    { 240 ;   ;Desired Score       ;Option        ;CaptionML=[ENU=Desired Score;
                                                              NOR=ùnsket poengsum];
                                                   OptionCaptionML=[ENU=" ,1,2,3,4,5,6";
                                                                    NOR=" ,1,2,3,4,5,6";
                                                                    SVE=" ,1,2,3,4,5,6"];
                                                   OptionString=[ ,1,2,3,4,5,6] }
    { 250 ;   ;Element Seq. No.    ;Integer       ;OnValidate=BEGIN
                                                                CopyDataFromSameLineWithSameObjectOrElement;
                                                              END;

                                                   CaptionML=ENU=Element Seq. No. }
    { 260 ;   ;Aggregation Correction Factor;Decimal;
                                                   CaptionML=ENU=Aggregation Correction Factor;
                                                   Editable=No }
    { 270 ;   ;Scale               ;Option        ;OnValidate=BEGIN
                                                                DetermineScore;
                                                              END;

                                                   CaptionML=[ENU=Scale;
                                                              NOR=Skala;
                                                              SVE=Skala];
                                                   OptionCaptionML=[ENU=" ,<2% (Incidental),2%-10% (Local),10%-30% (Regular),30%-70% (Significant),>70% (General)";
                                                                    NOR=" ,<2% (Tilfeldig),2%-10% (Lokal),10%-30% (Regelmessig),30%-70% (Betydelig),>70% (Generell)"];
                                                   OptionString=[ ,Incidental,Local,Regular,Significant,General];
                                                   Editable=No }
    { 280 ;   ;No Defects          ;Boolean       ;OnValidate=BEGIN
                                                                IF "No Defects" THEN BEGIN
                                                                //  VALIDATE("Scale Percentage", 0);
                                                                  Interest := Interest::" ";
                                                                  "Defect Code" := '';
                                                                  "Defect Description" := Text000;
                                                                  Intensity := Intensity::" ";
                                                                  DetermineScore;
                                                                END ELSE
                                                                  "Defect Description" := '';
                                                              END;

                                                   CaptionML=[ENU=No Defects;
                                                              NOR=Ingen defekter] }
    { 300 ;   ;NEN2767 Version Code;Code10        ;TableRelation="NEN2767 Version";
                                                   CaptionML=ENU=NEN2767 Version Code;
                                                   Editable=No }
    { 310 ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Open,Closed;
                                                                    SVE=ôppna,StÑngd];
                                                   OptionString=Open,Closed }
    { 500 ;   ;Measure Code        ;Code20        ;TableRelation="LTMP Maintenance Measure";
                                                   OnValidate=VAR
                                                                LTMPMaintenanceMeasure@1100528600 : Record 11128786;
                                                              BEGIN
                                                                IF "Measure Code" <> '' THEN BEGIN
                                                                  LTMPMaintenanceMeasure.GET("Measure Code");
                                                                  "Measure Description" := LTMPMaintenanceMeasure.Description;
                                                                  VALIDATE(Ratio, GetIndexedRatioByMeasureCode(WORKDATE));
                                                                  IF (LTMPMaintenanceMeasure."Close Defect") AND ("Defect Code" <> '') THEN
                                                                    VALIDATE(Status, Status::Closed);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Measure Code;
                                                   Description=500+ LTMP Fields }
    { 510 ;   ;Measure Description ;Text100       ;CaptionML=ENU=Measure Description }
    { 520 ;   ;Cycle (Years) (Needed);Integer     ;OnValidate=VAR
                                                                NEN2767LTMPSetup@1100528601 : Record 11128772;
                                                                EndYear@1100528600 : Integer;
                                                              BEGIN
                                                                IF "Cycle (Years) (Needed)" <> 0 THEN BEGIN
                                                                  IF "Ending Year (Needed)" = 0 THEN BEGIN
                                                                    NEN2767LTMPSetup.GET;
                                                                    IF FORMAT(NEN2767LTMPSetup."LTMP Planning Horizon") <> '' THEN BEGIN
                                                                      EndYear := DATE2DMY(CALCDATE(NEN2767LTMPSetup."LTMP Planning Horizon", TODAY), 3);
                                                                      VALIDATE("Ending Year (Needed)", EndYear);
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Cycle (Years) (Needed);
                                                   BlankZero=Yes }
    { 530 ;   ;Starting Year (Needed);Integer     ;OnValidate=BEGIN
                                                                VALIDATE("Ending Year (Needed)");
                                                              END;

                                                   CaptionML=ENU=Starting Year (Needed);
                                                   BlankZero=Yes }
    { 540 ;   ;Ending Year (Needed);Integer       ;OnValidate=BEGIN
                                                                IF "Ending Year (Needed)" > 0 THEN
                                                                  IF "Starting Year (Needed)" > "Ending Year (Needed)" THEN
                                                                    ERROR(Text001, FIELDCAPTION("Ending Year (Needed)"), FIELDCAPTION("Starting Year (Needed)"));
                                                              END;

                                                   CaptionML=ENU=Ending Year (Needed);
                                                   BlankZero=Yes }
    { 550 ;   ;Ratio               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Measure Quantity");
                                                              END;

                                                   CaptionML=[ENU=Ratio;
                                                              NOR=Ratio;
                                                              SVE=FîrhÜllande] }
    { 555 ;   ;Replacement Value per UOM;Decimal  ;OnValidate=BEGIN
                                                                IF "Replacement Value per UOM" <> xRec."Replacement Value per UOM" THEN
                                                                  "Replacement Value" := "Replacement Value per UOM" * Quantity;

                                                                UpdateOtherDataSameElementLines;
                                                              END;

                                                   CaptionML=ENU=Replacement Value per UOM }
    { 560 ;   ;Replacement Value   ;Decimal       ;OnValidate=BEGIN
                                                                IF "Replacement Value" <> xRec."Replacement Value" THEN
                                                                  IF Quantity <> 0 THEN
                                                                    "Replacement Value per UOM" := "Replacement Value" / Quantity
                                                                  ELSE
                                                                    "Replacement Value per UOM" := "Replacement Value";

                                                                UpdateOtherDataSameElementLines;
                                                              END;

                                                   CaptionML=[ENU=Replacement Value;
                                                              NOR=Erstatningsverdi;
                                                              SVE=ErsÑttningsvÑrde] }
    { 590 ;   ;Measure Quantity    ;Decimal       ;CaptionML=[ENU=Measure Quantity;
                                                              NOR=MÜlingsantall;
                                                              SVE=MÑtvÑrde, antal] }
    { 630 ;   ;Degradation         ;Boolean       ;OnValidate=BEGIN
                                                                IF Degradation THEN BEGIN
                                                                  TESTFIELD("No Defects", FALSE);
                                                                  TESTFIELD("Year of Manufacture");
                                                                  TESTFIELD("Theoretical Life Span");
                                                                  Interest := Interest::" ";
                                                                  "Defect Code" := '';
                                                                  "Defect Description" := Text002;
                                                                  Intensity := Intensity::" ";
                                                                END ELSE
                                                                  "Defect Description" := '';
                                                                DetermineScore;
                                                              END;

                                                   CaptionML=ENU=Degradation }
    { 640 ;   ;Theoretical Life Span;DateFormula  ;OnValidate=BEGIN
                                                                IF (FORMAT("Theoretical Life Span") = '') AND Degradation THEN
                                                                  MESSAGE(Text003, FIELDCAPTION("Theoretical Life Span"), FIELDCAPTION(Degradation));
                                                                UpdateOtherDataSameElementLines;
                                                                DetermineScore;
                                                              END;

                                                   CaptionML=[ENU=Theoretical Life Span;
                                                              NOR=Teoretisk levetid;
                                                              SVE=Teoretisk livslÑngd] }
    { 650 ;   ;Price Index Code    ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("NEN2767/LTMP Header"."Price Index Code" WHERE (Entry ID=FIELD(Header Entry ID)));
                                                   CaptionML=ENU=Price Index Code;
                                                   Editable=No }
    { 660 ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order".No.;
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 665 ;   ;Service Order Status;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order".Status WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=[ENU=Service Order Status;
                                                              NOR=Serviceordrestatus;
                                                              SVE=Serviceorderstatus];
                                                   OptionCaptionML=[ENU=Open,,In Process,Technical Finished,Administrative Finished,Finished,Cancelled;
                                                                    NOR=èpne,,Under behandling,Teknisk ferdig,Administrativt ferdig,Ferdig,Avbrutt;
                                                                    SVE=ôppna,,Under behandling,Tekniskt fÑrdig,Administrativt fÑrdig,FÑrdig,Avbrutet];
                                                   OptionString=Open,,In Process,Technical Finished,Administrative Finished,Finished,Cancelled;
                                                   Editable=No }
    { 670 ;   ;Try-out Quote       ;Code20        ;TableRelation="Try-out Quote";
                                                   CaptionML=[ENU=Try-out Quote;
                                                              NOR=Tilbudsforespõrsel;
                                                              SVE=AnbudsfîrfrÜgan] }
    { 675 ;   ;Try-out Quote Status;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Try-out Quote".Status WHERE (Code=FIELD(Try-out Quote)));
                                                   CaptionML=ENU=Try-out Quote Status;
                                                   Editable=No }
    { 680 ;   ;Created by Service Order No.;Code20;TableRelation="Service Order";
                                                   OnValidate=VAR
                                                                ServiceOrder@1100528600 : Record 11012823;
                                                                ObjectModificationMgt@1100528601 : Codeunit 11012835;
                                                              BEGIN
                                                                IF "Created by Service Order No." <> '' THEN BEGIN
                                                                  ServiceOrder.GET("Created by Service Order No.");
                                                                  CALCFIELDS("Created by SO Source Type");
                                                                  CASE "Created by SO Source Type" OF
                                                                    "Created by SO Source Type"::Call:
                                                                      "Created by SO Source Code" := ServiceOrder."Call No.";
                                                                    "Created by SO Source Type"::Contract:
                                                                      "Created by SO Source Code" := ServiceOrder."Service Contract No.";
                                                                    "Created by SO Source Type"::Modification:
                                                                      "Created by SO Source Code" := ObjectModificationMgt.GetModificationNoByServiceOrderNo("Created by Service Order No.");
                                                                  END;
                                                                END ELSE
                                                                  "Created by SO Source Code" := '';
                                                              END;

                                                   CaptionML=ENU=Created by Service Order No.;
                                                   Editable=No }
    { 683 ;   ;Created by SO Status;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order".Status WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=ENU=Created by SO Status;
                                                   OptionCaptionML=[ENU=Open,,In Process,Technical Finished,Administrative Finished,Finished,Cancelled;
                                                                    NOR=èpne,,Under behandling,Teknisk ferdig,Administrativt ferdig,Ferdig,Avbrutt;
                                                                    SVE=ôppna,,Under behandling,Tekniskt fÑrdig,Administrativt fÑrdig,FÑrdig,Avbrutet];
                                                   OptionString=Open,,In Process,Technical Finished,Administrative Finished,Finished,Cancelled;
                                                   Editable=No }
    { 685 ;   ;Created by SO Source Type;Option   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order"."Source Type" WHERE (No.=FIELD(Created by Service Order No.)));
                                                   CaptionML=ENU=Created by SO Source Type;
                                                   OptionCaptionML=[ENU=Call,Contract,Direct,Estimate,,,Modification,Replacement;
                                                                    NOR=Samtale,Kontrakt,Direkte,Kalkyle,,,Endring,Erstatning;
                                                                    SVE=Samtal,Kontrakt,Direkt,Kalkyl,,,éndring,ErsÑttning];
                                                   OptionString=Call,Contract,Direct,Estimate,,,Modification,Replacement;
                                                   Editable=No }
    { 687 ;   ;Created by SO Source Code;Code20   ;CaptionML=ENU=Created by SO Source Code;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Header Entry ID,Structure Line Code,Line No.;
                                                   Clustered=Yes }
    {    ;Object No.,Status                        }
    {    ;Service Order No.                        }
    {    ;Try-out Quote                            }
    {    ;Location Type,Location Code,Status       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=No Defects Detected';
      Text001@1100528601 : TextConst 'ENU=%1 must be after %2;NOR=%1 mÜ vëre etter %2.;SVE=%1 mÜste vara efter %2.';
      Text002@1100528602 : TextConst 'ENU=Degradation';
      Text003@1100528603 : TextConst 'ENU=Score cannot be determined with %1 is 0 and %2';
      Text004@1100528604 : TextConst 'ENU=Selected Lines are not part of same element';
      Text005@1100528605 : TextConst 'ENU=First Select Lines;NOR=Velg rader fõrst;SVE=VÑlj rad(er) fîrst';

    LOCAL PROCEDURE DetermineScore100@1100528600();
    VAR
      NEN2767Management@1100528600 : Codeunit 11012863;
    BEGIN
      IF NOT MultipleDefects THEN
        "Score (100%)" := 0
      ELSE
        IF NOT Degradation THEN
          VALIDATE("Score (100%)", NEN2767Management.GetConditionScore("NEN2767 Version Code", Interest, Intensity, Scale::General))
        ELSE
          VALIDATE("Score (100%)", NEN2767Management.GetConditionByDegradation("Theoretical Life Span", DetermineAgeByYearOfManufacture("Year of Manufacture")));
    END;

    PROCEDURE DetermineScore@1100528606();
    VAR
      NEN2767Management@1100528600 : Codeunit 11012863;
    BEGIN
      DetermineScore100;
      "Correction Factor" := 0;
      "Scale Percentage (Correction)" := 0;
      "Aggregation Correction Factor" := 0;
      IF MultipleDefects THEN BEGIN
        CASE TRUE OF
          DefectsOnSamePosition:
            VALIDATE(Score, GetWorstScore);
          DefectsSameInterestAndIntensity:
            VALIDATE(Score, GetScoreByCumulatedScale);
          ELSE
            DetermineCorrectionFactor;
            "Scale Percentage (Correction)" := "Scale Percentage" * "Correction Factor";
            DetermineAggregationCorrectionFactor;
            VALIDATE(Score, NEN2767Management.GetScoreByCorrectionFactor("Aggregation Correction Factor"));
        END;
      END ELSE BEGIN
        IF "No Defects" THEN
          VALIDATE(Score, Score::"1")
        ELSE
          IF NOT Degradation THEN
            VALIDATE(Score, NEN2767Management.GetConditionScore("NEN2767 Version Code", Interest, Intensity, Scale))
          ELSE
            VALIDATE(Score, NEN2767Management.GetConditionByDegradation("Theoretical Life Span", DetermineAgeByYearOfManufacture("Year of Manufacture")));
      END;
    END;

    LOCAL PROCEDURE DetermineCorrectionFactor@1100528601();
    VAR
      NEN2767Management@1100528600 : Codeunit 11012863;
    BEGIN
      "Correction Factor" := NEN2767Management.GetCorrectionFactorByScore("Score (100%)")
    END;

    PROCEDURE MultipleDefects@1100528602() : Boolean;
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
    BEGIN
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.SETFILTER("Line No.", '<>%1', "Line No.");
      EXIT(NOT NEN2767LTMPLine.ISEMPTY);
    END;

    PROCEDURE DetermineAggregationCorrectionFactor@1100528604();
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
    BEGIN
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.CALCSUMS("Scale Percentage", "Scale Percentage (Correction)");
      "Aggregation Correction Factor" := (NEN2767LTMPLine."Scale Percentage (Correction)" + (100 - NEN2767LTMPLine."Scale Percentage")) / 100;
    END;

    PROCEDURE Calculate@1100528608();
    BEGIN
      DetermineScore;
      MODIFY(TRUE);
    END;

    PROCEDURE GetNoOfDefectPictures@1100528605() : Integer;
    VAR
      NEN2767Management@1100528604 : Codeunit 11012863;
      DocumentProperties@1100528602 : Record 11012746;
    BEGIN
      IF "Line No." = 0 THEN
        EXIT(0);
      NEN2767Management.GetPictureDocumentProperties(Rec, 1, DocumentProperties);
      EXIT(DocumentProperties.COUNT)
    END;

    PROCEDURE ShowDefectDocuments@1100528603();
    VAR
      DocumentProperties@1100528600 : Record 11012746;
    BEGIN
      ShowAndSelectDefectDocuments(DocumentProperties);
    END;

    PROCEDURE ShowAndSelectDefectDocuments@1100528634(IDocumentProperties@1100528603 : Record 11012746);
    VAR
      DocumentProperties@1100528600 : Record 11012746;
      NEN2767Management@1100528601 : Codeunit 11012863;
      NEN2767DefectPictureList@1100528602 : Page 11128819;
    BEGIN
      NEN2767Management.GetPictureDocumentProperties(Rec, 1, DocumentProperties);
      NEN2767DefectPictureList.SETTABLEVIEW(DocumentProperties);
      NEN2767DefectPictureList.SetSource(Rec);
      IF IDocumentProperties."No." <> '' THEN
        NEN2767DefectPictureList.SETRECORD(IDocumentProperties);
      NEN2767DefectPictureList.RUNMODAL;
    END;

    PROCEDURE ShowElementDocument@1100528611();
    VAR
      NEN2767Management@1100528601 : Codeunit 11012863;
      DocumentProperties@1100528600 : Record 11012746;
    BEGIN
      NEN2767Management.GetPictureDocumentProperties(Rec, 0, DocumentProperties);
      PAGE.RUNMODAL(0, DocumentProperties);
    END;

    PROCEDURE SyncElementPicture@1100528607();
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
      DocumentProperties@1100528603 : Record 11012746;
      DocumentLink@1100528602 : Record 11012747;
      ToDocumentLink@1100528604 : Record 11012747;
      NEN2767Management@1100528601 : Codeunit 11012863;
      SeqNo@1100528605 : Integer;
    BEGIN
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.SETFILTER("Line No.", '<>%1', "Line No.");
      IF NEN2767LTMPLine.FINDSET(TRUE) THEN
        REPEAT
          NEN2767Management.GetPictureDocumentProperties(NEN2767LTMPLine, 0, DocumentProperties);
          IF DocumentProperties.FINDFIRST THEN
            REPEAT
              DocumentLink.SETRANGE("Document No.", DocumentProperties."No.");
              DocumentLink.SETRANGE("Record ID", NEN2767LTMPLine.RECORDID);
              DocumentLink.DELETEALL(TRUE);
            UNTIL DocumentProperties.NEXT = 0;

          NEN2767Management.GetPictureDocumentProperties(Rec, 0, DocumentProperties);
          IF DocumentProperties.FINDFIRST THEN
            REPEAT
              DocumentLink.RESET;
              DocumentLink.SETRANGE("Document No.", DocumentProperties."No.");
              IF DocumentLink.FINDLAST THEN
                SeqNo := DocumentLink."Seq. No."
              ELSE
                SeqNo := 0;
              DocumentLink.SETRANGE("Record ID", RECORDID);
              IF DocumentLink.FINDSET THEN
                REPEAT
                  SeqNo += 10000;
                  ToDocumentLink := DocumentLink;
                  ToDocumentLink."Seq. No." := SeqNo;
                  ToDocumentLink."Record ID" := NEN2767LTMPLine.RECORDID;
                  ToDocumentLink.INSERT(TRUE);
                UNTIL DocumentLink.NEXT = 0;
            UNTIL DocumentProperties.NEXT = 0;
        UNTIL NEN2767LTMPLine.NEXT = 0;
    END;

    PROCEDURE GetCostAmountByYear@1100528609(IYear@1100528600 : Integer) : Decimal;
    VAR
      LTMPPriceIndexHeader@1100528601 : Record 11128794;
      IndexedRatio@1100528603 : Decimal;
      CurrentRatio@1100528604 : Decimal;
    BEGIN
      CALCFIELDS("Price Index Code", "Measurement Date");
      IF (IYear = 0) OR (IYear < "Starting Year (Needed)") OR ((IYear > "Ending Year (Needed)") AND ("Ending Year (Needed)" > 0)) THEN
        EXIT(0);
      IF ("Cycle (Years) (Needed)" > 0) AND ("Ending Year (Needed)" > "Starting Year (Needed)") THEN BEGIN
        IF (IYear - "Starting Year (Needed)") MOD "Cycle (Years) (Needed)" = 0 THEN
          CurrentRatio := Ratio;
      END ELSE
        IF IYear = "Starting Year (Needed)" THEN
          CurrentRatio := Ratio;
      IF LTMPPriceIndexHeader.GET("Price Index Code") THEN
        IndexedRatio := LTMPPriceIndexHeader.GetIndexedAmount(DATE2DMY("Measurement Date", 3), CurrentRatio, IYear)
      ELSE
        IndexedRatio := CurrentRatio;
      EXIT(IndexedRatio * "Measure Quantity");
    END;

    LOCAL PROCEDURE GetIndexedRatioByMeasureCode@1100528631(ISeekDate@1100528600 : Date) CurrRatio : Decimal;
    VAR
      LTMPMaintenanceMeasure@1100528601 : Record 11128786;
      LTMPPriceIndexHeader@1100528602 : Record 11128794;
      FoundDate@1100528603 : Date;
      SeekYear@1100528604 : Integer;
      FoundYear@1100528605 : Integer;
    BEGIN
      IF LTMPMaintenanceMeasure.GET("Measure Code") THEN BEGIN
        CurrRatio := LTMPMaintenanceMeasure.GetRatio(ISeekDate, FoundDate);
        CALCFIELDS("Price Index Code");
        IF LTMPPriceIndexHeader.GET("Price Index Code") AND (FoundDate <> 0D) THEN BEGIN
          FoundYear := DATE2DMY(FoundDate, 3);
          SeekYear := DATE2DMY(ISeekDate, 3);
          CurrRatio := LTMPPriceIndexHeader.GetIndexedAmount(FoundYear, CurrRatio, SeekYear);
        END;
      END;
    END;

    LOCAL PROCEDURE DetermineAgeByYearOfManufacture@1100528612(IYearOfManufacture@1100528600 : Integer) Age : Integer;
    BEGIN
      IF IYearOfManufacture = 0 THEN
        EXIT(0);
      Age := DATE2DMY(TODAY, 3) - IYearOfManufacture;
      IF Age < 0 THEN
        Age := 0;
    END;

    PROCEDURE CreateRemainingLine@1100528613();
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
      CurrentElementLineNo@1100528602 : Integer;
      NewLineNo@1100528601 : Integer;
      ScalePercentage@1100528603 : Decimal;
    BEGIN
      CurrentElementLineNo := "Line No.";
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.SETFILTER("Line No.", '>%1', "Line No.");
      IF NEN2767LTMPLine.FINDSET THEN
        CurrentElementLineNo := NEN2767LTMPLine."Line No.";

      NEN2767LTMPLine.RESET;
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETFILTER("Line No.", '>%1', CurrentElementLineNo);
      IF NEN2767LTMPLine.FINDSET THEN BEGIN
        IF NEN2767LTMPLine."Line No." - CurrentElementLineNo > 1 THEN
          NewLineNo := CurrentElementLineNo + ROUND((NEN2767LTMPLine."Line No." - CurrentElementLineNo) / 2, 1)
        ELSE BEGIN
          NEN2767LTMPLine.RESET;
          NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
          IF NEN2767LTMPLine.FINDLAST THEN
            NewLineNo := NEN2767LTMPLine."Line No." + 10000;
        END;
      END ELSE
        NewLineNo := CurrentElementLineNo + 10000;

      NEN2767LTMPLine.RESET;
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.CALCSUMS("Scale Percentage");
      ScalePercentage := NEN2767LTMPLine."Scale Percentage";

      IF NEN2767LTMPLine."Scale Percentage" < 100 THEN BEGIN
        NEN2767LTMPLine.INIT;
        NEN2767LTMPLine.VALIDATE("Header Entry ID", "Header Entry ID");
        NEN2767LTMPLine."Line No." := NewLineNo;
        NEN2767LTMPLine."Object No." := "Object No.";
        NEN2767LTMPLine."Object Seq. No." := "Object Seq. No.";
        NEN2767LTMPLine."Building Part Code" := "Building Part Code";
        NEN2767LTMPLine."Element No." := "Element No.";
        NEN2767LTMPLine."Element Seq. No." := "Element Seq. No.";
        IF NEN2767LTMPLine."Object No." <> '' THEN
          NEN2767LTMPLine.DetermineStructureLineCode;
        NEN2767LTMPLine.INSERT(TRUE);
        UpdateOtherDataSameElementLines;
        NEN2767LTMPLine.FIND('=');
        NEN2767LTMPLine.VALIDATE("Scale Percentage", 100 - ScalePercentage);
        NEN2767LTMPLine.VALIDATE("No Defects", TRUE);
        NEN2767LTMPLine.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE CreateScenarioLines@1100528614();
    VAR
      LTMPScenarioHeader@1100528601 : Record 11128791;
      LTMPScenarioLine@1100528600 : Record 11128792;
    BEGIN
      LTMPScenarioHeader.SETRANGE("Header Entry ID", "Header Entry ID");
      IF NOT LTMPScenarioHeader.FINDSET THEN
        EXIT;
      REPEAT
        IF NOT LTMPScenarioLine.GET(
          "Header Entry ID", "Structure Line Code", LTMPScenarioHeader."Scenario No.", LTMPScenarioHeader."Version No.", "Line No.")
        THEN BEGIN
          LTMPScenarioLine."Header Entry ID" := LTMPScenarioHeader."Header Entry ID";
          LTMPScenarioLine."Structure Line Code" := "Structure Line Code";
          LTMPScenarioLine."Scenario No." := LTMPScenarioHeader."Scenario No.";
          LTMPScenarioLine."Version No." := LTMPScenarioHeader."Version No.";
          LTMPScenarioLine."Line No." := "Line No.";
          LTMPScenarioLine.FillFromNEN2767LTMPLine(Rec);
          LTMPScenarioLine.INSERT(TRUE);
        END;
      UNTIL LTMPScenarioHeader.NEXT = 0;
    END;

    LOCAL PROCEDURE DeleteScenarioLines@1100528616();
    VAR
      LTMPScenarioLine@1100528600 : Record 11128792;
    BEGIN
      LTMPScenarioLine.SETRANGE("Header Entry ID", "Header Entry ID");
      LTMPScenarioLine.SETRANGE("Line No.", "Line No.");
      LTMPScenarioLine.DELETEALL(TRUE);
    END;

    LOCAL PROCEDURE DefectsOnSamePosition@1100528610() : Boolean;
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
    BEGIN
      IF NOT MultipleDefects THEN
        EXIT(FALSE);
      IF "Defect Position" = '' THEN
        EXIT;
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.SETFILTER("Defect Position", '<>%1', "Defect Position");
      EXIT(NEN2767LTMPLine.ISEMPTY);
    END;

    LOCAL PROCEDURE DefectsSameInterestAndIntensity@1100528617() : Boolean;
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
    BEGIN
      IF NOT MultipleDefects THEN
        EXIT(FALSE);
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.SETFILTER(Interest, '<>%1', Interest);
      IF NOT NEN2767LTMPLine.ISEMPTY THEN
        EXIT(FALSE);
      NEN2767LTMPLine.SETRANGE(Interest);
      NEN2767LTMPLine.SETFILTER(Intensity, '<>%1', Intensity);
      IF NEN2767LTMPLine.ISEMPTY THEN
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetWorstScore@1100528624() : Integer;
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
      NEN2767Management@1100528601 : Codeunit 11012863;
      LineScore@1100528602 : Integer;
      WorstScore@1100528603 : Integer;
    BEGIN
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.SETRANGE("Defect Position", "Defect Position");
      IF NEN2767LTMPLine.FINDSET THEN
        REPEAT
          LineScore := NEN2767Management.GetConditionScore(NEN2767LTMPLine."NEN2767 Version Code", NEN2767LTMPLine.Interest, NEN2767LTMPLine.Intensity, NEN2767LTMPLine.Scale);
          IF WorstScore < LineScore THEN
            WorstScore := LineScore;
        UNTIL NEN2767LTMPLine.NEXT = 0;
      EXIT(WorstScore);
    END;

    LOCAL PROCEDURE GetScoreByCumulatedScale@1100528625() : Integer;
    VAR
      NEN2767LTMPLine@1100528601 : Record 11128785;
      NEN2767Management@1100528602 : Codeunit 11012863;
      CumulatedScale@1100528600 : Integer;
    BEGIN
      NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
      NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
      NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
      NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
      NEN2767LTMPLine.CALCSUMS("Scale Percentage");
      CumulatedScale := NEN2767Management.GetScaleByPercentage(NEN2767LTMPLine."Scale Percentage");
      EXIT(NEN2767Management.GetConditionScore("NEN2767 Version Code", Interest, Intensity, CumulatedScale));
    END;

    LOCAL PROCEDURE UpdateOtherDataSameElementLines@1100528621();
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
    BEGIN
      IF MultipleDefects THEN BEGIN
        NEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
        NEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
        NEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
        NEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
        NEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
        NEN2767LTMPLine.SETFILTER("Line No.", '<>%1', "Line No.");
        IF NEN2767LTMPLine.FINDSET THEN BEGIN
          REPEAT
            NEN2767LTMPLine.Description := Description;
            NEN2767LTMPLine."Replacement Value" := "Replacement Value";
            NEN2767LTMPLine."Replacement Value per UOM" := "Replacement Value per UOM";
            NEN2767LTMPLine."Year of Manufacture" := "Year of Manufacture";
            NEN2767LTMPLine.Quantity := Quantity;
            NEN2767LTMPLine."Unit of Measure" := "Unit of Measure";
            NEN2767LTMPLine."Theoretical Life Span" := "Theoretical Life Span";
            NEN2767LTMPLine."Brand Code" := "Brand Code";
            NEN2767LTMPLine."Model Code" := "Model Code";
            NEN2767LTMPLine."Type Code" := "Type Code";
            NEN2767LTMPLine."Desired Score" := "Desired Score";
            NEN2767LTMPLine.MODIFY(TRUE);
          UNTIL NEN2767LTMPLine.NEXT = 0;
          SyncElementPicture;
        END;
      END;
    END;

    LOCAL PROCEDURE CopyDataFromSameLineWithSameObjectOrElement@1100528627() : Boolean;
    VAR
      NEN2767LTMPLine@1100528600 : Record 11128785;
    BEGIN
      IF MultipleDefects THEN BEGIN
        IF GetSameObjectOrElementLine(NEN2767LTMPLine) THEN BEGIN
          Description := NEN2767LTMPLine.Description;
          "Replacement Value" := NEN2767LTMPLine."Replacement Value";
          "Replacement Value per UOM" := NEN2767LTMPLine."Replacement Value per UOM";
          "Year of Manufacture" := NEN2767LTMPLine."Year of Manufacture";
          Quantity := NEN2767LTMPLine.Quantity;
          "Unit of Measure" := NEN2767LTMPLine."Unit of Measure";
          "Theoretical Life Span" := NEN2767LTMPLine."Theoretical Life Span";
          "Brand Code" := NEN2767LTMPLine."Brand Code";
          "Brand Description" := NEN2767LTMPLine."Brand Description";
          "Model Code" := NEN2767LTMPLine."Model Code";
          "Model Description" := NEN2767LTMPLine."Model Description";
          "Type Code" := NEN2767LTMPLine."Type Code";
          "Type Description" := NEN2767LTMPLine."Type Description";
          "Desired Score" := NEN2767LTMPLine."Desired Score";
          IF MODIFY THEN;
          NEN2767LTMPLine.SyncElementPicture;
        END;
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE);
    END;

    LOCAL PROCEDURE GetSameObjectOrElementLine@1100528630(VAR ONEN2767LTMPLine@1100528600 : Record 11128785) : Boolean;
    BEGIN
      IF MultipleDefects THEN BEGIN
        ONEN2767LTMPLine.RESET;
        ONEN2767LTMPLine.SETRANGE("Header Entry ID", "Header Entry ID");
        CASE TRUE OF
          ("Location Type" = "Location Type"::"Service Location") AND ("Object No." <> ''):
            ONEN2767LTMPLine.SETRANGE("Object No.", "Object No.");
          ("Location Type" = "Location Type"::"Survey Location") AND ("Object Seq. No." <> 0):
            ONEN2767LTMPLine.SETRANGE("Object Seq. No.", "Object Seq. No.");
        ELSE
          ONEN2767LTMPLine.SETRANGE("Element No.", "Element No.");
          ONEN2767LTMPLine.SETRANGE("Element Seq. No.", "Element Seq. No.");
        END;
        IF NOT ONEN2767LTMPLine.FINDFIRST THEN
          CLEAR(ONEN2767LTMPLine)
        ELSE
          EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE SetOnSamePosition@1100528615(VAR IONEN2767LTMPLine@1100528600 : Record 11128785);
    VAR
      NEN2767LTMPLine@1100528601 : Record 11128785;
      LastUsedDefectPositionCode@1100528602 : Code[10];
      NewDefectPositionCode@1100528603 : Code[20];
    BEGIN
      IF IONEN2767LTMPLine.ISEMPTY THEN
        ERROR(Text005);
      IF NOT SelectedLinesAreSameElement(IONEN2767LTMPLine) THEN
        ERROR(Text004);
      IONEN2767LTMPLine.SETFILTER("Defect Position", '<>%1', '');
      IF IONEN2767LTMPLine.FINDFIRST THEN BEGIN
        IONEN2767LTMPLine.SETFILTER("Defect Position", '<>%1&<>%2', '', IONEN2767LTMPLine."Defect Position");
        IF IONEN2767LTMPLine.ISEMPTY THEN
          LastUsedDefectPositionCode := IONEN2767LTMPLine."Defect Position";
      END;
      IONEN2767LTMPLine.SETRANGE("Defect Position");

      IF LastUsedDefectPositionCode = '' THEN BEGIN
        IF IONEN2767LTMPLine.FINDFIRST THEN BEGIN
          NEN2767LTMPLine.SETRANGE("Header Entry ID", IONEN2767LTMPLine."Header Entry ID");
          NEN2767LTMPLine.SETRANGE("Object No.", IONEN2767LTMPLine."Object No.");
          NEN2767LTMPLine.SETRANGE("Object Seq. No.", IONEN2767LTMPLine."Object Seq. No.");
          NEN2767LTMPLine.SETRANGE("Element No.", IONEN2767LTMPLine."Element No.");
          NEN2767LTMPLine.SETRANGE("Element Seq. No.", IONEN2767LTMPLine."Element Seq. No.");
          NEN2767LTMPLine.SETFILTER("Defect Position", '<>%1', '');
          IF NEN2767LTMPLine.FINDSET THEN
            REPEAT
              IF (LastUsedDefectPositionCode = '') OR (LastUsedDefectPositionCode < NEN2767LTMPLine."Defect Position") THEN
                LastUsedDefectPositionCode := NEN2767LTMPLine."Defect Position";
            UNTIL NEN2767LTMPLine.NEXT = 0;
        END;
        IF LastUsedDefectPositionCode <> '' THEN
          LastUsedDefectPositionCode := INCSTR(LastUsedDefectPositionCode);
      END;

      IF LastUsedDefectPositionCode <> '' THEN
        NewDefectPositionCode := LastUsedDefectPositionCode
      ELSE
        NewDefectPositionCode := '001';
      IONEN2767LTMPLine.MODIFYALL("Defect Position", NewDefectPositionCode);
    END;

    LOCAL PROCEDURE SelectedLinesAreSameElement@1100528619(VAR INEN2767LTMPLine@1100528600 : Record 11128785) : Boolean;
    VAR
      NEN2767LTMPLine@1100528601 : Record 11128785;
    BEGIN
      IF INEN2767LTMPLine.FINDFIRST THEN;
      NEN2767LTMPLine.COPY(INEN2767LTMPLine);
      NEN2767LTMPLine.SETRANGE("Element No.", INEN2767LTMPLine."Element No.");
      NEN2767LTMPLine.SETRANGE("Element Seq. No.", INEN2767LTMPLine."Element Seq. No.");
      EXIT(NEN2767LTMPLine.COUNT = INEN2767LTMPLine.COUNT);
    END;

    LOCAL PROCEDURE FillNEN2767VersionCode@1100528618();
    VAR
      NEN2767LTMPHeader@1100528600 : Record 11128784;
    BEGIN
      IF NEN2767LTMPHeader.GET("Header Entry ID") THEN BEGIN
        NEN2767LTMPHeader.TESTFIELD("NEN2767 Version Code");
        VALIDATE("NEN2767 Version Code", NEN2767LTMPHeader."NEN2767 Version Code");
      END;
    END;

    LOCAL PROCEDURE GetHeader@1100528620(VAR ONEN2767LTMPHeader@1100528600 : Record 11128784);
    BEGIN
      ONEN2767LTMPHeader.GET("Header Entry ID");
    END;

    LOCAL PROCEDURE UpdateStructure@1100528626();
    VAR
      ServiceLocStructureLine@1100528602 : Record 11071720;
      ServiceSurveyStructureLine@1100528601 : Record 11071806;
      NEN2767LTMPHeader@1100528600 : Record 11128784;
    BEGIN
      IF "Structure Line Code" = '' THEN
        EXIT;
      CASE "Location Type" OF
        "Location Type"::"Service Location":
          IF "Object No." <> '' THEN
            IF ServiceLocStructureLine.GET("Location Code", "Structure No.", xRec."Structure Line Code", "Object No.") THEN
              ServiceLocStructureLine.DELETE(TRUE);
        "Location Type"::"Survey Location":
          IF "Object Seq. No." <> 0 THEN BEGIN
            GetHeader(NEN2767LTMPHeader);
            ServiceSurveyStructureLine.SETRANGE("Survey No.", NEN2767LTMPHeader."Survey No.");
            ServiceSurveyStructureLine.SETRANGE("Location Code", "Location Code");
            ServiceSurveyStructureLine.SETRANGE("Structure No.", NEN2767LTMPHeader."Structure No.");
            ServiceSurveyStructureLine.SETRANGE(Code, xRec."Structure Line Code");
            ServiceSurveyStructureLine.SETRANGE("Seq. No.", "Object Seq. No.");
            ServiceSurveyStructureLine.DELETEALL(TRUE);
          END;
      END;
      CheckAndAddToStructure;
    END;

    LOCAL PROCEDURE CheckAndAddToStructure@1100528622();
    VAR
      ServiceLocStructureLine@1100528600 : Record 11071720;
      ServiceSurveyStructureLine@1100528601 : Record 11071806;
      NEN2767LTMPHeader@1100528602 : Record 11128784;
      LineNo@1100528603 : Integer;
    BEGIN
      IF "Structure Line Code" = '' THEN
        EXIT;
      CASE "Location Type" OF
        "Location Type"::"Service Location":
          BEGIN
            ServiceLocStructureLine.SETRANGE("Service Location No.", "Location Code");
            ServiceLocStructureLine.SETRANGE("Structure No.", "Structure No.");
            ServiceLocStructureLine.SETRANGE("No.", "Structure Line Code");
            ServiceLocStructureLine.SETRANGE("Service Object No.", "Object No.");
            IF ServiceLocStructureLine.ISEMPTY THEN BEGIN
              CLEAR(ServiceLocStructureLine);
              ServiceLocStructureLine.VALIDATE("Service Location No.", "Location Code");
              ServiceLocStructureLine.VALIDATE("Structure No.", "Structure No.");
              ServiceLocStructureLine.VALIDATE("No.", "Structure Line Code");
              ServiceLocStructureLine.VALIDATE("Service Object No.", "Object No.");
              ServiceLocStructureLine.INSERT(TRUE);
            END;
          END;
        "Location Type"::"Survey Location":
          BEGIN
            GetHeader(NEN2767LTMPHeader);
            ServiceSurveyStructureLine.SETRANGE("Survey No.", NEN2767LTMPHeader."Survey No.");
            ServiceSurveyStructureLine.SETRANGE("Location Code", "Location Code");
            ServiceSurveyStructureLine.SETRANGE("Structure No.", NEN2767LTMPHeader."Structure No.");
            ServiceSurveyStructureLine.SETRANGE(Code, "Structure Line Code");
            IF ServiceSurveyStructureLine.FINDLAST THEN
              LineNo := ServiceSurveyStructureLine."Line No.";
            LineNo += 10000;
            ServiceSurveyStructureLine.SETRANGE("Seq. No.", "Object Seq. No.");
            IF ServiceSurveyStructureLine.ISEMPTY THEN BEGIN
              ServiceSurveyStructureLine."Survey No." := NEN2767LTMPHeader."Survey No.";
              ServiceSurveyStructureLine."Location Code" := "Location Code";
              ServiceSurveyStructureLine."Structure No." := NEN2767LTMPHeader."Structure No.";
              ServiceSurveyStructureLine.Code := "Structure Line Code";
              ServiceSurveyStructureLine."Line No." := LineNo;
              ServiceSurveyStructureLine."Seq. No." := "Object Seq. No.";
              ServiceSurveyStructureLine."Object Line" := TRUE;
              ServiceSurveyStructureLine.INSERT(TRUE);
            END;
          END;
      END;
    END;

    PROCEDURE DetermineStructureLineCode@1100528623();
    VAR
      ServiceLocStructureLine@1100528601 : Record 11071720;
      ServiceSurveyStructureLine@1100528600 : Record 11071806;
      NEN2767LTMPHeader@1100528602 : Record 11128784;
    BEGIN
      CASE "Location Type" OF
        "Location Type"::"Service Location":
          BEGIN
            ServiceLocStructureLine.SETRANGE("Service Location No.", "Location Code");
            ServiceLocStructureLine.SETRANGE("Structure No.", "Structure No.");
            ServiceLocStructureLine.SETRANGE("Service Object No.", "Object No.");
            ServiceLocStructureLine.SETRANGE("Object Line", TRUE);
            IF ServiceLocStructureLine.FINDFIRST THEN
              "Structure Line Code" := ServiceLocStructureLine."No.";
          END;
        "Location Type"::"Survey Location":
          BEGIN
            GetHeader(NEN2767LTMPHeader);
            ServiceSurveyStructureLine.SETRANGE("Survey No.", NEN2767LTMPHeader."Survey No.");
            ServiceSurveyStructureLine.SETRANGE("Location Code", "Location Code");
            ServiceSurveyStructureLine.SETRANGE("Structure No.", NEN2767LTMPHeader."Structure No.");
            ServiceSurveyStructureLine.SETRANGE("Seq. No.", "Object Seq. No.");
            ServiceSurveyStructureLine.SETRANGE("Object Line", TRUE);
            IF ServiceSurveyStructureLine.FINDFIRST THEN
              "Structure Line Code" := ServiceSurveyStructureLine.Code;
          END;
      END;
    END;

    PROCEDURE GetDocumentLinks@1100528632(VAR ODocumentLink@1100528600 : Record 11012747);
    BEGIN
      ODocumentLink.RESET;
      ODocumentLink.SETRANGE("Internal Company", COMPANYNAME);
      ODocumentLink.SETRANGE("Record ID", RECORDID);
    END;

    LOCAL PROCEDURE DeleteDocumentLinks@1100528628();
    VAR
      DocumentLink@1100528600 : Record 11012747;
    BEGIN
      GetDocumentLinks(DocumentLink);
      DocumentLink.DELETEALL(TRUE);
    END;

    LOCAL PROCEDURE CopyServiceObjectPictureToElement@1100528629();
    VAR
      ServiceObject@1100528600 : Record 11071691;
      DocumentProperties@1100528605 : Record 11012746;
      DocumentLink@1100528601 : Record 11012747;
      SeqNo@1100528602 : Integer;
    BEGIN
      IF "Line No." = 0 THEN
        EXIT;
      IF "Object No." = '' THEN
        EXIT;
      IF NOT ServiceObject.GET("Object No.") THEN
        EXIT;
      ServiceObject.GetPicturesAsDocumentProperties(DocumentProperties);
      IF NOT DocumentProperties.FINDFIRST THEN
        EXIT;
      DocumentLink.SETRANGE("Document No.", DocumentProperties."No.");
      DocumentLink.SETRANGE("Internal Company", COMPANYNAME);
      DocumentLink.FINDLAST;
      SeqNo := DocumentLink."Seq. No." + 10000;
      DocumentLink."Seq. No." := SeqNo;
      DocumentLink."Table No." := DATABASE::"NEN2767/LTMP Line";
      DocumentLink."Record ID" := RECORDID;
      DocumentLink.INSERT(TRUE);
    END;

    PROCEDURE ObjectNoOnLookup@1100528633() : Boolean;
    VAR
      NEN2767LTMPHeader@1100528603 : Record 11128784;
      ServiceObject@1100528602 : Record 11071691;
      NEN2767Management@1100528601 : Codeunit 11012863;
      DummyInt@1100528600 : Integer;
    BEGIN
      IF "Location Type" <> "Location Type"::"Service Location" THEN
        EXIT;
      GetHeader(NEN2767LTMPHeader);
      IF NOT NEN2767LTMPHeader."Use Existing Structure" THEN BEGIN
        ServiceObject.SETRANGE("Service Location No.", "Location Code");
        IF "Object No." <> '' THEN
          IF ServiceObject.GET("Object No.") THEN;
        IF PAGE.RUNMODAL(0, ServiceObject) = ACTION::LookupOK THEN BEGIN
          VALIDATE("Object No.", ServiceObject."No.");
          EXIT(TRUE);
        END;
      END ELSE BEGIN
        IF NEN2767Management.SelectStructureLineCodeAndObjectNo(NEN2767LTMPHeader, "Structure Line Code", "Object No.", DummyInt) THEN BEGIN
          VALIDATE("Object No.");
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

