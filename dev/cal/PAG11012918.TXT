OBJECT Page 11012918 Investment Proposal Budg. List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Investitionsvorschlag BudgetÅbersicht;
               ENU=Investment Proposal Budget List;
               NLD=Investeringsvoorstel budgetoverzicht;
               SVE=Investeringsfîrslag, budgetlista];
    SourceTable=Table11012914;
    DelayedInsert=Yes;
    PageType=List;
    OnInit=BEGIN
             RubricCodeEditable := TRUE;
             FieldEditable := TRUE;
             "House ModelEditable" := TRUE;
             "House Model DescriptionVisible" := TRUE;
             "House ModelVisible" := TRUE;
             gModified := FALSE;
           END;

    OnClosePage=BEGIN
                  IF gModified THEN
                    gInvPropRubricRec.UpdateSurcharges(gInvPropRubricRec, FALSE);
                END;

    OnAfterGetRecord=BEGIN
                       SetFieldEditable;
                     END;

    OnNewRecord=BEGIN
                  IF (GETFILTER(Date) <> '') THEN
                    Date := GETRANGEMIN(Date);

                  IF NOT CheckReferenceDate(FALSE) THEN
                    Date := 0D;
                END;

    OnInsertRecord=BEGIN
                     gModified := TRUE;
                   END;

    OnModifyRecord=BEGIN
                     IF NOT CheckModifyAllowed THEN
                       EXIT(FALSE);

                     gModified := TRUE;
                   END;

    OnDeleteRecord=BEGIN
                     IF NOT CheckModifyAllowed THEN
                       EXIT(FALSE);

                     gModified := TRUE;
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                GroupType=Repeater }

    { 1210190018;2;Field  ;
                Lookup=No;
                SourceExpr="Rubric Code";
                Editable=RubricCodeEditable;
                OnValidate=VAR
                             lvInvPropRubricRec@1210190001 : Record 11012913;
                           BEGIN
                             InvPropRubricRec.GET("Project No.", Version, "Rubric Code");
                             IF NOT (InvPropRubricRec.Type IN [InvPropRubricRec.Type::Costs, InvPropRubricRec.Type::Revenues]) THEN
                               FIELDERROR("Rubric Code", STRSUBSTNO(Text000, "Rubric Code"));

                             IF GETFILTER("Rubric Code") <> '' THEN BEGIN
                               lvInvPropRubricRec.SETRANGE("Project No.", "Project No.");
                               lvInvPropRubricRec.SETRANGE(Version, Version);
                               COPYFILTER("Rubric Code", lvInvPropRubricRec."Rubric Code");
                               lvInvPropRubricRec.FILTERGROUP(6);
                               lvInvPropRubricRec.SETRANGE("Rubric Code", "Rubric Code");
                               lvInvPropRubricRec.FILTERGROUP(0);
                               IF NOT lvInvPropRubricRec.FIND('-') THEN
                                 FIELDERROR("Rubric Code", STRSUBSTNO(Text001, "Rubric Code", GETFILTER("Rubric Code")));
                             END;
                           END;
                            }

    { 1210190001;2;Field  ;
                SourceExpr=Date;
                Editable=FieldEditable;
                OnValidate=VAR
                             lvDateRec@1210190000 : Record 2000000007;
                           BEGIN
                             IF GETFILTER(Date) <> '' THEN BEGIN
                               lvDateRec.SETRANGE("Period Type", lvDateRec."Period Type"::Date);
                               COPYFILTER(Date, lvDateRec."Period Start");
                               lvDateRec.FILTERGROUP(6);
                               lvDateRec.SETRANGE("Period Start", Date);
                               lvDateRec.FILTERGROUP(0);
                               IF NOT lvDateRec.FIND('-') THEN
                                 FIELDERROR(Date, STRSUBSTNO(Text001, Date, GETFILTER(Date)));
                             END;
                           END;
                            }

    { 1210190012;2;Field  ;
                SourceExpr=Description;
                Editable=FieldEditable }

    { 1100485000;2;Field  ;
                SourceExpr="House Model";
                Visible="House ModelVisible";
                Editable="House ModelEditable" }

    { 1100485002;2;Field  ;
                SourceExpr="House Model Description";
                Visible="House Model DescriptionVisible" }

    { 1210190016;2;Field  ;
                SourceExpr=Frequency;
                Editable=FieldEditable }

    { 1210190003;2;Field  ;
                SourceExpr=Quantity;
                Editable=FieldEditable }

    { 1210190014;2;Field  ;
                SourceExpr="Unit of Measure";
                Editable=FieldEditable }

    { 1210190005;2;Field  ;
                SourceExpr="Unit Price";
                Editable=FieldEditable }

    { 1210190007;2;Field  ;
                SourceExpr=Amount }

  }
  CODE
  {
    VAR
      ProjDevRec@1100485000 : Record 11012915;
      InvPropRubricRec@1210190001 : Record 11012913;
      gInvPropRubricRec@1210190004 : Record 11012913;
      gFormEditable@1210190002 : Boolean;
      gModified@1210190000 : Boolean;
      Text000@1210190003 : TextConst 'DEU=''%1'' nicht gestattet. Nur Rubriken der Arten ''Kosten'' und ''ErtrÑge'' sind gestattet.;ENU="''%1'' not allowed, only Rubrics of the types ''Cost'' and ''Revenues'' are allowed ";NLD=''%1'' niet toegestaan, alleen rubrieken van de soorten ''Kosten'' en ''Opbrengsten'' zijn toegestaan;SVE="''%1'' Ñr inte tillÜtet. Endast rubriker av typ Kostnad och IntÑkter tillÜts. "';
      Text001@1210190006 : TextConst 'DEU=''%1'' nicht gestattet, fÑllt ausserhalb des gestatteten Bereichs (%2);ENU=''%1'' not allowed, out of allowed range (%2);NLD=''%1'' niet toegestaan, valt buiten het toegestane bereik (%2);SVE=''%1'' Ñr inte tillÜtet eftersom intervallet (%2) har îverskridits';
      "House ModelVisible"@19033815 : Boolean INDATASET;
      "House Model DescriptionVisible"@19027150 : Boolean INDATASET;
      "House ModelEditable"@19059544 : Boolean INDATASET;
      FieldEditable@1100409000 : Boolean INDATASET;
      RubricCodeEditable@1100409001 : Boolean INDATASET;

    PROCEDURE SetSelections@1210190000(IInvPropRubricRec@1210190000 : Record 11012913;IFormEditable@1210190002 : Boolean);
    BEGIN
      gInvPropRubricRec.COPY(IInvPropRubricRec);
      gFormEditable := IFormEditable;
      CurrPage.EDITABLE(gFormEditable);

      IF IInvPropRubricRec."Project No." <> '' THEN
        ProjDevRec.GET(IInvPropRubricRec."Project No.");

      "House ModelEditable" := (ProjDevRec."Budget Level" = ProjDevRec."Budget Level"::Model);
      "House ModelVisible" := (ProjDevRec."Budget Level" = ProjDevRec."Budget Level"::Model);
      "House Model DescriptionVisible" := (ProjDevRec."Budget Level" = ProjDevRec."Budget Level"::Model);
    END;

    PROCEDURE GetModified@1210190001() : Boolean;
    BEGIN
      EXIT(gModified);
    END;

    PROCEDURE CheckModifyAllowed@1210190002() : Boolean;
    BEGIN
      IF NOT gFormEditable THEN
        EXIT(FALSE);

      IF InvPropRubricRec.GET("Project No.", Version, "Rubric Code") THEN BEGIN
        IF (InvPropRubricRec.Type IN [InvPropRubricRec.Type::Costs, InvPropRubricRec.Type::Revenues]) THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE SetFieldEditable@1100409000();
    BEGIN
      IF "Line No." > 0 THEN
        RubricCodeEditable := CheckModifyAllowed();

      FieldEditable := CheckModifyAllowed();
    END;

    BEGIN
    END.
  }
}

