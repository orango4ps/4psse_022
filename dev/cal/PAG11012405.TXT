OBJECT Page 11012405 Edit Word Documents
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=[ENU=Edit Word Documents;
               NOR=Rediger Word-dokument;
               SVE=Redigera Word-dokument];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=Yes;
    SourceTable=Table11012401;
    PageType=List;
    OnInit=BEGIN
             SecureEditable := TRUE;
           END;

    OnAfterGetRecord=BEGIN
                       GetDocumentData(Rec);
                     END;

    OnAfterGetCurrRecord=VAR
                           DocumentTemplate@1100528400 : Record 11012401;
                         BEGIN
                           GetDocumentData(Rec);
                           IF CurrPage.SignhostDocumentFB.PAGE.IsVisible THEN
                             CurrPage.SignhostDocumentFB.PAGE.SetSubPageLink(DocLinkRID[1], DocumentProperties."No.");

                           SecureEditable := FALSE;
                           IF DocumentTemplate.GET("Document Group", "Template Code") THEN
                             IF DocumentTemplate."Secured Document" = DocumentTemplate."Secured Document"::Both THEN
                               SecureEditable := TRUE;
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190002;1 ;ActionGroup;
                      Name=Document;
                      CaptionML=[ENU=D&ocument;
                                 NOR=D&okument;
                                 SVE=D&okument] }
      { 1210190004;2 ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[ENU=Card;
                                 NOR=Kort;
                                 SVE=Kort];
                      Image=EditLines;
                      OnAction=VAR
                                 WordDocFrm@1210190001 : Page 11012406;
                               BEGIN
                                 GetDocumentData(Rec);
                                 IF DocumentProperties."No." <> '' THEN BEGIN
                                   WordDocFrm.SETRECORD(DocumentProperties);
                                   WordDocFrm.RUN;
                                 END ELSE
                                   EXIT;
                               END;
                                }
      { 1210190015;2 ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=[ENU=Edit;
                                 NOR=Rediger;
                                 SVE=Redigera];
                      Promoted=Yes;
                      Image=Edit;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 EditDoc;
                               END;
                                }
      { 1210190019;2 ;Action    ;
                      CaptionML=[ENU=Create;
                                 NOR=Opprett;
                                 SVE=Skapa];
                      Promoted=Yes;
                      Image=AddAction;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentTemplate@1100528701 : Record 11012401;
                                 SelectedDocumentPropertiesNo@1100528401 : Code[20];
                                 UpdateDocument@1199198001 : Boolean;
                                 CopyAndUpdateDocument@1100528702 : Boolean;
                               BEGIN
                                 IF NOT ChooseCreateAction(UpdateDocument, CopyAndUpdateDocument, SelectedDocumentPropertiesNo, Rec) THEN
                                   EXIT;

                                 DocumentTemplate.COPY(Rec);
                                 CreateDoc(DocumentTemplate, SelectedDocumentPropertiesNo, TRUE, UpdateDocument, CopyAndUpdateDocument, FALSE);
                               END;
                                }
      { 1210190020;2 ;Action    ;
                      CaptionML=[ENU=Multiple Create;
                                 NOR=Opprett flere;
                                 SVE=Skapa flera];
                      Promoted=Yes;
                      Image=Documents;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentTemplate@1100525001 : Record 11012401;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(DocumentTemplate);
                                 IF DocumentTemplate.FINDSET(FALSE, FALSE) THEN BEGIN
                                   REPEAT
                                     CreateDoc(DocumentTemplate, '', TRUE, FALSE, FALSE, FALSE);
                                   UNTIL DocumentTemplate.NEXT = 0;
                                 END ELSE
                                   ERROR(Text001);
                               END;
                                }
      { 1210190021;2 ;Action    ;
                      CaptionML=[ENU=Create and Edit;
                                 NOR=Opprett og rediger;
                                 SVE=Skapa och redigera];
                      Promoted=Yes;
                      Image=AddAction;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentTemplate@1100528701 : Record 11012401;
                                 SelectedDocumentPropertiesNo@1100528400 : Code[20];
                                 UpdateDocument@1100528702 : Boolean;
                                 CopyAndUpdateDocument@1199198001 : Boolean;
                               BEGIN
                                 IF NOT ChooseCreateAction(UpdateDocument, CopyAndUpdateDocument, SelectedDocumentPropertiesNo, Rec) THEN
                                   EXIT;

                                 DocumentTemplate.COPY(Rec);
                                 CreateDoc(DocumentTemplate, SelectedDocumentPropertiesNo, TRUE, UpdateDocument, CopyAndUpdateDocument, TRUE);
                               END;
                                }
      { 1210190022;2 ;Action    ;
                      CaptionML=[ENU=Multiple C&reate and Edit;
                                 NOR=Opprett og &rediger fler;
                                 SVE=Skapa och &redigera flera];
                      Promoted=Yes;
                      Image=Documents;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentTemplate@1100525001 : Record 11012401;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(DocumentTemplate);
                                 IF DocumentTemplate.FINDSET(FALSE, FALSE) THEN BEGIN
                                   REPEAT
                                     CreateDoc(DocumentTemplate, '', TRUE, FALSE, FALSE, TRUE);
                                   UNTIL DocumentTemplate.NEXT = 0;
                                 END ELSE BEGIN
                                   ERROR(Text001);
                                 END;
                               END;
                                }
      { 1210190016;2 ;Action    ;
                      CaptionML=[ENU=Print;
                                 NOR=Skriv ut;
                                 SVE=Skriv ut];
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 DocumentTemplate@1100528701 : Record 11012401;
                               BEGIN
                                 DocumentTemplate.COPY(Rec);
                                 PrintDoc(DocumentTemplate);
                               END;
                                }
      { 1210190017;2 ;Action    ;
                      CaptionML=[ENU=M&ultiple Print;
                                 NOR=Fl&ere utskrifter;
                                 SVE=Fl&era utskrifter];
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 DocumentTemplate@1100525001 : Record 11012401;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(DocumentTemplate);
                                 IF DocumentTemplate.FINDSET(FALSE, FALSE) THEN BEGIN
                                   REPEAT
                                     PrintDoc(DocumentTemplate);
                                   UNTIL DocumentTemplate.NEXT = 0;
                                 END ELSE BEGIN
                                   ERROR(Text001);
                                 END;
                               END;
                                }
      { 1210190003;1 ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 1210190013;2 ;Action    ;
                      CaptionML=[ENU=Create Document and Send by E-Mail;
                                 NOR=Opprett dokument og send med e-post;
                                 SVE=Skapa dokument och skicka med e-post];
                      Promoted=Yes;
                      Enabled=MailMode;
                      Image=SendTo;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT MailMode THEN
                                   ERROR(Text006);
                                 CreateDocumentAndSendMail;
                               END;
                                }
      { 1100528400;2 ;Action    ;
                      CaptionML=ENU=Send to Signhost (Incl. PDF Attachments);
                      Visible=FALSE;
                      Enabled=EnableSignhost;
                      Image=Export;
                      OnAction=VAR
                                 SignhostManagement@1100528400 : Codeunit 11012637;
                               BEGIN
                                 IF GetDocumentData(Rec) THEN
                                   SignhostManagement.SendDocumentToSignhost(DocLinkRID[1], DocumentProperties)
                                 ELSE
                                   ERROR(Text010);
                               END;
                                }
      { 1100528405;2 ;Action    ;
                      CaptionML=ENU=Update Signhost Data;
                      Visible=FALSE;
                      Enabled=EnableSignhost;
                      Image=Import;
                      OnAction=VAR
                                 UpdateSignhostData@1100528400 : Codeunit 11020230;
                               BEGIN
                                 UpdateSignhostData.UpdateDataOfSignhostDocumentOfRecIDAndWordDoc(
                                   DocLinkRID[1], DocumentProperties."No.");
                               END;
                                }
      { 1100528404;2 ;Action    ;
                      CaptionML=ENU=Send Sign Request Again to Signers;
                      Visible=FALSE;
                      Enabled=EnableSignhost;
                      Image=Email;
                      OnAction=VAR
                                 SignhostManagement@1100528400 : Codeunit 11012637;
                               BEGIN
                                 SignhostManagement.SendSignRequestEMailToSigners(
                                   DocLinkRID[1], DocumentProperties."No.", TRUE);
                               END;
                                }
      { 1100525500;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100525501;1 ;ActionGroup;
                      CaptionML=[ENU=Documents;
                                 NOR=Dokument;
                                 SVE=Dokument] }
      { 1210190011;2 ;Action    ;
                      CaptionML=[ENU=Previous Versions;
                                 NOR=Tidligere versjoner;
                                 SVE=F”reg†ende versioner];
                      Image=ListPage;
                      OnAction=VAR
                                 DocumentList@11012001 : Page 11012407;
                                 DocumentProperties2@11012002 : Record 11012746;
                                 DocumentLink@1100525001 : Record 11012747;
                                 RID@1100525002 : RecordID;
                               BEGIN
                                 GetDocumentData(Rec);
                                 DocumentProperties2.SETRANGE("Document Group", "Document Group");
                                 DocumentProperties2.SETRANGE("Document Template", "Template Code");
                                 DocumentProperties2.SETRANGE("Contact No.", gCodContact);
                                 DocumentProperties2.SETRANGE("Internal Company", COMPANYNAME); //**4PS01.n
                                 IF gCodAct <> '' THEN
                                   DocumentProperties2.SETRANGE("Doc. Action", gCodAct);

                                 DocumentLink.SETRANGE("Document No.", DocumentProperties."No.");
                                 DocumentLink.SETRANGE(Source, TRUE);
                                 IF DocumentLink.FINDFIRST THEN BEGIN
                                   RID := DocumentLink."Record ID";

                                   DocumentLink.RESET;
                                   DocumentLink.SETCURRENTKEY("Record ID"); //mg, 05-07-11: M27801
                                   DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RID));
                                   DocumentLink.SETRANGE(Source, TRUE);
                                   DocumentLink.SETRANGE("Internal Company", COMPANYNAME);
                                   IF DocumentLink.FINDSET THEN BEGIN
                                     REPEAT
                                       IF DocumentProperties2.GET(DocumentLink."Document No.") THEN
                                         DocumentProperties2.MARK(TRUE);
                                     UNTIL DocumentLink.NEXT = 0;
                                   END;
                                 END;

                                 DocumentProperties2.MARKEDONLY(TRUE);

                                 DocumentList.SETTABLEVIEW(DocumentProperties2);
                                 DocumentList.SETRECORD(DocumentProperties2);
                                 DocumentList.SetEnableSignhost(EnableSignhost);
                                 DocumentList.RUNMODAL;

                                 DocumentProperties2.CLEARMARKS;
                               END;
                                }
      { 1100528401;2 ;Action    ;
                      CaptionML=ENU=Signhost Signers;
                      Visible=FALSE;
                      Enabled=EnableSignhost;
                      Image=Users;
                      OnAction=BEGIN
                                 ShowSignhostSigners(DocLinkRID[1], DocumentProperties, gCodContact);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Template Code";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=Version;
                CaptionML=[ENU=Version;
                           NOR=Versjon;
                           SVE=Version];
                SourceExpr=DocumentProperties.Version;
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                Name=Creation Date;
                CaptionML=[ENU=Subject;
                           NOR=Emne;
                           SVE=Ang†ende];
                SourceExpr=DocumentProperties.Subject;
                Editable=TRUE }

    { 1100528100;2;Field  ;
                SourceExpr="Publish on Service Portal";
                Visible=false }

    { 1100485001;2;Field  ;
                Name=Secure;
                CaptionML=[ENU=Secure;
                           NOR=Sikre;
                           SVE=Skydda];
                SourceExpr=DocumentProperties.Secure;
                Editable=SecureEditable }

    { 8   ;2   ;Field     ;
                Name=User;
                CaptionML=[ENU=User;
                           NOR=Bruker;
                           SVE=Anv„ndare];
                SourceExpr=DocumentProperties."User ID";
                Editable=FALSE }

    { 1210190000;2;Field  ;
                CaptionML=[ENU=Creation Date;
                           NOR=Opprettelsesdato;
                           SVE=Uppl„ggningsdatum];
                SourceExpr=DocumentProperties."Creation Date" }

    { 12  ;2   ;Field     ;
                Name=Creation Time;
                CaptionML=[ENU=Creation Time;
                           NOR=Opprettet (tid);
                           SVE=Skapades (tid)];
                SourceExpr=DocumentProperties."Creation Time";
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                Name=File;
                CaptionML=[ENU=File;
                           NOR=Fil;
                           SVE=Fil];
                SourceExpr=DocumentProperties.File;
                Editable=FALSE }

    { 1100409000;2;Field  ;
                CaptionML=[ENU=Web Link;
                           NOR=Weblink;
                           SVE=Webbl„nk];
                SourceExpr=DocumentProperties."Web link";
                Editable=false }

    { 1100528402;;Container;
                ContainerType=FactBoxArea }

    { 1100528403;1;Part   ;
                Name=SignhostDocumentFB;
                PagePartID=Page11129774;
                Visible=FALSE;
                PartType=Page }

  }
  CODE
  {
    VAR
      Text000@11012005 : TextConst 'ENU=Creating documents not possible from here;NOR=Kan ikke opprette dokument herfra;SVE=Det g†r inte att skapa dokument h„rifr†n';
      DocumentProperties@11012000 : Record 11012746;
      gCodContact@11012003 : Code[20];
      gCodAct@11012004 : Code[20];
      gCodPerson@1210190003 : Code[20];
      Text001@1100525001 : TextConst 'ENU=First select records;NOR=Marker poster f›rst;SVE=Markera f”rst poster';
      DocLinkRID@1199198001 : ARRAY [5] OF RecordID;
      DepartmentCode@1100528700 : Code[20];
      Text002@1100528400 : TextConst 'ENU=New version based on the current version and update the bookmarks';
      Text003@1100528401 : TextConst 'ENU=New version based on a version to be selected and update the bookmarks';
      Text004@1100528702 : TextConst 'ENU=New version based on the document template (%1)';
      Text005@1100528701 : TextConst 'ENU=Update the bookmarks in the current version';
      Recipient@1100528703 : Text[120];
      MailMode@1100528704 : Boolean INDATASET;
      Text006@1210190006 : TextConst 'ENU=No recipient selected to send an e-mail.;NOR=Ingen mottakere markert for † send E-Mail.;SVE=Inga mottagare markerade f”r att skicka e-post.';
      Text007@1210190007 : TextConst 'ENU=This %1 cannot be used for e-mail.;NOR=Denne %1 kan ikke brukes for E-Mail.;SVE=Denna %1 kan inte anv„ndas f”r e-post.';
      SecureEditable@19010427 : Boolean INDATASET;
      Text008@1100528705 : TextConst 'ENU=E-Mail could not be found in sent items.;NOR=Kan ikke finne E-post  i ''Sendt''-mappen;SVE=E-post kan inte hittas i ''Skickade mail''';
      Text009@1100528402 : TextConst 'ENU=Select how to create a Word Document.;NOR=Velg hvordan Word-dokumentet skal opprettes.;SVE=V„lj hur Word-dokumentet ska skapas.';
      CreateAction@1100528403 : 'Cancel,New Version based on Current Document,New Version based on Document to be Selected,New Version based on Template,Update Current Version';
      EnableSignhost@1100528404 : Boolean INDATASET;
      Text010@1100528405 : TextConst 'ENU=No Word Document found.';

    PROCEDURE InitTypeNo@1(iOptType@1199198002 : Option;iDocLinkRID@1199198000 : ARRAY [5] OF RecordID;NewDepartmentCode@1100528700 : Code[20];iCodContact@1199198001 : Code[20];iCodPerson@1199198003 : Code[20];iCodAct@1199198004 : Code[20]);
    VAR
      i@1199198005 : Integer;
    BEGIN
      // This function should not be used anymore. Use SetVars instead.
      // When there are no references to this function anymore this function can be deleted.
      FOR i := 1 TO ARRAYLEN(iDocLinkRID) DO
        DocLinkRID[i] := iDocLinkRID[i];

      DepartmentCode := NewDepartmentCode;
      gCodContact := iCodContact;
      gCodPerson := iCodPerson;
      gCodAct := iCodAct;
    END;

    PROCEDURE SetVars@1210190001(DocumementLinkIDs@1210190000 : ARRAY [5] OF RecordID;NewDepartmentCode@1210190003 : Code[20];NewContactNo@1210190001 : Code[20];NewContactPersonNo@1210190002 : Code[20];NewDocumentAction@1100528700 : Code[20]);
    VAR
      I@1210190004 : Integer;
    BEGIN
      FOR I := 1 TO ARRAYLEN(DocumementLinkIDs) DO
        DocLinkRID[I] := DocumementLinkIDs[I];

      DepartmentCode := NewDepartmentCode;
      gCodContact := NewContactNo;
      gCodPerson := NewContactPersonNo;
      gCodAct := NewDocumentAction;
    END;

    PROCEDURE SetMailVars@1100528705(NewRecipient@1100528700 : Text[120]);
    BEGIN
      MailMode := TRUE;
      Recipient := NewRecipient;
    END;

    PROCEDURE SetEnableSignhost@1100528401(EnableSignhost2@1100528400 : Boolean);
    BEGIN
      EnableSignhost := EnableSignhost2;
    END;

    PROCEDURE GetDocumentData@1210190000(DocumentTemplate@1210190000 : Record 11012401) : Boolean;
    BEGIN
      //Note: Function is also called extern

      DocumentProperties.SETCURRENTKEY("Document Group","Document Template","Internal Company","Contact No.","Doc. Action",Version);
      DocumentProperties.SetFiltersForGettingAllVersionsOfDocument(
        DocumentTemplate, TRUE, gCodContact, gCodAct);
      // Mark records with a document link to the source table. (First array in DocLinkRID)
      DocumentProperties.MarkDocumentsLinkedToRecordID(DocLinkRID[1]);
      // Only Marked records in filter
      DocumentProperties.MARKEDONLY(TRUE);

      IF NOT DocumentProperties.FINDLAST THEN BEGIN
        DocumentProperties.INIT;
        DocumentProperties."No." := '';
        IF DocumentTemplate."Secured Document" = DocumentTemplate."Secured Document"::Always THEN
          DocumentProperties.Secure := TRUE;

        DocumentProperties.CLEARMARKS();
        EXIT(FALSE);
      END;

      CASE DocumentTemplate."Secured Document" OF
        DocumentTemplate."Secured Document"::"Not":
          DocumentProperties.Secure := FALSE;
        DocumentTemplate."Secured Document"::Always:
          DocumentProperties.Secure := TRUE;
      END;

      DocumentProperties.CLEARMARKS();
      EXIT(TRUE);
    END;

    PROCEDURE CreateWordDoc@1210190002(DocumentTemplate@1210190001 : Record 11012401;SelectedDocumentPropertiesNo@1100528400 : Code[20];UpdateDocument@1199198000 : Boolean;CopyAndUpdateDocument@1100528700 : Boolean;SaveAsWordDoc@1100527300 : Boolean);
    VAR
      lCduWordInterface@1210190000 : Codeunit 11012406;
    BEGIN
      //Note: Function is also called extern
      DocumentTemplate.Subject := DocumentProperties.Subject;

      IF DocumentProperties.Secure THEN
        DocumentTemplate."Secured Document" := DocumentTemplate."Secured Document"::Always
      ELSE
        DocumentTemplate."Secured Document" := DocumentTemplate."Secured Document"::"Not";

      lCduWordInterface.GenerateDocFromTemplate2(DocumentTemplate,
                                                 DocLinkRID,
                                                 SelectedDocumentPropertiesNo,
                                                 DepartmentCode,
                                                 gCodContact,
                                                 gCodPerson,
                                                 gCodAct,
                                                 UpdateDocument,
                                                 CopyAndUpdateDocument,
                                                 SaveAsWordDoc);
    END;

    PROCEDURE CreateDoc@1100485001(DocumentTemplate@1100525000 : Record 11012401;SelectedDocumentPropertiesNo@1100528400 : Code[20];SaveAsWordDoc@1100485000 : Boolean;UpdateDocument@1199198000 : Boolean;CopyAndUpdateDocument@1100528700 : Boolean;ShowDoc@1100527300 : Boolean);
    BEGIN
      IF FORMAT(DocLinkRID[1]) = '' THEN
        ERROR(Text000);

      IF SelectedDocumentPropertiesNo = '' THEN
        GetDocumentData(DocumentTemplate);
      CreateWordDoc(DocumentTemplate, SelectedDocumentPropertiesNo, UpdateDocument, CopyAndUpdateDocument, SaveAsWordDoc);
      CurrPage.UPDATE;

      IF ShowDoc THEN BEGIN
        IF DocumentTemplate.FIND THEN BEGIN
          GetDocumentData(DocumentTemplate);
          IF DocumentProperties."D3 Document ID" = '' THEN
            DocumentProperties.EditDocument();
        END;
      END;
    END;

    PROCEDURE CreateDocumentAndSendMail@1100528701();
    VAR
      DocumentMailing@1100528700 : Codeunit 11012278;
      DocumentTemplate@1100528703 : Record 11012401;
      SelectedDocumentPropertiesNo@1100528401 : Code[20];
      UpdateDocument@1100528701 : Boolean;
      CopyAndUpdateDocument@1100528702 : Boolean;
    BEGIN
      IF NOT ("Use for" IN ["Use for"::"E-Mail", "Use for"::"Document and E-Mail"]) THEN
        ERROR(Text007, TABLECAPTION);

      IF NOT ChooseCreateAction(UpdateDocument, CopyAndUpdateDocument, SelectedDocumentPropertiesNo, Rec) THEN
        EXIT;

      // Create Word document
      DocumentTemplate.COPY(Rec);
      CreateDoc(DocumentTemplate, SelectedDocumentPropertiesNo, FALSE, UpdateDocument, CopyAndUpdateDocument, FALSE);
      DocumentTemplate.FIND;
      GetDocumentData(DocumentTemplate);

      // Commit after generating the document because sending the mail may take some time and you do not want tables remain locked
      COMMIT;

      IF NOT DocumentMailing.MailDocument(
        Recipient,
        DocumentProperties,
        ((DocumentTemplate."Attach to E-Mail as" = DocumentTemplate."Attach to E-Mail as"::Attachment) OR
        (DocumentTemplate."Attach to E-Mail as" = DocumentTemplate."Attach to E-Mail as"::"Attachment as PDF")))
      THEN BEGIN
        DocumentProperties.DELETE(TRUE); // Rollback
        MESSAGE(Text008);
      END;
    END;

    PROCEDURE EditDoc@1100485002();
    BEGIN
      GetDocumentData(Rec);
      DocumentProperties.EditDocument();
    END;

    PROCEDURE PrintDoc@1100485003(DocumentsRec@1100525000 : Record 11012401);
    VAR
      lCduWord@1100485000 : Codeunit 11012400;
      FileManagement@1100528400 : Codeunit 419;
      DocPrintFrm@1100485001 : Page 11012792;
      DocumentTemplate@1199198000 : Record 11012401;
      FilePath@1100525002 : Text[500];
      ClientFile@1100527800 : Text;
    BEGIN
      GetDocumentData(DocumentsRec);

      IF (DocumentProperties.GetStorageURI <> '')   THEN BEGIN
        DocumentProperties.WriteContentToClient(ClientFile);
        lCduWord.InitialiseForPrinting;
        lCduWord.Print(ClientFile);
        lCduWord.TerminateAfterPrinting;
        FileManagement.DeleteClientFile(ClientFile);
      END;

      DocumentTemplate.GET("Document Group", "Template Code");
      IF (DocumentTemplate.Type = Type::"Purchase Quote") OR  (DocumentTemplate.Type = Type::"Purchase Order") THEN BEGIN
      //db, 30-03-06
        DocPrintFrm.SetSelection(DocumentTemplate.Type, DocLinkRID[1]);
        DocPrintFrm.RUNMODAL;
      END;
    END;

    PROCEDURE ChooseCreateAction@1100528700(VAR UpdateDocument@1100528700 : Boolean;VAR CopyAndUpdateDocument@1100528707 : Boolean;VAR SelectedDocumentPropertiesNo@1100528401 : Code[20];DocumentTemplate@1100528701 : Record 11012401) : Boolean;
    VAR
      CreateActionChoice@1100525002 : Integer;
    BEGIN
      UpdateDocument := FALSE;
      CopyAndUpdateDocument := FALSE;
      SelectedDocumentPropertiesNo := '';

      IF DocumentProperties.DocumentVersionsExist(DocumentTemplate, gCodAct, DocLinkRID[1]) THEN BEGIN
        CreateActionChoice := DetermineCreateActionChoice(DocumentProperties, DocumentTemplate);
        CASE CreateActionChoice OF
          CreateAction::Cancel: EXIT(FALSE);
          CreateAction::"New Version based on Current Document":
            BEGIN
              UpdateDocument := TRUE;
              CopyAndUpdateDocument := TRUE;
              EXIT(TRUE);
            END;
          CreateAction::"New Version based on Document to be Selected":
            IF DocumentProperties.SelectDocumentVersion(DocumentTemplate, gCodAct, DocLinkRID[1]) THEN BEGIN
              UpdateDocument := TRUE;
              CopyAndUpdateDocument := TRUE;
              SelectedDocumentPropertiesNo := DocumentProperties."No.";
              EXIT(TRUE);
            END ELSE
              EXIT(FALSE);
          CreateAction::"New Version based on Template":
            BEGIN
              UpdateDocument := FALSE;
              EXIT(TRUE);
            END;
          CreateAction::"Update Current Version":
            BEGIN
              DocumentProperties.CheckFileIsInUse;
              UpdateDocument := TRUE;
              EXIT(TRUE);
            END;
        END;
      END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE DetermineCreateActionChoice@1100528422(DocumentProperties@1100528400 : Record 11012746;DocumentTemplate@1100528402 : Record 11012401) CreateActionChoice : Integer;
    VAR
      Choice@1100528401 : Integer;
    BEGIN
      IF (DocumentProperties.Version <> 0) THEN BEGIN
        Choice := STRMENU(
          STRSUBSTNO('%1,%2,%3,%4', Text002, Text003, STRSUBSTNO(Text004, DocumentTemplate."Template Code"), Text005),
          1, Text009);
        CASE Choice OF
          0:
            CreateActionChoice := CreateAction::Cancel;
          1:
            CreateActionChoice := CreateAction::"New Version based on Current Document";
          2:
            CreateActionChoice := CreateAction::"New Version based on Document to be Selected";
          3:
            CreateActionChoice := CreateAction::"New Version based on Template";
          4:
            CreateActionChoice := CreateAction::"Update Current Version";
        END;
      END ELSE BEGIN
        Choice := STRMENU(
          STRSUBSTNO('%1,%2', Text003, STRSUBSTNO(Text004, DocumentTemplate."Template Code")),
          2, Text009);
        CASE Choice OF
          0:
            CreateActionChoice := CreateAction::Cancel;
          1:
            CreateActionChoice := CreateAction::"New Version based on Document to be Selected";
          2:
            CreateActionChoice := CreateAction::"New Version based on Template";
        END;
      END;
    END;

    BEGIN
    END.
  }
}

