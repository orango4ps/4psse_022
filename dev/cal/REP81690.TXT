OBJECT Report 81690 Create Plant Loc For Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=RFC-068;
  }
  PROPERTIES
  {
    CaptionML=SVE=Skapa maskinutplacering f”r projekt;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1100285100;;DataItem;                  ;
               DataItemTable=Table11072003;
               DataItemTableView=SORTING(No.)
                                 ORDER(Ascending)
                                 WHERE(Project Status=FILTER(..Production));
               PrintOnlyIfDetail=Yes;
               OnAfterGetRecord=BEGIN
                                  IF NOT PlantLocation.GET(Job."No.") THEN
                                    JobRunCreatePlantLocation(Job);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PlantLocation@1100285100 : Record 11012554;

    PROCEDURE JobRunCreatePlantLocation@1100285000(IProjRec@1100485002 : Record 11072003);
    VAR
      ProjLocAddressRec@1100525008 : Record 11012011;
      PlantMgtCompRec@1100525005 : Record 11020586;
      TmpPlantMgtCompRec@1100525002 : TEMPORARY Record 11020586;
      PlantMgtCompListFrm@1100525001 : Page 11124918;
      CopyProjLocAddresses@1100525009 : Boolean;
      NoOfPlantComps@1100525000 : Integer;
      NoCreated@1100525004 : Integer;
      lvPlantLocCode@1100485000 : Code[20];
      lvText11012001@1100525007 : TextConst 'ENU=Create a Plant Location of Project ''%1''. Projectstatus is ''%2'', are you sure?;NOR=Opprette en maskinlokasjon for prosjekt ''%1''. prosjektstatus er ''%2''. Er du sikker?;SVE=Skapa en Maskinsplats f”r projekt ''%1''. Projektstatus „r ''%2''. Žr du s„ker?';
      lvText11012002@1100525006 : TextConst 'ENU=Do you want to create a Plant Location of Project ''%1''?;NOR=Vil du opprette en maskinlokasjon for prosjekt ''%1''?;SVE=Vill du skapa en Maskinsplats f”r projekt ''%1''?';
      lvText11012003@1100485003 : TextConst 'ENU=Plant Location ''%1'' is created in plant company ''%2''.;NOR=Maskinlokasjon ''%1'' er opprettet i maskinselskap ''%2''.;SVE=Lagerplats ''%1'' „r skapad i Maskinsf”retag ''%2''.';
      lvText11012004@1100485004 : TextConst 'ENU=Plant Location ''%1'' is created in %2 plant companies.;NOR=Maskinlokasjon ''%1'' er opprettet i %2 maskinselskaper.;SVE=Lagerplats ''%1'' „r skapad i %2 Maskinsf”retag.';
      lvText11012005@1100525003 : TextConst 'ENU=Including the Project Locations (Project Addresses)?;NOR=Inkludert prosjektlokasjoner (prosjektadresser)?;SVE=Inklusive projektst„llen (projektadresser)?';
    BEGIN
      //**4PS
      CopyProjLocAddresses := TRUE;

      NoCreated := 0;
      PlantMgtCompRec."Plant Company" := PlantMgtCompRec.GetPlantCompany(TRUE, NoOfPlantComps, '');
      IF NoOfPlantComps = 1 THEN BEGIN
        lvPlantLocCode := JobCreatePlantLocation(IProjRec, PlantMgtCompRec."Plant Company", CopyProjLocAddresses);
        IF lvPlantLocCode <> '' THEN
          NoCreated := 1;
      END;
    END;

    PROCEDURE JobCreatePlantLocation@1100485007(IProjRec@1100485000 : Record 11072003;IPlantCompany@1100485010 : Text[30];ICopyProjLocAddresses@1100525000 : Boolean) : Code[20];
    VAR
      lvProjSetupRec@1100485005 : Record 315;
      lvPlantSetupRec@1100485004 : Record 11012550;
      lvCustRec@1100485003 : Record 18;
      lvPlantLocRec@1100485001 : Record 11012554;
      lvPlantLocRec2@1100485002 : Record 11012554;
      lvText11012003@1100485009 : TextConst 'ENU=Plant Location ''%1'' is already present in Company ''%2'', with Project ''%3'' and Company Name ''%4''.;NOR=Maskinlokasjon ''%1'' finnes allerede i selskap ''%2'' med prosjekt ''%3'' og selskapsnavn ''%4''.;SVE=Maskinutplaceringen ''%1'' finns redan i f”retaget ''%2'' med projektet ''%3'' och f”retagsnamnet ''%4''.';
      lvDimValRec@1100525001 : Record 349;
      Employee@1210190000 : Record 5200;
      lvCompRec@1100285100 : Record 11020674;
    BEGIN
      //**4PS
      //* From a project in the 'Project Company' create a plant location in the 'Plant Company' this
      //* can be another company, so do not use 'VALIDATE'/'INSERT/MODIFY(TRUE)' on the Plant Location table!

      WITH IProjRec DO BEGIN
        TESTFIELD("No.");
        lvProjSetupRec.GET;
        lvProjSetupRec.TESTFIELD("Build Plant Location Code from");

        lvCompRec.GET(COMPANYNAME);  //* Huidige bedijf, dus niet het materieelbedrijf

        lvPlantSetupRec.CHANGECOMPANY(IPlantCompany);
        lvPlantSetupRec.GET;

        lvPlantLocRec2.CHANGECOMPANY(IPlantCompany);

        lvPlantLocRec.CHANGECOMPANY(IPlantCompany);
        lvPlantLocRec.INIT;
        lvPlantLocRec.InitRecord;
        CASE lvProjSetupRec."Build Plant Location Code from" OF
          lvProjSetupRec."Build Plant Location Code from"::ProjNo:
            lvPlantLocRec.Code := "No.";
          lvProjSetupRec."Build Plant Location Code from"::CompNoProjNo:
            lvPlantLocRec.Code := lvCompRec."Company No." + "No.";
          ELSE
            lvPlantLocRec.Code := '';
        END;
        lvPlantLocRec.TESTFIELD(Code);
        IF lvPlantLocRec2.GET(lvPlantLocRec.Code) THEN
          ERROR(lvText11012003, lvPlantLocRec.Code, IPlantCompany, lvPlantLocRec2."Project No.", lvPlantLocRec2."Company Name");

        lvPlantLocRec."Creation Date" := TODAY;
        lvPlantLocRec.Depot :=  FALSE;
        lvPlantLocRec."Project No." :=  "No.";
        IF IPlantCompany <> COMPANYNAME THEN BEGIN
          lvPlantLocRec."Company Name" := COMPANYNAME;
          //
          //* Klantnummer mag alleen gevuld worden (verplicht met die uit bedrijventabel) als project en materieelloctie
          //* in verschillende bedrijven zitten. Als in zelfde bedrijf dan moet klantnummer leeg blijven.
          lvCompRec.TESTFIELD("Customer No.");
          lvCustRec.CHANGECOMPANY(IPlantCompany);
          IF NOT lvCustRec.GET(lvCompRec."Customer No.") THEN
            lvCustRec.INIT;
          lvPlantLocRec."Customer No." := lvCompRec."Customer No.";
          lvPlantLocRec."VAT Bus. Posting Group" := lvCustRec."VAT Bus. Posting Group";
          IF lvPlantSetupRec."Def. Loc. Bus. P.Grp of Cust." THEN
            lvPlantLocRec."Gen. Bus. Posting Group" := lvCustRec."Gen. Bus. Posting Group";
        END;
        //mg.sn, 09-03-11: M25508
        IF lvPlantLocRec."Customer No." = '' THEN BEGIN
          lvPlantLocRec."Gen. Bus. Posting Group" := lvPlantSetupRec."Default Gen. Bus.Posting Group";
          lvPlantLocRec."VAT Bus. Posting Group" := lvPlantSetupRec."Default VAT Bus.Posting Group";
        END;
        //mg.en, 09-03-11: M25508
        //**4PS.sn DPA Plant
        //IF IPlantCompany = COMPANYNAME THEN  //*24614.o
        //  lvPlantLocRec."Department Code" := "Global Dimension 1 Code";  //*24614.o
        //**4PS.en DPA Plant
        lvPlantLocRec.Description :=  Description;
        lvPlantLocRec."Description 2" := "Description 2";
        lvPlantLocRec."Search Name" := "Search Description";
        lvPlantLocRec.Address := Address;
        lvPlantLocRec."Address 2" := "Address 2";
        lvPlantLocRec.City := City;
        IF "Site Manager" <> '' THEN BEGIN
          lvPlantLocRec.Contact := COPYSTR(SiteManagerName, 1 , MAXSTRLEN(Contact));
          //call C-027723.sn
          IF Employee.GET("Site Manager") THEN BEGIN
            lvPlantLocRec."Contact Phone No." := Employee."Phone No.";
            lvPlantLocRec."E-Mail" := Employee."E-Mail";
          END;
          //call C-027723.en
        END;
        lvPlantLocRec.County := County;
        lvPlantLocRec."Post Code" := "Post Code";
        lvPlantLocRec."Country/Region Code" := "Country/Region Code";
        lvPlantLocRec."Phone No." := "Phone No.";
        lvPlantLocRec."Fax No." := "Fax No.";
        lvPlantLocRec."Territory Code" := "Territory Code";
        lvPlantLocRec."Starting Date" := "Starting Date";
        lvPlantLocRec."Ending Date" := "Ending Date";
        IF "Global Dimension 1 Code" <> '' THEN BEGIN  //*24614.sn
          IF IPlantCompany = COMPANYNAME THEN
            lvPlantLocRec."Department Code" := "Global Dimension 1 Code"
          ELSE BEGIN
            lvDimValRec.CHANGECOMPANY(IPlantCompany);
            lvDimValRec.SETCURRENTKEY(Code, "Global Dimension No.");
            lvDimValRec.SETRANGE(Code, "Global Dimension 1 Code");
            lvDimValRec.SETRANGE("Global Dimension No.", 1);
            IF lvDimValRec.FINDFIRST THEN
              lvPlantLocRec."Department Code" := "Global Dimension 1 Code";
          END;
        END;                                           //*24614.en
        lvPlantLocRec.INSERT;

        IF ICopyProjLocAddresses THEN
          lvPlantLocRec.CopyProjLocsToPlantLocAddress(IPlantCompany);

        EXIT(lvPlantLocRec.Code);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

