OBJECT Report 11128520 Create NO Elec. Fin. Chrg Mems
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO10.00 (10643),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Elec. Fin. Chrg. Memos;
               NOR=Opprette elektroniske rentefakturaer;
               SVE=Skapa elektroniska r„ntefakturor];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

  }
  DATASET
  {
    { 6218;    ;DataItem;                    ;
               DataItemTable=Table304;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               IssuedFinChargeMemoHeader@1101100000 : Record 304;
                             BEGIN
                               Counter := 0;

                               // Any electronic finance charges?
                               IssuedFinChargeMemoHeader.COPY("Issued Fin. Charge Memo Header");
                               IssuedFinChargeMemoHeader.FILTERGROUP(6);
                               IssuedFinChargeMemoHeader.SETRANGE("E-Invoice",TRUE);
                               IF NOT IssuedFinChargeMemoHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic finance charges?
                               IssuedFinChargeMemoHeader.SETRANGE("E-Invoice",FALSE);
                               IF IssuedFinChargeMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               IssuedFinChargeMemoHeader.SETRANGE("E-Invoice");

                               // Some already sent?
                               IssuedFinChargeMemoHeader.SETRANGE("E-Invoice Created",TRUE);
                               IF IssuedFinChargeMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETRANGE("E-Invoice",TRUE);
                             END;

               OnAfterGetRecord=VAR
                                  EInvoiceExpIssFinChrg@1080000 : Codeunit 11128508;
                                BEGIN
                                  EInvoiceExpIssFinChrg.RUN("Issued Fin. Charge Memo Header");
                                  EInvoiceExpIssFinChrg.GetExportedFileInfo(TempEInvoiceTransferFile);
                                  TempEInvoiceTransferFile."Line No." := Counter + 1;
                                  TempEInvoiceTransferFile.INSERT;

                                  IF LogInteraction THEN
                                    SegManagement.LogDocument(
                                      19,"No.",0,0,DATABASE::Customer,"Customer No.",'','',"Posting Description",'');

                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=VAR
                                EInvoiceExportCommon@1080000 : Codeunit 11128519;
                              BEGIN
                                EInvoiceExportCommon.DownloadEInvoiceFile(TempEInvoiceTransferFile);
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Customer No.,EAN No.,Elec. Fin. Charge Memo Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1101100000;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=[ENU=Log Interaction;
                             NOR=Logg Interaksjon;
                             SVE=Logga interaktion];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempEInvoiceTransferFile@1080000 : TEMPORARY Record 11128516;
      SegManagement@1101100003 : Codeunit 5051;
      Counter@1101100000 : Integer;
      LogInteraction@1101100002 : Boolean;
      Text000@1101100007 : TextConst 'ENU=One or more issued finance charges that match your filter criteria are not electronic finance charges and will be skipped.\\Do you want to continue?;NOR=En eller flere rentefakturaer som passer i ditt utvalg er ikke elektroniske rentefakturaer og hoppes over.\\Vil du fortsette?;SVE=En eller flera r„ntefakturor som passar i ditt urval „r inte elektroniska r„ntefakturor och hoppas ”ver.\\Vill du forts„tta?';
      Text001@1101100006 : TextConst 'ENU=One or more electronic finance charges that match your filter criteria have been created before.\\Do you want to continue?;NOR=En eller flere rentefakturaer som passer i ditt utvalg har blitt opprettet tidligere.\\Vil du fortsette?;SVE=En eller flera r„ntefakturor som passar i ditt urval har skapats tidigare.\\Vill du forts„tta?';
      Text002@1101100005 : TextConst 'ENU=Successfully created %1 electronic finance charges.;NOR=Opprettet %1 elektroniske rentefakturaer;SVE=Lyckades skapa %1 elektroniska r„ntefakturor';
      Text003@1101100004 : TextConst 'ENU=Nothing to create.;NOR=Det finnes ingenting † opprette.;SVE=Det finns inget att skapa.';
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1101100000();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(19) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

