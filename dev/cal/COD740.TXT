OBJECT Codeunit 740 VAT Report Mediator
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      VATReportReleaseReopen@1000 : Codeunit 741;
      Text001@1002 : TextConst 'ENU=This action will also mark the report as released. Are you sure you want to continue?;NOR=Denne handlingen vil ogs† merke rapporten som frigitt. Er du sikker p† at du vil fortsette ?;SVE=Den h„r †tg„rden anger samtidigt att rapporten har sl„ppts. Vill du forts„tta?';

    [External]
    PROCEDURE GetLines@5(VATReportHeader@1001 : Record 740);
    VAR
      VATReportsConfiguration@1000 : Record 746;
    BEGIN
      VATReportHeader.TESTFIELD(Status,VATReportHeader.Status::Open);
      IF (VATReportHeader."VAT Report Type" = VATReportHeader."VAT Report Type"::Corrective) OR
         (VATReportHeader."VAT Report Type" = VATReportHeader."VAT Report Type"::Supplementary)
      THEN
        VATReportHeader.TESTFIELD("Original Report No.");

      VATReportsConfiguration.SETRANGE("VAT Report Type",VATReportHeader."VAT Report Config. Code");
      IF VATReportHeader."VAT Report Version" <> '' THEN
        VATReportsConfiguration.SETRANGE("VAT Report Version",VATReportHeader."VAT Report Version");
      VATReportsConfiguration.FINDFIRST;

      VATReportHeader.FILTERGROUP(2);
      VATReportHeader.SETRANGE("VAT Report Config. Code",VATReportHeader."VAT Report Config. Code");
      VATReportHeader.SETRANGE("No.",VATReportHeader."No.");
      CODEUNIT.RUN(VATReportsConfiguration."Suggest Lines Codeunit ID",VATReportHeader);
    END;

    [External]
    PROCEDURE Export@6(VATReportHeader@1001 : Record 740);
    VAR
      VATReportsConfiguration@1002 : Record 746;
    BEGIN
      CASE VATReportHeader."VAT Report Config. Code" OF
        VATReportHeader."VAT Report Config. Code"::"VAT Return":
          VATReportsConfiguration.SETRANGE("VAT Report Type",VATReportsConfiguration."VAT Report Type"::"VAT Return");
        VATReportHeader."VAT Report Config. Code"::"EC Sales List":
          VATReportsConfiguration.SETRANGE("VAT Report Type",VATReportsConfiguration."VAT Report Type"::"EC Sales List");
      END;
      IF VATReportHeader."VAT Report Version" <> '' THEN
        VATReportsConfiguration.SETRANGE("VAT Report Version",VATReportHeader."VAT Report Version");
      VATReportsConfiguration.FINDFIRST;

      IF VATReportsConfiguration."Content Codeunit ID" <> 0 THEN
        CODEUNIT.RUN(VATReportsConfiguration."Content Codeunit ID",VATReportHeader);
      IF VATReportsConfiguration."Submission Codeunit ID" <> 0 THEN
        CODEUNIT.RUN(VATReportsConfiguration."Submission Codeunit ID",VATReportHeader);
    END;

    [External]
    PROCEDURE Release@8(VATReportHeader@1000 : Record 740);
    BEGIN
      VATReportReleaseReopen.Release(VATReportHeader);
    END;

    [External]
    PROCEDURE Reopen@7(VATReportHeader@1000 : Record 740);
    BEGIN
      VATReportReleaseReopen.Reopen(VATReportHeader);
    END;

    [External]
    PROCEDURE Print@1(VATReportHeader@1000 : Record 740);
    BEGIN
      CASE VATReportHeader.Status OF
        VATReportHeader.Status::Open:
          PrintOpen(VATReportHeader);
        VATReportHeader.Status::Released:
          PrintReleased(VATReportHeader);
        VATReportHeader.Status::Submitted:
          PrintReleased(VATReportHeader);
      END;
    END;

    LOCAL PROCEDURE PrintOpen@2(VAR VATReportHeader@1000 : Record 740);
    VAR
      VATReportReleaseReopen@1002 : Codeunit 741;
    BEGIN
      VATReportHeader.TESTFIELD(Status,VATReportHeader.Status::Open);
      IF CONFIRM(Text001,TRUE) THEN BEGIN
        VATReportReleaseReopen.Release(VATReportHeader);
        PrintReleased(VATReportHeader);
      END
    END;

    LOCAL PROCEDURE PrintReleased@3(VAR VATReportHeader@1000 : Record 740);
    BEGIN
      VATReportHeader.SETRANGE("No.",VATReportHeader."No.");
      REPORT.RUNMODAL(REPORT::"VAT Report Print",FALSE,FALSE,VATReportHeader);
    END;

    [External]
    PROCEDURE Submit@4(VATReportHeader@1000 : Record 740);
    BEGIN
      VATReportReleaseReopen.Submit(VATReportHeader);
    END;

    BEGIN
    END.
  }
}

