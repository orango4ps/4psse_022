OBJECT Report 5979 Copy Service Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Servicebeleg kopieren;
               ENU=Copy Service Document;
               NLD=Servicedocument kopiâren;
               NOR=Kopier servicedokument];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  ConfirmManagement@1000 : Codeunit 27;
                BEGIN
                  IF DocNo = '' THEN
                    ERROR(Text004);
                  ValidateDocNo;
                  IF FromServContractHeader."Ship-to Code" <> ServContractHeader."Ship-to Code" THEN
                    IF NOT ConfirmManagement.ConfirmProcess(Text003,TRUE) THEN
                      CurrReport.QUIT;
                  AllLinesCopied := CopyDocMgt.CopyServContractLines(ServContractHeader,DocType,DocNo,OutServContractLine);
                END;

    OnPostReport=VAR
                   ConfirmManagement@1000 : Codeunit 27;
                 BEGIN
                   COMMIT;
                   IF NOT AllLinesCopied THEN
                     IF ConfirmManagement.ConfirmProcessUI(Text000,TRUE) THEN BEGIN
                       OutServContractLine.MARKEDONLY := TRUE;
                       PAGE.RUNMODAL(PAGE::"Service Contract Line List",OutServContractLine);
                     END;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DocNo <> '' THEN BEGIN
                     CASE DocType OF
                       DocType::Quote:
                         IF FromServContractHeader.GET(FromServContractHeader."Contract Type"::Quote,DocNo) THEN;
                       DocType::Contract:
                         IF FromServContractHeader.GET(FromServContractHeader."Contract Type"::Contract,DocNo) THEN;
                     END;
                     IF FromServContractHeader."Contract No." = '' THEN
                       DocNo := ''
                     ELSE
                       FromServContractHeader.CALCFIELDS(Name);
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Belegart;
                             ENU=Document Type;
                             NLD=Documenttype;
                             NOR=Bilagstype;
                             SVE=Dokumenttyp];
                  ToolTipML=[DEU=Gibt die Art des Servicebelegs an, den Sie kopieren mîchten.;
                             ENU=Specifies the type of service document that you want to copy.;
                             NLD=Hiermee wordt het soort servicedocument opgegeven dat u wilt kopiâren.;
                             NOR=Angir typen servicedokument du vil kopiere.];
                  OptionCaptionML=[DEU=Angebot,Vertrag;
                                   ENU=Quote,Contract;
                                   NLD=Offerte,Contract;
                                   NOR=Tilbud,Kontrakt];
                  ApplicationArea=#Service;
                  SourceExpr=DocType;
                  OnValidate=BEGIN
                               DocNo := '';
                               ValidateDocNo;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Verifikationsnr];
                  ToolTipML=[DEU=Gibt die Belegnummer an, aus der Sie Åber die Auswahl des Feldes kopieren mîchten.;
                             ENU=Specifies the document number that you want to copy from by choosing the field.;
                             NLD=Hiermee wordt het documentnummer waaruit u wilt kiezen, opgegeven door het veld te kiezen.;
                             NOR=Angir dokumentnummeret du vil kopiere fra, ved Ü merke av i feltet.];
                  ApplicationArea=#Service;
                  SourceExpr=DocNo;
                  OnValidate=BEGIN
                               ValidateDocNo;
                             END;

                  OnLookup=BEGIN
                             LookupDocNo;
                           END;
                            }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Debitorennr.;
                             ENU=Customer No.;
                             NLD=Klantnr.;
                             NOR=Kundenr.;
                             SVE=Kundnr];
                  ToolTipML=[DEU=Gibt die Nummer des Debitors an.;
                             ENU=Specifies the number of the customer.;
                             NLD=Hiermee wordt het nummer van de klant opgegeven.;
                             NOR=Angir nummeret for kunden.;
                             SVE=Anger kundens nummer.];
                  ApplicationArea=#Service;
                  SourceExpr=FromServContractHeader."Customer No.";
                  Editable=FALSE }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=Debitor Name;
                             ENU=Customer Name;
                             NLD=Klant;
                             NOR=Kundenavn;
                             SVE=Kundens namn];
                  ToolTipML=[DEU=Gibt den Debitorennamen aus einem Beleg an, den Sie zum Kopieren der Informationen ausgewÑhlt haben.;
                             ENU=Specifies the customer name from a document that you have selected to copy the information from.;
                             NLD=Hiermee wordt de klantnaam opgegeven uit een document dat u hebt geselecteerd om de gegevens uit te kopiâren.;
                             NOR=Angir kundenavnet fra et dokument som du har valgt Ü kopiere opplysninger fra.];
                  ApplicationArea=#Service;
                  SourceExpr=FromServContractHeader.Name;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ServContractHeader@1007 : Record 5965;
      FromServContractHeader@1010 : Record 5965;
      OutServContractLine@1004 : Record 5964;
      CopyDocMgt@1009 : Codeunit 6620;
      DocType@1026 : 'Quote,Contract';
      DocNo@1027 : Code[20];
      AllLinesCopied@1003 : Boolean;
      Text000@1005 : TextConst 'DEU=Es konnten nicht alle Servicevertragszeilen kopiert werden.\\Sollen diese Zeilen angezeigt werden?;ENU=It was not possible to copy all of the service contract lines.\\Do you want to see these lines?;NLD=Niet alle servicecontractregels konden worden gekopieerd.\\Wilt u deze regels weergeven?;NOR=Det var ikke mulig Ü kopiere alle servicekontraktlinjene.\\Vil du vise disse linjene?';
      Text002@1002 : TextConst 'DEU=Sie kînnen den Beleg nur mit derselben/demselben %1 kopieren.;ENU=You can only copy the document with the same %1.;NLD=U kunt alleen het document met eenzelfde %1 kopiâren.;NOR=Du kan bare kopiere dokumentet som har samme %1.';
      Text003@1006 : TextConst 'DEU=Der Beleg hat einen anderen Lief. an Code.\\Mîchten Sie fortfahren?;ENU=The document has a different ship-to code.\\Do you want to continue?;NLD=Het document heeft een andere verzendcode.\\Wilt u doorgaan?;NOR=Dette bilaget har en annen lever til-kode.\\Vil du fortsette?';
      Text004@1008 : TextConst 'DEU=Sie mÅssen das Feld Belegnr. ausfÅllen.;ENU=You must fill in the Document No. field.;NLD=U moet het veld Documentnr. invullen.;NOR=Du mÜ fylle ut feltet Bilagsnr.';

    [External]
    PROCEDURE SetServContractHeader@2(VAR NewServContractHeader@1000 : Record 5965);
    BEGIN
      ServContractHeader := NewServContractHeader;
    END;

    LOCAL PROCEDURE ValidateDocNo@10();
    BEGIN
      IF DocNo = '' THEN
        FromServContractHeader.INIT
      ELSE BEGIN
        FromServContractHeader.INIT;
        FromServContractHeader.GET(DocType,DocNo);
        IF FromServContractHeader."Customer No." <> ServContractHeader."Customer No." THEN
          ERROR(Text002,ServContractHeader.FIELDCAPTION("Customer No."));
        IF FromServContractHeader."Currency Code" <> ServContractHeader."Currency Code" THEN
          ERROR(Text002,ServContractHeader.FIELDCAPTION("Currency Code"));
        FromServContractHeader.CALCFIELDS(Name);
      END;
    END;

    LOCAL PROCEDURE LookupDocNo@3();
    BEGIN
      FromServContractHeader.FILTERGROUP := 2;
      FromServContractHeader.SETRANGE("Contract Type",CopyDocMgt.ServContractHeaderDocType(DocType));
      IF ServContractHeader."Contract Type" = CopyDocMgt.ServContractHeaderDocType(DocType) THEN
        FromServContractHeader.SETFILTER("Contract No.",'<>%1',ServContractHeader."Contract No.");
      FromServContractHeader."Contract Type" := CopyDocMgt.ServContractHeaderDocType(DocType);
      FromServContractHeader."Contract No." := DocNo;
      FromServContractHeader.SETCURRENTKEY("Customer No.","Currency Code","Ship-to Code");
      FromServContractHeader.SETRANGE("Customer No.",ServContractHeader."Customer No.");
      FromServContractHeader.SETRANGE("Currency Code",ServContractHeader."Currency Code");
      FromServContractHeader.FILTERGROUP := 0;
      FromServContractHeader.SETRANGE("Ship-to Code",ServContractHeader."Ship-to Code");
      IF PAGE.RUNMODAL(0,FromServContractHeader) = ACTION::LookupOK THEN
        DocNo := FromServContractHeader."Contract No.";
      ValidateDocNo;
    END;

    [External]
    PROCEDURE InitializeRequest@1(DocumentType@1000 : Option;DocumentNo@1001 : Code[20]);
    BEGIN
      DocType := DocumentType;
      DocNo := DocumentNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

