OBJECT Table 11125864 Try-out Quote Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Contact No.");
               UpdateModificationData;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               UpdateModificationData;
             END;

    OnDelete=BEGIN
               UpdateModificationData;
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=[ENU=Try-out Quote Line;
               NOR=Rad for tilbudsforesp›rsel;
               SVE=Rad f”r anbudsf”rfr†gan];
    LookupPageID=Page11012766;
  }
  FIELDS
  {
    { 1   ;   ;Try-out Quote       ;Code20        ;TableRelation="Try-out Quote";
                                                   CaptionML=[ENU=Try-out Quote;
                                                              NOR=Tilbudsforesp›rsel;
                                                              SVE=Anbudsf”rfr†gan];
                                                   NotBlank=Yes }
    { 2   ;   ;Contact No.         ;Code20        ;TableRelation=Contact WHERE (Person No.=FILTER(''));
                                                   OnValidate=BEGIN
                                                                IF "Contact No." <> xRec."Contact No." THEN BEGIN
                                                                  "Contact Person No." := '';
                                                                  "Salesperson Code" := '';
                                                                END;

                                                                IF "Contact No." <> '' THEN BEGIN
                                                                  ProspectRec.ValidateIsContact("Contact No.");
                                                                  ProspectRec.GET("Contact No.");
                                                                  "Contact Type" := ProspectRec."Contact Type";

                                                                  ValidateCompanySalesPerson;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Contact No.;
                                                              NOR=Kontaktnr;
                                                              SVE=Kontaktnr];
                                                   NotBlank=Yes }
    { 3   ;   ;Contact Person No.  ;Code20        ;TableRelation=Contact.No. WHERE (Company No.=FIELD(Contact No.),
                                                                                    Type=CONST(Person));
                                                   OnValidate=BEGIN
                                                                IF "Contact Person No." <> '' THEN BEGIN
                                                                  TESTFIELD("Contact No.");
                                                                  ContactRec.GET("Contact Person No.");
                                                                  ContactRec.TESTFIELD(Blocked, FALSE);
                                                                  IF ContactRec."Salesperson Code" <> '' THEN
                                                                    VALIDATE("Salesperson Code",ContactRec."Salesperson Code")
                                                                  ELSE
                                                                    ValidateCompanySalesPerson;
                                                                END ELSE
                                                                  VALIDATE("Contact No.");
                                                              END;

                                                   CaptionML=[ENU=Contact Person No.;
                                                              NOR=Kontaktperson;
                                                              SVE=Kontaktperson] }
    { 13  ;   ;Salesperson Code    ;Code20        ;TableRelation=Salesperson/Purchaser;
                                                   OnLookup=VAR
                                                              SalespersonByContact@1100528702 : Record 11012760;
                                                              SalespersonPurchaser@1100528701 : Record 13;
                                                              Marked@1100528700 : Integer;
                                                            BEGIN
                                                              IF ("Contact No." <> '') OR ("Contact Person No." <> '') THEN BEGIN
                                                                IF "Contact Person No." <> '' THEN
                                                                  SalespersonByContact.SETRANGE("Contact No.", "Contact Person No.")
                                                                ELSE
                                                                  SalespersonByContact.SETRANGE("Contact No.", "Contact No.");
                                                                IF SalespersonByContact.FINDSET THEN
                                                                  REPEAT
                                                                    SalespersonPurchaser.Code := SalespersonByContact."Salesperson Code";
                                                                    SalespersonPurchaser.MARK(TRUE);
                                                                    Marked := Marked + 1;
                                                                  UNTIL SalespersonByContact.NEXT = 0;
                                                                IF Marked > 0 THEN
                                                                  SalespersonPurchaser.MARKEDONLY(TRUE);
                                                              END;

                                                              IF PAGE.RUNMODAL(0, SalespersonPurchaser) = ACTION::LookupOK THEN
                                                                VALIDATE("Salesperson Code", SalespersonPurchaser.Code);
                                                            END;

                                                   CaptionML=[ENU=Salesperson Code;
                                                              NOR=Selgerkode;
                                                              SVE=S„ljarkod] }
    { 15  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 17  ;   ;Contact Type        ;Code20        ;TableRelation="Contact Type".Code;
                                                   CaptionML=[ENU=Contact Type;
                                                              NOR=Kontakttype;
                                                              SVE=Kontakttyp] }
    { 18  ;   ;Contact Name        ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              NOR=Kontaktnavn;
                                                              SVE=Kontaktnamn];
                                                   Editable=No }
    { 20  ;   ;Contact Person Name ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Person No.)));
                                                   CaptionML=[ENU=Contact Person Name;
                                                              NOR=Kontaktpersonens navn;
                                                              SVE=Kontaktpersonens namn];
                                                   Editable=No }
    { 21  ;   ;Try-out Quote Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Try-out Quote".Description WHERE (Code=FIELD(Try-out Quote)));
                                                   CaptionML=[ENU=Try-out Quote Description;
                                                              NOR=Beskrivelse av tilbudsforesp›rsel;
                                                              SVE=Beskrivning av anbudsf”rfr†gan];
                                                   Editable=No }
    { 22  ;   ;Contact Type Description;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Contact Type".Description WHERE (Code=FIELD(Contact Type)));
                                                   CaptionML=[ENU=Contact Type Description;
                                                              NOR=Beskrivelse av Kontakttype;
                                                              SVE=Kontakttypsbeskrivning];
                                                   Editable=No }
    { 30  ;   ;Salesperson Name    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Salesperson/Purchaser.Name WHERE (Code=FIELD(Salesperson Code)));
                                                   CaptionML=[ENU=Salesperson Name;
                                                              NOR=Selgernavn;
                                                              SVE=S„ljarnamn];
                                                   Editable=No }
    { 40  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 45  ;   ;Contact Filter      ;Code20        ;CaptionML=[ENU=Contact Filter;
                                                              NOR=Kontaktfilter;
                                                              SVE=Kontaktfilter];
                                                   Editable=No }
    { 50  ;   ;Contact Phone       ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Contact Filter)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Contact Phone;
                                                              NOR=Kontaktpersonens telefonnummer;
                                                              SVE=Kontaktpersonens telefonnummer];
                                                   Editable=No }
    { 51  ;   ;Contact Phone 2     ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No. 2" WHERE (No.=FIELD(Contact Filter)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Contact Phone 2;
                                                              NOR=Kontaktpersonens telefonnummer 2;
                                                              SVE=Kontaktpersonens telefonnummer 2];
                                                   Editable=No }
    { 52  ;   ;Contact Mobile Phone;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Mobile Phone No." WHERE (No.=FIELD(Contact Filter)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Contact Mobile Phone;
                                                              NOR=Kontaktmobilnummer;
                                                              SVE=Kontaktmobilnummer];
                                                   Editable=No }
    { 53  ;   ;Contact Fax         ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Fax No." WHERE (No.=FIELD(Contact Filter)));
                                                   CaptionML=[ENU=Contact Fax;
                                                              NOR=Kontaktfaks;
                                                              SVE=Kontaktfax];
                                                   Editable=No }
    { 54  ;   ;Contact E-Mail      ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.E-Mail WHERE (No.=FIELD(Contact Filter)));
                                                   CaptionML=[ENU=Contact E-Mail;
                                                              NOR=Kontakt e-mail;
                                                              SVE=Kontakte-post];
                                                   Editable=No }
    { 70  ;   ;Status              ;Code20        ;TableRelation="Try-out Status".Code WHERE (Type=CONST("Try-out (Quote) Line"));
                                                   OnLookup=VAR
                                                              TryoutStatus@1210190000 : Record 11012759;
                                                              TryoutLineStatuses@1210190001 : Page 11126074;
                                                            BEGIN
                                                              TryoutStatus.SETRANGE(Code, Status);
                                                              IF TryoutStatus.FINDFIRST THEN
                                                                TryoutLineStatuses.SETRECORD(TryoutStatus);
                                                              TryoutStatus.SETRANGE(Code);
                                                              TryoutLineStatuses.SETTABLEVIEW(TryoutStatus);
                                                              TryoutLineStatuses.LOOKUPMODE(TRUE);
                                                              IF TryoutLineStatuses.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                TryoutLineStatuses.GETRECORD(TryoutStatus);
                                                                VALIDATE(Status, TryoutStatus.Code);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status] }
    { 80  ;   ;Job Title           ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Job Title" WHERE (No.=FIELD(Contact Person No.)));
                                                   CaptionML=[ENU=Job Title;
                                                              SVE=Yrkestitel];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Try-out Quote,Line No.                  ;Clustered=Yes }
    {    ;Contact No.,Contact Person No.           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProspectRec@11012000 : Record 5050;
      ContactRec@11012001 : Record 5050;
      TryOutQouteRec@11012003 : Record 11012758;

    PROCEDURE ValidateCompanySalesPerson@1210190001();
    BEGIN
      ProspectRec.GET("Contact No.");

      IF ProspectRec."Salesperson Code" <> '' THEN BEGIN
        VALIDATE("Salesperson Code",ProspectRec."Salesperson Code");
      END ELSE
        IF TryOutQouteRec.GET("Try-out Quote") THEN BEGIN
          VALIDATE("Salesperson Code",TryOutQouteRec."Salesperson Code");
        END;
    END;

    PROCEDURE UpdateModificationData@1100485000();
    BEGIN
      IF TryOutQouteRec.GET("Try-out Quote")THEN BEGIN
        TryOutQouteRec."Change Date" := TODAY;
        TryOutQouteRec."Changed By" := USERID;
        TryOutQouteRec.MODIFY(TRUE)
      END;
    END;

    PROCEDURE ShowTodos@1100528700();
    VAR
      Todo@1100528700 : Record 5080;
    BEGIN
      SetToDoView(Todo);
      PAGE.RUNMODAL(PAGE::"Task List", Todo);
    END;

    PROCEDURE CreateToDo@1100529600();
    VAR
      Todo@1100528700 : Record 5080;
      TempToDo@1100529601 : TEMPORARY Record 5080;
    BEGIN
      SetToDoView(Todo);
      TempToDo.CreateTaskFromTask(Todo);
    END;

    LOCAL PROCEDURE SetToDoView@1100529603(VAR Todo@1100529601 : Record 5080);
    VAR
      Contact@1100529603 : Record 5050;
      TryoutQuote@1100529602 : Record 11012758;
      RecRef@1100529600 : RecordRef;
    BEGIN
      IF "Contact Person No." <> '' THEN
        Contact.GET("Contact Person No.")
      ELSE
        Contact.GET("Contact No.");

      RecRef.GETTABLE(Todo);
      Contact.SetContactFilterOnNAVTable(
        RecRef, Todo.FIELDNAME("Contact No."), Todo.FIELDNAME("Contact Company No."));
      Todo.SETVIEW(RecRef.GETVIEW(FALSE));

      Todo.SETCURRENTKEY("Contact Company No.",Date,"Contact No.",Closed);
      Todo.SETRANGE("Try-out Quote No.", "Try-out Quote");
      IF TryoutQuote.GET("Try-out Quote") THEN
        IF TryoutQuote."Try-out No." <> '' THEN
          Todo.SETRANGE("Try-out No.", TryoutQuote."Try-out No.");
      Todo.SETRANGE("System To-do Type", Todo."System To-do Type"::"Contact Attendee");
    END;

    BEGIN
    END.
  }
}

