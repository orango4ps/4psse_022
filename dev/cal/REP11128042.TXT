OBJECT Report 11128042 Copy Proj.Princ. C O S. Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PSSE;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   CostObjectCode := 'COSTOBJECT';
                 END;

  }
  DATASET
  {
    { 7494;    ;DataItem;                    ;
               DataItemTable=Table11012005;
               OnPreDataItem=BEGIN
                               NoOfUpdates := 0;

                               IF NewStartingDate = 0D THEN
                                 ERROR(Text003);

                               ToProjFilter := GETFILTER("Project No.");
                               IF (FromProjNo = '') AND (ToProjFilter = '') THEN
                                 ERROR(Text002);

                               ToPrincipalFilter := GETFILTER(Principal);
                               IF (FromPrincipal = '') AND (ToPrincipalFilter = '') THEN
                                 ERROR(Text008);


                               IF FromProjNo = '' THEN
                                 UseBasePriceTable := TRUE
                               ELSE
                                 UseBasePriceTable := FALSE;

                               IF ToProjFilter = '' THEN
                                 SETRANGE("Project No.",FromProjNo);

                               IF ToPrincipalFilter = '' THEN
                                 SETRANGE(Principal,FromPrincipal);

                               CostObjectBuffer.DELETEALL;

                               IF UseBasePriceTable THEN BEGIN

                                 //////////////////////////////////////////////
                                 // From Base table
                                 IF CostObjectSalesPrice.FINDSET THEN REPEAT
                                   IF NOT CostObjectBuffer.GET(CostObjectCode,CostObjectSalesPrice."Cost Object") THEN BEGIN
                                     CostObjectBuffer.INIT;
                                     CostObjectBuffer."Dimension Code" := CostObjectCode;
                                     CostObjectBuffer.Code := CostObjectSalesPrice."Cost Object";
                                     CostObjectBuffer.INSERT;
                                   END;
                                 UNTIL CostObjectSalesPrice.NEXT = 0;


                               END ELSE BEGIN

                                 //////////////////////////////////////////////
                                 // From within same table
                                 IF From_ProjPrincCostOSPrice.FINDSET THEN REPEAT

                                   IF NOT CostObjectBuffer.GET(CostObjectCode,From_ProjPrincCostOSPrice."Cost Object") THEN BEGIN
                                     CostObjectBuffer.INIT;
                                     CostObjectBuffer."Dimension Code" := CostObjectCode;
                                     CostObjectBuffer.Code := From_ProjPrincCostOSPrice."Cost Object";
                                     CostObjectBuffer.INSERT;
                                   END;
                                 UNTIL From_ProjPrincCostOSPrice.NEXT = 0;

                               END;

                               ProjPrincCostOSPriceBuffer.DELETEALL;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF UseBasePriceTable THEN BEGIN

                                    //////////////////////////////////////////////
                                    // From Base price table
                                    CostObjectSalesPrice.RESET;
                                    CostObjectBuffer.RESET;

                                    IF CostObjectBuffer.FINDSET THEN REPEAT

                                      // Get the latest price
                                      CostObjectSalesPrice.SETRANGE("Cost Object",CostObjectBuffer.Code);
                                      IF CostObjectSalesPrice.FINDLAST THEN BEGIN

                                        // Insert new line with new date and price in the buffer
                                        ProjPrincCostOSPriceBuffer.INIT;
                                        ProjPrincCostOSPriceBuffer.VALIDATE("Project No.","Project No.");
                                        ProjPrincCostOSPriceBuffer.VALIDATE("Principal No.",Principal);
                                        ProjPrincCostOSPriceBuffer.VALIDATE("Cost Object",CostObjectBuffer.Code);
                                        ProjPrincCostOSPriceBuffer.VALIDATE("Starting Date",NewStartingDate);

                                        CASE UpdateOption OF
                                        UpdateOption::"Fixed Amount":
                                          NewAmount := CostObjectSalesPrice."Sales Price" + NewValue;
                                        UpdateOption::Percent:
                                          NewAmount := CostObjectSalesPrice."Sales Price" * ((100 + NewValue) / 100);
                                        END;

                                        IF RoundAmount <> 0 THEN
                                          NewAmount := ROUND(NewAmount, RoundAmount)
                                        ELSE
                                          NewAmount := ROUND(NewAmount, 0.01);

                                        IF NewAmount < 0 THEN
                                          IF NOT CONFIRM(STRSUBSTNO(Text001,
                                            NewAmount,
                                            ProjPrincCostOSPriceBuffer."Project No.",
                                            ProjPrincCostOSPriceBuffer."Principal No.",
                                            ProjPrincCostOSPriceBuffer."Cost Object")) THEN
                                            ERROR(Text007);

                                        ProjPrincCostOSPriceBuffer.VALIDATE("Sales Price",NewAmount);
                                        ProjPrincCostOSPriceBuffer.INSERT;

                                      END;

                                    UNTIL CostObjectBuffer.NEXT = 0;

                                  END ELSE BEGIN

                                    //////////////////////////////////////////////
                                    // From within the same table
                                    From_ProjPrincCostOSPrice.RESET;
                                    CostObjectBuffer.RESET;

                                    IF CostObjectBuffer.FINDSET THEN REPEAT

                                      // Get the latest price
                                      From_ProjPrincCostOSPrice.SETRANGE("Project No.",FromProjNo);
                                      From_ProjPrincCostOSPrice.SETRANGE("Principal No.",FromPrincipal);
                                      From_ProjPrincCostOSPrice.SETRANGE("Cost Object",CostObjectBuffer.Code);
                                      IF From_ProjPrincCostOSPrice.FINDLAST THEN BEGIN

                                        // Insert new line with new date and price in the buffer
                                        // The buffer is used so that no lines are inserting to the table in the loop
                                        // and interrupting the last find price
                                        ProjPrincCostOSPriceBuffer.INIT;
                                        ProjPrincCostOSPriceBuffer.VALIDATE("Project No.","Project No.");
                                        ProjPrincCostOSPriceBuffer.VALIDATE("Principal No.",Principal);
                                        ProjPrincCostOSPriceBuffer.VALIDATE("Cost Object",CostObjectBuffer.Code);
                                        ProjPrincCostOSPriceBuffer.VALIDATE("Starting Date",NewStartingDate);

                                        CASE UpdateOption OF
                                        UpdateOption::"Fixed Amount":
                                          NewAmount := From_ProjPrincCostOSPrice."Sales Price" + NewValue;
                                        UpdateOption::Percent:
                                          NewAmount := From_ProjPrincCostOSPrice."Sales Price" * ((100 + NewValue) / 100);
                                        END;

                                        IF RoundAmount <> 0 THEN
                                          NewAmount := ROUND(NewAmount, RoundAmount)
                                        ELSE
                                          NewAmount := ROUND(NewAmount, 0.01);

                                        IF NewAmount < 0 THEN
                                          IF NOT CONFIRM(STRSUBSTNO(Text001,
                                            NewAmount,
                                            ProjPrincCostOSPriceBuffer."Project No.",
                                            ProjPrincCostOSPriceBuffer."Principal No.",
                                            ProjPrincCostOSPriceBuffer."Cost Object")) THEN
                                            ERROR(Text007);

                                        ProjPrincCostOSPriceBuffer.VALIDATE("Sales Price",NewAmount);
                                        ProjPrincCostOSPriceBuffer.INSERT;

                                      END;

                                    UNTIL CostObjectBuffer.NEXT = 0;

                                  END;
                                END;

               OnPostDataItem=BEGIN
                                // Update the real table from the buffer
                                ProjPrincCostOSPriceBuffer.RESET;
                                IF ProjPrincCostOSPriceBuffer.FINDSET THEN REPEAT
                                  To_ProjPrincCostOSPrice.SETRANGE("Project No.",ProjPrincCostOSPriceBuffer."Project No.");
                                  To_ProjPrincCostOSPrice.SETRANGE("Principal No.",ProjPrincCostOSPriceBuffer."Principal No.");
                                  To_ProjPrincCostOSPrice.SETRANGE("Cost Object",ProjPrincCostOSPriceBuffer."Cost Object");
                                  To_ProjPrincCostOSPrice.SETRANGE("Starting Date",ProjPrincCostOSPriceBuffer."Starting Date");

                                  IF NOT To_ProjPrincCostOSPrice.FINDFIRST THEN BEGIN
                                    To_ProjPrincCostOSPrice.VALIDATE("Project No.",ProjPrincCostOSPriceBuffer."Project No.");
                                    To_ProjPrincCostOSPrice.VALIDATE("Principal No.",ProjPrincCostOSPriceBuffer."Principal No.");
                                    To_ProjPrincCostOSPrice.VALIDATE("Cost Object",ProjPrincCostOSPriceBuffer."Cost Object");
                                    To_ProjPrincCostOSPrice.VALIDATE("Starting Date",ProjPrincCostOSPriceBuffer."Starting Date");
                                    To_ProjPrincCostOSPrice.VALIDATE("Sales Price",ProjPrincCostOSPriceBuffer."Sales Price");
                                    To_ProjPrincCostOSPrice.INSERT(TRUE);
                                    NoOfUpdates += 1;
                                  END;

                                UNTIL ProjPrincCostOSPriceBuffer.NEXT = 0;

                                MESSAGE(Text006,NoOfUpdates);
                              END;

               ReqFilterFields=Project No.,Principal }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100285005;;Container;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  ContainerType=ContentArea }

      { 1100285004;1;Field  ;
                  CaptionML=[ENU=Update Option;
                             NOR=Oppdater Alternativ;
                             SVE=Uppdateringsalternativ];
                  OptionCaptionML=[ENU=Fixed Amount,Percent;
                                   NOR=Fast bel›p,Prosent;
                                   SVE=Fast belopp,Procent];
                  SourceExpr=UpdateOption }

      { 1100285003;1;Field  ;
                  CaptionML=[ENU=From Proj. No.;
                             NOR=Fra prosjektnr;
                             SVE=Fr†nprojektnr];
                  SourceExpr=FromProjNo;
                  TableRelation=Job;
                  OnValidate=BEGIN
                               //IF FromProjNo <> '' THEN BEGIN
                               //  From_ProjPrincCostOSPrice.SETRANGE("Project No.",FromProjNo);
                               //  From_ProjPrincCostOSPrice.SETRANGE("Principal No.",FromPrincipal);
                               //  IF NOT From_ProjPrincCostOSPrice.FINDLAST THEN
                               //    ERROR(STRSUBSTNO(Text005,FromProjNo,FromPrincipal));
                               //END;
                             END;
                              }

      { 1100285006;1;Field  ;
                  CaptionML=[ENU=From Principal;
                             NOR=Fra Oppdragsgiver;
                             SVE=Fr†nuppdragsgivare];
                  SourceExpr=FromPrincipal;
                  TableRelation="Project Principal".Principal }

      { 1100285002;1;Field  ;
                  CaptionML=[ENU=Update w. Amount/Percentage;
                             NOR=Oppdater med bel›p/Prosent;
                             SVE=Uppdatera med belopp/procent];
                  SourceExpr=NewValue }

      { 1100285001;1;Field  ;
                  CaptionML=[ENU=Round Amount;
                             NOR=Avrundet bel›p;
                             SVE=Avrundat belopp];
                  SourceExpr=RoundAmount }

      { 1100285000;1;Field  ;
                  CaptionML=[ENU=New Starting Date;
                             NOR=Ny Startdato;
                             SVE=Nytt startdatum];
                  SourceExpr=NewStartingDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      From_ProjPrincCostOSPrice@1100285024 : Record 11020255;
      ProjPrincCostOSPriceBuffer@1100285013 : TEMPORARY Record 11020255;
      To_ProjPrincCostOSPrice@1100285015 : Record 11020255;
      CostObjectSalesPrice@1100285012 : Record 11020351;
      CostObjectBuffer@1100285011 : TEMPORARY Record 349;
      ToProjFilter@1100285010 : Text[250];
      ToPrincipalFilter@1100285021 : Text[250];
      FromProjNo@1100285009 : Code[20];
      FromPrincipal@1100285023 : Code[20];
      CostObjectCode@1100285008 : Code[20];
      UpdateOption@1100285007 : 'Fixed Amount,Percent';
      NoOfUpdates@1100285006 : Integer;
      NewAmount@1100285005 : Decimal;
      NewValue@1100285004 : Decimal;
      RoundAmount@1100285003 : Decimal;
      NewStartingDate@1100285002 : Date;
      UseBasePriceTable@1100285001 : Boolean;
      Text001@1100285020 : TextConst 'ENU=There will be a negative amount (%1) for Project No. %2, Principal %3, Cost Object %4. Would you like to proceed anyway?;NOR=Bel›pet kommer til † bli negativt (%1) for prosjektnr. %2, Oppdragsgiver %3, Kostobjekt %4. Vil du fortsette likevel?;SVE=Beloppet kommer att bli negativt (%1) f”r projektnr %2, uppdragsgivare %3, kostnadsobjekt %4. Vill du „nd† forts„tta?';
      Text002@1100285019 : TextConst 'ENU=From Project No. cannot be blank if To Project No. is blank.;NOR=Fra prosjektnr kan ikke v‘re blankt hivs Til prosjektnr er blankt.;SVE=Fr†nprojektnr f†r inte l„mnas blankt om Tillprojektnr inte anges.';
      Text003@1100285018 : TextConst 'ENU=New Starting Date must not be blank.;NOR=Ny Startdato kan ikke v‘re blank.;SVE=Nytt startdatum f†r inte vara blankt.';
      Text005@1100285017 : TextConst 'ENU=There is no information for Project No. %1, Principal %2 in the table.;NOR=Det finnes ingen informasjon i tabellen for prosjektnr. %1, Oppdragsgiver %2.;SVE=Det finns ingen information i tabellen f”r projektnr %1, uppdragsgivare %2.';
      Text006@1100285016 : TextConst 'ENU=%1 new line(s) were inserted.;NOR=%1 ny(e) rad(er) ble satt inn.;SVE=%1 ny(a) rad(er) skapades.';
      Text007@1100285014 : TextConst 'ENU=The operation was aborted.;NOR=Operasjonen ble avbrutt.;SVE=Operationen avbr”ts.';
      Text008@1100285022 : TextConst 'ENU=From Principal cannot be blank if To Principal is blank.;NOR=Fra Oppdragsgiver kan ikke v‘re blankt hvis Til Oppdragsgiver er blankt.;SVE=Fr†nuppdragsgivare f†r inte l„mnas blankt om Tilluppdragsgivare inte anges.';

    BEGIN
    {
      4PSSE
      130908 ITERO.LP New ProcessingOnly report to copy data.
    }
    END.
  }
  RDLDATA
  {
  }
}

