OBJECT Table 11072669 Rental Quantity Discount
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckAll();
               DetermineDiscountLevel();
               IF Level = 0 THEN
                 ERROR(Text000);
             END;

    OnRename=BEGIN
               CheckAll();
               DetermineDiscountLevel();
               IF Level = 0 THEN
                 ERROR(Text000);
             END;

    CaptionML=ENU=Rental Quantity Discount;
    LookupPageID=Page11125935;
    DrillDownPageID=Page11125935;
  }
  FIELDS
  {
    { 10  ;   ;Customer Discount Group;Code20     ;TableRelation="Customer Discount Group";
                                                   OnValidate=BEGIN
                                                                CheckCustomerDiscGroup();
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=[ENU=Customer Discount Group;
                                                              NOR=Kunderabattgruppe;
                                                              SVE=Kundrabattgrupp] }
    { 20  ;   ;Location            ;Code20        ;TableRelation="Plant Location";
                                                   OnValidate=BEGIN
                                                                CheckLocation();
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=[ENU=Location;
                                                              NOR=Plassering;
                                                              SVE=Lagerst„lle] }
    { 30  ;   ;Group Level         ;Code20        ;TableRelation="Grouping Level";
                                                   OnValidate=BEGIN
                                                                CheckPlantGroupLevel();
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=[ENU=Group Level;
                                                              NOR=Gruppeniv†;
                                                              SVE=Gruppniv†] }
    { 40  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=VAR
                                                                lvPlantNoRec@1100528800 : Record 11012552;
                                                                PlantType@1100525000 : Record 11012551;
                                                              BEGIN
                                                                PlantType.GET("Plant Type");
                                                                IF PlantType.Bulk THEN
                                                                  ERROR(Text002);
                                                                CheckPlantType();
                                                                IF "Plant No." <> '' THEN BEGIN
                                                                  IF ("Plant Type" = '') OR (("Plant Type" <> xRec."Plant Type") AND (NOT lvPlantNoRec.GET("Plant Type","Plant No."))) THEN
                                                                    "Plant No." := '';
                                                                END;
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 50  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                CheckPlantNo();
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr] }
    { 60  ;   ;Level               ;Option        ;CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   OptionCaptionML=[ENU=" ,Group Level,Plant Type,Plant No.,Customer Discount Group,Customer-Group Level,Customer-Plant Type,Customer-Plant No.,Location,Location-Group Level,Location-Plant Type,Location-Plant No.";
                                                                    NOR=" ,Gruppeniv†,Maskintype,Maskinnr.,Kunderabattgruppe,Kunde-Gruppeniv†,Kunde-Maskintype,Kunde-Maskinnr.,Plassering,Plassering-Gruppeniv†,Plassering-Maskintype,Plassering-Maskinnr.";
                                                                    SVE=" ,Gruppniv†,Maskintyp,Maskinnr,Kundrabattgrupp,Kundgruppsniv†,KundMaskinstyp,KundMaskinnr,Lagerst„lle,Lagerst„llets gruppniv†,Lagerst„llets Maskinstyp,Lagerst„llets Maskinnr"];
                                                   OptionString=[ ,PlantGroup,PlantType,PlantNo,Customer,CustomerPlantGroup,CustomerPlantType,CustomerPlantNo,Location,LocationPlantGroup,LocationPlantType,LocationPlantNo];
                                                   Editable=No }
    { 70  ;   ;From Number of Days ;Integer       ;CaptionML=ENU=From Number of Days;
                                                   MinValue=0 }
    { 80  ;   ;% of Unit Price     ;Decimal       ;CaptionML=ENU=% Of Unit Price;
                                                   MinValue=0 }
  }
  KEYS
  {
    {    ;Customer Discount Group,Location,Group Level,Plant Type,Plant No.,From Number of Days;
                                                   Clustered=Yes }
    {    ;From Number of Days,Level                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528800 : TextConst 'ENU=Unknown Discount Level.;NOR=Ukjent Rabattniv†.;SVE=Ok„nd rabattniv†.';
      Text001@1100528801 : TextConst 'ENU=''%1'' and ''%2'' may not be filled both.;NOR=%1'' og ''%2'' kan ikke begge fylles ut.;SVE=''%1'' och ''%2'' kan inte b†da fyllas i.';
      Text002@1100525000 : TextConst 'ENU=Rental Quantity Discount not allowed for Bulk plant';

    PROCEDURE CheckAll@1100528800();
    BEGIN
      CheckCustomerDiscGroup();
      CheckLocation();
      CheckPlantGroupLevel();
      CheckPlantType();
      CheckPlantNo();
    END;

    PROCEDURE CheckCustomerDiscGroup@1100528801();
    BEGIN
      IF ("Customer Discount Group" <> '') AND (Location <> '') THEN
        ERROR(Text001, FIELDCAPTION("Customer Discount Group"), FIELDCAPTION(Location));
    END;

    PROCEDURE CheckLocation@1100528802();
    BEGIN
      IF (Location <> '') AND ("Customer Discount Group" <> '') THEN
        ERROR(Text001, FIELDCAPTION(Location), FIELDCAPTION("Customer Discount Group"));
    END;

    PROCEDURE CheckPlantGroupLevel@1100528803();
    BEGIN
      IF ("Group Level" <> '') AND ("Plant Type" <> '') THEN
        ERROR(Text001, FIELDCAPTION("Group Level"), FIELDCAPTION("Plant Type"));
    END;

    PROCEDURE CheckPlantType@1100528804();
    BEGIN
      IF ("Plant Type" <> '') AND ("Group Level" <> '') THEN
        ERROR(Text001, FIELDCAPTION("Plant Type"), FIELDCAPTION("Group Level"));
    END;

    PROCEDURE CheckPlantNo@1100528805();
    BEGIN
      IF "Plant No." <> '' THEN
        TESTFIELD("Plant Type");
    END;

    PROCEDURE DetermineDiscountLevel@1100528806();
    BEGIN
      CASE TRUE OF
        (Location <> '') AND ("Plant Type" <> '') AND ("Plant No." <> ''):
          Level := Level::LocationPlantNo;
        (Location <> '') AND ("Plant Type" <> '') AND ("Plant No." = ''):
          Level := Level::LocationPlantType;
        (Location <> '') AND ("Group Level" <> ''):
          Level := Level::LocationPlantGroup;
        (Location <> '') :
          Level := Level::Location;
        ("Customer Discount Group" <> '') AND ( "Plant Type" <> '') AND ( "Plant No." <> ''):
          Level := Level::CustomerPlantNo;
        ("Customer Discount Group" <> '') AND ( "Plant Type" <> '') AND ( "Plant No." = ''):
          Level := Level::CustomerPlantType;
        ("Customer Discount Group" <> '') AND ( "Group Level" <> ''):
          Level := Level::CustomerPlantGroup;
        ("Customer Discount Group" <> '') :
          Level := Level::Customer;
        ("Plant Type" <> '') AND ("Plant No." <> ''):
          Level := Level::PlantNo;
        ("Plant Type" <> ''):
          Level := Level::PlantType;
        ("Group Level" <> ''):
          Level := Level::PlantGroup;
        ELSE
          Level := 0;
      END;
    END;

    BEGIN
    END.
  }
}

