OBJECT Table 11072340 DPT Resp Pers Perm ServLoc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CALCFIELDS("Subsite Structure");
               IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    OnDelete=BEGIN
               CheckRecordChangeAllowed;
             END;

    OnRename=BEGIN
               CheckRecordChangeAllowed;
             END;

    CaptionML=ENU=DPT Responsible Person Permission (Service);
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=,Access;
                                                   OptionString=,Access;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Service Location No.;Code20        ;TableRelation="Service Location".No.;
                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Line No.            ;Integer       ;TableRelation="Service Responsible Person DPT"."Line No." WHERE (Source Type=CONST(ServLocation),
                                                                                                                    Source Code=FIELD(Service Location No.));
                                                   CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer];
                                                   NotBlank=Yes }
    { 31  ;   ;Responsibility      ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Responsible Person DPT".Responsibility WHERE (Source Type=CONST(ServLocation),
                                                                                                                             Source Code=FIELD(Service Location No.),
                                                                                                                             Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Responsibility;
                                                              NOR=Ansvar;
                                                              SVE=Ansvar];
                                                   Editable=No }
    { 32  ;   ;Responsibility Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Responsibility.Description WHERE (Code=FIELD(Responsibility)));
                                                   CaptionML=[ENU=Responsibility Description;
                                                              NOR=Ansvar, Beskrivelse;
                                                              SVE=Ansvar, beskrivning];
                                                   Editable=No }
    { 33  ;   ;Employee No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Responsible Person DPT"."Employee No." WHERE (Source Type=CONST(ServLocation),
                                                                                                                             Source Code=FIELD(Service Location No.),
                                                                                                                             Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=Anst„llningsnr];
                                                   Editable=No }
    { 34  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn p† ansatt;
                                                              SVE=Namn p† anst„lld];
                                                   Editable=No }
    { 40  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Subsite ServLoc".Code WHERE (Service Location No.=FIELD(Service Location No.));
                                                   CaptionML=ENU=Subsite Code }
    { 41  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite ServLoc".Description WHERE (Service Location No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 42  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite ServLoc"."Subsite Structure" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 50  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     Subsite Structure=FILTER(>OneDocLibrary)) "DPT Subsite Doc Type ServLoc"."Document Type" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                                                                     Subsite Code=FIELD(Subsite Code));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 51  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 70  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" = "Permission Level"::Owner) AND ("Subsite Code" <> '') AND ("Document Type" = '') THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
    { 80  ;   ;Show in View OLD    ;Boolean       ;CaptionML=;
                                                   Editable=No }
    { 90  ;   ;Subsite Synced On   ;DateTime      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite ServLoc"."Synchronized On" WHERE (Service Location No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Subsite Synchronized On;
                                                   Editable=No }
    { 100 ;   ;Subsite Doc Type Synced On;DateTime;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Doc Type ServLoc"."Synchronized On" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                              Subsite Code=FIELD(Subsite Code),
                                                                                                                              Document Type=FIELD(Document Type)));
                                                   CaptionML=ENU=Subsite Document Type Synchronized On;
                                                   Editable=No }
    { 110 ;   ;Private Permission Level;Option    ;OnValidate=BEGIN
                                                                TESTFIELD("Subsite Code");
                                                                TESTFIELD("Document Type", '');
                                                                IF ("Permission Level" = "Permission Level"::Owner) THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  FIELDERROR("Private Permission Level");
                                                              END;

                                                   CaptionML=ENU=Private Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
  }
  KEYS
  {
    {    ;Type,Service Location No.,Line No.,Subsite Code,Document Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100409000 : TextConst 'ENU=You have no permission to change Portal Permission Records.';
      Text001@1100528300 : TextConst 'ENU=%1 Permission Records added and %2 Permission Records modified.';
      AddCounter@1100528302 : Integer;
      ModifyCounter@1100528303 : Integer;

    PROCEDURE CheckPermissions@1100409007();
    BEGIN
      CheckModifyPermission;
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100528303();
    VAR
      DPTRespPersPermServLoc@1100528300 : Record 11072340;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      IF (("Subsite Code" = '') AND ("Document Type" = '') AND ("Permission Level" = "Permission Level"::None)) THEN BEGIN
        DPTRespPersPermServLoc.SETRANGE(Type , DPTRespPersPermServLoc.Type::Access);
        DPTRespPersPermServLoc.SETRANGE("Service Location No.", "Service Location No.");
        DPTRespPersPermServLoc.SETRANGE("Line No.", "Line No.");
        DPTRespPersPermServLoc.SETFILTER("Subsite Code", '<>%1', "Subsite Code");
        DPTRespPersPermServLoc.MODIFYALL("Permission Level", "Permission Level"::None);
        EXIT;
      END;
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) THEN
        EXIT;

      IF ("Document Type" = '') THEN BEGIN
        DPTRespPersPermServLoc.SETRANGE(Type, Type);
        DPTRespPersPermServLoc.SETRANGE("Service Location No.", "Service Location No.");
        DPTRespPersPermServLoc.SETRANGE("Line No.", "Line No.");
        DPTRespPersPermServLoc.SETRANGE("Subsite Code", "Subsite Code");
        DPTRespPersPermServLoc.SETFILTER("Document Type", '<>%1', "Document Type");
        DPTRespPersPermServLoc.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        DPTRespPersPermServLoc.GET(Type, "Service Location No.", "Line No.", "Subsite Code", '');
        IF (DPTRespPersPermServLoc."Permission Level" = DPTRespPersPermServLoc."Permission Level"::None) THEN BEGIN
          "Permission Level" := "Permission Level"::Visitor;
        END;
        IF (DPTRespPersPermServLoc."Permission Level" < "Permission Level") THEN BEGIN
          DPTRespPersPermServLoc."Permission Level" := "Permission Level";
          DPTRespPersPermServLoc.MODIFY;
        END;
      END;
    END;

    PROCEDURE CheckModifyPermission@1100409000();
    VAR
      UserSetup@1100409000 : Record 91;
      ServiceResponsiblePersonDPT@1100409001 : Record 11072297;
      DPTRespPersPermServLoc@1100409002 : Record 11072340;
    BEGIN
      IF (NOT UserSetup.GET(USERID)) THEN
        ERROR(Text000);
      UserSetup.TESTFIELD("Employee No.");
      ServiceResponsiblePersonDPT.SETRANGE("Source Type", ServiceResponsiblePersonDPT."Source Type"::ServLocation);
      ServiceResponsiblePersonDPT.SETRANGE("Source Code", "Service Location No.");
      ServiceResponsiblePersonDPT.SETRANGE("Employee No.", UserSetup."Employee No.");
      IF (NOT ServiceResponsiblePersonDPT.FINDSET) THEN
        ERROR(Text000);
      REPEAT
        DPTRespPersPermServLoc.SETRANGE(Type, DPTRespPersPermServLoc.Type::Access);
        DPTRespPersPermServLoc.SETRANGE("Service Location No.", ServiceResponsiblePersonDPT."Source Code");
        DPTRespPersPermServLoc.SETRANGE("Line No.", ServiceResponsiblePersonDPT."Line No.");
        DPTRespPersPermServLoc.SETFILTER("Employee No.", '<>%1', ServiceResponsiblePersonDPT."Employee No.");
        DPTRespPersPermServLoc.SETFILTER("Permission Level", '<>%1', DPTRespPersPermServLoc."Permission Level"::Owner);
        IF (DPTRespPersPermServLoc.FINDFIRST) THEN
          ERROR(Text000);
      UNTIL (ServiceResponsiblePersonDPT.NEXT = 0);
    END;

    PROCEDURE AddPortalPermissions@1100528300(ServiceLocationNo@1100528300 : Code[20];LineNoFilter@1100528307 : Integer;SubsiteCodeFilter@1100528306 : Code[20];ShowMessage@1100528308 : Boolean);
    VAR
      ServiceResponsiblePersonDPT@1100528301 : Record 11072297;
      ServiceLocation@1100528304 : Record 11012801;
    BEGIN
      AddCounter := 0;
      ModifyCounter := 0;
      ServiceLocation.GET(ServiceLocationNo);

      ServiceResponsiblePersonDPT.SETRANGE("Source Type", ServiceResponsiblePersonDPT."Source Type"::ServLocation);
      ServiceResponsiblePersonDPT.SETRANGE("Source Code", ServiceLocationNo);
      IF (LineNoFilter <> 0) THEN
        ServiceResponsiblePersonDPT.SETRANGE("Line No.", LineNoFilter);
      IF (NOT ServiceResponsiblePersonDPT.FINDSET) THEN
        EXIT;

      REPEAT
        AddPortalPermission(ServiceResponsiblePersonDPT, SubsiteCodeFilter);
      UNTIL (ServiceResponsiblePersonDPT.NEXT = 0);
      IF (ShowMessage) THEN
        MESSAGE(Text001, AddCounter, ModifyCounter - AddCounter);
    END;

    PROCEDURE AddPortalPermission@1100528500(ServiceResponsiblePersonDPT@1100528300 : Record 11072297;SubsiteCodeFilter@1100528306 : Code[20]);
    VAR
      DPTSubsiteServLoc@1100528303 : Record 11072344;
      DPTSubsiteDocTypeServLoc@1100528302 : Record 11072345;
    BEGIN
      InsertRecord(ServiceResponsiblePersonDPT, 1, '', '');
      DPTSubsiteServLoc.SETRANGE("Service Location No.", ServiceResponsiblePersonDPT."Source Code");
      IF (SubsiteCodeFilter <> '') THEN
        DPTSubsiteServLoc.SETRANGE(Code, SubsiteCodeFilter);
      DPTSubsiteServLoc.SETRANGE("Subsite Structure", DPTSubsiteServLoc."Subsite Structure"::PermissionPerDocType);
      IF (DPTSubsiteServLoc.FINDSET) THEN BEGIN
        REPEAT
          DPTSubsiteDocTypeServLoc.SETRANGE("Service Location No.", ServiceResponsiblePersonDPT."Source Code");
          DPTSubsiteDocTypeServLoc.SETRANGE("Subsite Code", DPTSubsiteServLoc.Code);
          IF (DPTSubsiteDocTypeServLoc.FINDSET) THEN BEGIN
            InsertRecord(ServiceResponsiblePersonDPT, 1, DPTSubsiteServLoc.Code, ''); //1=Access
            REPEAT
              InsertRecord(ServiceResponsiblePersonDPT, 1, DPTSubsiteServLoc.Code, DPTSubsiteDocTypeServLoc."Document Type");
            UNTIL(DPTSubsiteDocTypeServLoc.NEXT = 0);
          END;
        UNTIL (DPTSubsiteServLoc.NEXT = 0);
      END;

      DPTSubsiteServLoc.SETFILTER("Subsite Structure", '<%1', DPTSubsiteServLoc."Subsite Structure"::PermissionPerDocType);
      IF (DPTSubsiteServLoc.FINDSET) THEN BEGIN
        REPEAT
          InsertRecord(ServiceResponsiblePersonDPT, 1, DPTSubsiteServLoc.Code, ''); //1=Access
        UNTIL (DPTSubsiteServLoc.NEXT = 0);
      END;
    END;

    LOCAL PROCEDURE InsertRecord@1100528302(ServiceResponsiblePersonDPT@1100528306 : Record 11072297;PermissionType@1100528300 : Option;SubsiteCode@1100528304 : Code[20];DocumentType@1100528305 : Code[20]);
    VAR
      DPTRespPersPermServLoc@1100528301 : Record 11072340;
      DPTDefRespPersPermServLoc@1100528303 : Record 11072341;
      ServiceLocation@1100528307 : Record 11012801;
    BEGIN
      ServiceLocation.GET(ServiceResponsiblePersonDPT."Source Code");
      IF (NOT DPTDefRespPersPermServLoc.GET(PermissionType, ServiceResponsiblePersonDPT.Responsibility,SubsiteCode, DocumentType)) THEN BEGIN
        DPTDefRespPersPermServLoc.INIT;
      END;

      IF (NOT DPTRespPersPermServLoc.GET(PermissionType, ServiceResponsiblePersonDPT."Source Code",
          ServiceResponsiblePersonDPT."Line No.", SubsiteCode, DocumentType))
      THEN BEGIN
        DPTRespPersPermServLoc.INIT;
        DPTRespPersPermServLoc.Type := PermissionType;
        DPTRespPersPermServLoc."Service Location No." := ServiceResponsiblePersonDPT."Source Code";
        DPTRespPersPermServLoc."Line No." := ServiceResponsiblePersonDPT."Line No.";
        DPTRespPersPermServLoc."Subsite Code" := SubsiteCode;
        DPTRespPersPermServLoc."Document Type" := DocumentType;
        DPTRespPersPermServLoc.INSERT;
        AddCounter += 1;
      END;
      DPTRespPersPermServLoc."Permission Level" := DPTDefRespPersPermServLoc."Permission Level";
      DPTRespPersPermServLoc."Private Permission Level" := DPTDefRespPersPermServLoc."Private Permission Level";
      DPTRespPersPermServLoc.MODIFY;
      ModifyCounter += 1;
    END;

    LOCAL PROCEDURE CheckRecordChangeAllowed@1100528307();
    VAR
      ServiceLocation@1100528300 : Record 11012801;
    BEGIN
      IF (Type <> Type::Access) OR (NOT ServiceLocation.GET("Service Location No.")) THEN
        EXIT;
    END;

    PROCEDURE MakePrivatePermEqualToPerm@1100529900();
    VAR
      DPTRespPersPermServLoc@1100529900 : Record 11072340;
    BEGIN
      DPTRespPersPermServLoc.SETRANGE(Type, Type::Access);
      DPTRespPersPermServLoc.SETRANGE("Service Location No.", "Service Location No.");
      DPTRespPersPermServLoc.SETFILTER("Subsite Code", '%1', '');
      DPTRespPersPermServLoc.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      DPTRespPersPermServLoc.SETFILTER("Subsite Code", '<>%1', '');
      DPTRespPersPermServLoc.SETFILTER("Document Type", '<>%1', '');
      DPTRespPersPermServLoc.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      DPTRespPersPermServLoc.SETFILTER("Document Type", '%1', '');
      IF (NOT DPTRespPersPermServLoc.FINDSET) THEN
        EXIT;
      REPEAT
        DPTRespPersPermServLoc."Private Permission Level" := DPTRespPersPermServLoc."Permission Level";
        DPTRespPersPermServLoc.MODIFY;
      UNTIL (DPTRespPersPermServLoc.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

