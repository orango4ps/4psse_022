OBJECT Table 11012322 Item to Replace
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckCostObject;
               Manual := TRUE;  //db, 16-08-04

               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN
                 CentrManagementCU.EditReplaceItem(Rec,1,xRec);  //db, 25-05-07
             END;

    OnModify=BEGIN
               CheckCostObject;

               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN
                 CentrManagementCU.EditReplaceItem(Rec,2,xRec);  //db, 25-05-07
             END;

    OnDelete=BEGIN
               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN
                 CentrManagementCU.EditReplaceItem(Rec,4,xRec);  //db, 25-05-07
             END;

    OnRename=BEGIN
               CheckCostObject;
             END;

    CaptionML=[ENU=Item to Replace;
               NOR=Artikkel som skal erstattes;
               SVE=Artikel som ska ers„ttas];
  }
  FIELDS
  {
    { 10  ;   ;Item No. (Old)      ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF ("Item No. (Old)" <> xRec."Item No. (Old)") AND ("Item No. (Old)" <> '') THEN
                                                                  ValidateItemOld(ItemTypeOpt::Item);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Item No. (Old);
                                                              NOR=Artikkelnr. (gammelt);
                                                              SVE=Artikelnr (gammalt)] }
    { 20  ;   ;Basic Item (Old)    ;Code20        ;TableRelation="Basic Item"."Product Code" WHERE (Manufacturer=FIELD("Manufacturer (Old)"));
                                                   OnValidate=BEGIN
                                                                IF ("Basic Item (Old)" <> xRec."Basic Item (Old)") AND ("Basic Item (Old)" <> '') THEN
                                                                  ValidateItemOld(ItemTypeOpt::"Basic Item");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Basic Item (Old);
                                                              NOR=Fabrikantartikkel (gammel);
                                                              SVE=Basartikel (gammal)] }
    { 30  ;   ;Manufacturer (Old)  ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Manufacturer (Old);
                                                              NOR=Fabrikant (gammel);
                                                              SVE=Tillverkare (gammal)] }
    { 40  ;   ;Trade Item (Old)    ;Code20        ;TableRelation="Trade Item"."Item Code" WHERE (Vendor=FIELD("Vendor (Old)"));
                                                   OnValidate=BEGIN
                                                                IF ("Trade Item (Old)" <> xRec."Trade Item (Old)") AND ("Trade Item (Old)" <> '') THEN
                                                                  ValidateItemOld(ItemTypeOpt::"Trade Item");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Trade Item (Old);
                                                              NOR=Katalogartikkel (gammel);
                                                              SVE=Anskaffningsartikel (gammal)] }
    { 50  ;   ;Vendor (Old)        ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Vendor (Old);
                                                              NOR=Leverand›r (gammel);
                                                              SVE=Leverant”r (gammal)] }
    { 70  ;   ;Item No. (New)      ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF ("Item No. (New)" <> xRec."Item No. (New)") AND ("Item No. (New)" <> '') THEN
                                                                  ValidateItemNew(ItemTypeOpt::Item);
                                                              END;

                                                   CaptionML=[ENU=Item No. (New);
                                                              NOR=Artikkelnr. (nytt);
                                                              SVE=Artikelnr (nytt)] }
    { 80  ;   ;Basic Item (New)    ;Code20        ;TableRelation="Basic Item"."Product Code" WHERE (Manufacturer=FIELD("Manufacturer (New)"));
                                                   OnValidate=BEGIN
                                                                IF ("Basic Item (New)" <> xRec."Basic Item (New)") AND ("Basic Item (New)" <> '') THEN
                                                                  ValidateItemNew(ItemTypeOpt::"Basic Item");
                                                              END;

                                                   CaptionML=[ENU=Basic Item (New);
                                                              NOR=Fabrikantartikkel (ny);
                                                              SVE=Basartikel (ny)] }
    { 90  ;   ;Manufacturer (New)  ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Manufacturer (New);
                                                              NOR=Produsent (ny);
                                                              SVE=Tillverkare (ny)] }
    { 100 ;   ;Trade Item (New)    ;Code20        ;TableRelation="Trade Item"."Item Code" WHERE (Vendor=FIELD("Vendor (New)"));
                                                   OnValidate=BEGIN
                                                                IF ("Trade Item (New)" <> xRec."Trade Item (New)") AND ("Trade Item (New)" <> '') THEN
                                                                  ValidateItemNew(ItemTypeOpt::"Trade Item");
                                                              END;

                                                   CaptionML=[ENU=Trade Item (New);
                                                              NOR=Katalogartikkel (ny);
                                                              SVE=Anskaffningsartikel (ny)] }
    { 110 ;   ;Vendor (New)        ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Vendor (New);
                                                              NOR=Leverand›r (ny);
                                                              SVE=Leverant”r (ny)] }
    { 130 ;   ;Description (Old)   ;Text50        ;CaptionML=[ENU=Description (Old);
                                                              NOR=Beskrivelse (gammel);
                                                              SVE=Beskrivning (gammal)] }
    { 140 ;   ;Description (New)   ;Text50        ;CaptionML=[ENU=Description (New);
                                                              NOR=Beskrivelse (ny);
                                                              SVE=Beskrivning (ny)] }
    { 150 ;   ;Cost Object (Old)   ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(<Sundry),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                DimMgt.GetDimValueRec(2, "Cost Object (Old)", DimValRec1, FALSE, '');
                                                                "Description (Old)" := DimValRec1.Name;
                                                              END;

                                                   CaptionML=[ENU=Cost Object (Old);
                                                              NOR=Kostnadsobjekt (gammelt);
                                                              SVE=Kostnadsobjekt (gammalt)] }
    { 160 ;   ;Cost Object (New)   ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(<Sundry),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                DimMgt.GetDimValueRec(2, "Cost Object (New)", DimValRec2, FALSE, '');
                                                                "Description (New)" := DimValRec2.Name;
                                                              END;

                                                   CaptionML=[ENU=Cost Object (New);
                                                              NOR=Kostnadsobjekt (nytt);
                                                              SVE=Kostnadsobjekt (nytt)] }
    { 170 ;   ;Select              ;Boolean       ;OnValidate=BEGIN
                                                                //db.sn, 24-06-14: C014467
                                                                //IF ("Trade Item (Old)" <> '') THEN BEGIN
                                                                //  TESTFIELD("Trade Item (New)");
                                                                //  TESTFIELD("Cost Object (Old)", '');
                                                                //END;
                                                                //IF ("Trade Item (New)" <> '') THEN BEGIN
                                                                //  TESTFIELD("Trade Item (Old)");
                                                                //  TESTFIELD("Cost Object (New)", '');
                                                                //END;
                                                                //
                                                                //IF ("Basic Item (Old)" <> '') THEN BEGIN
                                                                //  TESTFIELD("Basic Item (New)");
                                                                //  TESTFIELD("Cost Object (Old)", '');
                                                                //END;
                                                                //IF ("Basic Item (New)" <> '') THEN BEGIN
                                                                //  TESTFIELD("Basic Item (Old)");
                                                                //  TESTFIELD("Cost Object (New)", '');
                                                                //END;
                                                                //
                                                                //IF ("Item No. (Old)" <> '') THEN BEGIN
                                                                //  TESTFIELD("Item No. (New)");
                                                                //  TESTFIELD("Cost Object (Old)", '');
                                                                //END;
                                                                //IF ("Item No. (New)" <> '') THEN BEGIN
                                                                //  TESTFIELD("Item No. (Old)");
                                                                //  TESTFIELD("Cost Object (New)", '');
                                                                //END;
                                                                //db.en, 24-06-14: C014467

                                                                IF ("Cost Object (Old)" <> '') THEN BEGIN
                                                                  TESTFIELD("Cost Object (New)");
                                                                  TESTFIELD("Item No. (Old)", '');
                                                                  TESTFIELD("Basic Item (Old)", '');
                                                                  TESTFIELD("Trade Item (Old)", '');
                                                                END ELSE BEGIN
                                                                  IF "Item No. (Old)" + "Basic Item (Old)" + "Trade Item (Old)" = '' THEN
                                                                    TESTFIELD(Select, FALSE);
                                                                END;
                                                                IF ("Cost Object (New)" <> '') THEN BEGIN
                                                                  TESTFIELD("Cost Object (Old)");
                                                                  TESTFIELD("Item No. (New)", '');
                                                                  TESTFIELD("Basic Item (New)", '');
                                                                  TESTFIELD("Trade Item (New)", '');
                                                                END ELSE BEGIN
                                                                  IF "Item No. (New)" + "Basic Item (New)" + "Trade Item (New)" = '' THEN
                                                                    TESTFIELD(Select, FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Select;
                                                              NOR=Velg;
                                                              SVE=V„lj] }
    { 180 ;   ;Manual              ;Boolean       ;CaptionML=[ENU=Manual;
                                                              NOR=Manuell;
                                                              SVE=Manuell];
                                                   Editable=No }
    { 190 ;   ;Trade Item Exists   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Trade Item" WHERE (Vendor=FIELD("Vendor (New)"),
                                                                                         Item Code=FIELD("Trade Item (New)")));
                                                   CaptionML=[ENU=Trade Item Exists;
                                                              SVE=Anskaffningsartikeln finns];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Manual,Item No. (Old),Basic Item (Old),Manufacturer (Old),Trade Item (Old),Vendor (Old),Cost Object (Old);
                                                   Clustered=Yes }
    {    ;Select                                   }
    {    ;Manufacturer (Old),Basic Item (Old)      }
    {    ;Vendor (Old),Trade Item (Old)            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimValRec1@1210190006 : Record 349;
      DimValRec2@1210190007 : Record 349;
      DimMgt@1210190005 : Codeunit 408;
      ValidateCu@1210190001 : Codeunit 11012033;
      ItemTypeOpt@1210190000 : 'Item,Basic Item,Trade Item';
      dummyCde@1210190004 : Code[10];
      dummyDec@1210190003 : Decimal;
      dummyDat@1210190002 : Date;
      Text000@1210190008 : TextConst 'ENU=Cost Objects with different Cost Type not allowed;NOR=Kostnadsobjekter med forskjellig Kostnadstype er ikke tillatt;SVE=Kostnadsobjekt med olika kostnadstyper „r inte till†tna';
      dummyTxt@1210190010 : Text[50];
      CentrManagementCU@1100485000 : Codeunit 11012010;
      CompRelationRec@1100485001 : Record 11012059;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;

    PROCEDURE ValidateItemOld@3(LTypeArtOpt@11012000 : 'Item,Basic Item,Trade Item');
    BEGIN
      ValidateCu.ValidateItem(LTypeArtOpt, "Trade Item (Old)", "Item No. (Old)", "Manufacturer (Old)",
        "Vendor (Old)", "Basic Item (Old)", dummyCde, "Description (Old)",
        dummyCde, dummyDec, dummyDec, dummyDat, dummyDec, dummyTxt, dummyCde, dummyCde, dummyCde, dummyDat, '');
    END;

    PROCEDURE ValidateItemNew@1210190000(LTypeArtOpt@11012000 : 'Item,Basic Item,Trade Item');
    BEGIN
      ValidateCu.ValidateItem(LTypeArtOpt, "Trade Item (New)", "Item No. (New)", "Manufacturer (New)",
        "Vendor (New)", "Basic Item (New)", dummyCde, "Description (New)",
        dummyCde, dummyDec, dummyDec, dummyDat, dummyDec, dummyTxt,dummyCde, dummyCde, dummyCde, dummyDat, '');
    END;

    PROCEDURE CheckCostObject@1210190001();
    BEGIN
      IF "Cost Object (Old)" <> '' THEN BEGIN
        TESTFIELD("Cost Object (New)");
        IF ("Item No. (Old)" <> '') OR ("Basic Item (Old)" <> '') OR ("Trade Item (Old)" <> '') OR
           ("Manufacturer (Old)" <> '') OR ("Vendor (Old)" <> '')  THEN
          TESTFIELD("Cost Object (Old)", '');
      END;

      IF "Cost Object (New)" <> '' THEN BEGIN
        TESTFIELD("Cost Object (Old)");
        IF ("Item No. (New)" <> '') OR ("Basic Item (New)" <> '') OR ("Trade Item (New)" <> '') OR
           ("Manufacturer (New)" <> '') OR ("Vendor (New)" <> '')  THEN
          TESTFIELD("Cost Object (New)", '');
      END;

      IF ("Cost Object (Old)" <> '') AND ("Cost Object (New)" <> '') THEN BEGIN
        DimMgt.GetDimValueRec(2, "Cost Object (Old)", DimValRec1, TRUE, '');
        DimMgt.GetDimValueRec(2, "Cost Object (New)", DimValRec2, TRUE, '');
        IF DimValRec1."Cost Type" <> DimValRec2."Cost Type" THEN
          ERROR(Text000);
      END;
    END;

    PROCEDURE ReplaceBasicItem@1(BasicItemRec@11012000 : Record 11012316);
    VAR
      ReplaceRec@11012001 : Record 11012322;
    BEGIN
      WITH BasicItemRec DO BEGIN
        IF Expired = FALSE THEN EXIT;
        ReplaceRec.RESET;
        ReplaceRec.SETCURRENTKEY("Manufacturer (Old)", "Basic Item (Old)");  //db, 08-12-06
        ReplaceRec.SETRANGE("Manufacturer (Old)", Manufacturer);
        ReplaceRec.SETRANGE("Basic Item (Old)", "Product Code");
        IF NOT ReplaceRec.FIND('-') THEN BEGIN
          ReplaceRec.INIT;
          ReplaceRec."Manufacturer (Old)" := Manufacturer;
          ReplaceRec."Basic Item (Old)" := "Product Code";
          ReplaceRec."Description (Old)" := "Product Description";
          IF "Product Code (Successor)" <> '' THEN BEGIN  //db, 25-05-07
            ReplaceRec."Manufacturer (New)" := Manufacturer;
            ReplaceRec."Basic Item (New)" := "Product Code (Successor)";
          END;
          ReplaceRec.INSERT;
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN
            CentrManagementCU.EditReplaceItem(ReplaceRec,1,ReplaceRec);  //db, 25-05-07
        END;
      END;
    END;

    PROCEDURE ReplaceTradeItem@1210190002(TradeItemRec@11012000 : Record 11012317;OnlyExpired@1100528400 : Boolean) ItemToReplaceAdded : Boolean;
    VAR
      ReplaceRec@11012001 : Record 11012322;
    BEGIN
      WITH TradeItemRec DO BEGIN
        IF NOT Expired AND OnlyExpired THEN
          EXIT;
        ReplaceRec.RESET;
        ReplaceRec.SETCURRENTKEY("Vendor (Old)", "Trade Item (Old)");  //db, 08-12-06
        ReplaceRec.SETRANGE("Vendor (Old)", Vendor);
        ReplaceRec.SETRANGE("Trade Item (Old)", "Item Code");
        IF NOT ReplaceRec.FIND('-') THEN BEGIN
          ReplaceRec.INIT;
          ReplaceRec."Vendor (Old)" := Vendor;
          ReplaceRec."Trade Item (Old)" := "Item Code";
          ReplaceRec."Description (Old)" := "Item Description";
          IF "Item Code (Successor)" <> '' THEN BEGIN  //db, 25-05-07
            ReplaceRec."Vendor (New)" := Vendor;
            ReplaceRec."Trade Item (New)" := "Item Code (Successor)";
          END;
          ReplaceRec.INSERT;
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN
            CentrManagementCU.EditReplaceItem(ReplaceRec,1,ReplaceRec);  //db, 25-05-07
          ItemToReplaceAdded := TRUE;
        END;
      END;
    END;

    [External]
    PROCEDURE HandleAssistEditItem@1100528300(lvFieldNo@1210190000 : Integer) lvRefresh : Boolean;
    VAR
      WebshopItemMgt@1100528300 : Codeunit 11012306;
      RecRef@1100528301 : RecordRef;
      TradeItem@1100528900 : Record 11012317;
      TradeItemList@1100528901 : Page 11012334;
      NumItem@1100528902 : Integer;
    BEGIN
      //C060176
      IF lvFieldNo = FIELDNO("Trade Item (New)") THEN BEGIN
        RecRef.GETTABLE(Rec);
        WebshopItemMgt.RunModalDialogForMultipleItems("Vendor (New)", "Trade Item (New)", RecRef);
        WebshopItemMgt.GetMarkedItemsForAssistEdit(TradeItem);
        COMMIT;
        //
        CLEAR(TradeItemList);
        TradeItemList.LOOKUPMODE(TRUE);
        TradeItemList.SETRECORD(TradeItem);
        TradeItem.MARKEDONLY(TRUE);
        IF TradeItem.FINDFIRST THEN
          TradeItemList.SETTABLEVIEW(TradeItem);
        NumItem := TradeItem.COUNT;
        IF NumItem > 1 THEN BEGIN
          IF TradeItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
            TradeItemList.GETRECORD(TradeItem);
            NumItem := 1;
          END;
          lvRefresh := TRUE;
        END;
        IF NumItem = 1 THEN BEGIN
          "Manufacturer (New)" := TradeItem.Manufacturer;
          "Basic Item (New)" := TradeItem."Product Code";
          "Vendor (New)" := TradeItem.Vendor;
          VALIDATE("Trade Item (New)", TradeItem."Item Code");
        END;
      END;
    END;

    BEGIN
    END.
  }
}

