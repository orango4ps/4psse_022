OBJECT Codeunit 6086004 Web Job and Dimension Mgnt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.50,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WebHeaderDim@161024013 : ARRAY [8] OF Code[20];
      WebLineDim@161024012 : ARRAY [8] OF Code[20];

    PROCEDURE GetPurchLineJobTaskInfo@161024012(PurchLine@161024012 : Record 39;VAR JobTaskNo@161024013 : Code[20];VAR JobTaskDesc@161024014 : Text[50]);
    VAR
      JobTask@161024015 : Record 11072600;
    BEGIN
      JobTaskNo := '';
      JobTaskDesc := '';

      IF (PurchLine."Job No." = '') OR (PurchLine."Job Task No." = '') THEN
        EXIT;

      IF JobTask.GET(PurchLine."Job No.",PurchLine."Job Task No.") THEN BEGIN
        JobTaskNo := JobTask."Job Task No.";
        JobTaskDesc := JobTask.Description;
      END;
    END;

    PROCEDURE GetPurchInvLineJobTaskInfo@161024015(PurchInvLine@161024012 : Record 123;VAR JobTaskNo@161024013 : Code[20];VAR JobTaskDesc@161024014 : Text[50]);
    VAR
      JobTask@161024015 : Record 11072600;
    BEGIN
      JobTaskNo := '';
      JobTaskDesc := '';

      IF (PurchInvLine."Job No." = '') OR (PurchInvLine."Job Task No." = '') THEN
        EXIT;

      IF JobTask.GET(PurchInvLine."Job No.",PurchInvLine."Job Task No.") THEN BEGIN
        JobTaskNo := JobTask."Job Task No.";
        JobTaskDesc := JobTask.Description;
      END;
    END;

    PROCEDURE GetPurchCrMemoLineJobTaskInfo@161024016(PurchCrMemoLine@161024012 : Record 125;VAR JobTaskNo@161024013 : Code[20];VAR JobTaskDesc@161024014 : Text[50]);
    VAR
      JobTask@161024015 : Record 11072600;
    BEGIN
      JobTaskNo := '';
      JobTaskDesc := '';

      IF (PurchCrMemoLine."Job No." = '') OR (PurchCrMemoLine."Job Task No." = '') THEN
        EXIT;

      IF JobTask.GET(PurchCrMemoLine."Job No.",PurchCrMemoLine."Job Task No.") THEN BEGIN
        JobTaskNo := JobTask."Job Task No.";
        JobTaskDesc := JobTask.Description;
      END;
    END;

    PROCEDURE GetJobTaskDesc@161024014(JobNo@161024013 : Code[20];JobTaskNo@161024012 : Code[20]) : Text[50];
    VAR
      JobTask@161024014 : Record 11072600;
    BEGIN
      IF JobTask.GET(JobNo,JobTaskNo) THEN
        EXIT(JobTask.Description);
    END;

    PROCEDURE ValidateJobTaskNo@161024013(VAR PurchLine@161024012 : Record 39;JobTaskNo@161024013 : Code[20];CurrUserId@161024015 : Code[50]);
    BEGIN
      PurchLine.VALIDATE("Job Task No.",JobTaskNo);
    END;

    PROCEDURE GetPurchHeaderDim@161024019(PurchHeader@161024012 : Record 38;VAR DimCode@161024013 : ARRAY [8] OF Code[20]);
    VAR
      WebSetup@161024015 : Record 6085573;
      GLSetup@161024016 : Record 98;
      DimSetEntry@161024017 : Record 480;
      i@161024014 : Integer;
    BEGIN
      CLEAR(DimCode);

      GLSetup.GET;

      LoadWebHeaderDim;
      FOR i := 1 TO 8 DO BEGIN
        CASE WebHeaderDim[i] OF
          '':;

          GLSetup."Global Dimension 1 Code":
            DimCode[i] := PurchHeader."Shortcut Dimension 1 Code";

          GLSetup."Global Dimension 2 Code":
            DimCode[i] := PurchHeader."Shortcut Dimension 2 Code";

          ELSE
            IF DimSetEntry.GET(PurchHeader."Dimension Set ID",WebHeaderDim[i]) THEN
              DimCode[i] := DimSetEntry."Dimension Value Code";
        END;
      END;
    END;

    PROCEDURE GetPurchInvHeaderDim@161024034(PurchInvHeader@161024012 : Record 122;VAR DimCode@161024013 : ARRAY [8] OF Code[20]);
    VAR
      WebSetup@161024015 : Record 6085573;
      GLSetup@161024016 : Record 98;
      DimSetEntry@161024017 : Record 480;
      i@161024014 : Integer;
    BEGIN
      CLEAR(DimCode);

      GLSetup.GET;

      LoadWebHeaderDim;
      FOR i := 1 TO 8 DO BEGIN
        CASE WebHeaderDim[i] OF
          '':;

          GLSetup."Global Dimension 1 Code":
            DimCode[i] := PurchInvHeader."Shortcut Dimension 1 Code";

          GLSetup."Global Dimension 2 Code":
            DimCode[i] := PurchInvHeader."Shortcut Dimension 2 Code";

          ELSE
            IF DimSetEntry.GET(PurchInvHeader."Dimension Set ID",WebHeaderDim[i]) THEN
              DimCode[i] := DimSetEntry."Dimension Value Code";
        END;
      END;
    END;

    PROCEDURE GetPurchCrMemoHeaderDim@161024035(PurchCrMemoHeader@161024012 : Record 124;VAR DimCode@161024013 : ARRAY [8] OF Code[20]);
    VAR
      WebSetup@161024015 : Record 6085573;
      GLSetup@161024016 : Record 98;
      DimSetEntry@6085573 : Record 480;
      i@161024014 : Integer;
    BEGIN
      CLEAR(DimCode);

      GLSetup.GET;

      LoadWebHeaderDim;
      FOR i := 1 TO 8 DO BEGIN
        CASE WebHeaderDim[i] OF
          GLSetup."Global Dimension 1 Code":
            DimCode[i] := PurchCrMemoHeader."Shortcut Dimension 1 Code";

          GLSetup."Global Dimension 2 Code":
            DimCode[i] := PurchCrMemoHeader."Shortcut Dimension 2 Code";

          ELSE
            IF DimSetEntry.GET(PurchCrMemoHeader."Dimension Set ID",WebHeaderDim[i]) THEN
              DimCode[i] := DimSetEntry."Dimension Value Code";
        END;
      END;
    END;

    PROCEDURE GetPurchLineDim@161024027(PurchLine@161024012 : Record 39;VAR ValueCode@161024018 : ARRAY [8] OF Code[20];VAR ValueName@161024013 : ARRAY [8] OF Text[50];VAR DimRule@1160040000 : ARRAY [8] OF 'None,Mandatory,SameCode,NoCode');
    VAR
      DimSetEntry@161024017 : Record 480;
      TableID@1160040001 : Integer;
      DefaultDim@1160040002 : Record 352;
      i@161024014 : Integer;
    BEGIN
      CLEAR(ValueCode);
      CLEAR(ValueName);
      CLEAR(DimRule);

      LoadWebLineDim;
      FOR i := 1 TO 8 DO BEGIN
        IF WebLineDim[i] <> '' THEN BEGIN
          IF PurchLine."No." <> '' THEN BEGIN
            CASE PurchLine.Type OF
              PurchLine.Type::"G/L Account":
                TableID := DATABASE::"G/L Account";
              PurchLine.Type::Item:
                TableID := DATABASE::Item;
              PurchLine.Type::"Fixed Asset":
                TableID := DATABASE::"Fixed Asset";
              PurchLine.Type::"Charge (Item)":
                TableID := DATABASE::"Item Charge";
            END;
            IF DefaultDim.GET(TableID,PurchLine."No.",WebLineDim[i]) THEN
              DimRule[i] := DefaultDim."Value Posting";
          END;

          IF DimSetEntry.GET(PurchLine."Dimension Set ID",WebLineDim[i]) THEN BEGIN
            ValueCode[i] := DimSetEntry."Dimension Value Code";
            DimSetEntry.CALCFIELDS("Dimension Value Name");
            ValueName[i] := DimSetEntry."Dimension Value Name";
          END;
        END;
      END;
    END;

    PROCEDURE GetPurchInvLineDim@161024026(PurchInvLine@161024012 : Record 123;VAR ValueCode@161024019 : ARRAY [8] OF Code[20];VAR ValueName@161024018 : ARRAY [8] OF Text[50]);
    VAR
      DimSetEntry@6085573 : Record 480;
      i@161024014 : Integer;
    BEGIN
      CLEAR(ValueCode);
      CLEAR(ValueName);

      LoadWebLineDim;
      FOR i := 1 TO 8 DO BEGIN
        IF WebLineDim[i] <> '' THEN BEGIN
          IF DimSetEntry.GET(PurchInvLine."Dimension Set ID",WebLineDim[i]) THEN BEGIN
            ValueCode[i] := DimSetEntry."Dimension Value Code";
            DimSetEntry.CALCFIELDS("Dimension Value Name");
            ValueName[i] := DimSetEntry."Dimension Value Name";
          END;
        END;
      END;
    END;

    PROCEDURE GetPurchCrMemoLineDim@161024023(PurchCrMemoLine@161024012 : Record 125;VAR ValueCode@161024018 : ARRAY [8] OF Code[20];VAR ValueName@161024013 : ARRAY [8] OF Text[50]);
    VAR
      DimSetEntry@6085573 : Record 480;
      i@161024014 : Integer;
    BEGIN
      CLEAR(ValueCode);
      CLEAR(ValueName);

      LoadWebLineDim;
      FOR i := 1 TO 8 DO BEGIN
        IF WebLineDim[i] <> '' THEN BEGIN
          IF DimSetEntry.GET(PurchCrMemoLine."Dimension Set ID",WebLineDim[i]) THEN BEGIN
            ValueCode[i] := DimSetEntry."Dimension Value Code";
            DimSetEntry.CALCFIELDS("Dimension Value Name");
            ValueName[i] := DimSetEntry."Dimension Value Name";
          END;
        END;
      END;
    END;

    PROCEDURE UpdPurchLineJobPricesFromMatch@1(VAR PurchLine@1000 : Record 39) : Boolean;
    VAR
      PurchLineRel@1001 : Record 6085702;
      PurchLine2@1002 : Record 39;
    BEGIN
      IF NOT PurchLineRel.GET(PurchLine."Document Type",PurchLine."Document No.",PurchLine."Line No.") THEN
        EXIT(FALSE);

      PurchLine2.GET(PurchLineRel."Related Document Type",PurchLineRel."Related Document No.",PurchLineRel."Related Line No.");

      PurchLine."Job Unit Price" := PurchLine2."Job Unit Price";
      PurchLine."Job Total Price" := PurchLine2."Job Total Price";
      PurchLine."Job Unit Price (LCY)" := PurchLine2."Job Unit Price (LCY)";
      PurchLine."Job Total Price (LCY)" := PurchLine2."Job Total Price (LCY)";
      PurchLine."Job Line Amount (LCY)" := PurchLine2."Job Line Amount (LCY)";
      PurchLine."Job Line Disc. Amount (LCY)" := PurchLine2."Job Line Disc. Amount (LCY)";
      PurchLine."Job Line Amount" := PurchLine2."Job Line Amount";
      PurchLine."Job Line Discount %" := PurchLine2."Job Line Discount %";
      PurchLine."Job Line Discount Amount" := PurchLine2."Job Line Discount Amount";

      EXIT(TRUE);
    END;

    PROCEDURE UpdateWebDim@161024018(VAR PurchLine@161024017 : Record 39;Code@161024012 : Code[20];ValueCode@161024014 : Code[20];CurrUserId@161024019 : Code[50]);
    VAR
      GLSetup@161024018 : Record 98;
      DimSetEntry@161024013 : TEMPORARY Record 480;
      DimMgt@6085573 : Codeunit 408;
      Updated@161024016 : Boolean;
      Exists@6085574 : Boolean;
    BEGIN
      IF Code = '' THEN
        EXIT;

      GLSetup.GET;

      CASE Code OF
        GLSetup."Global Dimension 1 Code":
          BEGIN
            PurchLine.VALIDATE("Shortcut Dimension 1 Code",ValueCode);
            Updated := TRUE;
          END;

        GLSetup."Global Dimension 2 Code":
          BEGIN
            PurchLine.VALIDATE("Shortcut Dimension 2 Code",ValueCode);
            Updated := TRUE;
          END;
      END;

      IF NOT Updated THEN BEGIN
        DimMgt.GetDimensionSet(DimSetEntry,PurchLine."Dimension Set ID");

        IF DimSetEntry.GET(PurchLine."Dimension Set ID",Code) THEN BEGIN
          IF DimSetEntry."Dimension Value Code" = ValueCode THEN
            EXIT;

          Exists := TRUE;
        END ELSE IF ValueCode = '' THEN
          EXIT;

        IF Exists THEN BEGIN
          IF ValueCode = '' THEN
            DimSetEntry.DELETE(TRUE)
          ELSE BEGIN
            DimSetEntry.VALIDATE("Dimension Value Code",ValueCode);
            DimSetEntry.MODIFY(TRUE);
          END;
        END ELSE BEGIN
          DimSetEntry.INIT;
          DimSetEntry."Dimension Set ID" := PurchLine."Dimension Set ID";
          DimSetEntry.VALIDATE("Dimension Code",Code);
          DimSetEntry.VALIDATE("Dimension Value Code",ValueCode);
          DimSetEntry.INSERT(TRUE);
        END;

        PurchLine."Dimension Set ID" := DimMgt.GetDimensionSetID(DimSetEntry);
      END;
    END;

    PROCEDURE LoadWebLineDim@161024031();
    VAR
      WebSetup@161024012 : Record 6085573;
    BEGIN
      WebSetup.GET;
      WebLineDim[1] := WebSetup."Web: Dimension 1 Code (Lines)";
      WebLineDim[2] := WebSetup."Web: Dimension 2 Code (Lines)";
      WebLineDim[3] := WebSetup."Web: Dimension 3 Code (Lines)";
      WebLineDim[4] := WebSetup."Web: Dimension 4 Code (Lines)";
      WebLineDim[5] := WebSetup."Web: Dimension 5 Code (Lines)";
      WebLineDim[6] := WebSetup."Web: Dimension 6 Code (Lines)";
      WebLineDim[7] := WebSetup."Web: Dimension 7 Code (Lines)";
      WebLineDim[8] := WebSetup."Web: Dimension 8 Code (Lines)";
    END;

    PROCEDURE LoadWebHeaderDim@161024032();
    VAR
      WebSetup@161024012 : Record 6085573;
    BEGIN
      WebSetup.GET;
      WebHeaderDim[1] := WebSetup."Web: Dimension 1 Code (Header)";
      WebHeaderDim[2] := WebSetup."Web: Dimension 2 Code (Header)";
      WebHeaderDim[3] := WebSetup."Web: Dimension 3 Code (Header)";
      WebHeaderDim[4] := WebSetup."Web: Dimension 4 Code (Header)";
      WebHeaderDim[5] := WebSetup."Web: Dimension 5 Code (Header)";
      WebHeaderDim[6] := WebSetup."Web: Dimension 6 Code (Header)";
      WebHeaderDim[7] := WebSetup."Web: Dimension 7 Code (Header)";
      WebHeaderDim[8] := WebSetup."Web: Dimension 8 Code (Header)";
    END;

    BEGIN
    {
      //**4PS Tables renumbered
      1001 --> 11072600
    }
    END.
  }
}

