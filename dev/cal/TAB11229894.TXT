OBJECT Table 11229894 Implementation Portal Company
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Implementation Portal;
  }
  FIELDS
  {
    { 10  ;   ;Company Name        ;Text30        ;TableRelation=Company.Name;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Company Name;
                                                              NOR=Navn p† selskap;
                                                              SVE=F”retagsnamn] }
    { 20  ;   ;Select              ;Boolean       ;CaptionML=[ENU=Select;
                                                              NOR=Velg;
                                                              SVE=V„lj] }
  }
  KEYS
  {
    {    ;Company Name                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE FillAllCompanies@1100529200();
    VAR
      ImplementationPortalCompany@1100529202 : Record 11229894;
      Company@1100529201 : Record 2000000006;
    BEGIN
      IF Company.FINDSET THEN
        REPEAT
          IF NOT ImplementationPortalCompany.GET(Company.Name) THEN BEGIN
            ImplementationPortalCompany.INIT;
            ImplementationPortalCompany."Company Name" := Company.Name;
            ImplementationPortalCompany.INSERT;
          END;
        UNTIL Company.NEXT = 0;

      CLEAR(ImplementationPortalCompany);
      CLEAR(Company);
      IF ImplementationPortalCompany.FINDSET THEN
        REPEAT
          IF NOT Company.GET(ImplementationPortalCompany."Company Name") THEN
            ImplementationPortalCompany.DELETE;
        UNTIL ImplementationPortalCompany.NEXT = 0;
    END;

    PROCEDURE SetSelectForAllCompanies@1100529201(SelectBln@1100529201 : Boolean);
    VAR
      ImplementationPortalCompany@1100529200 : Record 11229894;
    BEGIN
      ImplementationPortalCompany.MODIFYALL(Select,SelectBln);
    END;

    PROCEDURE GetPostedInvoiceReportSelectionReportID@1100529204() ReportSelectionTxt : Text;
    VAR
      ReportSelections@1100529200 : Record 77;
    BEGIN
      ReportSelections.CHANGECOMPANY("Company Name");
      ReportSelections.SETRANGE(Usage,ReportSelections.Usage::"S.Invoice");
      IF ReportSelections.FINDSET THEN
        REPEAT
          IF ReportSelectionTxt = '' THEN
            ReportSelectionTxt := FORMAT(ReportSelections."Report ID")
          ELSE
            ReportSelectionTxt := ReportSelectionTxt + ',' + FORMAT(ReportSelections."Report ID");
        UNTIL ReportSelections.NEXT = 0;
    END;

    PROCEDURE GetGenericSalesReportID@1100529203() GenericReportTxt : Text;
    VAR
      GenericReport@1100529200 : Record 11071914;
      HoldReportID@1100529201 : Integer;
    BEGIN
      GenericReport.CHANGECOMPANY("Company Name");
      GenericReport.SETFILTER(ID,'<>%1',0);
      IF GenericReport.FINDSET THEN
        REPEAT
          IF HoldReportID <> GenericReport.ID THEN
            IF GenericReportTxt = '' THEN
              GenericReportTxt := FORMAT(GenericReport.ID)
            ELSE
              GenericReportTxt := GenericReportTxt + ',' + FORMAT(GenericReport.ID);
          HoldReportID := GenericReport.ID;
        UNTIL GenericReport.NEXT = 0;
    END;

    PROCEDURE GetNoOfRecordsInCompanyTable@1100529202(TableID@1100529200 : Integer) : Integer;
    VAR
      RecRef@1100529201 : RecordRef;
    BEGIN
      RecRef.OPEN(TableID,FALSE,"Company Name");
      EXIT(RecRef.COUNT);
      RecRef.CLOSE;
    END;

    BEGIN
    END.
  }
}

