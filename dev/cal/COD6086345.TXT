OBJECT Codeunit 6086345 Mileage-Validate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=6086338;
    Permissions=TableData 6086002=r,
                TableData 6086323=r,
                TableData 6086353=r,
                TableData 6086355=rimd,
                TableData 6086361=rimd;
    OnRun=BEGIN
            Code(Rec);
          END;

  }
  CODE
  {
    VAR
      ExcludeInboxCheck@1160040000 : Boolean;
      AccountMissmatch@1160040007 : TextConst 'DEU=%1 %2 unterscheidet sich von der aktuellen Konfigurationsausgabe %3.;ENU=The %1 %2 is different from the current setup output %3;NLD=De %1 %2 is afwijkend van de huidige ingestelde output %3';
      FieldMissing@1160040003 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified;NLD=%1 moet gespecificeerd zijn;NOR=%1 m† spesifiseres.;SVE=%1 m†ste specificeras.';
      GLDirectPostWhenJobTxt@1160040008 : TextConst 'DEU=Fr Aufwandskonto %1 wurden eine oder mehrere obligatorische Dimensionen nicht angegeben.;ENU=%1 %2 must allow %3 when a %4 is specified.;NLD=%1 %2 moet %3 toestaan ??wanneer een %4 is opgegeven.';
      InboxError@1160040001 : TextConst 'DEU=Diese Ausgabe ist in dem Ausgaben-Posteingang vorhanden und muss von daher verarbeitet wrden;ENU=This mileage exists in the Mileage Inbox and must be processed from there;NLD=Deze uitgave bestaat in de uitgave inbox en dient vanuit daar verwerkt te worden';
      InvalidValue@1160040002 : TextConst 'DEU=" Ungltig %1:%2.";ENU=Invalid %1:%2.;NLD=Ongeldige %1:%2.';
      ReimbursementMissmatch@1160040004 : TextConst 'DEU=%1 kann nicht %2 sein, wenn %3 %4 ist.;ENU=%1 cannot be %2, when %3 is %4.;NLD=%1 kan niet %2 zijn als %3 %4 is.';
      ReimbursementMissmatch2@1160040005 : TextConst 'DEU=%1 und %2 mssen einen Wert habben wenn %3 %4 ist;ENU=%1 and %2 need to have a value, when %3 is %4;NLD=%1 en %2 moeten een waarde hebben wanneer %3 %4 is';
      ReimbursementMissmatch3@1160040006 : TextConst 'DEU=Sie k”nnen nicht %1 und %2 in der selben Zeit angeben.;ENU=%1 and %2 need to be blank, when %3 is %4;NLD=%1 en %2 mogen geen waarde hebben wanneer %3 %4 is';
      VarianceLimits@1160040009 : TextConst 'DEU=Es besteht eine Differenz zwischen dem gemeldeten und dem berechneten Abstand.;ENU=There is a difference between the reported and the calculated distance;NLD=Er is een verschil tussen de gerapporteerde en de berekende afstand';
      FutureDateErr@1160040010 : TextConst 'DEU=%1 kann nicht in der Zukunft sein;ENU=The %1 cannot be in the future;NLD=%1 kan niet in de toekomst zijn';

    LOCAL PROCEDURE Code@1160040031(Mileage@1160040001 : Record 6086338);
    VAR
      GLAcc@1160040010 : Record 15;
      ContiniaUserSetup@1160040003 : Record 6086002;
      Mileage2@1160040008 : Record 6086338;
      MileageInbox@1160040005 : Record 6086353;
      EMComment@1160040000 : Record 6086361;
      EMDimMgt@1160040006 : Codeunit 6086318;
      ExpCmtMgt@1160040002 : Codeunit 6086323;
      ValidationError@1160040007 : Text[250];
      NewCalculatedAccount@1160040004 : Code[20];
      EmptyGUID@1160040009 : GUID;
    BEGIN
      // THERE ARE 5 MOTHODS OF POSTING:
      // 1. POSTING ON THIS VENDOR IN NAV (INTERNAL)
      // 2. POSTING IN THE INTEGRATION SYSTEMS (EXTERNAL)
      // 3. POSTING IN A NON-INTEGRATED SYSTEM (EXTERNAL,NO ACCOUNTS SPECIFIED);
      // 4. POSTING ON THIS VENDOR IN NAV (INTERNAL) + POSTING IN THE INTEGRATION SYSTEMS (EXTERNAL)
      // 5. POSTING ON THIS VENDOR IN NAV (INTERNAL) + POSTING IN A NON-INTEGRATED SYSTEM (EXTERNAL,NO ACCOUNTS SPECIFIED);

      EMComment.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
      EMComment.SETRANGE("Table ID",DATABASE::Mileage);
      EMComment.SETRANGE("Document Type",0);
      EMComment.SETRANGE("Document No.",'');
      EMComment.SETRANGE("Doc. Ref. No.",Mileage."Entry No.");
      EMComment.SETRANGE("Validation Comment",TRUE);
      EMComment.DELETEALL;

      IF Mileage."Registration Date" > TODAY THEN
        ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,
          'FUTURE DATE',STRSUBSTNO(FutureDateErr,Mileage.FIELDCAPTION("Registration Date"),FORMAT(TODAY)),TRUE);

      IF Mileage."Continia User ID" = '' THEN
        ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",
          EMComment.Importance::Error,'NO EMPLOYEE',STRSUBSTNO(FieldMissing,Mileage.FIELDCAPTION("Continia User ID")),TRUE);

      IF Mileage."Registration Date" = 0D THEN
        ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",
          EMComment.Importance::Error,'NO REGISTRATION DATE',STRSUBSTNO(FieldMissing,Mileage.FIELDCAPTION("Registration Date")),TRUE);

      IF Mileage."Vehicle Code" = '' THEN
        ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",
          EMComment.Importance::Error,'MISSING VEHICLE',STRSUBSTNO(FieldMissing,Mileage.FIELDCAPTION("Vehicle Code")),TRUE);

      IF Mileage."Registration Date" <> 0D THEN
        IF DATE2DMY(Mileage."Registration Date",3) > 2100 THEN
          ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,'INVALID REG. DATE',
            STRSUBSTNO(InvalidValue,Mileage.FIELDCAPTION("Registration Date"),Mileage."Registration Date"),TRUE);

      IF NOT Mileage.MileageBetweenVarianceLimits THEN
        ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",
          EMComment.Importance::Information,'VARIANCELIMIT',STRSUBSTNO(VarianceLimits),TRUE);

      // VALIDATIONS ONLY FOR POSTING ON THE VENDOR
      IF (Mileage."Reimbursement Method" = Mileage."Reimbursement Method"::"External System") AND
         (Mileage."Mileage Account Type" = Mileage."Mileage Account Type"::" ")
      THEN
        EXIT;

      IF Mileage."Reimbursement Method" = Mileage."Reimbursement Method"::"Vendor (on User)" THEN
        IF (Mileage."Mileage Account Type" = Mileage."Mileage Account Type"::" ") OR (Mileage."Mileage Account" = '') THEN
          ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,'MISSMATCHINTERNAL2',
            STRSUBSTNO(ReimbursementMissmatch2,Mileage.FIELDCAPTION("Mileage Account Type"),
            Mileage.FIELDCAPTION("Mileage Account"),
            Mileage.FIELDCAPTION("Reimbursement Method"),Mileage."Reimbursement Method"),TRUE)
        ELSE
          IF ((Mileage."External Posting Account Type" <> Mileage."External Posting Account Type"::" ") OR
             (Mileage."External Posting Account No." <> ''))
          THEN
            ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,'MISSMATCHINTERNAL3',
              STRSUBSTNO(ReimbursementMissmatch3,Mileage.FIELDCAPTION("External Posting Account Type"),
              Mileage.FIELDCAPTION("External Posting Account No."),
              Mileage.FIELDCAPTION("Reimbursement Method"),Mileage."Reimbursement Method"),TRUE);

      IF Mileage."Mileage Account Type" = Mileage."Mileage Account Type"::" " THEN
        ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,'MIL. ACC. TYPE',
          STRSUBSTNO(FieldMissing,Mileage.FIELDCAPTION("Mileage Account Type")),TRUE)
      ELSE BEGIN
        IF (Mileage."Reimbursement Method" <> Mileage."Reimbursement Method"::"External System") AND
           (Mileage."Mileage Account Type" <> Mileage."Mileage Account Type"::"G/L Account")
        THEN BEGIN
          //
          Mileage2."Reimbursement Method" := Mileage2."Reimbursement Method"::"External System";
          ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,'MISSMATCHEXTERNAL',
            STRSUBSTNO(ReimbursementMissmatch,Mileage.FIELDCAPTION("Reimbursement Method"),Mileage."Reimbursement Method",
              Mileage.FIELDCAPTION("Mileage Account Type"),Mileage."Mileage Account Type"),TRUE);
        END;

        IF (Mileage."Reimbursement Method" = Mileage."Reimbursement Method"::"External System") AND
           (Mileage."Mileage Account Type" = Mileage."Mileage Account Type"::"G/L Account")
        THEN BEGIN
          Mileage2."Reimbursement Method" := Mileage2."Reimbursement Method"::"Vendor (on User)";
          ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,'MISSMATCHINTERNAL',
            STRSUBSTNO(ReimbursementMissmatch,Mileage.FIELDCAPTION("Reimbursement Method"),Mileage."Reimbursement Method",
              Mileage.FIELDCAPTION("Mileage Account Type"),Mileage."Mileage Account Type"),TRUE);
        END;
      END;

      IF (Mileage."Mileage Account" = '') AND (Mileage."Job No." = '') AND
         (Mileage."Mileage Account Type" <> Mileage."Mileage Account Type"::" ")
      THEN
        ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",
          EMComment.Importance::Error,'NO MIL ACC',STRSUBSTNO(FieldMissing,Mileage.FIELDCAPTION("Mileage Account")),TRUE);

      IF (Mileage."Mileage Account" <> '') AND Mileage.PostingSetupChanged(NewCalculatedAccount) THEN
        ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Warning,'ACCOUNT MISSMATCH',
          STRSUBSTNO(AccountMissmatch,Mileage.FIELDCAPTION("Mileage Account"),Mileage."Mileage Account",NewCalculatedAccount),TRUE);

      IF (Mileage."Mileage Account" <> '') AND (Mileage."Job No." <> '') THEN
        IF GLAcc.GET(Mileage."Mileage Account") THEN
          IF NOT GLAcc."Direct Posting" THEN
            ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,'JOB DIRECT POST',
              STRSUBSTNO(GLDirectPostWhenJobTxt,Mileage.FIELDCAPTION("Mileage Account"),Mileage."Mileage Account",
                GLAcc.FIELDCAPTION("Direct Posting"),Mileage.FIELDCAPTION("Job No.")),TRUE);

      IF NOT ExcludeInboxCheck THEN
        IF Mileage."Mileage GUID" <> EmptyGUID THEN BEGIN
          MileageInbox.SETCURRENTKEY("Mileage GUID");
          MileageInbox.SETRANGE("Mileage GUID",Mileage."Mileage GUID");
          MileageInbox.SETFILTER(Status,'%1|%2',MileageInbox.Status::Pending,MileageInbox.Status::Error);
          IF MileageInbox.FINDFIRST THEN
            ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",EMComment.Importance::Error,'INBOX ERROR',InboxError,TRUE);
        END;

      IF ContiniaUserSetup.GET(Mileage."Continia User ID") THEN
        IF NOT EMDimMgt.CheckDefaultDim(
           ValidationError,DATABASE::Mileage,0,'',Mileage."Entry No.",DATABASE::Vendor,ContiniaUserSetup."Vendor No.")
        THEN
          ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",
            EMComment.Importance::Error,'GLACCDEFDIMNVD',ValidationError,TRUE);

      IF Mileage."Mileage Account" <> '' THEN
        IF NOT EMDimMgt.CheckDefaultDim(
           ValidationError,DATABASE::Mileage,0,'',Mileage."Entry No.",DATABASE::"G/L Account",Mileage."Mileage Account")
        THEN
          ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",
            EMComment.Importance::Error,'GLACCDEFDIMEA',ValidationError,TRUE);

      IF Mileage."Job No." <> '' THEN
        IF NOT EMDimMgt.CheckDefaultDim(ValidationError,DATABASE::Mileage,0,'',Mileage."Entry No.",DATABASE::Job,Mileage."Job No.") THEN
          ExpCmtMgt.AddComment(DATABASE::Mileage,0,'',Mileage."Entry No.",
            EMComment.Importance::Error,'JOBDEFDIMEA',ValidationError,TRUE);
    END;

    PROCEDURE CheckWithoutInboxValidation@1160040001(Mileage@1160040000 : Record 6086338);
    BEGIN
      ExcludeInboxCheck := TRUE;
      Code(Mileage);
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
}

