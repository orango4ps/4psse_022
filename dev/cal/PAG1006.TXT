OBJECT Page 1006 Job Task Dimensions Multiple
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Task Dimensions Multiple;
               NOR=Prosjektoppgavedimensjoner - flere;
               SVE=Multipel av dimensioner f”r projektaktivitet];
    SourceTable=Table11020400;
    PageType=List;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             CurrPage.LOOKUPMODE := TRUE;
           END;

    OnOpenPage=BEGIN
                 GetDefaultDim;
               END;

    OnAfterGetRecord=BEGIN
                       DimensionValueCodeOnFormat(FORMAT("Dimension Value Code"));
                     END;

    OnInsertRecord=BEGIN
                     SETRANGE("Dimension Code","Dimension Code");
                     IF NOT FIND('-') AND ("Dimension Code" <> '') THEN BEGIN
                       "Multiple Selection Action" := "Multiple Selection Action"::Change;
                       INSERT;
                     END;
                     SETRANGE("Dimension Code");
                     EXIT(FALSE);
                   END;

    OnModifyRecord=BEGIN
                     "Multiple Selection Action" := "Multiple Selection Action"::Change;
                     MODIFY;
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     "Multiple Selection Action" := "Multiple Selection Action"::Delete;
                     MODIFY;
                     EXIT(FALSE);
                   END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN
                         LookupOKOnPush;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the dimension that the dimension value filter will be linked to. To select a dimension codes, which are set up in the Dimensions window, click the drop-down arrow in the field.;
                           NOR=Angir koden for dimensjonen som dimensjonsverdifiltret blir koblet til. Hvis du vil velge en dimensjonskode som er definert i vinduet Dimensjoner, klikker du rullegardinlisten i feltet.;
                           SVE=Anger koden f”r den dimension som dimensionsv„rdefiltret ska vara kopplat till. V„lj en dimensionskod (som du har skapat i f”nstret Dimensioner) genom att klicka p† den nedrullningsbara pilen i f„ltet.];
                ApplicationArea=#Dimensions;
                SourceExpr="Dimension Code";
                OnValidate=BEGIN
                             IF (xRec."Dimension Code" <> '') AND (xRec."Dimension Code" <> "Dimension Code") THEN
                               ERROR(Text000,TABLECAPTION);
                           END;
                            }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the dimension value that the dimension value filter will be linked to. To select a value code, which are set up in the Dimensions window, choose the drop-down arrow in the field.;
                           NOR=Angir koden for dimensjonsverdien som dimensjonsverdifiltret blir koblet til. Hvis du vil velge en valutakode som er definert i vinduet Dimensjoner, klikker du rullegardinlisten i feltet.;
                           SVE=Anger koden f”r det dimensionsv„rde som dimensionsv„rdefiltret ska vara kopplat till. V„lj en v„rdekod (som du har skapat i f”nstret Dimensioner) genom att v„lja den nedrullningsbara pilen i f„ltet.];
                ApplicationArea=#Dimensions;
                SourceExpr="Dimension Value Code" }

  }
  CODE
  {
    VAR
      TempJobTaskDim2@1000 : TEMPORARY Record 11020400;
      TempJobTaskDim3@1001 : TEMPORARY Record 11020400;
      TempJobTask@1002 : TEMPORARY Record 11072600;
      TotalRecNo@1003 : Integer;
      Text000@1004 : TextConst 'ENU=You cannot rename a %1.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn p† %1.';
      Text001@1005 : TextConst 'ENU=(Conflict);NOR=(Konflikt);SVE=(Konflikt)';

    LOCAL PROCEDURE SetCommonJobTaskDim@1006();
    VAR
      JobTaskDim@1000 : Record 11020400;
    BEGIN
      SETRANGE(
        "Multiple Selection Action","Multiple Selection Action"::Delete);
      IF FIND('-') THEN
        REPEAT
          IF TempJobTaskDim3.FIND('-') THEN
            REPEAT
              IF JobTaskDim.GET(TempJobTaskDim3."Job No.",TempJobTaskDim3."Job Task No.","Dimension Code")
              THEN
                JobTaskDim.DELETE(TRUE);
            UNTIL TempJobTaskDim3.NEXT = 0;
        UNTIL NEXT = 0;
      SETRANGE(
        "Multiple Selection Action","Multiple Selection Action"::Change);
      IF FIND('-') THEN
        REPEAT
          IF TempJobTaskDim3.FIND('-') THEN
            REPEAT
              IF JobTaskDim.GET(TempJobTaskDim3."Job No.",TempJobTaskDim3."Job Task No.","Dimension Code")
              THEN BEGIN
                JobTaskDim."Dimension Code" := "Dimension Code";
                JobTaskDim."Dimension Value Code" := "Dimension Value Code";
                JobTaskDim.MODIFY(TRUE);
              END ELSE BEGIN
                JobTaskDim.INIT;
                JobTaskDim."Job No." := TempJobTaskDim3."Job No.";
                JobTaskDim."Job Task No." := TempJobTaskDim3."Job Task No.";
                JobTaskDim."Dimension Code" := "Dimension Code";
                JobTaskDim."Dimension Value Code" := "Dimension Value Code";
                JobTaskDim.INSERT(TRUE);
              END;
            UNTIL TempJobTaskDim3.NEXT = 0;
        UNTIL NEXT = 0;
    END;

    [External]
    PROCEDURE SetMultiJobTask@1007(VAR JobTask@1000 : Record 11072600);
    BEGIN
      TempJobTaskDim2.DELETEALL;
      TempJobTask.DELETEALL;
      WITH JobTask DO
        IF FIND('-') THEN
          REPEAT
            CopyJobTaskDimToJobTaskDim("Job No.","Job Task No.");
            TempJobTask.TRANSFERFIELDS(JobTask);
            TempJobTask.INSERT;
          UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE CopyJobTaskDimToJobTaskDim@1008(JobNo@1000 : Code[20];JobTaskNo@1001 : Code[20]);
    VAR
      JobTaskDim@1002 : Record 11020400;
    BEGIN
      TotalRecNo := TotalRecNo + 1;
      TempJobTaskDim3."Job No." := JobNo;
      TempJobTaskDim3."Job Task No." := JobTaskNo;
      TempJobTaskDim3.INSERT;

      JobTaskDim.SETRANGE("Job No.",JobNo);
      JobTaskDim.SETRANGE("Job Task No.",JobTaskNo);
      IF JobTaskDim.FIND('-') THEN
        REPEAT
          TempJobTaskDim2 := JobTaskDim;
          TempJobTaskDim2.INSERT;
        UNTIL JobTaskDim.NEXT = 0;
    END;

    LOCAL PROCEDURE GetDefaultDim@1009();
    VAR
      Dim@1000 : Record 348;
      RecNo@1001 : Integer;
    BEGIN
      RESET;
      DELETEALL;
      IF Dim.FIND('-') THEN
        REPEAT
          RecNo := 0;
          TempJobTaskDim2.SETRANGE("Dimension Code",Dim.Code);
          SETRANGE("Dimension Code",Dim.Code);
          IF TempJobTaskDim2.FIND('-') THEN
            REPEAT
              IF FIND('-') THEN BEGIN
                IF "Dimension Value Code" <> TempJobTaskDim2."Dimension Value Code" THEN
                  IF ("Multiple Selection Action" <> 10) AND
                     ("Multiple Selection Action" <> 21)
                  THEN BEGIN
                    "Multiple Selection Action" :=
                      "Multiple Selection Action" + 10;
                    "Dimension Value Code" := '';
                  END;
                MODIFY;
                RecNo := RecNo + 1;
              END ELSE BEGIN
                Rec := TempJobTaskDim2;
                INSERT;
                RecNo := RecNo + 1;
              END;
            UNTIL TempJobTaskDim2.NEXT = 0;

          IF FIND('-') AND (RecNo <> TotalRecNo) THEN
            IF ("Multiple Selection Action" <> 10) AND
               ("Multiple Selection Action" <> 21)
            THEN BEGIN
              "Multiple Selection Action" :=
                "Multiple Selection Action" + 10;
              "Dimension Value Code" := '';
              MODIFY;
            END;
        UNTIL Dim.NEXT = 0;

      RESET;
      SETCURRENTKEY("Dimension Code");
      SETFILTER(
        "Multiple Selection Action",'<>%1',"Multiple Selection Action"::Delete)
    END;

    LOCAL PROCEDURE LookupOKOnPush@19031339();
    BEGIN
      SetCommonJobTaskDim;
    END;

    LOCAL PROCEDURE DimensionValueCodeOnFormat@19000225(Text@19073275 : Text[1024]);
    BEGIN
      IF "Dimension Code" <> '' THEN
        IF ("Multiple Selection Action" = 10) OR
           ("Multiple Selection Action" = 21)
        THEN
          Text := Text001;
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
}

