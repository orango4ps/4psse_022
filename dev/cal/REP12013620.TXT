OBJECT Report 12013620 Print new Order Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF500000;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Print new Order Document;
               SVE=Skriv ut nytt Order dokument];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4272;    ;DataItem;                    ;
               DataItemTable=Table12013608;
               DataItemTableView=SORTING(Document Type,Document No.)
                                 WHERE(Document Type=CONST(Order));
               OnPreDataItem=BEGIN
                               ExFlowSetup.GET;

                               NoPrinted := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT PurchHeader.GET("Document Type","Document No.") THEN
                                    CurrReport.SKIP;

                                  IF OnlyWhenDocMissing THEN BEGIN
                                    IF ExFlowSetup."File storage type" = ExFlowSetup."File storage type"::Filesystem THEN BEGIN
                                      IF "Image Name" <> '' THEN
                                        IF ExFlowFileMgt.FileExist(ExFlowSetup."Path to Used Invoices"+
                                                                   ExFlowFileMgt.Path("Image Name"),ExFlowFileMgt.GetFileName("Image Name")) THEN
                                          CurrReport.SKIP;
                                    END ELSE BEGIN
                                      IF ExDoc2.RetrieveRecord(ExDoc2,PurchHeader."Document Type",PurchHeader."No.") THEN
                                        IF ExDoc2."Image Blob".HASVALUE THEN
                                          CurrReport.SKIP;
                                    END;
                                  END;

                                  PurchHeader.SETRECFILTER;
                                  CLEAR(ExQuoteToOrder);
                                  ExQuoteToOrder.PrintPODoc(PurchHeader);

                                  ExDoc.GET(ID);
                                  ExDoc."Change Date" := CREATEDATETIME(TODAY,TIME);
                                  ExDoc."Changed By" := USERID;
                                  ExDoc.MODIFY;

                                  COMMIT;

                                  NoPrinted := NoPrinted + 1;
                                END;

               OnPostDataItem=BEGIN
                                IF NoPrinted <> 0 THEN
                                  IF GUIALLOWED THEN
                                    MESSAGE(STRSUBSTNO(Text001,NoPrinted));
                              END;

               ReqFilterFields=Document No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100285001;0;Container;
                  ContainerType=ContentArea }

      { 1100285000;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ] }

      { 1100285004;2;Field  ;
                  CaptionML=[ENU=Only print for documents with no image attached;
                             SVE=Skriv enbart ut f”r dokument utan n†gon bildfil];
                  SourceExpr=OnlyWhenDocMissing }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PurchHeader@1100285003 : Record 38;
      ExFlowSetup@1100285004 : Record 12013601;
      ExDoc@1100285007 : Record 12013608;
      ExQuoteToOrder@1100285000 : Codeunit 12013607;
      ExFlowFileMgt@1100285002 : Codeunit 12013602;
      OnlyWhenDocMissing@1100285001 : Boolean;
      Text001@1100285005 : TextConst 'ENU=%1 Documents printed.;SVE=%1 dokument utskrivna.';
      NoPrinted@1100285006 : Integer;
      ExDoc2@1100285008 : Record 12013612;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

