OBJECT Page 11229411 Copy Unit Prices to Other Room
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=;
    PageType=StandardDialog;
    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1100525011;0;Container;
                ContainerType=ContentArea }

    { 1100525010;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopi‰ren van;
                           NOR=Kopier fra;
                           SVE=Kopiera fr†n];
                GroupType=Group }

    { 1100525000;2;Field  ;
                Name=FromServiceOrderNo;
                CaptionML=[DEU=Serviceauftrag;
                           ENU=Service Order;
                           NLD=Serviceorder;
                           NOR=Serviceordre;
                           SVE=Serviceorder];
                SourceExpr=FromServiceOrderNo;
                OnValidate=BEGIN
                             ServiceOrder.GET(FromServiceOrderNo);
                             IF ToServiceOrderNo = '' THEN
                               ToServiceOrderNo := FromServiceOrderNo;
                           END;

                OnLookup=BEGIN
                           IF PAGE.RUNMODAL(0, ServiceOrder) = ACTION::LookupOK THEN BEGIN
                             FromServiceOrderNo := ServiceOrder."No.";
                             IF ToServiceOrderNo = '' THEN
                               ToServiceOrderNo := FromServiceOrderNo;
                           END;
                         END;
                          }

    { 1100525009;2;Field  ;
                Name=FromRoom;
                CaptionML=[DEU=Raum;
                           ENU=Room;
                           NLD=Ruimte;
                           NOR=Rom;
                           SVE=Rum];
                SourceExpr=FromRoom;
                OnValidate=BEGIN
                             ServiceOrder.GET(FromServiceOrderNo);
                             SUPRoom.GET(ServiceOrder."Customer No.",FromRoom);
                             IF ToRoom = '' THEN
                               ToRoom := FromRoom;
                           END;

                OnLookup=BEGIN
                           IF ServiceOrder.GET(FromServiceOrderNo) THEN
                             SUPRoom.SETRANGE(SUPRoom."Customer No.",ServiceOrder."Customer No.");

                           IF PAGE.RUNMODAL(0, SUPRoom) = ACTION::LookupOK THEN BEGIN
                               FromRoom := SUPRoom.Code;
                               IF ToRoom = '' THEN
                                 ToRoom := FromRoom;
                           END;
                         END;
                          }

    { 1100525004;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopi‰ren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 1100525001;2;Field  ;
                Name=ToServiceOrderNo;
                CaptionML=[DEU=Serviceauftrag;
                           ENU=Service Order;
                           NLD=Serviceorder;
                           NOR=Serviceordre;
                           SVE=Serviceorder];
                SourceExpr=ToServiceOrderNo;
                OnValidate=BEGIN
                             ServiceOrder.GET(ToServiceOrderNo);
                           END;

                OnLookup=BEGIN
                           IF PAGE.RUNMODAL(0, ServiceOrder) = ACTION::LookupOK THEN
                             ToServiceOrderNo := ServiceOrder."No."
                         END;
                          }

    { 1100525003;2;Field  ;
                Name=ToRoom;
                CaptionML=[DEU=Raum;
                           ENU=Room;
                           NLD=Ruimte;
                           NOR=Rom;
                           SVE=Rum];
                SourceExpr=ToRoom;
                OnValidate=BEGIN
                             ServiceOrder.GET(ToServiceOrderNo);
                             SUPRoom.GET(ServiceOrder."Customer No.",ToRoom);
                           END;

                OnLookup=BEGIN
                           IF ServiceOrder.GET(ToServiceOrderNo) THEN
                             SUPRoom.SETRANGE(SUPRoom."Customer No.",ServiceOrder."Customer No.");

                           IF PAGE.RUNMODAL(0, SUPRoom) = ACTION::LookupOK THEN
                               ToRoom := SUPRoom.Code;
                         END;
                          }

    { 1100525002;1;Group  ;
                GroupType=Group }

    { 1100525005;2;Field  ;
                CaptionML=[DEU=Voraufnahme kopieren;
                           ENU=Copy Pre-Recording;
                           NLD=Kopieer vooropname];
                SourceExpr=CopyPreRecording }

  }
  CODE
  {
    VAR
      ServiceOrder@1100525005 : Record 11012823;
      SUPRoom@1100525006 : Record 11071950;
      CloseActionOK@1100525000 : Boolean;
      FromRoom@1100525001 : Code[20];
      ToRoom@1100525002 : Code[20];
      FromServiceOrderNo@1100525003 : Code[20];
      ToServiceOrderNo@1100525004 : Code[20];
      CopyPreRecording@1100525007 : Boolean;

    PROCEDURE SetDefaults@1210190001(ServiceOrderNo@1210190005 : Code[20];Room@1210190007 : Code[20]);
    BEGIN
      FromServiceOrderNo := ServiceOrderNo;
      FromRoom := Room;
      ToServiceOrderNo := ServiceOrderNo;
    END;

    LOCAL PROCEDURE CopyUnitPrices@1100525002();
    VAR
      SUPRecordingLine@1100525000 : Record 11071772;
      SUPRecordingLine1@1100525001 : Record 11071772;
      SUPRecordingLineNew@1100525002 : Record 11071772;
    BEGIN
      SUPRecordingLine.SETRANGE("Service Order No.",FromServiceOrderNo);
      SUPRecordingLine.SETRANGE(Room, FromRoom);
      IF SUPRecordingLine.FINDFIRST THEN BEGIN
        REPEAT
          SUPRecordingLineNew := SUPRecordingLine;
          SUPRecordingLineNew."Service Order No." := ToServiceOrderNo;
          SUPRecordingLineNew.Room := ToRoom;
          IF NOT CopyPreRecording THEN BEGIN
            SUPRecordingLineNew."Pre-Recording Quantity" := 0;
            SUPRecordingLineNew."Pre-Recording Amount" := 0;
          END;
          IF SUPRecordingLine1.FINDLAST() THEN
            SUPRecordingLineNew."Line No." := SUPRecordingLine1."Line No." + 10000
          ELSE
            SUPRecordingLineNew."Line No." := 10000;
          SUPRecordingLineNew."Recording Quantity" := 0;
          SUPRecordingLineNew."Recording Amount" := 0;
          SUPRecordingLineNew."Production Recording Amount" := 0;
          SUPRecordingLineNew."Quantity Recording" := 0;
          SUPRecordingLineNew."Length Recording" := 0;
          SUPRecordingLineNew."Width Recording" := 0;
          SUPRecordingLineNew."Height Recording" := 0;
          SUPRecordingLineNew.INSERT(TRUE);
        UNTIL SUPRecordingLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      CopyUnitPrices;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

