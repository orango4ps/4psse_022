OBJECT Codeunit 234 Gen. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=232;
    OnRun=BEGIN
            GenJnlBatch.COPY(Rec);
            Code;
            Rec := GenJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the report(s)?;NOR=Vil du bokf›re kladdene og skrive ut rapporten(e)?;SVE=Vill du bokf”ra journalerna och skriva ut rapporterna?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;NOR=Kladdene ble bokf›rt p† riktig m†te.;SVE=Journalerna „r bokf”rda.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";NOR="Det er ikke mulig † bokf›re alle kladdene. ";SVE="Det var inte m”jligt att bokf”ra alla journaler. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;NOR=Kladdene som ikke ble bokf›rt, er merket.;SVE=Journalerna som inte blev bokf”rda „r nu markerade.';
      GenJnlTemplate@1004 : Record 80;
      GenJnlBatch@1005 : Record 232;
      GenJnlLine@1006 : Record 81;
      GLReg@1007 : Record 45;
      CustLedgEntry@1010 : Record 21;
      VendLedgEntry@1011 : Record 25;
      GenJnlPostBatch@1008 : Codeunit 13;
      GenJnlManagement@1212 : Codeunit 230;
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    VAR
      ConfirmManagement@1002 : Codeunit 27;
      HideDialog@1003 : Boolean;
      IsHandled@1000 : Boolean;
      OrderByDocNoAndLineNo@1001 : Boolean;
      GenJournalTemplatePerRole@1100525000 : Record 11020419;
    BEGIN
      // If simple view is used then order gen. journal lines by doc no. and line no.
      IF GenJnlManagement.GetJournalSimplePageModePreference(PAGE::"General Journal") THEN
        OrderByDocNoAndLineNo := TRUE;

      WITH GenJnlBatch DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        GenJournalTemplatePerRole.CheckPermission(TABLECAPTION, "Journal Template Name",TRUE); //**4PS.n

        IF GenJnlTemplate."Force Posting Report" OR
           (GenJnlTemplate."Cust. Receipt Report ID" = 0) AND (GenJnlTemplate."Vendor Receipt Report ID" = 0)
        THEN
          GenJnlTemplate.TESTFIELD("Posting Report ID");

        OnBeforePostJournalBatch(GenJnlBatch,HideDialog);

        IF NOT HideDialog THEN
          IF NOT ConfirmManagement.ConfirmProcess(Text000,TRUE) THEN
            EXIT;

        FIND('-');
        REPEAT
          GenJnlLine."Journal Template Name" := "Journal Template Name";
          GenJnlLine."Journal Batch Name" := Name;
          GenJnlLine."Line No." := 1;
          IF OrderByDocNoAndLineNo THEN
            GenJnlLine.SETCURRENTKEY("Document No.","Line No.");
          CLEAR(GenJnlPostBatch);
          IF GenJnlPostBatch.RUN(GenJnlLine) THEN BEGIN
            OnAfterPostJournalBatch(GenJnlBatch);
            MARK(FALSE);
            IF GLReg.GET(GenJnlLine."Line No.") THEN BEGIN
              IF GenJnlTemplate."Cust. Receipt Report ID" <> 0 THEN BEGIN
                CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
      //        REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",FALSE,FALSE,CustLedgEntry); //**4PS.o
                REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",GUIALLOWED,FALSE,CustLedgEntry); //**4PS.n
              END;
              IF GenJnlTemplate."Vendor Receipt Report ID" <> 0 THEN BEGIN
                VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
      //        REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",FALSE,FALSE,VendLedgEntry); //**4PS.o
                REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",GUIALLOWED,FALSE,VendLedgEntry); //**4PS.n
              END;
              IF GenJnlTemplate."Posting Report ID" <> 0 THEN BEGIN
                GLReg.SETRECFILTER;
                OnBeforeGLRegPostingReportPrint(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg,IsHandled);
                IF NOT IsHandled THEN
                  //REPORT.RUN(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg); //**4PS.o
                  REPORT.RUN(GenJnlTemplate."Posting Report ID",GUIALLOWED,FALSE,GLReg); //**4PS.n
              END;
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          Name := '';
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostJournalBatch@4(VAR GenJournalBatch@1000 : Record 232);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeGLRegPostingReportPrint@2(VAR ReportID@1000 : Integer;ReqWindow@1001 : Boolean;SystemPrinter@1002 : Boolean;VAR GLRegister@1003 : Record 45;VAR Handled@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@3(VAR GenJournalBatch@1000 : Record 232;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

