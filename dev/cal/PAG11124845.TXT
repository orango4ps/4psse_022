OBJECT Page 11124845 Entity Types
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[DEU=Einheitsart;
               ENU=Entity Types;
               NLD=Entiteitsoorten;
               NOR=Enhetstyper;
               SVE=Enhetstyper];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11012526;
    PageType=List;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             InitRecords;
           END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Group  ;
                GroupType=Repeater }

    { 1100525001;2;Field  ;
                SourceExpr="Entity Type" }

    { 1100525003;2;Field  ;
                SourceExpr=Description }

  }
  CODE
  {

    PROCEDURE InitRecords@1100525000();
    VAR
      i@1100525002 : Integer;
      FldRef@1100525003 : FieldRef;
      RecRef@1100525004 : RecordRef;
      NrOfOptions@1100525005 : Integer;
      OptionString@1100525006 : Text[1024];
    BEGIN
      RecRef.OPEN(DATABASE::"Project Material Req. Line");
      FldRef := RecRef.FIELD(FIELDNO("Entity Type"));
      OptionString := FldRef.OPTIONSTRING;
      NrOfOptions := STRLEN(OptionString) - STRLEN(DELCHR(OptionString,'=',','))+1;
      FOR i := 1 TO NrOfOptions DO BEGIN
        "Line No." := i;
        "Entity Type" := i-1;
        Description := FORMAT("Entity Type");
        IF (STRLEN(Description) > 2) THEN
          INSERT;
      END;
    END;

    PROCEDURE GetSelectionFilter@2() : Text[250];
    VAR
      FirstEntType@1001 : Text[30];
      SelectionFilter@1003 : Text[250];
      EntTypeCount@1004 : Integer;
      More@1005 : Boolean;
    BEGIN
      CurrPage.SETSELECTIONFILTER(Rec);
      EntTypeCount := Rec.COUNT;
      IF EntTypeCount > 0 THEN BEGIN
        Rec.FIND('-');
        WHILE EntTypeCount > 0 DO BEGIN
          EntTypeCount := EntTypeCount - 1;
          Rec.MARKEDONLY(FALSE);
          FirstEntType := FORMAT(Rec."Entity Type");
          More := (EntTypeCount > 0);
          IF SelectionFilter <> '' THEN
            SelectionFilter := SelectionFilter + '|';
          SelectionFilter := SelectionFilter + FirstEntType;
          IF EntTypeCount > 0 THEN BEGIN
            Rec.MARKEDONLY(TRUE);
            Rec.NEXT;
          END;
        END;
      END;
      EXIT(SelectionFilter);
    END;

    BEGIN
    END.
  }
}

