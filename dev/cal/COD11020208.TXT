OBJECT Codeunit 11020208 TNT Post KIX Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'DEU=%1 ist keine gÅltige Postleitzahl fÅr die Post.;ENU=%1 is not a valid post code for TNT Post KIX.;NLD=%1 is geen geldige postcode voor TNT Post KIX.;NOR=%1 er ikke et gyldig postnummer for TNT Post KIX.;SVE=%1 Ñr inte ett giltigt postnummer fîr TNT Post KIX.';
      TNTPostKIXStopEntryBuffer@1100485001 : TEMPORARY Record 11020368;

    PROCEDURE BuildKIXCode@1100485000(Address@1100485000 : Text[100];PostCode@1100485001 : Code[20];CountryRegionCode@1100485002 : Code[10]) Result : Code[20];
    VAR
      HouseNo@1100485005 : Text[50];
      HouseNoExt@1100485006 : Text[50];
    BEGIN
      CountryRegionCode := NormalizeCountryRegionCode(CountryRegionCode);

      IF UseKIXCodeInCountryRegion(CountryRegionCode) THEN BEGIN
        IF (PostCode = '') THEN
          EXIT;

        PostCode := NormalizePostCode(PostCode, CountryRegionCode);
        Address := NormalizeAddress(Address);

        IF (STRPOS(Address, 'TO ') = 0) AND
           (STRPOS(Address, 'T/O ') = 0) AND
           (STRPOS(Address, 'T.O. ') = 0)
        THEN BEGIN
          Address := DELCHR(Address, '<', '0123456789'); // Skip leading numbers
          Address := DELCHR(Address, '<', DELCHR(Address, '=', '0123456789')); // Skip street name
          HouseNo := COPYSTR(Address, 1, STRLEN(Address) - STRLEN(DELCHR(Address, '<', '0123456789')));

          BufferIgnorableNumbers(PostCode);

          WHILE IsIgnorableNumber(HouseNo) DO BEGIN
            Address := DELSTR(Address, 1, STRLEN(HouseNo));
            Address := DELCHR(Address, '<', DELCHR(Address, '=', '0123456789'));
            HouseNo := COPYSTR(Address, 1, STRLEN(Address) - STRLEN(DELCHR(Address, '<', '0123456789')));
          END;

          HouseNoExt := DELSTR(Address, 1, STRLEN(HouseNo));
        END;

        Result := PostCode;
        IF HouseNo <> '' THEN BEGIN
          Result := Result + HouseNo;
          IF HouseNoExt <> '' THEN
            Result := Result + 'X' + HouseNoExt;
        END;
      END;
    END;

    LOCAL PROCEDURE NormalizeCountryRegionCode@1100485017(CountryRegionCode@1100485001 : Code[10]) : Code[10];
    VAR
      CompanyInfo@1100485000 : Record 79;
    BEGIN
      IF CountryRegionCode = '' THEN BEGIN
        CompanyInfo.GET;
        CompanyInfo.TESTFIELD("Country/Region Code");
        EXIT(CompanyInfo."Country/Region Code");
      END;

      EXIT(CountryRegionCode);
    END;

    LOCAL PROCEDURE NormalizePostCode@1100485034(PostCode@1100485000 : Code[20];CountryRegionCode@1100485001 : Code[10]) Result : Code[20];
    BEGIN
      Result := DELCHR(PostCode, '=');

      IF COPYSTR(Result, 1, STRLEN(CountryRegionCode) + 1) = STRSUBSTNO('%1-', CountryRegionCode) THEN
        Result := DELSTR(Result, 1, STRLEN(CountryRegionCode) + 1);

      IF STRLEN(Result) <> 6 THEN
        ERROR(Text000, PostCode);
    END;

    LOCAL PROCEDURE NormalizeAddress@1100485002(Address@1100485000 : Text[100]) : Text[100];
    BEGIN
      EXIT(UPPERCASE(Address));
    END;

    LOCAL PROCEDURE BufferIgnorableNumbers@1100485006(PostCode@1100485002 : Code[20]);
    VAR
      TNTPostKIXStopEntry@1100485001 : Record 11020368;
    BEGIN
      // Ignorable numbers already found should not be found again, to prevent
      // misinterpretation of e.g. "Plein 40-45 40". Buffer numbers, and remove
      // from buffer when found.

      TNTPostKIXStopEntryBuffer.RESET;
      TNTPostKIXStopEntryBuffer.DELETEALL;

      TNTPostKIXStopEntry.SETRANGE("Post Code", PostCode);

      IF TNTPostKIXStopEntry.FINDSET THEN
        REPEAT
          TNTPostKIXStopEntryBuffer := TNTPostKIXStopEntry;
          TNTPostKIXStopEntryBuffer.INSERT;
        UNTIL TNTPostKIXStopEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE IsIgnorableNumber@1100485001(Number@1100485001 : Text[50]) : Boolean;
    BEGIN
      IF TNTPostKIXStopEntryBuffer.FINDSET THEN
        REPEAT
          IF (TNTPostKIXStopEntryBuffer."Ignorable Characters" = Number) OR
             (TNTPostKIXStopEntryBuffer."Alt. Ignorable Characters" = Number)
          THEN BEGIN
            TNTPostKIXStopEntryBuffer.DELETE;
            EXIT(TRUE);
          END;
        UNTIL TNTPostKIXStopEntryBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE UseKIXCodeInCountryRegion@1100485023(CountryRegionCode@1100485001 : Code[10]) : Boolean;
    VAR
      CountryRegion@1100485000 : Record 9;
    BEGIN
      CountryRegion.GET(CountryRegionCode);
      EXIT(CountryRegion."Use TNT Post KIX Code");
    END;

    BEGIN
    END.
  }
}

