OBJECT Page 11012390 Import External Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import External Budget;
               NOR=Importer eksternt budsjett;
               SVE=Importera extern budget];
    SaveValues=No;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    PageType=NavigatePage;
    OnInit=BEGIN
             ElementOptoutVisible := TRUE;
             OptionBudgetVisible := TRUE;
             SettlementQuantityVisible := TRUE;
           END;

    OnOpenPage=VAR
                 ClientTypeManagement@1100528500 : Codeunit 4;
               BEGIN
                 IF OriginCde = '' THEN
                   ERROR('');

                 JobSetup.GET;

                 FileNameInputVISIBLE := ((OriginCde = 'TRAD-TXT') OR (OriginCde = 'IBIS-INFRA')) AND NOT ClientTypeManagement.IsCommonWebClientType;
                 SetElementOptoutVisible();
                 SetEnableElement;  //db, 10-10-16: C028543

                 OptionBudgetVisible := (FromOptionBudget AND (OriginCde <> 'IBIS-INFRA'));
                 SettlementQuantityVisible := (FromSettlementQuantity AND (OriginCde <> 'IBIS-INFRA'));

                 //db.sn, 18-04-13
                 FillElementVisible := (OriginCde = 'KPDGWW-XML');
                 FillCostObjectVisible := (OriginCde = 'KPDGWW-XML');
                 DeriveCostTypeVisible := (OriginCde = 'KPDGWW-XML');
                 ApplyEntriesVisible := (OriginCde = 'KPDGWW-XML');  //db, 20-05-16: C030223
                 //db.en, 18-04-13

                 CreateTenderParagrahVisible := (OriginCde = 'CUF-XML-GWW');  //dp00404
                 ApplyCodeEnable := (OriginCde = 'CUF-XML');  //C042315
                 ApplyCostPriceEnable := (OriginCde = 'CUF-XML-GWW');  //C051676
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525000;1 ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 3       ;2   ;Action    ;
                      CaptionML=[ENU=&Import;
                                 NOR=&Importer;
                                 SVE=&Import];
                      InFooterBar=Yes;
                      Image=Report;
                      OnAction=BEGIN
                                 CurrPage.EDITABLE(FALSE);
                                 FinishWizard;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190002;1;Field  ;
                CaptionML=[ENU=Import External Budget;
                           NOR=Importer eksternt budsjett;
                           SVE=Importera extern budget];
                Style=Strong;
                StyleExpr=TRUE }

    { 1   ;1   ;Field     ;
                CaptionML=[ENU=Project;
                           NOR=Prosjekt;
                           SVE=Projekt];
                SourceExpr=ProjectCde;
                OnValidate=BEGIN
                             ValidateProject(ProjectCde);
                           END;

                OnLookup=BEGIN
                           ProjRec."No.":= ProjectCde;
                           IF PAGE.RUNMODAL(PAGE::"Job List",ProjRec) = ACTION::LookupOK THEN BEGIN
                             ValidateProject(ProjRec."No.");
                             ProjectCde := ProjRec."No.";
                           END;
                         END;
                          }

    { 4   ;1   ;Field     ;
                CaptionML=[ENU=Adjustment;
                           NOR=Justering;
                           SVE=Justering];
                SourceExpr=AdjustmentCde;
                OnValidate=BEGIN
                             IF AdjustmentCde <> '' THEN
                               AdjustmentRec.GET(ProjectCde,AdjustmentCde);
                           END;

                OnLookup=BEGIN
                           AdjustmentRec.SETRANGE("Project No.",ProjectCde);
                           IF PAGE.RUNMODAL(PAGE::"Budget Adjustment List",AdjustmentRec) = ACTION::LookupOK THEN
                             AdjustmentCde := AdjustmentRec."No.";
                         END;
                          }

    { 6   ;1   ;Field     ;
                CaptionML=[ENU=Extension Contract;
                           NOR=ETA;
                           SVE=ŽTA];
                SourceExpr=ExtensionCde;
                OnValidate=BEGIN
                             IF ExtensionCde <> '' THEN
                               ExtensionRec.GET(ProjectCde,ExtensionCde);
                           END;

                OnLookup=BEGIN
                           ExtensionRec.SETRANGE("Project No.",ProjectCde);
                           IF PAGE.RUNMODAL(PAGE::"Extension List",ExtensionRec) = ACTION::LookupOK THEN
                             ExtensionCde := ExtensionRec."Contract No.";
                         END;
                          }

    { 1100485001;1;Field  ;
                Name=OnlyBudgetLines;
                CaptionML=[ENU=Import Budget Lines only;
                           NOR=Importer kun budsjettrader;
                           SVE=Importera endast budgetrader];
                SourceExpr=OnlyBudgetLines;
                Visible=OptionBudgetVisible }

    { 1210190012;1;Field  ;
                Name=HouseModel;
                CaptionML=[ENU=House Model;
                           NOR=Husmodell;
                           SVE=Husmodell];
                SourceExpr=HouseModelCde;
                Visible=OptionBudgetVisible;
                OnValidate=BEGIN
                             IF HouseModelCde <> '' THEN
                               HouseModelRec.GET(ProjectCde, HouseModelCde);
                           END;

                OnLookup=BEGIN
                           LookupHouseModel();
                         END;
                          }

    { 1100485000;1;Field  ;
                Name=Plot;
                CaptionML=[ENU=Plotno.;
                           NOR=Tegningnr;
                           SVE=Ritningnr];
                SourceExpr=PlotCde;
                Visible=OptionBudgetVisible;
                OnValidate=BEGIN
                             IF PlotCde <> '' THEN
                               PlotRec.GET(ProjectCde, PlotCde);
                           END;

                OnLookup=BEGIN
                           LookupPlot();
                         END;
                          }

    { 1210190003;1;Field  ;
                Name=MainGroup;
                CaptionML=[ENU=Option Main Group;
                           NOR=Alternativhovedgruppe;
                           SVE=Alternativhuvudgrupp];
                SourceExpr=MainGroupCde;
                Visible=OptionBudgetVisible;
                OnValidate=BEGIN
                             IF MainGroupCde <> '' THEN
                               MainGroupRec.GET(ProjectCde, PlotCde, MainGroupCde);
                           END;

                OnLookup=BEGIN
                           LookupMainGroup();
                         END;
                          }

    { 1210190007;1;Field  ;
                Name=Group;
                CaptionML=[ENU=Option Group;
                           NOR=Alternativgruppe;
                           SVE=Alternativgrupp];
                SourceExpr=GroupCde;
                Visible=OptionBudgetVisible;
                OnValidate=BEGIN
                             IF GroupCde <> '' THEN
                               GroupRec.GET(ProjectCde, PlotCde,  GroupCde);
                           END;

                OnLookup=BEGIN
                           LookupGroup();
                         END;
                          }

    { 1210190009;1;Field  ;
                Name=SubGroup;
                CaptionML=[ENU=Option Sub Group;
                           NOR=Alternativundergrupp;
                           SVE=Alternativundergrupp];
                SourceExpr=SubGroupCde;
                Visible=OptionBudgetVisible;
                OnValidate=BEGIN
                             IF SubGroupCde <> '' THEN
                               SubGroupRec.GET(ProjectCde, PlotCde, SubGroupCde);
                           END;

                OnLookup=BEGIN
                           LookupSubgroup();
                         END;
                          }

    { 8   ;1   ;Field     ;
                Name=Option;
                CaptionML=[ENU=Option;
                           NOR=Alternativ;
                           SVE=Alternativ];
                SourceExpr=OptionCde;
                Visible=OptionBudgetVisible;
                OnValidate=BEGIN
                             ValidateOption();
                           END;

                OnLookup=BEGIN
                           LookupOption();
                         END;
                          }

    { 1100528201;1;Field  ;
                Name=HouseModel2;
                CaptionML=[ENU=House Model;
                           NOR=Husmodell;
                           SVE=Husmodell];
                SourceExpr=HouseModelCde;
                Visible=SettlementQuantityVisible;
                OnValidate=BEGIN
                             IF HouseModelCde <> '' THEN
                               HouseModelRec.GET(ProjectCde, HouseModelCde);
                           END;

                OnLookup=BEGIN
                           LookupHouseModel();
                         END;
                          }

    { 1100528200;1;Field  ;
                Name=SettlementQuantity;
                CaptionML=ENU=Settlement Quantity;
                SourceExpr=SettlementQuantityCde;
                Visible=SettlementQuantityVisible;
                OnValidate=BEGIN
                             ValidateSettlQuantity();
                           END;

                OnLookup=BEGIN
                           LookupSettlQuantity();
                         END;
                          }

    { 1210190004;1;Field  ;
                Name=ElementOptout;
                CaptionML=[ENU=Do not process Elements;
                           NOR=Ikke prosesser elementer;
                           SVE=Behandla inte element];
                SourceExpr=ElementOptout;
                Visible=ElementOptoutVisible;
                OnValidate=BEGIN
                             //db.sn, 10-10-16: C028543
                             IF ElementOptout = TRUE THEN BEGIN
                               CodingSystemElement := '';
                               ElementCde := '';
                             END;
                             CodingSystemElementEnable := (ElementOptout = FALSE) AND (OriginCde <> 'KPDGWW-XML');
                             ElementEnable := (ElementOptout = FALSE);
                             //db.en, 10-10-16: C028543
                           END;
                            }

    { 1210190001;1;Field  ;
                Name=FileNameInput;
                CaptionML=[ENU=Filename;
                           NOR=Filnavn;
                           SVE=Filnamn];
                SourceExpr=FileNameTxt;
                Visible=FileNameInputVISIBLE;
                OnAssistEdit=VAR
                               FileAbsPathName@1100525000 : Text;
                             BEGIN
                               JobSetup.GET;
                               IF Folder = '' THEN
                                 Folder := JobSetup."Pathname External Estimates";
                               IF FileMgt.SelectFile('txt', Folder, FileNameTxt, FileAbsPathName) THEN
                                 IF FileNameTxt <> '' THEN
                                   FileNameTxt := COPYSTR(FileNameTxt, 2, STRLEN(FileNameTxt) - 5);  //verwijderen voorloopletter + extensie
                             END;
                              }

    { 1100525001;1;Field  ;
                Name=FillElementWith;
                CaptionML=[ENU=Fill Element with;
                           NOR=Fyll ut element med;
                           SVE=Fyll i element med];
                OptionCaptionML=[ENU=Sort Code,Tender Code;
                                 NOR=Typekode,Tilbudskode;
                                 SVE=" Typkod,Anbudskod"];
                SourceExpr=FillElementWith;
                Visible=FillElementVisible;
                Enabled=ElementEnable }

    { 1100527300;1;Field  ;
                Name=FillElementManually;
                CaptionML=ENU=Fill Element manually;
                SourceExpr=FillElementManually;
                Visible=FillElementVisible;
                Enabled=ElementEnable }

    { 1100525002;1;Field  ;
                Name=FillCostObjectWith;
                CaptionML=[ENU=Fill Cost Object with;
                           NOR=Fyll ut kostnadsobjekt med;
                           SVE=Fyll i kostnadsobjekt med];
                OptionCaptionML=[ENU=Code,Sort Code;
                                 NOR=Kode,Typekode;
                                 SVE=Kod,Typkod];
                SourceExpr=FillCostObjectWith;
                Visible=FillCostObjectVisible }

    { 1100527301;1;Field  ;
                Name=FillCostObjectManually;
                CaptionML=ENU=Fill Cost Object manually;
                SourceExpr=FillCostObjectManually;
                Visible=FillCostObjectVisible }

    { 1100525003;1;Field  ;
                Name=DeriveCostTypeFrom;
                CaptionML=[ENU=Derive Cost Type From;
                           NOR=Utled kosttype fra];
                OptionCaptionML=[ENU=Cost Type KPD,Cost Object Navision;
                                 NOR=Kosttype KPD, kostobjekt Navision];
                SourceExpr=DeriveCostTypeFrom;
                Visible=DeriveCostTypeVisible }

    { 1100525006;1;Field  ;
                Name=ApplyEntries;
                CaptionML=[ENU=Apply Entries;
                           NOR=Utlign poster;
                           SVE=Koppla transaktioner];
                OptionCaptionML=ENU=Suppress Details,Ignore Main Entry;
                SourceExpr=ApplyEntries;
                Visible=ApplyEntriesVisible }

    { 1100525004;1;Field  ;
                Name=CreateTenderParagrah;
                CaptionML=ENU=Tender Entries;
                SourceExpr=CreateTenderParagrah;
                Visible=CreateTenderParagrahVisible }

    { 1100529401;1;Field  ;
                Name=CodingSystemElement;
                CaptionML=ENU=Coding System Element;
                SourceExpr=CodingSystemElement;
                Enabled=CodingSystemElementEnable;
                OnValidate=BEGIN
                             //db, 10-10-16: C028543
                             IF ElementOptout THEN CodingSystemElement := '';
                             IF CodingSystemElement <> '' THEN
                               CodingSystem.GET(CodingSystemElement);
                           END;

                OnLookup=BEGIN
                           //db, 10-10-16: C028543
                           IF ElementOptout THEN EXIT;
                           IF PAGE.RUNMODAL(0,CodingSystem) = ACTION::LookupOK THEN
                             CodingSystemElement := CodingSystem."Coding System";
                         END;
                          }

    { 1100529400;1;Field  ;
                Name=ElementCde;
                CaptionML=ENU=Default Element;
                SourceExpr=ElementCde;
                Enabled=ElementEnable;
                OnValidate=BEGIN
                             //db, 10-10-16: C028543
                             IF ElementOptout THEN ElementCde := '';
                             IF ElementCde <> '' THEN BEGIN
                               IF NOT ProjectElement.GET(ProjectCde,ElementCde) THEN
                                 BaseElement.GET(ElementCde);
                             END;
                           END;

                OnLookup=BEGIN
                           //db, 10-10-16: C028543
                           IF ElementOptout THEN EXIT;
                           ProjectElement.SETRANGE("Project No.",ProjectCde);
                           IF ProjectElement.FINDFIRST THEN BEGIN
                             IF PAGE.RUNMODAL(0,ProjectElement) = ACTION::LookupOK THEN
                               ElementCde := ProjectElement.Element;
                           END ELSE BEGIN
                             IF PAGE.RUNMODAL(0,BaseElement) = ACTION::LookupOK THEN
                               ElementCde := BaseElement.Code;
                           END;
                         END;
                          }

    { 1100529402;1;Field  ;
                Name=CodingSystemTenderParagraph;
                CaptionML=ENU=Coding System Tender Paragraph;
                SourceExpr=CodingSystemTenderParagraph;
                Enabled=CodingSystemTenderParagraphEnable;
                OnValidate=BEGIN
                             //db, 10-10-16: C028543
                             IF CodingSystemTenderParagraph <> '' THEN
                               CodingSystem.GET(CodingSystemTenderParagraph);
                           END;

                OnLookup=BEGIN
                           //db, 10-10-16: C028543
                           IF PAGE.RUNMODAL(0,CodingSystem) = ACTION::LookupOK THEN
                             CodingSystemTenderParagraph := CodingSystem."Coding System";
                         END;
                          }

    { 1100525007;1;Field  ;
                Name=CodingSystemLocationCodeCUF;
                CaptionML=ENU=Coding System Location Code CUF;
                SourceExpr=CodingSystemLocationCodeCUF;
                Enabled=CodingSystemLocationCodeCUFEnable;
                OnValidate=BEGIN
                             IF CodingSystemLocationCodeCUF <> '' THEN
                               CodingSystem.GET(CodingSystemLocationCodeCUF);
                           END;

                OnLookup=BEGIN
                           IF PAGE.RUNMODAL(0,CodingSystem) = ACTION::LookupOK THEN
                             CodingSystemLocationCodeCUF := CodingSystem."Coding System";
                         END;
                          }

    { 1100528900;1;Field  ;
                Name=CodingSystemCostObject;
                CaptionML=ENU=Coding System Cost Object;
                SourceExpr=CodingSystemCostObject;
                Enabled=CodingSystemCostObjectEnable;
                OnValidate=BEGIN
                             //DP01702
                             IF CodingSystemCostObject <> '' THEN
                               CodingSystem.GET(CodingSystemCostObject);
                           END;

                OnLookup=BEGIN
                           //DP01702
                           IF PAGE.RUNMODAL(0,CodingSystem) = ACTION::LookupOK THEN
                             CodingSystemCostObject := CodingSystem."Coding System";
                         END;
                          }

    { 1100528500;1;Field  ;
                CaptionML=ENU=Apply CODE from BUDGETLINE;
                OptionCaptionML=ENU=" ,CostObject,Element,Location,TenderParagraph";
                SourceExpr=ApplyCode;
                Visible=ApplyCodeEnable;
                Enabled=ApplyCodeEnable }

    { 1100528501;1;Field  ;
                CaptionML=ENU=Apply COSTPRICE from BUDGETLINE;
                SourceExpr=ApplyCostPrice;
                Visible=ApplyCostPriceEnable;
                Enabled=ApplyCostPriceEnable }

    { 1100528100;1;Field  ;
                Name=UpdateVersionDate;
                CaptionML=ENU=Update Version Date budget with current workdate;
                SourceExpr=UpdateVersionDate }

  }
  CODE
  {
    VAR
      Text000@11012012 : TextConst 'ENU=Combinations of Adjustments, Extension and Options not allowed;NOR=Kombinasjoner av justeringer, ETA og alternativ er ikke tillatt;SVE=Kombinationer av justeringar, ŽTA och alternativ „r inte till†tna';
      JobSetup@1100485002 : Record 315;
      ProjRec@11012000 : Record 11072003;
      AdjustmentRec@11012001 : Record 11012003;
      ExtensionRec@11012002 : Record 11012004;
      HouseModelRec@1210190010 : Record 11012501;
      PlotRec@1100485000 : Record 11012500;
      MainGroupRec@1210190011 : Record 11012509;
      GroupRec@1210190012 : Record 11012504;
      SubGroupRec@1210190013 : Record 11012505;
      OptionRec@11012003 : Record 11012502;
      ErrorRec@1210190000 : Record 11012051;
      BudgetRec@1100485005 : Record 11012001;
      ProjectElement@1100529400 : Record 11012010;
      BaseElement@1100529401 : Record 11012060;
      CodingSystem@1100529406 : Record 11012088;
      FileMgt@1100525006 : Codeunit 419;
      ProjectCde@11012004 : Code[20];
      ExtensionCde@11012005 : Code[10];
      AdjustmentCde@11012006 : Code[10];
      OptionCde@11012007 : Code[20];
      OriginCde@11012008 : Code[20];
      PlotCde@1100485001 : Code[20];
      HouseModelCde@1210190009 : Code[20];
      MainGroupCde@1210190008 : Code[20];
      GroupCde@1210190007 : Code[20];
      SubGroupCde@1210190006 : Code[20];
      ElementCde@1100529402 : Code[20];
      CodingSystemElement@1100529404 : Code[20];
      CodingSystemTenderParagraph@1100529407 : Code[20];
      CodingSystemCostObject@1100528900 : Code[20];
      Folder@1100485004 : Text[250];
      FileNameTxt@1210190002 : Text[100];
      ElementOptout@1210190005 : Boolean;
      OnlyBudgetLines@1100485003 : Boolean;
      Text002@1100485006 : TextConst 'ENU=Budget Lines are already present. Continue import?;NOR=Budsjettrader finnes allerede. Fortsett importering?;SVE=Det finns redan budgetrader. Forts„tta importera?';
      FromOptionBudget@1100485007 : Boolean;
      Text003@1100485008 : TextConst 'ENU=When called from the Option Budget the Option Code is mandatory.;NOR=Alternativkode er obligatorisk ved henting fra alternativbudsjettet.;SVE=Alternativkod „r obligatorisk vid h„mtning fr†n alternativbudgeten.';
      Text004@1100485009 : TextConst 'ENU=Import Option Budget not allowed for Option with status ''%1''.;NOR=Importer alternativbudsjett er ikke tillatt for alternativ med status ''%1''.;SVE=Import av alternativbudget „r inte till†ten f”r alternativ med status ''%1''.';
      Text005@1100485010 : TextConst 'ENU=Project may not be empty.;NOR=Prosjekt kan ikke v‘re tomt.;SVE=Projektet f†r inte vara tomt.';
      OptionBudgetVisible@19006080 : Boolean INDATASET;
      ElementOptoutVisible@19064248 : Boolean INDATASET;
      FileNameInputVISIBLE@1210190001 : Boolean INDATASET;
      FillElementWith@1100525001 : Option;
      FillCostObjectWith@1100525000 : Option;
      FillElementVisible@1100525002 : Boolean INDATASET;
      FillCostObjectVisible@1100525003 : Boolean INDATASET;
      DeriveCostTypeFrom@1100525004 : 'CostTypeKPD,CostObjectNav';
      DeriveCostTypeVisible@1100525005 : Boolean INDATASET;
      CreateTenderParagrah@1100525010 : Boolean;
      CreateTenderParagrahVisible@1100525008 : Boolean INDATASET;
      SettlementQuantityCde@1100528200 : Code[20];
      SettlementQuantityRec@1100528201 : Record 11072243;
      SettlementQuantityVisible@1100528202 : Boolean INDATASET;
      FromSettlementQuantity@1100528203 : Boolean;
      ApplyEntries@1100525011 : 'SuppressDetails,DisableMain';
      ApplyEntriesVisible@1100525012 : Boolean INDATASET;
      ElementEnable@1100529403 : Boolean INDATASET;
      CodingSystemElementEnable@1100529405 : Boolean INDATASET;
      CodingSystemTenderParagraphEnable@1100529408 : Boolean INDATASET;
      CodingSystemLocationCodeCUF@1100525013 : Code[20];
      CodingSystemLocationCodeCUFEnable@1100525014 : Boolean;
      CodingSystemCostObjectEnable@1100528901 : Boolean INDATASET;
      CheckStartImport@1100525007 : Boolean;
      FillElementManually@1100527300 : Text[50];
      FillCostObjectManually@1100527302 : Text[50];
      ApplyCode@1100528500 : ' ,CostObject,Element,Location,TenderParagraph';
      ApplyCodeEnable@1100528501 : Boolean INDATASET;
      UpdateVersionDate@1100528502 : Boolean;
      Text006@1100528503 : TextConst 'ENU=Select file...';
      XmlFileFilter@1100528600 : TextConst 'ENU=XML Files (*.xml)|*.xml';
      ApplyCostPrice@1100528504 : Boolean;
      ApplyCostPriceEnable@1100528505 : Boolean INDATASET;

    PROCEDURE DetermineDefaults@1(lProjectCde@11012000 : Code[20];lAdjustmentCde@11012001 : Code[10];lExtensionCde@11012002 : Code[10];lOptionCde@11012003 : Code[20];lOriginCde@11012004 : Code[20]);
    VAR
      ProjRec@1100525001 : Record 11072003;
      ProjSetup@1100525000 : Record 315;
    BEGIN
      ProjectCde := lProjectCde;
      AdjustmentCde := lAdjustmentCde;
      ExtensionCde := lExtensionCde;
      OptionCde := lOptionCde;
      OriginCde := lOriginCde;

      IF OriginCde = 'CUF-XML-WECAP' THEN
        OriginCde := 'CUF-XML-GWW';  //db, 30-11-17

      //db.sn, 12-08-16: C016643 + T007154
      IF NOT FromSettlementQuantity THEN BEGIN  //C038256
        BudgetRec."Project No." := ProjectCde;
        BudgetRec."Extension Contract" := ExtensionCde;
        BudgetRec.Adjustment:= AdjustmentCde;
        BudgetRec.Option:= OptionCde;
        IF BudgetRec.BudgetFixed(BudgetRec) THEN
          BudgetRec.GiveErrorMessageBudgetFixed(BudgetRec);
      END;
      //db.en, 12-08-16: C016643 + T007154

      //db.sn, 18-04-13
      ProjSetup.GET;
      IF ProjRec.GET(ProjectCde) THEN BEGIN
        FillElementWith := ProjRec."Fill Element with (KPD)";
        FillCostObjectWith := ProjRec."Fill Cost Object with (KPD)";
      END ELSE BEGIN
        FillElementWith := ProjSetup."Fill Element with (KPD)";
        FillCostObjectWith := ProjSetup."Fill Cost Object with (KPD)";
      END;
      //db.en, 18-04-13

      // dp00404.sn
      IF OriginCde = 'CUF-XML-GWW' THEN BEGIN
        CreateTenderParagrah := TRUE;
      END;
      // dp00404.en
    END;

    PROCEDURE SetExtraDefaultsOptionBudget@1210190000(IHouseModel@11012000 : Code[20];IMainGroup@11012003 : Code[20];IGroup@11012004 : Code[20];ISubGroup@1210190000 : Code[20];IPlotNo@1100485000 : Code[20]);
    BEGIN
      FromOptionBudget := TRUE;
      HouseModelCde := IHouseModel;
      MainGroupCde := IMainGroup;
      GroupCde := IGroup;
      SubGroupCde := ISubGroup;
      PlotCde := IPlotNo;
    END;

    PROCEDURE SetElementOptoutVisible@1100485006();
    VAR
      lvProjRec@1100485000 : Record 11072003;
      ProjSetup@1100529400 : Record 315;
    BEGIN
      ElementOptoutVisible := FALSE;
      IF (OriginCde = 'TRAD-XML') OR (OriginCde = 'CUF-XML') THEN BEGIN  //db, 10-10-16: C028543
        ProjSetup.GET;
        CASE ProjSetup."Fill Element with (TRAD)" OF
          ProjSetup."Fill Element with (TRAD)"::"Alternative Code": CodingSystemElement := 'altcde';
          ProjSetup."Fill Element with (TRAD)"::"Middel Code": CodingSystemElement := 'cclcde';
          ProjSetup."Fill Element with (TRAD)"::"Tender Code": CodingSystemElement := 'bstcde';
        END;
      END;
      IF ((OriginCde = 'TRAD-CSV') OR (OriginCde = 'TRAD-TXT') OR
          (OriginCde = 'TRAD-XML') OR (OriginCde = 'CUF-XML') OR //db, 10-10-16: C028543
          (OriginCde = 'CUF-XML-GWW') OR (OriginCde = 'KPDGWW-XML')) THEN BEGIN  //DP01702
        IF (FromOptionBudget AND (ProjectCde = '')) THEN BEGIN
          ElementOptoutVisible := TRUE;
          EXIT;
        END;

        IF NOT lvProjRec.GET(ProjectCde) THEN lvProjRec.INIT;  //C038256
        IF (NOT lvProjRec."Posting Element Mandatory") THEN
          ElementOptoutVisible := TRUE;
      END;
    END;

    LOCAL PROCEDURE SetEnableElement@1100529400();
    BEGIN
      //db, 10-10-16: C028543
      CodingSystemElementEnable := (OriginCde = 'CUF-XML') OR (OriginCde = 'CUF-XML-GWW');  //DP01702
      CodingSystemTenderParagraphEnable := (OriginCde = 'CUF-XML');
      CodingSystemLocationCodeCUFEnable := (OriginCde = 'CUF-XML') OR (OriginCde = 'CUF-XML-GWW');  //DP01702
      CodingSystemCostObjectEnable := (OriginCde = 'CUF-XML') OR (OriginCde = 'CUF-XML-GWW');  //DP01702
      ElementEnable :=
        (OriginCde = 'CUF-XML') OR (OriginCde = 'CUF-XML-GWW') OR  //DP01702
        ((OriginCde = 'TRAD-XML') AND (ElementOptout = FALSE)) OR (OriginCde = 'KPDGWW-XML');
    END;

    PROCEDURE FinishWizard@1100525000();
    BEGIN
      StartImport;
    END;

    PROCEDURE StartImport@1210190002();
    VAR
      ClientTypeManagement@1100528500 : Codeunit 4;
      lvImportTradTXT@1100485001 : Codeunit 11012018;
      lvImportTRAD@1100485003 : XMLport 11012397;
      lvImportCUF@1100485002 : XMLport 11012391;
      lvImportIbisInfra@1100485004 : Codeunit 11012018;
      lvImportKPD@1000000000 : XMLport 11012395;
      lvImportKPDgww@1100525000 : XMLport 11012392;
      FromFile@1100528501 : Text;
      InStream@1100528502 : InStream;
    BEGIN
      IF ((ExtensionCde <> '') AND (AdjustmentCde <> '')) OR
         ((ExtensionCde <> '') AND (OptionCde <> '')) OR
         ((OptionCde <> '') AND (AdjustmentCde <> '')) OR
         ((SettlementQuantityCde <> '') AND (OptionCde <> '')) OR
         ((SettlementQuantityCde <> '') AND (ExtensionCde  <> '')) OR
         ((SettlementQuantityCde <> '') AND (AdjustmentCde <> ''))
      THEN
        ERROR(Text000);

      IF (ExtensionCde = '') AND (AdjustmentCde = '') AND (OptionCde = '') AND (SettlementQuantityCde= '') THEN BEGIN
        ProjRec.GET(ProjectCde);
        IF NOT FromSettlementQuantity THEN  //C038256
          ProjRec.TESTFIELD("Budget Fixed",FALSE);
      END;

      CheckStartImport := TRUE;  //db, 22-02-18
      ValidateProject(ProjectCde);
      ValidateOption();
      ValidateSettlQuantity();
      CheckStartImport := FALSE;  //db, 22-02-18

      //Check Budget Lines Present
      BudgetRec.SETRANGE("Project No.", ProjectCde);
      BudgetRec.SETRANGE("Extension Contract", ExtensionCde);
      BudgetRec.SETRANGE(Adjustment, AdjustmentCde);
      BudgetRec.SETRANGE(Option, OptionCde);
      IF FromSettlementQuantity THEN BEGIN
        IF SettlementQuantityCde <> '' THEN
          BudgetRec.SETRANGE("Settlement Quantity Code", SettlementQuantityCde)
        ELSE
          BudgetRec.SETFILTER("Settlement Quantity Code", '<>%1', '');
      END;

      BudgetRec.SETRANGE("Budget Summary Line", FALSE);
      IF NOT BudgetRec.ISEMPTY THEN
        IF NOT CONFIRM(Text002) THEN
          EXIT;

      IF UpdateVersionDate THEN
        WriteVersionDate;  //C045850

      ErrorRec.RESET;
      ErrorRec.SETRANGE("User ID", USERID);
      ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Import Budget");
      ErrorRec.DELETEALL;
      COMMIT;

      IF (NOT ElementOptoutVisible) AND ElementOptout THEN ElementOptout := FALSE;

      CASE OriginCde OF
        'TRAD-CSV':
          BEGIN
            IF FromSettlementQuantity THEN
              lvImportTRAD.SetDefaultsSettlQuantity(SettlementQuantityCde);
            lvImportTRAD.DetermineSelection(
              ProjectCde,AdjustmentCde,ExtensionCde,HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,ElementOptout,PlotCde);
            IF ClientTypeManagement.IsCommonWebClientType THEN BEGIN
              IF UPLOADINTOSTREAM(Text006, '', '', FromFile, InStream) THEN BEGIN
                lvImportTRAD.SETSOURCE(InStream);
                lvImportTRAD.IMPORT;
              END;
            END ELSE
              lvImportTRAD.RUN;
          END;
        'TRAD-TXT':
          BEGIN
            IF FromSettlementQuantity THEN
              lvImportTradTXT.SetDefaultsSettlQuantity(SettlementQuantityCde);
            lvImportTradTXT.ImportTradTxtBudget(
              ProjectCde,AdjustmentCde,ExtensionCde,HouseModelCde,MainGroupCde,GroupCde,
              SubGroupCde,OptionCde,Folder,FileNameTxt,PlotCde,ElementOptout);
            COMMIT;
          END;
        'TRAD-XML':
          BEGIN
            ImportFromTRADXML;
          END;
        'IBIS-INFRA':
          lvImportIbisInfra.ImportIbisInfraBudget(ProjectCde,AdjustmentCde,ExtensionCde,OnlyBudgetLines,Folder,FileNameTxt);
        'CUF':
          BEGIN
            IF FromSettlementQuantity THEN
              lvImportCUF.SetDefaultsSettlQuantity(SettlementQuantityCde);
            lvImportCUF.DetermineSelection(
              ProjectCde,AdjustmentCde,ExtensionCde,HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde);
            IF ClientTypeManagement.IsCommonWebClientType THEN BEGIN
              IF NOT UPLOADINTOSTREAM(Text006, '', '', FromFile, InStream) THEN
                ERROR('');
              lvImportCUF.SETSOURCE(InStream);
              lvImportCUF.IMPORT;
            END ELSE
              lvImportCUF.RUN;
          END;
        'CUF-XML':
          BEGIN
            ImportFromCUFXML;
          END;
        'CUF-XML-GWW':
          BEGIN
            ImportFromCUFXMLWECAP;
          END;
        'DUNCAN':
          BEGIN
            ImportFromDUNCAN;
          END;
        'KPDCONSTRUCT':
          BEGIN
            IF FromSettlementQuantity THEN
              lvImportKPD.SetDefaultsSettlQuantity(SettlementQuantityCde);
            lvImportKPD.DetermineSelection(
              ProjectCde,AdjustmentCde,ExtensionCde,HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde);
            lvImportKPD.RUN;
          END;
        'KPDGWW':
          BEGIN
            lvImportKPDgww.DetermineSelection(
              ProjectCde,AdjustmentCde,ExtensionCde,HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde);
            lvImportKPDgww.RUN;
          END;
        'KPDGWW-XML':
          BEGIN
            ImportFromKPDGWWXML;  //DP00200
          END;
      END;

      ErrorRec.SETRANGE("User ID", USERID);
      ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Import Budget");
      IF ErrorRec.FIND('-') THEN BEGIN
        COMMIT;
        PAGE.RUNMODAL(PAGE::Errors, ErrorRec);
      END;
    END;

    PROCEDURE ValidateProject@1100485001(IProjNo@1100485000 : Code[20]);
    BEGIN
      IF IProjNo <> '' THEN BEGIN
        ProjRec.GET(IProjNo);
        IF OriginCde = 'KPDGWW-XML'THEN BEGIN  //db, 22-04-13
          ProjRec."Fill Element with (KPD)" := FillElementWith;
          ProjRec."Fill Cost Object with (KPD)" :=  FillCostObjectWith;
          ProjRec.MODIFY;
        END;
      END ELSE BEGIN
        IF NOT FromOptionBudget AND NOT FromSettlementQuantity THEN  //C038256
          ERROR(Text005);
      END;
      IF NOT CheckStartImport THEN  //db, 22-02-18
        SetElementOptoutVisible();
    END;

    PROCEDURE LookupPlot@1100485000();
    BEGIN
      WITH PlotRec DO BEGIN
        IF ProjectCde <> '' THEN
          SETRANGE("Project No.", ProjectCde)
        ELSE
          SETRANGE("Project No.", '');
        IF HouseModelCde <> '' THEN
          SETRANGE("House Model",HouseModelCde);
        "Project No." := ProjectCde;
        "Plot No." := PlotCde;
        IF ProjectCde <> '' THEN
          IF PAGE.RUNMODAL(PAGE::"Plot Number List", PlotRec) = ACTION::LookupOK THEN
            PlotCde := PlotRec."Plot No.";
      END;
    END;

    PROCEDURE LookupHouseModel@1210190010();
    BEGIN
      WITH HouseModelRec DO BEGIN
        IF ProjectCde <> '' THEN
          SETRANGE("Project No.", ProjectCde)
         ELSE
          SETRANGE("Project No.", '');
        "Project No." := ProjectCde;
        Model := HouseModelCde;
        IF ProjectCde <> '' THEN BEGIN
          IF PAGE.RUNMODAL(PAGE::"House Models by Project List", HouseModelRec) = ACTION::LookupOK THEN
            HouseModelCde := Model;
        END ELSE BEGIN
          IF PAGE.RUNMODAL(PAGE::"House Model List", HouseModelRec) = ACTION::LookupOK THEN
            HouseModelCde := Model;
        END;
      END;
    END;

    PROCEDURE LookupMainGroup@1210190008();
    BEGIN
      WITH MainGroupRec DO BEGIN
        IF ProjectCde <> '' THEN
          SETRANGE("Project No.", ProjectCde)
         ELSE
          SETRANGE("Project No.", '');
        SETRANGE("Plot No.", PlotCde);
        "Project No." := ProjectCde;
        Code := MainGroupCde;
        IF ProjectCde <> '' THEN BEGIN
          IF PAGE.RUNMODAL(PAGE::"Project Option Main Group List", MainGroupRec) = ACTION::LookupOK THEN
            MainGroupCde := Code;
        END ELSE BEGIN
          IF PAGE.RUNMODAL(PAGE::"Option Main Group List", MainGroupRec) = ACTION::LookupOK THEN
            MainGroupCde := Code;
        END;
      END;
    END;

    PROCEDURE LookupGroup@1210190007();
    BEGIN
      WITH GroupRec DO BEGIN
        IF ProjectCde <> '' THEN
          SETRANGE("Project No.", ProjectCde)
         ELSE
          SETRANGE("Project No.", '');
        SETRANGE("Plot No.", PlotCde);
        "Project No." := ProjectCde;
        Code := GroupCde;
        IF ProjectCde <> '' THEN BEGIN
          IF PAGE.RUNMODAL(PAGE::"Project Option Groups", GroupRec) = ACTION::LookupOK THEN
            GroupCde := Code;
        END ELSE BEGIN
          IF PAGE.RUNMODAL(PAGE::"Option Groups", GroupRec) = ACTION::LookupOK THEN
            GroupCde := Code;
        END;
      END;
    END;

    PROCEDURE LookupSubgroup@1210190006();
    BEGIN
      WITH SubGroupRec DO BEGIN
        IF ProjectCde <> '' THEN
          SETRANGE("Project No.", ProjectCde)
         ELSE
          SETRANGE("Project No.", '');
        SETRANGE("Plot No.", PlotCde);
        "Project No." := ProjectCde;
        Code := SubGroupCde;
        IF ProjectCde <> '' THEN BEGIN
          IF PAGE.RUNMODAL(PAGE::"Project Option Sub Groups", SubGroupRec) = ACTION::LookupOK THEN
            SubGroupCde := Code;
        END ELSE BEGIN
          IF PAGE.RUNMODAL(PAGE::"Option Sub Groups", SubGroupRec) = ACTION::LookupOK THEN
            SubGroupCde := Code;
        END;
      END;
    END;

    PROCEDURE LookupOption@1210190005();
    BEGIN
      WITH OptionRec DO BEGIN
        IF ProjectCde <> '' THEN
          SETRANGE("Project No.", ProjectCde)
         ELSE
          SETRANGE("Project No.", '');
        SETRANGE("Plot No.", PlotCde);
        "Project No." := ProjectCde;
        "Plot No." := PlotCde;
        "House Model" := HouseModelCde;
        "Main Group" := MainGroupCde;
        Group := GroupCde;
        "Sub Group" := SubGroupCde;
        Option := OptionCde;
        IF PAGE.RUNMODAL(PAGE::"Option List",OptionRec) = ACTION::LookupOK THEN
          OptionCde := Option;
      END;
    END;

    PROCEDURE ValidateOption@1210190001();
    BEGIN
      IF OptionCde <> '' THEN BEGIN
        IF NOT FromOptionBudget THEN BEGIN
          OptionCde := '';  //* Dan leeg maken want 'Optie Code' gevuld is indicatie dat toevoegen in
          EXIT;             //* de optiebegroting ipv in de werkbegroting
        END;
        OptionRec.GET(ProjectCde, PlotCde, HouseModelCde, MainGroupCde, GroupCde, SubGroupCde, OptionCde);
        IF (ProjectCde <> '') AND (PlotCde <> '') AND
           (OptionRec.Status IN [OptionRec.Status::Order,OptionRec.Status::Invoice])
        THEN
          ERROR(Text004, OptionRec.Status);
          //* Niet toestaan dan allerlei problemen oa: mogelijk al termijnen aangemaakt/gefact,
          //* zou dan ook naar werkbegroting moeten (via functie OB->WB), hoe dan omgaan met
          //* fouten tijdens import, etc. Is so wie so niet logisch om dan nog/pas te importeren.
      END ELSE BEGIN
        IF FromOptionBudget THEN
          ERROR(Text003);  //* Moet dan gevuld zijn is indicatie optiebegroting ipv werkbegroting
      END;
    END;

    PROCEDURE SetExtraDefaultsSettlQuantity@1100528203(IHouseModelCde@1100528200 : Code[20];ISettlementQuantityCde@11012000 : Code[20]);
    BEGIN
      FromSettlementQuantity := TRUE;
      SettlementQuantityCde := ISettlementQuantityCde;
      HouseModelCde := IHouseModelCde;
    END;

    PROCEDURE LookupSettlQuantity@1100528206();
    BEGIN
      WITH SettlementQuantityRec DO BEGIN
        SETRANGE("Project No.", ProjectCde);
        SETRANGE(Code, SettlementQuantityCde);
        "Project No." := ProjectCde;
        Code := SettlementQuantityCde;
        IF PAGE.RUNMODAL(PAGE::"Settlement Quantity List",SettlementQuantityRec) = ACTION::LookupOK THEN
          SettlementQuantityCde := Code;
      END;
    END;

    PROCEDURE ValidateSettlQuantity@1100528205();
    BEGIN
      //copy of function ValidateOption
      IF SettlementQuantityCde <> '' THEN BEGIN
        IF NOT FromSettlementQuantity THEN BEGIN
          SettlementQuantityCde := '';
          EXIT;
        END;
        SettlementQuantityRec.GET(ProjectCde, HouseModelCde, SettlementQuantityCde);
      END;
    END;

    LOCAL PROCEDURE ImportFromCUFXML@2();
    VAR
      TempBlob@1100528501 : TEMPORARY Record 99008535;
      ParseCUFXMLDocument@1100525000 : Codeunit 11012390;
    BEGIN
      IF NOT SelectImportFile(TempBlob) THEN EXIT;  //DP02188

      IF FromSettlementQuantity THEN
        ParseCUFXMLDocument.SetDefaultsSettlQuantity(SettlementQuantityCde); //Must be set before ...InitCodeunit
      ParseCUFXMLDocument.InitCodeunit(ProjectCde,AdjustmentCde,ExtensionCde,
        HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde,
        ElementOptout,ElementCde,CodingSystemElement,CodingSystemTenderParagraph,CodingSystemLocationCodeCUF,
        CodingSystemCostObject,ApplyCode);
      ParseCUFXMLDocument.ProcessFile(TempBlob);
    END;

    LOCAL PROCEDURE ImportFromTRADXML@1210190003();
    VAR
      TempBlob@1100528502 : TEMPORARY Record 99008535;
      ParseTRADXMLDocument@1100525000 : Codeunit 11012391;
    BEGIN
      IF NOT SelectImportFile(TempBlob) THEN EXIT;  //DP02188

      IF FromSettlementQuantity THEN
        ParseTRADXMLDocument.SetDefaultsSettlQuantity(SettlementQuantityCde); //Must be set before ...InitCodeunit
      ParseTRADXMLDocument.InitCodeunit(ProjectCde,AdjustmentCde,ExtensionCde,
        HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde,
        ElementOptout,ElementCde);
      ParseTRADXMLDocument.ProcessFile(TempBlob);
    END;

    LOCAL PROCEDURE ImportFromKPDGWWXML@1100409001();
    VAR
      TempBlob@1100528501 : TEMPORARY Record 99008535;
      ParseGWWXMLDocument@1100525000 : Codeunit 11012392;
    BEGIN
      IF NOT SelectImportFile(TempBlob) THEN EXIT;  //DP02188

      ParseGWWXMLDocument.InitCodeunit(ProjectCde,AdjustmentCde,ExtensionCde,
        HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde,
        FillElementWith,FillCostObjectWith,DeriveCostTypeFrom,ApplyEntries,
        ElementOptout,ElementCde);
      ParseGWWXMLDocument.InitManuallyValues(FillElementManually, FillCostObjectManually);
      ParseGWWXMLDocument.ProcessFile(TempBlob);
    END;

    LOCAL PROCEDURE ImportFromCUFXMLWECAP@1100525003();
    VAR
      TempBlob@1100528501 : TEMPORARY Record 99008535;
      ParseCUFXMLDocument@1100525000 : Codeunit 11012393;
    BEGIN
      IF NOT SelectImportFile(TempBlob) THEN EXIT;  //DP02188

      ParseCUFXMLDocument.InitCodeunit(ProjectCde, AdjustmentCde, ExtensionCde,
        HouseModelCde, MainGroupCde, GroupCde, SubGroupCde, OptionCde, PlotCde, CreateTenderParagrah,
        ElementOptout,ElementCde,CodingSystemElement,CodingSystemLocationCodeCUF,
        CodingSystemCostObject,ApplyCostPrice);
      ParseCUFXMLDocument.ProcessFile(TempBlob);
    END;

    LOCAL PROCEDURE ImportFromDUNCAN@1100528601();
    VAR
      ImportDuncanBudgetPAGE@1100528604 : Page 11129716;
      ClientTypeManagement@1100528601 : Codeunit 4;
      ImportDuncanBudget@1100528600 : XMLport 11012394;
    BEGIN
      IF ClientTypeManagement.IsCommonWebClientType THEN BEGIN
        IF FromSettlementQuantity THEN
          ImportDuncanBudgetPAGE.SetDefaultsSettlQuantity(SettlementQuantityCde);
        ImportDuncanBudgetPAGE.DetermineSelection(ProjectCde,AdjustmentCde,ExtensionCde,HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde);
        ImportDuncanBudgetPAGE.RUN;
      END ELSE BEGIN
        IF FromSettlementQuantity THEN
          ImportDuncanBudget.SetDefaultsSettlQuantity(SettlementQuantityCde);
        ImportDuncanBudget.DetermineSelection(ProjectCde,AdjustmentCde,ExtensionCde,HouseModelCde,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde);
        ImportDuncanBudget.RUN;
      END;
    END;

    LOCAL PROCEDURE WriteVersionDate@1100528500();
    VAR
      Job@1100528500 : Record 11072003;
      BudgetAdjustment@1100528501 : Record 11012003;
      ExtensionContract@1100528502 : Record 11012004;
      PlotOption@1100528503 : Record 11012502;
      SettlementQuantity@1100528504 : Record 11072243;
    BEGIN
      //C045850:
      //update VersionDate on header while import procedure fills this for new lines by GetVersionDate on Budget Line
      IF AdjustmentCde <> '' THEN BEGIN
        IF BudgetAdjustment.GET(ProjectCde, AdjustmentCde) THEN BEGIN
          BudgetAdjustment."Version Date" := WORKDATE;
          BudgetAdjustment.MODIFY;
        END;
        EXIT;
      END;

      IF ExtensionCde <> '' THEN BEGIN
        IF ExtensionContract.GET(ProjectCde, ExtensionCde) THEN BEGIN
          ExtensionContract."Version Date" := WORKDATE;
          ExtensionContract.MODIFY;
        END;
        EXIT;
      END;

      IF OptionCde <> '' THEN BEGIN
        PlotOption.SETRANGE("Project No.", ProjectCde);
        PlotOption.SETRANGE("Plot No.", PlotCde);
        PlotOption.SETRANGE("House Model", HouseModelCde);
        PlotOption.SETRANGE("Main Group", MainGroupCde);
        PlotOption.SETRANGE(Group, GroupCde);
        PlotOption.SETRANGE("Sub Group", SubGroupCde);
        PlotOption.SETRANGE(Option, OptionCde);
        IF PlotOption.FINDSET THEN BEGIN
          REPEAT
            PlotOption."Version Date" := WORKDATE;
            PlotOption.MODIFY;
          UNTIL PlotOption.NEXT = 0;
        END;
        EXIT;
      END;

      IF SettlementQuantityCde <> '' THEN BEGIN
        SettlementQuantity.SETRANGE("Project No.", ProjectCde);
        SettlementQuantity.SETRANGE(Code, SettlementQuantityCde);
        IF SettlementQuantity.FINDSET THEN BEGIN
          REPEAT
            SettlementQuantity."Version Date" := WORKDATE;
            SettlementQuantity.MODIFY;
          UNTIL SettlementQuantity.NEXT = 0;
        END;
        EXIT;
      END;

      IF ProjectCde <> '' THEN BEGIN
        IF Job.GET(ProjectCde) THEN BEGIN
          Job."Version Date" := WORKDATE;
          Job.MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE SelectImportFile@1100528900(VAR TempBlob@1100528907 : TEMPORARY Record 99008535) : Boolean;
    VAR
      JobSetup@1100528908 : Record 315;
      FileName@1100528906 : Text[250];
      StartFileName@1100528905 : Text[250];
      FileManagement@1100528904 : Codeunit 419;
      ClientTypeManagement@1100528903 : Codeunit 4;
      OutStream@1100528902 : OutStream;
      InStream@1100528901 : InStream;
      FromFile@1100528900 : Text;
    BEGIN
      //DP02188
      IF NOT ClientTypeManagement.IsCommonWebClientType THEN BEGIN
        JobSetup.GET;
        StartFileName := JobSetup."Pathname External Estimates";
        IF StartFileName <> '' THEN BEGIN
          IF StartFileName[STRLEN(StartFileName)] <> '\' THEN
            StartFileName := StartFileName + '\';
          StartFileName := StartFileName + '*.xml';
        END;
        FileName := FileManagement.OpenFileDialog('',StartFileName,XmlFileFilter);
        IF FileName = '' THEN
          EXIT(FALSE);
        FileManagement.BLOBImport4PS(TempBlob, FileName, FALSE);
      END ELSE BEGIN
        TempBlob.Blob.CREATEOUTSTREAM(OutStream);
        IF NOT UPLOADINTOSTREAM(Text006, '', XmlFileFilter, FileName, InStream) THEN
          EXIT(FALSE);
        COPYSTREAM(OutStream, InStream);
      END;
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

