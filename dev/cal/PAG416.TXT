OBJECT Page 416 G/L Account Balance Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               NOR=Linjer;
               SVE=Rader];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,GLPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,GLPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       IF DebitCreditTotals THEN
                         GLAcc.CALCFIELDS("Net Change","Debit Amount","Credit Amount")
                       ELSE BEGIN
                         GLAcc.CALCFIELDS("Net Change");
                         IF GLAcc."Net Change" > 0 THEN BEGIN
                           GLAcc."Debit Amount" := GLAcc."Net Change";
                           GLAcc."Credit Amount" := 0
                         END ELSE BEGIN
                           GLAcc."Debit Amount" := 0;
                           GLAcc."Credit Amount" := -GLAcc."Net Change"
                         END
                       END
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 14  ;2   ;Field     ;
                CaptionML=[ENU=Period Start;
                           NOR=Periodens start;
                           SVE=Periodstart];
                ToolTipML=[ENU=Specifies the start date of the period defined on the line for the bank account balance.;
                           NOR=Angir startdatoen for perioden som er definert p† linjen for bankkontoens saldo.;
                           SVE=Anger startdatumet f”r den period som angetts p† raden f”r bankkontots saldo.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Period Start";
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Period Name;
                           NOR=Periodenavn;
                           SVE=Periodnamn];
                ToolTipML=[ENU=Specifies the name of the period shown in the line.;
                           NOR=Angir navnet p† perioden som vises p† linjen.;
                           SVE=Anger namnet p† perioden som visas p† raden.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Period Name";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                Name=DebitAmount;
                DrillDown=Yes;
                CaptionML=[ENU=Debit Amount;
                           NOR=Debetbel›p;
                           SVE=Debetbelopp];
                ToolTipML=[ENU=Specifies the debit amount for the period on the line.;
                           NOR=Angir debetbel›pet for perioden p† linjen.;
                           SVE=Anger debetbeloppet f”r perioden p† raden.];
                ApplicationArea=#Basic,#Suite;
                BlankNumbers=BlankZero;
                SourceExpr=GLAcc."Debit Amount";
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

    { 2   ;2   ;Field     ;
                Name=CreditAmount;
                DrillDown=Yes;
                CaptionML=[ENU=Credit Amount;
                           NOR=Kreditbel›p;
                           SVE=Kreditbelopp];
                ToolTipML=[ENU=Specifies the credit amount for the period on the line.;
                           NOR=Angir kreditbel›pet for perioden p† linjen.;
                           SVE=Anger kreditbeloppet f”r perioden p† raden.];
                ApplicationArea=#Basic,#Suite;
                BlankNumbers=BlankZero;
                SourceExpr=GLAcc."Credit Amount";
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

    { 10  ;2   ;Field     ;
                Name=NetChange;
                DrillDown=Yes;
                CaptionML=[ENU=Net Change;
                           NOR=Bevegelse;
                           SVE=Nettof”r„ndring];
                ToolTipML=[ENU=Specifies changes in the actual general ledger amount.;
                           NOR=Angir endringer i det faktiske finansbel›pet.;
                           SVE=Anger „ndringar av det faktiska f”r redovisningsbeloppet.];
                ApplicationArea=#Basic,#Suite;
                BlankZero=Yes;
                SourceExpr=GLAcc."Net Change";
                AutoFormatType=1;
                Visible=FALSE;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

  }
  CODE
  {
    VAR
      AccountingPeriod@1000 : Record 50;
      GLAcc@1001 : Record 15;
      PeriodFormMgt@1002 : Codeunit 359;
      GLPeriodLength@1003 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      AmountType@1004 : 'Net Change,Balance at Date';
      ClosingEntryFilter@1005 : 'Include,Exclude';
      DebitCreditTotals@1006 : Boolean;

    [External]
    PROCEDURE Set@1(VAR NewGLAcc@1000 : Record 15;NewGLPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance';NewClosingEntryFilter@1003 : 'Include,Exclude';NewDebitCreditTotals@1004 : Boolean);
    BEGIN
      GLAcc.COPY(NewGLAcc);
      GLPeriodLength := NewGLPeriodLength;
      AmountType := NewAmountType;
      ClosingEntryFilter := NewClosingEntryFilter;
      DebitCreditTotals := NewDebitCreditTotals;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE BalanceDrillDown@3();
    VAR
      GLEntry@1000 : Record 17;
      IsHandled@1001 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeBalanceDrillDown(GLAcc,GLPeriodLength,AmountType,ClosingEntryFilter,DebitCreditTotals,IsHandled);
      IF IsHandled THEN
        EXIT;

      SetDateFilter;
      GLEntry.RESET;
      GLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
      GLEntry.SETRANGE("G/L Account No.",GLAcc."No.");
      IF GLAcc.Totaling <> '' THEN
        GLEntry.SETFILTER("G/L Account No.",GLAcc.Totaling);
      GLEntry.SETFILTER("Posting Date",GLAcc.GETFILTER("Date Filter"));
      GLEntry.SETFILTER("Global Dimension 1 Code",GLAcc.GETFILTER("Global Dimension 1 Filter"));
      GLEntry.SETFILTER("Global Dimension 2 Code",GLAcc.GETFILTER("Global Dimension 2 Filter"));
      GLEntry.SETFILTER("Business Unit Code",GLAcc.GETFILTER("Business Unit Filter"));
      PAGE.RUN(0,GLEntry);
    END;

    LOCAL PROCEDURE SetDateFilter@2();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        GLAcc.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        GLAcc.SETRANGE("Date Filter",0D,"Period End");
      IF ClosingEntryFilter = ClosingEntryFilter::Exclude THEN BEGIN
        AccountingPeriod.SETCURRENTKEY("New Fiscal Year");
        AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
        IF GLAcc.GETRANGEMIN("Date Filter") = 0D THEN
          AccountingPeriod.SETRANGE("Starting Date",0D,GLAcc.GETRANGEMAX("Date Filter"))
        ELSE
          AccountingPeriod.SETRANGE(
            "Starting Date",
            GLAcc.GETRANGEMIN("Date Filter") + 1,
            GLAcc.GETRANGEMAX("Date Filter"));
        IF AccountingPeriod.FIND('-') THEN
          REPEAT
            GLAcc.SETFILTER(
              "Date Filter",GLAcc.GETFILTER("Date Filter") + '&<>%1',
              CLOSINGDATE(AccountingPeriod."Starting Date" - 1));
          UNTIL AccountingPeriod.NEXT = 0;
      END ELSE
        GLAcc.SETRANGE(
          "Date Filter",
          GLAcc.GETRANGEMIN("Date Filter"),
          CLOSINGDATE(GLAcc.GETRANGEMAX("Date Filter")));
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeBalanceDrillDown@4(VAR GLAccount@1000 : Record 15;GLPeriodLength@1004 : 'Day,Week,Month,Quarter,Year,Accounting Period';AmountType@1003 : 'Net Change,Balance at Date';ClosingEntryFilter@1002 : 'Include,Exclude';DebitCreditTotals@1001 : Boolean;VAR IsHandled@1005 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

