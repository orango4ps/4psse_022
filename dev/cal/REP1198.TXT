OBJECT Report 1198 Date Compress Resource Ledger
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 203=rimd,
                TableData 240=rimd,
                TableData 87=rimd,
                TableData 355=imd;
    CaptionML=[DEU=Datumskompr. Ressourcenposten;
               ENU=Date Compress Resource Ledger;
               NLD=Resourceposten comprimeren;
               NOR=Datokomprimer ressursposter];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Date Compress Resource Ledger",'',RetainDimText,Text010);
                  ResLedgEntryFilter := COPYSTR("Res. Ledger Entry".GETFILTERS,1,MAXSTRLEN(DateComprReg.Filter));
                END;

  }
  DATASET
  {
    { 4729;    ;DataItem;                    ;
               DataItemTable=Table203;
               DataItemTableView=SORTING(Resource No.,Posting Date);
               OnPreDataItem=VAR
                               GLSetup@1000 : Record 98;
                             BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date"));

                               Window.OPEN(Text004);

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress Res. Ledger");

                               SelectedDim.GetSelectedDim(
                                 USERID,3,REPORT::"Date Compress Resource Ledger",'',TempSelectedDim);
                               GLSetup.GET;
                               Retain[5] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress Resource Ledger",'',GLSetup."Global Dimension 1 Code");
                               Retain[6] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress Resource Ledger",'',GLSetup."Global Dimension 2 Code");

                               NewResLedgEntry.LOCKTABLE;
                               ResReg.LOCKTABLE;
                               DateComprReg.LOCKTABLE;

                               IF ResLedgEntry2.FIND('+') THEN;
                               LastEntryNo := ResLedgEntry2."Entry No.";
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");

                               InitRegisters;
                             END;

               OnAfterGetRecord=BEGIN
                                  ResLedgEntry2 := "Res. Ledger Entry";
                                  WITH ResLedgEntry2 DO BEGIN
                                    SETCURRENTKEY("Resource No.","Posting Date");
                                    COPYFILTERS("Res. Ledger Entry");
                                    SETRANGE("Entry Type","Entry Type");
                                    SETRANGE("Resource No.","Resource No.");
                                    SETRANGE("Resource Group No.","Resource Group No.");
                                    SETFILTER("Posting Date",DateComprMgt.GetDateFilter("Posting Date",EntrdDateComprReg,TRUE));

                                    IF RetainNo(FIELDNO("Document No.")) THEN
                                      SETRANGE("Document No.","Document No.");
                                    IF RetainNo(FIELDNO("Work Type Code")) THEN
                                      SETRANGE("Work Type Code","Work Type Code");
                                    IF RetainNo(FIELDNO("Job No.")) THEN
                                      SETRANGE("Job No.","Job No.");
                                    IF RetainNo(FIELDNO("Unit of Measure Code")) THEN
                                      SETRANGE("Unit of Measure Code","Unit of Measure Code");
                                    IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
                                      SETRANGE("Global Dimension 1 Code","Global Dimension 1 Code");
                                    IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
                                      SETRANGE("Global Dimension 2 Code","Global Dimension 2 Code");
                                    IF RetainNo(FIELDNO(Chargeable)) THEN
                                      SETRANGE(Chargeable,Chargeable);
                                    IF RetainNo(FIELDNO("Source Type")) THEN
                                      SETRANGE("Source Type","Source Type");
                                    IF RetainNo(FIELDNO("Source No.")) THEN
                                      SETRANGE("Source No.","Source No.");

                                    IF Quantity >= 0 THEN
                                      SETFILTER(Quantity,'>=0')
                                    ELSE
                                      SETFILTER(Quantity,'<0');

                                    InitNewEntry(NewResLedgEntry);

                                    DimBufMgt.CollectDimEntryNo(
                                      TempSelectedDim,"Dimension Set ID","Entry No.",
                                      0,FALSE,DimEntryNo);
                                    ComprDimEntryNo := DimEntryNo;
                                    SummarizeEntry(NewResLedgEntry,ResLedgEntry2);
                                    WHILE NEXT <> 0 DO BEGIN
                                      DimBufMgt.CollectDimEntryNo(
                                        TempSelectedDim,"Dimension Set ID","Entry No.",
                                        ComprDimEntryNo,TRUE,DimEntryNo);
                                      IF DimEntryNo = ComprDimEntryNo THEN
                                        SummarizeEntry(NewResLedgEntry,ResLedgEntry2);
                                    END;

                                    InsertNewEntry(NewResLedgEntry,ComprDimEntryNo);

                                    ComprCollectedEntries;
                                  END;

                                  IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(ResReg,DateComprReg);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF DateComprReg."No. Records Deleted" > NoOfDeleted THEN
                                  InsertRegisters(ResReg,DateComprReg);
                              END;

               ReqFilterFields=Entry Type,Resource No.,Resource Group No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;
                   IF EntrdResLedgEntry.Description = '' THEN
                     EntrdResLedgEntry.Description := Text009;

                   WITH "Res. Ledger Entry" DO BEGIN
                     InsertField(FIELDNO("Document No."),FIELDCAPTION("Document No."));
                     InsertField(FIELDNO("Work Type Code"),FIELDCAPTION("Work Type Code"));
                     InsertField(FIELDNO("Job No."),FIELDCAPTION("Job No."));
                     InsertField(FIELDNO("Unit of Measure Code"),FIELDCAPTION("Unit of Measure Code"));
                     InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
                     InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
                     InsertField(FIELDNO(Chargeable),FIELDCAPTION(Chargeable));
                     InsertField(FIELDNO("Source Type"),FIELDCAPTION("Source No."));
                     InsertField(FIELDNO("Source No."),FIELDCAPTION("Source No."));
                   END;

                   RetainDimText := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Date Compress Resource Ledger",'');
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das Datum an, ab dem fÅr den Bericht oder die Stapelverarbeitung Informationen verarbeitet werden.;
                             ENU=Specifies the date from which the report or batch job processes information.;
                             NLD=Hiermee wordt de datum opgegeven vanaf wanneer het rapport of de batchverwerking informatie verwerkt.;
                             NOR=Angir datoen som rapporten eller kjõrselen behandler informasjon fra.;
                             SVE=Anger datumet frÜn vilket rapporten eller batch-jobbet bearbetar information.];
                  ApplicationArea=#Jobs;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  ToolTipML=[DEU=Gibt das Datum an, bis zu dem fÅr den Bericht oder die Stapelverarbeitung Informationen verarbeitet werden.;
                             ENU=Specifies the date to which the report or batch job processes information.;
                             NLD=Hiermee wordt de datum opgegeven tot wanneer het rapport of de batchverwerking informatie verwerkt.;
                             NOR=Angir datoen som rapporten eller kjõrselen behandler informasjon til.;
                             SVE=Anger datumet till vilket rapporten eller batch-jobbet bearbetar information.];
                  ApplicationArea=#Jobs;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=PeriodenlÑnge;
                             ENU=Period Length;
                             NLD=Periodelengte;
                             NOR=Periodelengde;
                             SVE=Period];
                  ToolTipML=[DEU=Gibt die Periode an, fÅr die Daten im Bericht angezeigt werden. Geben Sie z. B. "1M" fÅr einen Monat, "30D" fÅr 30 Tage, "3Q" fÅr drei Quartale oder "5Y" fÅr fÅnf Jahre ein.;
                             ENU=Specifies the period for which data is shown in the report. For example, enter "1M" for one month, "30D" for thirty days, "3Q" for three quarters, or "5Y" for five years.;
                             NLD=Hiermee wordt de periode opgegeven waarvoor gegevens worden weergegeven in het rapport, bijvoorbeeld '1M' voor ÇÇn maand, '30D' voor dertig dagen '3Q' voor drie kwartaal of '5Y' voor vijf jaar.;
                             NOR=Angir perioden som rapporten skal vise data for. Angi for eksempel 1M for Çn mÜned, 30D for 30 dager, 3K for tre kvartaler eller 5è for fem Ür.;
                             SVE=Anger perioden som data visas fîr i rapporten. Ange till exempel "1M" fîr en mÜnad, "30D" fîr 30ˇdagar, "3K" fîr tre kvartal eller "5è" fîr fem Ür.];
                  OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;
                                   ENU=Day,Week,Month,Quarter,Year,Period;
                                   NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                   NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;
                                   SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period];
                  ApplicationArea=#Jobs;
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 4   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsbeschreibung;
                             ENU=Posting Description;
                             NLD=Boekingsomschrijving;
                             NOR=Bokfõringsbeskrivelse;
                             SVE=Bokfîringsbeskrivning];
                  ToolTipML=[DEU=Gibt eine Beschreibung an, die die von der Komprimierung erzeugten Posten begleitet. Die vorgegebene Beschreibung lautet "Datumskomprimiert".;
                             ENU=Specifies a text that accompanies the entries that result from the compression. The default description is Date Compressed.;
                             NLD=Hiermee wordt een tekst opgegeven voor de posten die tijdens de compressie worden gevormd. De standaardomschrijving is Gecomprimeerde post.;
                             NOR=Angir en tekst som fõlger med postene som er et resultat av komprimeringen. Standardbeskrivelsen er Datokomprimert.;
                             SVE=Anger en text som medfîljer transaktionerna som skapas vid komprimeringen. Standardbeskrivningen Ñr "Datumkomprimerad".];
                  ApplicationArea=#Jobs;
                  SourceExpr=EntrdResLedgEntry.Description }

      { 1907651001;2;Group  ;
                  CaptionML=[DEU=Feldinhalt behalten;
                             ENU=Retain Field Contents;
                             NLD=Te bewaren velden;
                             NOR=Behold feltinnhold;
                             SVE=BibehÜll fÑlt] }

      { 5   ;3   ;Field     ;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt die Nummer eines Belegs an, auf den die Datumskomprimierung zutrifft.;
                             ENU=Specifies the number of a document that the date compression will apply to.;
                             NLD=Hiermee wordt het nummer opgegeven van een document waarop de datumcompressie van toepassing is.;
                             NOR=Angir nummeret for et dokument som datokomprimeringen skal gjelde for.];
                  ApplicationArea=#Jobs;
                  SourceExpr=Retain[1] }

      { 6   ;3   ;Field     ;
                  CaptionML=[DEU=Arbeitstypencode;
                             ENU=Work Type Code;
                             NLD=Werksoort;
                             NOR=Arbeidstypekode;
                             SVE=Arbetstypkod];
                  ToolTipML=[DEU=Legt fest, dass Sie die Inhalte des Felds "Arbeitstypencode" behalten mîchten.;
                             ENU=Specifies that you want to retain the contents of the Work Type Code field.;
                             NLD=Hiermee wordt opgegeven dat u de inhoud van het veld Werksoort wilt behouden.;
                             NOR=Angir at du vil beholde innholdet i feltet Arbeidstypekode.];
                  ApplicationArea=#Jobs;
                  SourceExpr=Retain[2] }

      { 7   ;3   ;Field     ;
                  CaptionML=[DEU=Projektnr.;
                             ENU=Job No.;
                             NLD=Projectnr.;
                             NOR=Prosjektnr.;
                             SVE=Projektnr];
                  ToolTipML=[DEU=Gibt die Nummer des Projekts an.;
                             ENU=Specifies the job number.;
                             NLD=Hiermee wordt het projectnummer opgegeven.;
                             NOR=Angir prosjektnummeret.;
                             SVE=Anger projektnumret.];
                  ApplicationArea=#Jobs;
                  SourceExpr=Retain[3] }

      { 10  ;3   ;Field     ;
                  CaptionML=[DEU=Einheitencode;
                             ENU=Unit of Measure Code;
                             NLD=Eenheidscode;
                             NOR=Enhetskode;
                             SVE=Enhetskod];
                  ToolTipML=[DEU=Legt fest, dass Sie die Inhalte des Felds "Einheitencode" behalten mîchten.;
                             ENU=Specifies that you want to retain the contents of the Unit of Measure Code field.;
                             NLD=Hiermee wordt opgegeven dat u de inhoud van het veld Eenheidscode wilt behouden.;
                             NOR=Angir at du vil beholde innholdet i feltet Enhetskode.];
                  ApplicationArea=#Jobs;
                  SourceExpr=Retain[4] }

      { 8   ;3   ;Field     ;
                  CaptionML=[DEU=Herkunftsart;
                             ENU=Source Type;
                             NLD=Bronsoort;
                             NOR=Kildetype;
                             SVE=Ursprungstyp];
                  ToolTipML=[DEU=Legt fest, dass Sie die Inhalte des Felds "Herkunftsart" behalten mîchten.;
                             ENU=Specifies that you want to retain the contents of the Source Type field.;
                             NLD=Hiermee wordt opgegeven dat u de inhoud van het veld Bronsoort wilt behouden.;
                             NOR=Angir at du vil beholde innholdet i feltet Kildetype.];
                  ApplicationArea=#Jobs;
                  SourceExpr=Retain[8] }

      { 9   ;3   ;Field     ;
                  CaptionML=[DEU=Herkunftsnr.;
                             ENU=Source No.;
                             NLD=Bronnr.;
                             NOR=Kildenr.;
                             SVE=Ursprungsnr];
                  ToolTipML=[DEU=Legt fest, dass Sie die Inhalte des Felds "Herkunftsnr." behalten mîchten.;
                             ENU=Specifies that you want to retain the contents of the Source No. field.;
                             NLD=Hiermee wordt opgegeven dat u de inhoud van het veld Bronnr. wilt behouden.;
                             NOR=Angir at du vil beholde innholdet i feltet Kildenr.];
                  ApplicationArea=#Jobs;
                  SourceExpr=Retain[9] }

      { 11  ;3   ;Field     ;
                  CaptionML=[DEU=Fakturierbar;
                             ENU=Chargeable;
                             NLD=Factureerbaar;
                             NOR=Belastbar;
                             SVE=Debiterbar];
                  ToolTipML=[DEU=Legt fest, dass Sie die Inhalte des Felds "Fakturierbar" behalten mîchten, wenn Ressourcenposten komprimiert werden.;
                             ENU=Specifies that you want to retain the contents of the Chargeable field when compressing resource ledgers.;
                             NLD=Hiermee wordt opgegeven dat u de inhoud van het veld Factureerbaar wilt behouden wanneer resourcejournalen worden gecomprimeerd.;
                             NOR=Angir at du vil beholde innholdet i feltet Belastbar ved komprimering av ressursposter.];
                  ApplicationArea=#Jobs;
                  SourceExpr=Retain[7] }

      { 12  ;2   ;Field     ;
                  Name=RetainDimensions;
                  CaptionML=[DEU=Dimensionen behalten;
                             ENU=Retain Dimensions;
                             NLD=Te bewaren dimensies;
                             NOR=Behold dimensjoner;
                             SVE=BibehÜll dimensioner];
                  ToolTipML=[DEU=Legt fest, welche Dimensionsdaten bei der Komprimierung der Posten erhalten bleiben sollen. Je mehr Dimensionsinformationen Sie beibehalten, desto detaillierter werden die komprimierten Posten sein.;
                             ENU=Specifies which dimension information you want to retain when the entries are compressed. The more dimension information that you choose to retain, the more detailed the compressed entries are.;
                             NLD=Hiermee wordt opgegeven welke dimensiegegevens u bij de compressie van de posten wilt bewaren. Hoe meer dimensiegegevens u bewaart, hoe gedetailleerder het resultaat van de compressie is.;
                             NOR=Angir hvilken dimensjonsinformasjon du vil beholde nÜr postene komprimeres. Jo mer dimensjonsinformasjon du velger Ü beholde, jo mer detaljerte er de komprimerte postene.;
                             SVE=Anger den dimensionsinformation som du vill behÜlla nÑr transaktionerna komprimeras. Ju mer dimensionsinformation du vÑljer att behÜlla, desto mer detaljerade blir de komprimerade transaktionerna.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=RetainDimText;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Compress Resource Ledger",RetainDimText);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Durch diesen Batchauftrag werden Posten gelîscht. Daher muss vor dem AusfÅhren des Batchauftrags eine Datenbanksicherung erstellt werden.\\Mîchten Sie die Posten datumskomprimieren?;ENU=This batch job deletes entries.Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to date compress the entries?;NLD=Met deze batchverwerking worden posten verwijderd. Maak daarom eerst een reservekopie van de database voor u de batchverwerking uitvoert.\\Wilt u de posten comprimeren?;NOR=Denne kjõrselen sletter poster. Derfor er det viktig at du tar en sikkerhetskopi av databasen fõr du starter kjõrselen.\\Vil du datokomprimere postene?';
      Text003@1003 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified.;NLD=%1 moet opgegeven worden.;NOR=%1 mÜ angis.;SVE=%1 mÜste specificeras.';
      Text004@1004 : TextConst 'DEU=Datumskomprimierung fÅr Ressourcenposten wird ausgefÅhrt...\\Ressourcennr.             #1##########\Datum                     #2######\\Anzahl neuer Posten       #3######\Anzahl gelîschter Posten  #4######;ENU=Date compressing resource ledger entries...\\Resource No.         #1##########\Date                 #2######\\No. of new entries   #3######\No. of entries del.  #4######;NLD=Comprimeren resourceposten...\\Resourcenr.         #1##########\Datum                 #2######\\Nieuwe posten   #3######\Verwijderde posten #4######;NOR=Datokomprimerer ressursposter ...\\Ressursnr.         #1##########\Dato                 #2######\\Antall nye poster   #3######\Ant. slettede poster  #4######';
      Text009@1009 : TextConst 'DEU=Datumskomprimiert;ENU=Date Compressed;NLD=Gecomprimeerde post;NOR=Dato komprimert;SVE=Datumkomprimerad';
      Text010@1010 : TextConst 'DEU=Dimensionen behalten;ENU=Retain Dimensions;NLD=Te bewaren dimensies;NOR=Behold dimensjoner;SVE=BibehÜll dimensioner';
      SourceCodeSetup@1011 : Record 242;
      DateComprReg@1012 : Record 87;
      EntrdDateComprReg@1013 : Record 87;
      ResReg@1014 : Record 240;
      EntrdResLedgEntry@1015 : Record 203;
      NewResLedgEntry@1016 : Record 203;
      ResLedgEntry2@1017 : Record 203;
      SelectedDim@1018 : Record 369;
      TempSelectedDim@1019 : TEMPORARY Record 369;
      DimSelectionBuf@1020 : Record 368;
      DateComprMgt@1021 : Codeunit 356;
      DimBufMgt@1022 : Codeunit 411;
      DimMgt@1023 : Codeunit 408;
      Window@1024 : Dialog;
      ResLedgEntryFilter@1025 : Text[250];
      NoOfFields@1026 : Integer;
      Retain@1027 : ARRAY [10] OF Boolean;
      FieldNumber@1028 : ARRAY [10] OF Integer;
      FieldNameArray@1029 : ARRAY [10] OF Text[100];
      LastEntryNo@1030 : Integer;
      NoOfDeleted@1031 : Integer;
      ResRegExists@1032 : Boolean;
      i@1033 : Integer;
      ComprDimEntryNo@1034 : Integer;
      DimEntryNo@1035 : Integer;
      RetainDimText@1036 : Text[250];

    LOCAL PROCEDURE InitRegisters@4();
    VAR
      NextRegNo@1000 : Integer;
    BEGIN
      IF ResReg.FIND('+') THEN;
      ResReg.INIT;
      ResReg."No." := ResReg."No." + 1;
      ResReg."Creation Date" := TODAY;
      ResReg."Creation Time" := TIME;
      ResReg."Source Code" := SourceCodeSetup."Compress Res. Ledger";
      ResReg."User ID" := USERID;
      ResReg."From Entry No." := LastEntryNo + 1;

      IF DateComprReg.FINDLAST THEN
        NextRegNo := DateComprReg."No." + 1;

      DateComprReg.InitRegister(
        DATABASE::"Res. Ledger Entry",NextRegNo,
        EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date",EntrdDateComprReg."Period Length",
        ResLedgEntryFilter,ResReg."No.",SourceCodeSetup."Compress Res. Ledger");

      FOR i := 1 TO NoOfFields DO
        IF Retain[i] THEN
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);

      ResRegExists := FALSE;
      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@1(VAR ResReg@1000 : Record 240;VAR DateComprReg@1001 : Record 87);
    BEGIN
      ResReg."To Entry No." := NewResLedgEntry."Entry No.";

      IF ResRegExists THEN BEGIN
        ResReg.MODIFY;
        DateComprReg.MODIFY;
      END ELSE BEGIN
        ResReg.INSERT;
        DateComprReg.INSERT;
        ResRegExists := TRUE;
      END;
      COMMIT;

      NewResLedgEntry.LOCKTABLE;
      ResReg.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      ResLedgEntry2.RESET;
      IF ResLedgEntry2.FIND('+') THEN;
      IF LastEntryNo <> ResLedgEntry2."Entry No." THEN BEGIN
        LastEntryNo := ResLedgEntry2."Entry No.";
        InitRegisters;
      END;
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    LOCAL PROCEDURE SummarizeEntry@5(VAR NewResLedgEntry@1000 : Record 203;ResLedgEntry@1001 : Record 203);
    BEGIN
      WITH ResLedgEntry DO BEGIN
        NewResLedgEntry.Quantity := NewResLedgEntry.Quantity + Quantity;
        NewResLedgEntry."Quantity (Base)" := NewResLedgEntry."Quantity (Base)" + "Quantity (Base)";
        NewResLedgEntry."Total Cost" := NewResLedgEntry."Total Cost" + "Total Cost";
        NewResLedgEntry."Total Price" := NewResLedgEntry."Total Price" + "Total Price";
        DELETE;
        DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
        Window.UPDATE(4,DateComprReg."No. Records Deleted");
      END;
    END;

    [External]
    PROCEDURE ComprCollectedEntries@12();
    VAR
      ResLedgEntry@1000 : Record 203;
      OldDimEntryNo@1001 : Integer;
      Found@1002 : Boolean;
      ResLedgEntryNo@1003 : Integer;
    BEGIN
      OldDimEntryNo := 0;
      IF DimBufMgt.FindFirstDimEntryNo(DimEntryNo,ResLedgEntryNo) THEN BEGIN
        InitNewEntry(NewResLedgEntry);
        REPEAT
          ResLedgEntry.GET(ResLedgEntryNo);
          SummarizeEntry(NewResLedgEntry,ResLedgEntry);
          OldDimEntryNo := DimEntryNo;
          Found := DimBufMgt.NextDimEntryNo(DimEntryNo,ResLedgEntryNo);
          IF (OldDimEntryNo <> DimEntryNo) OR NOT Found THEN BEGIN
            InsertNewEntry(NewResLedgEntry,OldDimEntryNo);
            IF Found THEN
              InitNewEntry(NewResLedgEntry);
          END;
          OldDimEntryNo := DimEntryNo;
        UNTIL NOT Found;
      END;
      DimBufMgt.DeleteAllDimEntryNo;
    END;

    [External]
    PROCEDURE InitNewEntry@7(VAR NewResLedgEntry@1000 : Record 203);
    BEGIN
      LastEntryNo := LastEntryNo + 1;

      WITH ResLedgEntry2 DO BEGIN
        NewResLedgEntry.INIT;
        NewResLedgEntry."Entry No." := LastEntryNo;
        NewResLedgEntry."Entry Type" := "Entry Type";
        NewResLedgEntry."Resource No." := "Resource No.";
        NewResLedgEntry."Resource Group No." := "Resource Group No.";
        NewResLedgEntry."Posting Date" := GETRANGEMIN("Posting Date");
        NewResLedgEntry.Description := EntrdResLedgEntry.Description;
        NewResLedgEntry."Source Code" := SourceCodeSetup."Compress Res. Ledger";
        NewResLedgEntry."User ID" := USERID;

        IF RetainNo(FIELDNO("Document No.")) THEN
          NewResLedgEntry."Document No." := "Document No.";
        IF RetainNo(FIELDNO("Work Type Code")) THEN
          NewResLedgEntry."Work Type Code" := "Work Type Code";
        IF RetainNo(FIELDNO("Job No.")) THEN
          NewResLedgEntry."Job No." := "Job No.";
        IF RetainNo(FIELDNO("Unit of Measure Code")) THEN
          NewResLedgEntry."Unit of Measure Code" := "Unit of Measure Code";
        IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
          NewResLedgEntry."Global Dimension 1 Code" := "Global Dimension 1 Code";
        IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
          NewResLedgEntry."Global Dimension 2 Code" := "Global Dimension 2 Code";
        IF RetainNo(FIELDNO(Chargeable)) THEN
          NewResLedgEntry.Chargeable := Chargeable;

        Window.UPDATE(1,NewResLedgEntry."Resource No.");
        Window.UPDATE(2,NewResLedgEntry."Posting Date");
        DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
        Window.UPDATE(3,DateComprReg."No. of New Records");
      END;
    END;

    LOCAL PROCEDURE InsertNewEntry@23(VAR NewResLedgEntry@1000 : Record 203;DimEntryNo@1001 : Integer);
    VAR
      TempDimBuf@1002 : TEMPORARY Record 360;
      TempDimSetEntry@1005 : TEMPORARY Record 480;
    BEGIN
      TempDimBuf.DELETEALL;
      DimBufMgt.GetDimensions(DimEntryNo,TempDimBuf);
      DimMgt.CopyDimBufToDimSetEntry(TempDimBuf,TempDimSetEntry);
      NewResLedgEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
      NewResLedgEntry.INSERT;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

