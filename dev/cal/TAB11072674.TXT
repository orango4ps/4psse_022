OBJECT Table 11072674 Plant Maint. Scheme Version
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceSetup@1100528800 : Record 11012800;
             BEGIN
               ServiceSetup.GET();
               CheckKeyFieldsLevel();
             END;

    OnDelete=VAR
               PlantMaintSchemeVersionLine@1100528800 : Record 11072675;
               CancelledPlantMaintSchemeLine@1100529000 : Record 11072676;
             BEGIN
               PlantMaintSchemeVersionLine.SETRANGE(Level, Level);
               PlantMaintSchemeVersionLine.SETRANGE("Plant Type", "Plant Type");
               PlantMaintSchemeVersionLine.SETRANGE("Plant No.", "Plant No.");
               PlantMaintSchemeVersionLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
               PlantMaintSchemeVersionLine.SETRANGE("Index Date", "Index Date");
               PlantMaintSchemeVersionLine.DELETEALL(TRUE);

               CancelledPlantMaintSchemeLine.SETRANGE(Level, Level);
               CancelledPlantMaintSchemeLine.SETRANGE("Plant Type", "Plant Type");
               CancelledPlantMaintSchemeLine.SETRANGE("Plant No.", "Plant No.");
               CancelledPlantMaintSchemeLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
               CancelledPlantMaintSchemeLine.SETRANGE("Index Date", "Index Date");
               CancelledPlantMaintSchemeLine.DELETEALL;
             END;

    OnRename=VAR
               PlantMaintSchemeVersionLine@1100529000 : Record 11072675;
               PlantMaintSchemeVersionLine2@1100529001 : Record 11072675;
               CancelledPlantMaintSchemeLine@1100529002 : Record 11072676;
               CancelledPlantMaintSchemeLine2@1100529003 : Record 11072676;
             BEGIN
               TESTFIELD(Level, xRec.Level);
               TESTFIELD("Plant Type", xRec."Plant Type");
               TESTFIELD("Plant No.", xRec."Plant No.");
               TESTFIELD("Maintenance Scheme", xRec."Maintenance Scheme");
               CheckKeyFieldsLevel();
               IF ("Index Date" <> 0D) AND ("Index Date" <> xRec."Index Date") THEN BEGIN
                 PlantMaintSchemeVersionLine.SETRANGE(Level, Level);
                 PlantMaintSchemeVersionLine.SETRANGE("Plant Type", "Plant Type");
                 PlantMaintSchemeVersionLine.SETRANGE("Plant No.", "Plant No.");
                 PlantMaintSchemeVersionLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
                 PlantMaintSchemeVersionLine.SETRANGE("Index Date", xRec."Index Date");
                 IF PlantMaintSchemeVersionLine.FINDSET THEN BEGIN
                   REPEAT
                     CancelledPlantMaintSchemeLine.RESET;
                     CancelledPlantMaintSchemeLine.SETCURRENTKEY(
                       Level,"Plant Type","Plant No.","Maintenance Scheme","Index Date",
                       "Level (Cancelled)","Plant Type (Cancelled)","Maintenance Scheme (Cancelled)","Index Date (Cancelled)","Line No. (Cancelled)");
                     CancelledPlantMaintSchemeLine.SETRANGE(Level, PlantMaintSchemeVersionLine.Level);
                     CancelledPlantMaintSchemeLine.SETRANGE("Plant Type", PlantMaintSchemeVersionLine."Plant Type");
                     CancelledPlantMaintSchemeLine.SETRANGE("Maintenance Scheme", PlantMaintSchemeVersionLine."Maintenance Scheme");
                     CancelledPlantMaintSchemeLine.SETRANGE("Index Date", PlantMaintSchemeVersionLine."Index Date");
                     IF CancelledPlantMaintSchemeLine.FINDFIRST THEN BEGIN
                       CancelledPlantMaintSchemeLine2 := CancelledPlantMaintSchemeLine;
                       CancelledPlantMaintSchemeLine2."Index Date" := "Index Date";
                       CancelledPlantMaintSchemeLine2.INSERT;
                       CancelledPlantMaintSchemeLine.DELETE;
                     END;
                     IF Level IN [Level::"Maintenance Scheme", Level::"Plant Type"] THEN BEGIN
                       CancelledPlantMaintSchemeLine.RESET;
                       CancelledPlantMaintSchemeLine.SETCURRENTKEY(
                         "Level (Cancelled)","Plant Type (Cancelled)","Maintenance Scheme (Cancelled)","Index Date (Cancelled)","Line No. (Cancelled)");
                       CancelledPlantMaintSchemeLine.SETRANGE("Level (Cancelled)", PlantMaintSchemeVersionLine.Level);
                       CancelledPlantMaintSchemeLine.SETRANGE("Plant Type (Cancelled)", PlantMaintSchemeVersionLine."Plant Type");
                       CancelledPlantMaintSchemeLine.SETRANGE("Maintenance Scheme (Cancelled)", PlantMaintSchemeVersionLine."Maintenance Scheme");
                       CancelledPlantMaintSchemeLine.SETRANGE("Index Date (Cancelled)", PlantMaintSchemeVersionLine."Index Date");
                       CancelledPlantMaintSchemeLine.SETRANGE("Line No. (Cancelled)", PlantMaintSchemeVersionLine."Line No.");
                       IF CancelledPlantMaintSchemeLine.FINDFIRST THEN BEGIN
                         REPEAT
                           CancelledPlantMaintSchemeLine2 := CancelledPlantMaintSchemeLine;
                           CancelledPlantMaintSchemeLine2."Index Date (Cancelled)" := "Index Date";
                           CancelledPlantMaintSchemeLine2.INSERT;
                           CancelledPlantMaintSchemeLine.DELETE;
                         UNTIL CancelledPlantMaintSchemeLine.NEXT = 0;
                       END;
                     END;
                     PlantMaintSchemeVersionLine2 := PlantMaintSchemeVersionLine;
                     PlantMaintSchemeVersionLine2."Index Date" := "Index Date";
                     PlantMaintSchemeVersionLine2.INSERT;
                     PlantMaintSchemeVersionLine.DELETE;
                   UNTIL PlantMaintSchemeVersionLine.NEXT = 0;
                 END;
               END;
             END;

    CaptionML=ENU=Plant Maint. Scheme Version;
    LookupPageID=Page11125951;
    DrillDownPageID=Page11125951;
  }
  FIELDS
  {
    { 10  ;   ;Level               ;Option        ;CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   OptionCaptionML=ENU=Maintenance Scheme,Plant Type,Plant Number;
                                                   OptionString=Maintenance Scheme,Plant Type,Plant Number }
    { 15  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type".Code;
                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 20  ;   ;Plant No.           ;Code20        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Niv†kode;
                                                              SVE=Niv†kod] }
    { 40  ;   ;Maintenance Scheme  ;Code20        ;TableRelation="Plant Stnd. Maintenance Scheme";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Maintenance Scheme Description");
                                                              END;

                                                   CaptionML=[ENU=Maintenance Scheme;
                                                              NOR=Vedlikeholdsskjema;
                                                              SVE=Underh†llsschema];
                                                   NotBlank=Yes }
    { 50  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Stnd. Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme)));
                                                   CaptionML=[ENU=Maintenance Scheme Description;
                                                              NOR=Beskrivelse av vedlikeholdskjema;
                                                              SVE=Beskrivning av underh†llsschema];
                                                   Editable=No }
    { 60  ;   ;Index Date          ;Date          ;CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum];
                                                   NotBlank=Yes }
    { 70  ;   ;Cancelled           ;Boolean       ;CaptionML=[ENU=Cancelled;
                                                              NOR=Annullert;
                                                              SVE=Avbruten] }
  }
  KEYS
  {
    {    ;Level,Plant Type,Plant No.,Maintenance Scheme,Index Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE CheckKeyFieldsLevel@1100525001();
    BEGIN
      CASE Level OF
        Level::"Maintenance Scheme":
          BEGIN
            TESTFIELD("Plant Type", '');
            TESTFIELD("Plant No.", '');
          END;
        Level::"Plant Type":
          BEGIN
            TESTFIELD("Plant Type");
            TESTFIELD("Plant No.", '');
          END;
        Level::"Plant Number":
          BEGIN
            TESTFIELD("Plant Type");
            TESTFIELD("Plant No.");
          END;
      END;
      TESTFIELD("Maintenance Scheme");
      TESTFIELD("Index Date");
    END;

    PROCEDURE GetPlantDescription@1100528803() : Text;
    VAR
      PlantType@1100528802 : Record 11012551;
      PlantNumber@1100528801 : Record 11012552;
    BEGIN
      CASE Level OF
        Level::"Plant Type":
          IF PlantType.GET("Plant Type") THEN
            EXIT(PlantType.Description);
        Level::"Plant Number":
          IF PlantNumber.GET("Plant Type", "Plant No.") THEN
            EXIT(PlantNumber.Description);
      END;
      EXIT('');
    END;

    PROCEDURE NewerVersionExists@1100528600() : Boolean;
    VAR
      PlantMaintenanceSchemeVersion@1100528600 : Record 11072674;
    BEGIN
      PlantMaintenanceSchemeVersion := Rec;
      PlantMaintenanceSchemeVersion.SETRECFILTER;
      PlantMaintenanceSchemeVersion.SETFILTER("Index Date", '>%1', "Index Date");
      PlantMaintenanceSchemeVersion.SETRANGE(Cancelled, FALSE);
      EXIT(NOT PlantMaintenanceSchemeVersion.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

