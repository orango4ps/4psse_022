OBJECT Table 11229373 Subcontracting Activity Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               SubcActivityPriceHistory@1100529000 : Record 11229425;
             BEGIN
               SubcActivityPriceHistory.SETRANGE("Subcontracting Activity", "Subcontracting Activity");
               SubcActivityPriceHistory.SETRANGE("Subcontractor No.", "Subcontractor No.");
               SubcActivityPriceHistory.DELETEALL;
             END;

    OnRename=VAR
               SubcActivityPriceHistory@1100529000 : Record 11229425;
             BEGIN
               SubcActivityPriceHistory.SETRANGE("Subcontracting Activity", xRec."Subcontracting Activity");
               SubcActivityPriceHistory.SETRANGE("Subcontractor No.", xRec."Subcontractor No.");
               IF NOT SubcActivityPriceHistory.ISEMPTY THEN
                 ERROR(Text001, SubcActivityPriceHistory.TABLECAPTION);
             END;

    CaptionML=ENU=Subcontracting Activity Price;
    LookupPageID=Page11130276;
  }
  FIELDS
  {
    { 10  ;   ;Subcontracting Activity;Code10     ;TableRelation="Subcontracting Activity".Code;
                                                   CaptionML=ENU=Subcontracting Activity;
                                                   NotBlank=Yes }
    { 11  ;   ;Activity Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Subcontracting Activity".Description WHERE (Code=FIELD(Subcontracting Activity)));
                                                   CaptionML=[ENU=Activity Description;
                                                              NOR=Aktivitetsbeskrivelse;
                                                              SVE=Aktivitetsbeskrivning];
                                                   Editable=No }
    { 20  ;   ;Subcontractor No.   ;Code20        ;TableRelation=Vendor.No. WHERE (Subcontractor Type=FILTER(Subcontractor|Self Employed|Employment Agency));
                                                   CaptionML=[ENU=Subcontractor No.;
                                                              NOR=Underleverand›rnr.;
                                                              SVE=Underleverant”rsnr];
                                                   Description=C047965: allow empty field }
    { 21  ;   ;Subcontractor Name  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Subcontractor No.)));
                                                   CaptionML=[ENU=Subcontractor Name;
                                                              NOR=Underentrepren›rens navn;
                                                              SVE=Underentrepren”rens namn];
                                                   Editable=No }
    { 30  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(Subcontracting),
                                                                                               Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   CaptionClass='1,1,2' }
    { 40  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=Enhet] }
    { 50  ;   ;Price OLD           ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Price OLD", 0);
                                                              END;

                                                   CaptionML=ENU=Price OLD;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 60  ;   ;Preferred Subcontractor;Boolean    ;OnValidate=BEGIN
                                                                CheckOtherVendors;
                                                              END;

                                                   CaptionML=ENU=Preferred Subcontractor }
  }
  KEYS
  {
    {    ;Subcontracting Activity,Subcontractor No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529000 : TextConst 'ENU=Rename not allowed, %1 already exist';

    LOCAL PROCEDURE CheckOtherVendors@1100529500();
    VAR
      SubcontractingActivityPrice@1100529900 : Record 11229373;
      SubcontractingActivityPrice2@1100529901 : Record 11229373;
    BEGIN
      SubcontractingActivityPrice.SETRANGE("Subcontracting Activity", "Subcontracting Activity");
      SubcontractingActivityPrice.SETFILTER("Subcontractor No.", '<>%1', "Subcontractor No.");
      SubcontractingActivityPrice.SETRANGE("Preferred Subcontractor", TRUE);
      IF SubcontractingActivityPrice.FINDSET THEN BEGIN
        REPEAT
          SubcontractingActivityPrice2 := SubcontractingActivityPrice;
          SubcontractingActivityPrice2."Preferred Subcontractor" := FALSE;
          SubcontractingActivityPrice2.MODIFY;
        UNTIL SubcontractingActivityPrice.NEXT = 0;
      END;
    END;

    PROCEDURE GetPrice@1100529000(ReferenceDate@1100529001 : Date) : Decimal;
    VAR
      SubcActivityPriceHistory@1100529000 : Record 11229425;
    BEGIN
      IF ("Subcontracting Activity" <> '') THEN BEGIN  //C047965
        IF ReferenceDate = 0D THEN
          ReferenceDate := TODAY;
        SubcActivityPriceHistory.SETRANGE("Subcontracting Activity", "Subcontracting Activity");
        SubcActivityPriceHistory.SETRANGE("Subcontractor No.", "Subcontractor No.");
        SubcActivityPriceHistory.SETFILTER("Starting Date", '<=%1', ReferenceDate);
        IF SubcActivityPriceHistory.FINDLAST THEN
          EXIT(SubcActivityPriceHistory.Price);
      END;
      EXIT(0);
    END;

    PROCEDURE ShowPriceHistory@1100529001(ReferenceDate@1100529002 : Date;EditableMode@1100529003 : Boolean);
    VAR
      SubcActivityPriceHistory@1100529000 : Record 11229425;
      SubcActivityHistoryPrices@1100529004 : Page 11129897;
      Found@1100529001 : Boolean;
    BEGIN
      TESTFIELD("Subcontracting Activity");
      //TESTFIELD("Subcontractor No.");  //C047965
      IF ReferenceDate = 0D THEN
        ReferenceDate := WORKDATE;
      SubcActivityPriceHistory.FILTERGROUP(4);
      SubcActivityPriceHistory.SETRANGE("Subcontracting Activity", "Subcontracting Activity");
      SubcActivityPriceHistory.SETRANGE("Subcontractor No.", "Subcontractor No.");
      SubcActivityPriceHistory.FILTERGROUP(0);
      //
      SubcActivityPriceHistory.SETFILTER("Starting Date", '<=%1', ReferenceDate);
      Found := SubcActivityPriceHistory.FINDLAST;
      SubcActivityPriceHistory.SETRANGE("Starting Date");
      IF NOT Found THEN
        Found := SubcActivityPriceHistory.FINDFIRST;
      //
      SubcActivityHistoryPrices.SETTABLEVIEW(SubcActivityPriceHistory);
      SubcActivityHistoryPrices.SETRECORD(SubcActivityPriceHistory);
      SubcActivityHistoryPrices.EDITABLE(EditableMode);
      SubcActivityHistoryPrices.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

