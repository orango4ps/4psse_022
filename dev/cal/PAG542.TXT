OBJECT Page 542 Default Dimensions-Multiple
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Default Dimensions-Multiple;
               NOR=Standarddimensjoner - flere;
               SVE=Standard dimension/flera];
    SourceTable=Table352;
    DataCaptionExpr=GetCaption;
    PageType=List;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             CurrPage.LOOKUPMODE := TRUE;
           END;

    OnOpenPage=BEGIN
                 GetDefaultDim;
               END;

    OnAfterGetRecord=BEGIN
                       DimensionValueCodeOnFormat(FORMAT("Dimension Value Code"));
                       ValuePostingOnFormat(FORMAT("Value Posting"));
                     END;

    OnInsertRecord=BEGIN
                     SETRANGE("Dimension Code","Dimension Code");
                     IF NOT FIND('-') AND ("Dimension Code" <> '') THEN BEGIN
                       "Multi Selection Action" := "Multi Selection Action"::Change;
                       INSERT;
                     END;
                     SETRANGE("Dimension Code");
                     EXIT(FALSE);
                   END;

    OnModifyRecord=BEGIN
                     "Multi Selection Action" := "Multi Selection Action"::Change;
                     MODIFY;
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     "Multi Selection Action" := "Multi Selection Action"::Delete;
                     MODIFY;
                     EXIT(FALSE);
                   END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN
                         LookupOKOnPush;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the default dimension.;
                           NOR=Angir koden for standarddimensjonen.;
                           SVE=Anger standarddimensionens kod.];
                ApplicationArea=#Dimensions;
                SourceExpr="Dimension Code";
                OnValidate=BEGIN
                             IF (xRec."Dimension Code" <> '') AND (xRec."Dimension Code" <> "Dimension Code") THEN
                               ERROR(CannotRenameErr,TABLECAPTION);
                           END;
                            }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the dimension value code to suggest as the default dimension.;
                           NOR=Angir dimensjonsverdikoden som skal foresl†s som standarddimensjon.;
                           SVE=Anger den dimensionsv„rdekod som f”resl†s som standarddimension.];
                ApplicationArea=#Dimensions;
                SourceExpr="Dimension Value Code" }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies how default dimensions and their values must be used.;
                           NOR=Angir du hvordan standarddimensjoner og tilh›rende verdier skal brukes.;
                           SVE=Anger hur standarddimensionerna och deras v„rden m†ste anv„ndas.];
                ApplicationArea=#Dimensions;
                SourceExpr="Value Posting" }

  }
  CODE
  {
    VAR
      CannotRenameErr@1000 : TextConst 'ENU=You cannot rename a %1.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn p† %1.';
      Text001@1001 : TextConst 'ENU=(Conflict);NOR=(Konflikt);SVE=(Konflikt)';
      TempDefaultDim2@1003 : TEMPORARY Record 352;
      TempDefaultDim3@1004 : TEMPORARY Record 352;
      TotalRecNo@1005 : Integer;

    [External]
    PROCEDURE ClearTempDefaultDim@24();
    BEGIN
      TempDefaultDim2.DELETEALL;
    END;

    LOCAL PROCEDURE SetCommonDefaultDim@2();
    VAR
      DefaultDim@1000 : Record 352;
    BEGIN
      SETRANGE(
        "Multi Selection Action","Multi Selection Action"::Delete);
      IF FIND('-') THEN
        REPEAT
          IF TempDefaultDim3.FIND('-') THEN
            REPEAT
              IF DefaultDim.GET(
                   TempDefaultDim3."Table ID",TempDefaultDim3."No.","Dimension Code")
              THEN
                DefaultDim.DELETE(TRUE);
            UNTIL TempDefaultDim3.NEXT = 0;
        UNTIL NEXT = 0;
      SETRANGE(
        "Multi Selection Action","Multi Selection Action"::Change);
      IF FIND('-') THEN
        REPEAT
          IF TempDefaultDim3.FIND('-') THEN
            REPEAT
              IF DefaultDim.GET(
                   TempDefaultDim3."Table ID",TempDefaultDim3."No.","Dimension Code")
              THEN BEGIN
                DefaultDim."Dimension Code" := "Dimension Code";
                DefaultDim."Dimension Value Code" := "Dimension Value Code";
                DefaultDim."Value Posting" := "Value Posting";
                OnBeforeSetCommonDefaultCopyFields(DefaultDim,Rec);
                DefaultDim.MODIFY(TRUE);
              END ELSE BEGIN
                DefaultDim.INIT;
                DefaultDim."Table ID" := TempDefaultDim3."Table ID";
                DefaultDim."No." := TempDefaultDim3."No.";
                DefaultDim."Dimension Code" := "Dimension Code";
                DefaultDim."Dimension Value Code" := "Dimension Value Code";
                DefaultDim."Value Posting" := "Value Posting";
                OnBeforeSetCommonDefaultCopyFields(DefaultDim,Rec);
                DefaultDim.INSERT(TRUE);
              END;
            UNTIL TempDefaultDim3.NEXT = 0;
        UNTIL NEXT = 0;
    END;

    [External]
    PROCEDURE CopyDefaultDimToDefaultDim@14(TableID@1000 : Integer;No@1001 : Code[20]);
    VAR
      DefaultDim@1002 : Record 352;
    BEGIN
      TotalRecNo := TotalRecNo + 1;
      TempDefaultDim3."Table ID" := TableID;
      TempDefaultDim3."No." := No;
      TempDefaultDim3.INSERT;

      DefaultDim.SETRANGE("Table ID",TableID);
      DefaultDim.SETRANGE("No.",No);
      IF DefaultDim.FIND('-') THEN
        REPEAT
          TempDefaultDim2 := DefaultDim;
          TempDefaultDim2.INSERT;
        UNTIL DefaultDim.NEXT = 0;
    END;

    LOCAL PROCEDURE GetDefaultDim@15();
    VAR
      Dim@1000 : Record 348;
      RecNo@1001 : Integer;
    BEGIN
      RESET;
      DELETEALL;
      IF Dim.FIND('-') THEN
        REPEAT
          RecNo := 0;
          TempDefaultDim2.SETRANGE("Dimension Code",Dim.Code);
          SETRANGE("Dimension Code",Dim.Code);
          IF TempDefaultDim2.FIND('-') THEN
            REPEAT
              IF FINDFIRST THEN BEGIN
                IF "Dimension Value Code" <> TempDefaultDim2."Dimension Value Code" THEN BEGIN
                  IF ("Multi Selection Action" <> 10) AND
                     ("Multi Selection Action" <> 21)
                  THEN BEGIN
                    "Multi Selection Action" :=
                      "Multi Selection Action" + 10;
                    "Dimension Value Code" := '';
                  END;
                END;
                IF "Value Posting" <> TempDefaultDim2."Value Posting" THEN BEGIN
                  IF ("Multi Selection Action" <> 11) AND
                     ("Multi Selection Action" <> 21)
                  THEN BEGIN
                    "Multi Selection Action" :=
                      "Multi Selection Action" + 11;
                    "Value Posting" := "Value Posting"::" ";
                  END;
                END;
                MODIFY;
                RecNo := RecNo + 1;
              END ELSE BEGIN
                Rec := TempDefaultDim2;
                INSERT;
                RecNo := RecNo + 1;
              END;
            UNTIL TempDefaultDim2.NEXT = 0;

          IF FIND('-') AND (RecNo <> TotalRecNo) THEN
            IF ("Multi Selection Action" <> 10) AND
               ("Multi Selection Action" <> 21)
            THEN BEGIN
              "Multi Selection Action" :=
                "Multi Selection Action" + 10;
              "Dimension Value Code" := '';
              MODIFY;
            END;
        UNTIL Dim.NEXT = 0;

      RESET;
      SETCURRENTKEY("Dimension Code");
      SETFILTER(
        "Multi Selection Action",'<>%1',"Multi Selection Action"::Delete);
    END;

    LOCAL PROCEDURE LookupOKOnPush@19031339();
    BEGIN
      SetCommonDefaultDim;
    END;

    LOCAL PROCEDURE DimensionValueCodeOnFormat@19000225(Text@19073275 : Text[1024]);
    BEGIN
      IF "Dimension Code" <> '' THEN
        IF ("Multi Selection Action" = 10) OR
           ("Multi Selection Action" = 21)
        THEN
          Text := Text001;
    END;

    LOCAL PROCEDURE ValuePostingOnFormat@19024282(Text@19070135 : Text[1024]);
    BEGIN
      IF "Dimension Code" <> '' THEN
        IF ("Multi Selection Action" = 11) OR
           ("Multi Selection Action" = 21)
        THEN
          Text := Text001;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSetCommonDefaultCopyFields@23(VAR DefaultDimension@1000 : Record 352;FromDefaultDimension@1001 : Record 352);
    BEGIN
    END;

    [External]
    PROCEDURE SetMultiRecord@25(MasterRecord@1000 : Variant;NoField@1001 : Integer);
    VAR
      MasterRecordRef@1002 : RecordRef;
      NoFieldRef@1003 : FieldRef;
      No@1004 : Code[20];
    BEGIN
      ClearTempDefaultDim;

      MasterRecordRef.GETTABLE(MasterRecord);
      NoFieldRef := MasterRecordRef.FIELD(NoField);
      IF MasterRecordRef.FINDSET THEN
        REPEAT
          No := NoFieldRef.VALUE;
          CopyDefaultDimToDefaultDim(MasterRecordRef.NUMBER,No);
        UNTIL MasterRecordRef.NEXT = 0;
    END;

    [External]
    PROCEDURE SetMultiEmployee@1(VAR Employee@1000 : Record 5200);
    BEGIN
      //DEPRECATED - TO BE REMOVED FOR FALL 19
      ClearTempDefaultDim;
      WITH Employee DO
        IF FIND('-') THEN
          REPEAT
            CopyDefaultDimToDefaultDim(DATABASE::Employee,"No.");
          UNTIL NEXT = 0;
    END;

    BEGIN
    END.
  }
}

