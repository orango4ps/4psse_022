OBJECT Table 11229798 Consumed Web Service Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnModify=VAR
               ConsumedWebService@1100409000 : Record 11229797;
             BEGIN
               ConsumedWebService.GET("Web Service Code");
               IF (Active) THEN
                 ConsumedWebService.TESTFIELD(Active);
               IF ("Log Communication") THEN
                 ConsumedWebService.TESTFIELD("Log Communication");
             END;

    OnDelete=VAR
               ConsumedWebServLineNamesp@1100528300 : Record 11229838;
               ConsumedWebServiceLineLog@1100528301 : Record 11229887;
               ConsumedWebServiceParameter@1100528302 : Record 11229363;
             BEGIN
               ConsumedWebServLineNamesp.SETRANGE("Web Service Code", "Web Service Code");
               ConsumedWebServLineNamesp.DELETEALL;

               ConsumedWebServiceLineLog.SETCURRENTKEY("Web Service Code", "Seq. No.");
               ConsumedWebServiceLineLog.SETRANGE("Web Service Code", "Web Service Code");
               ConsumedWebServiceLineLog.SETRANGE("Seq. No.", "Seq. No.");
               ConsumedWebServiceLineLog.DELETEALL;

               ConsumedWebServiceParameter.SETRANGE(Type, ConsumedWebServiceParameter.Type::Line);
               ConsumedWebServiceParameter.SETRANGE("Web Service Code", "Web Service Code");
               ConsumedWebServiceParameter.SETRANGE("Seq. No.", "Seq. No.");
               ConsumedWebServiceParameter.DELETEALL;
             END;

    CaptionML=ENU=Consumed Web Service Line;
  }
  FIELDS
  {
    { 10  ;   ;Web Service Code    ;Code20        ;TableRelation="Consumed Web Service";
                                                   CaptionML=ENU=Web Service Code }
    { 20  ;   ;Seq. No.            ;Integer       ;CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr] }
    { 30  ;   ;Method Name         ;Text30        ;OnValidate=BEGIN
                                                                IF ("Request Root Tag" = '') THEN
                                                                  "Request Root Tag" := "Method Name";
                                                                IF ("Response Root Tag" = '') THEN
                                                                  "Response Root Tag" := "Method Name" + 'Response';
                                                              END;

                                                   CaptionML=ENU=Method Name }
    { 40  ;   ;Active              ;Boolean       ;CaptionML=[ENU=Active;
                                                              NOR=Aktiv;
                                                              SVE=Aktiv] }
    { 50  ;   ;Log Communication   ;Boolean       ;CaptionML=ENU=Log Communication }
    { 60  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 200 ;   ;Prefix Method Namespace;Text30     ;CaptionML=ENU=Method Namespace Prefix }
    { 201 ;   ;Method Namespace    ;Text250       ;OnValidate=BEGIN
                                                                //Not all namespaces end with a '/'
                                                                //IF (COPYSTR("Method Namespace", STRLEN("Method Namespace")) <> '/') THEN
                                                                //  "Method Namespace" += '/';
                                                              END;

                                                   CaptionML=ENU=Method Namespace }
    { 202 ;   ;Request Root Tag    ;Text50        ;OnValidate=BEGIN
                                                                //Not all namespaces end with a '/'
                                                                //IF (COPYSTR("Method Namespace", STRLEN("Method Namespace")) <> '/') THEN
                                                                //  "Method Namespace" += '/';
                                                              END;

                                                   CaptionML=ENU=Request Root Tag }
    { 203 ;   ;Response Root Tag   ;Text50        ;OnValidate=BEGIN
                                                                //Not all namespaces end with a '/'
                                                                //IF (COPYSTR("Method Namespace", STRLEN("Method Namespace")) <> '/') THEN
                                                                //  "Method Namespace" += '/';
                                                              END;

                                                   CaptionML=ENU=Response Root Tag }
    { 300 ;   ;Use Custom SOAP Body;Boolean       ;CaptionML=ENU=Use Custom SOAP Body }
    { 310 ;   ;Custom SOAP Body XML;BLOB          ;CaptionML=ENU=Custom SOAP Body XML }
  }
  KEYS
  {
    {    ;Web Service Code,Seq. No.               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528300 : TextConst 'ENU=SOAP Request failed.';
      Text001@1100528301 : TextConst 'ENU=SOAP Request successful.';
      Text002@1100525000 : TextConst 'ENU=SOAP Message: %1';

    PROCEDURE AddCwsLogLine@1100528305(VAR RequestRec@1100528300 : TEMPORARY Record 99008535;VAR ResponseRec@1100528301 : TEMPORARY Record 99008535;RecId@1100528304 : RecordID;StatusCode@1100528303 : Text);
    VAR
      ConsumedWebServiceLineLog@1100528302 : Record 11229887;
    BEGIN
      ConsumedWebServiceLineLog.INIT;
      ConsumedWebServiceLineLog."Entry No." := 0;
      ConsumedWebServiceLineLog."Web Service Code" := "Web Service Code";
      ConsumedWebServiceLineLog."Seq. No." := "Seq. No.";
      ConsumedWebServiceLineLog."Method Name" := "Method Name";
      ConsumedWebServiceLineLog."Date Send" := DT2DATE(CURRENTDATETIME);
      ConsumedWebServiceLineLog."Time Send" := DT2TIME(CURRENTDATETIME);
      ConsumedWebServiceLineLog."Status Code" := StatusCode;
      ConsumedWebServiceLineLog."Request Message" := RequestRec.Blob;
      ConsumedWebServiceLineLog."Response Message" := ResponseRec.Blob;
      ConsumedWebServiceLineLog."Message Id" := FORMAT(RecId);
      ConsumedWebServiceLineLog.INSERT(TRUE);
    END;

    [External]
    PROCEDURE StartNamespacePage@1100528302();
    VAR
      ConsumedWebServiceNamespace@1100528300 : Record 11229838;
    BEGIN
      ConsumedWebServiceNamespace.FILTERGROUP(9);
      ConsumedWebServiceNamespace.SETRANGE("Web Service Code", "Web Service Code");
      ConsumedWebServiceNamespace.SETRANGE("Seq. No.", "Seq. No.");
      ConsumedWebServiceNamespace.FILTERGROUP(9);
      PAGE.RUNMODAL(PAGE::"Consumed Web Service Namespace", ConsumedWebServiceNamespace);
    END;

    PROCEDURE GetDefaultNamespaces@1100528303();
    VAR
      ConsumedWebServiceNamespace@1100528300 : Record 11229838;
      ConsumedWebServiceNamespace1@1100528301 : Record 11229838;
    BEGIN
      ConsumedWebServiceNamespace.SETRANGE("Web Service Code", "Web Service Code");
      ConsumedWebServiceNamespace.SETRANGE("Seq. No.", 0);
      IF (NOT ConsumedWebServiceNamespace.FINDSET) THEN
        EXIT;

      REPEAT
        ConsumedWebServiceNamespace1.COPY(ConsumedWebServiceNamespace);
        ConsumedWebServiceNamespace1."Seq. No." := "Seq. No.";
        IF (ConsumedWebServiceNamespace1.INSERT(TRUE)) THEN;
      UNTIL (ConsumedWebServiceNamespace.NEXT = 0);
    END;

    PROCEDURE ExecuteLine@1100528304();
    VAR
      ConsumedWebServiceMgt@1100528300 : Codeunit 11229310;
      RequestXML@1100528302 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ResponseXML@1100528301 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      RequestNamespaceManager@1100528303 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamespaceManager";
      ResponseNamespaceManager@1100528304 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamespaceManager";
    BEGIN
      ConsumedWebServiceMgt.CreateRequest("Web Service Code", "Seq. No.", RequestXML, RequestNamespaceManager);
      IF (ConsumedWebServiceMgt.SendCWS("Web Service Code", "Seq. No.", RequestXML, ResponseXML, ResponseNamespaceManager, '-999')) THEN
        MESSAGE(Text001)
      ELSE
        MESSAGE(Text000);
    END;

    PROCEDURE CopyLine@1100528301();
    VAR
      ConsumedWebServiceLine@1100528301 : Record 11229798;
      ConsumedWebServiceNamespace@1100528303 : Record 11229838;
      ConsumedWebServiceNamespace1@1100528300 : Record 11229838;
      LastSeqNo@1100528302 : Integer;
    BEGIN
      LastSeqNo := 10000;
      ConsumedWebServiceLine.SETRANGE("Web Service Code", "Web Service Code");
      IF (ConsumedWebServiceLine.FINDLAST) THEN
        LastSeqNo := ConsumedWebServiceLine."Seq. No.";
      ConsumedWebServiceLine.COPY(Rec);
      ConsumedWebServiceLine."Seq. No." := LastSeqNo + 10000;
      ConsumedWebServiceLine.INSERT(TRUE);

      ConsumedWebServiceNamespace.SETRANGE("Web Service Code", "Web Service Code");
      ConsumedWebServiceNamespace.SETRANGE("Seq. No.", "Seq. No.");
      IF (ConsumedWebServiceNamespace.FINDSET) THEN BEGIN
        REPEAT
          ConsumedWebServiceNamespace1.COPY(ConsumedWebServiceNamespace);
          ConsumedWebServiceNamespace1."Seq. No." := ConsumedWebServiceLine."Seq. No.";
          ConsumedWebServiceNamespace1.INSERT(TRUE);
        UNTIL (ConsumedWebServiceNamespace.NEXT = 0);
      END;
    END;

    PROCEDURE EditCustomBody@1100528300();
    VAR
      ConsumedWebServiceMgt@1100528300 : Codeunit 11229310;
    BEGIN
      ConsumedWebServiceMgt.EditSOAPBody(Rec);
    END;

    PROCEDURE InsertLine@1100525006(ConsumedWebService@1100525000 : Record 11229797;SeqNo@1100528301 : Integer;MethodName@1100525002 : Text[30];RequestRootTag@1100525003 : Text[50];ResponseRootTag@1100525004 : Text[50];PrefixMethodNamespace@1100525005 : Text[30];MethodNamespace@1100525006 : Text[250]);
    VAR
      ConsumedWebServiceLine@1100525001 : Record 11229798;
    BEGIN
      ConsumedWebServiceLine.INIT;
      ConsumedWebServiceLine."Web Service Code" := ConsumedWebService.Code;
      ConsumedWebServiceLine."Seq. No." := SeqNo;
      ConsumedWebServiceLine."Method Name" := MethodName;
      ConsumedWebServiceLine."Request Root Tag" := RequestRootTag;
      ConsumedWebServiceLine."Response Root Tag" := ResponseRootTag;
      ConsumedWebServiceLine."Prefix Method Namespace" := PrefixMethodNamespace;
      ConsumedWebServiceLine."Method Namespace" := MethodNamespace;
      ConsumedWebServiceLine.Active := TRUE;
      ConsumedWebServiceLine."Log Communication" := TRUE;
      ConsumedWebServiceLine.Description := STRSUBSTNO(Text002, ConsumedWebServiceLine."Method Name");
      ConsumedWebServiceLine.INSERT(TRUE);
    END;

    PROCEDURE DeleteLinesByCode@1100525013(ConsumedWebServiceCode@1100525000 : Code[20]);
    VAR
      ConsumedWebServiceLine@1100528300 : Record 11229798;
    BEGIN
      IF (ConsumedWebServiceCode = '') THEN
        EXIT;

      ConsumedWebServiceLine.SETRANGE("Web Service Code", ConsumedWebServiceCode);
      ConsumedWebServiceLine.DELETEALL;
    END;

    BEGIN
    END.
  }
}

