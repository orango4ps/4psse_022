OBJECT Codeunit 903 Release Assembly Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=900;
    Permissions=TableData 900=m,
                TableData 901=r;
    OnRun=VAR
            AssemblyLine@1001 : Record 901;
            InvtSetup@1000 : Record 313;
            WhseAssemblyRelease@1002 : Codeunit 904;
          BEGIN
            IF Status = Status::Released THEN
              EXIT;

            OnBeforeReleaseAssemblyDoc(Rec);

            AssemblyLine.SETRANGE("Document Type","Document Type");
            AssemblyLine.SETRANGE("Document No.","No.");
            AssemblyLine.SETFILTER(Type,'<>%1',AssemblyLine.Type::" ");
            AssemblyLine.SETFILTER(Quantity,'<>0');
            IF NOT AssemblyLine.FIND('-') THEN
              ERROR(Text001,"Document Type","No.");

            InvtSetup.GET;
            IF InvtSetup."Location Mandatory" THEN BEGIN
              AssemblyLine.SETRANGE(Type,AssemblyLine.Type::Item);
              IF AssemblyLine.FINDSET THEN
                REPEAT
                  IF AssemblyLine.IsInventoriableItem THEN
                    AssemblyLine.TESTFIELD("Location Code");
                UNTIL AssemblyLine.NEXT = 0;
            END;

            Status := Status::Released;
            MODIFY;

            IF "Document Type" = "Document Type"::Order THEN
              WhseAssemblyRelease.Release(Rec);

            //**4PS.sn
            IF "Plant Order No." <> '' THEN
              ReleasePlantOrder("Plant Order No.");
            //**4PS.en

            OnAfterReleaseAssemblyDoc(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst '@@@="%1 = Document Type, %2 = No.";ENU=There is nothing to release for %1 %2.;NOR=Det finnes ingenting † frigi for %1 %2.;SVE=Det finns inget att sl„ppa f”r %1 %2.';

    [External]
    PROCEDURE Reopen@1(VAR AssemblyHeader@1000 : Record 900);
    VAR
      WhseAssemblyRelease@1001 : Codeunit 904;
    BEGIN
      WITH AssemblyHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;

        OnBeforeReopenAssemblyDoc(AssemblyHeader);

        Status := Status::Open;
        MODIFY(TRUE);

        IF "Document Type" = "Document Type"::Order THEN
          WhseAssemblyRelease.Reopen(AssemblyHeader);

        OnAfterReopenAssemblyDoc(AssemblyHeader);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReleaseAssemblyDoc@2(VAR AssemblyHeader@1000 : Record 900);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReopenAssemblyDoc@5(VAR AssemblyHeader@1000 : Record 900);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReleaseAssemblyDoc@3(VAR AssemblyHeader@1000 : Record 900);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReopenAssemblyDoc@4(VAR AssemblyHeader@1000 : Record 900);
    BEGIN
    END;

    PROCEDURE ReleasePlantOrder@1000000019(PlantOrderNo@1000000000 : Code[20]);
    VAR
      PlantOrder@1100529601 : Record 11012556;
      ExtendedPlantOrderFunctions@1100529600 : Codeunit 11020503;
    BEGIN
      //**4PS DP01497
      IF PlantOrder.GET(PlantOrderNo) THEN
        IF PlantOrder.Status = PlantOrder.Status::Open THEN
          ExtendedPlantOrderFunctions.RunReleasePlantOrder(PlantOrder);
    END;

    BEGIN
    END.
  }
}

