OBJECT Table 11124913 Transport Cost Request WO
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckRequestStatus;
               IF "Line No." = 0 THEN
                 "Line No." := GetFreeLineNo;
               TESTFIELD("Line No.");
               "Created for Resource No." := '';
               "Created Transport Work Order" := '';
               "Collective List No.":= '';
             END;

    OnModify=BEGIN
               CheckRequestStatus;
             END;

    OnDelete=BEGIN
               CheckRequestStatus;
             END;

    OnRename=BEGIN
               CheckRequestStatus;
             END;

    CaptionML=ENU=Transport Cost (Request Workorder);
  }
  FIELDS
  {
    { 10  ;   ;Request No.         ;Code20        ;TableRelation="Resource Request";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Request Description", "Request Status");
                                                              END;

                                                   CaptionML=[ENU=Request No.;
                                                              NOR=Foresp›rselnr.;
                                                              SVE=Nr f”r f”rfr†gan];
                                                   NotBlank=Yes }
    { 20  ;   ;Work Order No.      ;Code20        ;TableRelation="Work Order" WHERE (Source Type=FILTER(Request),
                                                                                     Source No.=FIELD(Request No.),
                                                                                     Resource Type=CONST(Plant));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Work Order Description");
                                                                IF "Line No." = 0 THEN
                                                                  "Line No." := GetFreeLineNo;
                                                              END;

                                                   CaptionML=[ENU=Work Order No.;
                                                              SVE=Serviceordernr];
                                                   NotBlank=Yes }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer] }
    { 40  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=FILTER(<>Revenue));
                                                   OnValidate=VAR
                                                                DimensionValue@1100527200 : Record 349;
                                                                DimensionMgt@1100527201 : Codeunit 408;
                                                              BEGIN
                                                                IF "Cost Object" <> '' THEN BEGIN
                                                                  DimensionMgt.GetDimValueRec(2, "Cost Object", DimensionValue, TRUE, '');
                                                                  IF ("Cost Object" <> xRec."Cost Object") THEN BEGIN
                                                                    Description := DimensionValue.Name;
                                                                    VALIDATE("Unit of Measure", DimensionValue."Unit of Measure");
                                                                    VALIDATE("Unit Price", DimensionValue."Sales Price");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   CaptionClass='1,1,2' }
    { 50  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 60  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 70  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=Enhet] }
    { 80  ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                Amount := ROUND(Quantity * "Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              NOR=Pris;
                                                              SVE=Pris];
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 90  ;   ;Amount              ;Decimal       ;OnValidate=VAR
                                                                GLSetup@1100528300 : Record 98;
                                                              BEGIN
                                                                IF ("Unit Price" <> 0) THEN
                                                                  TESTFIELD(Quantity);
                                                                IF Quantity <> 0 THEN BEGIN
                                                                  "Unit Price" := Amount / Quantity
                                                                END ELSE
                                                                  "Unit Price" := Amount;
                                                                GLSetup.GET;
                                                                "Unit Price" := ROUND("Unit Price",GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 100 ;   ;Request Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource Request".Description WHERE (No.=FIELD(Request No.)));
                                                   CaptionML=[ENU=Request Description;
                                                              SVE=F”rfr†gan beskrivning];
                                                   Editable=No }
    { 101 ;   ;Request Status      ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource Request".Status WHERE (No.=FIELD(Request No.)));
                                                   CaptionML=ENU=Request Status;
                                                   OptionCaptionML=ENU=Open,,,,,,,,,,Finished;
                                                   OptionString=Open,,,,,,,,,,Finished;
                                                   Editable=No }
    { 110 ;   ;Work Order Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Work Order".Description WHERE (No.=FIELD(Work Order No.)));
                                                   CaptionML=ENU=Work Order Description;
                                                   Editable=No }
    { 120 ;   ;Created for Resource No.;Code20    ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Plant));
                                                   CaptionML=ENU=Created for Resource No.;
                                                   Description=Only filled/used if create from standard transport cost;
                                                   Editable=No }
    { 130 ;   ;Created Transport Work Order;Code20;TableRelation="Work Order";
                                                   CaptionML=ENU=Created Transport Work Order;
                                                   Description=Only filled/used if create from standard transport cost;
                                                   Editable=No }
    { 140 ;   ;Collective List No. ;Code20        ;TableRelation="Resource Req. Collective-List";
                                                   CaptionML=[ENU=Collective List No.;
                                                              NOR=Samlingsfaktura nr.;
                                                              SVE=Samlingsfaktura nr.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Request No.,Work Order No.,Line No.     ;Clustered=Yes }
    {    ;Work Order No.,Line No.                  }
    {    ;Collective List No.,Request No.          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE GetFreeLineNo@1100529001() : Integer;
    VAR
      TransportCostRequestWO@1100529000 : Record 11124913;
    BEGIN
      IF ("Request No." = '') OR ("Work Order No." = '') THEN
        EXIT(0);
      TransportCostRequestWO.SETRANGE("Request No.", "Request No.");
      TransportCostRequestWO.SETRANGE("Work Order No.", "Work Order No.");
      IF TransportCostRequestWO.FINDLAST THEN
        EXIT(TransportCostRequestWO."Line No." + 10);
      EXIT(10);
    END;

    LOCAL PROCEDURE CheckRequestStatus@1100529000();
    BEGIN
      CALCFIELDS("Request Status");
      IF "Request Status" >= "Request Status"::Finished THEN
        FIELDERROR("Request Status");
    END;

    BEGIN
    END.
  }
}

