OBJECT Table 289 Payment Method
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,NAVDK;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=VAR
               IdentityManagement@1000 : Codeunit 9801;
             BEGIN
               IF IdentityManagement.IsInvAppId THEN
                 IF NOT "Use for Invoicing" THEN
                   VALIDATE("Use for Invoicing",TRUE);

               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    OnDelete=VAR
               PaymentMethodTranslation@1000 : Record 466;
             BEGIN
               PaymentMethodTranslation.SETRANGE("Payment Method Code",Code);
               PaymentMethodTranslation.DELETEALL;
             END;

    OnRename=BEGIN
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    CaptionML=[ENU=Payment Method;
               NOR=Betalingsm†te;
               SVE=Betalningss„tt];
    LookupPageID=Page427;
    DrillDownPageID=Page427;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=[ENU=Bal. Account Type;
                                                              NOR=Motkontotype;
                                                              SVE=Motkontotyp];
                                                   OptionCaptionML=[ENU=G/L Account,Bank Account;
                                                                    NOR=Finanskonto,Bankkonto;
                                                                    SVE=Redov.konto,Bankkonto];
                                                   OptionString=G/L Account,Bank Account }
    { 4   ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account No." <> '' THEN
                                                                  TESTFIELD("Direct Debit",FALSE);
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=[ENU=Bal. Account No.;
                                                              NOR=Motkontonr.;
                                                              SVE=Motkonto] }
    { 6   ;   ;Direct Debit        ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Direct Debit" THEN
                                                                  "Direct Debit Pmt. Terms Code" := '';
                                                                IF "Direct Debit" THEN
                                                                  TESTFIELD("Bal. Account No.",'');
                                                              END;

                                                   CaptionML=[ENU=Direct Debit;
                                                              NOR=Direct Debit;
                                                              SVE=Autogiro] }
    { 7   ;   ;Direct Debit Pmt. Terms Code;Code10;TableRelation="Payment Terms";
                                                   OnValidate=BEGIN
                                                                IF "Direct Debit Pmt. Terms Code" <> '' THEN
                                                                  TESTFIELD("Direct Debit",TRUE);
                                                              END;

                                                   CaptionML=[ENU=Direct Debit Pmt. Terms Code;
                                                              NOR=Betalingsbet.kode for Direct Debit;
                                                              SVE=Betalningsvillkorskod f”r autogiro] }
    { 8   ;   ;Pmt. Export Line Definition;Code20 ;OnLookup=VAR
                                                              DataExchLineDef@1000 : Record 1227;
                                                              TempDataExchLineDef@1001 : TEMPORARY Record 1227;
                                                              DataExchDef@1002 : Record 1222;
                                                            BEGIN
                                                              DataExchLineDef.SETFILTER(Code,'<>%1','');
                                                              IF DataExchLineDef.FINDSET THEN BEGIN
                                                                REPEAT
                                                                  DataExchDef.GET(DataExchLineDef."Data Exch. Def Code");
                                                                  IF DataExchDef.Type = DataExchDef.Type::"Payment Export" THEN BEGIN
                                                                    TempDataExchLineDef.INIT;
                                                                    TempDataExchLineDef.Code := DataExchLineDef.Code;
                                                                    TempDataExchLineDef.Name := DataExchLineDef.Name;
                                                                    IF TempDataExchLineDef.INSERT THEN;
                                                                  END;
                                                                UNTIL DataExchLineDef.NEXT = 0;
                                                                IF PAGE.RUNMODAL(PAGE::"Pmt. Export Line Definitions",TempDataExchLineDef) = ACTION::LookupOK THEN
                                                                  "Pmt. Export Line Definition" := TempDataExchLineDef.Code;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Pmt. Export Line Definition;
                                                              NOR=Definisjon av betalingseksportlinje;
                                                              SVE=Definition f”r bet.exportrad] }
    { 9   ;   ;Bank Data Conversion Pmt. Type;Text50;
                                                   TableRelation="Bank Data Conversion Pmt. Type";
                                                   CaptionML=[ENU=Bank Data Conversion Pmt. Type;
                                                              NOR=Betalingstype for bankdatakonvertering;
                                                              SVE=Bet.typ f”r bankdatakonvertering] }
    { 10  ;   ;Use for Invoicing   ;Boolean       ;OnValidate=VAR
                                                                IdentityManagement@1000 : Codeunit 9801;
                                                              BEGIN
                                                                IF IdentityManagement.IsInvAppId THEN
                                                                  IF NOT "Use for Invoicing" THEN
                                                                    ERROR(UseForInvoicingErr);
                                                              END;

                                                   CaptionML=[ENU=Use for Invoicing;
                                                              NOR=Bruk til fakturering;
                                                              SVE=Anv„nd f”r fakturering] }
    { 11  ;   ;Last Modified Date Time;DateTime   ;CaptionML=[ENU=Last Modified Date Time;
                                                              NOR=Dato/klokkeslett for siste endring;
                                                              SVE=Senast „ndrad, datum och tid];
                                                   Editable=No }
    { 8000;   ;Id                  ;GUID          ;CaptionML=[ENU=Id;
                                                              NOR=ID;
                                                              SVE=ID] }
    { 13600;  ;Payment Channel     ;Option        ;OnValidate=BEGIN
                                                                IF "Payment Channel" = "Payment Channel"::"Payment Slip" THEN
                                                                  ERROR(Text13600, FIELDCAPTION("Payment Channel"),"Payment Channel");
                                                              END;

                                                   CaptionML=[ENU=Payment Channel;
                                                              SVE=Betalningskanal];
                                                   OptionCaptionML=[ENU=" ,Payment Slip,Account Transfer,National Clearing,Direct Debit";
                                                                    SVE=" ,Talong,Konto”verf”ring,Bank”verf”ring,Autogiro"];
                                                   OptionString=[ ,Payment Slip,Account Transfer,National Clearing,Direct Debit];
                                                   Description=NAVDK (13600) }
    { 13601;  ;Payment Type Validation;Option     ;CaptionML=ENU=Payment Type Validation;
                                                   OptionCaptionML=ENU=" ,FIK 71,FIK 73,FIK 01,FIK 04,Domestic,International";
                                                   OptionString=[ ,FIK 71,FIK 73,FIK 01,FIK 04,Domestic,International];
                                                   Description=NAVDK (13601) }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Code,Description                         }
  }
  CODE
  {
    VAR
      UseForInvoicingErr@1000 : TextConst 'ENU=The Use for Invoicing property must be set to true in the Invoicing App.;NOR=Egenskapen Bruk til fakturering m† settes tilÿTrue i Microsoft Invoicing.';
      Text13600@13600 : TextConst 'ENU=%1 %2 is not supported in this version of OIOUBL.;SVE=%1 %2 st”ds inte i denna version av OIOXML.';

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    [External]
    PROCEDURE TranslateDescription@3(Language@1001 : Code[10]);
    VAR
      PaymentMethodTranslation@1003 : Record 466;
    BEGIN
      IF PaymentMethodTranslation.GET(Code,Language) THEN
        VALIDATE(Description,COPYSTR(PaymentMethodTranslation.Description,1,MAXSTRLEN(Description)));
    END;

    [External]
    PROCEDURE GetDescriptionInCurrentLanguage@1() : Text[100];
    VAR
      Language@1000 : Record 8;
      PaymentMethodTranslation@1001 : Record 466;
    BEGIN
      IF PaymentMethodTranslation.GET(Code,Language.GetUserLanguage) THEN
        EXIT(PaymentMethodTranslation.Description);

      EXIT(Description);
    END;

    BEGIN
    END.
  }
}

