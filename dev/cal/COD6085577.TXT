OBJECT Codeunit 6085577 Document Importer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.05;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            CODC.IsCompanyActive(TRUE);
            CODC.GetNewDocuments;
            LoadLocalFiles('');
            ImportDocuments;
          END;

  }
  CODE
  {
    VAR
      CODC@1000 : Codeunit 6085617;
      DocModMgt@1160040004 : Codeunit 6085613;
      XmlLib@1000000020 : Codeunit 6085573;
      DialogMgt@1160040007 : Codeunit 6085633;
      Window@161024014 : Dialog;
      Text001@6085580 : TextConst 'DEU=OCR-Dateien werden importiert\Belegkategorie #1######################\Status @2@@@@@@@@@@@@@@@@@@@@@@;ENU=Importing Files\Document Category #1######################\Status            @2@@@@@@@@@@@@@@@@@@@@@@;NLD=Importeren bestanden\Documentcategorie #1######################\Status            @2@@@@@@@@@@@@@@@@@@@@@@';
      Text002@161024016 : TextConst 'DEU=Keine Dateien importiert.;ENU=No files imported.;NLD=Er zijn geen bestanden geimporteerd';
      Text003@161024012 : TextConst 'DEU=%1 Belege wurden in andere Unternehmen verschoben.;ENU=%1 documents have been moved to other companies.;NLD=%1 documenten zijn verplaatst naar andere bedrijven.';
      Text004@161024015 : TextConst 'DEU=%1 Dateien wurden importiert.;ENU=%1 files have been imported.;NLD=%1 bestanden zijn geimporteerd.';
      Text005@1160040001 : TextConst 'DEU=Fr die folgenden Belegkategorien wurden benutzerdefinierte Importe durchgefhrt: %1;ENU=Custom imports have been run for the following document categories: %1;NLD=Maatwerkimports zijn uitgevoerd voor de volgende documentcategorie‰n: %1';
      Text006@1160040002 : TextConst 'DEU=1 Datei wurde importiert.;ENU=1 file has been imported.;NLD=1 bestand ge‹mporteerd.';
      Text007@1160040005 : TextConst 'DEU=Dateien aus derselben E-Mail werden zusammengefgt;ENU=Merging files from same E-mail;NLD=Samenvoegen van bestanden uit dezelfde E-mail';
      MovedFiles@1160040006 : Integer;
      Text008@1160040000 : TextConst 'DEU=Dateien werden von Continia Online heruntergeladen;ENU=Importing File...;NLD=Importeren bestand?';
      MovedFilesToUIC@1160040003 : Integer;

    PROCEDURE ImportDocument@1160040002(DocCatCode@1160040003 : Code[20];Path@1160040002 : Text[1024];Filename@1160040001 : Text[100]) DocNo : Code[20];
    VAR
      Document@1160040005 : Record 6085590;
      MessageText@1160040000 : Text[1024];
      Status@1160040004 : 'File Imported,File Already Imported,Do Not Import';
    BEGIN
      Window.OPEN(Text008);

      Status := ImportDocument2(DocCatCode,Path,Filename,DocNo);

      IF Status = Status::"File Imported" THEN BEGIN
        Document.GET(DocNo);
        ProcessDocument(Document,FALSE);
      END;

      MessageText := STRSUBSTNO(Text006);

      IF MovedFiles > 0 THEN
        MessageText := MessageText + '\\' + STRSUBSTNO(Text003,MovedFiles);

      Window.CLOSE;

      MESSAGE(MessageText);
    END;

    PROCEDURE ImportDocuments@1000000000();
    VAR
      DCSetup@1160040001 : Record 6085573;
      DocCat@161024016 : Record 6085575;
      Document@161024020 : Record 6085590;
      TempDoc@1160040006 : TEMPORARY Record 6085756;
      CaptureMgt@1160040003 : Codeunit 6085576;
      TempFileStorage@1000 : Codeunit 6085665;
      DocNo@161024021 : Code[20];
      FileName@161024015 : Text[50];
      FileArray@1160040008 : ARRAY [10000] OF Text[1024];
      Path@1000000004 : Text[1024];
      DestPath@161024019 : Text[1024];
      MessageText@1160040004 : Text[1024];
      CustomMsg@1160040005 : Text[1024];
      CustomMsgTmp@1160040010 : Text[1024];
      FileCount@6085573 : Integer;
      i@1160040009 : Integer;
      Count@161024013 : Integer;
      ImportedFiles@161024012 : Integer;
      RecordCountOK@1160040007 : Integer;
      Status@1160040002 : 'File Imported,File Already Imported,Do Not Import';
    BEGIN
      DCSetup.GET;
      IF DCSetup."Document Storage Type" = DCSetup."Document Storage Type"::"File System" THEN
        DCSetup.TESTFIELD("Archive File Path");
      DCSetup.IsUICSetupConsistent(TRUE);

      TempFileStorage.SetDeleteProhibited := TRUE;

      Document.LOCKTABLE;

      IF GUIALLOWED THEN
        DialogMgt.OpenWindow(Text001);

      DocModMgt.SetHideWindow(TRUE);

      DocCat.SETRANGE("Drag and Drop Category",FALSE);
      IF DocCat.FINDSET THEN
        REPEAT
          IF GUIALLOWED THEN
            IF DocCat.Description <> '' THEN
              DialogMgt.UpdateWindow(1,DocCat.Description)
            ELSE
              DialogMgt.UpdateWindow(1,DocCat.Code);

          DocCat.UpdateCounters;

          IF DocCat."Codeunit ID: Import Files" IN [0,CODEUNIT::"Document Importer"] THEN BEGIN
            IF DocCat."Archive E-mails on Document" THEN
              IF DCSetup."Document Storage Type" = DCSetup."Document Storage Type"::"File System" THEN
                DCSetup.TESTFIELD("Archive File Path");

            Path := DocCat.GetCategoryPath(2);
            IF TempFileStorage.DirectoryExists(Path) THEN BEGIN
              FileCount := TempFileStorage.GetFilesInDir2(Path,'*.tiff',FileArray);

              FOR i := 1 TO FileCount DO BEGIN
                IF GUIALLOWED THEN
                  DialogMgt.UpdateWindowDec(2,CalcProgress(FileCount,i));

                FileName := DELCHR(COPYSTR(FileArray[i],STRLEN(Path)+2),'=','.tiff');
                Status := ImportDocument2(DocCat.Code,Path,FileName,DocNo);
                Document.GET(DocNo);

                IF Status = Status::"File Imported" THEN BEGIN
                  IF NOT IsMergeEmail(TempDoc,Document) THEN
                    ProcessDocument(Document,FALSE);

                  ImportedFiles += 1;
                END;
              END;
            END;
          END ELSE BEGIN
            DialogMgt.SetDCDocImport(TRUE);
            CODEUNIT.RUN(DocCat."Codeunit ID: Import Files");
            DialogMgt.SetDCDocImport(FALSE);
            DialogMgt.GetRecordCount(RecordCountOK);
            ImportedFiles += RecordCountOK;

            CustomMsgTmp := DialogMgt.GetCustomMsg;
            IF CustomMsgTmp <> '' THEN
              IF CustomMsg = '' THEN
                CustomMsg := CustomMsgTmp
              ELSE
                CustomMsg := CustomMsg + '\\' + CustomMsgTmp;
          END;
        UNTIL DocCat.NEXT = 0;

      IF GUIALLOWED THEN
        DialogMgt.CloseWindow;

      MergeAndProcessEmail(TempDoc);

      TempFileStorage.SetDeleteProhibited := FALSE;
      TempFileStorage.Clear;

      IF GUIALLOWED THEN BEGIN
        IF ImportedFiles > 0 THEN BEGIN
          IF ImportedFiles > 1 THEN
            MessageText := STRSUBSTNO(Text004,ImportedFiles)
          ELSE
            MessageText := STRSUBSTNO(Text006);
        END ELSE
          MessageText := Text002;

        IF MovedFiles > 0 THEN
          MessageText := MessageText + '\\' + STRSUBSTNO(Text003,MovedFiles);

        IF CustomMsg <> '' THEN
          MessageText := MessageText + '\\' + CustomMsg;

        MESSAGE(MessageText);
      END;
    END;

    LOCAL PROCEDURE ImportDocument2@1160040001(DocCatCode@1160040003 : Code[20];Path@1160040002 : Text[1024];Filename@1160040001 : Text[100];VAR DocNo@1160040000 : Code[20]) : Integer;
    VAR
      Document@1160040004 : Record 6085590;
      DocCat@1160040006 : Record 6085575;
      TempFile@1001 : TEMPORARY Record 6085608;
      TempFileStorage@1000 : Codeunit 6085665;
      Status@1160040007 : 'File Imported,File Already Imported,Do Not Import';
    BEGIN
      DocCat.GET(DocCatCode);

      Status := ImportDocumentData(DocCat.Code,Path,Filename,DocNo);
      Document.GET(DocNo);

      IF Status IN [Status::"File Imported",Status::"File Already Imported"] THEN BEGIN
        TempFileStorage.GetFile(Path + '\' + Filename + '.tiff',TempFile);
        Document.SetTiffFile(TempFile);

        TempFileStorage.GetFile(Path + '\' + Filename + '.pdf',TempFile);
        Document.SetPdfFile(TempFile);

        IF TempFileStorage.FileExists(Path + '\' + Filename + '.eml') THEN
          IF DocCat."Archive E-mails on Document" THEN BEGIN
            TempFileStorage.GetFile(Path + '\' + Filename + '.eml',TempFile);
            Document.SetEmailFile(TempFile);
          END;

        COMMIT; // Make sure that the document is committed before we move around with the files.
        TempFileStorage.DeleteFile(Path + '\' + Filename + '.eml');
        TempFileStorage.DeleteFile(Path + '\' + Filename + '.tiff');
        TempFileStorage.DeleteFile(Path + '\' + Filename + '.pdf');
        TempFileStorage.DeleteFile(Path + '\' + Filename + '.xml');

        IF COPYSTR(Filename,1,3) = 'CO-' THEN
          CODC.UpdateDocument(DocCat,COPYSTR(Filename,4));
      END;

      EXIT(Status);
    END;

    LOCAL PROCEDURE ImportDocumentData@1000000001(DocCatCode@1000000003 : Code[20];Path@1000000000 : Text[1024];Filename@1000000001 : Text[100];VAR DocNo@1000000018 : Code[20]) : Integer;
    VAR
      DocCat@1160040000 : Record 6085575;
      Document@1000000002 : Record 6085590;
      Page@1000000012 : Record 6085591;
      TempWord@1000000022 : TEMPORARY Record 6085592;
      Word@1000000013 : Record 6085592;
      TempFileStorage@1003 : Codeunit 6085665;
      DateParserMgt@1160040002 : Codeunit 6085586;
      CryptographyMgt@1160040003 : Codeunit 6085599;
      XmlDoc@1000000004 : Codeunit 6192800;
      EmailNode@1160040001 : Codeunit 6192801;
      WordNodeList@1001 : Codeunit 6192802;
      BarcodeNodeList@1002 : Codeunit 6192802;
      PagesNodeList@1000000005 : Codeunit 6192802;
      PageNode@1000000007 : Codeunit 6192801;
      Node@1000 : Codeunit 6192801;
      ReadStream@1004 : InStream;
      OCRProcessedDateTimeUTC@1160040005 : Text[250];
      i@1000000009 : Integer;
      Status@1160040004 : 'File Imported,File Already Imported,Do Not Import';
    BEGIN
      Document.SETCURRENTKEY(Filename);
      Document.SETRANGE(Filename,Filename);
      IF Document.FINDFIRST THEN BEGIN
        DocNo := Document."No.";
        EXIT(Status::"File Already Imported");
      END;

      TempFileStorage.GetFileReadStream(Path + '\' + Filename + '.xml',ReadStream);
      XmlDoc.LoadFromStream(ReadStream);

      XmlDoc.GetDocumentElement(Node);
      Node.SelectSingleNode(PageNode,'Pages');
      PageNode.SelectNodes(PagesNodeList,'Page');
      FOR i := 0 TO PagesNodeList.Count - 1 DO BEGIN
        PagesNodeList.GetItem(PageNode,i);
        IF PageNode.SelectNodes(WordNodeList,'Words/Word') THEN
          LoadWordsToTempTable(i+1,Word.Type::Text,WordNodeList,TempWord);
        IF PageNode.SelectNodes(BarcodeNodeList,'Barcodes/Barcode') THEN
          LoadWordsToTempTable(i+1,Word.Type::Barcode,BarcodeNodeList,TempWord);
      END;

      DocCat.GET(DocCatCode);

      DocNo := '';

      Document.INIT;
      Document."No." := '';
      Document.INSERT(TRUE);
      Document."Document Category Code" := DocCat.Code;
      Document.Filename := Filename;
      XmlDoc.GetDocumentElement(Node);

      OCRProcessedDateTimeUTC := XmlLib.GetNodeText(Node,'OCRProcessedDateTimeUTC');
      IF OCRProcessedDateTimeUTC <> '' THEN BEGIN
        DateParserMgt.ParseUTC(OCRProcessedDateTimeUTC);
        Document."OCR Process Completed" := DateParserMgt.GetDatetime;
      END;
      Document."PDF File Hash" := CryptographyMgt.GetFileSHA1Hash(STRSUBSTNO('%1\%2.pdf',Path,Filename));

      XmlDoc.GetDocumentElement(Node);
      IF Node.SelectSingleNode(EmailNode,'Email') THEN BEGIN
        Document."From E-Mail Address" := COPYSTR(XmlLib.GetNodeText(EmailNode,'FromAddress'),1,
          MAXSTRLEN(Document."From E-Mail Address"));
        Document."From E-Mail Name" := COPYSTR(XmlLib.GetNodeText(EmailNode,'FromName'),1,MAXSTRLEN(Document."From E-Mail Name"));
        Document."To E-Mail Address" := COPYSTR(XmlLib.GetNodeText(EmailNode,'ToAddress'),1,MAXSTRLEN(Document."To E-Mail Address"));
        Document."To E-Mail Name" := COPYSTR(XmlLib.GetNodeText(EmailNode,'ToName'),1,MAXSTRLEN(Document."To E-Mail Name"));
        DateParserMgt.ParseUTC(XmlLib.GetNodeText(EmailNode,'Date'));
        Document."E-Mail Date" :=  DateParserMgt.GetDatetime;
        Document."E-Mail Inbox Subfolder" :=
          COPYSTR(XmlLib.GetNodeText(EmailNode,'InboxSubfolder'),1,MAXSTRLEN(Document."E-Mail Inbox Subfolder"));
        Document."E-Mail Subject" := COPYSTR(XmlLib.GetNodeText(EmailNode,'Subject'),1,MAXSTRLEN(Document."E-Mail Subject"));
        IF DocCat."Archive E-mails on Document" THEN
          Document."E-Mail GUID" := XmlLib.GetNodeGuid(EmailNode,'Guid');
      END;
      Document.MODIFY(TRUE);

      FOR i := 0 TO PagesNodeList.Count - 1 DO BEGIN
        PagesNodeList.GetItem(PageNode,i);
        Page.INIT;
        Page."Document No." := Document."No.";
        Page."Page No." := i + 1;
        Page."Original Filename" := COPYSTR(XmlLib.GetNodeText(PageNode,'OriginalFilename'),1,MAXSTRLEN(Page."Original Filename"));
        Page.Height := XmlLib.Text2Int(XmlLib.GetNodeText(PageNode,'Height'));
        Page.Width := XmlLib.Text2Int(XmlLib.GetNodeText(PageNode,'Width'));

        CASE XmlLib.GetNodeText(PageNode,'TiffColorMode') OF
          'BlackWhite':
            Page."TIFF Image Color Mode" := Page."TIFF Image Color Mode"::"Black & White";
          'Gray':
            Page."TIFF Image Color Mode" := Page."TIFF Image Color Mode"::Gray;
          'Color':
            Page."TIFF Image Color Mode" := Page."TIFF Image Color Mode"::Color;
        END;
        Page."TIFF Image Resolution" := XmlLib.Text2Int(XmlLib.GetNodeText(PageNode,'TiffResolution'));

        Page.INSERT(TRUE);

        TempWord.RESET;
        TempWord.SETRANGE("Page No.",Page."Page No.");
        IF TempWord.FINDSET THEN
          REPEAT
            Word.TRANSFERFIELDS(TempWord);
            Word."Document No." := Document."No.";
            Word."Page No." := Page."Page No.";
            Word."Entry No." := 0;
            Word.INSERT(TRUE);

            IF Word.Bottom > Page."Bottom Word Pos." THEN
              Page."Bottom Word Pos." := Word.Bottom;
          UNTIL TempWord.NEXT = 0;

        Page.MODIFY;
      END;

      CLEAR(XmlDoc);

      DocNo := Document."No.";
      EXIT(Status::"File Imported");
    END;

    PROCEDURE LoadLocalFiles@1(FileName@6085573 : Text[1024]);
    VAR
      DocCat@1000 : Record 6085575;
      TempFile@1008 : TEMPORARY Record 6085608;
      FileInfo@1006 : Codeunit 6085666;
      FileSysMgt@1002 : Codeunit 6085584;
      TempFileStorage@1007 : Codeunit 6085665;
      Path@1001 : Text[250];
      FileArray@1004 : ARRAY [10000] OF Text[1024];
      FileCount@1003 : Integer;
      i@1005 : Integer;
    BEGIN
      DocCat.SETRANGE("Drag and Drop Category",FALSE);
      IF DocCat.FINDSET THEN
        REPEAT
          Path := DocCat.GetCategoryPath(2);
          IF FileName <> '' THEN
            FileCount := FileSysMgt.GetFilesInDir2(Path,STRSUBSTNO('%1.*',FileName),FileArray)
          ELSE
            FileCount := FileSysMgt.GetFilesInDir2(Path,'*.*',FileArray);

          FOR i := 1 TO FileCount DO
            IF FileInfo.GetFileExtension(FileArray[i]) IN ['tiff','pdf','xml','eml'] THEN BEGIN
              TempFile.CreateFromFileIfExist(FileArray[i],FALSE);
              TempFileStorage.AddFile(TempFile);
            END;
        UNTIL DocCat.NEXT = 0;
    END;

    LOCAL PROCEDURE LoadWordsToTempTable@1000000005(PageNo@1000000001 : Integer;Type@1000000005 : Integer;NodeList@1000000000 : Codeunit 6192802;VAR TempWord@1000000002 : Record 6085592);
    VAR
      Node@1000000003 : Codeunit 6192801;
      i@1000000004 : Integer;
    BEGIN
      FOR i := 0 TO NodeList.Count - 1 DO BEGIN
        NodeList.GetItem(Node,i);

        TempWord.INIT;
        TempWord."Page No." := PageNo;
        TempWord."Entry No." := TempWord."Entry No." + 1;
        TempWord.Type := Type;
        TempWord.Word := COPYSTR(XmlLib.GetNodeText(Node,'Text'),1,MAXSTRLEN(TempWord.Word));
        TempWord."Barcode Type" := XmlLib.GetNodeText(Node,'Type');
        TempWord.Top := XmlLib.Text2Int(XmlLib.GetNodeText(Node,'Top'));
        TempWord.Left := XmlLib.Text2Int(XmlLib.GetNodeText(Node,'Left'));
        TempWord.Bottom := XmlLib.Text2Int(XmlLib.GetNodeText(Node,'Bottom'));
        TempWord.Right := XmlLib.Text2Int(XmlLib.GetNodeText(Node,'Right'));
        TempWord.INSERT;
      END;
    END;

    LOCAL PROCEDURE CalcProgress@161024017(VAR TotalCount@161024012 : Integer;VAR Index@161024013 : Integer) : Integer;
    BEGIN
      EXIT(ROUND(Index / TotalCount * 10000,1,'>'));
    END;

    LOCAL PROCEDURE ProcessDocument@1160040007(Document@1160040001 : Record 6085590;SkipAutoSplit@1160040005 : Boolean);
    VAR
      DocCat@1160040002 : Record 6085575;
      Document2@1160040000 : Record 6085590;
      Page@1160040003 : Record 6085591;
      AutoSplitPage@1160040009 : TEMPORARY Record 6085766;
      DocMoveToComp@1160040006 : Codeunit 6085758;
      CaptureEngine@1160040004 : Codeunit 6085575;
      TargetCompany@1160040007 : Text[250];
    BEGIN
      IF NOT SkipAutoSplit THEN
        DocModMgt.AutoSplitDocument(Document,AutoSplitPage);

      DocCat.GET(Document."Document Category Code");

      IF DocCat."Auto Move to Company" THEN BEGIN
        TargetCompany := DocMoveToComp.IdentifyTargetCompany(Document,DocCat);
        CASE TargetCompany OF
          COMPANYNAME:
            ; // leave the document in the current company
          '':
            BEGIN
              DocMoveToComp.MoveDocToUICDoc(Document);
              MovedFilesToUIC := MovedFilesToUIC + 1;
            END;
          ELSE
            BEGIN
              DocMoveToComp.MoveDocument(Document,TargetCompany);
              MovedFiles := MovedFiles + 1;
            END;
        END;

        IF AutoSplitPage.FINDSET THEN
          REPEAT
            Document2.GET(AutoSplitPage."Document No.");

            TargetCompany := DocMoveToComp.IdentifyTargetCompany(Document2,DocCat);
            CASE TargetCompany OF
              COMPANYNAME:
                ; // leave the document in the current company
              '':
                BEGIN
                  DocMoveToComp.MoveDocToUICDoc(Document2);
                  MovedFilesToUIC := MovedFilesToUIC + 1;
                END;
              ELSE
                BEGIN
                  DocMoveToComp.MoveDocument(Document2,TargetCompany);
                  MovedFiles := MovedFiles + 1;
                END;
            END;
          UNTIL AutoSplitPage.NEXT = 0;
      END;

      IF Document.FIND('=') THEN BEGIN
        CaptureEngine.SetHideWindow(TRUE);
        CaptureEngine.CaptureDocument(Document);
      END;

      IF AutoSplitPage.FINDSET THEN
        REPEAT
          IF Document2.GET(AutoSplitPage."Document No.") THEN
            CaptureEngine.CaptureDocument(Document2);
        UNTIL AutoSplitPage.NEXT = 0;

      COMMIT;

      IF DocCat."Register Documents Automatic." THEN BEGIN
        IF Document.FIND('=') THEN BEGIN
          Document."Batch Register" := TRUE;
          Document.RegisterHideErrors;
        END;

        IF AutoSplitPage.FINDSET THEN
          REPEAT
            IF Document2.GET(AutoSplitPage."Document No.") THEN BEGIN
              Document2."Batch Register" := TRUE;
              Document2.RegisterHideErrors;
            END;
          UNTIL AutoSplitPage.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE MergeAndProcessEmail@1160040013(VAR TempDoc@1160040000 : TEMPORARY Record 6085756);
    VAR
      Document@1160040001 : Record 6085590;
      DocModMgt@1160040003 : Codeunit 6085613;
      PrevEmailGUID@1160040004 : GUID;
      EmptyGUID@1160040002 : GUID;
    BEGIN
      IF TempDoc.ISEMPTY THEN
        EXIT;

      IF GUIALLOWED THEN
        Window.OPEN(Text007);

      IF TempDoc.FINDSET THEN
        REPEAT
          IF Document.GET(TempDoc."No.") THEN
            DocModMgt.MergeEmail(Document);

          ProcessDocument(Document,TRUE);
        UNTIL TempDoc.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;
    END;

    LOCAL PROCEDURE IsMergeEmail@1160040014(VAR TempDoc@1160040001 : TEMPORARY Record 6085756;VAR Document@1160040002 : Record 6085590) : Boolean;
    VAR
      IsEmpty@1160040004 : Boolean;
      EntryNo@1160040005 : Integer;
      EmptyGUID@1160040000 : GUID;
    BEGIN
      IF Document."E-Mail GUID" = EmptyGUID THEN
        EXIT;

      TempDoc.SETRANGE("E-Mail GUID",Document."E-Mail GUID");
      IsEmpty := TempDoc.ISEMPTY;
      TempDoc.RESET;
      IF NOT IsEmpty THEN
        EXIT(TRUE);

      IF NOT DocModMgt.IsMergeEmail(Document) THEN
        EXIT(FALSE);

      IF TempDoc.FINDLAST THEN
        EntryNo := TempDoc."Entry No." + 1
      ELSE
        EntryNo := 1;

      TempDoc.INIT;
      TempDoc."Entry No." := EntryNo;
      TempDoc."No." := Document."No.";
      TempDoc."E-Mail GUID" := Document."E-Mail GUID";
      TempDoc.INSERT;

      EXIT(TRUE);
    END;

    PROCEDURE GetDocInfo@1160040000(Filename@1160040004 : Text[1024];VAR FromEMailAddr@1160040003 : Text[200];VAR EMailSubject@1160040002 : Text[200];VAR EMailDate@1160040001 : DateTime;VAR OCRProcessed@1160040000 : DateTime);
    VAR
      DateParserMgt@1160040005 : Codeunit 6085586;
      XmlDoc@1160040007 : Codeunit 6192800;
      EmailNode@1160040006 : Codeunit 6192801;
      DocumentElement@1001 : Codeunit 6192801;
      FileSysMgt@1002 : Codeunit 6085584;
      OCRProcessedDateTimeUTC@1000 : Text[30];
    BEGIN
      IF NOT FileSysMgt.FileExists(Filename) THEN
        EXIT;

      XmlDoc.Load(Filename);

      XmlDoc.GetDocumentElement(DocumentElement);
      OCRProcessedDateTimeUTC := XmlLib.GetNodeText(DocumentElement,'OCRProcessedDateTimeUTC');
      IF OCRProcessedDateTimeUTC <> '' THEN BEGIN
        DateParserMgt.ParseUTC(OCRProcessedDateTimeUTC);
        OCRProcessed := DateParserMgt.GetDatetime;
      END;

      XmlDoc.GetDocumentElement(DocumentElement);

      IF DocumentElement.SelectSingleNode(EmailNode,'Email') THEN BEGIN
        FromEMailAddr := COPYSTR(XmlLib.GetNodeText(EmailNode,'FromAddress'),1,MAXSTRLEN(FromEMailAddr));

        DateParserMgt.ParseUTC(XmlLib.GetNodeText(EmailNode,'Date'));
        EMailDate :=  DateParserMgt.GetDatetime;

        EMailSubject := COPYSTR(XmlLib.GetNodeText(EmailNode,'Subject'),1,MAXSTRLEN(EMailSubject));
      END;
    END;

    BEGIN
    END.
  }
}

