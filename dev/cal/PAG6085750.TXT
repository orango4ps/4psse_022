OBJECT Page 6085750 Approval Group Card Subpage
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.5.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Genehmigungsgruppenkarte Unterseite;
               ENU=Approval Group Card Subpage;
               NLD=Goedkeuringsgroepen-kaart subkaart];
    SourceTable=Table348;
    PageType=ListPart;
    OnAfterGetRecord=VAR
                       AppvlGroupDim@161024012 : Record 6085751;
                     BEGIN
                       IsActive := AppvlGroupDim.GET(CurrAppvlGroupCode,Code);
                       IsMandatory := AppvlGroupDim.Mandatory;
                     END;

  }
  CONTROLS
  {
    { 161024012;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 161024013;1;Group   ;
                Name=Group;
                GroupType=Repeater }

    { 161024014;2;Field   ;
                Name=Code;
                SourceExpr=Code }

    { 161024015;2;Field   ;
                Name=Name;
                SourceExpr=Name }

    { 161024016;2;Field   ;
                Name=Active;
                CaptionML=[DEU=Aktiv;
                           ENU=Active;
                           NLD=Actief;
                           SVE=Aktiv];
                SourceExpr=IsActive;
                OnValidate=VAR
                             AppvlGroupDim@161024012 : Record 6085751;
                           BEGIN
                             IF IsActive THEN BEGIN
                               IF NOT AppvlGroupDim.GET(CurrAppvlGroupCode,Code) THEN BEGIN
                                 AppvlGroupDim."Approval Group Code" := CurrAppvlGroupCode;
                                 AppvlGroupDim."Dimension Code" := Code;
                                 AppvlGroupDim.INSERT;
                               END;
                             END ELSE BEGIN
                               IF AppvlGroupDim.GET(CurrAppvlGroupCode,Code) THEN BEGIN
                                 AppvlGroupDim.DELETE;
                                 IsMandatory := FALSE;
                               END;
                             END;
                           END;
                            }

    { 161024017;2;Field   ;
                Name=Mandatory;
                CaptionML=[DEU=Notwendig;
                           ENU=Mandatory;
                           NLD=Verplicht;
                           SVE=Obligatoriskt];
                SourceExpr=IsMandatory;
                OnValidate=VAR
                             AppvlGroupDim@161024012 : Record 6085751;
                           BEGIN
                             IF NOT IsActive THEN
                               ERROR(Text001);

                             IF AppvlGroupDim.GET(CurrAppvlGroupCode,Code) THEN BEGIN
                               AppvlGroupDim.Mandatory := IsMandatory;
                               AppvlGroupDim.MODIFY;
                             END;
                           END;
                            }

  }
  CODE
  {
    VAR
      CurrAppvlGroupCode@161024014 : Code[10];
      IsActive@161024012 : Boolean;
      IsMandatory@161024013 : Boolean;
      Text001@161024015 : TextConst 'DEU=Dimension muss aktiviert sein;ENU=Dimension must be actived;NLD=Dimensie moet worden goedgekeurd';

    PROCEDURE SetAppvlGroupCode@161024012(NewAppvlGroupCode@161024012 : Code[10]);
    BEGIN
      CurrAppvlGroupCode := NewAppvlGroupCode;
    END;

    PROCEDURE UpdatePage@161024013();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

