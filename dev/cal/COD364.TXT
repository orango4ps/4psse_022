OBJECT Codeunit 364 PostPurch-Delete
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 120=i,
                TableData 121=rid,
                TableData 122=ri,
                TableData 123=rid,
                TableData 124=i,
                TableData 125=rid,
                TableData 6650=i,
                TableData 6651=rid;
    OnRun=BEGIN
            //**4PS.sn
            // Added insert permissions for tables 120, 121, 122, 123, 124, 125, 6650 and 6651.
            // This has already been solved by Microsoft in a next roll-up.
            //**4PS.en
          END;

  }
  CODE
  {
    VAR
      ItemTrackingMgt@1004 : Codeunit 6500;
      MoveEntries@1005 : Codeunit 361;
      DocumentDeletionErr@1006 : TextConst '@@@=%1 - Posting Date;ENU=You cannot delete posted purchase documents that are posted after %1. \\The date is defined by the Allow Document Deletion Before field in the Purchases & Payables Setup window.;NOR=Du kan ikke slette bokf›rte kj›psdokumenter som bokf›res etter %1. \\Datoen blir definert av feltet Tillat sletting av dokument f›r i vinduet Kj›psoppsett.;SVE=Du kan inte ta bort bokf”rda ink”psdokument som har bokf”rts efter %1. \\Datumet definieras i f„ltet Till†t borttag av dokument f”re i f”nstret Ink”psinst„llningar.';

    [External]
    PROCEDURE DeleteHeader@18(PurchHeader@1000 : Record 38;VAR PurchRcptHeader@1001 : Record 120;VAR PurchInvHeader@1002 : Record 122;VAR PurchCrMemoHdr@1003 : Record 124;VAR ReturnShptHeader@1004 : Record 6650;VAR PurchInvHeaderPrepmt@1006 : Record 122;VAR PurchCrMemoHdrPrepmt@1005 : Record 124);
    VAR
      PurchInvLine@1007 : Record 123;
      PurchCrMemoLine@1008 : Record 125;
      PurchRcptLine@1009 : Record 121;
      ReturnShptLine@1012 : Record 6651;
      SourceCode@1010 : Record 230;
      SourceCodeSetup@1011 : Record 242;
    BEGIN
      WITH PurchHeader DO BEGIN
        SourceCodeSetup.GET;
        SourceCodeSetup.TESTFIELD("Deleted Document");
        SourceCode.GET(SourceCodeSetup."Deleted Document");

        InitDeleteHeader(
          PurchHeader,PurchRcptHeader,PurchInvHeader,PurchCrMemoHdr,
          ReturnShptHeader,PurchInvHeaderPrepmt,PurchCrMemoHdrPrepmt,SourceCode.Code);
        IF PurchRcptHeader."No." <> '' THEN BEGIN
          PurchRcptHeader.INSERT;
          PurchRcptLine.INIT;
          PurchRcptLine."Document No." := PurchRcptHeader."No.";
          PurchRcptLine."Line No." := 10000;
          PurchRcptLine.Description := SourceCode.Description;
          PurchRcptLine.INSERT;
        END;

        IF ReturnShptHeader."No." <> '' THEN BEGIN
          ReturnShptHeader.INSERT;
          ReturnShptLine.INIT;
          ReturnShptLine."Document No." := ReturnShptHeader."No.";
          ReturnShptLine."Line No." := 10000;
          ReturnShptLine.Description := SourceCode.Description;
          ReturnShptLine.INSERT;
        END;

        IF PurchInvHeader."No." <> '' THEN BEGIN
          PurchInvHeader.INSERT;
          PurchInvLine.INIT;
          PurchInvLine."Document No." := PurchInvHeader."No.";
          PurchInvLine."Line No." := 10000;
          PurchInvLine.Description := SourceCode.Description;
          PurchInvLine.INSERT;
        END;

        IF PurchCrMemoHdr."No." <> '' THEN BEGIN
          PurchCrMemoHdr.INSERT(TRUE);
          PurchCrMemoLine.INIT;
          PurchCrMemoLine."Document No." := PurchCrMemoHdr."No.";
          PurchCrMemoLine."Line No." := 10000;
          PurchCrMemoLine.Description := SourceCode.Description;
          PurchCrMemoLine.INSERT;
        END;

        IF PurchInvHeaderPrepmt."No." <> '' THEN BEGIN
          PurchInvHeaderPrepmt.INSERT;
          PurchInvLine."Document No." := PurchInvHeaderPrepmt."No.";
          PurchInvLine."Line No." := 10000;
          PurchInvLine.Description := SourceCode.Description;
          PurchInvLine.INSERT;
        END;

        IF PurchCrMemoHdrPrepmt."No." <> '' THEN BEGIN
          PurchCrMemoHdrPrepmt.INSERT;
          PurchCrMemoLine.INIT;
          PurchCrMemoLine."Document No." := PurchCrMemoHdrPrepmt."No.";
          PurchCrMemoLine."Line No." := 10000;
          PurchCrMemoLine.Description := SourceCode.Description;
          PurchCrMemoLine.INSERT;
        END;
      END;

      OnAfterDeleteHeader(
        PurchHeader,PurchRcptHeader,PurchInvHeader,PurchCrMemoHdr,ReturnShptHeader,PurchInvHeaderPrepmt,PurchCrMemoHdrPrepmt);
    END;

    [External]
    PROCEDURE DeletePurchRcptLines@1(PurchRcptHeader@1000 : Record 120);
    VAR
      PurchRcptLine@1001 : Record 121;
      lPurchHeader@1210190000 : Record 38;
      OrderNotClosed@1210190001 : Boolean;
    BEGIN
      //**4PS.sn
      IF lPurchHeader.GET(lPurchHeader."Document Type"::Order,PurchRcptHeader."Order No.") THEN
        OrderNotClosed := lPurchHeader.Status < lPurchHeader.Status::Closed
      ELSE
        lPurchHeader.INIT;
      //**4PS.en

      PurchRcptLine.SETRANGE("Document No.",PurchRcptHeader."No.");
      IF PurchRcptLine.FIND('-') THEN
        REPEAT
          PurchRcptLine.TESTFIELD("Quantity Invoiced",PurchRcptLine.Quantity);
          //**4PS.sn
          IF OrderNotClosed THEN
            IF lPurchHeader."Amounts only" THEN
              PurchRcptLine.TESTFIELD("Amnt. Rcd. Not Invoiced",0)
            ELSE
          //**4PS.en
              PurchRcptLine.TESTFIELD("Quantity Invoiced",PurchRcptLine.Quantity);
          OnBeforeDeletePurchRcptLines(PurchRcptLine);
          PurchRcptLine.DELETE;
        UNTIL PurchRcptLine.NEXT = 0;
      ItemTrackingMgt.DeleteItemEntryRelation(
        DATABASE::"Purch. Rcpt. Line",0,PurchRcptHeader."No.",'',0,0,TRUE);

      MoveEntries.MoveDocRelatedEntries(DATABASE::"Purch. Rcpt. Header",PurchRcptHeader."No.");
    END;

    [External]
    PROCEDURE DeletePurchInvLines@2(PurchInvHeader@1000 : Record 122);
    VAR
      PurchInvLine@1001 : Record 123;
    BEGIN
      PurchInvLine.SETRANGE("Document No.",PurchInvHeader."No.");
      IF PurchInvLine.FIND('-') THEN
        REPEAT
          OnBeforeDeletePurchInvLines(PurchInvLine);
          PurchInvLine.DELETE;
          ItemTrackingMgt.DeleteValueEntryRelation(PurchInvLine.RowID1);
        UNTIL PurchInvLine.NEXT = 0;

      MoveEntries.MoveDocRelatedEntries(DATABASE::"Purch. Inv. Header",PurchInvHeader."No.");
    END;

    [External]
    PROCEDURE DeletePurchCrMemoLines@3(PurchCrMemoHeader@1000 : Record 124);
    VAR
      PurchCrMemoLine@1001 : Record 125;
    BEGIN
      PurchCrMemoLine.SETRANGE("Document No.",PurchCrMemoHeader."No.");
      IF PurchCrMemoLine.FIND('-') THEN
        REPEAT
          OnBeforeDeletePurchCrMemoLines(PurchCrMemoLine);
          PurchCrMemoLine.DELETE;
        UNTIL PurchCrMemoLine.NEXT = 0;
      ItemTrackingMgt.DeleteItemEntryRelation(
        DATABASE::"Purch. Cr. Memo Line",0,PurchCrMemoHeader."No.",'',0,0,TRUE);

      MoveEntries.MoveDocRelatedEntries(DATABASE::"Purch. Cr. Memo Hdr.",PurchCrMemoHeader."No.");
    END;

    [External]
    PROCEDURE DeletePurchShptLines@5800(ReturnShptHeader@1000 : Record 6650);
    VAR
      ReturnShipmentLine@1001 : Record 6651;
    BEGIN
      ReturnShipmentLine.SETRANGE("Document No.",ReturnShptHeader."No.");
      IF ReturnShipmentLine.FIND('-') THEN
        REPEAT
          OnBeforeDeletePurchShptLines(ReturnShipmentLine);
          ReturnShipmentLine.TESTFIELD("Quantity Invoiced",ReturnShipmentLine.Quantity);
          ReturnShipmentLine.DELETE;
        UNTIL ReturnShipmentLine.NEXT = 0;
      ItemTrackingMgt.DeleteItemEntryRelation(
        DATABASE::"Return Shipment Line",0,ReturnShptHeader."No.",'',0,0,TRUE);

      MoveEntries.MoveDocRelatedEntries(DATABASE::"Return Shipment Header",ReturnShptHeader."No.");
    END;

    [External]
    PROCEDURE InitDeleteHeader@19(PurchHeader@1000 : Record 38;VAR PurchRcptHeader@1001 : Record 120;VAR PurchInvHeader@1002 : Record 122;VAR PurchCrMemoHdr@1003 : Record 124;VAR ReturnShptHeader@1004 : Record 6650;VAR PurchInvHeaderPrepmt@1006 : Record 122;VAR PurchCrMemoHdrPrepmt@1005 : Record 124;SourceCode@1008 : Code[10]);
    VAR
      PurchSetup@1007 : Record 312;
    BEGIN
      OnBeforeInitDeleteHeader(
        PurchHeader,PurchRcptHeader,PurchInvHeader,PurchCrMemoHdr,ReturnShptHeader,PurchInvHeaderPrepmt,PurchCrMemoHdrPrepmt,
        SourceCode);

      WITH PurchHeader DO BEGIN
        CLEAR(PurchRcptHeader);
        CLEAR(PurchInvHeader);
        CLEAR(PurchCrMemoHdr);
        CLEAR(ReturnShptHeader);
        PurchSetup.GET;

        IF ("Receiving No. Series" <> '') AND ("Receiving No." <> '') THEN BEGIN
          PurchRcptHeader.TRANSFERFIELDS(PurchHeader);
          PurchRcptHeader."No." := "Receiving No.";
          PurchRcptHeader."Posting Date" := TODAY;
          PurchRcptHeader."User ID" := USERID;
          PurchRcptHeader."Created on" := TODAY; //**4PS.n
          PurchRcptHeader."Source Code" := SourceCode;
        END;

        IF ("Return Shipment No. Series" <> '') AND ("Return Shipment No." <> '') THEN BEGIN
          ReturnShptHeader.TRANSFERFIELDS(PurchHeader);
          ReturnShptHeader."No." := "Return Shipment No.";
          ReturnShptHeader."Posting Date" := TODAY;
          ReturnShptHeader."User ID" := USERID;
          ReturnShptHeader."Source Code" := SourceCode;
        END;

        IF ("Posting No. Series" <> '') AND
           (("Document Type" IN ["Document Type"::Order,"Document Type"::Invoice]) AND
            ("Posting No." <> '') OR
            ("Document Type" = "Document Type"::Invoice) AND
            ("No. Series" = "Posting No. Series"))
        THEN BEGIN
          PurchInvHeader.TRANSFERFIELDS(PurchHeader);
          IF "Posting No." <> '' THEN
            PurchInvHeader."No." := "Posting No.";
          IF "Document Type" = "Document Type"::Invoice THEN BEGIN
            PurchInvHeader."Pre-Assigned No. Series" := "No. Series";
            PurchInvHeader."Pre-Assigned No." := "No.";
          END ELSE BEGIN
            PurchInvHeader."Pre-Assigned No. Series" := '';
            PurchInvHeader."Pre-Assigned No." := '';
            PurchInvHeader."Order No. Series" := "No. Series";
            PurchInvHeader."Order No." := "No.";
          END;
          PurchInvHeader."Posting Date" := TODAY;
          PurchInvHeader."User ID" := USERID;
          PurchInvHeader."Source Code" := SourceCode;
          //**4PS.sn
          PurchInvHeader."Amount incl. VAT" := 0; //Call C018927
          PurchInvHeader."On Hold" := '';         //Call C018927
          //**4PS.en
        END;

        IF ("Posting No. Series" <> '') AND
           (("Document Type" IN ["Document Type"::"Return Order","Document Type"::"Credit Memo"]) AND
            ("Posting No." <> '') OR
            ("Document Type" = "Document Type"::"Credit Memo") AND
            ("No. Series" = "Posting No. Series"))
        THEN BEGIN
          PurchCrMemoHdr.TRANSFERFIELDS(PurchHeader);
          IF "Posting No." <> '' THEN
            PurchCrMemoHdr."No." := "Posting No.";
          PurchCrMemoHdr."Pre-Assigned No. Series" := "No. Series";
          PurchCrMemoHdr."Pre-Assigned No." := "No.";
          PurchCrMemoHdr."Posting Date" := TODAY;
          PurchCrMemoHdr."User ID" := USERID;
          PurchCrMemoHdr."Source Code" := SourceCode;
          //**4PS.sn
          PurchCrMemoHdr."Amount incl. VAT" := 0; //Call C018927
          PurchCrMemoHdr."On Hold" := '';         //Call C018927
          //**4PS.en
        END;

        IF ("Prepayment No. Series" <> '') AND ("Prepayment No." <> '') THEN BEGIN
          TESTFIELD("Document Type","Document Type"::Order);
          PurchInvHeaderPrepmt.TRANSFERFIELDS(PurchHeader);
          PurchInvHeaderPrepmt."No." := "Prepayment No.";
          PurchInvHeaderPrepmt."Order No. Series" := "No. Series";
          PurchInvHeaderPrepmt."Prepayment Order No." := "No.";
          PurchInvHeaderPrepmt."Posting Date" := TODAY;
          PurchInvHeaderPrepmt."Pre-Assigned No. Series" := '';
          PurchInvHeaderPrepmt."Pre-Assigned No." := '';
          PurchInvHeaderPrepmt."User ID" := USERID;
          PurchInvHeaderPrepmt."Source Code" := SourceCode;
          PurchInvHeaderPrepmt."Prepayment Invoice" := TRUE;
        END;

        IF ("Prepmt. Cr. Memo No. Series" <> '') AND ("Prepmt. Cr. Memo No." <> '') THEN BEGIN
          TESTFIELD("Document Type","Document Type"::Order);
          PurchCrMemoHdrPrepmt.TRANSFERFIELDS(PurchHeader);
          PurchCrMemoHdrPrepmt."No." := "Prepmt. Cr. Memo No.";
          PurchCrMemoHdrPrepmt."Prepayment Order No." := "No.";
          PurchCrMemoHdrPrepmt."Posting Date" := TODAY;
          PurchCrMemoHdrPrepmt."Pre-Assigned No. Series" := '';
          PurchCrMemoHdrPrepmt."Pre-Assigned No." := '';
          PurchCrMemoHdrPrepmt."User ID" := USERID;
          PurchCrMemoHdrPrepmt."Source Code" := SourceCode;
          PurchCrMemoHdrPrepmt."Prepayment Credit Memo" := TRUE;
        END;
      END;

      OnAfterInitDeleteHeader(
        PurchHeader,PurchRcptHeader,PurchInvHeader,PurchCrMemoHdr,ReturnShptHeader,PurchInvHeaderPrepmt,PurchCrMemoHdrPrepmt);
    END;

    [External]
    PROCEDURE IsDocumentDeletionAllowed@4(PostingDate@1001 : Date);
    VAR
      PurchSetup@1000 : Record 312;
    BEGIN
      PurchSetup.GET;
      PurchSetup.TESTFIELD("Allow Document Deletion Before");
      IF PostingDate >= PurchSetup."Allow Document Deletion Before" THEN
        ERROR(DocumentDeletionErr,PurchSetup."Allow Document Deletion Before");
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterDeleteHeader@9(VAR PurchaseHeader@1000 : Record 38;VAR PurchRcptHeader@1001 : Record 120;VAR PurchInvHeader@1002 : Record 122;VAR PurchCrMemoHdr@1003 : Record 124;VAR ReturnShipmentHeader@1004 : Record 6650;VAR PurchInvHeaderPrepmt@1005 : Record 122;VAR PurchCrMemoHdrPrepmt@1006 : Record 124);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitDeleteHeader@11(VAR PurchHeader@1007 : Record 38;VAR PurchRcptHeader@1006 : Record 120;VAR PurchInvHeader@1005 : Record 122;VAR PurchCrMemoHdr@1004 : Record 124;VAR ReturnShptHeader@1003 : Record 6650;VAR PurchInvHeaderPrepmt@1002 : Record 122;VAR PurchCrMemoHdrPrepmt@1001 : Record 124);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeletePurchRcptLines@5(PurchRcptLine@1000 : Record 121);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeletePurchInvLines@6(PurchInvLine@1000 : Record 123);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeletePurchCrMemoLines@7(PurchCrMemoLine@1000 : Record 125);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeletePurchShptLines@8(ReturnShptLine@1000 : Record 6651);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInitDeleteHeader@10(VAR PurchHeader@1007 : Record 38;VAR PurchRcptHeader@1006 : Record 120;VAR PurchInvHeader@1005 : Record 122;VAR PurchCrMemoHdr@1004 : Record 124;VAR ReturnShptHeader@1003 : Record 6650;VAR PurchInvHeaderPrepmt@1002 : Record 122;VAR PurchCrMemoHdrPrepmt@1001 : Record 124;VAR SourceCode@1000 : Code[10]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

