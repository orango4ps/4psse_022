OBJECT Codeunit 11012351 Hyperlink Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            ActiveSession@1100525000 : Record 2000000110;
          BEGIN
            MESSAGE('%1', APPLICATIONPATH);

            ActiveSession.SETRANGE("Session ID", SESSIONID);
            ActiveSession.FINDFIRST;
            MESSAGE(FORMAT(ActiveSession));
          END;

  }
  CODE
  {
    VAR
      ServerEnvironmentManagement@1100525000 : Codeunit 11012284;

    PROCEDURE CreatePageURL@1100525000(PageID@1100525001 : Integer;Bookmark@1100525002 : RecordRef) : Text;
    VAR
      ActiveSession@1100525000 : Record 2000000110;
      Port@1100525003 : Integer;
    BEGIN
      ActiveSession.SETRANGE("Session ID", SESSIONID);
      IF NOT ActiveSession.FINDFIRST THEN
        EXIT;

      Port := ServerEnvironmentManagement.GetClientServicesPort;

      EXIT(
        STRSUBSTNO('DynamicsNAV://%1:%2/%3/%4/runpage?page=%5&bookmark=%6&mode=View',
          ActiveSession."Server Computer Name",
          Port,
          ActiveSession."Server Instance Name",
          STRSUBSTNO('%1', COMPANYNAME),
          PageID,
          FORMAT(Bookmark.RECORDID, 0, 10)));
    END;

    PROCEDURE CreatePageURLWithFilter@1100525004(VAR Filter@1100525000 : RecordRef) : Text;
    VAR
      ActiveSession@1100525002 : Record 2000000110;
      Port@1100525001 : Integer;
    BEGIN
      ActiveSession.SETRANGE("Session ID", SESSIONID);
      IF NOT ActiveSession.FINDFIRST THEN
        EXIT;

      Port := ServerEnvironmentManagement.GetClientServicesPort;

      EXIT(
        STRSUBSTNO('DynamicsNAV://%1:%2/%3/%4/runreport?report=%5%6',
          ActiveSession."Server Computer Name",
          Port,
          ActiveSession."Server Instance Name",
          STRSUBSTNO('%1', COMPANYNAME),
          REPORT::"Hyperlink Page",
          CreateURLFilter(Filter)));
    END;

    PROCEDURE CreateReportURL@1100525003(ReportID@1100525001 : Integer;VAR Filter@1100525000 : RecordRef) : Text;
    VAR
      ActiveSession@1100525003 : Record 2000000110;
      Port@1100525002 : Integer;
    BEGIN
      ActiveSession.SETRANGE("Session ID", SESSIONID);
      IF NOT ActiveSession.FINDFIRST THEN
        EXIT;

      Port := ServerEnvironmentManagement.GetClientServicesPort;

      EXIT(
        STRSUBSTNO('DynamicsNAV://%1:%2/%3/%4/runreport?report=%5%6',
          ActiveSession."Server Computer Name",
          Port,
          ActiveSession."Server Instance Name",
          STRSUBSTNO('%1', COMPANYNAME),
          ReportID,
          CreateURLFilter(Filter)));
    END;

    PROCEDURE CreateURLFilter@1100525002(RecRef@1100525002 : RecordRef) Result : Text;
    VAR
      TableFilter@1100525000 : Text;
      FldRef@1100525001 : FieldRef;
      FieldNumber@1100525004 : Text;
      FieldNo@1100525005 : Integer;
    BEGIN
      TableFilter := RecRef.GETVIEW(FALSE);
      IF TableFilter = '' THEN
        EXIT;

      IF STRPOS(UPPERCASE(TableFilter), 'WHERE') = 0 THEN
        EXIT;

      TableFilter := COPYSTR(TableFilter, STRPOS(UPPERCASE(TableFilter), 'WHERE'));
      REPEAT
        FieldNumber := COPYSTR(TableFilter, STRPOS(UPPERCASE(TableFilter), 'FIELD') + 5, STRPOS(TableFilter, '=') - (STRPOS(UPPERCASE(TableFilter), 'FIELD') + 5));
        EVALUATE(FieldNo, FieldNumber);
        FldRef := RecRef.FIELD(FieldNo);

        TableFilter := COPYSTR(TableFilter, STRPOS(TableFilter, '=') + 1);
        Result := Result + '&filter=%22' + RecRef.NAME + '%22.' + '%22' + FldRef.NAME + '%22:' + FldRef.GETFILTER;
      UNTIL STRPOS(UPPERCASE(TableFilter), 'FIELD') = 0;
    END;

    BEGIN
    END.
  }
}

