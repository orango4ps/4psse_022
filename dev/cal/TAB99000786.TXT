OBJECT Table 99000786 Routing Version
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Version Code" = '' THEN BEGIN
                 RoutingHeader.GET("Routing No.");
                 RoutingHeader.TESTFIELD("Version Nos.");
                 NoSeriesMgt.InitSeries(RoutingHeader."Version Nos.",xRec."No. Series",0D,VersionCode,"No. Series");
                 IF STRLEN(VersionCode) > MAXSTRLEN("Version Code") THEN
                   ERROR(Text000,
                     FIELDCAPTION("Version Code"),
                     NoSeriesLine.FIELDCAPTION("Starting No."),
                     RoutingHeader."Version Nos.",
                     NoSeries.TABLECAPTION,
                     MAXSTRLEN("Version Code"));

                 "Version Code" := VersionCode;
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               RtngLine@1000 : Record 99000764;
             BEGIN
               RtngLine.LOCKTABLE;
               RtngLine.SETRANGE("Routing No.","Routing No.");
               RtngLine.SETRANGE("Version Code","Version Code");
               RtngLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               IF Status = Status::Certified THEN
                 ERROR(Text001,TABLECAPTION,FIELDCAPTION(Status),FORMAT(Status));
             END;

    CaptionML=[DEU=Arbeitsplanversion;
               ENU=Routing Version;
               NLD=Bewerkingsplanversie;
               NOR=Ruteversjon;
               SVE=Oper.f”ljdsversion];
    LookupPageID=Page99000808;
    DrillDownPageID=Page99000808;
  }
  FIELDS
  {
    { 1   ;   ;Routing No.         ;Code20        ;TableRelation="Routing Header";
                                                   CaptionML=[DEU=Arbeitsplannr.;
                                                              ENU=Routing No.;
                                                              NLD=Bew.-plannr.;
                                                              NOR=Rutenr.;
                                                              SVE=Operationsf”ljdsnr];
                                                   NotBlank=Yes }
    { 2   ;   ;Version Code        ;Code20        ;CaptionML=[DEU=Versionscode;
                                                              ENU=Version Code;
                                                              NLD=Versiecode;
                                                              NOR=Versjonskode;
                                                              SVE=Versionskod] }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 10  ;   ;Starting Date       ;Date          ;CaptionML=[DEU=Startdatum;
                                                              ENU=Starting Date;
                                                              NLD=Begindatum;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 20  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                RoutingHeader@1000 : Record 99000763;
                                                                SkipCommit@1001 : Boolean;
                                                              BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN BEGIN
                                                                  RoutingHeader.GET("Routing No.");
                                                                  CheckRouting.Calculate(RoutingHeader,"Version Code");
                                                                END;
                                                                MODIFY(TRUE);

                                                                SkipCommit := FALSE;
                                                                OnValidateStatusBeforeCommit(Rec,SkipCommit);
                                                                IF NOT SkipCommit THEN
                                                                  COMMIT;
                                                              END;

                                                   CaptionML=[DEU=Status;
                                                              ENU=Status;
                                                              NLD=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[DEU=Neu,Zertifiziert,In Entwicklung,Abgelaufen;
                                                                    ENU=New,Certified,Under Development,Closed;
                                                                    NLD=Nieuw,Gecertificeerd,In ontwikkeling,Afgesloten;
                                                                    NOR=Ny,Sertifisert,Under utvikling,Lukket;
                                                                    SVE=Ny,Godk„nd,Under utveckling,Avslutad];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Status = Status::Certified THEN
                                                                  FIELDERROR(Status);
                                                              END;

                                                   CaptionML=[DEU=Art;
                                                              ENU=Type;
                                                              NLD=Soort;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[DEU=Seriell,Parallel;
                                                                    ENU=Serial,Parallel;
                                                                    NLD=Serieel,Parallel;
                                                                    NOR=Seriell,Parallell;
                                                                    SVE=Seriell,Parallell];
                                                   OptionString=Serial,Parallel }
    { 22  ;   ;Last Date Modified  ;Date          ;CaptionML=[DEU=Korrigiert am;
                                                              ENU=Last Date Modified;
                                                              NLD=Gewijzigd op;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 50  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Nummernserie;
                                                              ENU=No. Series;
                                                              NLD=Nr.-reeks;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Routing No.,Version Code                ;Clustered=Yes }
    {    ;Routing No.,Starting Date                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoSeries@1007 : Record 308;
      NoSeriesLine@1006 : Record 309;
      RoutingHeader@1000 : Record 99000763;
      RtngVersion@1001 : Record 99000786;
      CheckRouting@1002 : Codeunit 99000752;
      NoSeriesMgt@1003 : Codeunit 396;
      Text000@1008 : TextConst 'DEU=Der neue %1 kann nicht nach den Standards erstellt werden,\da die %2 fr  %3 %4 mehr als %5 Zeichen enth„lt.;ENU=The new %1 cannot be generated by default\because the %2 for %3 %4 contains more than %5 characters.;NLD=De nieuwe %1 kan standaard niet worden gemaakt\omdat %2 voor %4 %3 meer dan %5 tekens bevat.;NOR=Den nye %1 kan ikke opprettes som standard\fordi %2 for %3 %4 inneholder mer enn %5 tegn.;SVE=Den nya %1 kan inte genereras automatiskt\d„rf”r att %2 f”r %3 %4 inneh†ller mer „n %5 tecken.';
      VersionCode@1005 : Code[20];
      Text001@1004 : TextConst 'DEU=Sie k”nnen den %1 nicht umbenennen, wenn der %2 %3 ist.;ENU=You cannot rename the %1 when %2 is %3.;NLD=U kunt de %1 niet hernoemen als %2 %3 is.;NOR=Du kan ikke gi %1 nytt navn n†r %2 er %3.;SVE=Du kan inte byta namn p† %1 n„r %2 „r %3.';

    [External]
    PROCEDURE AssistEdit@2(OldRoutVersion@1000 : Record 99000786) : Boolean;
    BEGIN
      WITH RtngVersion DO BEGIN
        RtngVersion := Rec;
        RoutingHeader.GET("Routing No.");
        RoutingHeader.TESTFIELD("Version Nos.");
        IF NoSeriesMgt.SelectSeries(RoutingHeader."Version Nos.",OldRoutVersion."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries(VersionCode);
          IF STRLEN(VersionCode) > MAXSTRLEN("Version Code") THEN
            ERROR(Text000,
              FIELDCAPTION("Version Code"),
              NoSeriesLine.FIELDCAPTION("Starting No."),
              RoutingHeader."Version Nos.",
              NoSeries.TABLECAPTION,
              MAXSTRLEN("Version Code"));

          "Version Code" := VersionCode;
          Rec := RtngVersion;
          EXIT(TRUE);
        END;
      END;
    END;

    [External]
    PROCEDURE Caption@1() : Text;
    VAR
      RtngHeader@1000 : Record 99000763;
    BEGIN
      IF GETFILTERS = '' THEN
        EXIT('');

      IF "Routing No." = '' THEN
        EXIT('');

      RtngHeader.GET("Routing No.");
      EXIT(
        STRSUBSTNO(
          '%1 %2 %3',RtngHeader."No.",RtngHeader.Description,"Version Code"));
    END;

    [Integration]
    LOCAL PROCEDURE OnValidateStatusBeforeCommit@3(VAR RoutingVersion@1000 : Record 99000786;VAR SkipCommit@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

