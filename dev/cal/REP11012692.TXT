OBJECT Report 11012692 Update Actual Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aktuelles Budget Åbernehmen;
               ENU=Update Actual Budget;
               NLD=Doorvoeren actueel budget;
               NOR=Oppdater reelt budsjett];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   IF LinesInserted > 0 THEN
                     MESSAGE(Text002, YearBudget.TABLECAPTION, FORMAT(gYearBudget) + ' ' + gBudgetName);
                 END;

    UseRequestPage=No;
  }
  DATASET
  {
    { 5773;    ;DataItem;                    ;
               DataItemTable=Table11012692;
               DataItemTableView=SORTING(Year Budget,Budget Name);
               OnPreDataItem=BEGIN
                               YearBudget.GET(gYearBudget,gBudgetName);
                               GlBudgetEntry.SETRANGE("Budget Name",gBudgetName);
                               GlBudgetEntry.SETRANGE(Date,YearBudget."Start Date",YearBudget."End Date");
                               IF GlBudgetEntry.FIND('-') THEN
                                 IF CONFIRM(STRSUBSTNO(Text000,gBudgetName,YearBudget."Start Date",YearBudget."End Date")) THEN
                                   GlBudgetEntry.DELETEALL;
                               GlBudgetEntry.RESET;

                               SETRANGE("Year Budget", gYearBudget);
                               SETRANGE("Budget Name", gBudgetName);
                             END;

               OnAfterGetRecord=VAR
                                  YearBudgetAllocation@1100525001 : Record 11012694;
                                  YearBudgetAllocationLine@1100525000 : Record 11012693;
                                  YearBudgetAccount@1100525002 : Record 11012691;
                                BEGIN
                                  IF Allocation = '' THEN BEGIN
                                    YearBudgetAccount.GET("Year Budget","Budget Name","G/L Account");
                                    IF YearBudgetAccount.Allocation = '' THEN
                                      ERROR(STRSUBSTNO(Text001,"G/L Account"))
                                    ELSE
                                      YearBudgetAllocation.GET("Year Budget","Budget Name",YearBudgetAccount.Allocation);
                                  END ELSE
                                    YearBudgetAllocation.GET("Year Budget","Budget Name",Allocation);

                                  YearBudgetAllocationLine.SETRANGE("Year Budget",YearBudgetAllocation."Year Budget");
                                  YearBudgetAllocationLine.SETRANGE("Budget Name",YearBudgetAllocation."Budget Name");
                                  YearBudgetAllocationLine.SETRANGE(Allocation,YearBudgetAllocation.Allocation);
                                  YearBudgetAllocationLine.FIND('-');
                                  REPEAT
                                    YearBudgetAllocationLine.TESTFIELD("Start Date");
                                    YearBudgetAllocationLine.CALCFIELDS("Total Number of Points");

                                    GlBudgetEntry.INIT;
                                    GlBudgetEntry."Entry No." := 0;
                                    GlBudgetEntry."Budget Name" := "Budget Name";
                                    GlBudgetEntry."G/L Account No." := "G/L Account";
                                    GlBudgetEntry.Date := YearBudgetAllocationLine."Start Date";
                                    GlBudgetEntry."Global Dimension 1 Code" := "Department Code";
                                    GlBudgetEntry.Amount := ("Budget Amount"/YearBudgetAllocationLine."Total Number of Points") * YearBudgetAllocationLine."Number of Points";
                                    GlBudgetEntry.Description := Description;
                                    GlBudgetEntry."User ID" := USERID;

                                    GetDimensions;

                                    GlBudgetEntry.INSERT(TRUE);
                                    LinesInserted := LinesInserted + 1;
                                  UNTIL YearBudgetAllocationLine.NEXT = 0;
                                END;

               ReqFilterFields=Year Budget,Budget Name }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLSetup@1100525000 : Record 98;
      GlBudgetEntry@1210190000 : Record 96;
      YearBudget@1210190003 : Record 11012690;
      gYearBudget@1210190001 : Integer;
      gBudgetName@1210190002 : Code[20];
      Text000@1210190004 : TextConst 'DEU=Budget bereis fÅr Budgetname %1 von %2 bis %3 vorhanden. Mîchten Sie lîschen?;ENU=There''s already a budget for budgetname %1 from %2 until %3, remove this budget?;NLD=Reeds budget aanwezig voor Budgetnaam %1 van %2 tot %3, verwijderen ?;NOR=Det finnes allerede et budsjett for budsjettnavnet %1 fra %2 til %3. Vil du fjerne dette budsjettet?';
      Text001@1210190006 : TextConst 'DEU=Kein VerteilungsschlÅssel fÅr Budgetzeile, Kontonr. %1 vorhanden;ENU=No dividingkey present for budgetline, accountno. %1;NLD=Geen verdeelsleutel aanwezig voor budgetregel, rekeningnr. %1;NOR=Det finnes ingen delenõkkel for budsjettrad, kontonr %1';
      LinesInserted@1100485000 : Integer;
      Text002@1100485001 : TextConst 'DEU=%1 %2 ist in Ist-Budget umgesetzt worden;ENU=%1 %2 is transferred to Actual Budget.;NLD=%1 %2 is overgezet naar werkelijk budget.;NOR=%1 %2 er overfõrt til reelt budsjett.';
      GlobalDim1Code@1100525009 : Code[20];
      GlobalDim2Code@1100525008 : Code[20];
      BudgetDim1Code@1100525006 : Code[20];
      BudgetDim2Code@1100525005 : Code[20];
      BudgetDim3Code@1100525004 : Code[20];
      BudgetDim4Code@1100525003 : Code[20];

    PROCEDURE SetDefaults@1210190001(lYearBudget@1210190001 : Integer;lBudgetName@1210190000 : Code[20]);
    BEGIN
      gYearBudget := lYearBudget;
      gBudgetName := lBudgetName;
    END;

    PROCEDURE GetDimensions@1100525000();
    VAR
      GLBudgetName@1100525000 : Record 95;
      TempDimSetEntry@1100525003 : TEMPORARY Record 480;
      DimMgt@1100525002 : Codeunit 408;
    BEGIN
      IF "Year Budget Department"."Line No." <> 0 THEN BEGIN
        IF GLBudgetName.GET("Year Budget Department"."Budget Name") THEN BEGIN
          GLSetup.GET;
          GlobalDim1Code := GLSetup."Global Dimension 1 Code";
          GlobalDim2Code := GLSetup."Global Dimension 2 Code";
          BudgetDim1Code := GLBudgetName."Budget Dimension 1 Code";
          BudgetDim2Code := GLBudgetName."Budget Dimension 2 Code";
          BudgetDim3Code := GLBudgetName."Budget Dimension 3 Code";
          BudgetDim4Code := GLBudgetName."Budget Dimension 4 Code";
        END;

        DimMgt.GetDimensionSet(TempDimSetEntry,"Year Budget Department"."Dimension Set ID");

        IF TempDimSetEntry.FINDSET THEN
          REPEAT
            IF TempDimSetEntry."Dimension Value Code" <> '' THEN
              InsertGLBudgetDim(TempDimSetEntry."Dimension Code",TempDimSetEntry."Dimension Value Code");
           UNTIL TempDimSetEntry.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertGLBudgetDim@4(DimCode@1000 : Code[20];DimValCode@1001 : Code[20]);
    BEGIN
      CASE DimCode OF
        GlobalDim1Code: GlBudgetEntry."Global Dimension 1 Code" := DimValCode;
        GlobalDim2Code: GlBudgetEntry."Global Dimension 2 Code" := DimValCode;
        BudgetDim1Code: GlBudgetEntry."Budget Dimension 1 Code" := DimValCode;
        BudgetDim2Code: GlBudgetEntry."Budget Dimension 2 Code" := DimValCode;
        BudgetDim3Code: GlBudgetEntry."Budget Dimension 3 Code" := DimValCode;
        BudgetDim4Code: GlBudgetEntry."Budget Dimension 4 Code" := DimValCode;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

