OBJECT Table 11012077 Hour Portal Fields Templ Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Hour Portal Fields Templ Line;
  }
  FIELDS
  {
    { 10  ;   ;Template Code       ;Code20        ;CaptionML=[ENU=Template Code;
                                                              NOR=Malkode;
                                                              SVE=Mallkod];
                                                   NotBlank=Yes }
    { 20  ;   ;Table No.           ;Option        ;OnValidate=BEGIN
                                                                "Line No." := 0;
                                                                "WS Field Name" := '';
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr];
                                                   OptionCaptionML=ENU=" ,Hour Accounting Line,Site Mgt Hours Line";
                                                   OptionString=[ ,Hour Accounting Line,Site Mgt Hours Line];
                                                   NotBlank=Yes }
    { 30  ;   ;Line No.            ;Integer       ;OnValidate=VAR
                                                                Field@1100528300 : Record 2000000041;
                                                              BEGIN
                                                                CASE "Table No." OF
                                                                  "Table No."::"Hour Accounting Line":
                                                                    IF (NOT Field.GET(DATABASE::"Hour Accounting Line", "Line No.")) THEN Field.INIT;
                                                                  "Table No."::"Site Mgt Hours Line":
                                                                    IF (NOT Field.GET(DATABASE::"Site Mngmnt. Hours Line", "Line No.")) THEN Field.INIT;
                                                                  ELSE
                                                                    Field.INIT;
                                                                END;
                                                                "WS Field Name" := GetWSFieldName(Field.FieldName);
                                                              END;

                                                   OnLookup=VAR
                                                              FieldTableManagement@1100529200 : Codeunit 11012372;
                                                            BEGIN
                                                              CASE "Table No." OF
                                                                "Table No."::"Hour Accounting Line": BEGIN
                                                                  VALIDATE("Line No.",FieldTableManagement.LookupFieldNo(DATABASE::"Hour Accounting Line", "Line No."));
                                                                END;
                                                                "Table No."::"Site Mgt Hours Line": BEGIN
                                                                  VALIDATE("Line No.",FieldTableManagement.LookupFieldNo(DATABASE::"Site Mngmnt. Hours Line", "Line No."));
                                                                END;
                                                                ELSE
                                                                  FIELDERROR("Table No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 31  ;   ;WS Field Name       ;Text50        ;CaptionML=ENU=Web Service Field Name }
    { 33  ;   ;WS Seq. No.         ;Integer       ;CaptionML=ENU=Web Service Seq. No. }
    { 40  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::"Not Used": BEGIN
                                                                    Visible := FALSE;
                                                                    "Read Only" := FALSE;
                                                                  END;
                                                                  Type::Mandatory: BEGIN
                                                                    Visible := TRUE;
                                                                    "Read Only" := FALSE;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Type];
                                                   OptionCaptionML=ENU=Not Used,Optional,Mandatory;
                                                   OptionString=Not Used,Optional,Mandatory }
    { 50  ;   ;Visible             ;Boolean       ;CaptionML=[ENU=Visible;
                                                              NOR=Synlig;
                                                              SVE=Synlig] }
    { 60  ;   ;Read Only           ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Is Standard", FALSE);
                                                              END;

                                                   CaptionML=[ENU=Read Only;
                                                              NOR=Kun lese;
                                                              SVE=Read Only];
                                                   Editable=No }
    { 70  ;   ;Is Standard         ;Boolean       ;CaptionML=ENU=Is Standard;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Template Code,Table No.,Line No.        ;Clustered=Yes }
    {    ;Template Code,Table No.,WS Seq. No.      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetLastLineNo@1100528300() : Integer;
    VAR
      HourPortalFieldsTemplLine@1100528300 : Record 11012077;
    BEGIN
      HourPortalFieldsTemplLine.SETRANGE("Template Code", "Template Code");
      HourPortalFieldsTemplLine.SETRANGE("Table No.", "Table No.");
      IF (HourPortalFieldsTemplLine.FINDLAST) THEN
        EXIT(HourPortalFieldsTemplLine."Line No.");
      EXIT(0);
    END;

    LOCAL PROCEDURE GetWSFieldName@1100525001(FieldName@1100525000 : Text) : Text;
    BEGIN
      CASE "Table No." OF
        "Table No."::"Hour Accounting Line":
          FieldName := GetWSFieldNameHourAccLine(FieldName);
        "Table No."::"Site Mgt Hours Line":
          FieldName := GetWSFieldNameSiteMgtLine(FieldName);
      END;

      EXIT(DELCHR(CONVERTSTR(FieldName, ' ', '_'), '<=>', '.'));
    END;

    LOCAL PROCEDURE GetWSFieldNameSiteMgtLine@1100525002(FieldName@1100525000 : Text) : Text;
    BEGIN
      CASE FieldName OF
        'Object Name':
          EXIT('Object Description');
      END;
      EXIT(FieldName);
    END;

    LOCAL PROCEDURE GetWSFieldNameHourAccLine@1100525003(FieldName@1100525000 : Text) : Text;
    BEGIN
      EXIT(FieldName);
    END;

    BEGIN
    END.
  }
}

