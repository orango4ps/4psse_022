OBJECT Report 11125353 Send Time Sheet Reminders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Time Sheet-Erinnerungen senden;
               ENU=Send Time Sheet Reminders;
               NLD=Herinneringen urenverantwoording sturen];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   WeekNo := DATE2DWY(TODAY, 2) -1;
                   Year := DATE2DWY(TODAY, 3);
                 END;

    OnPreReport=BEGIN
                  IF (Year = 0) OR (WeekNo = 0) THEN
                    ERROR(Text002);
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1100527000;;DataItem;                  ;
               DataItemTable=Table5200;
               DataItemTableView=SORTING(Status)
                                 WHERE(Status=CONST(Active));
               OnPreDataItem=BEGIN
                               SETFILTER("Account Hours",'%1',Employee."Account Hours"::"Required according to Norm");
                             END;

               OnAfterGetRecord=VAR
                                  PostedHourHeader@1100527003 : Record 11012084;
                                  HourAccountingLine@1100527002 : Record 11012039;
                                  TimeSheetReminderSent@1100527001 : Record 11229377;
                                  HourAccounting@1100527000 : Record 11012038;
                                BEGIN
                                  IF PostedHourHeader.GET(Year, WeekNo, "No.") THEN
                                    CurrReport.SKIP;

                                  IF TimeSheetReminderSent.GET(Year, WeekNo, "No.") THEN
                                    CurrReport.SKIP;

                                  IF NOT HourAccounting.GET(Year, WeekNo, "No.") THEN BEGIN
                                    SendMessage("No.", STRSUBSTNO(Text000, WeekNo, Year));
                                    CurrReport.SKIP;
                                  END;

                                  HourAccountingLine.SETRANGE(Year, Year);
                                  HourAccountingLine.SETRANGE(Week, WeekNo);
                                  HourAccountingLine.SETRANGE("Employee No.", "No.");
                                  IF NOT HourAccountingLine.FINDFIRST THEN BEGIN
                                    SendMessage("No." , STRSUBSTNO(Text000, WeekNo, Year));
                                    CurrReport.SKIP;
                                  END;

                                  HourAccountingLine.CALCFIELDS("Normal Hours");
                                  IF HourAccountingLine."Normal Hours" < "Norm Hours" THEN BEGIN
                                    SendMessage("No.", STRSUBSTNO(Text001, WeekNo, Year, "Norm Hours"));
                                    CurrReport.SKIP;
                                  END;
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100527003;;Container;
                  ContainerType=ContentArea }

      { 1100527002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100527000;2;Field  ;
                  CaptionML=[DEU=Jahr;
                             ENU=Year;
                             NLD=Jaar;
                             NOR=r;
                             SVE=r];
                  SourceExpr=Year }

      { 1100527001;2;Field  ;
                  CaptionML=[DEU=Woche;
                             ENU=Week;
                             NLD=Week;
                             NOR=Uke;
                             SVE=Vecka];
                  SourceExpr=WeekNo }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      MailTemplateManagement@1100525000 : Codeunit 11012622;
      WeekNo@1100527001 : Integer;
      Year@1100527000 : Integer;
      Text000@1100527004 : TextConst 'DEU=Erinnerung: Time Sheet fr Woche %1.%2 ist f„llig.;ENU=Reminder: Time Sheet for week %1.%2 is due.;NLD=Herinnering: urenverantwoording voor week %1.%2 wordt verwacht.';
      Text001@1100527003 : TextConst 'DEU=Erinnerung: Time Sheet fr Woche %1.%2 ergibt nicht mindestens %3 Stunden.;ENU=Reminder: Time Sheet for week %1.%2 does not add up to at least %3 hours.;NLD=Herinnering: Urenverantwoording voor week %1.%2 heeft een totaal van minder dan %3 uur.';
      Text002@1100527002 : TextConst 'DEU=Bitte geben Sie eine Wochennummer und ein Jahr an.;ENU=Please specify a week no. and a year.;NLD=Geef a.u.b. een weeknr. en een jaar in.';

    PROCEDURE SendMessage@1100527000(EmployeeNo@1100527001 : Code[20];Message@1100527000 : Text[250]);
    VAR
      Employee@1100527004 : Record 5200;
      TimeSheetReminderSent@1100527003 : Record 11229377;
      MailTemplate@1100525001 : Record 11229450;
      DocumentMailing@1100527005 : Codeunit 260;
      MailTemplateNo@1100525000 : Integer;
      Subject@1100525002 : Text;
    BEGIN
      MailTemplateNo := 111;
      Employee.GET(EmployeeNo);
      Employee.TESTFIELD("Company E-Mail");
      Subject := Message;
      MailTemplateManagement.GetMailTemplate(MailTemplate, MailTemplateNo, Employee."Global Dimension 1 Code");
      IF (MailTemplate."Use Mail Template Subject Text") THEN
        Subject := MailTemplateManagement.GetLayoutTextSubstitutedSubjectOnly(MailTemplateNo, '', Employee);

      DocumentMailing.EmailFile4PS(
        '',
        '',
        '',
        Employee."No.",
        Employee."Company E-Mail",
        MailTemplateNo,
        Employee."Global Dimension 1 Code",
        '',
        Employee,
        Subject,
        '',
        TRUE);

      TimeSheetReminderSent.Year := Year;
      TimeSheetReminderSent."Week No." := WeekNo;
      TimeSheetReminderSent."Employee No." := EmployeeNo;
      TimeSheetReminderSent."Date/Time" := CURRENTDATETIME;
      TimeSheetReminderSent.Message := Message;
      TimeSheetReminderSent.INSERT;

      // Don't let future failures roll-back the INSERT above,
      // because when we get here, reminders have actually been sent.
      COMMIT;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

