OBJECT Report 11012413 Remove Duplicate Contacts
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Lîschen doppelte Kontakte;
               ENU=Remove Duplicate Contacts;
               NLD=Dubbele relaties verwijderen;
               NOR=Fjern dupliserte kontakter;
               SVE=Ta bort dubblettkontakter];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  SourceContact@1100528700 : Record 5050;
                  TargetContact@1100528701 : Record 5050;
                  ContactFeatureType@1100528702 : Record 11012753;
                  ContactAltAddress@1100528703 : Record 5051;
                  ContactIndustryGroup@1100528704 : Record 5058;
                  ContactJobResponsibility@1100528705 : Record 5067;
                  ContactMailingGroup@1100528706 : Record 5056;
                  SalesPersonByContact@1100528707 : Record 11012760;
                BEGIN
                  IF SourceContactNo = '' THEN
                    ERROR(Text004, Text001);
                  IF TargetContactNo = '' THEN
                    ERROR(Text004, Text002);

                  IF NOT SourceContact.GET(SourceContactNo) THEN
                    ERROR(Text003, Text001);
                  IF NOT TargetContact.GET(TargetContactNo) THEN
                    ERROR(Text003, Text002);

                  IF SourceContact.Type <> TargetContact.Type THEN
                    ERROR(Text005, Text001, Text002);

                  IF SourceContactNo = TargetContactNo THEN
                    ERROR(Text007, Text001, Text002);

                  IF (SourceContact.Type = SourceContact.Type::Person) AND
                     (SourceContact."Company No." <> TargetContact."Company No.") THEN
                    ERROR(Text006, SourceContact.FIELDCAPTION("Company No."), Text001, Text002);

                  // Central Data Management Checks

                  // Companies
                  IF SourceContact.Type = SourceContact.Type::Company THEN
                    IF NOT ChangingCompanyIsAllowed(SourceContactNo) THEN
                      ERROR(Text010, SourceContact.TABLECAPTION);
                  IF TargetContact.Type = TargetContact.Type::Company THEN
                    IF NOT ChangingCompanyIsAllowed(TargetContactNo) THEN
                      ERROR(Text010, TargetContact.TABLECAPTION);

                  // Persons
                  IF SourceContact.Type = SourceContact.Type::Person THEN
                    IF NOT ChangingContactPersonIsAllowed(SourceContactNo) THEN
                      ERROR(Text010, SourceContact.TABLECAPTION);
                  IF TargetContact.Type = TargetContact.Type::Person THEN
                    IF NOT ChangingContactPersonIsAllowed(TargetContactNo) THEN
                      ERROR(Text010, TargetContact.TABLECAPTION);

                  // Contact Features
                  IF SourceContact.Type = SourceContact.Type::Company THEN
                    IF NOT ContactFeatureTypeIsEditable(SourceContactNo, ContactFeatureType."Relation Type"::Company) THEN
                      ERROR(Text010, ContactFeatureType.TABLECAPTION);
                  IF TargetContact.Type = TargetContact.Type::Company THEN
                    IF NOT ContactFeatureTypeIsEditable(TargetContactNo, ContactFeatureType."Relation Type"::Company) THEN
                      ERROR(Text010, ContactFeatureType.TABLECAPTION);
                  IF SourceContact.Type = SourceContact.Type::Person THEN
                    IF NOT ContactFeatureTypeIsEditable(SourceContactNo, ContactFeatureType."Relation Type"::Contact) THEN
                      ERROR(Text010, ContactFeatureType.TABLECAPTION);
                  IF SourceContact.Type = SourceContact.Type::Person THEN
                    IF NOT ContactFeatureTypeIsEditable(TargetContactNo, ContactFeatureType."Relation Type"::Contact) THEN
                      ERROR(Text010, ContactFeatureType.TABLECAPTION);

                  // Contact Alternative Addresses
                  IF NOT ContactAltAddressIsEditable(SourceContactNo) THEN
                    ERROR(Text010, ContactAltAddress.TABLECAPTION);
                  IF NOT ContactAltAddressIsEditable(TargetContactNo) THEN
                    ERROR(Text010, ContactAltAddress.TABLECAPTION);

                  // Contact Industry Groups
                  IF NOT ContactIndustryGroupIsEditable(SourceContactNo) THEN
                    ERROR(Text010, ContactIndustryGroup.TABLECAPTION);
                  IF NOT ContactIndustryGroupIsEditable(TargetContactNo) THEN
                    ERROR(Text010, ContactIndustryGroup.TABLECAPTION);

                  // Contact Job Responsibilities
                  IF SourceContact.Type = SourceContact.Type::Person THEN
                    IF NOT FunctionGroupIsEditable(SourceContactNo) THEN
                      ERROR(Text010, ContactJobResponsibility.TABLECAPTION);
                  IF TargetContact.Type = TargetContact.Type::Person THEN
                    IF NOT FunctionGroupIsEditable(TargetContactNo) THEN
                      ERROR(Text010, ContactJobResponsibility.TABLECAPTION);

                  // Contact Mailing Groups
                  IF NOT ContactMailingGroupIsEditable(SourceContactNo) THEN
                    ERROR(Text010, ContactMailingGroup.TABLECAPTION);
                  IF NOT ContactMailingGroupIsEditable(TargetContactNo) THEN
                    ERROR(Text010, ContactMailingGroup.TABLECAPTION);

                  // Salespersons by Contact
                  IF NOT SalesPersonByContactIsEditable(SourceContactNo) THEN
                    ERROR(Text010, SalesPersonByContact.TABLECAPTION);
                  IF NOT SalesPersonByContactIsEditable(TargetContactNo) THEN
                    ERROR(Text010, SalesPersonByContact.TABLECAPTION);

                  UpdateOtherCompanies := DetermineUpdateOtherCompanies;
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 ORDER(Ascending)
                                 WHERE(Number=CONST(1));
               OnPostDataItem=VAR
                                CompanyRelationsCentralMgt@1100528700 : Record 11012059;
                              BEGIN
                                DelDuplContactBusinessRelation('');
                                DelDuplContactFeatureType('');
                                DelDuplContactAltAddress('');
                                DelDuplContactAltAddrDateRange('');
                                DelDuplContactIndustryGroup('');
                                DelDuplContactJobRespons('');
                                DelDuplContactMailingGroup('');
                                DelDuplSalespersonByContact('');
                                DelDuplContactWebSource('');
                                DelDuplHoldingStructure('');
                                DelDuplContactProfileAnswer('');
                                DelDuplRlshpMgtCommentLine('');

                                IF UpdateOtherCompanies THEN BEGIN
                                  // If for a sub table central data management is disabled or not implemented, remove the duplicates to
                                  // avoid errors.
                                  CompanyRelationsCentralMgt.SETFILTER(Company, '<>%1', COMPANYNAME);
                                  CompanyRelationsCentralMgt.SETRANGE("Company Contact Data", COMPANYNAME);
                                  IF CompanyRelationsCentralMgt.FINDSET THEN
                                    REPEAT
                                      DelDuplContactBusinessRelation(CompanyRelationsCentralMgt.Company);
                                      DelDuplContactFeatureType(CompanyRelationsCentralMgt.Company);
                                      DelDuplContactAltAddress(CompanyRelationsCentralMgt.Company);
                                      DelDuplContactAltAddrDateRange(CompanyRelationsCentralMgt.Company);
                                      DelDuplContactIndustryGroup(CompanyRelationsCentralMgt.Company);
                                      DelDuplContactJobRespons(CompanyRelationsCentralMgt.Company);
                                      DelDuplContactMailingGroup(CompanyRelationsCentralMgt.Company);
                                      DelDuplSalespersonByContact(CompanyRelationsCentralMgt.Company);
                                      DelDuplContactWebSource(CompanyRelationsCentralMgt.Company);
                                      DelDuplHoldingStructure(CompanyRelationsCentralMgt.Company);
                                      DelDuplContactProfileAnswer(CompanyRelationsCentralMgt.Company);
                                      DelDuplRlshpMgtCommentLine(CompanyRelationsCentralMgt.Company);
                                    UNTIL CompanyRelationsCentralMgt.NEXT = 0;
                                END;

                                RenameContact;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  Name=Contact No. to Be Removed;
                  CaptionML=[DEU=Zu lîschende Kontaktnr.;
                             ENU=Contact No. to Be Removed;
                             NLD=Te verwijderen relatienr.;
                             NOR=Kontaktnr som skal fjernes;
                             SVE=Kontaktnr som ska tas bort];
                  SourceExpr=SourceContactNo;
                  TableRelation=Contact }

      { 1100525003;2;Field  ;
                  Name=Replace by Contact No.;
                  CaptionML=[DEU=Ersetzen von Kontaktnr.;
                             ENU=Replace by Contact No.;
                             NLD=Vervangen door relatienr.;
                             NOR=Erstatt med kontaktnr;
                             SVE=ErsÑtt med kontaktnr];
                  SourceExpr=TargetContactNo;
                  TableRelation=Contact }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SourceContactNo@1100528700 : Code[20];
      TargetContactNo@1100528701 : Code[20];
      Text001@1100528705 : TextConst 'DEU=Zu lîschende Kontaktnr.;ENU=Contact No. to Be Replaced;NLD=Te verwijderen relatienr.;NOR=Kontaktnr som skal erstattes;SVE=Kontaktnr som ska ersÑttas';
      Text002@1100528704 : TextConst 'DEU=Ersetzen von Kontaktnr.;ENU=Replace by Contact No.;NLD=Vervangen door relatienr.;NOR=Erstatt med kontaktnr;SVE=ErsÑtt med kontaktnr';
      Text003@1100528702 : TextConst 'DEU=%1 nicht vorhanden;ENU=%1 does not exist.;NLD=%1 bestaat niet.;NOR=%1 finnes ikke.;SVE=%1 finns inte.';
      Text004@1100528703 : TextConst 'DEU=%1 ist leer.;ENU=%1 is empty.;NLD=%1 is niet gevuld.;NOR=%1 er tom.;SVE=%1 Ñr tom.';
      Text005@1100528706 : TextConst 'DEU=%1 und %2 sind nicht von der gleichen Art.;ENU=%1 and %2 do not have the same type.;NLD=%1 en %2 zijn niet van hetzelfde type.;NOR=%1 og %2 har ikke samme type.;SVE=%1 och %2 har inte samma typ.';
      Text006@1100528707 : TextConst 'DEU=%1 von %2 und %3 stimmen nicht Åberein.;ENU=%1 of %2 and %3 are not equal.;NLD=%1 van %2 en %3 zijn niet gelijk.;NOR=%1 av %2 og %3 er ikke like.;SVE=%1 av %2 och %3 Ñr inte lika.';
      Text007@1100528708 : TextConst 'DEU=%1 und %2 haben die gleiche Nummer.;ENU=%1 and %2 do have the same number.;NLD=%1 en %2 hebben hetzelfde nummer.;NOR=%1 og %2 har samme nummer.;SVE=%1 och %2 har samma nummer.';
      Text010@1100528711 : TextConst 'DEU=Tabelle %1 wird zentral verwaltet. Lîschen ist nicht zulÑssig.;ENU=Table %1 is managed centrally, modifying is not allowed.;NLD=Tabel %1 wordt centraal beheerd, wijzigen is niet toegestaan.;NOR=Tabell %1 hÜndteres sentralt og kan ikke endres.;SVE=Tabell %1 hanteras centralt och fÜr inte Ñndras.';
      UpdateOtherCompanies@1100528709 : Boolean;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;

    PROCEDURE DelDuplContactBusinessRelation@1100528715(Company@1100528704 : Text[30]);
    VAR
      SourceContactBusinessRelation@1100528700 : Record 5054;
      TargetContactBusinessRelation@1100528701 : Record 5054;
      Customer@1100528703 : Record 18;
      Vendor@1100528702 : Record 23;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactBusinessRelation.CHANGECOMPANY(Company);
        TargetContactBusinessRelation.CHANGECOMPANY(Company);
        Customer.CHANGECOMPANY(Company);
        Vendor.CHANGECOMPANY(Company);
      END;

      SourceContactBusinessRelation.SETCURRENTKEY("Contact No.","Business Relation Code");
      SourceContactBusinessRelation.SETRANGE("Contact No.", SourceContactNo);
      IF SourceContactBusinessRelation.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactBusinessRelation.SETRANGE("Contact No.", TargetContactNo);
          TargetContactBusinessRelation.SETRANGE("Business Relation Code", SourceContactBusinessRelation."Business Relation Code");
          IF TargetContactBusinessRelation.FINDFIRST THEN BEGIN
            IF SourceContactBusinessRelation."No." <> TargetContactBusinessRelation."No." THEN BEGIN
              CASE SourceContactBusinessRelation."Link to Table" OF
                SourceContactBusinessRelation."Link to Table"::Customer:
                  BEGIN
                    Customer.GET(SourceContactBusinessRelation."No.");
                    Customer.VALIDATE(Blocked, Customer.Blocked::All);
                    Customer.MODIFY(TRUE);
                  END;
                SourceContactBusinessRelation."Link to Table"::Vendor:
                  BEGIN
                    Vendor.GET(SourceContactBusinessRelation."No.");
                    Vendor.VALIDATE(Blocked, Vendor.Blocked::All);
                    Vendor.MODIFY(TRUE);
                  END;
              END;
            END;
            SourceContactBusinessRelation.DELETE(TRUE); // Source already exists for target, delete source
          END;
        UNTIL SourceContactBusinessRelation.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplContactFeatureType@1100528717(Company@1100528702 : Text[30]);
    VAR
      SourceContactFeatureType@1100528700 : Record 11012753;
      TargetContactFeatureType@1100528701 : Record 11012753;
      TargetContactFeatureType2@1210190000 : Record 11012753;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactFeatureType.CHANGECOMPANY(Company);
        TargetContactFeatureType.CHANGECOMPANY(Company);
      END;

      SourceContactFeatureType.SETCURRENTKEY("Relation Type",Code,"Line No.");
      SourceContactFeatureType.SETFILTER("Relation Type", '%1|%2',
        SourceContactFeatureType."Relation Type"::Company,
        SourceContactFeatureType."Relation Type"::Contact);
      SourceContactFeatureType.SETRANGE(Code, SourceContactNo);
      IF SourceContactFeatureType.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactFeatureType.RESET;
          TargetContactFeatureType.SETRANGE("Relation Type", SourceContactFeatureType."Relation Type");
          TargetContactFeatureType.SETRANGE(Code, TargetContactNo);
          TargetContactFeatureType.SETRANGE("Feature Group", SourceContactFeatureType."Feature Group");
          TargetContactFeatureType.SETRANGE(Feature, SourceContactFeatureType.Feature);
          IF TargetContactFeatureType.FINDFIRST THEN BEGIN
            SourceContactFeatureType."Feature Group" := ''; // This avoids obligatory check error in delete trigger
            SourceContactFeatureType.DELETE(TRUE); // Source already exists for target, delete source
          END ELSE BEGIN
            // Modify Line No. to avoid duplicates
            TargetContactFeatureType.SETRANGE("Feature Group");
            TargetContactFeatureType.SETRANGE(Feature);
            TargetContactFeatureType.SETRANGE("Line No.", SourceContactFeatureType."Line No.");
            IF TargetContactFeatureType.FINDFIRST THEN BEGIN
              TargetContactFeatureType2 := TargetContactFeatureType;
              TargetContactFeatureType2."Line No." :=
                TargetContactFeatureType2.GetFreeLineNo(
                  TargetContactFeatureType2."Relation Type",
                  TargetContactFeatureType2.Code);
              TargetContactFeatureType."Feature Group" := ''; // This avoids obligatory check error in delete trigger
              TargetContactFeatureType.DELETE(TRUE);
              TargetContactFeatureType2.INSERT(TRUE);
            END;
          END;
        UNTIL SourceContactFeatureType.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplContactAltAddress@1100528718(Company@1100528702 : Text[30]);
    VAR
      SourceContactAltAddress@1100528700 : Record 5051;
      TargetContactAltAddress@1100528701 : Record 5051;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactAltAddress.CHANGECOMPANY(Company);
        TargetContactAltAddress.CHANGECOMPANY(Company);
      END;

      SourceContactAltAddress.SETCURRENTKEY("Contact No.",Code);
      SourceContactAltAddress.SETRANGE("Contact No.", SourceContactNo);
      IF SourceContactAltAddress.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactAltAddress.SETRANGE("Contact No.", TargetContactNo);
          TargetContactAltAddress.SETRANGE(Code, SourceContactAltAddress.Code);
          IF TargetContactAltAddress.FINDFIRST THEN
            SourceContactAltAddress.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceContactAltAddress.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplContactAltAddrDateRange@1100528719(Company@1100528700 : Text[30]);
    VAR
      SourceContactAltAddrDateRange@1100528701 : Record 5052;
      TargetContactAltAddrDateRange@1100528702 : Record 5052;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactAltAddrDateRange.CHANGECOMPANY(Company);
        TargetContactAltAddrDateRange.CHANGECOMPANY(Company);
      END;

      SourceContactAltAddrDateRange.SETCURRENTKEY("Contact No.","Starting Date");
      SourceContactAltAddrDateRange.SETRANGE("Contact No.", SourceContactNo);
      IF SourceContactAltAddrDateRange.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactAltAddrDateRange.SETRANGE("Contact No.", TargetContactNo);
          TargetContactAltAddrDateRange.SETRANGE("Starting Date", SourceContactAltAddrDateRange."Starting Date");
          IF TargetContactAltAddrDateRange.FINDFIRST THEN
            SourceContactAltAddrDateRange.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceContactAltAddrDateRange.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplContactIndustryGroup@1100528720(Company@1100528700 : Text[30]);
    VAR
      SourceContactIndustryGroup@1100528701 : Record 5058;
      TargetContactIndustryGroup@1100528702 : Record 5058;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactIndustryGroup.CHANGECOMPANY(Company);
        TargetContactIndustryGroup.CHANGECOMPANY(Company);
      END;

      SourceContactIndustryGroup.SETCURRENTKEY("Contact No.","Industry Group Code");
      SourceContactIndustryGroup.SETRANGE("Contact No.", SourceContactNo);
      IF SourceContactIndustryGroup.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactIndustryGroup.SETRANGE("Contact No.", TargetContactNo);
          TargetContactIndustryGroup.SETRANGE("Industry Group Code", SourceContactIndustryGroup."Industry Group Code");
          IF TargetContactIndustryGroup.FINDFIRST THEN
            TargetContactIndustryGroup.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceContactIndustryGroup.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplContactJobRespons@1100528721(Company@1100528700 : Text[30]);
    VAR
      SourceContactJobResponsibility@1100528701 : Record 5067;
      TargetContactJobResponsibility@1100528702 : Record 5067;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactJobResponsibility.CHANGECOMPANY(Company);
        TargetContactJobResponsibility.CHANGECOMPANY(Company);
      END;

      SourceContactJobResponsibility.SETCURRENTKEY("Contact No.","Job Responsibility Code");
      SourceContactJobResponsibility.SETRANGE("Contact No.", SourceContactNo);
      IF SourceContactJobResponsibility.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactJobResponsibility.SETRANGE("Contact No.", TargetContactNo);
          TargetContactJobResponsibility.SETRANGE("Job Responsibility Code", SourceContactJobResponsibility."Job Responsibility Code");
          IF TargetContactJobResponsibility.FINDFIRST THEN
            SourceContactJobResponsibility.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceContactJobResponsibility.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplContactMailingGroup@1100528722(Company@1100528700 : Text[30]);
    VAR
      SourceContactMailingGroup@1100528701 : Record 5056;
      TargetContactMailingGroup@1100528702 : Record 5056;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactMailingGroup.CHANGECOMPANY(Company);
        TargetContactMailingGroup.CHANGECOMPANY(Company);
      END;

      SourceContactMailingGroup.SETCURRENTKEY("Contact No.","Mailing Group Code");
      SourceContactMailingGroup.SETRANGE("Contact No.", SourceContactNo);
      IF SourceContactMailingGroup.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactMailingGroup.SETRANGE("Contact No.", TargetContactNo);
          TargetContactMailingGroup.SETRANGE("Mailing Group Code", SourceContactMailingGroup."Mailing Group Code");
          IF TargetContactMailingGroup.FINDFIRST THEN
            SourceContactMailingGroup.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceContactMailingGroup.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplSalespersonByContact@1100528723(Company@1100528700 : Text[30]);
    VAR
      SourceSalespersonByContact@1100528701 : Record 11012760;
      TargetSalespersonByContact@1100528702 : Record 11012760;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceSalespersonByContact.CHANGECOMPANY(Company);
        TargetSalespersonByContact.CHANGECOMPANY(Company);
      END;

      SourceSalespersonByContact.SETCURRENTKEY("Contact No.","Salesperson Code");
      SourceSalespersonByContact.SETRANGE("Contact No.", SourceContactNo);
      IF SourceSalespersonByContact.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetSalespersonByContact.SETRANGE("Contact No.", TargetContactNo);
          TargetSalespersonByContact.SETRANGE("Salesperson Code", SourceSalespersonByContact."Salesperson Code");
          IF TargetSalespersonByContact.FINDFIRST THEN
            SourceSalespersonByContact.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceSalespersonByContact.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplContactWebSource@1100528724(Company@1100528700 : Text[30]);
    VAR
      SourceContactWebSource@1100528701 : Record 5060;
      TargetContactWebSource@1100528702 : Record 5060;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactWebSource.CHANGECOMPANY(Company);
        TargetContactWebSource.CHANGECOMPANY(Company);
      END;

      SourceContactWebSource.SETCURRENTKEY("Contact No.","Web Source Code");
      SourceContactWebSource.SETRANGE("Contact No.", SourceContactNo);
      IF SourceContactWebSource.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactWebSource.SETRANGE("Contact No.", TargetContactNo);
          TargetContactWebSource.SETRANGE("Web Source Code", SourceContactWebSource."Web Source Code");
          IF TargetContactWebSource.FINDFIRST THEN
            SourceContactWebSource.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceContactWebSource.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplHoldingStructure@1100528725(Company@1100528700 : Text[30]);
    VAR
      SourceHoldingStructure@1100528701 : Record 11020206;
      TargetHoldingStructure@1100528702 : Record 11020206;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceHoldingStructure.CHANGECOMPANY(Company);
        TargetHoldingStructure.CHANGECOMPANY(Company);
      END;

      SourceHoldingStructure.SETCURRENTKEY(Contact,"Line No.");
      SourceHoldingStructure.SETRANGE(Contact, SourceContactNo);
      IF SourceHoldingStructure.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetHoldingStructure.SETRANGE(Contact, TargetContactNo);
          IF TargetHoldingStructure.FINDFIRST THEN
            SourceHoldingStructure.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceHoldingStructure.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplContactProfileAnswer@1100528726(Company@1100528700 : Text[30]);
    VAR
      SourceContactProfileAnswer@1100528701 : Record 5089;
      TargetContactProfileAnswer@1100528702 : Record 5089;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceContactProfileAnswer.CHANGECOMPANY(Company);
        TargetContactProfileAnswer.CHANGECOMPANY(Company);
      END;

      SourceContactProfileAnswer.SETCURRENTKEY("Contact No.","Profile Questionnaire Code","Line No.");
      SourceContactProfileAnswer.SETRANGE("Contact No.", SourceContactNo);
      IF SourceContactProfileAnswer.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetContactProfileAnswer.SETRANGE("Contact No.", TargetContactNo);
          IF TargetContactProfileAnswer.FINDFIRST THEN
            SourceContactProfileAnswer.DELETE(TRUE); // Source already exists for target, delete source
        UNTIL SourceContactProfileAnswer.NEXT = 0;
      END;
    END;

    PROCEDURE DelDuplRlshpMgtCommentLine@1100528727(Company@1100528700 : Text[30]);
    VAR
      SourceRlshpMgtCommentLine@1100528701 : Record 5061;
      TargetRlshpMgtCommentLine@1100528702 : Record 5061;
    BEGIN
      IF Company <> '' THEN BEGIN
        SourceRlshpMgtCommentLine.CHANGECOMPANY(Company);
        TargetRlshpMgtCommentLine.CHANGECOMPANY(Company);
      END;

      SourceRlshpMgtCommentLine.SETCURRENTKEY("Table Name","No.","Sub No.","Line No.");
      SourceRlshpMgtCommentLine.SETRANGE("Table Name", SourceRlshpMgtCommentLine."Table Name"::Contact);
      SourceRlshpMgtCommentLine.SETRANGE("No.", SourceContactNo);
      SourceRlshpMgtCommentLine.SETRANGE("Sub No.", 0);
      IF SourceRlshpMgtCommentLine.FINDSET(TRUE) THEN BEGIN
        REPEAT
          TargetRlshpMgtCommentLine.SETRANGE("Table Name", SourceRlshpMgtCommentLine."Table Name");
          TargetRlshpMgtCommentLine.SETRANGE("No.", TargetContactNo);
          TargetRlshpMgtCommentLine.SETRANGE("Sub No.", 0);
          TargetRlshpMgtCommentLine.SETRANGE("Line No.", SourceRlshpMgtCommentLine."Line No.");
          IF TargetRlshpMgtCommentLine.FINDFIRST THEN
            // Modify Line No. to avoid duplicates
            TargetRlshpMgtCommentLine.RENAME(
              TargetRlshpMgtCommentLine."Table Name",
              TargetRlshpMgtCommentLine."No.",
              TargetRlshpMgtCommentLine."Sub No.",
              TargetRlshpMgtCommentLine.GetFreeLineNo(
                TargetRlshpMgtCommentLine."Table Name",
                TargetRlshpMgtCommentLine."No.",
                TargetRlshpMgtCommentLine."Sub No."));
         UNTIL SourceRlshpMgtCommentLine.NEXT = 0;
      END;
    END;

    PROCEDURE RenameContact@1100528700();
    VAR
      SourceContact@1100528701 : Record 5050;
      TargetContact@1100528702 : Record 5050;
      xRecSourceContact@1100528700 : Record 5050;
      DocumentProperties@1210190000 : Record 11012746;
      CompanyRelationsCentralMgt@1100528704 : Record 11012059;
      DuplicateManagement@1100528703 : Codeunit 5060;
      CentralDataManagement@1100525000 : Codeunit 11012010;
    BEGIN
      SourceContact.GET(SourceContactNo);
      TargetContact.GET(TargetContactNo);

      DuplicateManagement.RemoveContIndex(SourceContact, FALSE);

      DocumentProperties.HandleRenameofContact(
        COMPANYNAME, SourceContactNo, TargetContactNo, SourceContact."Document Group", FALSE);

      IF UpdateOtherCompanies THEN BEGIN
        CompanyRelationsCentralMgt.SETFILTER(Company, '<>%1', COMPANYNAME);
        CompanyRelationsCentralMgt.SETRANGE("Company Contact Data", COMPANYNAME);
        IF CompanyRelationsCentralMgt.FINDSET THEN
          REPEAT
            DocumentProperties.HandleRenameofContact(
              CompanyRelationsCentralMgt.Company, SourceContactNo, TargetContactNo, SourceContact."Document Group", FALSE);
          UNTIL CompanyRelationsCentralMgt.NEXT = 0;
      END;

      DeleteContactInOtherCompanies(SourceContact, TargetContact);
      TargetContact.DELETE(FALSE);

      // Handle Central Data Management
      xRecSourceContact := SourceContact;
      SourceContact."No." := TargetContactNo;
      CentralDataManagement.EditContact(SourceContact, 3, xRecSourceContact); // Rename
      SourceContact := xRecSourceContact;

      SourceContact.RENAME(TargetContactNo);
      SourceContact.MODIFY;
      xRecSourceContact := SourceContact;
      SourceContact.TRANSFERFIELDS(TargetContact, FALSE);
      SourceContact.OnModifyFunc(xRecSourceContact, TRUE); // update contactpersons in central company
      SourceContact.MODIFY(TRUE);

      // Update the contact name in documents
      UpdateContactNameDocuments(COMPANYNAME, SourceContact);

      IF UpdateOtherCompanies THEN BEGIN
        CompanyRelationsCentralMgt.SETFILTER(Company, '<>%1', COMPANYNAME);
        CompanyRelationsCentralMgt.SETRANGE("Company Contact Data", COMPANYNAME);
        IF CompanyRelationsCentralMgt.FINDSET THEN
          REPEAT
            UpdateContactNameDocuments(CompanyRelationsCentralMgt.Company, SourceContact);
          UNTIL CompanyRelationsCentralMgt.NEXT = 0;
      END;
    END;

    PROCEDURE DeleteContactInOtherCompanies@1100528702(SourceContact@1100528700 : Record 5050;TargetContact@1100528705 : Record 5050);
    VAR
      CompanyRelationsCentralMgt@1100528701 : Record 11012059;
      DuplicateManagement@1100528706 : Codeunit 5060;
    BEGIN
      IF NOT UpdateOtherCompanies THEN
        EXIT;

      CompanyRelationsCentralMgt.SETFILTER(Company, '<>%1', COMPANYNAME);
      CompanyRelationsCentralMgt.SETRANGE("Company Contact Data", COMPANYNAME);
      IF CompanyRelationsCentralMgt.FINDSET THEN
        REPEAT
          TargetContact.CHANGECOMPANY(CompanyRelationsCentralMgt.Company);
          IF TargetContact.FIND('=') THEN
            TargetContact.DELETE(FALSE);

          SourceContact.CHANGECOMPANY(CompanyRelationsCentralMgt.Company);
          IF SourceContact.FIND('=') THEN
            DuplicateManagement.RemoveContIndexComp(SourceContact, FALSE, CompanyRelationsCentralMgt.Company);
        UNTIL CompanyRelationsCentralMgt.NEXT = 0;
    END;

    PROCEDURE UpdateContactNameDocuments@1100528705(Company@1100528701 : Text[30];SourceContact@1100528700 : Record 5050);
    VAR
      DocumentProperties@1100528702 : Record 11012746;
    BEGIN
      IF SourceContact.IsCompany OR SourceContact.IsPerson THEN
        DocumentProperties.SETRANGE("Contact No.", SourceContact."No.")
      ELSE BEGIN
        DocumentProperties.SETRANGE("Contact No.", SourceContact."Company No.");
        DocumentProperties.SETRANGE("Contact Person No.", SourceContact."No.");
      END;
      DocumentProperties.SETRANGE("Internal Company", Company);
      IF DocumentProperties.FINDSET THEN
        REPEAT
          IF SourceContact.IsCompany OR SourceContact.IsPerson THEN
            DocumentProperties.VALIDATE("Contact No.")
          ELSE BEGIN
            DocumentProperties.VALIDATE("Contact No.");
            DocumentProperties.VALIDATE("Contact Person No.");
          END;
          DocumentProperties.MODIFY(TRUE);
        UNTIL DocumentProperties.NEXT = 0;
    END;

    PROCEDURE "-"@1100528729();
    BEGIN
    END;

    LOCAL PROCEDURE ChangingCompanyIsAllowed@1100528403(ContactNo@1100528701 : Code[20]) : Boolean;
    VAR
      CompanyRelationsCentralMgt@1100528400 : Record 11012059;
      CentralDataMgt@1100528700 : Codeunit 11012010;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(CentralDataMgt.ChangingCompanyIsAllowed(ContactNo));
    END;

    LOCAL PROCEDURE ChangingContactPersonIsAllowed@1100528404(ContactNo@1100528701 : Code[20]) : Boolean;
    VAR
      CompanyRelationsCentralMgt@1100528400 : Record 11012059;
      CentralDataMgt@1100528700 : Codeunit 11012010;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(CentralDataMgt.ChangingPersonIsAllowed(ContactNo));
    END;

    LOCAL PROCEDURE ContactFeatureTypeIsEditable@1100528400(ContactNo@1100528700 : Code[20];RelationType@1100528701 : Option) : Boolean;
    VAR
      CompanyRelationsCentralMgt@1100528400 : Record 11012059;
      CentralDataMgt@1100528401 : Codeunit 11012010;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(CentralDataMgt.ContactFeatureTypeIsEditable(ContactNo, RelationType));
    END;

    LOCAL PROCEDURE ContactAltAddressIsEditable@1100528701(ContactNo@1100528700 : Code[20]) : Boolean;
    VAR
      CentralDataMgt@1100528400 : Codeunit 11012010;
      CompanyRelationsCentralMgt@1100528401 : Record 11012059;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(CentralDataMgt.ContactAltAddressIsEditable(ContactNo));
    END;

    LOCAL PROCEDURE ContactIndustryGroupIsEditable@1100528706(ContactNo@1100528700 : Code[20]) : Boolean;
    VAR
      CentralDataMgt@1100528400 : Codeunit 11012010;
      CompanyRelationsCentralMgt@1100528401 : Record 11012059;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(CentralDataMgt.ContactIndustryGroupIsEditable(ContactNo));
    END;

    LOCAL PROCEDURE FunctionGroupIsEditable@1100528709(ContactNo@1100528700 : Code[20]) : Boolean;
    VAR
      CentralDataMgt@1100528400 : Codeunit 11012010;
      CompanyRelationsCentralMgt@1100528401 : Record 11012059;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(CentralDataMgt.PersonFunctionGroupIsEditable(ContactNo));
    END;

    LOCAL PROCEDURE ContactMailingGroupIsEditable@1100528712(ContactNo@1100528700 : Code[20]) : Boolean;
    VAR
      CentralDataMgt@1100528400 : Codeunit 11012010;
      CompanyRelationsCentralMgt@1100528401 : Record 11012059;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(CentralDataMgt.ContactMailingGroupIsEditable(ContactNo));
    END;

    PROCEDURE SalesPersonByContactIsEditable@1100528716(ContactNo@1100528702 : Code[20]) : Boolean;
    VAR
      CentralDataMgt@1100528701 : Codeunit 11012010;
      CompanyRelationsCentralMgt@1100528700 : Record 11012059;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(CentralDataMgt.SalesPersonByContactIsEditable(ContactNo));
    END;

    PROCEDURE "--"@1100528730();
    BEGIN
    END;

    PROCEDURE DetermineUpdateOtherCompanies@1100528703() : Boolean;
    VAR
      MarketingSetup@1100528704 : Record 5079;
      CompanyRelationsCentralMgt@1100528703 : Record 11012059;
      LeadingCompany@1100528700 : Text[50];
      Contact@1100528702 : Record 5050;
    BEGIN
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(FALSE);

      MarketingSetup.GET;
      IF NOT MarketingSetup."Central Management" THEN
        EXIT(FALSE);

      LeadingCompany := CompanyRelationsCentralMgt.GetLeadingCompany(DATABASE::"Marketing Setup", TRUE);
      IF COMPANYNAME <> LeadingCompany THEN
        EXIT(FALSE);

      IF Contact.GET(TargetContactNo) THEN BEGIN
        IF (Contact.Type = Contact.Type::Company) AND (NOT MarketingSetup.Companies) THEN
          EXIT(FALSE);
        IF (Contact.Type = Contact.Type::Person) AND (NOT MarketingSetup.Persons) THEN
          EXIT(FALSE);
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

