OBJECT Page 5540 Item Availability by Timeline
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Availability by Timeline;
               NOR=Varetilgjengelighet per tidslinje;
               SVE=Artikeldisposition per tidslinje];
    ApplicationArea=#Basic,#Suite;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table5541;
    DataCaptionExpr=PageCaption;
    SourceTableView=SORTING(Due Date)
                    ORDER(Ascending);
    PageType=Worksheet;
    UsageCategory=Tasks;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             BlockCurrRecNotifications := FALSE;
             IgnoreChanges := FALSE;
             TemplateNameNewSupply := '';
             WorksheetNameNewSupply := '';
             State := State::" ";
           END;

    OnOpenPage=BEGIN
                 ItemSelected := Item."No." <> '';
               END;

    OnAfterGetRecord=BEGIN
                       Description := TimelineEventDescription;
                       ActionMsg := ActionMessage;
                     END;

    OnQueryClosePage=BEGIN
                       IF NOT (State = State::"Unsaved Changes") THEN
                         EXIT(TRUE);

                       EXIT(CONFIRM(Text003));
                     END;

    OnAfterGetCurrRecord=BEGIN
                           IF (NOT BlockCurrRecNotifications) AND (CurrReferenceNo <> "Reference No.") THEN BEGIN
                             CurrReferenceNo := "Reference No.";
                             CurrPage.Visualization.SelectTransaction("Reference No.");
                           END;
                         END;

    ActionList=ACTIONS
    {
      { 3       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 21      ;1   ;Action    ;
                      Name=Delete;
                      ShortCutKey=Ctrl+Delete;
                      CaptionML=[ENU=Delete;
                                 NOR=Slett;
                                 SVE=Ta bort];
                      ToolTipML=[ENU=Delete the record.;
                                 NOR=Slett posten.;
                                 SVE=Ta bort transaktionen.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Delete;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(Text009,TRUE) THEN
                                   EXIT;

                                 DeleteSelectedRecords;
                               END;
                                }
      { 6       ;1   ;Action    ;
                      Name=<Reload>;
                      CaptionML=[ENU=Reload;
                                 NOR=Last inn pÜ nytt;
                                 SVE=LÑs in pÜ nytt];
                      ToolTipML=[ENU=Update the window with values created by other users since you opened the window.;
                                 NOR=Oppdater vinduet med verdier som er opprettet av andre brukere siden du Üpnet vinduet.;
                                 SVE=Uppdatera fînstret med vÑrden som har skapats av andra anvÑndare sedan du îppnade fînstret.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 InitAndCreateTimelineEvents;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=<TransferChanges>;
                      CaptionML=[ENU=Save Changes;
                                 NOR=Lagre endringer;
                                 SVE=Spara Ñndringar];
                      ToolTipML=[ENU=Transfer the changes to the planning worksheet lines.;
                                 NOR=Overfõr endringene til planleggingsforslagslinjene.;
                                 SVE=ôverfîr Ñndringarna till planeringsfîrslagsraderna.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SuggestLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 TransferChangesToPlanningWksh;
                                 InitAndCreateTimelineEvents;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=Options;
                           NOR=Alternativer;
                           SVE=Alternativ];
                GroupType=Group }

    { 30  ;2   ;Field     ;
                Name=<ItemNo>;
                CaptionML=[ENU=Item No.;
                           NOR=Varenr.;
                           SVE=Artikelnr];
                ToolTipML=[ENU=Specifies the number of the item you want to view item availability for.;
                           NOR=Angir nummeret pÜ varen du vil vise varedisposisjon for.;
                           SVE=Anger numret pÜ den artikel som du vill visa artikeldisposition fîr.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ItemNo;
                TableRelation=Item;
                Importance=Promoted;
                OnValidate=VAR
                             ItemNo2@1001 : Code[20];
                           BEGIN
                             IF ItemNo <> Item."No." THEN BEGIN
                               ItemNo2 := ItemNo;
                               Item.RESET;
                               Item.GET(ItemNo);
                               IF LocationFilter <> '' THEN
                                 Item.SETFILTER("Location Filter",LocationFilter);
                               ItemSelected := TRUE;
                               InitAndCreateTimelineEvents;
                               VariantFilter := Item."Variant Filter";
                               ItemNo := ItemNo2;
                             END;
                           END;
                            }

    { 35  ;2   ;Field     ;
                Name=<VariantFilter>;
                CaptionML=[ENU=Variant Filter;
                           NOR=Variantfilter;
                           SVE=Variantfilter];
                ToolTipML=[ENU=Specifies a variant code to view only the projected available balance for that variant of the item.;
                           NOR=Angir en variantkode for Ü vise bare den forventede disponible beholdningen for den varianten av varen.;
                           SVE=Anger en variantkod om du vill visa lagerutveckling îver tiden bara fîr den varianten av artikeln.];
                ApplicationArea=#Planning;
                SourceExpr=VariantFilter;
                Importance=Promoted;
                OnValidate=VAR
                             VariantFilter2@1001 : Text;
                           BEGIN
                             IF VariantFilter <> Item.GETFILTER("Variant Filter") THEN BEGIN
                               CalcItemAvailTimeline.FindVariantWithinFilter(ItemNo,VariantFilter);

                               VariantFilter2 := VariantFilter;
                               Item.SETRANGE("Variant Filter");
                               IF VariantFilter <> '' THEN
                                 Item.SETFILTER("Variant Filter",VariantFilter);
                               InitAndCreateTimelineEvents;
                               VariantFilter := VariantFilter2;
                             END;
                           END;

                OnLookup=VAR
                           ItemVariant@1001 : Record 5401;
                           ItemVariants@1000 : Page 5401;
                         BEGIN
                           ItemVariant.SETRANGE("Item No.",ItemNo);
                           ItemVariants.SETTABLEVIEW(ItemVariant);
                           ItemVariants.LOOKUPMODE := TRUE;
                           IF ItemVariants.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ItemVariants.GETRECORD(ItemVariant);
                             Text := ItemVariant.Code;
                             EXIT(TRUE);
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 4   ;2   ;Field     ;
                Name=<LocationFilter>;
                CaptionML=[ENU=Location Filter;
                           NOR=Lokasjonsfilter;
                           SVE=LagerstÑllefilter];
                ToolTipML=[ENU=Specifies a location code to view only the projected available balance for the item at that location.;
                           NOR=Angir en lokasjonskode for Ü vise bare den forventede disponible beholdningen for varen pÜ den lokasjonen.;
                           SVE=Anger en lagerstÑllekod om du vill visa lagerutveckling îver tiden bara fîr artikeln pÜ det lagerstÑllet.];
                ApplicationArea=#Location;
                SourceExpr=LocationFilter;
                Importance=Promoted;
                OnValidate=VAR
                             LocationFilter2@1001 : Text;
                           BEGIN
                             IF LocationFilter <> Item.GETFILTER("Location Filter") THEN BEGIN
                               CalcItemAvailTimeline.FindLocationWithinFilter(LocationFilter);

                               LocationFilter2 := LocationFilter;
                               Item.SETRANGE("Location Filter");
                               IF LocationFilter <> '' THEN
                                 Item.SETFILTER("Location Filter",LocationFilter);
                               InitAndCreateTimelineEvents;
                               LocationFilter := LocationFilter2;
                             END;
                           END;

                OnLookup=VAR
                           Location@1001 : Record 14;
                           LocationList@1000 : Page 15;
                         BEGIN
                           LocationList.SETTABLEVIEW(Location);
                           LocationList.LOOKUPMODE := TRUE;
                           IF LocationList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             LocationList.GETRECORD(Location);
                             Text := Location.Code;
                             EXIT(TRUE);
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 13  ;2   ;Field     ;
                Name=<LastUpdateTime>;
                CaptionML=[ENU=Last Updated;
                           NOR=Sist oppdatert;
                           SVE=Senast uppdaterad];
                ToolTipML=[ENU=Specifies when the availability figures in the Item Availability by Timeline window were last updated.;
                           NOR=Angir nÜr tilgjengelighetstallene i vinduet Varetilgjengelighet per tidslinje sist ble oppdatert.;
                           SVE=Anger nÑr tillgÑnglighetssiffrorna i fînstret Artikeldisposition per tidslinje senast uppdaterades.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LastUpdateTime;
                Importance=Promoted;
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                Name=<ForecastName>;
                CaptionML=[ENU=Forecast Name;
                           NOR=Prognosenavn;
                           SVE=Prognosnamn];
                ToolTipML=[ENU=Specifies a demand forecast you want to include as anticipated demand in the graphical view of projected available balance.;
                           NOR=Angir en behovsprognose som du õnsker Ü inkludere som forventet behov i den grafiske visningen av forventet disponibel beholdning.;
                           SVE=Anger en efterfrÜgeprognos som du vill ta med som fîrvÑntad efterfrÜgan i den grafiska vyn av lagerutveckling îver tiden.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ForecastName;
                TableRelation="Production Forecast Name";
                Importance=Promoted;
                OnValidate=VAR
                             ForecastName2@1001 : Code[10];
                           BEGIN
                             ForecastName2 := ForecastName;
                             InitAndCreateTimelineEvents;
                             ForecastName := ForecastName2;
                           END;
                            }

    { 16  ;2   ;Field     ;
                Name=<IncludeBlanketOrders>;
                CaptionML=[ENU=Include Blanket Sales Orders;
                           NOR=Inkluder rammeordrer;
                           SVE=Ta med fîrsÑljningsavropsorder];
                ToolTipML=[ENU=Specifies if you want to include anticipated demand from blanket sales orders in the graphical view of projected available balance.;
                           NOR=Angir om du õnsker Ü inkludere forventet behov fra rammeordrer i den grafiske visningen av forventet disponibel beholdning.;
                           SVE=Anger om du vill ta med fîrvÑntad efterfrÜgan frÜn fîrsÑljningsavropsorder i den grafiska vyn av lagerutveckling îver tiden.];
                ApplicationArea=#Suite;
                SourceExpr=IncludeBlanketOrders;
                Importance=Promoted;
                OnValidate=VAR
                             IncludeBlanketOrders2@1001 : Boolean;
                           BEGIN
                             IncludeBlanketOrders2 := IncludeBlanketOrders;
                             InitAndCreateTimelineEvents;
                             IncludeBlanketOrders := IncludeBlanketOrders2;
                           END;
                            }

    { 8   ;1   ;Group     ;
                CaptionML=[ENU=Timeline;
                           NOR=Tidslinje;
                           SVE=Tidslinje];
                GroupType=Group }

    { 15  ;2   ;Field     ;
                Name=Visualization;
                ApplicationArea=#Basic,#Suite;
                Editable=FALSE;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.TimelineVisualization;PublicKeyToken=31bf3856ad364e35] }

    { 10  ;1   ;Group     ;
                Name=changeListGroup;
                CaptionML=[ENU=Event Changes;
                           NOR=Hendelsesendringer;
                           SVE=HÑndelseÑndringar];
                GroupType=GridLayout }

    { 20  ;2   ;Group     ;
                Name=changeList;
                CaptionML=[ENU=Change List;
                           NOR=Endringsliste;
                           SVE=éndringslista];
                GroupType=Repeater }

    { 5   ;3   ;Field     ;
                Name=ActionMessage;
                CaptionML=[ENU=Action Message;
                           NOR=Handlingsmelding;
                           SVE=ètgÑrdsmeddelande];
                ToolTipML=[ENU=Specifies the action to take to rebalance the demand-supply situation shown in the graph on the Timeline FastTab.;
                           NOR=Angir hvilken handling du kan utfõre for Ü gjenopprette balansen i leveringsbehovet som vises i grafen pÜ hurtigfanen Tidslinje.;
                           SVE=Anger ÜtgÑrden fîr att balansera om efterfrÜgan och tillgÜng som visas i grafen pÜ snabbfliken Tidslinje.];
                OptionCaptionML=[ENU=" ,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel";
                                 NOR=" ,Ny,Endre ant.,Tidsplanl. pÜ nytt,Tidsplanl. pÜ nytt og endre ant.,Kanseller";
                                 SVE=" ,Nytt,éndra antal,Omplanera,Omplanera & Ñndra antal,Avbryt"];
                ApplicationArea=#Manufacturing;
                SourceExpr=ActionMsg;
                Editable=FALSE }

    { 2   ;3   ;Field     ;
                Name=Description;
                CaptionML=[ENU=Description;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                ToolTipML=[ENU=Specifies the description of the planning worksheet lines that are represented on the Timeline FastTab.;
                           NOR=Angir beskrivelsen av planleggingsforslagslinjene som er representert pÜ hurtigfanen Tidslinje.;
                           SVE=Anger en beskrivning av planeringsfîrslagsraderna som representeras pÜ snabbfliken Tidslinje.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              ShowDocument("Reference No.");
                            END;
                             }

    { 17  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the due date that is stated on the existing supply order, when an action message proposes to reschedule the order.;
                           NOR=Angir forfallsdatoen som angis pÜ den eksisterende forsyningsordren, nÜr en handlingsmelding foreslÜr Ü tidsplanlegge en ordre pÜ nytt.;
                           SVE=Visar fîrfallodatumet som anges pÜ den befintliga leveransordern nÑr ett ÜtgÑrdsmeddelande fîreslÜr att ordern ska omplaneras.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Original Due Date" }

    { 14  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the due date of the suggested supply order that the line on the Event Changes FastTab represents.;
                           NOR=Angir forfallsdatoen for den foreslÜtte forsyningsordren som linjen pÜ hurtigfanen Hendelsesendringer representerer.;
                           SVE=Anger fîrfallodatumet fîr leveransorderfîrslaget som raden pÜ snabbfliken HÑndelseÑndringar representerar.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Due Date";
                OnValidate=BEGIN
                             IF "Due Date" <> xRec."Due Date" THEN BEGIN
                               TESTFIELD("Due Date");
                               CurrPage.Visualization.RescheduleTransaction("Reference No.",CREATEDATETIME("Due Date",TempTimelineEvent.DefaultTime));
                               State := State::"Unsaved Changes";
                             END;
                           END;
                            }

    { 12  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity on the supply order, when an action message suggests a change to the quantity on the order.;
                           NOR=Angir antallet pÜ forsyningsordren nÜr en handlingsmelding foreslÜr Ü endre antallet pÜ ordren.;
                           SVE=Anger antalet pÜ leveransordern nÑr ett ÜtgÑrdsmeddelande fîreslÜr att antalet fîr en order ska Ñndras.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Original Quantity" }

    { 11  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity of the suggested supply order that the line on the Event Changes FastTab represents.;
                           NOR=Angir antallet for den foreslÜtte forsyningsordren som linjen pÜ hurtigfanen Hendelsesendringer representerer.;
                           SVE=Anger antalet fîr leveransorderfîrslaget som raden pÜ snabbfliken HÑndelseÑndringar representerar.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             IF Quantity <> xRec.Quantity THEN BEGIN
                               CurrPage.Visualization.ChangeTransactionQuantity("Reference No.",Quantity);
                               State := State::"Unsaved Changes";
                             END;
                           END;
                            }

    { 18  ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the reference number for the item in the timeline event change table.;
                           NOR=Angir referansenummeret for varen i tabellen for endring av tidslinjehendelse.;
                           SVE=Anger referensnumret fîr artikeln i tabellen Tidslinje fîr hÑndelseÑndring.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Reference No.";
                Visible=false }

  }
  CODE
  {
    VAR
      Item@1004 : Record 27;
      TempTimelineEvent@1015 : TEMPORARY Record 5540;
      CalcItemAvailTimeline@1023 : Codeunit 5540;
      ItemNo@1003 : Code[20];
      LocationFilter@1009 : Text;
      VariantFilter@1008 : Text;
      ForecastName@1005 : Code[10] INDATASET;
      TemplateNameNewSupply@1018 : Code[10];
      WorksheetNameNewSupply@1017 : Code[10];
      Description@1006 : Text[250];
      ActionMsg@1013 : ' ,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel';
      State@1024 : ' ,Initialized,Unsaved Changes';
      LastUpdateTime@1016 : DateTime;
      IncludeBlanketOrders@1014 : Boolean INDATASET;
      Text001@1022 : TextConst 'ENU=The changes have been saved to the planning or requisition worksheet.;NOR=Endringene er lagret i planleggings- eller bestillingsforslaget.;SVE=éndringarna har sparats i planeringsfîrslaget eller rekvisitionsfîrslaget.';
      CurrReferenceNo@1057 : Text[200];
      BlockCurrRecNotifications@1007 : Boolean;
      Text002@1012 : TextConst 'ENU=The changes have been saved to the planning or requisition worksheet.\\New supply is saved to worksheet Template Name %1, Batch Name %2, Location Code %3, and Variant Code %4.;NOR=Endringene er blitt lagret i planleggings- eller bestillingsforslaget.\\Ny forsyning lagres i forslaget med malnavn %1, bunkenavn %2, lokasjonskode %3 og variantkode %4.;SVE=éndringarna har sparats i planerings- eller rekvisitionsfîrslaget.\\Ny leverans sparas i fîrslaget Mallnamn %1, Journalnamn %2, LagerstÑllekod %3 och Variantkod %4.';
      Text003@1019 : TextConst 'ENU=All unsaved changes will not be saved. Are you sure that you want to close the window?;NOR=Ingen ulagrede endringer vil bli lagret. Er du sikker pÜ at du vil lukke vinduet?;SVE=éndringar som inte har sparats gÜr fîrlorade. Vill du stÑnga fînstret?';
      Text004@1020 : TextConst 'ENU=Show Projected Available Balance;NOR=Vis forventet disponibel beholdning;SVE=Visa lagerutveckling îver tiden';
      Text005@1021 : TextConst 'ENU=Include Planning Suggestions;NOR=Inkluder planleggingsforslag;SVE=Ta med planeringsfîrslag';
      Text006@1000 : TextConst 'ENU=Workdate;NOR=Arbeidsdato;SVE=Arbetsdatum';
      Text007@1001 : TextConst 'ENU=Today;NOR=I dag;SVE=Dagens datum';
      IgnoreChanges@1025 : Boolean;
      ItemSelected@1002 : Boolean INDATASET;
      Text008@1010 : TextConst 'ENU=New Supply %1;NOR=Ny forsyning %1;SVE=Ny leverans %1';
      Text009@1011 : TextConst 'ENU=Do you want to delete?;NOR=Vil du slette?;SVE=Vill du utfîra borttagningen?';

    PROCEDURE InitAndCreateTimelineEvents@14();
    BEGIN
      InitTimeline;
      CreateTimelineEvents;
      SetScenarioTitles;
      State := State::Initialized;
    END;

    LOCAL PROCEDURE InitTimeline@1();
    VAR
      timeSpan@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.TimeSpan";
    BEGIN
      CurrPage.Visualization.RemoveSpecialDates;
      CurrPage.Visualization.AddSpecialDate(CREATEDATETIME(WORKDATE,TempTimelineEvent.DefaultTime),Text006);
      CurrPage.Visualization.AddSpecialDate(CREATEDATETIME(TODAY,TempTimelineEvent.DefaultTime),Text007);
      CurrPage.Visualization.TransactionPrecision := Decimals(Item."Rounding Precision");
      CurrPage.Visualization.TransactionUnitOfMeasure := Item."Base Unit of Measure";
      CurrPage.Visualization.InitialLevelDateMargin := timeSpan.FromDays(CalcItemAvailTimeline.InitialTimespanDays);
      CurrPage.Visualization.FinalLevelDateMargin := timeSpan.FromDays(CalcItemAvailTimeline.FinalTimespanDays);
    END;

    LOCAL PROCEDURE UpdateItemRequestFields@11(VAR Item@1000 : Record 27);
    BEGIN
      ItemNo := Item."No.";
      LocationFilter := '';
      IF Item.GETFILTER("Location Filter") <> '' THEN
        LocationFilter := Item.GETFILTER("Location Filter");
      VariantFilter := '';
      IF Item.GETFILTER("Variant Filter") <> '' THEN
        VariantFilter := Item.GETFILTER("Variant Filter");
    END;

    LOCAL PROCEDURE PageCaption@5() : Text[250];
    BEGIN
      EXIT(STRSUBSTNO('%1 %2',Item."No.",Item.Description));
    END;

    [External]
    PROCEDURE SetItem@3(VAR NewItem@1000 : Record 27);
    BEGIN
      Item.COPY(NewItem);
      UpdateItemRequestFields(Item);
    END;

    [External]
    PROCEDURE SetForecastName@2(NewForcastName@1000 : Code[10]);
    BEGIN
      ForecastName := NewForcastName;
    END;

    [External]
    PROCEDURE SetWorksheet@8(NewTemplateName@1001 : Code[10];NewWorksheetName@1000 : Code[10]);
    BEGIN
      TemplateNameNewSupply := NewTemplateName;
      WorksheetNameNewSupply := NewWorksheetName;
    END;

    LOCAL PROCEDURE ExportDataToTimeline@19(VAR TimelineEvent@1003 : Record 5540);
    VAR
      transactionTable@1002 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionDataTable";
      visualizationScenarios@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.TimelineVisualization, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.TimelineVisualization.VisualizationScenarios";
      WithForecast@1001 : Integer;
      WithForecastAndAdjustments@1004 : Integer;
    BEGIN
      TimelineEvent.TransferToTransactionTable(TimelineEvent,transactionTable);

      WithForecast := 2; // Enum value for CoreTransactionsWithForecast check box
      WithForecastAndAdjustments := 4; // Enum value for CoreTransactionsWithForecastAndAdjustments check box
      visualizationScenarios := WithForecast + WithForecastAndAdjustments; // Show the specified check boxes
      CurrPage.Visualization.SetTransactions(transactionTable,visualizationScenarios);
    END;

    LOCAL PROCEDURE ImportChangesFromTimeline@18();
    VAR
      changeTable@1003 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesDataTable";
    BEGIN
      BlockCurrRecNotifications := TRUE;

      changeTable := CurrPage.Visualization.GetTransactionChanges;
      TransferFromTransactionChangeTable(Rec,changeTable);
      SetCurrentChangeRec(CurrReferenceNo);

      BlockCurrRecNotifications := FALSE;
    END;

    LOCAL PROCEDURE CreateTimelineEvents@17();
    BEGIN
      RESET;
      DELETEALL;
      TempTimelineEvent.DELETEALL;
      Item.SETRANGE("Drop Shipment Filter",FALSE);
      CalcItemAvailTimeline.Initialize(Item,ForecastName,IncludeBlanketOrders,0D,TRUE);
      CalcItemAvailTimeline.CreateTimelineEvents(TempTimelineEvent);
      ExportDataToTimeline(TempTimelineEvent);
      LastUpdateTime := CURRENTDATETIME;
    END;

    LOCAL PROCEDURE DeleteSelectedRecords@10();
    VAR
      TempTimelineEventChange@1001 : TEMPORARY Record 5541;
    BEGIN
      TempTimelineEventChange.COPY(Rec,TRUE);
      CurrPage.SETSELECTIONFILTER(TempTimelineEventChange);
      IF TempTimelineEventChange.FINDSET THEN BEGIN
        IgnoreChanges := TRUE;
        REPEAT
          CurrPage.Visualization.DeleteTransaction(TempTimelineEventChange."Reference No.");
        UNTIL TempTimelineEventChange.NEXT = 0;
        ImportChangesFromTimeline;
        IgnoreChanges := FALSE;
        IF (State = State::"Unsaved Changes") AND (COUNT = 0) THEN
          State := State::Initialized;
      END;
    END;

    LOCAL PROCEDURE ShowDocument@4(RefNo@1001 : Text[255]);
    VAR
      ReferenceNo@1000 : Integer;
    BEGIN
      IF EVALUATE(ReferenceNo,RefNo) THEN BEGIN
        TempTimelineEvent.GET(ReferenceNo);
        CalcItemAvailTimeline.ShowDocument(TempTimelineEvent."Source Document ID");
      END;
    END;

    LOCAL PROCEDURE SetCurrentChangeRec@20(RefNo@1000 : Text[255]);
    BEGIN
      IF ISEMPTY OR (RefNo = '') THEN
        EXIT;

      SETRANGE("Reference No.",RefNo);
      IF FINDFIRST THEN BEGIN
        CurrPage.SETRECORD(Rec);
        CurrPage.UPDATE(FALSE);
      END;
      SETRANGE("Reference No.");
    END;

    LOCAL PROCEDURE TimelineEventDescription@12() : Text[250];
    VAR
      ReferenceNo@1000 : Integer;
    BEGIN
      IF NewSupply THEN
        EXIT(STRSUBSTNO(Text008,FORMAT("Due Date")));

      IF EVALUATE(ReferenceNo,"Reference No.") THEN
        IF TempTimelineEvent.GET(ReferenceNo) THEN
          EXIT(TempTimelineEvent.Description);

      EXIT("Reference No.");
    END;

    LOCAL PROCEDURE SetScenarioTitles@6();
    VAR
      visualizationScenarios@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.TimelineVisualization, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.TimelineVisualization.VisualizationScenarios";
    BEGIN
      CurrPage.Visualization.SetScenarioTitle(visualizationScenarios.CoreTransactionsWithForecast,Text004);
      CurrPage.Visualization.SetScenarioTitle(visualizationScenarios.CoreTransactionsWithForecastAndAdjustments,Text005);
    END;

    LOCAL PROCEDURE TransferChangesToPlanningWksh@7();
    VAR
      LocationCodeNewSupply@1002 : Code[10];
      VariantCodeNewSupply@1001 : Code[10];
      NewSupplyTransfer@1000 : Boolean;
    BEGIN
      NewSupplyTransfer :=
        CalcItemAvailTimeline.TransferChangesToPlanningWksh(
          Rec,ItemNo,LocationFilter,VariantFilter,TemplateNameNewSupply,WorksheetNameNewSupply);

      IF NOT NewSupplyTransfer THEN
        MESSAGE(Text001)
      ELSE BEGIN
        IF LocationCodeNewSupply = '' THEN
          LocationCodeNewSupply := CalcItemAvailTimeline.BlankValue;
        IF VariantCodeNewSupply = '' THEN
          VariantCodeNewSupply := CalcItemAvailTimeline.BlankValue;
        MESSAGE(Text002,TemplateNameNewSupply,WorksheetNameNewSupply,LocationCodeNewSupply,VariantCodeNewSupply)
      END;
    END;

    LOCAL PROCEDURE Decimals@29(Precision@1000 : Decimal) Decimals : Integer;
    BEGIN
      IF (Precision <= 0) OR (Precision > 1) THEN
        EXIT(DefaultPrecision);

      Decimals := 0;

      WHILE (Decimals <= MaxPrecision) AND (ROUND(Precision,1,'>') > Precision) DO BEGIN
        Decimals += 1;
        Precision *= 10;
      END;

      EXIT(Decimals);
    END;

    LOCAL PROCEDURE DefaultPrecision@35() : Integer;
    BEGIN
      EXIT(5);
    END;

    LOCAL PROCEDURE MaxPrecision@39() : Integer;
    BEGIN
      EXIT(5);
    END;

    EVENT Visualization@-5::TransactionActivated@30(refNo@1000 : Text);
    BEGIN
      ShowDocument(refNo);
    END;

    EVENT Visualization@-5::SelectedTransactionChanged@31(refNo@1000 : Text);
    BEGIN
      SetCurrentChangeRec(refNo);
    END;

    EVENT Visualization@-5::TransactionValueChanged@32(refNo@1000 : Text);
    BEGIN
      IF IgnoreChanges THEN
        EXIT;

      IF refNo <> '' THEN BEGIN // Specific Transaction Value Changed
        State := State::"Unsaved Changes";
        IF NewSupply THEN
          CurrReferenceNo := COPYSTR(refNo,1,MAXSTRLEN("Reference No."));
      END;

      ImportChangesFromTimeline;
    END;

    EVENT Visualization@-5::AddInReady@33();
    BEGIN
      IF ItemSelected THEN
        InitAndCreateTimelineEvents;
    END;

    BEGIN
    END.
  }
}

