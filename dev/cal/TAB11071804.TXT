OBJECT Table 11071804 Service Survey Location
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceSetup@1100528600 : Record 11012800;
             BEGIN
               IF "Location Structure Format" = '' THEN BEGIN
                 ServiceSetup.GET;
                 "Location Structure Format" := ServiceSetup."Def. Location Structure Format";
               END;
             END;

    OnDelete=VAR
               ServSurveyLocStructure@1100528600 : Record 11071805;
               ServSurvLocationObject@1100528601 : Record 11071813;
             BEGIN
               ServSurveyLocStructure.SETRANGE("Survey No.", "Survey No.");
               ServSurveyLocStructure.SETRANGE("Location Code", "Location Code");
               ServSurveyLocStructure.DELETEALL(TRUE);

               ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
               ServSurvLocationObject.SETRANGE("Location Code", "Location Code");
               ServSurvLocationObject.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Service Site Survey Location;
               NOR=Kartleggingslokasjon for Serviceplass;
               SVE=Serviceplats kartl„ggn.plats];
    LookupPageID=Page11126254;
    DrillDownPageID=Page11126254;
  }
  FIELDS
  {
    { 10  ;   ;Survey No.          ;Code20        ;TableRelation="Service Survey".No.;
                                                   CaptionML=[ENU=Survey No.;
                                                              NOR=Kartlegging nr.;
                                                              SVE=Kartl„ggningsnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Location Code       ;Code10        ;CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 50  ;   ;Address             ;Text100       ;OnValidate=BEGIN
                                                                VALIDATE("Post Code");
                                                              END;

                                                   CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress];
                                                   Editable=Yes }
    { 60  ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2;
                                                              SVE=Adress 2];
                                                   Editable=Yes }
    { 70  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City, "Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                                NormalizePostCodeHouseNo();
                                                                DetermineServiceLocation();
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr;
                                                              SVE=Postnr];
                                                   Editable=Yes }
    { 75  ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              NOR=Fylke/delstat;
                                                              SVE=Delstat];
                                                   Editable=Yes }
    { 80  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              NOR=Lands-/regionkode;
                                                              SVE=Lands-/regionkod] }
    { 90  ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              NOR=Sted;
                                                              SVE=Ort];
                                                   Editable=Yes }
    { 110 ;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              NOR=Telefonnr.;
                                                              SVE=Telefonnr] }
    { 120 ;   ;Fax No.             ;Text30        ;CaptionML=[ENU=Fax No.;
                                                              NOR=Telefaxnr;
                                                              SVE=Telefaxnr] }
    { 130 ;   ;E-Mail              ;Text80        ;CaptionML=[ENU=Email;
                                                              NOR=E-post;
                                                              SVE=E-post] }
    { 140 ;   ;Mobile Phone No.    ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Mobile Phone No.;
                                                              NOR=Mobiltelefonnr;
                                                              SVE=Mobiltelefonnr] }
    { 150 ;   ;Customer Feature    ;Text30        ;CaptionML=[ENU=Customer Feature;
                                                              NOR=Kundefunksjon;
                                                              SVE=Kundfunktion] }
    { 160 ;   ;Complex Code        ;Code20        ;TableRelation=Complex;
                                                   CaptionML=[ENU=Complex Code;
                                                              NOR=Komplekskode;
                                                              SVE=Servicekomplexkod] }
    { 170 ;   ;Location Structure Format;Code20   ;OnValidate=VAR
                                                                ServiceSurveyStructureLine@1100528602 : Record 11071806;
                                                                pos@1100528600 : Integer;
                                                                Level@1100528601 : Integer;
                                                              BEGIN
                                                                IF "Location Structure Format" = '' THEN
                                                                  EXIT;

                                                                Level := 1;
                                                                REPEAT
                                                                  pos += 1;
                                                                  IF COPYSTR("Location Structure Format", pos, 1) = '.' THEN
                                                                    Level += 1;
                                                                UNTIL pos = STRLEN("Location Structure Format");
                                                                IF Level > 3 THEN
                                                                  ERROR(Text000);

                                                                ServiceSurveyStructureLine.SETRANGE("Survey No.", "Survey No.");
                                                                ServiceSurveyStructureLine.SETRANGE("Location Code", "Location Code");
                                                                IF NOT ServiceSurveyStructureLine.ISEMPTY THEN
                                                                  ERROR(Text001, ServiceSurveyStructureLine.TABLECAPTION);
                                                              END;

                                                   CaptionML=[ENU=Location Structure Format;
                                                              NOR=Plassering, Strukturformat;
                                                              SVE=Lagerställe, strukturformat] }
    { 190 ;   ;Include in Contract ;Boolean       ;InitValue=Yes;
                                                   OnValidate=VAR
                                                                ServSurvLocationObject@1100528600 : Record 11071813;
                                                              BEGIN
                                                                ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
                                                                ServSurvLocationObject.SETRANGE("Location Code", "Location Code");
                                                                ServSurvLocationObject.MODIFYALL("Include in Contract", ServSurvLocationObject."Include in Contract"::"With Maintenance", TRUE);
                                                              END;

                                                   CaptionML=[ENU=Include in Contract;
                                                              NOR=Inkluder i kontrakt;
                                                              SVE=Inkludera i kontrakt] }
    { 200 ;   ;Service Location No.;Code20        ;TableRelation="Service Location";
                                                   OnValidate=VAR
                                                                ServiceLocation@1100528600 : Record 11012801;
                                                                CopyServiceLocationInfo@1100528601 : Boolean;
                                                              BEGIN
                                                                IF ServiceLocation.GET("Service Location No.") THEN BEGIN
                                                                  TestServiceLocationIsUnique;
                                                                  IF NOT AutoConfirmServiceLocationSelection THEN
                                                                    CopyServiceLocationInfo := CONFIRM(Text002)
                                                                  ELSE
                                                                    CopyServiceLocationInfo := TRUE;
                                                                  IF CopyServiceLocationInfo THEN BEGIN
                                                                    Description := ServiceLocation.Description;
                                                                    "Description 2" := ServiceLocation."Description 2";
                                                                    Address := ServiceLocation.Address;
                                                                    "Address 2" := ServiceLocation."Address 2";
                                                                    "Post Code" := ServiceLocation."Post Code";
                                                                    "Country/Region Code" := ServiceLocation."Country/Region Code";
                                                                    City := ServiceLocation.City;
                                                                    "Phone No." := ServiceLocation."Phone No.";
                                                                    "Fax No." := ServiceLocation."Fax No.";
                                                                    "E-Mail" := ServiceLocation."E-Mail";
                                                                    "Mobile Phone No." := ServiceLocation."Mobile Phone No.";
                                                                    "Customer Feature" := ServiceLocation."Customer Feature";
                                                                    "Complex Code" := ServiceLocation."Complex Code";
                                                                    "House No." := ServiceLocation."House No.";
                                                                    "House No. Ext." := ServiceLocation."House No. Ext.";
                                                                    "Service Location Created" := TRUE;
                                                                    "Location Structure Format" := ServiceLocation."Location Structure Format";
                                                                    Latitude := ServiceLocation.Latitude;
                                                                    Longitude := ServiceLocation.Longitude;
                                                                  END;
                                                                  CopyServiceLocationStucture;
                                                                END;
                                                                IF "Service Location No." = '' THEN
                                                                  "Service Location Created" := FALSE;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceLocation@1100528600 : Record 11012801;
                                                              ServiceSurvey@1100528601 : Record 11071803;
                                                              ContractObject@1100528602 : Record 11071702;
                                                            BEGIN
                                                              ServiceSurvey.GET("Survey No.");
                                                              IF ServiceSurvey."Existing Contract No." = '' THEN BEGIN
                                                                ServiceLocation.SETCURRENTKEY("Post Code", "House No.");
                                                                IF "Post Code" <> '' THEN
                                                                  ServiceLocation.SETRANGE("Post Code", "Post Code");
                                                                IF "House No." <> '' THEN
                                                                  ServiceLocation.SETRANGE("House No.", "House No.");
                                                                IF "House No. Ext." <> '' THEN
                                                                  ServiceLocation.SETRANGE("House No. Ext.", "House No. Ext.");
                                                              END ELSE BEGIN
                                                                ContractObject.SETRANGE("Service Contract No.", ServiceSurvey."Existing Contract No.");
                                                                ContractObject.SETFILTER("Starting Date", '<=%1', ServiceSurvey."Reference Date");
                                                                ContractObject.SETFILTER("Ending Date", '%1|>%2', 0D, ServiceSurvey."Reference Date");
                                                                ContractObject.SETFILTER("Service Location No.", '<>%1', '');
                                                                IF ContractObject.FINDSET THEN
                                                                  REPEAT
                                                                    ServiceLocation."No." := ContractObject."Service Location No.";
                                                                    ServiceLocation.MARK(TRUE);
                                                                  UNTIL ContractObject.NEXT = 0;
                                                                ServiceLocation.MARKEDONLY(TRUE);
                                                              END;
                                                              IF "Service Location No." <> '' THEN
                                                                ServiceLocation.GET("Service Location No.");
                                                              IF PAGE.RUNMODAL(0, ServiceLocation) = ACTION::LookupOK THEN
                                                                VALIDATE("Service Location No.", ServiceLocation."No.");
                                                            END;

                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr] }
    { 210 ;   ;Service Location Created;Boolean   ;CaptionML=[ENU=Service Location Created;
                                                              NOR=Servicelokasjon opprettet;
                                                              SVE=Serviceplats skapad];
                                                   Editable=No }
    { 220 ;   ;House No.           ;Code20        ;CaptionML=[ENU=House No.;
                                                              NOR=Husnr.;
                                                              SVE=Husnr];
                                                   Editable=No }
    { 230 ;   ;House No. Ext.      ;Code10        ;CaptionML=[ENU=House No. Extension;
                                                              NOR=Husnr ETA;
                                                              SVE=Husnr ÄTA];
                                                   Editable=No }
    { 240 ;   ;Total Cost Amount   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Location Object"."Cost Amount" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                      Location Code=FIELD(Location Code)));
                                                   CaptionML=[ENU=Total Cost Amount;
                                                              NOR=Totalt kostbel›p;
                                                              SVE=Totalt kostnadsbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 250 ;   ;Total Sales Amount  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Location Object"."Calculated Sales Amount" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                                  Location Code=FIELD(Location Code)));
                                                   CaptionML=[ENU=Total Sales Amount;
                                                              NOR=Totalt salgsbel›p;
                                                              SVE=Totalt f”rs„ljningsbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 260 ;   ;Total Hours         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Location Object"."Total Hours" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                      Location Code=FIELD(Location Code)));
                                                   CaptionML=[ENU=Total Hours;
                                                              NOR=Timer totalt;
                                                              SVE=Timmar totalt];
                                                   Editable=No }
    { 270 ;   ;Latitude            ;Code30        ;OnValidate=BEGIN
                                                                ValidateGPSCoordinate(Latitude);
                                                              END;

                                                   CaptionML=[ENU=Latitude;
                                                              NOR=Breddegrad;
                                                              SVE=Latitude];
                                                   CharAllowed=09.. }
    { 280 ;   ;Longitude           ;Code30        ;OnValidate=BEGIN
                                                                ValidateGPSCoordinate(Longitude);
                                                              END;

                                                   CaptionML=[ENU=Longitude;
                                                              NOR=Lengdegrad;
                                                              SVE=Longitude];
                                                   CharAllowed=09.. }
  }
  KEYS
  {
    {    ;Survey No.,Location Code                ;Clustered=Yes }
    {    ;Survey No.,Post Code                     }
    {    ;Survey No.,Latitude,Longitude            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PostCode@1210190000 : Record 225;
      Text000@1100528600 : TextConst 'ENU=Only 3 levels allowed;NOR=kun tre niv†er tillates;SVE=Endast tre niv†er till†ts';
      Text001@1100528601 : TextConst 'ENU=%1 already exists;NOR=%1 finnes allerede;SVE=%1 finns redan.';
      Text002@1100528602 : TextConst 'ENU=Copy Address of Service Location?;NOR=Kopiere adresse for servicelokasjon?;SVE=Kopiera adress f”r serviceplats?';
      Text003@1100528603 : TextConst 'ENU=Service Location %1 already exists in this Survey';
      AutoConfirmServiceLocationSelection@1100528604 : Boolean;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimMgt@1100525000 : Codeunit 408;
      DimValRec@1100525001 : Record 349;
    BEGIN
      IF ShortcutDimCode = '' THEN EXIT;
      DimMgt.ValidateDimValueCode(FieldNumber, ShortcutDimCode);
      DimMgt.GetDimValueRec(FieldNumber, ShortcutDimCode, DimValRec, TRUE,'');
      IF NOT DimMgt.CheckDimValue(DimValRec."Dimension Code", ShortcutDimCode) THEN
        ERROR(DimMgt.GetDimErr);
    END;

    PROCEDURE NormalizePostCodeHouseNo@1100526200();
    VAR
      ObjectImpWorkSheetMgt@1100526200 : Codeunit 11012839;
      PostCode@1100526201 : Code[20];
    BEGIN
      IF ObjectImpWorkSheetMgt.NormalizePostCode("Post Code", "Country/Region Code", PostCode) THEN
        "Post Code" := PostCode;
      "House No." := ObjectImpWorkSheetMgt.DetermineHouseNo(Address, "House No. Ext.");
    END;

    PROCEDURE GetMarginPerc@1100528603() MarginPerc : Decimal;
    BEGIN
      CALCFIELDS("Total Cost Amount", "Total Sales Amount");
      IF "Total Sales Amount" <> 0 THEN
        MarginPerc := ("Total Sales Amount" - "Total Cost Amount") / "Total Sales Amount" * 100
      ELSE
        MarginPerc := 0;
    END;

    PROCEDURE GetNextLocationCode@1100528600() : Code[10];
    VAR
      ServiceSurveyLocation@1100528600 : Record 11071804;
    BEGIN
      ServiceSurveyLocation.SETRANGE("Survey No.", "Survey No.");
      IF NOT ServiceSurveyLocation.FINDLAST THEN
        EXIT('001')
      ELSE
        EXIT(INCSTR(ServiceSurveyLocation."Location Code"));
    END;

    PROCEDURE DetermineServiceLocation@1100528601();
    VAR
      ServiceLocation@1100528600 : Record 11012801;
    BEGIN
      "Service Location No." := '';
      ServiceLocation.SETCURRENTKEY("Post Code");
      ServiceLocation.SETRANGE("Post Code", "Post Code");
      ServiceLocation.SETRANGE("House No.", "House No.");
      ServiceLocation.SETRANGE("House No. Ext.", "House No. Ext.");
      IF ServiceLocation.FINDFIRST THEN
        "Service Location No." := ServiceLocation."No.";
    END;

    PROCEDURE ShowDetails@1100528616();
    VAR
      ServSurvLocObjDetail@1100528600 : Record 11071807;
    BEGIN
      ServSurvLocObjDetail.SETRANGE("Survey No.", "Survey No.");
      ServSurvLocObjDetail.SETRANGE("Location Code", "Location Code");
      PAGE.RUNMODAL(PAGE::"Service Survey Line Details", ServSurvLocObjDetail);
    END;

    LOCAL PROCEDURE CopyServiceLocationStucture@1100528602();
    VAR
      ServiceLocationStructure@1100528600 : Record 11071719;
      ServiceLocStructureLine@1100528601 : Record 11071720;
      ServSurveyLocStructure@1100528602 : Record 11071805;
      ServiceSurveyStructureLine@1100528603 : Record 11071806;
    BEGIN
      IF "Service Location No." = '' THEN
        EXIT;
      ServiceLocationStructure.SETRANGE("Service Location No.", "Service Location No.");
      IF ServiceLocationStructure.FINDSET THEN
        REPEAT
          IF NOT ServSurveyLocStructure.GET("Survey No.", "Location Code", ServiceLocationStructure."No.") THEN BEGIN
            ServSurveyLocStructure.INIT;
            ServSurveyLocStructure."Survey No." := "Survey No.";
            ServSurveyLocStructure."Location Code" := "Location Code";
            ServSurveyLocStructure."Structure No." := ServiceLocationStructure."No.";
            ServSurveyLocStructure.Description := ServiceLocationStructure.Description;
            ServSurveyLocStructure."Available for 4PS Mobile" := ServiceLocationStructure."Available for Field Service";
            ServSurveyLocStructure.INSERT(TRUE);

            ServiceLocStructureLine.SETRANGE("Service Location No.", ServiceLocationStructure."Service Location No.");
            ServiceLocStructureLine.SETRANGE("Structure No.", ServiceLocationStructure."No.");
            ServiceLocStructureLine.SETRANGE("Object Line", FALSE);
            IF ServiceLocStructureLine.FINDSET THEN
              REPEAT
                ServiceSurveyStructureLine.INIT;
                ServiceSurveyStructureLine."Survey No." := ServSurveyLocStructure."Survey No.";
                ServiceSurveyStructureLine."Location Code" := ServSurveyLocStructure."Location Code";
                ServiceSurveyStructureLine."Structure No." := ServSurveyLocStructure."Structure No.";
                ServiceSurveyStructureLine.Code := ServiceLocStructureLine."No.";
                ServiceSurveyStructureLine."Element Level 1" := ServiceLocStructureLine."Element Level 1";
                ServiceSurveyStructureLine."Element Level 2" := ServiceLocStructureLine."Element Level 2";
                ServiceSurveyStructureLine.Level := ServiceLocStructureLine.Level;
                ServiceSurveyStructureLine."Code Description" := ServiceLocStructureLine.Description;
                ServiceSurveyStructureLine.INSERT(TRUE);
              UNTIL ServiceLocStructureLine.NEXT = 0;
          END;
        UNTIL ServiceLocationStructure.NEXT = 0;
    END;

    LOCAL PROCEDURE TestServiceLocationIsUnique@1100528624();
    VAR
      ServiceSurveyLocation@1100528600 : Record 11071804;
    BEGIN
      IF "Service Location No." = '' THEN
        EXIT;
      ServiceSurveyLocation.SETRANGE("Survey No.", "Survey No.");
      ServiceSurveyLocation.SETRANGE("Service Location No.", "Service Location No.");
      ServiceSurveyLocation.SETFILTER("Location Code", '<>%1', "Location Code");
      IF NOT ServiceSurveyLocation.ISEMPTY THEN
        ERROR(Text003, "Service Location No.");
    END;

    PROCEDURE SetAutoConfirmServiceLocationSelection@1100528604(IAutoConfirmServiceLocationSelection@1100528600 : Boolean);
    BEGIN
      AutoConfirmServiceLocationSelection := IAutoConfirmServiceLocationSelection;
    END;

    LOCAL PROCEDURE ValidateGPSCoordinate@1100529400(Value@1100529400 : Code[30]);
    VAR
      ServiceLocation@1100528400 : Record 11012801;
    BEGIN
      ServiceLocation.ValidateGPSCoordinate(Value);
    END;

    BEGIN
    END.
  }
}

