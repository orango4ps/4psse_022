OBJECT Table 11071716 Object Filter
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Code);
               "Creation Date" := TODAY;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               ObjectFilterLine@1210190000 : Record 11071718;
             BEGIN
               ObjectFilterLine.SETRANGE("Filter Code", Code);
               ObjectFilterLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               ObjectFilterLine@1210190000 : Record 11071718;
               NewObjectFilterLine@1210190001 : Record 11071718;
             BEGIN
               IF Code <> xRec.Code THEN BEGIN
                 ObjectFilterLine.SETRANGE("Filter Code", xRec.Code);
                 IF ObjectFilterLine.FINDFIRST THEN
                   REPEAT
                     NewObjectFilterLine := ObjectFilterLine;
                     NewObjectFilterLine."Filter Code" := Code;
                     NewObjectFilterLine.INSERT(TRUE);
                     ObjectFilterLine.DELETE(TRUE);
                   UNTIL ObjectFilterLine.NEXT = 0;
               END;
             END;

    CaptionML=[ENU=Object Filter;
               NOR=Objektfilter;
               SVE=Objektfilter];
    LookupPageID=Page11126096;
    DrillDownPageID=Page11126096;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 20  ;   ;Expired             ;Boolean       ;CaptionML=[ENU=Expired;
                                                              NOR=Utg†tt;
                                                              SVE=Utg†ngna] }
    { 30  ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettelsesdato;
                                                              SVE=Uppl„ggningsdatum];
                                                   Editable=No }
    { 40  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 50  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=Progress      @1@@@@@@@@@@@\Found Objects #2###########;NOR=Fremdrift @1@@@@@@@@@@@\Objekter funnet #2###########;SVE=F”rlopp      @1@@@@@@@@@@@\Hittade objekt #2###########';

    PROCEDURE ProcessFilter@1210190000(VAR OTmpServiceObject@1210190000 : TEMPORARY Record 11071691;ShowDialogWindow@1100528400 : Boolean);
    VAR
      ObjectFilterLine@1210190001 : Record 11071718;
      ServiceObject@1210190004 : Record 11071691;
      ServiceObjectFromLocations@1100528603 : Record 11071691;
      StandardObject@1210190005 : Record 11020334;
      TmpServiceObject@1210190006 : TEMPORARY Record 11071691;
      ObjectSpecificationLine@1210190008 : Record 11071781;
      StdObjectSpecificationLine@1210190014 : Record 11071781;
      ServiceLocation@1100528600 : Record 11012801;
      ServiceContract@1100528601 : Record 11012812;
      ContractObject@1100528602 : Record 11071702;
      TableRef@1210190002 : RecordRef;
      FieldRef@1210190003 : FieldRef;
      ServiceObjectFound@1210190010 : Boolean;
      DialogMsg@1210190011 : Dialog;
      RecordCount@1210190012 : Integer;
      RecordCounter@1210190013 : Integer;
      RecordsFound@1210190016 : Integer;
    BEGIN
      TESTFIELD(Expired, FALSE);

      TmpServiceObject.COPY(OTmpServiceObject, TRUE); // Make sure given recordset is temporary

      OTmpServiceObject.RESET;
      OTmpServiceObject.DELETEALL;

      IF ShowDialogWindow THEN BEGIN
        DialogMsg.OPEN(Text000);
        DialogMsg.UPDATE(2, 0);
      END;

      TableRef.GETTABLE(ServiceContract);
      ObjectFilterLine.SETRANGE("Filter Code", Code);
      ObjectFilterLine.SETRANGE("Table Type", ObjectFilterLine."Table Type"::"Service Contract");
      ObjectFilterLine.SETFILTER("Value Filter", '<>%1', '');
      IF ObjectFilterLine.FINDSET THEN BEGIN
        REPEAT
          FieldRef := TableRef.FIELD(ObjectFilterLine."Field No.");
          FieldRef.SETFILTER(ObjectFilterLine."Value Filter");
        UNTIL ObjectFilterLine.NEXT = 0;
      //  ServiceContract.SETVIEW(TableRef.GETVIEW); // settable not working
        TableRef.SETTABLE(ServiceContract);
        IF ServiceContract.FINDSET THEN
          REPEAT
            ContractObject.SETRANGE("Service Contract No.", ServiceContract."No.");
            IF ContractObject.FINDSET THEN
              REPEAT
                ServiceObject."No." := ContractObject."Service Object No.";
                ServiceObject.MARK(TRUE);
              UNTIL ContractObject.NEXT = 0;
          UNTIL ServiceContract.NEXT = 0;
        ServiceObject.MARKEDONLY(TRUE);
      END;

      TableRef.GETTABLE(ServiceLocation);
      ObjectFilterLine.SETRANGE("Filter Code", Code);
      ObjectFilterLine.SETRANGE("Table Type", ObjectFilterLine."Table Type"::"Service Location");
      ObjectFilterLine.SETFILTER("Value Filter", '<>%1', '');
      IF ObjectFilterLine.FINDSET THEN BEGIN
        REPEAT
          FieldRef := TableRef.FIELD(ObjectFilterLine."Field No.");
          FieldRef.SETFILTER(ObjectFilterLine."Value Filter");
        UNTIL ObjectFilterLine.NEXT = 0;
      //  ServiceLocation.SETVIEW(TableRef.GETVIEW); // settable not working
        TableRef.SETTABLE(ServiceLocation);
        IF NOT ServiceLocation.ISEMPTY THEN BEGIN
          IF ServiceObject.MARKEDONLY THEN BEGIN
            GetObjectDSFromServiceLocations(ServiceLocation, ServiceObjectFromLocations);
            UnmarkServiceObjectDS(ServiceObject, ServiceObjectFromLocations);
          END ELSE
            GetObjectDSFromServiceLocations(ServiceLocation, ServiceObject);
        END ELSE
          ServiceObject.MARKEDONLY(TRUE);
      END;

      TableRef.GETTABLE(ServiceObject);
      TableRef.FILTERGROUP(7);
      ObjectFilterLine.SETRANGE("Filter Code", Code);
      ObjectFilterLine.SETRANGE("Table Type", ObjectFilterLine."Table Type"::"Service Object");
      ObjectFilterLine.SETFILTER("Value Filter", '<>%1', '');
      IF ObjectFilterLine.FINDSET THEN BEGIN
        REPEAT
          FieldRef := TableRef.FIELD(ObjectFilterLine."Field No.");
          FieldRef.SETFILTER(ObjectFilterLine."Value Filter");
        UNTIL ObjectFilterLine.NEXT = 0;
      END;
      ServiceObject.SETVIEW(TableRef.GETVIEW); // settable not working
      TableRef.FILTERGROUP(0);

      IF ServiceObject.FINDSET THEN BEGIN
        RecordCount := ServiceObject.COUNT;
        REPEAT
          RecordCounter +=1;
          IF ShowDialogWindow THEN
            DialogMsg.UPDATE(1, ROUND(RecordCounter / RecordCount * 9999, 1));
          ServiceObjectFound := TRUE;

          StandardObject.FILTERGROUP(7);
          StandardObject.SETRANGE("No.", ServiceObject."Standard Object No.");
          StandardObject.FILTERGROUP(0);
          ObjectFilterLine.SETRANGE("Table Type", ObjectFilterLine."Table Type"::"Standard Object");
          ObjectFilterLine.SETFILTER("Value Filter", '<>%1', '');
          IF ObjectFilterLine.FINDSET THEN BEGIN
            TableRef.GETTABLE(StandardObject);
            REPEAT
              FieldRef := TableRef.FIELD(ObjectFilterLine."Field No.");
              FieldRef.SETFILTER(ObjectFilterLine."Value Filter");
            UNTIL ObjectFilterLine.NEXT = 0;
            StandardObject.SETVIEW(TableRef.GETVIEW);
            IF StandardObject.ISEMPTY THEN
              ServiceObjectFound := FALSE;
          END;

          IF ServiceObjectFound THEN BEGIN
            ObjectFilterLine.SETRANGE("Table Type", ObjectFilterLine."Table Type"::"Object Specification");
            ObjectFilterLine.SETFILTER("Value Filter", '<>%1', '');
            IF ObjectFilterLine.FINDSET THEN BEGIN
              REPEAT
                StdObjectSpecificationLine.RESET;
                IF ServiceObject."Standard Object No." <> '' THEN BEGIN
                  StdObjectSpecificationLine.SETRANGE("Specification Type",
                    StdObjectSpecificationLine."Specification Type"::"Standard Object");
                  StdObjectSpecificationLine.SETRANGE(Code, ServiceObject."Standard Object No.");
                  StdObjectSpecificationLine.SETRANGE("Specification Code", ObjectFilterLine."Specification Code");
                  StdObjectSpecificationLine.SETRANGE("Object Level", FALSE);
                  IF StdObjectSpecificationLine.FINDFIRST THEN BEGIN //C024551 20-07-15 DJN
                    CASE StdObjectSpecificationLine."Field Type" OF
                      StdObjectSpecificationLine."Field Type"::Date:
                        StdObjectSpecificationLine.SETFILTER("Value (Date)", ObjectFilterLine."Value Filter");
                      StdObjectSpecificationLine."Field Type"::Value:
                        StdObjectSpecificationLine.SETFILTER("Value (Value)", ObjectFilterLine."Value Filter");
                      ELSE
                        StdObjectSpecificationLine.SETFILTER(Value, ObjectFilterLine."Value Filter");
                    END;
                  END;
                END;
                ObjectSpecificationLine.RESET;
                //DP00118.sn
                ObjectSpecificationLine.SETFILTER("Specification Type", '%1|%2',
                  ObjectSpecificationLine."Specification Type"::Object,
                  ObjectSpecificationLine."Specification Type"::"Object Line");
                //DP00118.en
                ObjectSpecificationLine.SETRANGE(Code, ServiceObject."No.");
                ObjectSpecificationLine.SETRANGE("Specification Code", ObjectFilterLine."Specification Code");
                IF ObjectSpecificationLine.FINDFIRST THEN BEGIN //C024551 20-07-15 DJN
                  CASE ObjectSpecificationLine."Field Type" OF
                    ObjectSpecificationLine."Field Type"::Date:
                      ObjectSpecificationLine.SETFILTER("Value (Date)", ObjectFilterLine."Value Filter");
                    ObjectSpecificationLine."Field Type"::Value:
                      ObjectSpecificationLine.SETFILTER("Value (Value)", ObjectFilterLine."Value Filter");
                    ELSE
                      ObjectSpecificationLine.SETFILTER(Value, ObjectFilterLine."Value Filter");
                  END;
                END;
                IF ((ServiceObject."Standard Object No." <> '') AND
                  StdObjectSpecificationLine.ISEMPTY AND ObjectSpecificationLine.ISEMPTY) OR
                  ((ServiceObject."Standard Object No." = '') AND ObjectSpecificationLine.ISEMPTY) THEN
                  ServiceObjectFound := FALSE;
              UNTIL NOT ServiceObjectFound OR (ObjectFilterLine.NEXT = 0);
            END;
          END;

          IF ServiceObjectFound THEN BEGIN
            OTmpServiceObject := ServiceObject;
            OTmpServiceObject.INSERT;
            RecordsFound += 1;
            IF ShowDialogWindow THEN
              DialogMsg.UPDATE(2, RecordsFound);
          END;
        UNTIL ServiceObject.NEXT = 0;
      END;

      IF ShowDialogWindow THEN
        DialogMsg.CLOSE;
    END;

    PROCEDURE GetObjectDSFromServiceLocations@1100528605(VAR IServiceLocation@1100528600 : Record 11012801;VAR OServiceObject@1100528601 : Record 11071691);
    VAR
      BuildingObjectRelation@1100528602 : Record 11071820;
    BEGIN
      OServiceObject.RESET;
      OServiceObject.SETCURRENTKEY("Service Location No.");
      IF IServiceLocation.FINDSET THEN BEGIN
        REPEAT
          OServiceObject.SETRANGE("Service Location No.", IServiceLocation."No.");
          IF OServiceObject.FINDSET THEN
            REPEAT
              OServiceObject.MARK(TRUE);
            UNTIL OServiceObject.NEXT = 0;
          IF IServiceLocation."Building Code" <> '' THEN BEGIN
            OServiceObject.SETCURRENTKEY("No.");
            OServiceObject.SETRANGE("Service Location No.");
            BuildingObjectRelation.SETRANGE("Building Code", IServiceLocation."Building Code");
            IF BuildingObjectRelation.FINDSET THEN
              REPEAT
                OServiceObject."No." := BuildingObjectRelation."Object No.";
                OServiceObject.MARK(TRUE);
              UNTIL BuildingObjectRelation.NEXT = 0;

          END;
        UNTIL IServiceLocation.NEXT = 0;
        OServiceObject.SETRANGE("Service Location No.");
      END;
      OServiceObject.SETCURRENTKEY("No.");
      OServiceObject.MARKEDONLY(TRUE);
    END;

    PROCEDURE UnmarkServiceObjectDS@1100528616(VAR IOFullServiceObject@1100528600 : Record 11071691;VAR IMarkedServiceObject@1100528601 : Record 11071691);
    BEGIN
      IF IOFullServiceObject.MARKEDONLY THEN BEGIN
        IF IOFullServiceObject.FINDSET THEN
          REPEAT
            IF IMarkedServiceObject.GET(IOFullServiceObject."No.") THEN
              IF NOT IMarkedServiceObject.MARK THEN
                IOFullServiceObject.MARK(FALSE);
          UNTIL IOFullServiceObject.NEXT = 0;
      END ELSE BEGIN
        IF IMarkedServiceObject.FINDSET THEN
          REPEAT
            IOFullServiceObject."No." := IMarkedServiceObject."No.";
            IOFullServiceObject.MARK(TRUE);
          UNTIL IMarkedServiceObject.NEXT = 0;
        IMarkedServiceObject.MARKEDONLY(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

