OBJECT Table 6086325 EM Attendee Inbox
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Company Name;
    OnInsert=VAR
               ExpenseAttendeesInbox@1160040000 : Record 6086325;
             BEGIN
               CheckInbox;
               IF "Entry No." = 0 THEN BEGIN
                 ExpenseAttendeesInbox.SETRANGE("Table ID","Table ID");
                 ExpenseAttendeesInbox.SETRANGE("Doc. Ref. No.","Doc. Ref. No.");
                 IF ExpenseAttendeesInbox.FINDLAST THEN
                   "Entry No." := ExpenseAttendeesInbox."Entry No." + 1
                 ELSE
                   "Entry No." := 1;
               END;
             END;

    OnModify=BEGIN
               CheckInbox;
             END;

    OnDelete=BEGIN
               CheckInbox;
             END;

    OnRename=BEGIN
               CheckInbox;
             END;

    CaptionML=[DEU=Teilnehmer Posteingang;
               ENU=Attendee Inbox;
               NLD=Deelnemer postvak IN];
    LookupPageID=Page6086326;
    DrillDownPageID=Page6086326;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=[DEU=Tabellen-ID;
                                                              ENU=Table ID;
                                                              NLD=Tabel-id;
                                                              SVE=Tabell-ID] }
    { 2   ;   ;Doc. Ref. No.       ;Integer       ;TableRelation=IF (Table ID=CONST(6086323)) "Expense Inbox"
                                                                 ELSE IF (Table ID=CONST(6086353)) "Mileage Inbox";
                                                   CaptionML=[DEU=Bel. Referenznr.;
                                                              ENU=Doc. Ref. No.;
                                                              NLD=Doc. Ref. nr.];
                                                   Editable=No }
    { 3   ;   ;Entry No.           ;Integer       ;CaptionML=[DEU=Lfd. Nr.;
                                                              ENU=Entry No.;
                                                              NLD=Volgnr.;
                                                              SVE=L”pnr] }
    { 10  ;   ;Type                ;Option        ;CaptionML=[DEU=Art;
                                                              ENU=Type;
                                                              NLD=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[DEU=Mitarbeiter,Gast;
                                                                    ENU=Employee,Guest;
                                                                    NLD=Werknemer,Gast];
                                                   OptionString=Employee,Guest }
    { 20  ;   ;Name                ;Text50        ;CaptionML=[DEU=Name;
                                                              ENU=Name;
                                                              NLD=Naam;
                                                              SVE=Namn] }
    { 30  ;   ;Company Name        ;Text50        ;CaptionML=[DEU=Mandantenname;
                                                              ENU=Company Name;
                                                              NLD=Bedrijfsnaam;
                                                              SVE=F”retagsnamn] }
  }
  KEYS
  {
    {    ;Table ID,Doc. Ref. No.,Entry No.        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      MultipleEmployees@1160040002 : TextConst 'DEU=%1 Mitarbeiter;ENU=%1 Employees;NLD=%1 werknemers';
      MultipleGuests@1160040000 : TextConst 'DEU=%1 G„ste;ENU=%1 Guests;NLD=%1 gasten';
      SingleEmployee@1160040003 : TextConst 'DEU=%1 Mitarbeiter;ENU=%1 Employee;NLD=%1 werknemer';
      SingleGuest@1160040001 : TextConst 'DEU=%1 Gast;ENU=%1 Guest;NLD=%1 gast';

    LOCAL PROCEDURE CheckInbox@1160040000();
    VAR
      ExpAllocationInbox@1160040002 : Record 6086322;
      ExpenseInbox@1160040000 : Record 6086323;
      MileageInbox@1160040001 : Record 6086353;
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Expense Inbox":
          BEGIN
            ExpenseInbox.GET("Doc. Ref. No.");
            IF ExpenseInbox.Status = ExpenseInbox.Status::Accepted THEN
              ExpenseInbox.TESTFIELD(Status,ExpenseInbox.Status::Error);
          END;
        DATABASE::"Mileage Inbox":
          BEGIN
            MileageInbox.GET("Doc. Ref. No.");
            IF MileageInbox.Status = MileageInbox.Status::Accepted THEN
              MileageInbox.TESTFIELD(Status,MileageInbox.Status::Error);
          END;
        DATABASE::"Expense Allocation Inbox":
          BEGIN
            ExpAllocationInbox.GET("Doc. Ref. No.");
            ExpenseInbox.GET(ExpAllocationInbox."Inbox Entry No.");
            IF ExpenseInbox.Status = ExpenseInbox.Status::Accepted THEN
              ExpenseInbox.TESTFIELD(Status,ExpenseInbox.Status::Error);
          END;
      END;
    END;

    PROCEDURE GetAttendeesForDisplay@1160040004(TableID@1160040003 : Integer;DocRefNo@1160040004 : Integer) DisplayTxt : Text[150];
    VAR
      ExpAttendeeInbox@1160040000 : Record 6086325;
      NoOfEmployees@1160040002 : Integer;
      NoOfGuests@1160040001 : Integer;
    BEGIN
      ExpAttendeeInbox.SETRANGE("Table ID",TableID);
      ExpAttendeeInbox.SETRANGE("Doc. Ref. No.",DocRefNo);
      CASE ExpAttendeeInbox.COUNT OF
        0:
          EXIT;

        1:
          BEGIN
            ExpAttendeeInbox.FINDFIRST;
            IF ExpAttendeeInbox.Name <> '' THEN
              EXIT(STRSUBSTNO('%1 (%2)',ExpAttendeeInbox.Name,ExpAttendeeInbox.Type))
            ELSE
              EXIT(STRSUBSTNO('%1 (%2)',ExpAttendeeInbox."Company Name",ExpAttendeeInbox.Type));
          END;

        ELSE BEGIN
          ExpAttendeeInbox.SETRANGE(Type,ExpAttendeeInbox.Type::Employee);
          NoOfEmployees := ExpAttendeeInbox.COUNT;
          ExpAttendeeInbox.SETRANGE(Type,ExpAttendeeInbox.Type::Guest);
          NoOfGuests := ExpAttendeeInbox.COUNT;

          IF NoOfEmployees = 1 THEN
            DisplayTxt := STRSUBSTNO(SingleEmployee,NoOfEmployees)
          ELSE
            DisplayTxt := STRSUBSTNO(MultipleEmployees,NoOfEmployees);

          IF NoOfEmployees > 0 THEN
            DisplayTxt := DisplayTxt + ', ';

          IF NoOfGuests = 1 THEN
            DisplayTxt := DisplayTxt + STRSUBSTNO(SingleGuest,NoOfGuests)
          ELSE
            DisplayTxt := DisplayTxt + STRSUBSTNO(MultipleGuests,NoOfGuests);

        END;
      END;
    END;

    BEGIN
    END.
  }
}

