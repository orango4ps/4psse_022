OBJECT Codeunit 6085595 Template Field Suggestions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.50;
  }
  PROPERTIES
  {
    TableNo=6085580;
    OnRun=VAR
            Template@161024012 : Record 6085579;
            Caption@161024014 : Record 6085581;
            Suggestion@161024013 : Record 6085587;
          BEGIN
            Suggestion.SETRANGE("Template No.");
            Suggestion.SETRANGE(Type,Type);
            Suggestion.SETRANGE(Code,Code);
            Suggestion.DELETEALL;

            Template.GET("Template No.");
            Template.SETRANGE("Category Code",Template."Category Code");
            Template.SETRANGE(Type,Template.Type::" ");
            IF Template.FINDSET(FALSE,FALSE) THEN
              REPEAT
                Caption.SETRANGE("Template No.",Template."No.");
                Caption.SETRANGE(Type,Type);
                Caption.SETRANGE(Code,Code);
                IF Caption.FINDSET(FALSE,FALSE) THEN
                  REPEAT
                    IF NOT Suggestion.GET("Template No.",Type,Code,COPYSTR(Caption.Caption,1,MAXSTRLEN(Suggestion."Search Caption")))
                    THEN BEGIN
                      Suggestion."Template No." := "Template No.";
                      Suggestion.Type := Type;
                      Suggestion.Code := Code;
                      Suggestion."Search Caption" := COPYSTR(Caption.Caption,1,MAXSTRLEN(Suggestion."Search Caption"));
                      Suggestion.Caption := Caption.Caption;
                      Suggestion."No. of Occurences" := 1;
                      Suggestion.INSERT;
                    END ELSE BEGIN
                      Suggestion."No. of Occurences" := Suggestion."No. of Occurences" + 1;
                      Suggestion.MODIFY;
                    END;
                  UNTIL Caption.NEXT = 0;
              UNTIL Template.NEXT = 0;

            Caption.SETRANGE("Template No.","Template No.");
            Caption.SETRANGE(Type,Type);
            Caption.SETRANGE(Code,Code);
            IF Caption.FINDSET(FALSE,FALSE) THEN
              REPEAT
                Suggestion.SETRANGE("Search Caption",COPYSTR(Caption.Caption,1,MAXSTRLEN(Suggestion."Search Caption")));
                Suggestion.DELETEALL;
              UNTIL Caption.NEXT = 0
          END;

  }
  CODE
  {
    VAR
      Text001@161024012 : TextConst 'DEU=Der %1 %2 wurde erstellt.;ENU=%1 %2 was created.;NLD=%1 %2 is gemaakt.;NOR=%1 %2 ble opprettet.;SVE=%1 %2 skapades.';

    PROCEDURE Submit@161024012(VAR Suggestion@161024012 : Record 6085587);
    VAR
      Caption@161024013 : Record 6085581;
      NextEntryNo@161024014 : Integer;
      i@161024015 : Integer;
    BEGIN
      Caption.SETRANGE("Template No.",Suggestion."Template No.");
      Caption.SETRANGE(Type,Suggestion.Type);
      Caption.SETRANGE(Code,Suggestion.Code);
      IF Caption.FINDLAST THEN
        NextEntryNo := Caption."Entry No.";

      Suggestion.ASCENDING(TRUE);
      Suggestion.SETRANGE(Use,TRUE);
      IF Suggestion.FINDSET(FALSE,FALSE) THEN
        REPEAT
          Caption.SETRANGE(Caption,Suggestion.Caption);
          IF NOT Caption.FINDSET(FALSE,FALSE) THEN BEGIN
            i := i + 1;
            NextEntryNo := NextEntryNo + 10000;
            Caption."Template No." := Suggestion."Template No.";
            Caption.Type := Suggestion.Type;
            Caption.Code := Suggestion.Code;
            Caption."Entry No." := NextEntryNo;
            Caption.Caption := Suggestion.Caption;
            Caption.INSERT(TRUE);
          END;

        UNTIL Suggestion.NEXT = 0;

      MESSAGE(Text001,i,Caption.TABLECAPTION);
    END;

    BEGIN
    END.
  }
}

