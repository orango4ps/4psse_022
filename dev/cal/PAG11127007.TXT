OBJECT Page 11127007 Hour Line Template V2 FSA WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=;
    SourceTable=Table11229799;
    SourceTableView=SORTING(Source,Receiving Company,Code)
                    WHERE(Type=FILTER(Service|Indirect|Project));
    PageType=List;
    SourceTableTemporary=Yes;
  }
  CONTROLS
  {
    { 1100528000;;Container;
                Name=content;
                ContainerType=ContentArea }

    { 1100528001;1;Field  ;
                Name=ResourceNo;
                CaptionML=ENU=<ResourceNo>;
                SourceExpr=ResourceNo;
                OnValidate=BEGIN
                             FillSourceTable;
                           END;
                            }

    { 1100528002;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100528003;2;Field  ;
                Name=ReceivingCompany;
                SourceExpr="Receiving Company" }

    { 1100528004;2;Field  ;
                Name=Code;
                SourceExpr=Code }

    { 1100528005;2;Field  ;
                Name=Description;
                SourceExpr=Description }

    { 1100528006;2;Field  ;
                Name=Type;
                SourceExpr=Type }

    { 1100526000;2;Field  ;
                Name=Expenses;
                SourceExpr=GetExpenses }

    { 1100526001;2;Field  ;
                Name=SeqNo;
                SourceExpr="Seq. No." }

  }
  CODE
  {
    VAR
      ResourceNo@1100528000 : Code[20];

    LOCAL PROCEDURE FillSourceTable@1100528000();
    VAR
      MobileSetup@1100528004 : Record 11012676;
      Resource@1100528003 : Record 11229278;
      Employee@1100528002 : Record 5200;
      HourLineTemplate@1100528001 : Record 11229799;
      SiteMgtCAO@1100528000 : Record 11012364;
      ExportFilterSetMgt@1100527000 : Codeunit 11012200;
    BEGIN
      Rec.RESET;
      Rec.DELETEALL;

      MobileSetup.GET;

      IF Resource.GET(ResourceNo) THEN BEGIN
        Employee.CHANGECOMPANY(Resource.Company);
        IF NOT Employee.GET(Resource."Source No.") THEN
          Employee.INIT;

        IF Resource."Filter Set (Mobile)" <> '' THEN
          ExportFilterSetMgt.SetExportFiltersOnHourLineTemplate(Resource."Filter Set (Mobile)", COMPANYNAME, HourLineTemplate);
      END;

      IF MobileSetup."Hours Destination" = MobileSetup."Hours Destination"::"Hour Accounting" THEN BEGIN
        HourLineTemplate.FILTERGROUP(8);
        HourLineTemplate.SETRANGE(Source, HourLineTemplate.Source::"Hour Accounting");
        HourLineTemplate.SETFILTER(Type, '%1|%2|%3', HourLineTemplate.Type::Service, HourLineTemplate.Type::Project, HourLineTemplate.Type::Indirect);
        HourLineTemplate.FILTERGROUP(0);
        IF HourLineTemplate.FINDSET THEN BEGIN
          REPEAT
            Rec.TRANSFERFIELDS(HourLineTemplate);
            Rec.INSERT;
          UNTIL HourLineTemplate.NEXT = 0;
        END;
      END ELSE BEGIN
        HourLineTemplate.FILTERGROUP(8);
        HourLineTemplate.SETRANGE(Source, HourLineTemplate.Source::"Site Mngmnt. Hours");
        HourLineTemplate.SETFILTER(Type, '%1|%2|%3', HourLineTemplate.Type::Service, HourLineTemplate.Type::Project, HourLineTemplate.Type::Indirect);
        HourLineTemplate.FILTERGROUP(0);
        IF HourLineTemplate.FINDSET THEN BEGIN
          REPEAT
            SiteMgtCAO.FILTERGROUP(8);
            SiteMgtCAO.SETFILTER(Code, HourLineTemplate."CAO Filter");
            SiteMgtCAO.FILTERGROUP(9);
            SiteMgtCAO.SETFILTER(Code, Employee.CAO);
            SiteMgtCAO.FILTERGROUP(0);
            IF NOT SiteMgtCAO.ISEMPTY THEN BEGIN
              Rec.TRANSFERFIELDS(HourLineTemplate);
              Rec.INSERT;
            END;
          UNTIL HourLineTemplate.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE GetExpenses@1100526000() : Boolean;
    VAR
      HourLineTemplateLine@1100526001 : Record 11012078;
      WageComponent@1100526000 : Record 11012014;
    BEGIN
      HourLineTemplateLine.SETRANGE(Source, Source);
      HourLineTemplateLine.SETRANGE("Receiving Company", "Receiving Company");
      HourLineTemplateLine.SETRANGE("Template Code", Code);
      HourLineTemplateLine.SETFILTER("Start Date", '<=%1', TODAY);
      IF HourLineTemplateLine.FINDLAST THEN
        IF WageComponent.GET(HourLineTemplateLine."Wage Component") THEN
          EXIT(WageComponent."Component Type" = WageComponent."Component Type"::Expenses);
    END;

    BEGIN
    END.
  }
}

