OBJECT Table 204 Unit of Measure
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               SetLastDateTimeModified;

               ExchangeFSA('I');  //**4PS.n
               ExchangeMOB('I');  //**4PS.n
             END;

    OnModify=BEGIN
               SetLastDateTimeModified;

               ExchangeFSA('M');  //**4PS.n
               ExchangeMOB('M');  //**4PS.n
             END;

    OnDelete=BEGIN
               UnitOfMeasureTranslation.SETRANGE(Code,Code);
               UnitOfMeasureTranslation.DELETEALL;

               ExchangeFSA('D');  //**4PS.n
               ExchangeMOB('D');  //**4PS.n
             END;

    OnRename=BEGIN
               SetLastDateTimeModified; //**4PS.n

               UpdateItemBaseUnitOfMeasure;

               ExchangeFSA('R');  //**4PS.n
               ExchangeMOB('R');  //**4PS.n
             END;

    CaptionML=[ENU=Unit of Measure;
               NOR=Enht.;
               SVE=M†ttenhet];
    LookupPageID=Page209;
    DrillDownPageID=Page209;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;International Standard Code;Code10 ;CaptionML=[ENU=International Standard Code;
                                                              NOR=Internasjonal standardkode;
                                                              SVE=Internationell standardkod] }
    { 4   ;   ;Symbol              ;Text10        ;CaptionML=[ENU=Symbol;
                                                              NOR=Symbol;
                                                              SVE=Symbol] }
    { 5   ;   ;Last Modified Date Time;DateTime   ;CaptionML=[ENU=Last Modified Date Time;
                                                              NOR=Dato/klokkeslett for siste endring;
                                                              SVE=Senast „ndrad, datum och tid];
                                                   Editable=No }
    { 8000;   ;Id                  ;GUID          ;CaptionML=[ENU=Id;
                                                              NOR=ID;
                                                              SVE=ID] }
    { 11012000;;Last Date Modified ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              SVE=Uppdateringsdatum] }
    { 11012990;;Export to FSA      ;Boolean       ;CaptionML=[ENU=Export to Field Service;
                                                              SVE=Exportera till f„ltservice];
                                                   Description=Indication new/changed. BDA= Buitendienstadmin. (Field Service Admin.), provisional this is only Field Vision. }
    { 11012991;;Date/Time Export to FSA;DateTime  ;CaptionML=[ENU=Date/Time Export to Field Service;
                                                              SVE=Datum/tid, export till f„ltservice] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Description                              }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Code,Description,International Standard Code }
  }
  CODE
  {
    VAR
      UnitOfMeasureTranslation@1000 : Record 5402;

    LOCAL PROCEDURE UpdateItemBaseUnitOfMeasure@1();
    VAR
      Item@1000 : Record 27;
    BEGIN
      Item.SETCURRENTKEY("Base Unit of Measure");
      Item.SETRANGE("Base Unit of Measure",xRec.Code);
      IF NOT Item.ISEMPTY THEN
        Item.MODIFYALL("Base Unit of Measure",Code,TRUE);
    END;

    [External]
    PROCEDURE GetDescriptionInCurrentLanguage@2() : Text[50];
    VAR
      Language@1000 : Record 8;
      UnitOfMeasureTranslation@1001 : Record 5402;
    BEGIN
      IF UnitOfMeasureTranslation.GET(Code,Language.GetUserLanguage) THEN
        EXIT(UnitOfMeasureTranslation.Description);
      EXIT(Description);
    END;

    [External]
    PROCEDURE CreateListInCurrentLanguage@3(VAR TempUnitOfMeasure@1000 : TEMPORARY Record 204);
    VAR
      UnitOfMeasure@1001 : Record 204;
    BEGIN
      IF UnitOfMeasure.FINDSET THEN
        REPEAT
          TempUnitOfMeasure := UnitOfMeasure;
          TempUnitOfMeasure.Description := UnitOfMeasure.GetDescriptionInCurrentLanguage;
          TempUnitOfMeasure.INSERT;
        UNTIL UnitOfMeasure.NEXT = 0;
    END;

    LOCAL PROCEDURE SetLastDateTimeModified@4();
    VAR
      DotNet_DateTimeOffset@1000 : Codeunit 3006;
    BEGIN
      "Last Modified Date Time" := DotNet_DateTimeOffset.ConvertToUtcDateTime(CURRENTDATETIME);

      "Last Date Modified" := TODAY; //**4PS.n
    END;

    PROCEDURE ExchangeFSA@1100485000(IMode@1100485003 : Code[1]);
    VAR
      lvProjSetupRec@1100485001 : Record 315;
      lvFieldVisionExchangeMgtCU@1100485000 : Codeunit 11012091;
    BEGIN
      //**4PS.n
      IF IMode = 'I' THEN BEGIN
        "Export to FSA" := FALSE;
        "Date/Time Export to FSA" := 0DT;
      END;
      lvProjSetupRec.GET;
      IF lvProjSetupRec."Field Service Active" THEN BEGIN
        IF IMode <> 'D' THEN BEGIN
          "Export to FSA" := TRUE;
          IF IMode = 'R' THEN
            lvFieldVisionExchangeMgtCU.StoreDeletedUnitOfMeasure(xRec);
        END ELSE BEGIN
          lvFieldVisionExchangeMgtCU.StoreDeletedUnitOfMeasure(Rec);
        END;
      END;
    END;

    LOCAL PROCEDURE ExchangeMOB@1100528703(Mode@1100528702 : Code[1]);
    VAR
      JobsSetup@1100528700 : Record 315;
      DeletedDataMgtMobile@1100528701 : Codeunit 11012440;
    BEGIN
      //**4PS.n
      IF NOT JobsSetup.IsFSAActive THEN
        EXIT;

      IF (Mode = 'D') THEN
        DeletedDataMgtMobile.StoreDeletedUnitOfMeasure(Rec);

      IF (Mode = 'R') THEN
        DeletedDataMgtMobile.StoreDeletedUnitOfMeasure(xRec);
    END;

    BEGIN
    {
      4PS 06.06.2019 TAKHO changed return value's length in function GetDescriptionInCurrentLanguage from Text 10 to Text 50
    }
    END.
  }
}

