OBJECT Codeunit 11012577 PlantProfileManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'DEU=Es ist kein %1 vorhanden.;ENU=There is no %1 present.;NLD=Er is geen %1 aanwezig.;NOR=Det finnes ingen %1.;SVE=Det finns ingen %1.';

    PROCEDURE GetFirstPlantProfile@2() : Code[10];
    VAR
      lvPlantProfileRec@1000 : Record 11012580;
    BEGIN
      IF lvPlantProfileRec.FIND('-') THEN
        EXIT(lvPlantProfileRec.Code);

      EXIT('');
    END;

    PROCEDURE ShowPlantQuestionList@7(ILevel@1100529000 : Option;IPlantType@1000 : Code[20];IPlantNo@1210190001 : Code[10];IMaterialInventoryLineNo@1100529001 : Integer;IProfileCode@1210190000 : Code[10]);
    VAR
      lvPlantProfileAnswersFrm@1001 : Page 11012614;
    BEGIN
      IF IPlantType = '' THEN
        EXIT;

      lvPlantProfileAnswersFrm.SetParameters(ILevel, IPlantType, IPlantNo, IMaterialInventoryLineNo,
        PlantProfileAllowed(ILevel, IPlantType, IPlantNo, IMaterialInventoryLineNo, IProfileCode), TRUE);
      lvPlantProfileAnswersFrm.RUNMODAL;
    END;

    LOCAL PROCEDURE PlantProfileAllowed@4(ILevel@1100529000 : Option;IPlantType@1210190001 : Code[20];IPlantNo@1210190004 : Code[10];IMaterialInventoryLineNo@1100529001 : Integer;IProfileCode@1210190000 : Code[10]) : Code[10];
    VAR
      lvPlantProfileRec@1210190002 : Record 11012580;
      lvPlantProfileAnswerRec@1210190003 : Record 11012582;
    BEGIN
      IF (IProfileCode <> '') THEN BEGIN
        IF lvPlantProfileRec.GET(IProfileCode) THEN
          EXIT(IProfileCode);
      END;

      lvPlantProfileAnswerRec.SETRANGE(Level, ILevel);
      lvPlantProfileAnswerRec.SETRANGE("Plant Type", IPlantType);
      CASE ILevel OF
        lvPlantProfileAnswerRec.Level::PlantNumber:
          lvPlantProfileAnswerRec.SETRANGE("Plant No.", IPlantNo);
        lvPlantProfileAnswerRec.Level::MaterialInventory:
          BEGIN
            lvPlantProfileAnswerRec.SETRANGE("Plant No.", IPlantNo);
            lvPlantProfileAnswerRec.SETRANGE("Material Inventory Line No.", IMaterialInventoryLineNo);
          END;
      END;
      IF lvPlantProfileAnswerRec.FINDFIRST THEN
        EXIT(lvPlantProfileAnswerRec."Profile Code");

      IF lvPlantProfileRec.FINDFIRST THEN
        EXIT(lvPlantProfileRec.Code);

      ERROR(Text001,lvPlantProfileRec.TABLECAPTION);
    END;

    PROCEDURE SetProfileFilter@6(IProfileCode@1000 : Code[10];VAR IOPlantQuestionListRec@1001 : Record 11012581);
    BEGIN
      IOPlantQuestionListRec.FILTERGROUP := 2;
      IOPlantQuestionListRec.SETRANGE("Profile Code",IProfileCode);
      IOPlantQuestionListRec.FILTERGROUP := 0;
      IF IOPlantQuestionListRec.FIND('-') THEN;
    END;

    PROCEDURE GetLineFilterUsedAnswers@1210190000(IPlantQuestionListRec@1210190000 : Record 11012581) : Text[30];
    VAR
      lvPlantQuestionListRec@1210190001 : Record 11012581;
    BEGIN
      WITH IPlantQuestionListRec DO BEGIN
        IF Type = Type::Answer THEN
          EXIT(STRSUBSTNO('=%1', "Line No."));

        lvPlantQuestionListRec.SETRANGE(Type,Type::Question);
        lvPlantQuestionListRec.SETRANGE("Profile Code","Profile Code");
        lvPlantQuestionListRec.SETFILTER("Line No.",'>%1',"Line No.");
        IF lvPlantQuestionListRec.FIND('-') THEN
          EXIT(STRSUBSTNO('%1..%2', "Line No.", (lvPlantQuestionListRec."Line No." - 1) ))
        ELSE
          EXIT(STRSUBSTNO('>=%1', "Line No."));
      END;
    END;

    PROCEDURE CheckOpenQuestion@1210190001(IProfileCode@1210190002 : Code[10];ILineNo@1210190003 : Integer) : Boolean;
    VAR
      lvPlantQuestionListRec@1210190000 : Record 11012581;
    BEGIN
      WITH lvPlantQuestionListRec DO BEGIN
        SETRANGE(Type,Type::Question);
        SETRANGE("Profile Code",IProfileCode);
        SETFILTER("Line No.",'<%1',ILineNo);
        IF FIND('+') THEN
          EXIT("Open Question");
        EXIT(FALSE);
      END;
    END;

    PROCEDURE UpdateLineNoQuestion@1100485000(ILevel@1100529000 : Option;IPlantType@1100485003 : Code[20];IPlantNo@1100485002 : Code[10];IMaterialInventoryLineNo@1100529001 : Integer;IProfileCode@1100485001 : Code[10]);
    VAR
      lvPlantProfileAnswerRec@1100485000 : Record 11012582;
      lvPlantQuestionListRec@1100485004 : Record 11012581;
      lvPrevProfileCode@1100485005 : Code[10];
      lvPrevQuestLineNo@1100485006 : Integer;
      lvNewQuestLineNo@1100485007 : Integer;
    BEGIN
      WITH lvPlantProfileAnswerRec DO BEGIN
        SETRANGE(Level, ILevel);
        SETRANGE("Plant Type", IPlantType);
        CASE ILevel OF
          Level::PlantNumber:
            SETRANGE("Plant No.", IPlantNo);
          lvPlantProfileAnswerRec.Level::MaterialInventory:
            BEGIN
              SETRANGE("Plant No.", IPlantNo);
              SETRANGE("Material Inventory Line No.", IMaterialInventoryLineNo);
            END;
        END;
        IF IProfileCode <> '' THEN
          SETRANGE("Profile Code", IProfileCode);
        IF NOT FINDFIRST THEN
          EXIT;

        lvPrevProfileCode := '';
        lvPrevQuestLineNo := -1;
        REPEAT
          lvNewQuestLineNo := 0;
          lvPlantQuestionListRec.SETRANGE("Profile Code", "Profile Code");
          lvPlantQuestionListRec.SETFILTER("Line No.",'<%1', "Line No.");
          lvPlantQuestionListRec.SETRANGE(Type, lvPlantQuestionListRec.Type::Question);
          IF lvPlantQuestionListRec.FIND('+') THEN BEGIN
            IF ("Profile Code" <> lvPrevProfileCode) OR (lvPlantQuestionListRec."Line No." <> lvPrevQuestLineNo) THEN BEGIN
              lvNewQuestLineNo := lvPlantQuestionListRec."Line No.";
              lvPrevProfileCode := "Profile Code";
              lvPrevQuestLineNo := lvNewQuestLineNo;
            END;
          END;
          IF ("Line No. Question" <> lvNewQuestLineNo) THEN BEGIN
            "Line No. Question" := lvNewQuestLineNo;
            MODIFY(TRUE); //C037249 - parameter changed
          END;
        UNTIL NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

