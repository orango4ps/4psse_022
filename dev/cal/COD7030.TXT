OBJECT Codeunit 7030 Campaign Target Group Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ContBusRel@1000 : Record 5054;
      SegLine@1001 : Record 11072040;
      CampaignTargetGr@1002 : Record 7030;
      Text000@1003 : TextConst 'ENU=%1 %2 is now activated.;NOR=%1 %2 er n† aktivert.;SVE=%1 %2 „r nu aktiverade.';
      Text001@1004 : TextConst 'ENU=%1 %2 is now deactivated.;NOR=%1 %2 er n† deaktivert.;SVE=%1 %2 „r nu avaktiverade.';
      Text002@1005 : TextConst 'ENU=To activate the sales prices and/or line discounts, you must apply the relevant %1(s) to the %2 and place a check mark in the %3 field on the %1.;NOR=Hvis du vil aktivere salgsprisene og/eller linjerabattene, m† du bruke aktuelle %1 p† %2 og sette en hake i feltet %3 p† %1.;SVE=Om du vill aktivera f”rs„ljningspriserna och/eller radrabatterna m†ste du koppla relevant %1 till %2 och markera f„ltet %3 p† %1.';
      InteractLogEntry@1010 : Record 11072027;
      Text004@1006 : TextConst 'ENU=There are no Sales Prices or Sales Line Discounts currently linked to this %1. Do you still want to activate?;NOR=Ingen salgspriser eller salgslinjerabatter er for ›yeblikket knyttet til %1. Vil du likevel aktivere?;SVE=Det finns f”r n„rvarande inga f”rs„ljningspriser eller f”rs.radrabatter l„nkade till %1. Vill du aktivera?';
      Text006@1011 : TextConst 'ENU=Activating prices for the Contacts...\\;NOR=Aktiverer priser for kontaktene...\\;SVE=Aktiverar priser f”r kontrakten...\\';
      Text007@1007 : TextConst 'ENU=Segment Lines  @1@@@@@@@@@@;NOR=Segmentlinjer          @1@@@@@@@@@@;SVE=Segmentrader          @1@@@@@@@@@@';
      Text008@1012 : TextConst 'ENU=Logged Segment Lines  @1@@@@@@@@@@;NOR=Loggf›rte segmentlinjer@1@@@@@@@@@@;SVE=Loggade segmentrader  @1@@@@@@@@@@';
      CompName@1100529600 : Text[30];

    [External]
    PROCEDURE ActivateCampaign@1(VAR Campaign@1000 : Record 11072034);
    VAR
      SalesPrice@1002 : Record 7002;
      SalesLineDisc@1003 : Record 7004;
      ConfirmManagement@1009 : Codeunit 27;
      Window@1005 : Dialog;
      Found@1001 : Boolean;
      Continue@1004 : Boolean;
      NoOfRecords@1006 : Integer;
      i@1007 : Integer;
      IsHandled@1008 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeActivateCampaign(Campaign,IsHandled);
      IF IsHandled THEN
        EXIT;

      SalesPrice.SETCURRENTKEY("Sales Type","Sales Code");
      SalesPrice.SETRANGE("Sales Type",SalesPrice."Sales Type"::Campaign);
      SalesPrice.SETRANGE("Sales Code",Campaign."No.");
      SalesLineDisc.SETCURRENTKEY("Sales Type","Sales Code");
      SalesLineDisc.SETRANGE("Sales Type",SalesLineDisc."Sales Type"::Campaign);
      SalesLineDisc.SETRANGE("Sales Code",Campaign."No.");
      IF NOT (SalesPrice.FINDFIRST OR SalesLineDisc.FINDFIRST) THEN BEGIN
        Continue :=
          ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text004,Campaign.TABLECAPTION),TRUE);
        IF Continue = FALSE THEN
          EXIT;
      END;
      CampaignTargetGr.LOCKTABLE;
      Found := FALSE;

      WITH SegLine DO BEGIN
        SETCURRENTKEY("Campaign No.");
        SETRANGE("Campaign No.",Campaign."No.");
        SETRANGE("Campaign Target",TRUE);

        IF FIND('-') THEN BEGIN
          Found := TRUE;
          i := 0;
          Window.OPEN(
            Text006 +
            Text007);
          NoOfRecords := COUNT;
          REPEAT
            i := i + 1;
            AddSegLinetoTargetGr(SegLine);
            Window.UPDATE(1,ROUND(i / NoOfRecords * 10000,1));
          UNTIL NEXT = 0;
          Window.CLOSE;
        END;
      END;

      WITH InteractLogEntry DO BEGIN
        SETCURRENTKEY("Campaign No.","Campaign Target");
        SETRANGE("Campaign No.",Campaign."No.");
        SETRANGE("Campaign Target",TRUE);
        SETRANGE(Postponed,FALSE);
        IF FIND('-') THEN BEGIN
          Found := TRUE;
          i := 0;
          Window.OPEN(
            Text006 +
            Text008);
          NoOfRecords := COUNT;
          REPEAT
            i := i + 1;
            AddInteractionLogEntry(InteractLogEntry);
            Window.UPDATE(1,ROUND(i / NoOfRecords * 10000,1));
          UNTIL NEXT = 0;
          Window.CLOSE;
        END;
      END;
      IF Found THEN BEGIN
        COMMIT;
        MESSAGE(Text000,Campaign.TABLECAPTION,Campaign."No.")
      END ELSE
        ERROR(Text002,SegLine.TABLECAPTION,Campaign.TABLECAPTION,SegLine.FIELDCAPTION("Campaign Target"));
    END;

    [External]
    PROCEDURE DeactivateCampaign@2(VAR Campaign@1000 : Record 11072034;ShowMessage@1001 : Boolean);
    BEGIN
      CampaignTargetGr.LOCKTABLE;

      CampaignTargetGr.SETCURRENTKEY("Campaign No.");
      CampaignTargetGr.SETRANGE("Campaign No.",Campaign."No.");
      IF NOT CampaignTargetGr.ISEMPTY THEN
        CampaignTargetGr.DELETEALL;
      IF ShowMessage THEN
        MESSAGE(Text001,Campaign.TABLECAPTION,Campaign."No.");
    END;

    [External]
    PROCEDURE AddSegLinetoTargetGr@3(SegLine@1001 : Record 11072040);
    BEGIN
      WITH SegLine DO
        IF ("Campaign No." <> '') AND "Campaign Target" THEN BEGIN
          ContBusRel.SETCURRENTKEY("Link to Table","Contact No.");
          ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
          ContBusRel.SETRANGE("Contact No.","Contact Company No.");
          IF ContBusRel.FINDFIRST THEN
            InsertTargetGroup(CampaignTargetGr.Type::Customer,ContBusRel."No.","Campaign No.")
          ELSE
            InsertTargetGroup(
              CampaignTargetGr.Type::Contact,"Contact Company No.","Campaign No.");
        END;
    END;

    [External]
    PROCEDURE DeleteSegfromTargetGr@5(SegLine@1001 : Record 11072040);
    VAR
      SegLine2@1000 : Record 11072040;
    BEGIN
      WITH SegLine DO
        IF "Campaign No." <> '' THEN BEGIN
          SegLine2.SETCURRENTKEY("Campaign No.","Contact Company No.","Campaign Target");
          SegLine2.SETRANGE("Campaign No.","Campaign No.");
          SegLine2.SETRANGE("Contact Company No.","Contact Company No.");
          SegLine2.SETRANGE("Campaign Target",TRUE);

          InteractLogEntry.SETCURRENTKEY("Campaign No.","Contact Company No.","Campaign Target");
          InteractLogEntry.SETRANGE("Campaign No.","Campaign No.");
          InteractLogEntry.SETRANGE("Contact Company No.","Contact Company No.");
          InteractLogEntry.SETRANGE("Campaign Target",TRUE);
          InteractLogEntry.SETRANGE(Postponed,FALSE);

          IF SegLine2.COUNT + InteractLogEntry.COUNT = 1 THEN BEGIN
            ContBusRel.SETCURRENTKEY("Link to Table","Contact No.");
            ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
            ContBusRel.SETRANGE("Contact No.","Contact Company No.");

            IF ContBusRel.FINDFIRST THEN BEGIN
              IF CampaignTargetGr.GET(
                   CampaignTargetGr.Type::Customer,ContBusRel."No.","Campaign No.")
              THEN
                CampaignTargetGr.DELETE;
            END ELSE
              IF CampaignTargetGr.GET(
                   CampaignTargetGr.Type::Contact,"Contact No.","Campaign No.")
              THEN
                CampaignTargetGr.DELETE;
          END;
        END;
    END;

    LOCAL PROCEDURE AddInteractionLogEntry@6(InteractionLogEntry@1000 : Record 11072027);
    BEGIN
      WITH InteractionLogEntry DO
        IF ("Campaign No." <> '') AND "Campaign Target" THEN BEGIN
          ContBusRel.SETCURRENTKEY("Link to Table","Contact No.");
          ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
          ContBusRel.SETRANGE("Contact No.","Contact Company No.");
          IF ContBusRel.FINDFIRST THEN
            InsertTargetGroup(CampaignTargetGr.Type::Customer,ContBusRel."No.","Campaign No.")
          ELSE
            InsertTargetGroup(
              CampaignTargetGr.Type::Contact,"Contact Company No.","Campaign No.");
        END;
    END;

    [External]
    PROCEDURE DeleteContfromTargetGr@7(InteractLogEntry@1000 : Record 11072027);
    VAR
      InteractLogEntry2@1001 : Record 11072027;
    BEGIN
      WITH InteractLogEntry DO
        IF "Campaign No." <> '' THEN BEGIN
          InteractLogEntry2.SETCURRENTKEY("Campaign No.","Contact Company No.","Campaign Target");
          InteractLogEntry2.SETRANGE("Campaign No.","Campaign No.");
          InteractLogEntry2.SETRANGE("Contact Company No.","Contact Company No.");
          InteractLogEntry2.SETRANGE("Campaign Target",TRUE);
          InteractLogEntry2.SETRANGE(Postponed,FALSE);

          SegLine.SETCURRENTKEY("Campaign No.","Contact Company No.","Campaign Target");
          SegLine.SETRANGE("Campaign No.","Campaign No.");
          SegLine.SETRANGE("Contact Company No.","Contact Company No.");
          SegLine.SETRANGE("Campaign Target",TRUE);

          IF InteractLogEntry2.COUNT + COUNT = 1 THEN BEGIN
            ContBusRel.SETCURRENTKEY("Link to Table","Contact No.");
            ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
            ContBusRel.SETRANGE("Contact No.","Contact Company No.");

            IF ContBusRel.FINDFIRST THEN BEGIN
              IF CampaignTargetGr.GET(
                   CampaignTargetGr.Type::Customer,ContBusRel."No.","Campaign No.")
              THEN
                CampaignTargetGr.DELETE;
            END ELSE
              IF CampaignTargetGr.GET(
                   CampaignTargetGr.Type::Contact,"Contact No.","Campaign No.")
              THEN
                CampaignTargetGr.DELETE;
          END;
        END;
    END;

    [External]
    PROCEDURE ConverttoCustomer@9(Contact@1000 : Record 5050;Customer@1002 : Record 18);
    VAR
      CampaignTargetGr2@1001 : Record 7030;
    BEGIN
      WITH Contact DO BEGIN
        CampaignTargetGr2.SETCURRENTKEY("No.");
        CampaignTargetGr2.SETRANGE("No.","No.");
        IF CampaignTargetGr2.FIND('-') THEN
          REPEAT
            InsertTargetGroup(
              CampaignTargetGr2.Type::Customer,Customer."No.",CampaignTargetGr2."Campaign No.");
            CampaignTargetGr2.DELETE;
          UNTIL CampaignTargetGr2.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE ConverttoContact@8(Cust@1000 : Record 18;CompanyContNo@1001 : Code[20]);
    VAR
      CampaignTargetGr2@1002 : Record 7030;
    BEGIN
      WITH Cust DO BEGIN
        //**4PS.sn
        IF (CompName <> '') AND (CompName <> COMPANYNAME) THEN
          CampaignTargetGr2.CHANGECOMPANY(CompName);
        //**4PS.en
        CampaignTargetGr2.SETRANGE("No.","No.");
        IF CampaignTargetGr2.FIND('-') THEN
          REPEAT
            InsertTargetGroup(
              CampaignTargetGr2.Type::Contact,CompanyContNo,CampaignTargetGr2."Campaign No.");
            CampaignTargetGr2.DELETE;
          UNTIL CampaignTargetGr2.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertTargetGroup@4(Type@1000 : Option;No@1001 : Code[20];CampaignNo@1002 : Code[20]);
    BEGIN
      CampaignTargetGr.Type := Type;
      CampaignTargetGr."No." := No;
      CampaignTargetGr."Campaign No." := CampaignNo;
      CampaignTargetGr.INSERT(TRUE);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeActivateCampaign@10(VAR Campaign@1000 : Record 11072034;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    PROCEDURE SetCompany@1100529600(CompNameNew@1100529600 : Text[30]);
    BEGIN
      //**4PS
      CompName := CompNameNew;
      CampaignTargetGr.CHANGECOMPANY(CompName);
    END;

    BEGIN
    END.
  }
}

