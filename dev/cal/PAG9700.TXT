OBJECT Page 9700 Email Dialog
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Send Email;
               NOR=Send e-post;
               SVE=Skicka e-post];
    SourceTable=Table9500;
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             HasAttachment := FALSE;
           END;

    OnOpenPage=VAR
                 TempBlob@1003 : TEMPORARY Record 99008535;
                 FileManagement@1005 : Codeunit 419;
                 OfficeMgt@1001 : Codeunit 1630;
                 OrigMailBodyText@1000 : Text;
               BEGIN
                 OriginalFromEmail := OrigEmailItem."From Address";

                 IF NOT IsEditEnabled THEN
                   LocalEdit := FALSE;
                 IF ForceOutlook THEN
                   LocalEdit := TRUE;
                 IF NOT LocalEdit THEN
                   ShownFromEmail := OriginalFromEmail
                 ELSE
                   ShownFromEmail := '';

                 EmailItem.Subject := OrigEmailItem.Subject;
                 EmailItem."Attachment Name" := OrigEmailItem."Attachment Name";

                 InitBccCcText;
                 SendToText := OrigEmailItem."Send to";
                 IF OrigEmailItem."Send CC" <> '' THEN
                   CcText := OrigEmailItem."Send CC"
                 ELSE
                   EmailItem."Send CC" := CcText;
                 IF OrigEmailItem."Send BCC" <> '' THEN
                   BccText := OrigEmailItem."Send BCC"
                 ELSE
                   EmailItem."Send BCC" := BccText;

                 BodyText := '';

                 //IF EmailItem."Plaintext Formatted" THEN //**4PS.o
                 //**4PS.sn
                 IF EmailItem."Plaintext Formatted" OR
                    (OrigEmailItem."Body File Path" = '')
                 THEN
                 //**4PS.en
                   EmailItem."Message Type" := EmailItem."Message Type"::"Custom Message"
                 ELSE BEGIN
                   EmailItem."Body File Path" := OrigEmailItem."Body File Path";
                   FileManagement.BLOBImportFromServerFile(TempBlob,EmailItem."Body File Path");
                   EmailItem.Body := TempBlob.Blob;
                   EmailItem."Message Type" := EmailItem."Message Type"::"From Email Body Template";
                 END;

                 OrigMailBodyText := EmailItem.GetBodyText;
                 IF OrigMailBodyText <> '' THEN
                   BodyText := OrigMailBodyText
                 ELSE
                   EmailItem.SetBodyText(BodyText);

                 UpdatePlainTextVisible;
                 IsOfficeAddin := OfficeMgt.IsAvailable;
               END;

    OnClosePage=BEGIN
                  Rec := EmailItem;
                END;

    OnQueryClosePage=BEGIN
                       //**4PS.sn
                       IF (CloseAction IN [ACTION::OK,ACTION::LookupOK]) AND
                          NOT LocalEdit
                       THEN
                         IF (EmailItem."Send to" = '') AND
                            (EmailItem."Send CC" = '') AND
                            (EmailItem."Send BCC" = '')
                         THEN
                           EmailItem.TESTFIELD("Send to");
                       //**4PS.en
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 3   ;1   ;Field     ;
                Name=FromAddress;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=From;
                           NOR=Fra;
                           SVE=Fr†n];
                ToolTipML=[ENU=Specifies the sender of the email.;
                           NOR=Angir avsenderen av e-post.;
                           SVE=Anger e-postmeddelandets avs„ndare.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ShownFromEmail;
                Visible=False;
                Enabled=False }

    { 10  ;1   ;Field     ;
                Name=SendTo;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=To;
                           NOR=Til;
                           SVE=Till];
                ToolTipML=[ENU=Specifies the recipient of the email.;
                           NOR=Angir mottakeren av e-post.;
                           SVE=Anger e-postmeddelandets mottagare.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SendToText;
                OnValidate=BEGIN
                             EmailItem.VALIDATE("Send to",SendToText);
                             SendToText := EmailItem."Send to";
                           END;
                            }

    { 4   ;1   ;Field     ;
                Name=CcText;
                CaptionML=[ENU=Cc;
                           NOR=Kopi;
                           SVE=Kopia];
                ToolTipML=[ENU=Specifies one or more additional recipients.;
                           NOR=Angir ‚n eller flere tilleggsmottakere.;
                           SVE=Anger en eller flera ytterligare mottagare.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CcText;
                OnValidate=BEGIN
                             EmailItem.VALIDATE("Send CC",CcText);
                             CcText := EmailItem."Send CC";
                           END;
                            }

    { 7   ;1   ;Field     ;
                Name=BccText;
                CaptionML=[ENU=Bcc;
                           NOR=Blindkopi;
                           SVE=Dold kopia];
                ToolTipML=[ENU=Specifies one or more additional recipients.;
                           NOR=Angir ‚n eller flere tilleggsmottakere.;
                           SVE=Anger en eller flera ytterligare mottagare.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=BccText;
                OnValidate=BEGIN
                             EmailItem.VALIDATE("Send BCC",BccText);
                             BccText := EmailItem."Send BCC";
                           END;
                            }

    { 11  ;1   ;Field     ;
                Name=Subject;
                CaptionML=[ENU=Subject;
                           NOR=Emne;
                           SVE=Ang†ende];
                ToolTipML=[ENU=Specifies the text that will display as the subject of the email.;
                           NOR=Angir teksten som vises som emnet i e-posten.;
                           SVE=Anger texten som visas som e-postmeddelandets „mne.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=EmailItem.Subject }

    { 2   ;1   ;Field     ;
                Name=Attachment Name;
                CaptionML=[ENU=First Attachment Name;
                           NOR=Navn p† vedlegg;
                           SVE=Bilagenamn];
                ToolTipML=[ENU=Specifies the name of the attached document.;
                           NOR=Angir navnet p† det vedlagte dokumentet.;
                           SVE=Anger det bifogade dokumentets namn.];
                ApplicationArea=#All;
                SourceExpr=EmailItem."Attachment Name";
                Visible=HasAttachment;
                Enabled=NOT IsOfficeAddin;
                Editable=IsOfficeAddin;
                OnAssistEdit=VAR
                               MailManagement@1000 : Codeunit 9520;
                             BEGIN
                               MailManagement.DownloadPdfAttachment(EmailItem);
                             END;
                              }

    { 1100528500;1;Field  ;
                CaptionML=ENU=No. of Attachments;
                SourceExpr=EmailItem.GetNoOfAttachments;
                MultiLine=Yes;
                OnDrillDown=BEGIN
                              EmailItem.OnDrillDownNoOfAttachments;
                            END;
                             }

    { 5   ;1   ;Field     ;
                Name=MessageContents;
                CaptionML=[ENU=Message Content;
                           NOR=Meldingsinnhold;
                           SVE=Inneh†ll i meddelande];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=EmailItem."Message Type";
                Visible=NOT PlainTextVisible;
                OnValidate=VAR
                             TempBlob@1001 : Record 99008535;
                             FileManagement@1000 : Codeunit 419;
                           BEGIN
                             UpdatePlainTextVisible;

                             CASE EmailItem."Message Type" OF
                               EmailItem."Message Type"::"From Email Body Template":
                                 BEGIN
                                   FileManagement.BLOBImportFromServerFile(TempBlob,EmailItem."Body File Path");
                                   EmailItem.Body := TempBlob.Blob;
                                   BodyText := EmailItem.GetBodyText;
                                 END;
                               EmailItem."Message Type"::"Custom Message":
                                 BEGIN
                                   BodyText := PreviousBodyText;
                                   EmailItem.SetBodyText(BodyText);
                                 END;
                             END;
                           END;
                            }

    { 14  ;1   ;Group     ;
                GroupType=Group }

    { 12  ;2   ;Group     ;
                Visible=NOT PlainTextVisible;
                GroupType=Group }

    { 6   ;3   ;Field     ;
                Name=BodyHTMLMessage;
                CaptionML=[ENU=Body HTML Message;
                           NOR=HTML-meldingstekst;
                           SVE=Text f”r HTML-meddelande];
                ToolTipML=[ENU=Specifies a preview of the email message.;
                           NOR=Angir en forh†ndsvisning av e-postmeldingen.;
                           SVE=Anger en f”rhandsgranskning av e-postmeddelandet.];
                ApplicationArea=#Basic,#Suite;
                MultiLine=Yes;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.WebPageViewer;PublicKeyToken=31bf3856ad364e35];
                ShowCaption=No }

    { 13  ;2   ;Group     ;
                Visible=PlainTextVisible;
                GroupType=Group }

    { 8   ;3   ;Field     ;
                Name=BodyText;
                CaptionML=[ENU=Message;
                           NOR=Melding;
                           SVE=Meddelande];
                ToolTipML=[ENU=Specifies the body of the email.;
                           NOR=Angir br›dteksten i e-posten.;
                           SVE=Anger br”dtexten i e-postmeddelandet.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=BodyText;
                MultiLine=Yes;
                OnValidate=BEGIN
                             EmailItem.SetBodyText(BodyText);

                             IF "Message Type" = "Message Type"::"Custom Message" THEN
                               PreviousBodyText := BodyText;
                           END;

                ShowCaption=No }

    { 9   ;1   ;Field     ;
                Name=OutlookEdit;
                CaptionML=[ENU=Edit in Outlook;
                           NOR=Rediger i Outlook;
                           SVE=Redigera i Outlook];
                ToolTipML=[ENU=Specifies that Microsoft Outlook will open so you can complete the email there.;
                           NOR=Angir at Microsoft Outlook †pnes slik at du kan fullf›re e-posten der.;
                           SVE=Anger att Microsoft Outlook ”ppnas s† att du kan skriva klart e-postmeddelandet h„r.];
                ApplicationArea=#All;
                SourceExpr=LocalEdit;
                Visible=IsEditEnabled;
                MultiLine=Yes;
                OnValidate=BEGIN
                             IF LocalEdit = TRUE THEN
                               ShownFromEmail := ''
                             ELSE
                               ShownFromEmail := OriginalFromEmail;
                           END;
                            }

  }
  CODE
  {
    VAR
      EmailItem@1009 : Record 9500;
      OrigEmailItem@1001 : Record 9500;
      ClientTypeManagement@1077 : Codeunit 4030;
      LocalEdit@1003 : Boolean;
      IsEditEnabled@1006 : Boolean;
      HasAttachment@1008 : Boolean;
      ForceOutlook@1007 : Boolean;
      PlainTextVisible@1013 : Boolean;
      IsOfficeAddin@1012 : Boolean;
      OriginalFromEmail@1000 : Text[250];
      BodyText@1002 : Text;
      SendToText@1005 : Text[250];
      BccText@1010 : Text[250];
      CcText@1011 : Text[250];
      ShownFromEmail@1004 : Text;
      PreviousBodyText@1014 : Text;
      TempEmailAttachment@1100528500 : TEMPORARY Record 11229294;

    [External]
    PROCEDURE SetValues@1(ParmEmailItem@1000 : Record 9500;ParmOutlookSupported@1005 : Boolean;ParmSmtpSupported@1001 : Boolean);
    BEGIN
      EmailItem := ParmEmailItem;
      EmailItem.SetTempEmailAttachments(TempEmailAttachment); //**4PS.n
      OrigEmailItem.COPY(ParmEmailItem);

      ForceOutlook := ParmOutlookSupported AND NOT ParmSmtpSupported;
      IsEditEnabled := ParmOutlookSupported AND (ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Windows);
      IF NOT IsEditEnabled THEN
        LocalEdit := FALSE
      ELSE
        LocalEdit := TRUE;

      IF EmailItem."Attachment File Path" <> '' THEN
        HasAttachment := TRUE;

      //**4PS.sn
      IF LocalEdit AND
         NOT ParmEmailItem."Edit in Outlook"
      THEN
        LocalEdit := FALSE;
      //**4PS.en
    END;

    [External]
    PROCEDURE GetDoEdit@6() : Boolean;
    BEGIN
      EXIT(LocalEdit);
    END;

    LOCAL PROCEDURE UpdatePlainTextVisible@11();
    BEGIN
      PlainTextVisible := EmailItem."Message Type" = EmailItem."Message Type"::"Custom Message";
    END;

    LOCAL PROCEDURE InitBccCcText@2();
    BEGIN
      BccText := '';
      CcText := '';
    END;

    PROCEDURE SetTempEmailAttachments@1100528510(VAR TempEmailAttachment2@1100528500 : TEMPORARY Record 11229294);
    BEGIN
      //**4PS
      IF TempEmailAttachment2.FINDSET THEN
        REPEAT
          TempEmailAttachment := TempEmailAttachment2;
          TempEmailAttachment.INSERT;
        UNTIL TempEmailAttachment2.NEXT = 0;
    END;

    EVENT BodyHTMLMessage@-6::ControlAddInReady@3(callbackUrl@1000 : Text);
    BEGIN
      CurrPage.BodyHTMLMessage.LinksOpenInNewWindow;
      CurrPage.BodyHTMLMessage.SetContent(BodyText);
    END;

    EVENT BodyHTMLMessage@-6::DocumentReady@7();
    BEGIN
    END;

    EVENT BodyHTMLMessage@-6::Callback@4(data@1000 : Text);
    BEGIN
    END;

    EVENT BodyHTMLMessage@-6::Refresh@5(CallbackUrl@1000 : Text);
    BEGIN
      CurrPage.BodyHTMLMessage.LinksOpenInNewWindow;
      CurrPage.BodyHTMLMessage.SetContent(BodyText);
    END;

    BEGIN
    END.
  }
}

