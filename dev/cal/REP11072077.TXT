OBJECT Report 11072077 Date Compress Insurance Ledger
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00 (5697);
  }
  PROPERTIES
  {
    Permissions=TableData 87=rimd,
                TableData 355=imd,
                TableData 11072088=rimd,
                TableData 11072093=rimd;
    CaptionML=[DEU=Datumskomp. Vers.-Posten;
               ENU=Date Compress Insurance Ledger;
               NLD=Verzekeringsposten comprimeren;
               NOR=Datokomprimer Forsikringsreskontro;
               SVE=Datumkompr. fîrsÑkringstrans.];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Date Compress Insurance Ledger",'',RetainDimText,Text010);
                  InsCoverageLedgEntryFilter := COPYSTR("Ins. Coverage Ledger Entry".GETFILTERS,1,MAXSTRLEN(DateComprReg.Filter));
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 8513;    ;DataItem;                    ;
               DataItemTable=Table11072088;
               DataItemTableView=SORTING(FA No.,Insurance No.,Disposed FA);
               OnPreDataItem=VAR
                               GLSetup@1000 : Record 98;
                             BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date"));

                               Window.OPEN(
                                 Text004 +
                                 Text005 +
                                 Text006 +
                                 Text007 +
                                 Text008);

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress Insurance Ledger");

                               SelectedDim.GetSelectedDim(
                                 USERID,3,REPORT::"Date Compress Insurance Ledger",'',TempSelectedDim);
                               GLSetup.GET;
                               Retain[2] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress Insurance Ledger",'',GLSetup."Global Dimension 1 Code");
                               Retain[3] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress Insurance Ledger",'',GLSetup."Global Dimension 2 Code");

                               NewInsCoverageLedgEntry.LOCKTABLE;
                               InsuranceReg.LOCKTABLE;
                               DateComprReg.LOCKTABLE;

                               IF InsCoverageLedgEntry2.FIND('+') THEN;
                               LastEntryNo := InsCoverageLedgEntry2."Entry No.";
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");

                               InitRegisters;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF ("Insurance No." <> '') AND OnlyIndexEntries AND NOT "Index Entry" THEN
                                    CurrReport.SKIP;
                                  InsCoverageLedgEntry2 := "Ins. Coverage Ledger Entry";
                                  WITH InsCoverageLedgEntry2 DO BEGIN
                                    SETCURRENTKEY("FA No.","Insurance No.");
                                    COPYFILTERS("Ins. Coverage Ledger Entry");

                                    SETRANGE("Insurance No.","Insurance No.");
                                    SETRANGE("FA No.","FA No.");
                                    SETRANGE("Document Type","Document Type");
                                    SETRANGE("Index Entry","Index Entry");
                                    SETRANGE("Disposed FA","Disposed FA");
                                    SETFILTER("Posting Date",DateComprMgt.GetDateFilter("Posting Date",EntrdDateComprReg,TRUE));

                                    IF RetainNo(FIELDNO("Document No.")) THEN
                                      SETRANGE("Document No.","Document No.");
                                    IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
                                      SETRANGE("Global Dimension 1 Code","Global Dimension 1 Code");
                                    IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
                                      SETRANGE("Global Dimension 2 Code","Global Dimension 2 Code");

                                    InitNewEntry(NewInsCoverageLedgEntry);

                                    DimBufMgt.CollectDimEntryNo(
                                      TempSelectedDim,"Dimension Set ID","Entry No.",
                                      0,FALSE,DimEntryNo);
                                    ComprDimEntryNo := DimEntryNo;
                                    SummarizeEntry(NewInsCoverageLedgEntry,InsCoverageLedgEntry2);
                                    WHILE NEXT <> 0 DO BEGIN
                                      DimBufMgt.CollectDimEntryNo(
                                        TempSelectedDim,"Dimension Set ID","Entry No.",
                                        ComprDimEntryNo,TRUE,DimEntryNo);
                                      IF DimEntryNo = ComprDimEntryNo THEN
                                        SummarizeEntry(NewInsCoverageLedgEntry,InsCoverageLedgEntry2);
                                    END;

                                    InsertNewEntry(NewInsCoverageLedgEntry,ComprDimEntryNo);

                                    ComprCollectedEntries;
                                  END;

                                  IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(InsuranceReg,DateComprReg);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF DateComprReg."No. Records Deleted" > NoOfDeleted THEN
                                  InsertRegisters(InsuranceReg,DateComprReg);
                              END;

               ReqFilterFields=Insurance No.,FA No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;
                   IF EntrdInsCoverageLedgEntry.Description = '' THEN
                     EntrdInsCoverageLedgEntry.Description := Text009;

                   WITH "Ins. Coverage Ledger Entry" DO BEGIN
                     InsertField(FIELDNO("Document No."),FIELDCAPTION("Document No."));
                     InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
                     InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
                   END;

                   RetainDimText := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Date Compress Insurance Ledger",'');
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das Datum an, ab dem fÅr den Bericht oder die Stapelverarbeitung Informationen verarbeitet werden.;
                             ENU=Specifies the date from which the report or batch job processes information.;
                             NLD=Hiermee wordt de datum opgegeven vanaf wanneer het rapport of de batchverwerking informatie verwerkt.;
                             SVE=Anger datumet frÜn vilket rapporten eller buntjobbet bearbetar information.];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  ToolTipML=[DEU=Gibt das Datum an, bis zu dem fÅr den Bericht oder die Stapelverarbeitung Informationen verarbeitet werden.;
                             ENU=Specifies the date to which the report or batch job processes information.;
                             NLD=Hiermee wordt de datum opgegeven tot wanneer het rapport of de batchverwerking informatie verwerkt.;
                             NOR=Angir datoen som rapporten eller kjõrselen behandler informasjon til.;
                             SVE=Anger datumet till vilket rapporten eller batch-jobbet bearbetar information.];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  Name=PeriodLength;
                  CaptionML=[DEU=PeriodenlÑnge;
                             ENU=Period Length;
                             NLD=Periodelengte;
                             NOR=Periodelengde;
                             SVE=Period];
                  ToolTipML=[DEU=Gibt die Periode an, fÅr die Daten im Bericht angezeigt werden. Geben Sie z. B. "1M" fÅr einen Monat, "30D" fÅr 30 Tage, "3Q" fÅr drei Quartale oder "5Y" fÅr fÅnf Jahre ein.;
                             ENU=Specifies the period for which data is shown in the report. For example, enter "1M" for one month, "30D" for thirty days, "3Q" for three quarters, or "5Y" for five years.;
                             NLD=Hiermee wordt de periode opgegeven waarvoor gegevens worden weergegeven in het rapport, bijvoorbeeld '1M' voor ≤≤n maand, '30D' voor dertig dagen '3Q' voor drie kwartaal of '5Y' voor vijf jaar.;
                             SVE=Anger perioden som data visas fîr i rapporten. Ange till exempel "1M" fîr en mÜnad, "30D" fîr 30ˇdagar, "3K" fîr tre kvartal eller "5è" fîr fem Ür.];
                  OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;
                                   ENU=Day,Week,Month,Quarter,Year,Period;
                                   NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                   NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;
                                   SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 4   ;2   ;Field     ;
                  Name=PostingDescription;
                  CaptionML=[DEU=Buchungsbeschreibung;
                             ENU=Posting Description;
                             NLD=Omschrijving;
                             NOR=Bokfõringsbeskrivelse;
                             SVE=Bokfîringsbeskrivning];
                  ToolTipML=[DEU=Gibt das Buchungsdatum an, das von der Stapelverarbeitung als Filter verwendet werden soll.;
                             ENU=Specifies the posting date to be used by the batch job as a filter.;
                             NLD=Hiermee wordt de boekingsdatum opgegeven die door de batchverwerking als filter moet worden gebruikt.;
                             NOR=Angir bokfõringsdatoen som skal brukes av kjõrselen som et filter.;
                             SVE=Anger bokfîringsdatumet som ska anvÑndas i batch-jobbet som ett filter.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=EntrdInsCoverageLedgEntry.Description }

      { 6   ;2   ;Field     ;
                  Name=OnlyIndexEntries;
                  CaptionML=[DEU=Nur Indexposten;
                             ENU=Only Index Entries;
                             NLD=Alleen indexposten;
                             NOR=Kun indekstransaksjoner;
                             SVE=Endast indextransaktioner];
                  ToolTipML=[DEU=Gibt an, falls nur Indexposten datumskomprimiert werden sollen. Mit der Stapelverarbeitung "Anlagen indexieren" kînnen Sie Anlagen indexieren, die mit einem bestimmten AfA-Buch verbunden sind. Die Stapelverarbeitung erstellt Posten in einem Buch.-Blatt, basierend auf den von Ihnen angegebenen Optionen. Sie kînnen das Buch.-Blatt dann buchen oder die Posten, wenn nîtig, vor dem Buchen korrigieren.;
                             ENU=Specifies if only index entries are date compressed. With the Index Fixed Assets batch job, you can index fixed assets that are linked to a specific depreciation book. The batch job creates entries in a journal based on the conditions that you specify. You can then post the journal or adjust the entries before posting, if necessary.;
                             NLD=Hiermee wordt opgegeven of alleen indexposten worden gecomprimeerd. Met de batchverwerking Index vast activum comprimeren kunt u vaste activa indexeren die zijn gekoppeld aan een specifiek afschrijvingsboek. De batchverwerking maakt posten in een dagboek op basis van de door u opgegeven voorwaarden. U kunt het dagboek vervolgens boeken of de posten eventueel voor het boeken aanpassen.;
                             NOR=Angir om bare indeksposter blir datokomprimeres. Ved hjelp av kjõrselen Indeksreg. aktiva kan du indeksregulere aktiva som er knyttet til et spesielt avskrivningstablÜ. Kjõrselen oppretter poster i en kladd pÜ bakgrunn av betingelsene du angir. Du kan deretter bokfõre kladden eller justere postene fõr bokfõring, om nõdvendig.;
                             SVE=Anger om enbart indextransaktioner datumkomprimeras. Med batch-jobbet Indexera anlÑggningstillgÜngar kan du indexera anlÑggningstillgÜngar som Ñr lÑnkade till en viss avskrivningsregel. Batch-jobbet skapar transaktioner i en journal baserat pÜ de villkor du anger. Sedan kan du bokfîra journalen eller justera transaktionerna fîre bokfîring om sÜ behîvs.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=OnlyIndexEntries }

      { 1907651001;2;Group  ;
                  CaptionML=[DEU=Feldinhalt behalten;
                             ENU=Retain Field Contents;
                             NLD=Te bewaren velden;
                             NOR=Behold felt;
                             SVE=BibehÜll fÑlt] }

      { 5   ;3   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Dokumentnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt, wenn das Feld leer ist, die nÑchste verfÅgbare Nummer in der resultierenden Buch.-Blattzeile an. Wenn keine Nummernserie eingerichtet wurde, geben Sie die Belegnummer ein, die der resultierenden Buch.-Blattzeile zugewiesen werden soll.;
                             ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                             NLD=Hiermee wordt, als u het veld leeg laat, het volgende beschikbare nummer op de resulterende dagboekregel opgegeven. Als geen nummerreeks is ingesteld, voert u het documentnummer in dat u wilt toewijzen aan de resulterende dagboekregel.;
                             NOR=Angir, hvis du lar feltet vëre tomt, det neste tilgjengelige nummeret for den resulterende kladdelinjen. Hvis en nummer serie ikke er definert, angir du bilagsnummeret som du vil tilordne til den resulterende kladdelinjen.;
                             SVE=Om du inte anger nÜgot i fÑltet anges nÑsta lediga nummer pÜ journalraden. Om ingen nummerserie har konfigurerats anger du det dokumentnummer som du vill tilldela journalraden.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=Retain[1] }

      { 8   ;2   ;Field     ;
                  Name=RetainDimensions;
                  CaptionML=[DEU=Dimensionen behalten;
                             ENU=Retain Dimensions;
                             NLD=Te bewaren dimensies;
                             NOR=Behold dimensjoner;
                             SVE=BibehÜll dimensioner];
                  ToolTipML=[DEU=Legt fest, welche Dimensionsdaten bei der Komprimierung der Posten erhalten bleiben sollen. Je mehr Dimensionsinformationen Sie beibehalten, desto detaillierter werden die komprimierten Posten sein.;
                             ENU=Specifies which dimension information you want to retain when the entries are compressed. The more dimension information that you choose to retain, the more detailed the compressed entries are.;
                             NLD=Hiermee wordt opgegeven welke dimensiegegevens u bij de compressie van de posten wilt bewaren. Hoe meer dimensiegegevens u bewaart, hoe gedetailleerder het resultaat van de compressie is.;
                             NOR=Angir hvilken dimensjonsinformasjon du vil beholde nÜr postene komprimeres. Jo mer dimensjonsinformasjon du velger Ü beholde, jo mer detaljerte er de komprimerte postene.;
                             SVE=Anger den dimensionsinformation som du vill behÜlla nÑr transaktionerna komprimeras. Ju mer dimensionsinformation du vÑljer att behÜlla, desto mer detaljerade blir de komprimerade transaktionerna.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=RetainDimText;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Compress Insurance Ledger",RetainDimText);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Durch diesen Batchauftrag werden Posten gelîscht. Daher muss vor dem AusfÅhren des Batchauftrags eine Datenbanksicherung erstellt werden.\\Mîchten Sie die Posten datumskomprimieren?;ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to date compress the entries?;NLD=Met deze batchverwerking worden posten verwijderd. Maak daarom eerst een reservekopie van de database voor u de batchverwerking uitvoert.\\Wilt u de posten comprimeren?;NOR=Denne Batchjobben fjerner transaksjoner. Det er derfor viktig at du tar en sikkerhetskopi pÜ databasen fõr du kjõrer Batchjobbent.\\Vil du datokomprimere transaksjonene?;SVE=Detta batchjobb tar bort transaktioner. Det Ñr dÑrfîr viktigt att du tar en sÑkerhetskopia pÜ databasen innan du kîr batchjobbet.\\Vill du datumkomprimera transaktionerna?';
      Text003@1003 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified.;NLD=%1 moet opgegeven worden.;NOR=%1 mÜ spesifiseres.;SVE=%1 mÜste specificeras.';
      Text004@1004 : TextConst 'DEU=Datumskomprimierung Versicherungsposten...\\;ENU=Date compressing insurance ledger entries...\\;NLD=Comprimeren verzekeringsposten...\\;NOR=Datokomprimerer Reskontrotransaksjoner for forsikring ...\\;SVE=Datumkomprimerar fîrsÑkringstransaktioner...\\';
      Text005@1005 : TextConst 'DEU=Versicherungsnr.    #1##########\;ENU=Insurance No.        #1##########\;NLD=Verzekeringsnr.      #1##########\;NOR=Forsikringsnr.        #1##########\;SVE=FîrsÑkringsnr        #1##########\';
      Text006@1006 : TextConst 'DEU=Datum                #2######\\;ENU=Date                 #2######\\;NLD=Datum                #2######\\;NOR=Dato               #2######\\;SVE=Datum                #2######\\';
      Text007@1007 : TextConst 'DEU=Anzahl neue Posten   #3######\;ENU=No. of new entries   #3######\;NLD=Nieuwe posten        #3######\;NOR=Antall nye transaksjoner     #3######\;SVE=Antal nya trans.     #3######\';
      Text008@1008 : TextConst 'DEU=Anz. gelîsch. Posten #4######;ENU=No. of entries del.  #4######;NLD=Verwijderde posten   #4######;NOR=Antall fjernede transaksjoner  #4######;SVE=Antal bortt. trans.  #4######';
      Text009@1009 : TextConst 'DEU=Datumskomprimiert;ENU=Date Compressed;NLD=Gecomprimeerde post;NOR=Dato komprimert;SVE=Datumkomprimerad';
      Text010@1010 : TextConst 'DEU=Dimensionen behalten;ENU=Retain Dimensions;NLD=Te bewaren dimensies;NOR=Behold dimensjoner;SVE=BibehÜll dimensioner';
      SourceCodeSetup@1011 : Record 242;
      DateComprReg@1012 : Record 87;
      EntrdDateComprReg@1013 : Record 87;
      InsuranceReg@1014 : Record 11072093;
      EntrdInsCoverageLedgEntry@1015 : Record 11072088;
      NewInsCoverageLedgEntry@1016 : Record 11072088;
      InsCoverageLedgEntry2@1017 : Record 11072088;
      SelectedDim@1018 : Record 369;
      TempSelectedDim@1019 : TEMPORARY Record 369;
      DimSelectionBuf@1020 : Record 368;
      DateComprMgt@1021 : Codeunit 356;
      DimBufMgt@1022 : Codeunit 411;
      DimMgt@1023 : Codeunit 408;
      Window@1024 : Dialog;
      InsCoverageLedgEntryFilter@1025 : Text[250];
      NoOfFields@1026 : Integer;
      Retain@1027 : ARRAY [10] OF Boolean;
      FieldNumber@1028 : ARRAY [10] OF Integer;
      FieldNameArray@1029 : ARRAY [10] OF Text[100];
      LastEntryNo@1030 : Integer;
      NoOfDeleted@1031 : Integer;
      InsuranceRegExists@1032 : Boolean;
      OnlyIndexEntries@1033 : Boolean;
      i@1034 : Integer;
      ComprDimEntryNo@1035 : Integer;
      DimEntryNo@1036 : Integer;
      RetainDimText@1037 : Text[250];

    LOCAL PROCEDURE InitRegisters@4();
    VAR
      NextRegNo@1000 : Integer;
    BEGIN
      IF InsuranceReg.FIND('+') THEN;
      InsuranceReg.INIT;
      InsuranceReg."No." := InsuranceReg."No." + 1;
      InsuranceReg."Creation Date" := TODAY;
      InsuranceReg."Creation Time" := TIME;
      InsuranceReg."Source Code" := SourceCodeSetup."Compress Insurance Ledger";
      InsuranceReg."User ID" := USERID;
      InsuranceReg."From Entry No." := LastEntryNo + 1;

      IF DateComprReg.FINDLAST THEN
        NextRegNo := DateComprReg."No." + 1;

      DateComprReg.InitRegister(
        DATABASE::"Ins. Coverage Ledger Entry",NextRegNo,
        EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date",EntrdDateComprReg."Period Length",
        InsCoverageLedgEntryFilter,InsuranceReg."No.",SourceCodeSetup."Compress Insurance Ledger");

      FOR i := 1 TO NoOfFields DO
        IF Retain[i] THEN
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);

      InsuranceRegExists := FALSE;
      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@1(VAR InsuranceReg@1000 : Record 11072093;VAR DateComprReg@1001 : Record 87);
    BEGIN
      InsuranceReg."To Entry No." := NewInsCoverageLedgEntry."Entry No.";

      IF InsuranceRegExists THEN BEGIN
        InsuranceReg.MODIFY;
        DateComprReg.MODIFY;
      END ELSE BEGIN
        InsuranceReg.INSERT;
        DateComprReg.INSERT;
        InsuranceRegExists := TRUE;
      END;
      COMMIT;

      NewInsCoverageLedgEntry.LOCKTABLE;
      InsuranceReg.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      InsCoverageLedgEntry2.RESET;
      IF InsCoverageLedgEntry2.FIND('+') THEN;
      IF LastEntryNo <> InsCoverageLedgEntry2."Entry No." THEN BEGIN
        LastEntryNo := InsCoverageLedgEntry2."Entry No.";
        InitRegisters;
      END;
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    LOCAL PROCEDURE SummarizeEntry@5(VAR NewInsCoverageLedgEntry@1000 : Record 11072088;InsCoverageLedgEntry@1001 : Record 11072088);
    BEGIN
      WITH InsCoverageLedgEntry DO BEGIN
        NewInsCoverageLedgEntry.Amount := NewInsCoverageLedgEntry.Amount + Amount;
        DELETE;
        DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
        Window.UPDATE(4,DateComprReg."No. Records Deleted");
      END;
    END;

    LOCAL PROCEDURE ComprCollectedEntries@12();
    VAR
      InsCoverageLedgEntry@1000 : Record 11072088;
      OldDimEntryNo@1001 : Integer;
      Found@1002 : Boolean;
      InsCoverageLedgEntryNo@1003 : Integer;
    BEGIN
      OldDimEntryNo := 0;
      IF DimBufMgt.FindFirstDimEntryNo(DimEntryNo,InsCoverageLedgEntryNo) THEN BEGIN
        InitNewEntry(NewInsCoverageLedgEntry);
        REPEAT
          InsCoverageLedgEntry.GET(InsCoverageLedgEntryNo);
          SummarizeEntry(NewInsCoverageLedgEntry,InsCoverageLedgEntry);
          OldDimEntryNo := DimEntryNo;
          Found := DimBufMgt.NextDimEntryNo(DimEntryNo,InsCoverageLedgEntryNo);
          IF (OldDimEntryNo <> DimEntryNo) OR NOT Found THEN BEGIN
            InsertNewEntry(NewInsCoverageLedgEntry,OldDimEntryNo);
            IF Found THEN
              InitNewEntry(NewInsCoverageLedgEntry);
          END;
          OldDimEntryNo := DimEntryNo;
        UNTIL NOT Found;
      END;
      DimBufMgt.DeleteAllDimEntryNo;
    END;

    [External]
    PROCEDURE InitNewEntry@7(VAR NewInsCoverageLedgEntry@1000 : Record 11072088);
    BEGIN
      LastEntryNo := LastEntryNo + 1;
      WITH InsCoverageLedgEntry2 DO BEGIN
        NewInsCoverageLedgEntry.INIT;
        NewInsCoverageLedgEntry."Entry No." := LastEntryNo;

        NewInsCoverageLedgEntry."Insurance No." := "Insurance No.";
        NewInsCoverageLedgEntry."FA No." := "FA No.";
        NewInsCoverageLedgEntry."Document Type" := "Document Type";
        NewInsCoverageLedgEntry."Index Entry" := "Index Entry";
        NewInsCoverageLedgEntry."Disposed FA" := "Disposed FA";

        NewInsCoverageLedgEntry."Posting Date" := GETRANGEMIN("Posting Date");
        NewInsCoverageLedgEntry.Description := EntrdInsCoverageLedgEntry.Description;
        NewInsCoverageLedgEntry."Source Code" := SourceCodeSetup."Compress Insurance Ledger";
        NewInsCoverageLedgEntry."User ID" := USERID;
        IF RetainNo(FIELDNO("Document No.")) THEN
          NewInsCoverageLedgEntry."Document No." := "Document No.";
        IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
          NewInsCoverageLedgEntry."Global Dimension 1 Code" := "Global Dimension 1 Code";
        IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
          NewInsCoverageLedgEntry."Global Dimension 2 Code" := "Global Dimension 2 Code";

        Window.UPDATE(1,NewInsCoverageLedgEntry."Insurance No.");
        Window.UPDATE(2,NewInsCoverageLedgEntry."Posting Date");
        DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
        Window.UPDATE(3,DateComprReg."No. of New Records");
      END;
    END;

    LOCAL PROCEDURE InsertNewEntry@23(VAR NewInsCoverageLedgEntry@1000 : Record 11072088;DimEntryNo@1001 : Integer);
    VAR
      TempDimBuf@1002 : TEMPORARY Record 360;
      TempDimSetEntry@1005 : TEMPORARY Record 480;
    BEGIN
      TempDimBuf.DELETEALL;
      DimBufMgt.GetDimensions(DimEntryNo,TempDimBuf);
      DimMgt.CopyDimBufToDimSetEntry(TempDimBuf,TempDimSetEntry);
      NewInsCoverageLedgEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
      NewInsCoverageLedgEntry.INSERT;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

