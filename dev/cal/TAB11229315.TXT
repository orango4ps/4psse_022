OBJECT Table 11229315 Purchase Line SC Application
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Purchase Line SC Application;
  }
  FIELDS
  {
    { 10  ;   ;Purchase Order      ;Code20        ;TableRelation="Purchase Header".No. WHERE (Document Type=CONST(Order));
                                                   CaptionML=[ENU=Purchase Order;
                                                              NOR=Innkj›psordre;
                                                              SVE=Ink”psorder];
                                                   NotBlank=Yes }
    { 20  ;   ;Purchase Line       ;Integer       ;TableRelation="Purchase Line"."Line No." WHERE (Document Type=CONST(Order),
                                                                                                   Document No.=FIELD(Purchase Order));
                                                   CaptionML=[ENU=Purchase Line;
                                                              NOR=Bestillingslinje;
                                                              SVE=Ink”psrad];
                                                   NotBlank=Yes }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 40  ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum] }
    { 50  ;   ;Claimed Amount      ;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS("Current Outstanding Amount");
                                                                IF "Current Outstanding Amount" < 0 THEN BEGIN
                                                                  IF "Current Outstanding Amount" > "Claimed Amount" THEN
                                                                    FIELDERROR("Claimed Amount");
                                                                END ELSE BEGIN
                                                                  IF "Current Outstanding Amount" < "Claimed Amount" THEN
                                                                    FIELDERROR("Claimed Amount");
                                                                END;

                                                                IF CurrFieldNo <> 0 THEN BEGIN
                                                                  "Certified Amount" := 0;
                                                                  "Certified Date" := 0D;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Claimed Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 60  ;   ;Certified Amount    ;Decimal       ;OnValidate=BEGIN
                                                                IF "Claimed Amount" < 0 THEN BEGIN
                                                                  IF "Claimed Amount" > "Certified Amount" THEN
                                                                    FIELDERROR("Certified Amount");
                                                                END ELSE BEGIN
                                                                  IF "Claimed Amount" < "Certified Amount" THEN
                                                                    FIELDERROR("Certified Amount");
                                                                END;

                                                                "Certified Date" := WORKDATE;
                                                              END;

                                                   CaptionML=ENU=Certified Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 70  ;   ;Certified Date      ;Date          ;CaptionML=ENU=Certified Date }
    { 80  ;   ;Comment             ;Text250       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 90  ;   ;Receipt Created     ;Boolean       ;CaptionML=ENU=Receipt Created }
    { 100 ;   ;Order Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Line Amount" WHERE (Document Type=FILTER(Order),
                                                                                                           Document No.=FIELD(Purchase Order),
                                                                                                           Line No.=FIELD(Purchase Line)));
                                                   CaptionML=[ENU=Order Amount;
                                                              NOR=Ordrebel›p;
                                                              SVE=Orderbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 110 ;   ;Current Outstanding Amount;Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Outst. Amount exc. VAT" WHERE (Document Type=FILTER(Order),
                                                                                                                      Document No.=FIELD(Purchase Order),
                                                                                                                      Line No.=FIELD(Purchase Line)));
                                                   CaptionML=ENU=Current Outstanding Amount;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 120 ;   ;Currency Code       ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Currency Code" WHERE (Document Type=FILTER(Order),
                                                                                                             Document No.=FIELD(Purchase Order),
                                                                                                             Line No.=FIELD(Purchase Line)));
                                                   TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod];
                                                   Editable=No }
    { 130 ;   ;Application Date    ;Date          ;CaptionML=ENU=Application Date }
  }
  KEYS
  {
    {    ;Purchase Order,Purchase Line,Line No.   ;MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PurchaseHeader@1100528200 : Record 38;
      PurchasesPayablesSetup@1100528201 : Record 312;
      Text11012001@1100528202 : TextConst 'ENU=It is only possible to post released Orders;NOR=Det er kun mulig † bokf›re frigitte ordre;SVE=Det „r bara m”jligt att bokf”ra sl„ppta ordrar';
      PurchaseLine@1100528204 : Record 39;
      PurchPost@1100528205 : Codeunit 90;

    PROCEDURE CreateReceipts@1100528200(VAR PurchaseOrderLineSCClaim@1100528200 : Record 11229315);
    BEGIN
      PurchasesPayablesSetup.GET;
      PurchaseOrderLineSCClaim.SETRANGE("Receipt Created", FALSE);
      PurchaseOrderLineSCClaim.SETFILTER("Certified Amount", '<>%1', 0);
      IF PurchaseOrderLineSCClaim.FINDSET(TRUE, FALSE) THEN BEGIN
        IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order, PurchaseOrderLineSCClaim."Purchase Order") THEN BEGIN

          IF (PurchasesPayablesSetup."Release Purch Orders Required") AND
             (PurchaseHeader.Status = PurchaseHeader.Status::Open) THEN BEGIN
             MESSAGE(Text11012001);
             EXIT;
          END;
          IF NOT PurchaseHeader.IsApprovedForPosting THEN
            EXIT;

          REPEAT
            CreateOneReceipt(PurchaseOrderLineSCClaim);
            PostOneReceipt(PurchaseOrderLineSCClaim);
          UNTIL PurchaseOrderLineSCClaim.NEXT = 0;

        END;
      END;
    END;

    LOCAL PROCEDURE CreateOneReceipt@1100528201(VAR PurchaseOrderLineSCClaim@1100528200 : Record 11229315);
    BEGIN
      PurchaseLine.GET(
        PurchaseLine."Document Type"::Order,
        PurchaseOrderLineSCClaim."Purchase Order",
        PurchaseOrderLineSCClaim."Purchase Line");

      PurchaseLine.VALIDATE("Amnt. to Receive", PurchaseOrderLineSCClaim."Certified Amount");
      PurchaseLine.MODIFY(TRUE);
      PurchaseLine.CreatePurchOrderControl(FALSE);

      PurchaseLine.SETRANGE("Document Type", PurchaseLine."Document Type"::Order);
      PurchaseLine.SETRANGE("Document No.", PurchaseOrderLineSCClaim."Purchase Order");
      PurchaseLine.FINDSET;
      REPEAT
        IF PurchaseLine."Line No." <> PurchaseOrderLineSCClaim."Purchase Line" THEN BEGIN
          PurchaseLine.VALIDATE("Amnt. to Receive", 0);
          PurchaseLine.MODIFY(TRUE);
        END;
      UNTIL PurchaseLine.NEXT = 0;

      IF PurchaseOrderLineSCClaim."Certified Date" = 0D THEN
        PurchaseOrderLineSCClaim."Certified Date" := PurchaseHeader."Posting Date";
      PurchaseOrderLineSCClaim."Receipt Created" := TRUE;
      PurchaseOrderLineSCClaim.MODIFY;
    END;

    LOCAL PROCEDURE PostOneReceipt@1100528202(VAR PurchaseOrderLineSCClaim@1100528200 : Record 11229315);
    BEGIN
      CLEAR(PurchPost);
      PurchaseHeader.Receive := TRUE;
      PurchaseHeader.Invoice := FALSE;
      PurchaseHeader."Print Posted Documents" := FALSE;
      PurchaseHeader."Posting Date" := PurchaseOrderLineSCClaim."Certified Date";
      PurchPost.RUN(PurchaseHeader);
    END;

    BEGIN
    {
      4PS 26-09-16 UKR-C27454 VAKAT: BI022 Claims recording - field Application Date Added
    }
    END.
  }
}

