OBJECT Codeunit 130411 Sys. Warmup Scenarios
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [Test]
    [External]
    PROCEDURE WarmupInvoicePosting@1();
    VAR
      Customer@1006 : Record 18;
      Item@1002 : Record 27;
      SalesHeader@1000 : Record 36;
    BEGIN
      IF NOT Customer.FINDFIRST THEN
        EXIT;

      Item.SETFILTER(Inventory,'<>%1',0);
      IF NOT Item.FINDFIRST THEN
        EXIT;

      CreateSalesInvoice(SalesHeader,Customer,Item);
      PostSalesInvoice(SalesHeader);
    END;

    LOCAL PROCEDURE GetRandomString@2() : Text;
    BEGIN
      EXIT(DELCHR(FORMAT(CREATEGUID),'=','{}-'));
    END;

    LOCAL PROCEDURE CreateSalesInvoice@3(VAR SalesHeader@1000 : Record 36;Customer@1001 : Record 18;Item@1002 : Record 27);
    BEGIN
      CreateSalesHeader(SalesHeader,Customer);
      CreateSalesLine(SalesHeader,Item);
    END;

    LOCAL PROCEDURE PostSalesInvoice@4(VAR SalesHeader@1000 : Record 36);
    BEGIN
      SalesHeader.Ship := TRUE;
      SalesHeader.Invoice := TRUE;
      CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader);
    END;

    LOCAL PROCEDURE CreateSalesHeader@5(VAR SalesHeader@1000 : Record 36;Customer@1001 : Record 18);
    BEGIN
      SalesHeader.VALIDATE("Document Type",SalesHeader."Document Type"::Invoice);
      SalesHeader."No." := COPYSTR(GetRandomString,1,MAXSTRLEN(SalesHeader."No."));
      SalesHeader.INSERT(TRUE);
      SalesHeader.VALIDATE("Sell-to Customer No.",Customer."No.");
      SalesHeader.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE CreateSalesLine@6(SalesHeader@1000 : Record 36;Item@1001 : Record 27);
    VAR
      SalesLine@1002 : Record 37;
    BEGIN
      SalesLine.VALIDATE("Document Type",SalesHeader."Document Type");
      SalesLine.VALIDATE("Document No.",SalesHeader."No.");
      SalesLine.VALIDATE("Sell-to Customer No.",SalesHeader."Sell-to Customer No.");
      SalesLine.VALIDATE("Line No.",10000);
      SalesLine.INSERT(TRUE);
      SalesLine.VALIDATE(Type,SalesLine.Type::Item);
      SalesLine.VALIDATE("No.",Item."No.");
      SalesLine.VALIDATE(Quantity,1);
      SalesLine.MODIFY(TRUE);
    END;

    BEGIN
    END.
  }
}

