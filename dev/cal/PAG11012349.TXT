OBJECT Page 11012349 Items to Replace
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Items to Replace;
               NOR=Artikler som skal erstattes;
               SVE=Artiklar som ska ers„ttas];
    SourceTable=Table11012322;
    DelayedInsert=Yes;
    PageType=List;
    UsageCategory=Tasks;
    OnInit=BEGIN
             "Cost Object (Old)Editable" := TRUE;
             "Vendor (Old)Editable" := TRUE;
             "Trade Item (Old)Editable" := TRUE;
             "Manufacturer (Old)Editable" := TRUE;
             "Basic Item (Old)Editable" := TRUE;
             "Item No. (Old)Editable" := TRUE;
             ManualEditable := TRUE;
           END;

    OnOpenPage=VAR
                 CompRel@11012000 : Record 11012059;
                 CentrManagementCU@11012001 : Codeunit 11012010;
                 NavBln@11012002 : Boolean;
                 DiscBln@1100485000 : Boolean;
                 DiscGrpBln@1100528300 : Boolean;
                 RcpBln@11012005 : Boolean;
                 FPSLicenseManagement@1100528500 : Codeunit 11229289;
               BEGIN
                 IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRel) THEN BEGIN
                   CentrManagementCU.ItemFormEditable(NavBln,DiscBln,DiscGrpBln,RcpBln);
                   IF (NavBln) THEN BEGIN
                     //db.sn, 25-05-07: vervallen artikel centraal beheer; keuze vervangend artikel decentraal
                     ManualEditable := FALSE;
                     "Item No. (Old)Editable" := FALSE;
                     "Basic Item (Old)Editable" := FALSE;
                     "Manufacturer (Old)Editable" := FALSE;
                     "Trade Item (Old)Editable" := FALSE;
                     "Vendor (Old)Editable" := FALSE;
                     "Cost Object (Old)Editable" := FALSE;
                     //db.en, 25-05-07
                   END;
                 END;
               END;

    OnAfterGetRecord=BEGIN
                       CheckMissingTradeItem;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190012;1 ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 1100409000;2 ;Action    ;
                      Name=ImportTradeItems;
                      CaptionML=ENU=Import Trade Items to replace;
                      Image=Import;
                      OnAction=VAR
                                 ImportTradeItemstoreplace@1100529500 : Page 11072682;
                               BEGIN
                                 ImportTradeItemstoreplace.RUN;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 1210190013;2 ;Action    ;
                      CaptionML=[ENU=Assign collectively (Select);
                                 NOR=Tilordne kollektivt (velg);
                                 SVE=Tilldela kollektivt (v„lj)];
                      Promoted=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 MarkSelection(TRUE);
                               END;
                                }
      { 1210190014;2 ;Action    ;
                      CaptionML=[ENU=Disconnect collectively (Select);
                                 NOR=Koble fra kollektivt (velg);
                                 SVE=Koppla bort kollektivt (v„lj)];
                      Promoted=Yes;
                      Image=Reject;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 MarkSelection(FALSE);
                               END;
                                }
      { 1210190045;2 ;Action    ;
                      ShortCutKey=F9;
                      Ellipsis=Yes;
                      CaptionML=[ENU=&Process;
                                 NOR=&Behandle;
                                 SVE=&Behandla];
                      Promoted=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF PAGE.RUNMODAL(PAGE::"Replace Items") = ACTION::OK THEN;  //Force OK and Cancel button
                               END;
                                }
      { 1100528901;2 ;Action    ;
                      Name=CreateTradeItems;
                      CaptionML=ENU=Create Trade Items (webshop);
                      Image=CreateLinesFromJob;
                      OnAction=BEGIN
                                 GetTradeItemsFromWebShop;  //C060176
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 1210190010;2;Field  ;
                SourceExpr=Manual;
                Visible=FALSE;
                Editable=ManualEditable }

    { 2   ;2   ;Field     ;
                SourceExpr="Item No. (Old)";
                Editable="Item No. (Old)Editable" }

    { 4   ;2   ;Field     ;
                SourceExpr="Manufacturer (Old)";
                Visible=FALSE;
                Editable="Manufacturer (Old)Editable" }

    { 6   ;2   ;Field     ;
                SourceExpr="Basic Item (Old)";
                Visible=FALSE;
                Editable="Basic Item (Old)Editable";
                OnLookup=BEGIN
                           CLEAR(BasicItemFrm);
                           BasicItemFrm.LOOKUPMODE(TRUE);
                           BasicItemRec.InitLookup(BasicItemRec, "Basic Item (Old)", "Manufacturer (Old)");
                           BasicItemFrm.SETRECORD(BasicItemRec);

                           IF BasicItemFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             BasicItemFrm.GETRECORD(BasicItemRec);
                             "Manufacturer (Old)" := BasicItemRec.Manufacturer;
                             VALIDATE("Basic Item (Old)", BasicItemRec."Product Code");
                           END;
                         END;
                          }

    { 8   ;2   ;Field     ;
                SourceExpr="Vendor (Old)";
                Editable="Vendor (Old)Editable" }

    { 10  ;2   ;Field     ;
                SourceExpr="Trade Item (Old)";
                Editable="Trade Item (Old)Editable";
                OnLookup=BEGIN
                           CLEAR(TradeItemFrm);
                           TradeItemFrm.LOOKUPMODE(TRUE);
                           TradeItemRec.InitLookup(TradeItemRec, "Basic Item (Old)", "Manufacturer (Old)", "Trade Item (Old)", "Vendor (Old)");
                           TradeItemFrm.SETRECORD(TradeItemRec);

                           IF TradeItemFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             TradeItemFrm.GETRECORD(TradeItemRec);
                             "Manufacturer (Old)" := TradeItemRec.Manufacturer;
                             "Basic Item (Old)" := TradeItemRec."Product Code";
                             "Vendor (Old)" := TradeItemRec.Vendor;
                             VALIDATE("Trade Item (Old)", TradeItemRec."Item Code");
                           END;
                         END;
                          }

    { 1210190004;2;Field  ;
                SourceExpr="Cost Object (Old)";
                Visible=FALSE;
                Editable="Cost Object (Old)Editable" }

    { 1210190000;2;Field  ;
                SourceExpr="Description (Old)" }

    { 1210190008;2;Field  ;
                SourceExpr=Select }

    { 16  ;2   ;Field     ;
                SourceExpr="Item No. (New)" }

    { 18  ;2   ;Field     ;
                SourceExpr="Manufacturer (New)";
                Visible=FALSE }

    { 20  ;2   ;Field     ;
                SourceExpr="Basic Item (New)";
                Visible=FALSE;
                OnLookup=BEGIN
                           CLEAR(BasicItemFrm);
                           BasicItemFrm.LOOKUPMODE(TRUE);
                           BasicItemRec.InitLookup(BasicItemRec, "Basic Item (New)", "Manufacturer (New)");
                           BasicItemFrm.SETRECORD(BasicItemRec);

                           IF BasicItemFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             BasicItemFrm.GETRECORD(BasicItemRec);
                             "Manufacturer (New)" := BasicItemRec.Manufacturer;
                             VALIDATE("Basic Item (New)", BasicItemRec."Product Code");
                           END;
                         END;
                          }

    { 22  ;2   ;Field     ;
                SourceExpr="Vendor (New)" }

    { 24  ;2   ;Field     ;
                SourceExpr="Trade Item (New)";
                Style=Attention;
                StyleExpr=MissingTradeItem;
                OnLookup=BEGIN
                           CLEAR(TradeItemFrm);
                           TradeItemFrm.LOOKUPMODE(TRUE);
                           TradeItemRec.InitLookup(TradeItemRec, "Basic Item (New)", "Manufacturer (New)", "Trade Item (New)", "Vendor (New)");
                           TradeItemFrm.SETRECORD(TradeItemRec);

                           IF TradeItemFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             TradeItemFrm.GETRECORD(TradeItemRec);
                             "Manufacturer (New)" := TradeItemRec.Manufacturer;
                             "Basic Item (New)" := TradeItemRec."Product Code";
                             "Vendor (New)" := TradeItemRec.Vendor;
                             VALIDATE("Trade Item (New)", TradeItemRec."Item Code");
                           END;
                         END;

                OnAssistEdit=BEGIN
                               //C060176
                               IF (HandleAssistEditItem(FIELDNO("Trade Item (New)"))) THEN
                                 CurrPage.UPDATE(TRUE);
                             END;
                              }

    { 1210190006;2;Field  ;
                SourceExpr="Cost Object (New)";
                Visible=FALSE }

    { 1210190002;2;Field  ;
                SourceExpr="Description (New)" }

    { 1100528900;2;Field  ;
                SourceExpr="Trade Item Exists" }

  }
  CODE
  {
    VAR
      BasicItemRec@1100485003 : Record 11012316;
      TradeItemRec@1100485002 : Record 11012317;
      BasicItemFrm@1100485001 : Page 11012333;
      TradeItemFrm@1100485000 : Page 11012334;
      Text001@1210190000 : TextConst 'ENU=First select line(s).;NOR=Velg rad(er) f›rst.;SVE=V„lj f”rst rad(er).';
      ManualEditable@19061609 : Boolean INDATASET;
      "Item No. (Old)Editable"@19003321 : Boolean INDATASET;
      "Basic Item (Old)Editable"@19057055 : Boolean INDATASET;
      "Manufacturer (Old)Editable"@19040209 : Boolean INDATASET;
      "Trade Item (Old)Editable"@19005738 : Boolean INDATASET;
      "Vendor (Old)Editable"@19002269 : Boolean INDATASET;
      "Cost Object (Old)Editable"@19029260 : Boolean INDATASET;
      MissingTradeItem@1100528900 : Boolean INDATASET;

    PROCEDURE MarkSelection@1210190000(lvAction@1210190001 : Boolean);
    VAR
      ReplaceRec@1210190000 : Record 11012322;
    BEGIN
      CurrPage.SETSELECTIONFILTER(ReplaceRec);
      IF NOT ReplaceRec.FINDFIRST THEN BEGIN
        MESSAGE(Text001);
        EXIT;
      END;

      REPEAT
        ReplaceRec.VALIDATE(Select, lvAction);
        ReplaceRec.MODIFY;
      UNTIL ReplaceRec.NEXT = 0;

      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE GetTradeItemsFromWebShop@1100528901();
    VAR
      ReplaceRec@1100528900 : Record 11012322;
    BEGIN
      //C060176
      CurrPage.SETSELECTIONFILTER(ReplaceRec);
      IF ReplaceRec.FINDFIRST THEN BEGIN
        REPEAT
          IF (ReplaceRec."Trade Item (New)" <> '') THEN BEGIN
            ReplaceRec.CALCFIELDS("Trade Item Exists");
            IF ReplaceRec."Trade Item Exists" = FALSE THEN
              AddTradeItem(ReplaceRec."Vendor (New)", ReplaceRec."Trade Item (New)");
          END;
        UNTIL ReplaceRec.NEXT = 0;
      END;
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE AddTradeItem@1100528704(VAR VendorNo@1100528700 : Code[20];VAR TradeItemNo@1100528701 : Code[20]) : Boolean;
    VAR
      WebshopItemMgt@1100528702 : Codeunit 11012306;
      LicensePermission@1100528703 : Record 2000000043;
    BEGIN
      //C060176: copy from codeunit 11012199
      IF NOT LicensePermission.GET(LicensePermission."Object Type"::Codeunit, CODEUNIT::"2ba Unifeed License") OR
         (LicensePermission."Execute Permission" = LicensePermission."Execute Permission"::" ") THEN
        EXIT(FALSE);
      WebshopItemMgt.UpdateTradeItemDataForOneItem(VendorNo, TradeItemNo, FALSE);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckMissingTradeItem@1100528900();
    BEGIN
      //C060176
      MissingTradeItem := FALSE;
      IF "Trade Item (New)" <> '' THEN BEGIN
        CALCFIELDS("Trade Item Exists");
        MissingTradeItem := ("Trade Item Exists" = FALSE);
      END;
    END;

    BEGIN
    END.
  }
}

