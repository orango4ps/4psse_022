OBJECT Table 6086311 Continia User Credit Card
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 270=r;
    DataCaptionFields=Continia User ID,Card No.;
    OnInsert=VAR
               ContiniaUserSetup@1160040000 : Record 6086002;
             BEGIN
               TESTFIELD("Continia User ID");

               IF ContiniaUserSetup.GET("Continia User ID") THEN
                 IF NOT ContiniaUserSetup."Expense Management User" THEN BEGIN
                   ContiniaUserSetup."Expense Management User" := TRUE;
                   ContiniaUserSetup.MODIFY;
                 END;
             END;

    OnModify=BEGIN
               TESTFIELD("Continia User ID");
             END;

    OnDelete=BEGIN
               FailIfIncompletedBankTrans("Card No.","Continia User ID",TRUE);
             END;

    CaptionML=[DEU=Continia-Benutzer-Kreditkarte;
               ENU=Continia User Credit Card;
               NLD=Continia gebruiker creditcard];
    LookupPageID=Page6086331;
    DrillDownPageID=Page6086331;
  }
  FIELDS
  {
    { 1   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=BEGIN
                                                                IF ("Continia User ID" <> xRec."Continia User ID") AND (xRec."Continia User ID" <> '') THEN
                                                                  FailIfIncompletedBankTrans("Card No.",xRec."Continia User ID",TRUE);
                                                              END;

                                                   CaptionML=[DEU=Continia-Benutzer-ID;
                                                              ENU=Continia User ID;
                                                              NLD=Continia gebruikers-id;
                                                              SVE=AnstÑllningsnr];
                                                   NotBlank=Yes }
    { 3   ;   ;Card No.            ;Code20        ;OnValidate=VAR
                                                                EMSetup@1160040001 : Record 6086300;
                                                                UserCreditCard@1160040000 : Record 6086311;
                                                              BEGIN
                                                                EMSetup.GET;
                                                                IF NOT EMSetup."Allow duplicate Card ID" THEN
                                                                  IF "Card No." <> '' THEN BEGIN
                                                                    UserCreditCard.SETCURRENTKEY("Card No.");
                                                                    UserCreditCard.SETRANGE("Card No.","Card No.");
                                                                    UserCreditCard.SETFILTER("Continia User ID",'<>%1',UserCreditCard."Continia User ID");
                                                                    IF UserCreditCard.FINDFIRST THEN
                                                                      ERROR(DoubleCardErr,"Card No.",UserCreditCard."Continia User ID",EMSetup.TABLECAPTION);
                                                                  END;

                                                                IF ("Card No." <> xRec."Card No.") AND (xRec."Card No." <> '') THEN
                                                                  FailIfIncompletedBankTrans(xRec."Card No.","Continia User ID",TRUE);
                                                              END;

                                                   CaptionML=[DEU=Kartennr.;
                                                              ENU=Card No.;
                                                              NLD=Kaartnr.];
                                                   NotBlank=Yes }
    { 10  ;   ;Account Type        ;Option        ;OnValidate=VAR
                                                                EMSetup@1160040000 : Record 6086300;
                                                              BEGIN
                                                                IF "Account Type" <> xRec."Account Type" THEN
                                                                  VALIDATE("Account No.",'');

                                                                IF "Account Type" = "Account Type"::Vendor THEN BEGIN
                                                                  EMSetup.GET;
                                                                  EMSetup.TESTFIELD("Expense Posting",EMSetup."Expense Posting"::"Always Purchase Invoice");
                                                                  EMSetup.TESTFIELD("Post Bank Trans. on Import",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Bankkontoart;
                                                              ENU=Bank Account Type;
                                                              NLD=Rekeningtype];
                                                   OptionCaptionML=[DEU=Sachkonto,,Kreditor,Bankkonto;
                                                                    ENU=G/L Account,,Vendor,Bank Account;
                                                                    NLD=Grootboekrekening,,Leverancier,Bankrekening];
                                                   OptionString=G/L Account,,Vendor,Bank Account }
    { 11  ;   ;Account No.         ;Code20        ;TableRelation=IF (Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Account Type=CONST(Vendor)) Vendor;
                                                   OnValidate=VAR
                                                                EMSetup@1160040000 : Record 6086300;
                                                                ContiniaUserCreditCard@1160040002 : Record 6086311;
                                                                BankTransaction@1160040001 : Record 6086330;
                                                                NumberOfCreditCard@1160040003 : Integer;
                                                              BEGIN
                                                                ValidateAccount;

                                                                BankTransaction.SETCURRENTKEY("Card No.");
                                                                BankTransaction.SETRANGE("Card No.","Card No.");
                                                                BankTransaction.SETRANGE("Continia User ID","Continia User ID");
                                                                BankTransaction.MODIFYALL("Bank Account Type","Account Type");
                                                                BankTransaction.MODIFYALL("Bank Account No.","Account No.");

                                                                IF CurrencyChanged THEN
                                                                  IF NOT CONFIRM(CurrencyChangedConfirm,FALSE) THEN
                                                                    ERROR('');

                                                                EMSetup.GET;
                                                                IF EMSetup."Post Bank Trans. on Import" THEN
                                                                  FailIfIncompletedBankTrans("Card No.","Continia User ID",TRUE);

                                                                IF CurrencyChanged THEN
                                                                  UpdateBankTransCurrCode(
                                                                    GetAccountCurrencyCode(xRec."Account Type",xRec."Account No."),GetAccountCurrencyCode("Account Type","Account No."),
                                                                      "Continia User ID");

                                                                IF (xRec."Account No." <> "Account No.") AND ("Account No." <> '') THEN BEGIN
                                                                  ContiniaUserCreditCard.SETCURRENTKEY("Card No.");
                                                                  ContiniaUserCreditCard.SETRANGE("Card No.","Card No.");
                                                                  ContiniaUserCreditCard.SETFILTER("Continia User ID",'<>%1',"Continia User ID");
                                                                  NumberOfCreditCard := ContiniaUserCreditCard.COUNT;
                                                                  IF NumberOfCreditCard > 0 THEN
                                                                    IF CONFIRM(MoreCreditCardExist,FALSE,FORMAT(NumberOfCreditCard),TABLECAPTION,
                                                                        ContiniaUserCreditCard.FIELDCAPTION("Card No."),"Card No.",BankTransaction.TABLECAPTION)
                                                                    THEN BEGIN
                                                                      ContiniaUserCreditCard.FINDSET;
                                                                      REPEAT
                                                                        ContiniaUserCreditCard."Account Type" := "Account Type";
                                                                        ContiniaUserCreditCard."Account No." := "Account No.";
                                                                        ContiniaUserCreditCard.MODIFY;
                                                                        BankTransaction.SETRANGE("Card No.",ContiniaUserCreditCard."Card No.");
                                                                        BankTransaction.SETRANGE("Continia User ID",ContiniaUserCreditCard."Continia User ID");
                                                                        BankTransaction.MODIFYALL("Bank Account Type","Account Type");
                                                                        BankTransaction.MODIFYALL("Bank Account No.","Account No.");
                                                                        IF CurrencyChanged THEN
                                                                          UpdateBankTransCurrCode(
                                                                           GetAccountCurrencyCode(xRec."Account Type",xRec."Account No."),GetAccountCurrencyCode("Account Type","Account No."),
                                                                           ContiniaUserCreditCard."Continia User ID");
                                                                      UNTIL ContiniaUserCreditCard.NEXT = 0;
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Bankkontonummer;
                                                              ENU=Bank Account No.;
                                                              NLD=Rekeningnr.;
                                                              NOR=Bankkontonr.;
                                                              SVE=Bankkontonr] }
    { 20  ;   ;Mapping Count       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Credit Card User Mapping" WHERE (Card No.=FIELD(Card No.),
                                                                                                       Continia User ID=FIELD(Continia User ID)));
                                                   CaptionML=[DEU=Anzahl Feldzuordnung;
                                                              ENU=Field Mapping Count;
                                                              NLD=Aantal veldmapping];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Continia User ID,Card No.               ;Clustered=Yes }
    {    ;Card No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      BnkTrMustBePostedTxt@1160040001 : TextConst 'DEU=Alle Banktransaktionen mÅssen Åbereinstimmen und gebucht werden.;ENU=All bank transactions must be matched and posted.;NLD=Alle banktransacties dienen gematched en geboekt te worden.';
      CurrencyChangedConfirm@1160040000 : TextConst 'DEU=Die WÑhrung des Kontos wurde geÑndert. Sind Sie sicher, dass Sie alle nicht gebuchten Ausgaben aktualisieren mîchten?;ENU=The currency of the account has been changed. Are you sure you want to update all the unposted expense?;NLD=De valutacode van de rekening is gewijzigd. Weet u zeker dat u de ongeboekte uitgaven wilt bijwerken?';
      DoubleCardErr@1000 : TextConst 'DEU=Es ist bereits eine Karte mit der Nummer %1 mit %2 verknÅpft.;ENU=There is already a card with number %1 associated to %2.\You need to change %3 to allow having duplicate Card ID.;NLD=Er is al een kaart met nummer %1 gekoppeld aan %2.\U moet %3 wijzigen om dubbele kaart-ID toe te staan.';
      MatchedExpToPostTxt@1160040002 : TextConst 'DEU=Alle abgeglichenen Ausgaben mÅssen gebucht werden.;ENU=All matched expenses must be posted.;NLD=Alle gematchte uitgaven dienen geboekt te worden.';
      MoreCreditCardExist@1160040003 : TextConst 'DEU=%1 extra %2 mit %3 gleich %4 vorhanden.\\Muss %2 und dazugehîriger %5 geÑndert werden?;ENU=%1 additional %2 with %3 equals to %4 exist.\\Should this %2 and corresponding %5 be changed, too?;NLD=%1 extra %2 met %3 gelijk aan %4 aanwezig.\\Zou deze %2 en bijbehorende %5 ook moeten worden gewijzigd?';

    PROCEDURE GetAccountCurrencyCode@1160040003(AccountType@1160040000 : 'G/L Account,,Vendor,Bank Account';AccountNo@1160040003 : Code[20]) : Code[10];
    VAR
      Vendor@1160040002 : Record 23;
      BankAccount@1160040001 : Record 270;
    BEGIN
      IF AccountNo = '' THEN
        EXIT;

      CASE AccountType OF
        AccountType::"G/L Account":
          EXIT;

        AccountType::Vendor:
          IF Vendor.GET(AccountNo) THEN
            EXIT(Vendor."Currency Code");

        AccountType::"Bank Account":
          IF BankAccount.GET(AccountNo) THEN
            EXIT(BankAccount."Currency Code");
      END;
    END;

    LOCAL PROCEDURE ValidateAccount@1160040001();
    BEGIN
      CheckAccount("Account Type","Account No.");
    END;

    PROCEDURE CheckAccount@1160040004(AccountType@1160040004 : 'G/L Account,,Vendor,Bank Account';AccountNo@1160040003 : Code[20]);
    VAR
      GLAccount@1160040001 : Record 15;
      Vendor@1160040002 : Record 23;
      Bank@1160040000 : Record 270;
    BEGIN
      IF "Account No." = '' THEN
        EXIT;

      CASE "Account Type" OF
        "Account Type"::"G/L Account" :
          BEGIN
            GLAccount.GET("Account No.");
            GLAccount.CheckGLAcc;
            GLAccount.TESTFIELD("Direct Posting");
          END;

        "Account Type"::Vendor :
          BEGIN
            Vendor.GET("Account No.");
            Vendor.TESTFIELD(Blocked,Vendor.Blocked::" ");
          END;

        "Account Type"::"Bank Account" :
          BEGIN
            Bank.GET("Account No.");
            Bank.TESTFIELD(Blocked,FALSE);
          END;
      END;
    END;

    LOCAL PROCEDURE CurrencyChanged@1160040000() : Boolean;
    BEGIN
      IF "Account No." <> xRec."Account No." THEN
        EXIT(GetAccountCurrencyCode(xRec."Account Type",xRec."Account No.") <> GetAccountCurrencyCode("Account Type","Account No."));
    END;

    LOCAL PROCEDURE UpdateBankTransCurrCode@1160040002(OldCurrencyCode@1160040002 : Code[10];NewCurrencyCode@1160040003 : Code[10];ContiniaUserId@1160040001 : Code[50]);
    VAR
      Expense@1160040005 : Record 6086320;
      BankTransaction@1160040000 : Record 6086330;
    BEGIN
      BankTransaction.SETRANGE("Card No.","Card No.");
      BankTransaction.SETRANGE("Continia User ID",ContiniaUserId);
      IF BankTransaction.FINDSET THEN
        REPEAT
          IF Expense.GET(BankTransaction.GetMatchedExpenseEntryNo) THEN BEGIN
            IF (NOT Expense.Posted) AND (Expense."Bank Currency Code" = OldCurrencyCode) THEN BEGIN
              Expense.VALIDATE("Bank Currency Code",NewCurrencyCode);
              Expense."Bank-Currency Amount" := BankTransaction."Bank-Currency Amount";
              Expense.MODIFY;

              BankTransaction."Bank Currency Code" := NewCurrencyCode;
              BankTransaction.MODIFY;
            END;
          END ELSE BEGIN
            BankTransaction."Bank Currency Code" := NewCurrencyCode;
            BankTransaction.MODIFY;
          END;
        UNTIL BankTransaction.NEXT = 0;
    END;

    PROCEDURE FailIfIncompletedBankTrans@1160040007(CardNo@1160040004 : Code[20];ContiniaUserID@1160040005 : Code[50];FailOnUnmatchedTrans@1160040003 : Boolean);
    VAR
      Expense@1160040002 : Record 6086320;
      BankTransaction@1160040000 : Record 6086330;
    BEGIN
      BankTransaction.SETRANGE("Card No.",CardNo);
      BankTransaction.SETRANGE("Continia User ID",ContiniaUserID);
      BankTransaction.SETRANGE("Matched to Expense",TRUE);
      IF BankTransaction.FINDSET THEN
        REPEAT
          Expense.GET(BankTransaction.GetMatchedExpenseEntryNo);
          IF NOT Expense.Posted THEN
            ERROR(MatchedExpToPostTxt);
        UNTIL BankTransaction.NEXT = 0;

      IF FailOnUnmatchedTrans THEN BEGIN
        BankTransaction.SETRANGE("Matched to Expense",FALSE);
        IF NOT BankTransaction.ISEMPTY THEN
          ERROR(BnkTrMustBePostedTxt);
      END;
    END;

    PROCEDURE InitAccountTypeAndNo@1160040006();
    VAR
      EMSetup@1160040000 : Record 6086300;
    BEGIN
      EMSetup.GET;
      IF EMSetup."Card Transaction Bal. No." <> '' THEN BEGIN
        "Account Type" := EMSetup."Card Transaction Bal. Type";
        "Account No." := EMSetup."Card Transaction Bal. No.";
      END;
    END;

    PROCEDURE HasUserCreditCard@1160040005(User@1160040000 : Code[50]) : Boolean;
    VAR
      ContiniaUserCreditCard@1160040001 : Record 6086311;
    BEGIN
      ContiniaUserCreditCard.SETRANGE("Continia User ID",User);
      EXIT(ContiniaUserCreditCard.FINDFIRST);
    END;

    BEGIN
    END.
  }
}

