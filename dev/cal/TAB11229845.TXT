OBJECT Table 11229845 Rental Unit Feature Type
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Entity Type" = "Entity Type"::RentalPackage THEN
                 TESTFIELD("Starting Date Package");
               "Line No." := GetNextLineNo();
               FillModificationData;
             END;

    OnModify=BEGIN
               FillModificationData;
             END;

    OnDelete=VAR
               RentalUnitFeatureGroup@1100528400 : Record 11229843;
               RentalUnitFeatureType@1100528401 : Record 11229845;
             BEGIN
               IF "Feature Group Code" <> '' THEN BEGIN
                 IF RentalUnitFeatureGroup.GET("Feature Group Code") THEN BEGIN
                   RentalUnitFeatureType.SETRANGE("Project No.", "Project No.");
                   RentalUnitFeatureType.SETRANGE("Rental Unit", "Rental Unit");
                   RentalUnitFeatureType.SETRANGE("Starting Date Package", "Starting Date Package");
                   RentalUnitFeatureType.SETRANGE("Entity Type", "Entity Type");
                   RentalUnitFeatureType.SETRANGE("Feature Group Code", "Feature Group Code");
                   RentalUnitFeatureType.SETFILTER("Line No.", '<>%1', "Line No.");
                   IF NOT RentalUnitFeatureType.FINDFIRST THEN BEGIN
                     CASE "Entity Type" OF
                       "Entity Type"::RentalUnit:
                         RentalUnitFeatureGroup.TESTFIELD("Obligatory for Rental Units", FALSE);
                       "Entity Type"::RentalPackage:
                         RentalUnitFeatureGroup.TESTFIELD("Obligatory for Rental Packages", FALSE);
                     END;
                   END;
                 END;
               END;
             END;

    OnRename=BEGIN
               ERROR(Text999);
             END;

    CaptionML=ENU=Rental Unit Feature Type;
    LookupPageID=Page11229824;
    DrillDownPageID=Page11229824;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Rental Unit         ;Code20        ;TableRelation="Rental Unit"."Rental Unit" WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Rental Unit;
                                                              NOR=Leieenhet;
                                                              SVE=Uthyrningsenhet];
                                                   NotBlank=Yes }
    { 25  ;   ;Starting Date Package;Date         ;TableRelation="Rental Package"."Starting Date" WHERE (Project No.=FIELD(Project No.),
                                                                                                         Rental Unit=FIELD(Rental Unit));
                                                   CaptionML=[ENU=Starting Date Package;
                                                              NOR=Startdato for Pakke] }
    { 30  ;   ;Entity Type         ;Option        ;CaptionML=[ENU=Entity Type;
                                                              NOR=Enhetstype;
                                                              SVE=Enhetstyp];
                                                   OptionCaptionML=ENU=" ,RentalUnit,RentalPackage";
                                                   OptionString=,RentalUnit,RentalPackage }
    { 50  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 60  ;   ;Feature Group Code  ;Code10        ;TableRelation="Rental Unit Feature Group";
                                                   OnValidate=VAR
                                                                RentalUnitFeature@1100528400 : Record 11229844;
                                                              BEGIN
                                                                IF ("Feature Group Code" <> xRec."Feature Group Code") THEN BEGIN
                                                                  IF NOT RentalUnitFeature.GET("Feature Group Code", "Feature Code") THEN
                                                                    "Feature Code" := '';
                                                                  CheckFeatureOnOtherLine;
                                                                END;
                                                                CALCFIELDS("Feature Group Description");
                                                              END;

                                                   CaptionML=[ENU=Feature Group Code;
                                                              NOR=Funksjonsgruppekode;
                                                              SVE=Funktionsgruppskod] }
    { 65  ;   ;Feature Group Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Rental Unit Feature Group".Description WHERE (Code=FIELD(Feature Group Code)));
                                                   CaptionML=[ENU=Feature Group Description;
                                                              NOR=Beskrivelse av funksjonsgruppe;
                                                              SVE=Beskrivning av funktionsgrupp] }
    { 80  ;   ;Feature Code        ;Code10        ;TableRelation="Rental Unit Feature"."Feature Code" WHERE (Feature Group Code=FIELD(Feature Group Code));
                                                   OnValidate=BEGIN
                                                                IF ("Feature Code" <> xRec."Feature Code") THEN
                                                                  CheckFeatureOnOtherLine;
                                                                CALCFIELDS("Feature Description");
                                                              END;

                                                   CaptionML=[ENU=Feature Code;
                                                              NOR=Funksjonskode;
                                                              SVE=Funktionskod] }
    { 90  ;   ;Feature Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Rental Unit Feature".Description WHERE (Feature Group Code=FIELD(Feature Group Code),
                                                                                                               Feature Code=FIELD(Feature Code)));
                                                   CaptionML=[ENU=Feature Description;
                                                              NOR=Funksjonsbeskrivelse;
                                                              SVE=Funktionsbeskrivning];
                                                   Editable=No }
    { 100 ;   ;Value               ;Decimal       ;CaptionML=[ENU=Value;
                                                              NOR=Verdi;
                                                              SVE=V„rde] }
    { 110 ;   ;Remarks             ;Text50        ;CaptionML=[ENU=Remarks;
                                                              NOR=Merknader;
                                                              SVE=Anm„rkningar] }
    { 120 ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified By;
                                                              NOR=Modified By;
                                                              SVE=Modified By] }
    { 130 ;   ;Last Date-Time Modified;DateTime   ;CaptionML=[ENU=Last Date-Time Modified;
                                                              NOR=Dato/klokkeslett for siste endring;
                                                              SVE=Žndrades senast (datum/tid)] }
  }
  KEYS
  {
    {    ;Project No.,Rental Unit,Starting Date Package,Entity Type,Line No.;
                                                   Clustered=Yes }
    {    ;Feature Group Code,Feature Code          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528400 : TextConst 'ENU=Attention, %1 ''%2'' - ''%3'' is already present.';
      Text001@1100528300 : TextConst 'ENU=Not all of the obligatory Features are filled.;NOR=Alle obligatoriske funksjoner er ikke fylt ut.;SVE=Alla obligatoriska funktioner har inte fyllts i.';
      Text002@1100528301 : TextConst 'ENU=Not all of the obligatory Values are filled.;NOR=Alle obligatoriske verdier er ikke fylt ut.;SVE=Alla obligatoriska v„rden har inte fyllts i.';
      Text999@1100528401 : TextConst 'ENU=Rename not allowed.;NOR=Ikke tillatt † bytte navn.;SVE=Det „r inte till†tet att byta namn.';

    PROCEDURE GetNextLineNo@1100485000() : Integer;
    VAR
      RentalUnitFeatureType@1100485000 : Record 11229845;
    BEGIN
      RentalUnitFeatureType.SETRANGE("Project No.", "Project No.");
      RentalUnitFeatureType.SETRANGE("Rental Unit", "Rental Unit");
      RentalUnitFeatureType.SETRANGE("Starting Date Package", "Starting Date Package");
      RentalUnitFeatureType.SETRANGE("Entity Type", "Entity Type");
      IF RentalUnitFeatureType.FINDLAST THEN
        EXIT(RentalUnitFeatureType."Line No." + 10000);
      EXIT(10000);
    END;

    LOCAL PROCEDURE FillModificationData@1100485006();
    BEGIN
      "Modified by" := USERID;
      "Last Date-Time Modified" := CURRENTDATETIME;
    END;

    PROCEDURE CheckFeatureOnOtherLine@1100485005();
    VAR
      RentalUnitFeatureType@1100485000 : Record 11229845;
    BEGIN
      IF ("Feature Group Code" = '') OR ("Feature Code" = '') THEN
        EXIT;

      RentalUnitFeatureType.SETRANGE("Project No.", "Project No.");
      RentalUnitFeatureType.SETRANGE("Rental Unit", "Rental Unit");
      RentalUnitFeatureType.SETRANGE("Starting Date Package", "Starting Date Package");
      RentalUnitFeatureType.SETRANGE("Entity Type", "Entity Type");
      RentalUnitFeatureType.SETFILTER("Line No.", '<>%1', "Line No.");
      RentalUnitFeatureType.SETRANGE("Feature Group Code", "Feature Group Code");
      RentalUnitFeatureType.SETRANGE("Feature Code", "Feature Code");
      IF RentalUnitFeatureType.FINDFIRST THEN
        MESSAGE(Text000, FIELDCAPTION("Feature Code"), "Feature Group Code", "Feature Code");
    END;

    PROCEDURE CreateFeatureTypes@1210190000();
    VAR
      RentalUnitFeatureType@1100485000 : Record 11229845;
      RentalUnitFeatureGroup@1210190000 : Record 11229843;
    BEGIN
      TESTFIELD("Project No.");
      TESTFIELD("Rental Unit");
      IF "Entity Type" = "Entity Type"::RentalPackage THEN
        TESTFIELD("Starting Date Package");

      RentalUnitFeatureType.COPY(Rec);
      WITH RentalUnitFeatureType DO BEGIN
        CASE "Entity Type" OF
          "Entity Type"::RentalUnit:
            RentalUnitFeatureGroup.SETRANGE("Rental Units", TRUE);
          "Entity Type"::RentalPackage:
            RentalUnitFeatureGroup.SETRANGE("Rental Packages", TRUE);
        END;

        IF NOT ISEMPTY THEN BEGIN
          //IF lines already exist then only add obligatory feature groups
          CASE "Entity Type" OF
            "Entity Type"::RentalUnit:
              RentalUnitFeatureGroup.SETRANGE("Obligatory for Rental Units", TRUE);
            "Entity Type"::RentalPackage:
              RentalUnitFeatureGroup.SETRANGE("Obligatory for Rental Packages", TRUE);
          END;
        END;

        IF RentalUnitFeatureGroup.FINDSET THEN BEGIN
          REPEAT
            SETRANGE("Feature Group Code", RentalUnitFeatureGroup.Code);
            IF NOT FIND('-') THEN BEGIN
              INIT;
              "Feature Group Code" := RentalUnitFeatureGroup.Code;
              INSERT(TRUE);
            END;
          UNTIL RentalUnitFeatureGroup.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE CheckMandatoryValues@1100528300(EntityType@1100529002 : Option;ProjectNo@1100528300 : Code[20];RentalUnit@1100529000 : Code[20];StartDate@1100529001 : Date);
    VAR
      RentalUnitFeatureType@1100528301 : Record 11229845;
      RentalUnitFeatureGroup@1100528302 : Record 11229843;
      RentalUnitFeature@1100528303 : Record 11229844;
    BEGIN
      CASE EntityType OF
        RentalUnitFeatureType."Entity Type"::RentalUnit:
          RentalUnitFeatureGroup.SETRANGE("Rental Units", TRUE);
        RentalUnitFeatureType."Entity Type"::RentalPackage:
          RentalUnitFeatureGroup.SETRANGE("Rental Packages", TRUE);
        ELSE
          EXIT;
      END;
      IF NOT RentalUnitFeatureGroup.FINDSET THEN
        EXIT;

      RentalUnitFeatureType.SETRANGE("Project No.", ProjectNo);
      RentalUnitFeatureType.SETRANGE("Rental Unit", RentalUnit);
      IF EntityType = RentalUnitFeatureType."Entity Type"::RentalPackage THEN
        RentalUnitFeatureType.SETRANGE("Starting Date Package", StartDate);
      RentalUnitFeatureType.SETRANGE("Entity Type", EntityType);
      REPEAT
        RentalUnitFeatureType.SETRANGE("Feature Group Code", RentalUnitFeatureGroup.Code);
        //
        RentalUnitFeature.SETRANGE("Feature Group Code", RentalUnitFeatureGroup.Code);
        IF (RentalUnitFeature.FINDSET) THEN BEGIN
          IF ((EntityType = RentalUnitFeatureType."Entity Type"::RentalUnit) AND
              (RentalUnitFeatureGroup."Obligatory for Rental Units")) OR
             ((EntityType = RentalUnitFeatureType."Entity Type"::RentalPackage) AND
              (RentalUnitFeatureGroup."Obligatory for Rental Packages"))
          THEN BEGIN
            RentalUnitFeatureType.SETRANGE("Feature Code");  // Reset filter
            IF RentalUnitFeatureType.ISEMPTY THEN BEGIN
              MESSAGE(Text001);
              EXIT;
            END ELSE BEGIN
              RentalUnitFeatureType.SETFILTER("Feature Code", '%1', '');
              IF NOT RentalUnitFeatureType.ISEMPTY THEN BEGIN
                MESSAGE(Text001);
                EXIT;
              END;
            END;
          END;
          REPEAT
            IF ((EntityType = RentalUnitFeatureType."Entity Type"::RentalUnit) AND
                (RentalUnitFeatureGroup."Value Obl. for Rental Units")) OR
               ((EntityType = RentalUnitFeatureType."Entity Type"::RentalPackage) AND
                (RentalUnitFeatureGroup."Value Obl. for Rental Packages"))
            THEN BEGIN
              RentalUnitFeatureType.SETRANGE("Feature Code", RentalUnitFeature."Feature Code");
              RentalUnitFeatureType.SETRANGE(Value, 0);
              IF NOT RentalUnitFeatureType.ISEMPTY THEN BEGIN
                MESSAGE(Text002);
                EXIT;
              END;
              RentalUnitFeatureType.SETRANGE(Value);  // Reset filter
            END;
          UNTIL RentalUnitFeature.NEXT = 0;
        END;
      UNTIL RentalUnitFeatureGroup.NEXT = 0;
    END;

    BEGIN
    {
      Copied from 'Assembly Features'
    }
    END.
  }
}

