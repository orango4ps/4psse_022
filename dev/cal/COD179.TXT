OBJECT Codeunit 179 Reversal-Post
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    TableNo=179;
    OnRun=VAR
            GLReg@1003 : Record 45;
            GenJnlTemplate@1002 : Record 80;
            PostedDeferralHeader@1007 : Record 1704;
            DeferralUtilities@1006 : Codeunit 1720;
            GenJnlPostReverse@1001 : Codeunit 17;
            Txt@1004 : Text[1024];
            WarningText@1000 : Text[250];
            Number@1005 : Integer;
            Handled@1008 : Boolean;
          BEGIN
            RESET;
            SETRANGE("Entry Type","Entry Type"::"Fixed Asset");
            IF FINDFIRST THEN
              WarningText := Text007;
            SETRANGE("Entry Type");
            IF PrintRegister THEN
              Txt := Text004 + WarningText + '\' + Text005
            ELSE
              Txt := Text004 + WarningText + '\' + Text002;
            IF NOT FINDFIRST THEN
              ERROR(Text006);

            IF NOT HideDialog THEN
              IF NOT CONFIRM(Txt,FALSE) THEN
                EXIT;

            ReversalEntry := Rec;
            IF "Reversal Type" = "Reversal Type"::Transaction THEN
              ReversalEntry.SetReverseFilter("Transaction No.","Reversal Type")
            ELSE
              ReversalEntry.SetReverseFilter("G/L Register No.","Reversal Type");
            ReversalEntry.CheckEntries;
            GET(1);
            IF "Reversal Type" = "Reversal Type"::Register THEN
              Number := "G/L Register No."
            ELSE
              Number := "Transaction No.";
            IF NOT ReversalEntry.VerifyReversalEntries(Rec,Number,"Reversal Type") THEN
              ERROR(Text008);
            GenJnlPostReverse.Reverse(ReversalEntry,Rec);
            IF PrintRegister THEN BEGIN
              GenJnlTemplate.VALIDATE(Type);
              IF GenJnlTemplate."Posting Report ID" <> 0 THEN
                IF GLReg.FINDLAST THEN BEGIN
                  GLReg.SETRECFILTER;
                  OnBeforeGLRegPostingReportPrint(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg,Handled);
                  IF NOT Handled THEN
                    REPORT.RUN(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg);
                END;
            END;
            DELETEALL;
            PostedDeferralHeader.DeleteForDoc(DeferralUtilities.GetGLDeferralDocType,ReversalEntry."Document No.",'',0,'');
            IF NOT HideDialog THEN
              MESSAGE(Text003);
          END;

  }
  CODE
  {
    VAR
      Text002@1007 : TextConst 'ENU=Do you want to reverse the entries?;NOR=Vil du tilbakef›re postene?;SVE=Vill du †terf”ra transaktionerna?';
      Text003@1005 : TextConst 'ENU=The entries were successfully reversed.;NOR=Postene er tilbakef›rt.;SVE=Transaktionerna har †terf”rts.';
      Text004@1012 : TextConst 'ENU=To reverse these entries, correcting entries will be posted.;NOR=Riktige poster vil bli bokf›rt for † tilbakef›re disse postene.;SVE=Korrigeringstransaktioner bokf”rs om du vill †terf”ra transaktionerna.';
      Text005@1000 : TextConst 'ENU=Do you want to reverse the entries and print the report?;NOR=Vil du tilbakef›re postene og skrive ut rapporten?;SVE=Vill du †terf”ra posterna och skriva ut rapporten?';
      Text006@1001 : TextConst 'ENU=There is nothing to reverse.;NOR=Det finnes ingenting † tilbakef›re.;SVE=Det finns inget att †terf”ra.';
      Text007@1004 : TextConst 'ENU=\There are one or more FA Ledger Entries. You should consider using the fixed asset function Cancel Entries.;NOR=\Det finnes en eller flere aktivaposter. Du b›r vurdere † bruke aktivafunksjonen Kanseller poster.;SVE=\Det finns en eller flera anl„ggningstransaktioner. Du b”r anv„nda funktionen R„tta transaktioner f”r anl„ggningstransaktioner.';
      Text008@1003 : TextConst 'ENU=Changes have been made to posted entries after the window was opened.\Close and reopen the window to continue.;NOR=Det er gjort endringer i bokf›rte poster etter at vinduet ble †pnet.\Lukk vinduet og †pne det p† nytt for † fortsette.;SVE=Žndringar har gjorts i bokf”rda transaktioner efter att f”nstret ”ppnades.\St„ng och ”ppna f”nstret p† nytt f”r att forts„tta.';
      ReversalEntry@1002 : Record 179;
      PrintRegister@1009 : Boolean;
      HideDialog@1006 : Boolean;

    [External]
    PROCEDURE SetPrint@1000(NewPrintRegister@1001 : Boolean);
    BEGIN
      PrintRegister := NewPrintRegister;
    END;

    [External]
    PROCEDURE SetHideDialog@1(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeGLRegPostingReportPrint@2(VAR ReportID@1000 : Integer;ReqWindow@1001 : Boolean;SystemPrinter@1002 : Boolean;VAR GLRegister@1003 : Record 45;VAR Handled@1004 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

