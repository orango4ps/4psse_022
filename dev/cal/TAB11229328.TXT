OBJECT Table 11229328 Purchase Inspection Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Modified by" := USERID;
               "Modified on" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               "Modified by" := USERID;
               "Modified on" := CURRENTDATETIME;
             END;

    CaptionML=ENU=Purchase Inspection Line;
    LookupPageID=Page11126650;
    DrillDownPageID=Page11126650;
  }
  FIELDS
  {
    { 10  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              NOR=Bilagstype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=ENU=,Quote,Order,,,,,Purchase Requisition;
                                                   OptionString=,Quote,Order,,,,,Purchase Requisition }
    { 20  ;   ;Document No.        ;Code20        ;TableRelation=IF (Document Type=CONST(Order)) "Purchase Header".No. WHERE (Document Type=CONST(Order))
                                                                 ELSE IF (Document Type=CONST(Quote)) "Purchase Header".No. WHERE (Document Type=CONST(Quote))
                                                                 ELSE IF (Document Type=CONST(Purchase Requisition)) "Purchase Requisition".No.;
                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Dokumentnr.;
                                                              SVE=Dokumentnr] }
    { 30  ;   ;Document Line No.   ;Integer       ;TableRelation=IF (Document Type=CONST(Order)) "Purchase Line"."Line No." WHERE (Document Type=CONST(Order),
                                                                                                                                   Document No.=FIELD(Document No.))
                                                                                                                                   ELSE IF (Document Type=CONST(Quote)) "Purchase Line"."Line No." WHERE (Document Type=CONST(Quote),
                                                                                                                                                                                                          Document No.=FIELD(Document No.))
                                                                                                                                                                                                          ELSE IF (Document Type=CONST(Purchase Requisition)) "Purchase Requisition Line"."Line No." WHERE (Purchase Requisition No.=FIELD(Document No.));
                                                   CaptionML=[ENU=Document Line No.;
                                                              NOR=Dokumentlinjenr.;
                                                              SVE=Dokumentradnr] }
    { 40  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 50  ;   ;Procurement Inspection Type;Code20 ;TableRelation="Procurement Inspection Type".Code;
                                                   CaptionML=ENU=Procurement Inspection Type }
    { 55  ;   ;Inspection Type Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Procurement Inspection Type".Description WHERE (Code=FIELD(Procurement Inspection Type)));
                                                   CaptionML=ENU=Procurement Inspection Type Description;
                                                   Editable=No }
    { 60  ;   ;Inspector Name      ;Text100       ;OnLookup=BEGIN
                                                              ShowEmployee("Employee No.");
                                                            END;

                                                   CaptionML=[ENU=Inspector Name;
                                                              NOR=Inspekt›rens navn;
                                                              SVE=Inspekt”rens namn] }
    { 65  ;   ;Employee No.        ;Code20        ;TableRelation=Employee.No.;
                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=Anst„llningsnr] }
    { 70  ;   ;Inspection Date     ;Date          ;CaptionML=[ENU=Inspection Date;
                                                              NOR=Inspeksjonsdato;
                                                              SVE=Inspektionsdatum] }
    { 80  ;   ;Location            ;Text50        ;CaptionML=[ENU=Location;
                                                              NOR=Plassering;
                                                              SVE=Lagerst„lle] }
    { 90  ;   ;Procurement Inspection Result;Code20;
                                                   TableRelation="Procurement Inspection Result".Code;
                                                   CaptionML=ENU=Procurement Inspection Result }
    { 95  ;   ;Inspection Result Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Procurement Inspection Result".Description WHERE (Code=FIELD(Procurement Inspection Result)));
                                                   CaptionML=ENU=Procurement Inspection Result Description;
                                                   Editable=No }
    { 100 ;   ;Comment             ;Text80        ;CaptionML=[ENU=Comment;
                                                              NOR=Kommentar;
                                                              SVE=Kommentar] }
    { 110 ;   ;Modified by         ;Text50        ;CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=Žndrad av] }
    { 120 ;   ;Modified on         ;DateTime      ;CaptionML=[ENU=Modified on;
                                                              NOR=Endret den;
                                                              SVE=Žndrad den] }
  }
  KEYS
  {
    {    ;Document Type,Document No.,Document Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529400 : TextConst 'ENU=Copy selected inspection codes to all the requisition lines of requisition %1?';

    PROCEDURE ShowEmployee@1210190000(EmployeeCode@1100528200 : Code[20]);
    VAR
      Employee@1210190000 : Record 5200;
    BEGIN
      IF ShowEmployee2(EmployeeCode, Employee) THEN BEGIN
        VALIDATE("Employee No.", Employee."No.");
        "Inspector Name" := Employee."Full Name";
      END;
    END;

    PROCEDURE ShowEmployee2@1210190001(EmployeeCode@1210190000 : Code[20];VAR Employee@1210190006 : Record 5200) : Boolean;
    BEGIN
      IF Employee.GET(EmployeeCode) THEN;
      IF PAGE.RUNMODAL(0, Employee) = ACTION::LookupOK THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE PrintRecords@1100528400() : Boolean;
    VAR
      ReportSelections@1100528200 : Record 77;
      TempReportSelections@1100528401 : TEMPORARY Record 77;
      PurchaseInspectionLine@1100528400 : Record 11229328;
      DocumentPrint@1100528201 : Codeunit 229;
    BEGIN
      PurchaseInspectionLine.COPY(Rec);
      IF PurchaseInspectionLine.ISEMPTY THEN
        EXIT(FALSE);

      ReportSelections.SETRANGE(Usage, 6006);
      IF ReportSelections.ISEMPTY THEN
        EXIT(FALSE);

      DocumentPrint.GetReportSelectionForPrintUsageVendor(
        6006,'',TRUE,TempReportSelections);
      IF TempReportSelections.FINDSET THEN BEGIN
        REPEAT
          REPORT.RUNMODAL(TempReportSelections."Report ID", TRUE, TRUE, PurchaseInspectionLine);
        UNTIL TempReportSelections.NEXT = 0;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE CopyInspectionLines@1100529400(VAR PurchInspectionLine@1100529401 : Record 11229328);
    VAR
      PurchaseRequisitionLine@1100529400 : Record 11229331;
      PurchInspectionLineLoc@1100529402 : Record 11229328;
      PurchInspectionLineNew@1100529403 : Record 11229328;
      LineNo@1100529404 : Integer;
    BEGIN
      //**4PS.sn BI006 KD 07-11-16
      IF PurchInspectionLine.ISEMPTY THEN
        EXIT;

      PurchInspectionLine.FINDFIRST;
      IF (PurchInspectionLine."Document Type" <> PurchInspectionLine."Document Type"::"Purchase Requisition") OR
         (PurchInspectionLine."Document No." = '') OR (PurchInspectionLine."Document Line No." = 0)
      THEN
        EXIT;

      IF NOT CONFIRM(Text001,TRUE,PurchInspectionLine."Document No.") THEN
        EXIT;

      PurchaseRequisitionLine.SETRANGE("Purchase Requisition No.",PurchInspectionLine."Document No.");
      PurchaseRequisitionLine.SETFILTER("Line No.",'<>%1',PurchInspectionLine."Document Line No.");
      IF PurchaseRequisitionLine.ISEMPTY THEN
        EXIT;

      PurchInspectionLineLoc.SETRANGE("Document Type",PurchInspectionLineLoc."Document Type"::"Purchase Requisition");
      PurchInspectionLineLoc.SETRANGE("Document No.",PurchInspectionLine."Document No.");

      PurchaseRequisitionLine.FINDSET;
      REPEAT
        LineNo := 0;
        PurchInspectionLineLoc.SETRANGE("Document Line No.",PurchaseRequisitionLine."Line No.");
        IF PurchInspectionLineLoc.FINDLAST THEN
          LineNo := PurchInspectionLineLoc."Line No.";

        PurchInspectionLine.FINDSET;
        REPEAT
          LineNo += 10000;
          PurchInspectionLineNew := PurchInspectionLine;
          PurchInspectionLineNew."Document Line No." := PurchaseRequisitionLine."Line No.";
          PurchInspectionLineNew."Line No." := LineNo;
          PurchInspectionLineNew.INSERT(TRUE);
        UNTIL PurchInspectionLine.NEXT = 0;
      UNTIL PurchaseRequisitionLine.NEXT = 0;
      //**4PS.en BI006 KD 07-11-16
    END;

    BEGIN
    {
      4PS 07-11-16 UKR-C21173 KD: New project inventory screen (BI006) - Added options Quote, Purchase Requisition to Document Type; changed relations to tables; added trigger CopyInspectionLines
    }
    END.
  }
}

