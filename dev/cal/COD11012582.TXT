OBJECT Codeunit 11012582 Plant - Element Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PlantLocRec@1100525000 : Record 11012554;
      Text001@1100525002 : TextConst 'DEU=Werkzeuglagerort nicht gefÅllt. Element nicht gestattet.;ENU=Plant Location not filled, Element not allowed.;NLD=Materieellocatie niet gevuld, element niet toegestaan.;NOR=Maskin lokasjon ikke fylt ut. Elementet tillates ikke.;SVE=Maskinutplaceringen har inte fyllts i. Elementet tillÜts inte.';
      Text002@1100525001 : TextConst 'DEU=Werkzeuglagerort ''%1'' nicht vorhanden. Element nicht gestattet.;ENU=Plant Location ''%1'' not present, Element not allowed.;NLD=Materieellocatie ''%1'' niet aanwezig, element niet toegestaan.;NOR=Maskin lokasjon ''%1'' mangler, Elementet tillates ikke.;SVE=Maskinutplaceringen ''%1'' finns inte. Elementet tillÜts inte.';
      Text003@1100525004 : TextConst 'DEU=Werkzeuglagerort ''%1'' ist ein Depot. Element nicht gestattet.;ENU=Plant Location ''%1'' is a depot, Element not allowed.;NLD=Materieellocatie ''%1'' is een depot, element niet toegestaan.;NOR=Maskin lokasjon ''%1'' er en depot. Elementet tillates ikke.;SVE=Maskinutplaceringen ''%1'' Ñr en depÜ. Elementet tillÜts inte.';
      Text004@1100525003 : TextConst 'DEU=Mit Werkzeuglagerort ''%1'' ist kein Projekt verbunden. Element nicht gestattet.;ENU=There is no Project linked to Plant Location ''%1'', Element not allowed.;NLD=Aan materieellocatie ''%1'' is geen project gekoppeld, element niet toegestaan.;NOR=Inget prosjekt har koblet til maskin lokasjon ''%1'', elementet er ikke tillatt.;SVE=Inget projekt har lÑnkats till Maskinsplatsen ''%1'', elementet Ñr inte tillÜtet.';

    PROCEDURE OnValidateElement@1100525003(IPlantLoc@1100525000 : Code[20];VAR IElement@1100525001 : Code[20];IPlantCompany@1100525002 : Text[30];TestElementNotBlocked@1100528000 : Boolean);
    BEGIN
      IF IElement = '' THEN
        EXIT;
      CheckElementAllowedForPlantLoc(IPlantLoc, IPlantCompany, PlantLocRec, TRUE);
      FormatElement(AdjustProjectCompany(IPlantCompany, PlantLocRec."Company Name"), IElement);
      CheckProjectElement(AdjustProjectCompany(IPlantCompany, PlantLocRec."Company Name"), PlantLocRec."Project No.", IElement, TestElementNotBlocked);
    END;

    PROCEDURE OnLookupElement@1100525005(IPlantLoc@1100525002 : Code[20];IElement@1100525001 : Code[20];IPlantCompany@1100525000 : Text[30];IEditableMode@1100525003 : Boolean) : Code[20];
    BEGIN
      IF IPlantLoc = '' THEN
        EXIT('');
      IF NOT CheckElementAllowedForPlantLoc(IPlantLoc, IPlantCompany, PlantLocRec, FALSE) THEN
        EXIT('');
      EXIT(LookupProjectElement(
        AdjustProjectCompany(IPlantCompany, PlantLocRec."Company Name"), PlantLocRec."Project No.", IElement, IEditableMode));
    END;

    PROCEDURE CheckElementAllowedForPlantLoc@1100525000(IPlantLoc@1100525000 : Code[20];IPlantCompany@1100525001 : Text[30];VAR OPlantLocRec@1100525006 : Record 11012554;IErrorMode@1100525002 : Boolean) : Boolean;
    BEGIN
      CLEAR(OPlantLocRec);
      IF IPlantLoc = '' THEN BEGIN
        IF IErrorMode THEN
          ERROR(Text001);
        EXIT(FALSE);
      END;
      IF IPlantCompany <> '' THEN
        OPlantLocRec.CHANGECOMPANY(IPlantCompany);
      IF NOT OPlantLocRec.GET(IPlantLoc) THEN BEGIN
        IF IErrorMode THEN
          ERROR(Text002, IPlantLoc);
        EXIT(FALSE);
      END;
      IF OPlantLocRec.Depot THEN BEGIN
        IF IErrorMode THEN
          ERROR(Text003, IPlantLoc);
        EXIT(FALSE);
      END;
      IF OPlantLocRec."Project No." = '' THEN BEGIN
        IF IErrorMode THEN
          ERROR(Text004, IPlantLoc);
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE AdjustProjectCompany@1100525008(IPlantCompany@1100525000 : Text[30];IProjectCompany@1100525001 : Text[30]) : Text[30];
    BEGIN
      IF (IProjectCompany = '') AND (IPlantCompany <> '') THEN
        EXIT(IPlantCompany);

      EXIT(IProjectCompany);
    END;

    PROCEDURE FormatElement@1100525007(IProjectCompany@1100525002 : Text[30];VAR IOElement@1100525000 : Code[20]);
    VAR
      lvElementFormatCU@1100525001 : Codeunit 11012022;
    BEGIN
      IF IOElement = '' THEN
        EXIT;
      lvElementFormatCU.FormatElem(IOElement, 2, IProjectCompany);
    END;

    PROCEDURE CheckProjectElement@1100525002(IProjectCompany@1100525001 : Text[30];IProjectNo@1100525002 : Code[20];IElement@1100525000 : Code[20];TestElementNotBlocked@1100528000 : Boolean);
    VAR
      lvProjElementRec@1100525003 : Record 11012010;
    BEGIN
      IF IElement = '' THEN
        EXIT;
      IF IProjectCompany <> '' THEN
        lvProjElementRec.CHANGECOMPANY(IProjectCompany);
      lvProjElementRec.GET(IProjectNo, IElement);
      IF TestElementNotBlocked THEN
        lvProjElementRec.TESTFIELD(Blocked, FALSE);
    END;

    PROCEDURE LookupProjectElement@1100525004(IProjectCompany@1100525003 : Text[30];IProjectNo@1100525002 : Code[20];IElement@1100525001 : Code[20];IEditableMode@1100525004 : Boolean) : Code[20];
    VAR
      lvProjElementRec@1100525000 : Record 11012010;
    BEGIN
      IF IProjectNo = '' THEN
        EXIT;
      IF IProjectCompany <> '' THEN
        lvProjElementRec.CHANGECOMPANY(IProjectCompany);
      lvProjElementRec.FILTERGROUP(9);
      lvProjElementRec.SETRANGE("Project No.", IProjectNo);
      lvProjElementRec.FILTERGROUP(0);
      lvProjElementRec."Project No." := IProjectNo;
      lvProjElementRec.Element := IElement;
      IF NOT IEditableMode THEN
        PAGE.RUNMODAL(0, lvProjElementRec)
      ELSE BEGIN
        IF PAGE.RUNMODAL(0, lvProjElementRec) = ACTION::LookupOK THEN
          EXIT(lvProjElementRec.Element);
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

