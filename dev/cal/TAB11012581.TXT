OBJECT Table 11012581 Plant Question List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=VAR
               lvPlantProfileAnswerRec@1210190000 : Record 11012582;
               lvPlantProfileMng@1210190001 : Codeunit 11012577;
             BEGIN
               IF (Type = Type::Answer) AND (Description <> xRec.Description) THEN BEGIN
                 lvPlantProfileAnswerRec.SETCURRENTKEY("Profile Code", "Line No.");
                 lvPlantProfileAnswerRec.SETRANGE("Profile Code", "Profile Code");
                 lvPlantProfileAnswerRec.SETRANGE("Line No.", "Line No.");
                 IF lvPlantProfileAnswerRec.FIND('-') THEN BEGIN
                   IF NOT lvPlantProfileMng.CheckOpenQuestion(
                     lvPlantProfileAnswerRec."Profile Code", lvPlantProfileAnswerRec."Line No.")
                   THEN BEGIN
                     REPEAT
                       lvPlantProfileAnswerRec.Answer := Description;
                       lvPlantProfileAnswerRec.MODIFY(TRUE); //C037249 - parameter added
                     UNTIL lvPlantProfileAnswerRec.NEXT = 0;
                   END;
                 END;
               END;
             END;

    OnDelete=VAR
               lvPlantProfileAnswerRec@1210190000 : Record 11012582;
               PlantQuestionList@1100525001 : Record 11012581;
               lvPlantProfileMng@1210190001 : Codeunit 11012577;
             BEGIN
               lvPlantProfileAnswerRec.SETCURRENTKEY("Profile Code", "Line No.");
               lvPlantProfileAnswerRec.SETRANGE("Profile Code", "Profile Code");
               lvPlantProfileAnswerRec.SETFILTER("Line No.", lvPlantProfileMng.GetLineFilterUsedAnswers(Rec));
               IF lvPlantProfileAnswerRec.FIND('-') THEN
                 ERROR(Text000, TABLECAPTION, FIELDCAPTION("Profile Code"), "Profile Code", FIELDCAPTION("Line No."), "Line No.");

               IF (Type = Type::Question) THEN BEGIN
                 PlantQuestionList.SETRANGE("Profile Code", "Profile Code");
                 lvPlantProfileAnswerRec.COPYFILTER("Line No.", PlantQuestionList."Line No.");
                 PlantQuestionList.DELETEALL;
               END;
             END;

    CaptionML=[ENU=Plant Question List;
               NOR=Maskinens SpõrsmÜlsliste;
               SVE=Maskinens frÜgelista];
  }
  FIELDS
  {
    { 1   ;   ;Profile Code        ;Code10        ;TableRelation="Plant Question Profile";
                                                   CaptionML=[ENU=Profile Code;
                                                              NOR=Profilkode;
                                                              SVE=Profilkod];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 3   ;   ;Type                ;Option        ;InitValue=Answer;
                                                   OnValidate=BEGIN
                                                                IF Type = Type::Answer THEN BEGIN
                                                                 TESTFIELD("Multiple Answers",FALSE);
                                                                 TESTFIELD("Open Question",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Question,Answer;
                                                                    NOR=SpõrsmÜl,Svar;
                                                                    SVE=FrÜga,Svar];
                                                   OptionString=Question,Answer }
    { 4   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 5   ;   ;Multiple Answers    ;Boolean       ;OnValidate=BEGIN
                                                                IF "Multiple Answers" THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Multiple Answers;
                                                              NOR=Flere Svar;
                                                              SVE=Flera svar] }
    { 6   ;   ;Open Question       ;Boolean       ;OnValidate=VAR
                                                                lvPlantProfileAnswerRec@1210190001 : Record 11012582;
                                                                lvPlantQuestionListRec@1210190002 : Record 11012581;
                                                                lvPlantProfileMng@1210190000 : Codeunit 11012577;
                                                              BEGIN
                                                                IF "Open Question" THEN
                                                                  TESTFIELD(Type,Type::Question);

                                                                IF (Type = Type::Question) AND (NOT "Open Question") AND ("Open Question" <> xRec."Open Question") THEN BEGIN
                                                                  lvPlantProfileAnswerRec.SETCURRENTKEY("Profile Code", "Line No.");
                                                                  lvPlantProfileAnswerRec.SETRANGE("Profile Code", "Profile Code");
                                                                  lvPlantProfileAnswerRec.SETFILTER("Line No.", lvPlantProfileMng.GetLineFilterUsedAnswers(Rec));
                                                                  IF lvPlantProfileAnswerRec.FIND('-') THEN BEGIN
                                                                    REPEAT
                                                                      IF lvPlantQuestionListRec.GET(lvPlantProfileAnswerRec."Profile Code", lvPlantProfileAnswerRec."Line No.") THEN BEGIN
                                                                        IF lvPlantProfileAnswerRec.Answer <> lvPlantQuestionListRec.Description THEN
                                                                          ERROR(Text001);
                                                                      END;
                                                                    UNTIL lvPlantProfileAnswerRec.NEXT = 0;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Open Question;
                                                              NOR=èpne SpõrsmÜl;
                                                              SVE=ôppna frÜga] }
    { 15  ;   ;Profile Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Question Profile".Description WHERE (Code=FIELD(Profile Code)));
                                                   CaptionML=[ENU=Profile Description;
                                                              NOR=Profilbeskrivelse;
                                                              SVE=Profile Description];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Profile Code,Line No.                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=You can not delete %1 %2 ''%3'', %4 ''%5'' because it is still present at the plant.;NOR=Du kan ikke fjerne%1 %2 ''%3'', %4 ''%5'' siden det fortsatt finnes pÜ maskinen.;SVE=Du kan inte ta bort %1 %2 ''%3'', %4 ''%5'' eftersom det fortfarande finns pÜ Maskinen.';
      Text001@1210190001 : TextConst 'ENU=Not allowed, there are already answers of this question present at the plant.;NOR=Ikke tillatt, det finnes allerede svar pÜ dette spõrsmÜlet pÜ maskinen.;SVE=Inte tillÜtet, det finns redan svar pÜ denna frÜga pÜ Maskinen.';

    BEGIN
    END.
  }
}

