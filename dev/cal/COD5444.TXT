OBJECT Codeunit 5444 Graph Business Setting
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      BusinessSettingReadWriteRoleTxt@1040 : TextConst '@@@={Locked};ENU=BusinessProfiles-Internal.ReadWrite;NOR=BusinessProfiles-Internal.ReadWrite';
      RegisterConnectionsTxt@1000 : TextConst '@@@={Locked};ENU=Registering connections for Business Setting.;NOR=Registering connections for Business Setting.';
      NoGraphAccessTxt@1001 : TextConst '@@@={Locked}  %1 - The error message.;ENU=Error accessing the Graph Business Setting table. %1;NOR=Error accessing the Graph Business Setting table. %1';

    [Internal]
    PROCEDURE GetMSPayBusinessSetting@15() MSPayData : Text;
    VAR
      GraphBusinessSetting@1001 : Record 5460;
      GraphSubscriptionManagement@1000 : Codeunit 5450;
      ConnectionId@1003 : Text;
    BEGIN
      SENDTRACETAG(
        '00001WD',GraphSubscriptionManagement.TraceCategory,VERBOSITY::Normal,
        RegisterConnectionsTxt,DATACLASSIFICATION::SystemMetadata);

      ConnectionId := FORMAT(CREATEGUID);
      IF NOT HASTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,ConnectionId) THEN
        REGISTERTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,ConnectionId,
          GetConnectionString);

      SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,ConnectionId,TRUE);

      IF GraphBusinessSetting.ISEMPTY THEN
        SENDTRACETAG(
          '00001WE',GraphSubscriptionManagement.TraceCategory,VERBOSITY::Error,
          STRSUBSTNO(NoGraphAccessTxt,GETLASTERRORTEXT),DATACLASSIFICATION::CustomerContent);

      MSPayData := GetMSPayData(GraphBusinessSetting);
      UNREGISTERTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,ConnectionId);
    END;

    LOCAL PROCEDURE EntityEndpoint@36() : Text[250];
    BEGIN
      EXIT('https://outlook.office365.com/SmallBusiness/api/v1/users(''{SHAREDCONTACTS}'')/BusinessSettings');
    END;

    LOCAL PROCEDURE EntityListEndpoint@34() : Text[250];
    BEGIN
      EXIT('https://outlook.office365.com/SmallBusiness/api/v1/users(''{SHAREDCONTACTS}'')/BusinessSettings');
    END;

    LOCAL PROCEDURE ResourceUri@2() : Text;
    BEGIN
      EXIT('https://outlook.office365.com');
    END;

    LOCAL PROCEDURE GetConnectionString@33() ConnectionString : Text;
    VAR
      GraphConnectionSetup@1000 : Codeunit 5456;
    BEGIN
      ConnectionString := GraphConnectionSetup.ConstructConnectionString(EntityEndpoint,EntityListEndpoint,
          ResourceUri,BusinessSettingReadWriteRoleTxt);
    END;

    LOCAL PROCEDURE GetMSPayData@1(VAR GraphBusinessSetting@1000 : Record 5460) MSPayData : Text;
    BEGIN
      IF GraphBusinessSetting.FINDSET THEN
        REPEAT
          IF (GraphBusinessSetting.Name = 'BusinessCenter_System_MicrosoftPaySettings') AND
             (GraphBusinessSetting.Scope = 'System')
          THEN BEGIN
            MSPayData := GraphBusinessSetting.GetDataString;
            EXIT;
          END
        UNTIL GraphBusinessSetting.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

