OBJECT Codeunit 5626 FA General Report
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5600=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Posting Date Filter;NOR=Bokf›ringsdatofilter;SVE=Datumfilter bokf”ring';
      Text001@1001 : TextConst 'ENU=You must specify the Starting Date and the Ending Date.;NOR=Du m† angi start- og sluttdato.;SVE=Du m†ste specificera startdatum och slutdatum.';
      Text002@1002 : TextConst 'ENU=The Starting Date is later than the Ending Date.;NOR=Startdatoen er senere enn sluttdatoen.;SVE=Startdatum „r senare „n slutdatum.';
      Text003@1003 : TextConst 'ENU=You must not specify closing dates.;NOR=Du kan ikke angi avslutningsdato.;SVE=Du ska inte specificera avslutsdatum.';
      Text004@1004 : TextConst 'ENU=You must specify the First Depreciation Date and the Last Depreciation Date.;NOR=Du m† angi f›rste og siste avskrivningsdato.;SVE=Du m†ste specificera f”rsta avskrivningsdatumet och sista avskrivningsdatumet.';
      Text005@1005 : TextConst 'ENU=The First Depreciation Date is later than the Last Depreciation Date.;NOR=F›rste avskrivningsdato er etter siste avskrivningsdato.;SVE=Det f”rsta avskrivningsdatumet „r senare „n det sista avskrivningsdatumet.';
      Text006@1006 : TextConst 'ENU=Sorting fixed assets;NOR=Sorterer aktiva;SVE=Sorterar anl.tillg†ngar';
      FADeprBook@1007 : Record 5612;
      FALedgEntry@1008 : Record 5601;
      DepreciationCalc@1009 : Codeunit 5616;

    [External]
    PROCEDURE GetLastDate@2(FANo@1000 : Code[20];PostingType@1001 : Integer;EndingDate@1002 : Date;DeprBookCode@1003 : Code[10];GLEntry@1004 : Boolean) : Date;
    VAR
      FirstLast@1005 : Text[1];
    BEGIN
      CLEARALL;
      IF PostingType = 0 THEN
        EXIT(0D);
      IF EndingDate = 0D THEN
        EndingDate := DMY2DATE(31,12,9999);
      WITH FALedgEntry DO BEGIN
        RESET;
        IF GLEntry THEN BEGIN
          SETCURRENTKEY(
            "FA No.","Depreciation Book Code","FA Posting Category","FA Posting Type","Posting Date");
          SETRANGE("Depreciation Book Code",DeprBookCode);
          SETRANGE("FA No.",FANo);
          SETRANGE("FA Posting Category","FA Posting Category"::" ");
          SETRANGE("Posting Date",0D,EndingDate);
        END ELSE BEGIN
          DepreciationCalc.SetFAFilter(FALedgEntry,FANo,DeprBookCode,TRUE);
          SETRANGE("FA Posting Date",0D,EndingDate);
        END;
        FirstLast := '+';
        CASE PostingType OF
          FADeprBook.FIELDNO("Last Acquisition Cost Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          FADeprBook.FIELDNO("Last Depreciation Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::Depreciation);
          FADeprBook.FIELDNO("Last Write-Down Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Write-Down");
          FADeprBook.FIELDNO("Last Appreciation Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::Appreciation);
          FADeprBook.FIELDNO("Last Custom 1 Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 1");
          FADeprBook.FIELDNO("Last Custom 2 Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 2");
          FADeprBook.FIELDNO("Last Salvage Value Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Salvage Value");
          FADeprBook.FIELDNO("Acquisition Date"),
          FADeprBook.FIELDNO("G/L Acquisition Date"):
            BEGIN
              SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
              FirstLast := '-';
            END;
          FADeprBook.FIELDNO("Disposal Date"):
            BEGIN
              SETRANGE("FA Posting Type","FA Posting Type"::"Proceeds on Disposal");
              FirstLast := '-';
            END;
        END;

        IF FIND(FirstLast) THEN BEGIN
          IF GLEntry THEN
            EXIT("Posting Date");

          EXIT("FA Posting Date");
        END;
        EXIT(0D);
      END;
    END;

    [External]
    PROCEDURE CalcFAPostedAmount@1(FANo@1000 : Code[20];PostingType@1001 : Integer;Period@1002 : 'Before Starting Date,Net Change,at Ending Date';StartingDate@1003 : Date;EndingDate@1004 : Date;DeprBookCode@1005 : Code[10];BeforeAmount@1006 : Decimal;UntilAmount@1007 : Decimal;OnlyReclassified@1008 : Boolean;OnlyBookValue@1009 : Boolean) : Decimal;
    BEGIN
      CLEARALL;
      IF PostingType = 0 THEN
        EXIT(0);
      IF EndingDate = 0D THEN
        EndingDate := DMY2DATE(31,12,9999);
      WITH FALedgEntry DO BEGIN
        CASE PostingType OF
          FADeprBook.FIELDNO("Book Value"):
            SETCURRENTKEY("FA No.","Depreciation Book Code","Part of Book Value");
          FADeprBook.FIELDNO("Depreciable Basis"):
            SETCURRENTKEY("FA No.","Depreciation Book Code","Part of Depreciable Basis");
          ELSE BEGIN
            SETCURRENTKEY(
              "FA No.","Depreciation Book Code",
              "FA Posting Category","FA Posting Type","FA Posting Date");
            SETRANGE("FA Posting Category","FA Posting Category"::" ");
          END;
        END;
        SETRANGE("FA No.",FANo);
        SETRANGE("Depreciation Book Code",DeprBookCode);
        IF OnlyReclassified THEN
          SETRANGE("Reclassification Entry",TRUE);
        IF OnlyBookValue THEN
          SETRANGE("Part of Book Value",TRUE);
        CASE PostingType OF
          FADeprBook.FIELDNO("Acquisition Cost"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          FADeprBook.FIELDNO(Depreciation):
            SETRANGE("FA Posting Type","FA Posting Type"::Depreciation);
          FADeprBook.FIELDNO("Write-Down"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Write-Down");
          FADeprBook.FIELDNO(Appreciation):
            SETRANGE("FA Posting Type","FA Posting Type"::Appreciation);
          FADeprBook.FIELDNO("Custom 1"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 1");
          FADeprBook.FIELDNO("Custom 2"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 2");
          FADeprBook.FIELDNO("Proceeds on Disposal"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Proceeds on Disposal");
          FADeprBook.FIELDNO("Gain/Loss"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Gain/Loss");
          FADeprBook.FIELDNO("Salvage Value"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Salvage Value");
          FADeprBook.FIELDNO("Book Value"):
            SETRANGE("Part of Book Value",TRUE);
          FADeprBook.FIELDNO("Depreciable Basis"):
            SETRANGE("Part of Depreciable Basis",TRUE);
        END;
        OnBeforeCalcFAPostedAmount(FALedgEntry,PostingType);
        CASE Period OF
          Period::"Before Starting Date":
            SETRANGE("FA Posting Date",0D,StartingDate - 1);
          Period::"Net Change":
            SETRANGE("FA Posting Date",StartingDate,EndingDate);
          Period::"at Ending Date":
            SETRANGE("FA Posting Date",0D,EndingDate);
        END;
        CALCSUMS(Amount);

        IF (PostingType = FADeprBook.FIELDNO("Book Value")) OR
           (PostingType = FADeprBook.FIELDNO(Depreciation))
        THEN
          CASE Period OF
            Period::"Before Starting Date":
              Amount := Amount + BeforeAmount;
            Period::"Net Change":
              Amount := Amount - BeforeAmount + UntilAmount;
            Period::"at Ending Date":
              Amount := Amount + UntilAmount;
          END;
        EXIT(Amount);
      END;
    END;

    [External]
    PROCEDURE CalcGLPostedAmount@5(FANo@1000 : Code[20];PostingType@1001 : Integer;Period@1002 : ' ,Disposal,Bal. Disposal';StartingDate@1003 : Date;EndingDate@1004 : Date;DeprBookCode@1005 : Code[10]) : Decimal;
    BEGIN
      CLEARALL;
      IF PostingType = 0 THEN
        EXIT(0);
      IF EndingDate = 0D THEN
        EndingDate := DMY2DATE(31,12,9999);
      WITH FALedgEntry DO BEGIN
        SETCURRENTKEY(
          "FA No.","Depreciation Book Code",
          "FA Posting Category","FA Posting Type","Posting Date");
        SETRANGE("FA No.",FANo);
        SETRANGE("Depreciation Book Code",DeprBookCode);
        SETRANGE("FA Posting Category",Period);
        SETRANGE("Posting Date",StartingDate,EndingDate);
        CASE PostingType OF
          FADeprBook.FIELDNO("Acquisition Cost"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          FADeprBook.FIELDNO(Depreciation):
            SETRANGE("FA Posting Type","FA Posting Type"::Depreciation);
          FADeprBook.FIELDNO("Write-Down"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Write-Down");
          FADeprBook.FIELDNO(Appreciation):
            SETRANGE("FA Posting Type","FA Posting Type"::Appreciation);
          FADeprBook.FIELDNO("Custom 1"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 1");
          FADeprBook.FIELDNO("Custom 2"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 2");
          FADeprBook.FIELDNO("Proceeds on Disposal"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Proceeds on Disposal");
          FADeprBook.FIELDNO("Gain/Loss"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Gain/Loss");
          FADeprBook.FIELDNO("Book Value on Disposal"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Book Value on Disposal");
        END;
        CALCSUMS(Amount);
        EXIT(Amount);
      END;
    END;

    [External]
    PROCEDURE AppendFAPostingFilter@4(VAR FA@1000 : Record 5600;StartingDate@1001 : Date;EndingDate@1002 : Date);
    BEGIN
      IF (StartingDate = 0D) AND (EndingDate = 0D) THEN
        EXIT;
      IF StartingDate = 0D THEN
        FA.SETFILTER("FA Posting Date Filter",'..%1',EndingDate)
      ELSE
        IF EndingDate = 0D THEN
          FA.SETFILTER("FA Posting Date Filter",'%1..',StartingDate)
        ELSE
          FA.SETFILTER("FA Posting Date Filter",'%1..%2',StartingDate,EndingDate);
    END;

    [External]
    PROCEDURE AppendPostingDateFilter@7(VAR FAFilter@1000 : Text;StartingDate@1001 : Date;EndingDate@1002 : Date);
    VAR
      PostingDateFilter@1003 : Text[50];
    BEGIN
      PostingDateFilter := STRSUBSTNO('%1: %2..%3',Text000,StartingDate,EndingDate);
      IF FAFilter = '' THEN
        FAFilter := PostingDateFilter
      ELSE
        FAFilter := FAFilter + STRSUBSTNO('%1 %2',',',PostingDateFilter);
    END;

    [External]
    PROCEDURE ValidateDates@3(StartingDate@1000 : Date;EndingDate@1001 : Date);
    BEGIN
      IF (EndingDate = 0D) OR (StartingDate <= 00000101D) THEN
        ERROR(Text001);

      IF StartingDate > EndingDate THEN
        ERROR(Text002);

      IF (NORMALDATE(StartingDate) <> StartingDate) OR (NORMALDATE(EndingDate) <> EndingDate) THEN
        ERROR(Text003);
    END;

    [External]
    PROCEDURE ValidateDeprDates@6(StartingDate@1000 : Date;EndingDate@1001 : Date);
    BEGIN
      IF (EndingDate = 0D) OR (StartingDate <= 00000101D) THEN
        ERROR(Text004);

      IF StartingDate > EndingDate THEN
        ERROR(Text005);

      IF (NORMALDATE(StartingDate) <> StartingDate) OR (NORMALDATE(EndingDate) <> EndingDate) THEN
        ERROR(Text003);
    END;

    [External]
    PROCEDURE SetFAPostingGroup@8(VAR FA2@1000 : Record 5600;DeprBookCode@1001 : Code[10]);
    VAR
      FA@1002 : Record 5600;
      FADeprBook@1003 : Record 5612;
      Window@1004 : Dialog;
    BEGIN
      Window.OPEN(Text006);
      FA.LOCKTABLE;
      FA.COPY(FA2);
      FA.SETRANGE("FA Posting Group");
      IF FA.FIND('-') THEN
        REPEAT
          IF FADeprBook.GET(FA."No.",DeprBookCode) THEN
            IF FA."FA Posting Group" <> FADeprBook."FA Posting Group" THEN BEGIN
              FA."FA Posting Group" := FADeprBook."FA Posting Group";
              FA.MODIFY;
            END;
        UNTIL FA.NEXT = 0;
      COMMIT;
      Window.CLOSE;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCalcFAPostedAmount@9(VAR FALedgerEntry@1001 : Record 5601;PostingType@1000 : Integer);
    BEGIN
    END;

    PROCEDURE CalculateNoOfUnits@1210190000(lRecFixedAsset@1210190000 : Record 5600;iDatStart@1210190001 : Date;iDatEnd@1210190002 : Date;VAR vDecNoOfUnits@1210190004 : ARRAY [4] OF Decimal);
    VAR
      lRecFA_LedgerEntry@1210190003 : Record 5601;
    BEGIN
      //**4PS.n
      // Function: CalculateNoOfUnits

      WITH lRecFA_LedgerEntry DO BEGIN
        CLEAR(vDecNoOfUnits);

        SETRANGE("FA No.", lRecFixedAsset."No.");
        SETRANGE("FA Posting Date", 0D, iDatStart);
        SETRANGE("FA Posting Type", "FA Posting Type"::"Acquisition Cost");
        SETRANGE("FA Posting Category", lRecFA_LedgerEntry."FA Posting Category"::Disposal);
        IF (COUNT <> 0) THEN
          EXIT;

        SETFILTER("FA Posting Category", '%1', "FA Posting Category"::" ");
        IF (lRecFA_LedgerEntry.FIND('-')) THEN
          REPEAT
            vDecNoOfUnits[1] += lRecFA_LedgerEntry.Quantity;
          UNTIL (lRecFA_LedgerEntry.NEXT = 0);

        // Acquisitions (+/-) in period
        vDecNoOfUnits[4] := vDecNoOfUnits[1];
        SETRANGE("FA Posting Date", iDatStart + 1, iDatEnd);
        IF (lRecFA_LedgerEntry.FIND('-')) THEN
          REPEAT
            IF (lRecFA_LedgerEntry.Quantity > 0.0) THEN
              vDecNoOfUnits[2] += lRecFA_LedgerEntry.Quantity
            ELSE
              vDecNoOfUnits[3] += ABS(lRecFA_LedgerEntry.Quantity);
            vDecNoOfUnits[4] += lRecFA_LedgerEntry.Quantity;
          UNTIL (lRecFA_LedgerEntry.NEXT = 0);

        // Disposed of within period?
        SETRANGE("FA Posting Category", lRecFA_LedgerEntry."FA Posting Category"::Disposal);
        IF (COUNT <> 0) THEN BEGIN
          vDecNoOfUnits[3] := vDecNoOfUnits[4];
          vDecNoOfUnits[4] := 0;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

