OBJECT Table 11012271 Exchange Logbook (FSA)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               DeleteErrors();
               DeleteImportLines();

               //* NB: Not allowed to delete complete processed export-files (on 4PS side).
               //*     These files should be imported and moved/deleted by BDA.
               IF (Direction = Direction::Import) OR
                  ((Direction = Direction::Export) AND (Status <> Status::Processed))
               THEN BEGIN
                 IF (Directory <> '') AND ("File Name" <> '') THEN BEGIN
                   IF FileMgt.ClientFileExists(Directory + "File Name") THEN
                     FileMgt.DeleteClientFile4PS(Directory + "File Name", TRUE);
                 END;
               END;
             END;

    CaptionML=[ENU=Exchange Logbook Field Service;
               NOR=Vekslingsfeil Loggbok Feltservice;
               SVE=V„xlingsfel loggb. f„ltserv.];
  }
  FIELDS
  {
    { 10  ;   ;Date/Time           ;DateTime      ;CaptionML=[ENU=Date/Time;
                                                              NOR=Dato/tid;
                                                              SVE=Datum/tid] }
    { 20  ;   ;Message Type        ;Option        ;CaptionML=[ENU=Message Type;
                                                              NOR=Meldingstype;
                                                              SVE=Meddelandetyp];
                                                   OptionCaptionML=[ENU=Reference Data,Employees,Service Orders,Items,Customers,Service Locations,,,,,,,,Status,Debrief,Text,Hours (Service),Indirect Hours;
                                                                    NOR=Referansedata,Ansatte,Serviceordre,Artikler,Kunder,Servicelokasjoner,,,,,,,,Status,Debriefing,Tekst,Timer (service),Indirekte timer;
                                                                    SVE=Referensdata,Anst„llda,Serviceorder,Artiklar,Kunder,Serviceplatser,,,,,,,,Status,Avrapportering,Text,Timmar (service),Indirekta timmar];
                                                   OptionString=RefData,Empl,ServOrder,Item,Cust,ServLoc,,,,,,,,Status,Debrief,Text,ServHours,IndirectHours;
                                                   Description=Options in tables 11012271, 11012272 and 11012273 should be equal }
    { 25  ;   ;Direction           ;Option        ;CaptionML=[ENU=Direction;
                                                              NOR=Retning;
                                                              SVE=Riktning];
                                                   OptionCaptionML=[ENU=Export,Import;
                                                                    NOR=Eksporter,Importer;
                                                                    SVE=Exportera,Importera];
                                                   OptionString=Export,Import }
    { 30  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=In Process,Error,Processed;
                                                                    NOR=Under behandling,feil,behandlet;
                                                                    SVE=I process,fel,behandlad];
                                                   OptionString=In Process,Error,Processed }
    { 40  ;   ;Directory           ;Text250       ;CaptionML=[ENU=Directory;
                                                              NOR=Katalog;
                                                              SVE=Katalog] }
    { 45  ;   ;File Name           ;Text50        ;CaptionML=[ENU=File Name;
                                                              NOR=Filnavn;
                                                              SVE=Filnamn] }
    { 50  ;   ;Quantity            ;Integer       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 60  ;   ;No. of Errors       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Exchange Error (FSA)" WHERE (Date/Time=FIELD(Date/Time),
                                                                                                   Message Type=FIELD(Message Type),
                                                                                                   Type=CONST(Error)));
                                                   CaptionML=[ENU=Errors;
                                                              NOR=Feil;
                                                              SVE=Fel];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 61  ;   ;No. of Notes        ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Exchange Error (FSA)" WHERE (Date/Time=FIELD(Date/Time),
                                                                                                   Message Type=FIELD(Message Type),
                                                                                                   Type=CONST(Note)));
                                                   CaptionML=[ENU=Notes;
                                                              NOR=Noteringer;
                                                              SVE=Noteringar];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 65  ;   ;No. of Calls        ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Exchange Error (FSA)" WHERE (Date/Time=FIELD(Date/Time),
                                                                                                   Message Type=FIELD(Message Type)));
                                                   CaptionML=[ENU=Calls;
                                                              NOR=Samtaler;
                                                              SVE=samtal];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 70  ;   ;No. not Processed   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Import Line (FSA)" WHERE (Date/Time=FIELD(Date/Time),
                                                                                                Message Type=FIELD(Message Type)));
                                                   CaptionML=[ENU=Not Processed;
                                                              NOR=Ikke behandlet;
                                                              SVE=Inte behandlad];
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Date/Time,Message Type                  ;Clustered=Yes }
    {    ;Message Type,Date/Time                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'ENU=File ''%1'' is not (completely) processed, delete not allowed.;NOR=Filen ''%1'' er ikke (fullstendig) behandlet, fjerning ikke tillatt.;SVE=Filen ''%1'' har inte (helt) behandlats, radering inte till†tet.';
      Text001@1100485001 : TextConst 'ENU=File ''%1'' is not (completely) processed, after deleting the logbook line this is not possible anymore.\ \Are you sure you want to delete this logbook line?;NOR=Filen ''%1'' er ikke (fullstendig) behandlet, etter fjerning av loggboksraden er dette ikke lenger mulig.\ \Er du sikker p† † du vil fjerne denne loggboksraden?;SVE=Filen ''%1'' har inte (helt) behandlats, efter radering av loggboksraden „r detta inte l„ngre m”jligt.\ \Žr du s„ker p† att du vill ta bort den h„r loggboksraden?';
      FileMgt@1100525500 : Codeunit 419;

    PROCEDURE CheckDeleteLogbookAllowed@1100485003(IMode@1100485000 : Integer) : Boolean;
    BEGIN
      //* Mode: 0=No error/question (further depening on calling process), 1=Error, 2=Question
      //* Delete a record (from logbook form) should be possible when manual (then question).

      IF (Status = Status::Processed) THEN
        EXIT(TRUE);

      IF (Direction = Direction::Export) THEN BEGIN
        IF Quantity = 0 THEN
          EXIT(TRUE);
        IF (Directory = '') OR ("File Name" = '') THEN
          EXIT(TRUE);
        IF NOT FileMgt.ClientFileExists(Directory + "File Name") THEN
          EXIT(TRUE);
      END;

      IF (Direction = Direction::Import) THEN BEGIN
        CALCFIELDS("No. not Processed");
        IF ("No. not Processed" <= 0) THEN BEGIN
          IF (Directory = '') OR ("File Name" = '') THEN
            EXIT(TRUE);
          IF NOT FileMgt.ClientFileExists(Directory + "File Name") THEN
            EXIT(TRUE);
        END;
      END;

      IF IMode = 1 THEN
        ERROR(Text000, "File Name");
      IF IMode = 2 THEN BEGIN
        IF CONFIRM(Text001, FALSE, "File Name") THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE WriteExchangeError@1100485000(IErrorText@1100485001 : Text[250];IType@1100485002 : Option;IImportLineNo@1100485003 : Integer) : Integer;
    VAR
      lvErrorRec@1100485000 : Record 11012272;
    BEGIN
      lvErrorRec.INIT;
      lvErrorRec."Date/Time" := "Date/Time";
      lvErrorRec."Message Type" := "Message Type";
      lvErrorRec."Line No." := 0;  //* Will be determined at Insert(True) on the Insert trigger of the error table
      lvErrorRec.Type := IType;
      lvErrorRec.Description := COPYSTR(IErrorText, 1, MAXSTRLEN(lvErrorRec.Description));
      lvErrorRec."Import Line No." := IImportLineNo;
      lvErrorRec.INSERT(TRUE);

      EXIT(lvErrorRec."Line No.");
    END;

    PROCEDURE DeleteErrors@1100485001();
    VAR
      lvErrorRec@1100485000 : Record 11012272;
    BEGIN
      //* Before calling this function, determine if deleting is allowed (if necessary), add no tests to this function
      lvErrorRec.SETRANGE("Date/Time", "Date/Time");
      lvErrorRec.SETRANGE("Message Type", "Message Type");
      lvErrorRec.DELETEALL;
    END;

    PROCEDURE DeleteImportLines@1100485002();
    VAR
      lvImportLineRec@1100485000 : Record 11012273;
    BEGIN
      //* Before calling this function, determine if deleting is allowed (if necessary), add no tests to this function
      lvImportLineRec.SETRANGE("Date/Time", "Date/Time");
      lvImportLineRec.SETRANGE("Message Type", "Message Type");
      lvImportLineRec.DELETEALL(FALSE);  //* FALSE!
    END;

    BEGIN
    END.
  }
}

