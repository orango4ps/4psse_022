OBJECT Report 6661 Delete Invd Purch. Ret. Orders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    AccessByPermission=TableData 38=RD;
    CaptionML=[DEU=Erledigte Einkaufsreklamationen l”schen;
               ENU=Delete Invoiced Purchase Return Orders;
               NLD=Gefactureerde inkoopretourorders verwijderen;
               NOR=Slett fakturerte bestillingsreturer];
    ApplicationArea=#PurchReturnOrder;
    ProcessingOnly=Yes;
    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Return Order));
               ReqFilterHeadingML=[DEU=Einkaufsreklamation;
                                   ENU=Purchase Return Order;
                                   NLD=Inkoopretourorder;
                                   NOR=Bestillingsretur;
                                   SVE=Ink”psreturorder];
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                  ArchiveManagement@1002 : Codeunit 5063;
                                  PostPurchDelete@1001 : Codeunit 364;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  AllLinesDeleted := TRUE;
                                  ItemChargeAssgntPurch.RESET;
                                  ItemChargeAssgntPurch.SETRANGE("Document Type","Document Type");
                                  ItemChargeAssgntPurch.SETRANGE("Document No.","No.");
                                  PurchLine.RESET;
                                  PurchLine.SETRANGE("Document Type","Document Type");
                                  PurchLine.SETRANGE("Document No.","No.");
                                  PurchLine.SETFILTER("Quantity Invoiced",'<>0');
                                  IF PurchLine.FIND('-') THEN BEGIN
                                    PurchLine.SETRANGE("Quantity Invoiced");
                                    PurchLine.SETFILTER("Outstanding Quantity",'<>0');
                                    IF NOT PurchLine.FIND('-') THEN BEGIN
                                      PurchLine.SETRANGE("Outstanding Quantity");
                                      PurchLine.SETFILTER("Return Qty. Shipped Not Invd.",'<>0');
                                      IF NOT PurchLine.FIND('-') THEN BEGIN
                                        PurchLine.LOCKTABLE;
                                        IF NOT PurchLine.FIND('-') THEN BEGIN
                                          PurchLine.SETRANGE("Return Qty. Shipped Not Invd.");
                                          ArchiveManagement.AutoArchivePurchDocument("Purchase Header");
                                          IF PurchLine.FIND('-') THEN
                                            REPEAT
                                              PurchLine.CALCFIELDS("Qty. Assigned");
                                              IF ((PurchLine."Qty. Assigned" = PurchLine."Quantity Invoiced") AND
                                                  (PurchLine."Qty. Assigned" <> 0)) OR
                                                 (PurchLine.Type <> PurchLine.Type::"Charge (Item)")
                                              THEN BEGIN
                                                IF PurchLine.Type = PurchLine.Type::"Charge (Item)" THEN BEGIN
                                                  ItemChargeAssgntPurch.SETRANGE("Document Line No.",PurchLine."Line No.");
                                                  ItemChargeAssgntPurch.DELETEALL;
                                                END;
                                                IF PurchLine.HASLINKS THEN
                                                  PurchLine.DELETELINKS;
                                                OnBeforePurchLineDelete(PurchLine);
                                                PurchLine.DELETE;
                                              END ELSE
                                                AllLinesDeleted := FALSE;
                                            UNTIL PurchLine.NEXT = 0;

                                          IF AllLinesDeleted THEN BEGIN
                                            PostPurchDelete.DeleteHeader(
                                              "Purchase Header",PurchRcptHeader,PurchInvHeader,PurchCrMemoHeader,
                                              ReturnShptHeader,PrepmtPurchInvHeader,PrepmtPurchCrMemoHeader);
                                            ReservePurchLine.DeleteInvoiceSpecFromHeader("Purchase Header");

                                            PurchCommentLine.SETRANGE("Document Type","Document Type");
                                            PurchCommentLine.SETRANGE("No.","No.");
                                            PurchCommentLine.DELETEALL;

                                            WhseRequest.SETRANGE("Source Type",DATABASE::"Purchase Line");
                                            WhseRequest.SETRANGE("Source Subtype","Document Type");
                                            WhseRequest.SETRANGE("Source No.","No.");
                                            IF NOT WhseRequest.ISEMPTY THEN
                                              WhseRequest.DELETEALL(TRUE);

                                            ApprovalsMgmt.DeleteApprovalEntries(RECORDID);

                                            OnBeforeDeletePurchaseHeader("Purchase Header");
                                            DELETE;
                                          END;
                                          COMMIT;
                                        END;
                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Buy-from Vendor No.,Pay-to Vendor No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Bearbeite Bestellungen #1##########;ENU=Processing purch. orders #1##########;NLD=Verwerken ink.-orders   #1##########;NOR=Behandler bestillinger   #1##########;SVE=Behandlar ink”psorder    #1##########';
      PurchLine@1001 : Record 39;
      PurchRcptHeader@1011 : Record 120;
      PurchInvHeader@1010 : Record 122;
      PurchCrMemoHeader@1009 : Record 124;
      ReturnShptHeader@1008 : Record 6650;
      PrepmtPurchInvHeader@1015 : Record 122;
      PrepmtPurchCrMemoHeader@1014 : Record 124;
      PurchCommentLine@1002 : Record 43;
      ItemChargeAssgntPurch@1005 : Record 5805;
      WhseRequest@1007 : Record 5765;
      ReservePurchLine@1013 : Codeunit 99000834;
      Window@1004 : Dialog;
      AllLinesDeleted@1006 : Boolean;

    [Integration]
    LOCAL PROCEDURE OnBeforePurchLineDelete@1001(VAR PurchLine@1000 : Record 39);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeletePurchaseHeader@1(VAR PurchaseHeader@1000 : Record 38);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

