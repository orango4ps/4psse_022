OBJECT Report 11012797 SUP Copy Cust. to Int. Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=SUP Debitor zu internem Budget kopieren;
               ENU=SUP Copy Customer to Internal Budget;
               NLD=SEP klant naar interne begroting kopi‰ren];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1100528600;;DataItem;                  ;
               DataItemTable=Table11071769;
               DataItemTableView=SORTING(Price Book Code,Index Date,Code);
               OnAfterGetRecord=BEGIN
                                  DeleteOldInternalBudgetLines("Price Book Code", "Index Date", Code);
                                END;

               ReqFilterFields=Price Book Code,Index Date,Code }

    { 1100528601;1;DataItem;                 ;
               DataItemTable=Table11071771;
               DataItemTableView=SORTING(Price Book Code,Index Date,Unit Price Code,Line No.);
               OnAfterGetRecord=BEGIN
                                  CopyCustomerToInternalBudgetLine("SUP Customer Budget Line");
                                END;

               DataItemLink=Price Book Code=FIELD(Price Book Code),
                            Index Date=FIELD(Index Date),
                            Unit Price Code=FIELD(Code) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {

    PROCEDURE DeleteOldInternalBudgetLines@1100528601(IPriceBookCode@1100528601 : Code[20];IIndexDate@1100528602 : Date;IUnitPriceCode@1100528603 : Code[20]);
    VAR
      SUPInternalBudgetLine@1100528600 : Record 11071770;
    BEGIN
      SUPInternalBudgetLine.SETRANGE("Price Book Code", IPriceBookCode);
      SUPInternalBudgetLine.SETRANGE("Index Date", IIndexDate);
      SUPInternalBudgetLine.SETRANGE("Unit Price Code", IUnitPriceCode);
      SUPInternalBudgetLine.DELETEALL(TRUE);
    END;

    PROCEDURE CopyCustomerToInternalBudgetLine@1100528600(ISUPCustomerBudgetLine@1100528600 : Record 11071771);
    VAR
      SUPInternalBudgetLine@1100528601 : Record 11071770;
      LineNo@1100528602 : Integer;
    BEGIN
      SUPInternalBudgetLine.SETRANGE("Price Book Code", ISUPCustomerBudgetLine."Price Book Code");
      SUPInternalBudgetLine.SETRANGE("Index Date", ISUPCustomerBudgetLine."Index Date");
      SUPInternalBudgetLine.SETRANGE("Unit Price Code", ISUPCustomerBudgetLine."Unit Price Code");
      IF SUPInternalBudgetLine.FINDLAST THEN
        LineNo := SUPInternalBudgetLine."Line No.";

      IF ISUPCustomerBudgetLine."Gross Price" <> 0 THEN BEGIN
        LineNo += 10000;
        SUPInternalBudgetLine.INIT;
        SUPInternalBudgetLine."Index Date" := ISUPCustomerBudgetLine."Index Date";
        SUPInternalBudgetLine.VALIDATE("Price Book Code", ISUPCustomerBudgetLine."Price Book Code");
        SUPInternalBudgetLine."Unit Price Code" := ISUPCustomerBudgetLine."Unit Price Code";
        SUPInternalBudgetLine."Line No." := LineNo;
        SUPInternalBudgetLine.INSERT(TRUE);
        SUPInternalBudgetLine."Cost Type" := SUPInternalBudgetLine."Cost Type"::Material;
        SUPInternalBudgetLine."Cost Object" := GetCostObject(ISUPCustomerBudgetLine);
        SUPInternalBudgetLine.Description := ISUPCustomerBudgetLine.Description;
        SUPInternalBudgetLine.Quantity := ISUPCustomerBudgetLine.Quantity;
        SUPInternalBudgetLine."Gross Price" := ISUPCustomerBudgetLine."Gross Price";
        SUPInternalBudgetLine.VALIDATE("Cost Price", ISUPCustomerBudgetLine."Gross Price");
        IF ISUPCustomerBudgetLine."VAT Code Material" <> '' THEN
          SUPInternalBudgetLine."VAT Prod. Posting Group" := ISUPCustomerBudgetLine."VAT Code Material"
        ELSE
          SUPInternalBudgetLine."VAT Prod. Posting Group" := GetVATCode(ISUPCustomerBudgetLine, ISUPCustomerBudgetLine."VAT Code Material Customer");
        SUPInternalBudgetLine."Unit of Measure" := GetUOM(ISUPCustomerBudgetLine);
        SUPInternalBudgetLine.MODIFY(TRUE);
      END;

      IF ISUPCustomerBudgetLine.Norm <> 0 THEN BEGIN
        LineNo += 10000;
        SUPInternalBudgetLine.INIT;
        SUPInternalBudgetLine."Index Date" := ISUPCustomerBudgetLine."Index Date";
        SUPInternalBudgetLine.VALIDATE("Price Book Code", ISUPCustomerBudgetLine."Price Book Code");
        SUPInternalBudgetLine."Unit Price Code" := ISUPCustomerBudgetLine."Unit Price Code";
        SUPInternalBudgetLine."Line No." := LineNo;
        SUPInternalBudgetLine.INSERT(TRUE);
        SUPInternalBudgetLine."Cost Type" := SUPInternalBudgetLine."Cost Type"::Labor;
        SUPInternalBudgetLine."Cost Object" := GetCostObject(ISUPCustomerBudgetLine);
        SUPInternalBudgetLine.Description := ISUPCustomerBudgetLine.Description;
        SUPInternalBudgetLine.Quantity := ISUPCustomerBudgetLine.Quantity;
        SUPInternalBudgetLine.Norm := ISUPCustomerBudgetLine.Norm;
        IF ISUPCustomerBudgetLine."VAT Code Men Hours" <> '' THEN
          SUPInternalBudgetLine."VAT Prod. Posting Group" := ISUPCustomerBudgetLine."VAT Code Men Hours"
        ELSE
          SUPInternalBudgetLine."VAT Prod. Posting Group" := GetVATCode(ISUPCustomerBudgetLine, ISUPCustomerBudgetLine."VAT Code Men Hours Customer");
        SUPInternalBudgetLine.VALIDATE("Rate Code", GetRateCode(ISUPCustomerBudgetLine));
        SUPInternalBudgetLine."Unit of Measure" := GetUOM(ISUPCustomerBudgetLine);
        SUPInternalBudgetLine.MODIFY(TRUE);
      END;
    END;

    PROCEDURE GetVATCode@1100528606(ISUPCustomerBudgetLine@1100528600 : Record 11071771;IVATCode@1100528602 : Code[10]) : Code[10];
    VAR
      SUPVATCustomerRelation@1100528601 : Record 11071992;
    BEGIN
      IF SUPVATCustomerRelation.GET(ISUPCustomerBudgetLine."Price Book Code", ISUPCustomerBudgetLine."Index Date", IVATCode) THEN
        EXIT(SUPVATCustomerRelation."VAT Prod. Posting Group");
    END;

    PROCEDURE GetRateCode@1100528605(ISUPCustomerBudgetLine@1100528600 : Record 11071771) : Code[10];
    VAR
      SUPRateCustomerRelation@1100528601 : Record 11071993;
    BEGIN
      SUPRateCustomerRelation.SETRANGE("Price Book Code", ISUPCustomerBudgetLine."Price Book Code");
      SUPRateCustomerRelation.SETRANGE("Index Date", ISUPCustomerBudgetLine."Index Date");
      SUPRateCustomerRelation.SETRANGE("Rate Customer", ISUPCustomerBudgetLine."Hour Rate");
      IF SUPRateCustomerRelation.FINDFIRST THEN
        EXIT(SUPRateCustomerRelation."Rate Code");
    END;

    PROCEDURE GetUOM@1100528604(ISUPCustomerBudgetLine@1100528600 : Record 11071771) : Code[10];
    VAR
      SUPUOMCustomerRelation@1100528601 : Record 11071994;
    BEGIN
      IF SUPUOMCustomerRelation.GET(ISUPCustomerBudgetLine."Price Book Code", ISUPCustomerBudgetLine."Index Date", ISUPCustomerBudgetLine."Unit of Measure") THEN
        EXIT(SUPUOMCustomerRelation."Unit of Measure");
    END;

    PROCEDURE GetCostObject@1100527000(ISUPCustomerBudgetLine@1100527000 : Record 11071771) : Code[20];
    VAR
      SUPCostObjectCustRelation@1100527001 : Record 11072323;
    BEGIN
      IF SUPCostObjectCustRelation.GET(ISUPCustomerBudgetLine."Price Book Code", ISUPCustomerBudgetLine."Index Date", ISUPCustomerBudgetLine."STABU Code") THEN
        EXIT(SUPCostObjectCustRelation."Cost Object");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

