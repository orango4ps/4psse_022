OBJECT Codeunit 6086314 Reminder E-Mail
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.08;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            Code;
          END;

  }
  CODE
  {
    VAR
      AmountCap@1005 : TextConst 'DEU=Betrag;ENU=Amount;NLD=Bedrag;NOR=Belõp;SVE=Belopp';
      AmountLCYCap@1006 : TextConst 'DEU=Betrag (MW);ENU=Amount (LCY);NLD=Bedrag (LV);NOR=Belõp (NOK);SVE=Belopp (BVA)';
      CodeTxt@1160040008 : TextConst 'DEU=Code;ENU=Code;NLD=Code;SVE=Kod';
      CurrencyCodeCap@1009 : TextConst 'DEU=Currency;ENU=Currency;NLD=Valuta;NOR=Valuta;SVE=Valuta';
      DescriptionCap@1004 : TextConst 'DEU=Beschreibung;ENU=Description;NLD=Omschrijving;NOR=Beskrivelse;SVE=Beskrivning';
      DocumentDateCap@1002 : TextConst 'DEU=Datum;ENU=Date;NLD=Datum;NOR=Dato;SVE=Datum';
      ExpenseTxt@1160040006 : TextConst 'DEU=Erstattung;ENU=Expense;NLD=Uitgave;SVE=UtlÑgg';
      ExpenseTypeCap@1003 : TextConst 'DEU=Art;ENU=Type;NLD=Type;NOR=Type;SVE=Typ';
      LinkCap@1007 : TextConst 'DEU=Link;ENU=Link;NLD=Link;NOR=Lenke;SVE=LÑnk';
      ListTitleCaption@1001 : TextConst 'DEU=Erinnerungen, die beachtet werden mÅssen (%1);ENU=Reminders that need attention (%1);NLD=Herinneringen die actie vereisen (%1)';
      MileageTxt@1160040007 : TextConst 'DEU=Kilometerleistung;ENU=Mileage;NLD=Kilometrage;SVE=UtlÑgg';
      NoRemSentTxt@1160040003 : TextConst 'DEU=Es wurde keine E-Mail gesendet.\\Es gab keine offene Erinnerung oder die ausgewÑhlte Kostenposition ist nicht offen fÅr den Benutzer der Kostenposition.;ENU=No E-mail was sent.\\There was no reminder pending or the selected Expense is not pending the expense user.;NLD=Geen e-mail verzonden.\\Er waren geen afwachtende herinneringen of de geselecteerde uitgave wacht niet op de gebruiker.';
      RemimdersSentTxt@1160040004 : TextConst 'DEU=%1 Erinnerungs-E-Mail gesendet.;ENU=%1 reminder e-mails sent.;NLD=%1 herinneringse-mails verzonden.';
      ReminderPortalTitle@1160040000 : TextConst 'DEU=Alle Erinnerungen auf Webportal einsehen;ENU=See all Reminders on Web Portal;NLD=Alle herinneringen inzien op het web';
      ReminderTextCap@1011 : TextConst 'DEU=Erinnerung in Webbrowser îffnen;ENU=Open Reminder in web browser;NLD=Open herinnering in browser';
      SendingReminderTxt@1160040002 : TextConst 'DEU=Erinnerung wird gesendet an: #1###############;ENU=Sending Reminder to: #1###############;NLD=Herinnering versturen naar: #1###############';
      SettlementTxt@1160040005 : TextConst 'DEU=Ausgleich;ENU=Settlement;NLD=Afrekening;SVE=AvrÑkning';
      ReminderTemplateHTML1@1160040010 : TextConst 'ENU="<!DOCTYPE html><html><body><style>h1 { font-weight: bold; font-family:''Segoe UI''; font-size:20px; }  h2 { font-weight: normal; font-family:''Segoe UI''; font-size:12px; }  table, th, td { border: 2px solid black; border-collapse: collapse; border-color: black; } th, td { padding: 4px; }"';
      ReminderTemplateHTML2@1160040011 : TextConst 'ENU="            table#t01 tr.even { background-color: #D9D9D9; font-family:''Segoe UI''; font-size:11px; } table#t01 tr.odd { background-color: white; font-family:''Segoe UI''; font-size:11px; } table#t01 th { color: white; background-color: #5B9BD5; font-weight: bold; font-family:''Segoe UI''; font-size:12px; }                         </style><h1>#LISTTITLE#</h1><table id=""t01"">#LISTLABELS# #LISTLINES#</table><a href=""#PORTALURL#""><h2>#PORTALTITLE#</h2></a></body></html>"';

    LOCAL PROCEDURE Code@1160040003();
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
    BEGIN
      SendNotificationEmails(ContiniaUserSetup,FALSE);
    END;

    PROCEDURE SendExpNotificationEmails@1160040006(VAR Expense@1160040000 : Record 6086320;Force@1160040001 : Boolean);
    VAR
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      Expense.SETRANGE(Status,Expense.Status::"Pending Expense User");
      IF Expense.FINDSET THEN BEGIN
        REPEAT
          IF ContiniaUserSetup.GET(Expense."Continia User ID") THEN
            ContiniaUserSetup.MARK(TRUE);
        UNTIL Expense.NEXT = 0;

        ContiniaUserSetup.MARKEDONLY(TRUE);
        SendNotificationEmails(ContiniaUserSetup,Force);
      END;
    END;

    PROCEDURE SendMilNotificationEmails@1160040007(VAR Mileage@1160040000 : Record 6086338;Force@1160040001 : Boolean);
    VAR
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      Mileage.SETRANGE(Status,Mileage.Status::"Pending Expense User");
      IF Mileage.FINDSET THEN BEGIN
        REPEAT
          IF ContiniaUserSetup.GET(Mileage."Continia User ID") THEN
            ContiniaUserSetup.MARK(TRUE);
        UNTIL Mileage.NEXT = 0;

        ContiniaUserSetup.MARKEDONLY(TRUE);
        SendNotificationEmails(ContiniaUserSetup,Force);
      END;
    END;

    PROCEDURE SendExpHeaderNotEmails@1160040000(VAR ExpHeader@1160040000 : Record 6086339;Force@1160040001 : Boolean);
    VAR
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      ExpHeader.SETRANGE(Status,ExpHeader.Status::"Pending Expense User");
      IF ExpHeader.FINDSET THEN BEGIN
        REPEAT
          IF ContiniaUserSetup.GET(ExpHeader."Continia User ID") THEN
            ContiniaUserSetup.MARK(TRUE);
        UNTIL ExpHeader.NEXT = 0;

        ContiniaUserSetup.MARKEDONLY(TRUE);
        SendNotificationEmails(ContiniaUserSetup,Force);
      END;
    END;

    LOCAL PROCEDURE SendNotificationEmails@1160040002(VAR ContiniaUserSetup@1009 : Record 6086002;Force@1160040006 : Boolean);
    VAR
      ContiniaUser@1160040008 : Record 6086001;
      EMSetup@1003 : Record 6086300;
      Expense@1160040002 : Record 6086320;
      Mileage@1160040013 : Record 6086338;
      ExpHeader@1160040000 : Record 6086339;
      EMReminder@1013 : Record 6086364;
      BigStringLines@1011 : Codeunit 6085587;
      BigStringTemplate@1010 : Codeunit 6085587;
      SmtpMailManagement@1001 : Codeunit 6085598;
      ExpenseApprovalEMail@1000 : Codeunit 6086313;
      Window@1160040007 : Dialog;
      NVInStream@1006 : InStream;
      ClassText@1160040011 : Text[30];
      FontColorEndTag@6086300 : Text[30];
      FontColorText@1160040010 : Text[30];
      RemindersUrl@1017 : Text[1024];
      LineNo@1160040003 : Integer;
      NoOfMails@1018 : Integer;
      SendEmail@1160040005 : Boolean;
    BEGIN
      NoOfMails := 0;

      // GET REMINDER TEMPLATE
      EMSetup.GET;
      EMSetup.TESTFIELD("Error E-Mail");
      EMSetup.TESTFIELD("Reminder E-Mail Subject");
      EMSetup.CALCFIELDS("Reminder Template");

      IF ContiniaUserSetup.FINDSET THEN BEGIN
        IF GUIALLOWED THEN
          Window.OPEN(SendingReminderTxt);

        REPEAT
          // MAIL SETUP
          RemindersUrl := GetReminderUrl(ContiniaUserSetup."Continia User ID");
          ContiniaUser.GET(ContiniaUserSetup."Continia User ID");
          ExpenseApprovalEMail.SetupMail(SmtpMailManagement,ContiniaUserSetup,
            STRSUBSTNO(EMSetup."Reminder E-Mail Subject",ContiniaUser.Name));

          SendEmail := Force;
          LineNo := 0;
          BigStringLines.Clear;

          //
          // BUILD LINES
          //
          ExpHeader.RESET;
          ExpHeader.SETCURRENTKEY("Continia User ID");
          ExpHeader.SETRANGE("Continia User ID",ContiniaUserSetup."Continia User ID");
          ExpHeader.SETRANGE(Status,ExpHeader.Status::"Pending Expense User");
          ExpHeader.SETRANGE(Posted,FALSE);
          IF ExpHeader.FINDSET THEN
            REPEAT
              CLEAR(FontColorText);
              CLEAR(FontColorEndTag);
              LineNo += 1;

              IF ReminderFound(ExpHeader.NextReminderDate) THEN BEGIN
                SendEmail := TRUE;
                EMReminder.CreateReminder(
                  DATABASE::"Expense Header",ExpHeader."Document Type"::Settlement,
                  ExpHeader."No.",0,ExpHeader."Continia User ID");
                FontColorText := '<font color="red">';
                FontColorEndTag := '</font>';
              END;

              IF LineNo MOD 2 = 0 THEN
                ClassText := '<tr class="even">'
              ELSE
                ClassText := '<tr class="odd">';

              BigStringLines.Append(
                CreateSettlementReminderLine(
                  ExpHeader,FontColorText + ClassText + '<td>','</td><td>','</td><td align="right">','</td></tr>' + FontColorEndTag));
            UNTIL ExpHeader.NEXT = 0;

          Expense.RESET;
          Expense.SETCURRENTKEY("Continia User ID","Document Date");
          Expense.SETRANGE("Continia User ID",ContiniaUserSetup."Continia User ID");
          Expense.SETRANGE(Status,Expense.Status::"Pending Expense User");
          Expense.SETRANGE(Posted,FALSE);
          Expense.SETRANGE("Settlement No.",'');
          IF Expense.FINDSET THEN
            REPEAT
              CLEAR(FontColorText);
              CLEAR(FontColorEndTag);
              LineNo += 1;

              IF ReminderFound(Expense.NextReminderDate) THEN BEGIN
                SendEmail := TRUE;
                EMReminder.CreateReminder(DATABASE::Expense,0,'',Expense."Entry No.",Expense."Continia User ID");
                FontColorText := '<font color="red">';
                FontColorEndTag := '</font>';
              END;

              IF LineNo MOD 2 = 0 THEN
                ClassText := '<tr class="even">'
              ELSE
                ClassText := '<tr class="odd">';

              BigStringLines.Append(
                CreateExpReminderLine(
                  Expense,FontColorText + ClassText + '<td>','</td><td>','</td><td align="right">','</td></tr>' + FontColorEndTag));

            UNTIL Expense.NEXT = 0;

          Mileage.RESET;
          Mileage.SETCURRENTKEY("Continia User ID");
          Mileage.SETRANGE("Continia User ID",ContiniaUserSetup."Continia User ID");
          Mileage.SETRANGE(Status,Mileage.Status::"Pending Expense User");
          Mileage.SETRANGE(Posted,FALSE);
          Mileage.SETRANGE("Settlement No.",'');
          IF Mileage.FINDSET THEN
            REPEAT
              CLEAR(FontColorText);
              CLEAR(FontColorEndTag);
              LineNo += 1;

              IF ReminderFound(Mileage.NextReminderDate) THEN BEGIN
                SendEmail := TRUE;
                EMReminder.CreateReminder(DATABASE::Mileage,0,'',Mileage."Entry No.",Mileage."Continia User ID");
                FontColorText := '<font color="red">';
                FontColorEndTag := '</font>';
              END;

              IF LineNo MOD 2 = 0 THEN
                ClassText := '<tr class="even">'
              ELSE
                ClassText := '<tr class="odd">';

              BigStringLines.Append(
                CreateMilReminderLine(
                  Mileage,FontColorText + ClassText + '<td>','</td><td>','</td><td align="right">','</td></tr>' + FontColorEndTag));

            UNTIL Mileage.NEXT = 0;

          //
          //SEND MAIL
          //

          IF SendEmail THEN BEGIN
            IF GUIALLOWED THEN
              Window.UPDATE(1,FORMAT(ContiniaUserSetup."Continia User ID"));

            BigStringTemplate.Clear;
            IF EMSetup."Reminder Template".HASVALUE THEN BEGIN
              EMSetup."Reminder Template".CREATEINSTREAM(NVInStream);
              BigStringTemplate.LoadFromStream(NVInStream);
            END ELSE BEGIN
              BigStringTemplate.Append(ReminderTemplateHTML1);
              BigStringTemplate.Append(ReminderTemplateHTML2);
            END;

            BigStringTemplate.Replace('#LISTTITLE#',STRSUBSTNO(ListTitleCaption,FORMAT(TODAY,0,'<day,2>. <month text> <year4>')));
            BigStringTemplate.Replace('#LISTLABELS#',CreateReminderHeader('<tr><th>','</th><th>','</th></tr>'));
            BigStringTemplate.Replace2('#LISTLINES#',BigStringLines);
            BigStringTemplate.Replace('#PORTALURL#',RemindersUrl);
            BigStringTemplate.Replace('#PORTALTITLE#',ReminderPortalTitle);

            // SEND E-MAIL
            SmtpMailManagement.SetBodyFromBigStringMgnt(BigStringTemplate);
            SmtpMailManagement.SetStartTLS(EMSetup."SMTP Require SSL/TLS");
            IF NOT SmtpMailManagement.Send THEN
              SmtpMailManagement.DisplayLastError;

            NoOfMails := NoOfMails + 1;
          END;

        UNTIL ContiniaUserSetup.NEXT = 0;

        IF GUIALLOWED THEN
          Window.CLOSE;
      END;

      IF GUIALLOWED THEN
        IF NoOfMails = 0 THEN
          MESSAGE(NoRemSentTxt)
        ELSE
          MESSAGE(RemimdersSentTxt,NoOfMails);
    END;

    LOCAL PROCEDURE GetReminderUrl@4(_UserID@1001 : Code[50]) : Text[1024];
    VAR
      EMOnlineMgt@1160040000 : Codeunit 6086305;
      Email@1000 : Text[1024];
    BEGIN
      Email := GetUserEmail(_UserID);
      IF Email <> '' THEN
        EXIT(EMOnlineMgt.GetBaseServerUrl + 'account/login');
    END;

    LOCAL PROCEDURE GetUserEmail@1(_UserID@1000 : Code[50]) : Text[250];
    VAR
      ContiniaUser@1001 : Record 6086001;
    BEGIN
      IF ContiniaUser.GET(_UserID) THEN
        EXIT(ContiniaUser."E-Mail");

      EXIT('');
    END;

    LOCAL PROCEDURE CreateReminderHeader@1160040016(FirstSeparator@1160040000 : Text[30];SecondSeparator@1160040001 : Text[30];LastSeparator@1160040002 : Text[30]) : Text[1024];
    BEGIN
      EXIT(
        FirstSeparator +
        DocumentDateCap + SecondSeparator +
        ExpenseTypeCap + SecondSeparator +
        CodeTxt + SecondSeparator +
        DescriptionCap + SecondSeparator +
        CurrencyCodeCap + SecondSeparator +
        AmountCap + SecondSeparator +
        AmountLCYCap + SecondSeparator +
        LinkCap + LastSeparator);
    END;

    LOCAL PROCEDURE CreateExpReminderLine@1160040004(VAR Expense@1000 : Record 6086320;FirstSeparator@1160040000 : Text[50];SecondSeparator@1160040001 : Text[50];RightAlignSeparator@1160040004 : Text[50];LastSeparator@1160040002 : Text[50]) : Text[1024];
    VAR
      EMOnlineMgt@1160040003 : Codeunit 6086305;
      ReminderUrlSrc@1160040005 : Text[1024];
    BEGIN
      ReminderUrlSrc := '<a href="%1">%2</a>';

      EXIT(
        FirstSeparator +
        FORMAT(Expense."Document Date",0,'<day,2>. <month text> <year4>') + SecondSeparator +
        ExpenseTxt + SecondSeparator +
        Expense."Expense Type" + SecondSeparator +
        FORMAT(Expense.Description) + SecondSeparator +
        FORMAT(Expense."Currency Code") +
        RightAlignSeparator + GetAmtTxt(Expense.Amount) +
        RightAlignSeparator + GetAmtTxt(Expense."Amount (LCY)") + SecondSeparator +
        STRSUBSTNO(ReminderUrlSrc,EMOnlineMgt.GetExpenseReminderUrl(Expense),ReminderTextCap) + LastSeparator);
    END;

    LOCAL PROCEDURE CreateMilReminderLine@1160040005(VAR Mileage@1000 : Record 6086338;FirstSeparator@1160040000 : Text[50];SecondSeparator@1160040001 : Text[50];RightAlignSeparator@1160040004 : Text[50];LastSeparator@1160040002 : Text[50]) : Text[1024];
    VAR
      EMOnlineMgt@1160040003 : Codeunit 6086305;
      ReminderUrlSrc@1160040005 : Text[1024];
    BEGIN
      ReminderUrlSrc := '<a href="%1">%2</a>';

      EXIT(
        FirstSeparator +
        FORMAT(Mileage."Registration Date",0,'<day,2>. <month text> <year4>') + SecondSeparator +
        MileageTxt + SecondSeparator +
        Mileage."Vehicle Code" + SecondSeparator +
        FORMAT(Mileage.Description) + SecondSeparator +
        '' +
        RightAlignSeparator + GetAmtTxt(Mileage."Amount (LCY)") +
        RightAlignSeparator + GetAmtTxt(Mileage."Amount (LCY)") + SecondSeparator +
        STRSUBSTNO(ReminderUrlSrc,EMOnlineMgt.GetMileageReminderUrl(Mileage),ReminderTextCap) + LastSeparator);
    END;

    LOCAL PROCEDURE CreateSettlementReminderLine@1160040011(VAR ExpHeader@1000 : Record 6086339;FirstSeparator@1160040000 : Text[50];SecondSeparator@1160040001 : Text[50];RightAlignSeparator@1160040004 : Text[50];LastSeparator@1160040002 : Text[50]) : Text[1024];
    VAR
      EMOnlineMgt@1160040003 : Codeunit 6086305;
      ReminderUrlSrc@1160040005 : Text[1024];
    BEGIN
      ReminderUrlSrc := '<a href="%1">%2</a>';

      EXIT(
        FirstSeparator +
        FORMAT(ExpHeader."Date Created",0,'<day,2>. <month text> <year4>') + SecondSeparator +
        SettlementTxt + ' (' + ExpHeader.GetOverviewDetails + ')' + SecondSeparator +
        '' + SecondSeparator +
        FORMAT(ExpHeader.Description) + SecondSeparator +
        '' +
        RightAlignSeparator + GetAmtTxt(ExpHeader.GetAmountLCY) +
        RightAlignSeparator + GetAmtTxt(ExpHeader.GetAmountLCY) + SecondSeparator +
        STRSUBSTNO(ReminderUrlSrc,EMOnlineMgt.GetSettlementReminderUrl(ExpHeader),ReminderTextCap) + LastSeparator);
    END;

    LOCAL PROCEDURE ReminderFound@1160040001(NextReminderDate@1160040000 : Date) : Boolean;
    BEGIN
      EXIT ((NextReminderDate <> 0D) AND (NextReminderDate <= TODAY));
    END;

    LOCAL PROCEDURE GetAmtTxt@5(Amount@1001 : Decimal) : Text[50];
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      GLSetup.GET;
      EXIT(FORMAT(Amount,0,STRSUBSTNO('<Precision,%1><Standard Format,0>',GLSetup."Amount Decimal Places")));
    END;

    BEGIN
    END.
  }
}

