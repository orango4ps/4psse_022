OBJECT Report 11072153 Copy Service Survey Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Strukturzeilen kopieren;
               ENU=Copy Structure Lines;
               NLD=Structuurregels kopiâren];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 9890;    ;DataItem;OriginalStructureLine;
               DataItemTable=Table11071806;
               DataItemTableView=SORTING(Survey No.,Location Code,Structure No.,Code,Line No.);
               OnPreDataItem=BEGIN
                               SETRANGE("Survey No.", FromSurveyNo);
                               SETRANGE("Location Code", FromLocationCode);
                               SETRANGE("Structure No.", FromStructureNo);
                               SETRANGE(Code, FromCode);
                               SETRANGE("Line No.", 0);
                             END;

               OnAfterGetRecord=BEGIN
                                  StartProcess;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   CopySelectedLinesVisible := CopySelectedLines;
                 END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525007;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525001;2;Group  ;
                  CaptionML=[DEU=Kopieren von;
                             ENU=Copy From;
                             NLD=Kopieer van;
                             NOR=Kopier fra;
                             SVE=Kopiera frÜn];
                  GroupType=Group }

      { 1100525002;3;Field  ;
                  CaptionML=[DEU=öbersicht;
                             ENU=Survey;
                             NLD=Inventarisatie;
                             NOR=Kartlegging;
                             SVE=KartlÑggning];
                  SourceExpr=FromSurveyNo;
                  Editable=FALSE }

      { 1100525003;3;Field  ;
                  CaptionML=[DEU=öbersichtsbeschreibung;
                             ENU=Survey Description;
                             NLD=Omschrijving inventarisatie;
                             NOR=Kartlegging, beskrivelse;
                             SVE=KartlÑggning, beskrivning];
                  SourceExpr=FromSurveyDescription;
                  Editable=FALSE }

      { 1100525008;3;Field  ;
                  CaptionML=[DEU=Lagerort;
                             ENU=Location;
                             NLD=Locatie;
                             NOR=Plassering;
                             SVE=LagerstÑlle];
                  SourceExpr=FromLocationCode;
                  Editable=FALSE }

      { 1100525010;3;Field  ;
                  CaptionML=[DEU=Struktur;
                             ENU=Structure;
                             NLD=Structuur;
                             NOR=Struktur;
                             SVE=Struktur];
                  SourceExpr=FromStructureNo;
                  Editable=FALSE }

      { 1100525012;3;Field  ;
                  CaptionML=[DEU=Code;
                             ENU=Code;
                             NLD=Code;
                             NOR=Kode;
                             SVE=Kod];
                  SourceExpr=FromCode;
                  Editable=FALSE }

      { 1100525014;3;Field  ;
                  CaptionML=[DEU=Codebeschreibung;
                             ENU=Code Description;
                             NLD=Omschrijving code;
                             NOR=Kodebeskrivelse;
                             SVE=Kod,,,,,Beskrivning];
                  SourceExpr=FromCodeDescription;
                  Editable=FALSE }

      { 1100525004;2;Group  ;
                  CaptionML=[DEU=Kopieren in;
                             ENU=Copy To;
                             NLD=Kopieer naar;
                             NOR=Kopier til;
                             SVE=Kopiera till];
                  GroupType=Group }

      { 1100525005;3;Field  ;
                  CaptionML=[DEU=öbersicht;
                             ENU=Survey;
                             NLD=Inventarisatie;
                             NOR=Kartlegging;
                             SVE=KartlÑggning];
                  SourceExpr=ToSurveyNo;
                  Editable=FALSE }

      { 1100525006;3;Field  ;
                  CaptionML=[DEU=öbersichtsbeschreibung;
                             ENU=Survey Description;
                             NLD=Omschrijving inventarisatie;
                             NOR=Kartlegging, beskrivelse;
                             SVE=KartlÑggning, beskrivning];
                  SourceExpr=ToSurveyDescription;
                  Editable=FALSE }

      { 1100525009;3;Field  ;
                  CaptionML=[DEU=Lagerort;
                             ENU=Location;
                             NLD=Locatie;
                             NOR=Plassering;
                             SVE=LagerstÑlle];
                  SourceExpr=ToLocationCode;
                  Editable=FALSE }

      { 1100525011;3;Field  ;
                  CaptionML=[DEU=Struktur;
                             ENU=Structure;
                             NLD=Structuur;
                             NOR=Struktur;
                             SVE=Struktur];
                  SourceExpr=ToStructureNo;
                  Editable=FALSE }

      { 1100525013;3;Field  ;
                  Name=ToCode;
                  CaptionML=[DEU=Code;
                             ENU=Code;
                             NLD=Code;
                             NOR=Kode;
                             SVE=Kod];
                  SourceExpr=ToCode;
                  OnValidate=BEGIN
                               ValidateToCode;
                             END;

                  OnLookup=VAR
                             SurveyStructureLine@1100525001 : Record 11071806;
                           BEGIN
                             SurveyStructureLine.SETRANGE("Survey No.", ToSurveyNo);
                             SurveyStructureLine.SETRANGE("Location Code", ToLocationCode);
                             SurveyStructureLine.SETRANGE("Structure No.", ToStructureNo);
                             IF PAGE.RUNMODAL(0, SurveyStructureLine) = ACTION::LookupOK THEN BEGIN
                               ToCode := SurveyStructureLine.Code;
                               ValidateToCode;
                             END;
                           END;
                            }

      { 1100525015;3;Field  ;
                  CaptionML=[DEU=Codebeschreibung;
                             ENU=Code Description;
                             NLD=Omschrijving code;
                             NOR=Kodebeskrivelse;
                             SVE=Kod,,,,,Beskrivning];
                  SourceExpr=ToCodeDescription }

      { 1100525016;3;Field  ;
                  CaptionML=[DEU=Process;
                             ENU=Process;
                             NLD=Verwerking;
                             NOR=Behandle;
                             SVE=Process];
                  SourceExpr=CopyMethod;
                  Editable=FALSE }

      { 1100529600;3;Field  ;
                  Name=CopySelectedLines;
                  CaptionML=[DEU=AusgewÑhlte Zeilen kopieren;
                             ENU=Copy Selected Lines;
                             NLD=Geselecteerde regels kopiâren];
                  SourceExpr=CopySelectedLines;
                  Visible=CopySelectedLinesVisible }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      FromSurveyNo@1100528601 : Code[20];
      ToSurveyNo@1100528602 : Code[20];
      FromSurveyDescription@1100528603 : Text[50];
      ToSurveyDescription@1100528604 : Text[50];
      FromLocationCode@1100525001 : Code[20];
      ToLocationCode@1100525000 : Code[20];
      FromStructureNo@1100525002 : Integer;
      ToStructureNo@1100525003 : Integer;
      FromCode@1100525004 : Code[20];
      ToCode@1100525005 : Code[20];
      FromCodeDescription@1100525007 : Text[100];
      ToCodeDescription@1100525006 : Text[100];
      CopyObjectsOnly@1100525008 : Boolean;
      Text000@1100525014 : TextConst 'DEU=Strukturzeile %1 mit Ebene 0 kann von diesem Vorgang nicht verarbeitet werden;ENU=Structuur Line %1 with Level 0 can not be processed by this procedure;NLD=Structuurregel %1 met niveau 0 kan niet verwerkt worden in deze procedure';
      Text001@1100525009 : TextConst 'DEU=Bestimmungscode ist bereits vorhanden;ENU=Destination Code already exists;NLD=Bestemmingscode bestaat reeds';
      Text002@1100525010 : TextConst 'DEU=Ebenenherkunftscode stimmt nicht mit Ebenenbestimmungscode Åberein;ENU=Level Source Code does not match with Level Desctionation Code;NLD=Niveau broncode komt niet overeen met niveau bestemmingscode';
      CopyMethod@1100525011 : Text[250];
      Text003@1100525012 : TextConst 'DEU=Nur Strukturzeilen mit Objekt werden kopiert;ENU=Only Structure Lines with Object will be copied;NLD=Alleen structuurregels met object worden gekopieerd';
      Text004@1100525013 : TextConst 'DEU=Alle Strukturzeilen kopieren;ENU=Copy all structure lines;NLD=Kopieer alle structuurregels';
      sp1@1100525015 : Integer;
      sp2@1100525016 : Integer;
      CopySelectedLines@1100529600 : Boolean;
      TmpSelectedLine@1100529601 : TEMPORARY Record 11071806;
      CopySelectedLinesVisible@1100529602 : Boolean INDATASET;

    PROCEDURE SetDefaults@1100528604(ISurveyNo@1100528600 : Code[20];ILocationCode@1100525000 : Code[20];IStructureNo@1100525003 : Integer;ICode@1100525005 : Code[20]);
    VAR
      ServiceSurvey@1100528601 : Record 11071803;
      SurveyLocation@1100525001 : Record 11071804;
      SurveyStructure@1100525002 : Record 11071805;
      SurveyStructureLine@1100525004 : Record 11071806;
    BEGIN
      FromSurveyNo := ISurveyNo;
      ServiceSurvey.GET(FromSurveyNo);
      FromSurveyDescription := ServiceSurvey.Description;
      ToSurveyNo := FromSurveyNo;
      ToSurveyDescription := FromSurveyDescription;
      //
      FromLocationCode := ILocationCode;
      SurveyLocation.GET(ISurveyNo, ILocationCode);
      ToLocationCode := FromLocationCode;
      //
      FromStructureNo := IStructureNo;
      SurveyStructure.GET(ISurveyNo, ILocationCode, IStructureNo);
      ToStructureNo := FromStructureNo;
      //
      FromCode := ICode;
      SurveyStructureLine.SETRANGE("Survey No.", FromSurveyNo);
      SurveyStructureLine.SETRANGE("Location Code", FromLocationCode);
      SurveyStructureLine.SETRANGE("Structure No.", FromStructureNo);
      SurveyStructureLine.SETRANGE(Code, FromCode);
      SurveyStructureLine.FINDSET;
      IF SurveyStructureLine.Level = 0 THEN
        ERROR(Text000, SurveyStructureLine.Code);

      FromCodeDescription := SurveyStructureLine."Code Description";
      //ToCode := FromCode;
      ToCodeDescription := FromCodeDescription;

      CheckFormat;
    END;

    LOCAL PROCEDURE ValidateToCode@1100525000();
    VAR
      SurveyStructureLine@1100525002 : Record 11071806;
      TmpSurveyStructureLine@1100525001 : TEMPORARY Record 11071806;
      FromLevel@1100525000 : Integer;
    BEGIN
      CopyObjectsOnly := FALSE;

      IF STRLEN(ToCode) < STRLEN(FromCode) THEN BEGIN
        TmpSurveyStructureLine."Survey No." := ToSurveyNo;
        TmpSurveyStructureLine."Location Code" := ToLocationCode;
        TmpSurveyStructureLine."Structure No." := ToStructureNo;
        TmpSurveyStructureLine.VALIDATE(Code, ToCode);  //format code
        ToCode := TmpSurveyStructureLine.Code;
      END;

      SurveyStructureLine.SETRANGE("Survey No.", ToSurveyNo);
      SurveyStructureLine.SETRANGE("Location Code", ToLocationCode);
      SurveyStructureLine.SETRANGE("Structure No.", ToStructureNo);
      SurveyStructureLine.SETRANGE(Code, ToCode);
      IF SurveyStructureLine.FINDSET THEN BEGIN
        ToCodeDescription := SurveyStructureLine."Code Description";
        CopyObjectsOnly := TRUE;
        MESSAGE(Text001);
      END;

      IF CopyObjectsOnly = FALSE THEN BEGIN
        TmpSurveyStructureLine."Survey No." := FromSurveyNo;
        TmpSurveyStructureLine."Location Code" := FromLocationCode;
        TmpSurveyStructureLine."Structure No." := FromStructureNo;
        TmpSurveyStructureLine.VALIDATE(Code, FromCode);
        FromLevel := TmpSurveyStructureLine.Level;
        //
        TmpSurveyStructureLine."Survey No." := ToSurveyNo;
        TmpSurveyStructureLine."Location Code" := ToLocationCode;
        TmpSurveyStructureLine."Structure No." := ToStructureNo;
        TmpSurveyStructureLine.VALIDATE(Code, ToCode);
        IF TmpSurveyStructureLine.Level <> FromLevel THEN BEGIN
          CopyObjectsOnly := TRUE;
          MESSAGE(Text002);
        END;
      END;

      IF CopyObjectsOnly THEN
        CopyMethod := Text003
      ELSE
        CopyMethod := Text004;
    END;

    LOCAL PROCEDURE StartProcess@1100525001();
    VAR
      SurveyStructureLine@1100525008 : Record 11071806;
      TmpSurveyStructureLine@1100525007 : TEMPORARY Record 11071806;
      SurveyLocationObject@1100525006 : Record 11071813;
      TmpSurveyLocationObject@1100525005 : TEMPORARY Record 11071813;
      SurveyObjectDetail@1100525004 : Record 11071807;
      TmpSurveyObjectDetail@1100525003 : TEMPORARY Record 11071807;
      SurveyObjectAddAct@1100525002 : Record 11071814;
      TmpSurveyObjectAddAct@1100525001 : TEMPORARY Record 11071814;
      SurvObjAddActProperty@1100528600 : Record 11071955;
      TmpSurvObjAddActProperty@1100528601 : TEMPORARY Record 11071955;
      LastSeqNo@1100525000 : Integer;
      LastLineNo@1100525009 : Integer;
      NewCode@1100525010 : Code[20];
    BEGIN
      WITH OriginalStructureLine DO BEGIN
        SurveyLocationObject.SETRANGE("Survey No.", "Survey No.");
        SurveyLocationObject.SETRANGE("Location Code", "Location Code");
        IF SurveyLocationObject.FINDLAST THEN
          LastSeqNo := SurveyLocationObject."Seq. No.";
        SurveyStructureLine.SETRANGE("Survey No.", "Survey No.");
        SurveyStructureLine.SETRANGE("Location Code", "Location Code");
        SurveyStructureLine.SETRANGE("Structure No.", "Structure No.");
        SurveyStructureLine.SETRANGE(Code, ToCode);
        IF SurveyStructureLine.FINDLAST THEN
          LastLineNo := SurveyStructureLine."Line No.";
        //
        IF CopyObjectsOnly THEN BEGIN
          TmpSurveyStructureLine.SETRANGE("Survey No.", ToSurveyNo);
          TmpSurveyStructureLine.SETRANGE("Location Code", ToLocationCode);
          TmpSurveyStructureLine.SETRANGE("Structure No.", ToStructureNo);
          TmpSurveyStructureLine.SETRANGE(Code, ToCode);
          IF TmpSurveyStructureLine.FINDLAST THEN
            LastLineNo := TmpSurveyStructureLine."Line No.";
        END;
        //
        SurveyStructureLine.RESET;
        SurveyStructureLine.SETRANGE("Survey No.", "Survey No.");
        SurveyStructureLine.SETRANGE("Location Code", "Location Code");
        SurveyStructureLine.SETRANGE("Structure No.", "Structure No.");
        IF Level = 1 THEN
          SurveyStructureLine.SETRANGE("Element Level 1", Code);
        IF Level = 2 THEN
          SurveyStructureLine.SETRANGE("Element Level 2", Code);
        IF Level = 3 THEN
          SurveyStructureLine.SETRANGE(Code, Code);
        IF CopyObjectsOnly THEN
          SurveyStructureLine.SETRANGE("Object Line", TRUE);
        IF SurveyStructureLine.FINDSET THEN BEGIN
          REPEAT
            TmpSurveyStructureLine := SurveyStructureLine;
            NewCode := ToCode;
            IF NOT CopyObjectsOnly THEN BEGIN
              IF Level = 1 THEN
                NewCode := COPYSTR(ToCode,1,sp1) + COPYSTR(SurveyStructureLine.Code,sp1+1,STRLEN(FromCode)-sp1);
              IF Level = 2 THEN
                NewCode := COPYSTR(ToCode,1,sp2) + COPYSTR(SurveyStructureLine.Code,sp2+1,STRLEN(FromCode)-sp2);
            END;

            IF (NOT CopySelectedLines) OR
               (TmpSelectedLine.GET(SurveyStructureLine."Survey No.", SurveyStructureLine."Location Code",
                  SurveyStructureLine."Structure No.", SurveyStructureLine.Code, SurveyStructureLine."Line No."))
            THEN BEGIN
              //TmpSurveyStructureLine.VALIDATE(Code, NewCode);
              TmpSurveyStructureLine.Code := NewCode;  //C028336 (db, 07-01-16)
              IF SurveyStructureLine."Object Line" THEN BEGIN
                TmpSurveyStructureLine."Seq. No." := LastSeqNo + 1;
                TmpSurveyStructureLine."Code Description" := '';
                LastSeqNo := TmpSurveyStructureLine."Seq. No.";
              END ELSE BEGIN
                IF TmpSurveyStructureLine.Code = ToCode THEN
                  TmpSurveyStructureLine."Code Description":= ToCodeDescription;
              END;
              IF CopyObjectsOnly THEN BEGIN
                TmpSurveyStructureLine."Line No." := LastLineNo + 1;
                LastLineNo := TmpSurveyStructureLine."Line No.";
              END;
              TmpSurveyStructureLine.INSERT;
              //
              IF SurveyStructureLine."Object Line" THEN BEGIN
                SurveyLocationObject.SETRANGE("Survey No.", SurveyStructureLine."Survey No.");
                SurveyLocationObject.SETRANGE("Location Code", SurveyStructureLine."Location Code");
                SurveyLocationObject.SETRANGE("Seq. No.", SurveyStructureLine."Seq. No.");
                IF SurveyLocationObject.FINDSET THEN BEGIN
                  REPEAT
                    TmpSurveyLocationObject := SurveyLocationObject;
                    TmpSurveyLocationObject."Seq. No." := TmpSurveyStructureLine."Seq. No.";
                    TmpSurveyLocationObject.INSERT;
                  UNTIL SurveyLocationObject.NEXT = 0;
                END;
                //
                SurveyObjectDetail.SETRANGE("Survey No.", SurveyStructureLine."Survey No.");
                SurveyObjectDetail.SETRANGE("Location Code", SurveyStructureLine."Location Code");
                SurveyObjectDetail.SETRANGE("Seq. No.", SurveyStructureLine."Seq. No.");
                IF SurveyObjectDetail.FINDSET THEN BEGIN
                  REPEAT
                    TmpSurveyObjectDetail := SurveyObjectDetail;
                    TmpSurveyObjectDetail."Seq. No." := TmpSurveyStructureLine."Seq. No.";
                    TmpSurveyObjectDetail.INSERT;
                  UNTIL SurveyObjectDetail.NEXT = 0;
                END;
                //
                SurveyObjectAddAct.SETRANGE("Survey No.", SurveyStructureLine."Survey No.");
                SurveyObjectAddAct.SETRANGE("Location Code", SurveyStructureLine."Location Code");
                SurveyObjectAddAct.SETRANGE("Seq. No.", SurveyStructureLine."Seq. No.");
                IF SurveyObjectAddAct.FINDSET THEN BEGIN
                  REPEAT
                    TmpSurveyObjectAddAct := SurveyObjectAddAct;
                    TmpSurveyObjectAddAct."Seq. No." := TmpSurveyStructureLine."Seq. No.";
                    TmpSurveyObjectAddAct.INSERT;
                  UNTIL SurveyObjectAddAct.NEXT = 0;
                END;
                //
                SurvObjAddActProperty.SETRANGE("Survey No.", SurveyStructureLine."Survey No.");
                SurvObjAddActProperty.SETRANGE("Location Code", SurveyStructureLine."Location Code");
                SurvObjAddActProperty.SETRANGE("Seq. No.", SurveyStructureLine."Seq. No.");
                IF SurvObjAddActProperty.FINDSET THEN
                  REPEAT
                    TmpSurvObjAddActProperty := SurvObjAddActProperty;
                    TmpSurvObjAddActProperty."Seq. No." := TmpSurveyStructureLine."Seq. No.";
                    TmpSurvObjAddActProperty.INSERT;
                  UNTIL SurvObjAddActProperty.NEXT = 0;
              END;
            END;
          UNTIL SurveyStructureLine.NEXT = 0;
        END;
      END;

      IF TmpSurveyStructureLine.FINDSET THEN BEGIN
        REPEAT
          SurveyStructureLine := TmpSurveyStructureLine;
          SurveyStructureLine.VALIDATE(Code, TmpSurveyStructureLine.Code);  //C028336 (db, 07-01-16)
          SurveyStructureLine.INSERT(TRUE);
        UNTIL TmpSurveyStructureLine.NEXT = 0;
      END;
      IF TmpSurveyLocationObject.FINDSET THEN BEGIN
        REPEAT
          SurveyLocationObject := TmpSurveyLocationObject;
          SurveyLocationObject.INSERT;
        UNTIL TmpSurveyLocationObject.NEXT = 0;
      END;
      IF TmpSurveyObjectDetail.FINDSET THEN BEGIN
        REPEAT
          SurveyObjectDetail := TmpSurveyObjectDetail;
          SurveyObjectDetail.INSERT;
        UNTIL TmpSurveyObjectDetail.NEXT = 0;
      END;
      IF TmpSurveyObjectAddAct.FINDSET THEN BEGIN
        REPEAT
          SurveyObjectAddAct := TmpSurveyObjectAddAct;
          SurveyObjectAddAct.INSERT;
        UNTIL TmpSurveyObjectAddAct.NEXT = 0;
      END;
      IF TmpSurvObjAddActProperty.FINDSET THEN BEGIN
        REPEAT
          SurvObjAddActProperty := TmpSurvObjAddActProperty;
          SurvObjAddActProperty.INSERT;
        UNTIL TmpSurvObjAddActProperty.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CheckFormat@16();
    VAR
      lvText000@1100525000 : TextConst 'DEU=Nicht mehr als 3 Stufen zulÑssig;ENU=only 3 levels allowed;NLD=niet meer dan 3 niveaus toegestaan;NOR=kun tre nivÜer tillates;SVE=endast tre nivÜer tillÜts';
      x@1100525001 : Integer;
      lp@1100525002 : Integer;
    BEGIN
      x := 0;
      sp1 := 0;
      sp2 := 0;
      lp := STRLEN(FromCode);

      REPEAT
        x := x + 1;
        IF COPYSTR(FromCode,x,1) = '.' THEN BEGIN
          IF sp1 = 0 THEN
            sp1 := x
          ELSE
            IF sp2 = 0 THEN
              sp2 := x
            ELSE
              ERROR(lvText000);
        END;
      UNTIL x = lp;
    END;

    PROCEDURE SetCopySelectedLines@1100529600(VAR ServSurveyStrLine@1100529602 : Record 11071806);
    BEGIN
      TmpSelectedLine.RESET;
      TmpSelectedLine.DELETEALL;

      CopySelectedLines := ServSurveyStrLine.COUNT > 1;
      IF NOT CopySelectedLines THEN
        EXIT;

      ServSurveyStrLine.FINDFIRST;
      SetDefaults(ServSurveyStrLine."Survey No.", ServSurveyStrLine."Location Code", ServSurveyStrLine."Structure No.", ServSurveyStrLine.Code);
      REPEAT
        TmpSelectedLine := ServSurveyStrLine;
        TmpSelectedLine.INSERT;
      UNTIL ServSurveyStrLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

