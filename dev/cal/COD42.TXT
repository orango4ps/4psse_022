OBJECT Codeunit 42 CaptionManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    LOCAL PROCEDURE ResolveCaptionClass@1(Language@1000 : Integer;CaptionClassExpr@1001 : Text) Caption : Text;
    VAR
      CaptionArea@1002 : Text;
      CaptionExpr@1003 : Text;
      Resolved@1004 : Boolean;
    BEGIN
      // <summary>
      // Resolves a given CaptionClass expression and a language id into a caption.
      // The function either resolves the CaptionClass expression or emits OnResolveCaptionClass event.
      // </summary>
      // <param name="Language">The current language id </param>
      // <param name="CaptionClassExpr">The CaptionClass expression to be resolved.
      // Usually in the following format: <Caption Area>,<Caption Expression></param>
      // <returns>The resolved caption or the original CaptionClass expression if the resolution was unsuccessful.</returns>

      IF SplitCaptionClassExpr(CaptionClassExpr,CaptionArea,CaptionExpr) THEN
        IF CaptionArea = '3' THEN BEGIN
          Caption := CaptionExpr;
          Resolved := TRUE;
        END ELSE
          OnResolveCaptionClass(CaptionArea,CaptionExpr,Language,Caption,Resolved);

      // if Caption hasn't been resolved, fallback to CaptionClassExpr
      IF NOT Resolved THEN
        Caption := CaptionClassExpr;
    END;

    LOCAL PROCEDURE SplitCaptionClassExpr@2(CaptionClassExpr@1000 : Text;VAR CaptionArea@1004 : Text;VAR CaptionExpr@1002 : Text) : Boolean;
    VAR
      CommaPosition@1001 : Integer;
    BEGIN
      // <summary>
      // Splits given CaptionClass expression into caption area and caption expression (provided by var parameters)
      // </summary>
      // <param name="CaptionClassExpr">The CaptionClass expression to be split</param>
      // <param name="CaptionArea">The caption area extracted from the CaptionClass expression</param>
      // <param name="CaptionExpr">The caption expression extracted from the CaptionClass expression</param>
      // <returns>Returns whether the split succeeded or not</returns>

      CommaPosition := STRPOS(CaptionClassExpr,',');
      IF CommaPosition > 0 THEN BEGIN
        CaptionArea := COPYSTR(CaptionClassExpr,1,CommaPosition - 1);
        CaptionExpr := COPYSTR(CaptionClassExpr,CommaPosition + 1);
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    [EventSubscriber(Codeunit,2000000004,CaptionClassTranslate)]
    LOCAL PROCEDURE DoResolveCaptionClass@15(Language@1000 : Integer;CaptionExpr@1001 : Text[1024];VAR Translation@1002 : Text[1024]);
    BEGIN
      Translation := COPYSTR(ResolveCaptionClass(Language,CaptionExpr),1,MAXSTRLEN(Translation));
      OnAfterCaptionClassTranslate(Language,CaptionExpr,Translation);
    END;

    [Integration]
    LOCAL PROCEDURE OnResolveCaptionClass@5(CaptionArea@1001 : Text;CaptionExpr@1002 : Text;Language@1000 : Integer;VAR Caption@1003 : Text;VAR Resolved@1004 : Boolean);
    BEGIN
      // <summary>
      // Integration event for resolving CaptionClass expression, split into CaptionArea and CaptionExpr.
      // Note there should be a single subscriber per caption area.
      // The event implements the "resolved" pattern - if a subscriber resolves the caption, it should set Resolved to TRUE.
      // </summary>
      // <param name="CaptionArea">The caption area used in the CaptionClass expression. Should be unique for every subscriber</param>
      // <param name="CaptionExpr">The caption expression used for resolving the CaptionClass expression</param>
      // <param name="Language">The current language id that can be used for resolving the CaptionClass expression</param>
      // <param name="Caption">Var parameter - the resolved caption</param>
      // <param name="Resolved">Boolean for marking whether the CaptionClass expression was resolved</param>
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCaptionClassTranslate@1061(Language@1001 : Integer;CaptionExpression@1000 : Text;VAR Caption@1002 : Text[1024]);
    BEGIN
      // <summary>
      // Integration event for after resolving CaptionClass expression.
      // </summary>
      // <param name="Language">The current language id</param>
      // <param name="CaptionExpression">The original CaptionClass expression</param>
      // <param name="Caption">The resolved caption expression</param>
    END;

    PROCEDURE CaptionClassTranslate@1100525000(Language@1100525001 : Integer;CaptionClassExpr@1100525000 : Text) Caption : Text;
    BEGIN
      //**4PS
      EXIT(ResolveCaptionClass(Language,CaptionClassExpr));
    END;

    BEGIN
    END.
  }
}

