OBJECT Table 11071702 Contract Object
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceContract@1100528600 : Record 11012812;
             BEGIN
               ServiceContract.GET("Service Contract No.");
               "Invoice Interval" := ServiceContract."Invoice Interval";
               IF ServiceContract."Ending Date" <> 0D THEN
                 IF ("Ending Date" = 0D) OR ("Ending Date" > ServiceContract."Ending Date") THEN
                   "Ending Date" := ServiceContract."Ending Date";
               "Plan Method" := ServiceContract."Plan Method";

               IF ServiceContract."Contract Form" = ServiceContract."Contract Form"::"Location Contract" THEN
                 ServiceContract.FIELDERROR("Contract Form");
             END;

    OnDelete=VAR
               ContractObjectMaintenance@1100526200 : Record 11071692;
               ServiceInspectionLine@1100528600 : Record 11071751;
               ServiceContract@1100528601 : Record 11012812;
               ContractAmountsbyDate@1100528602 : Record 11071708;
               ReferenceNorm@1100528603 : Record 11012856;
             BEGIN
               ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
               ContractObjectMaintenance.SETRANGE("Object No.", "Service Object No.");
               ContractObjectMaintenance.DELETEALL(TRUE);

               ContractAmountsbyDate.SETRANGE(Type, ContractAmountsbyDate.Type::"Price Agreement");
               ContractAmountsbyDate.SETRANGE("Service Contract No.", "Service Contract No.");
               ContractAmountsbyDate.SETRANGE("Service Object No.", "Service Object No.");
               ContractAmountsbyDate.DELETEALL(TRUE);

               ServiceInspectionLine.SETRANGE("Inspection Type", ServiceInspectionLine."Inspection Type"::"Contract Object");
               ServiceInspectionLine.SETRANGE("Service Contract No.", "Service Contract No.");
               ServiceInspectionLine.SETRANGE(Code, "Service Object No.");
               ServiceInspectionLine.DELETEALL(TRUE);

               ReferenceNorm.SETRANGE("Service Contract No.", "Service Contract No.");
               ReferenceNorm.SETRANGE("Object No.", "Service Object No.");
               ReferenceNorm.DELETEALL(TRUE);

               IF "Service Package" <> '' THEN
                 IF ServiceContract.GET("Service Contract No.") THEN
                   ServiceContract.RemoveUnusedContractDiscountTerms("Service Object No.", 0, '', '');

               UpdateServicePackageOnServiceContract(2);
             END;

    CaptionML=[ENU=Contract Object;
               NOR=Kontraktobjekt;
               SVE=Kontraktsobjekt];
  }
  FIELDS
  {
    { 10  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract.No.;
                                                   CaptionML=[ENU=Service Contract No.;
                                                              NOR=Servicekontraktnr;
                                                              SVE=Servicekontraktnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Service Object No.  ;Code20        ;TableRelation=Service-Object.No.;
                                                   OnValidate=VAR
                                                                ServiceContract@1100526200 : Record 11012812;
                                                                ContractObject@1100526202 : Record 11071702;
                                                                ServiceObject@1100526203 : Record 11071691;
                                                                ContractObjectMaintenance@1100525001 : Record 11071692;
                                                                MaintenanceProposalLine@1100525002 : Record 11071696;
                                                                ActiveContractNo@1100526201 : Code[20];
                                                                ServiceLocationNo@1100525000 : Code[20];
                                                              BEGIN
                                                                ServiceContract.GET(Rec."Service Contract No.");
                                                                IF "Service Object No." <> '' THEN BEGIN
                                                                  IF ServiceObject.GET("Service Object No.") THEN BEGIN
                                                                    ServiceObject.TESTFIELD("Standard Object No.");
                                                                    ServiceLocationNo := ServiceObject.GetPrefServiceLocationNo(TRUE);
                                                                    IF ServiceLocationNo <> '' THEN
                                                                      VALIDATE("Service Location No.", ServiceLocationNo);
                                                                    ServiceObject.TESTFIELD(Blocked, FALSE);
                                                                    Subcontracting := ServiceObject.Subcontracting;
                                                                  END;
                                                                  IF xRec."Service Object No." = '' THEN
                                                                    VALIDATE("Starting Date", TODAY);
                                                                  "Service Category (Call)" := ServiceContract."Service Category (Call)";
                                                                  "Service Category (Direct)" := ServiceContract."Service Category (Direct)";
                                                                  "Service Index Method" := ServiceContract."Service Index Method";
                                                                  HandleSMAgreement(TRUE, TRUE);
                                                                  UpdateReferenceNorms;
                                                                END;

                                                                IF xRec."Service Object No." = '' THEN BEGIN
                                                                  IF ServiceContract."Ending Date" <> 0D THEN
                                                                    "Ending Date" := ServiceContract."Ending Date";

                                                                  IF "Service Package" = '' THEN
                                                                    VALIDATE("Service Package", ServiceContract."Service Package");
                                                                END;

                                                                IF xRec."Service Object No." <> "Service Object No." THEN BEGIN
                                                                  ActiveContractNo := MaintenanceManagementCU.GetActualContractForObject("Service Object No.", TODAY);
                                                                  IF ActiveContractNo <> '' THEN BEGIN
                                                                    ServiceContract.GET(ActiveContractNo);
                                                                    IF ServiceContract.Status = ServiceContract.Status::"Invoicing Allowed" THEN BEGIN
                                                                      ContractObject.GET(ActiveContractNo, "Service Object No.");
                                                                      IF ContractObject."Ending Date" = 0D THEN
                                                                        MESSAGE(Text005, ActiveContractNo)
                                                                      ELSE
                                                                        MESSAGE(Text005 + Text006, ActiveContractNo, ContractObject."Ending Date");
                                                                    END;
                                                                  END;
                                                                  IF xRec."Service Object No." <> '' THEN BEGIN
                                                                    ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                    ContractObjectMaintenance.SETRANGE("Object No.", xRec."Service Object No.");
                                                                    IF NOT ContractObjectMaintenance.ISEMPTY THEN
                                                                      ERROR(Text012, xRec."Service Object No.", ContractObjectMaintenance.TABLECAPTION);
                                                                    MaintenanceProposalLine.SETCURRENTKEY("Service Contract No.", "Object No.");
                                                                    MaintenanceProposalLine.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                    MaintenanceProposalLine.SETRANGE("Object No.", xRec."Service Object No.");
                                                                    IF NOT MaintenanceProposalLine.ISEMPTY THEN
                                                                      ERROR(Text012, xRec."Service Object No.", MaintenanceProposalLine.TABLECAPTION);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Object No.;
                                                              NOR=Objektnr.;
                                                              SVE=Objektnr];
                                                   NotBlank=Yes }
    { 25  ;   ;Service Package     ;Code10        ;TableRelation="Service Package".Code WHERE (Source Type=CONST(None));
                                                   OnValidate=VAR
                                                                ServiceContract@1100528600 : Record 11012812;
                                                              BEGIN
                                                                CALCFIELDS("Standard Object No.");
                                                                ServiceContract.AddServicePackageDiscountToContract("Service Contract No.", "Service Package");
                                                                GetServiceCategoryContract;
                                                                FillDefaultMaintScheme;
                                                                UpdateServicePackageOnServiceContract(1);

                                                                //IF MODIFY THEN //C028734.o
                                                                IF ServiceContract.GET("Service Contract No.") THEN
                                                                  ServiceContract.RemoveUnusedContractDiscountTerms("Service Object No.", 0, "Service Package", '');
                                                              END;

                                                   OnLookup=VAR
                                                              ServicePackage@1100526200 : Code[20];
                                                            BEGIN
                                                              //mg, 14-01-11: M24715
                                                              CALCFIELDS("Standard Object No.");
                                                              ServicePackage := "Service Package";
                                                              IF ServiceObjectManagementCU.LookupServicePackageStdObj("Standard Object No.", ServicePackage) THEN
                                                                VALIDATE("Service Package", ServicePackage);
                                                            END;

                                                   CaptionML=[ENU=Service Package;
                                                              NOR=Servicepakke;
                                                              SVE=Servicepaket] }
    { 30  ;   ;Standard Object No. ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Standard Object No." WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Standard Object No.;
                                                              NOR=Standardobjektnr;
                                                              SVE=Standardobjektsnr];
                                                   Editable=No }
    { 40  ;   ;Standard Object Description;Text100;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object".Description WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=[ENU=Standard Object Description;
                                                              NOR=Standard objektsbeskrivelse;
                                                              SVE=Standardobjektsbeskrivning];
                                                   Editable=No }
    { 42  ;   ;Standard Object Description 2;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object"."Description 2" WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=[ENU=Standard Object Description 2;
                                                              SVE=Standardobjektsbeskrivning 2];
                                                   Editable=No }
    { 45  ;   ;Brand Code          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Brand Code" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=ENU=Brand Code;
                                                   Editable=No }
    { 50  ;   ;Brand               ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Brand".Description WHERE (Code=FIELD(Brand Code)));
                                                   CaptionML=[ENU=Brand;
                                                              NOR=Varemerke;
                                                              SVE=M„rke];
                                                   Editable=No }
    { 60  ;   ;Service Object Description;Text100 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Object Description;
                                                              NOR=Objektbeskrivelse;
                                                              SVE=Objektbeskrivning];
                                                   Editable=No }
    { 65  ;   ;Service Object Description 2;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Description 2" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=ENU=Object Description 2;
                                                   Editable=No }
    { 70  ;   ;Service Location No.;Code20        ;TableRelation="Service Location";
                                                   OnValidate=VAR
                                                                ContractObjectMaintenance@1100525000 : Record 11071692;
                                                                ServiceContractArrangement@1100528600 : Record 11071705;
                                                              BEGIN
                                                                ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                ContractObjectMaintenance.SETRANGE("Object No.", "Service Object No.");
                                                                IF ContractObjectMaintenance.FINDSET THEN
                                                                  REPEAT
                                                                    ContractObjectMaintenance.VALIDATE("Service Location No.", "Service Location No.");
                                                                    ContractObjectMaintenance.MODIFY;
                                                                   UNTIL ContractObjectMaintenance.NEXT = 0;

                                                                ServiceContractArrangement.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                ServiceContractArrangement.SETRANGE("Object No.", "Service Object No.");
                                                                ServiceContractArrangement.SETRANGE("Service Order No.", '');
                                                                IF ServiceContractArrangement.FINDSET THEN
                                                                  REPEAT
                                                                    ServiceContractArrangement.VALIDATE("Service Location No.", "Service Location No.");
                                                                    ServiceContractArrangement.MODIFY;
                                                                   UNTIL ServiceContractArrangement.NEXT = 0;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceLocation@1100528600 : Record 11012801;
                                                            BEGIN
                                                              IF ServiceLocation.GET("Service Location No.") THEN;
                                                              PAGE.RUNMODAL(0, ServiceLocation);
                                                            END;

                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr];
                                                   Editable=No }
    { 80  ;   ;Object Group        ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object"."Object Group" WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=[ENU=Object Group;
                                                              NOR=Objektgruppe;
                                                              SVE=Objektgrupp];
                                                   Editable=No }
    { 90  ;   ;Starting Date       ;Date          ;OnValidate=VAR
                                                                ServiceContract@1100528600 : Record 11012812;
                                                              BEGIN
                                                                IF "Starting Date" <> 0D THEN
                                                                  IF ("Ending Date" <> 0D) AND ("Starting Date" > "Ending Date") THEN
                                                                    FIELDERROR("Starting Date", STRSUBSTNO(Text000, FIELDCAPTION("Ending Date")));
                                                                IF "Invoice From" = 0D THEN BEGIN
                                                                  "Invoice From" := "Starting Date";
                                                                  IF NOT ServiceContract.GET("Service Contract No.") THEN
                                                                    ServiceContract.INIT;
                                                                  IF ServiceContract."Invoice Type" = ServiceContract."Invoice Type"::Dynamic THEN
                                                                    IF "Invoice From" < ServiceContract."Invoiced Until" THEN
                                                                      "Invoice From" := ServiceContract."Invoiced Until" +1;
                                                                END;
                                                                UpdateMaintenanceDates;
                                                                IF CurrFieldNo = FIELDNO("Starting Date") THEN
                                                                  HandleSMAgreement(TRUE, TRUE);
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 100 ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                ValidateEndingDate;
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 110 ;   ;Invoice From        ;Date          ;CaptionML=[ENU=Invoice From;
                                                              NOR=Faktura fra;
                                                              SVE=Faktura fr†n] }
    { 120 ;   ;Invoiced Until      ;Date          ;OnValidate=BEGIN
                                                                IF GUIALLOWED AND
                                                                   (CurrFieldNo = FIELDNO("Invoiced Until")) AND
                                                                   ("Invoiced Until" <> xRec."Invoiced Until")
                                                                THEN BEGIN
                                                                  IF (NOT CONFIRM(Text009, FALSE, FIELDCAPTION("Invoiced Until"), xRec."Invoiced Until", "Invoiced Until")) THEN
                                                                    "Invoiced Until" := xRec."Invoiced Until";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Invoiced Until;
                                                              NOR=Fakturert til;
                                                              SVE=Fakturerad till] }
    { 125 ;   ;Last Invoiced Until ;Date          ;CaptionML=[ENU=Last Invoiced Until;
                                                              NOR=Sist fakturert frem til;
                                                              SVE=Senast fakturerad till];
                                                   Editable=No }
    { 130 ;   ;Price Agreement     ;Decimal       ;CaptionML=[ENU=Price Agreement;
                                                              NOR=Prisavtale;
                                                              SVE=Prisavtal] }
    { 140 ;   ;Customer Object Group;Code10       ;TableRelation="Service Group".No. WHERE (Customer No.=FIELD(Customer No.));
                                                   CaptionML=[ENU=Customer Object Group;
                                                              NOR=Opjektgruppe for kunde;
                                                              SVE=Kundobjektsgrupp] }
    { 150 ;   ;Customer No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Contract."Customer No." WHERE (No.=FIELD(Service Contract No.)));
                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr];
                                                   Editable=No }
    { 160 ;   ;Service Contract Status;Option     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Contract.Status WHERE (No.=FIELD(Service Contract No.)));
                                                   CaptionML=[ENU=Service Contract Status;
                                                              NOR=Servicekontraktstatus;
                                                              SVE=Servicekontraktsstatus];
                                                   OptionCaptionML=[ENU=Applied,Offered,Order,Invoicing Allowed,Expired,Closed;
                                                                    NOR=Anvendt,Tilbudt,Ordre,Fakturering Tillatt,Utg†tt;
                                                                    SVE=Till„mpad,Anbudsgiven,Order,Fakturering till†ten,Utg†ngen,St„ngd];
                                                   OptionString=Applied,Offered,Order,Invoicing Allowed,Expired,Closed;
                                                   Editable=No }
    { 170 ;   ;Service Location Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Service Location Description;
                                                              NOR=Servicelokasjonbeskrivelse;
                                                              SVE=Serviceplatsbeskrivning];
                                                   Editable=No }
    { 180 ;   ;Address             ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Address WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress];
                                                   Editable=No }
    { 190 ;   ;Address 2           ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Address 2" WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2;
                                                              SVE=Adress 2];
                                                   Editable=No }
    { 200 ;   ;City                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".City WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=City;
                                                              NOR=Sted;
                                                              SVE=Ort];
                                                   Editable=No }
    { 210 ;   ;County              ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".County WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=County;
                                                              NOR=Fylke/delstat;
                                                              SVE=Delstat];
                                                   Editable=No }
    { 220 ;   ;Post Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Post Code" WHERE (No.=FIELD(Service Location No.)));
                                                   TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr;
                                                              SVE=Postnr];
                                                   Editable=No }
    { 230 ;   ;Invoice Interval    ;DateFormula   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Contract."Invoice Interval" WHERE (No.=FIELD(Service Contract No.)));
                                                   CaptionML=[ENU=Invoice Interval;
                                                              NOR=Faktureringsintervall;
                                                              SVE=Faktureringsintervall];
                                                   Editable=No }
    { 240 ;   ;Quantity            ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Quantity WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   Editable=No }
    { 250 ;   ;Service Contract Blocked;Boolean   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Contract.Blocked WHERE (No.=FIELD(Service Contract No.)));
                                                   CaptionML=[ENU=Service Contract Blocked;
                                                              NOR=Servicekontrakt blokkert;
                                                              SVE=Servicekontrakt blockerat];
                                                   Editable=No }
    { 260 ;   ;Subcontracting      ;Boolean       ;OnValidate=BEGIN
                                                                FillDefaultMaintScheme;
                                                              END;

                                                   CaptionML=[ENU=Subcontracting;
                                                              NOR=Underentrepen›r;
                                                              SVE=UE] }
    { 270 ;   ;Blocked             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Blocked WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Blocked;
                                                              NOR=Blokkert;
                                                              SVE=Sp„rrad];
                                                   Editable=No }
    { 280 ;   ;Plan Method         ;Option        ;OnValidate=VAR
                                                                ContractObjectMaintenance@1100525000 : Record 11071692;
                                                              BEGIN
                                                                IF "Plan Method" = "Plan Method"::"Theoretical Maintenance Date" THEN BEGIN
                                                                  ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                  ContractObjectMaintenance.SETRANGE("Object No.", "Service Object No.");
                                                                  ContractObjectMaintenance.SETFILTER("Linked to Maintenance Scheme", '<>%1', '');
                                                                  IF ContractObjectMaintenance.FINDFIRST THEN
                                                                    ContractObjectMaintenance.TESTFIELD("Linked to Maintenance Scheme", '');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Plan Method;
                                                              NOR=Planlegg Metode;
                                                              SVE=Planmetod];
                                                   OptionCaptionML=[ENU=Theoretical Maintenance Date,Last Execution Date;
                                                                    NOR=Teoretisk Vedlikeholdsdato, Siste Gjennomf›ringsdato];
                                                   OptionString=Theoretical Maintenance Date,Last Execution Date }
    { 290 ;   ;Customer Feature    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Customer Feature" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Customer Feature;
                                                              NOR=Kundefunksjon;
                                                              SVE=Kundfunktion];
                                                   Editable=No }
    { 300 ;   ;Object Customer     ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Object Customer" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Object Customer;
                                                              NOR=Objektkunde;
                                                              SVE=Objektkund];
                                                   Editable=No }
    { 310 ;   ;S&M Agreement Level ;Option        ;CaptionML=ENU=S&M Agreement Level;
                                                   OptionCaptionML=ENU=" ,Customer Price Group,Customer,,Contract,,Object";
                                                   OptionString=[ ,Customer Price Group,Customer,,Contract,,Object] }
    { 320 ;   ;Status              ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Status WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   Editable=No }
    { 330 ;   ;Service Category (Call);Code10     ;TableRelation="Service Category".Code WHERE (Service Contract No.=FIELD(Service Contract No.));
                                                   CaptionML=ENU=Service Category (Call) }
    { 340 ;   ;Service Category (Direct);Code10   ;TableRelation="Service Category".Code WHERE (Service Contract No.=FIELD(Service Contract No.));
                                                   CaptionML=ENU=Service Category (Direct) }
    { 350 ;   ;Contract Group      ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Contract."Contract Group" WHERE (No.=FIELD(Service Contract No.)));
                                                   CaptionML=[ENU=Contract Group;
                                                              NOR=Kontraktgruppe;
                                                              SVE=Kontraktsgrupp];
                                                   Editable=No }
    { 360 ;   ;Last Maint. Date (Conversion);Date ;CaptionML=ENU=Last Maintenance Date (Conversion);
                                                   Editable=No }
    { 370 ;   ;Building            ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Building WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Building;
                                                              NOR=Byggning;
                                                              SVE=Byggnad];
                                                   Editable=No }
    { 380 ;   ;Floor               ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Floor WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Floor;
                                                              NOR=Gulv;
                                                              SVE=V†ning];
                                                   Editable=No }
    { 390 ;   ;Room                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Room WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Room;
                                                              NOR=Rom;
                                                              SVE=Rum];
                                                   Editable=No }
    { 400 ;   ;Index Starting Date ;Date          ;OnValidate=BEGIN
                                                                IF "Index Starting Date" <> 0D THEN
                                                                  TestServiceContractInvoiceMethodDynamic;
                                                              END;

                                                   CaptionML=ENU=Index Starting Date }
    { 410 ;   ;Index Interval      ;DateFormula   ;OnValidate=BEGIN
                                                                IF FORMAT("Index Interval") <> '' THEN BEGIN
                                                                  TestServiceContractInvoiceMethodDynamic;
                                                                  IF CALCDATE("Index Interval", TODAY) <= TODAY THEN
                                                                    ERROR(Text013, FIELDCAPTION("Index Interval"));
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Index Interval }
    { 420 ;   ;Last Index Date     ;Date          ;OnValidate=BEGIN
                                                                IF "Last Index Date" <> 0D THEN
                                                                  TestServiceContractInvoiceMethodDynamic
                                                              END;

                                                   CaptionML=[ENU=Last Index Date;
                                                              NOR=Siste indeksdato;
                                                              SVE=Senaste indexdatum] }
    { 430 ;   ;Service Index Method;Code10        ;TableRelation="Service Index Method";
                                                   CaptionML=[ENU=Service Index Method;
                                                              NOR=Indeksmetode for service;
                                                              SVE=Serviceindexmetod] }
    { 440 ;   ;Service Category (Contract);Code10 ;TableRelation="Service Category".Code WHERE (Service Contract No.=FIELD(Service Contract No.));
                                                   OnValidate=VAR
                                                                ContractObjectMaintenance@1100529600 : Record 11071692;
                                                              BEGIN
                                                                ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                ContractObjectMaintenance.SETRANGE("Object No.", "Service Object No.");
                                                                IF NOT ContractObjectMaintenance.ISEMPTY THEN
                                                                  ContractObjectMaintenance.MODIFYALL("Service Category", "Service Category (Contract)");
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Service Category (Contract) }
  }
  KEYS
  {
    {    ;Service Contract No.,Service Object No. ;Clustered=Yes }
    {    ;Service Object No.,Service Contract No.  }
    {    ;Service Contract No.,Service Location No. }
    {    ;Service Category (Contract)              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100526200 : TextConst 'ENU=must occur before %1;NOR=m† inntreffe f›r %1;SVE=m†ste intr„ffa f”re %1';
      Text001@1100528602 : TextConst 'ENU=must occur after %1';
      Text003@1100526203 : TextConst 'ENU=Update Maintenance Schemes for Object %1 (Contract %2) with Maintenance Scheme of Standard Object?;SVE=Ta bort befintliga underh†llsscheman f”r objekt %1, servicepaket %2?';
      ServiceObjectManagementCU@1100526206 : Codeunit 11012808;
      MaintenanceManagementCU@1100526205 : Codeunit 11012829;
      Text005@1100526207 : TextConst 'ENU=Object already active at contract %1;NOR=Objektet er allerede aktivt p† kontrakt %1;SVE=Objektet „r redan aktivt p† kontrakt %1';
      Text006@1100526208 : TextConst 'ENU=" with ending date %2";NOR=med sluttdato %2;SVE=" med slutdatum %2"';
      Text007@1100528600 : TextConst 'ENU=must occur on or before Ending Date of Contract';
      DoNotCreateMaintenanceLines@1100525000 : Boolean;
      Text009@1100409000 : TextConst 'ENU=Are you sure you want to change the %1 from %2 into %3?;NOR=Er du sikker p† at du ikke vil endre %1 fra %2 til %3?;SVE=Žr du s„ker p† att du vill „ndra %1 fr†n %2 till %3?';
      Text010@1100528601 : TextConst 'ENU=must occur on or before Ending Date of Service Object';
      DoNotConfirmMaintenanceSchemeCreation@1100528000 : Boolean;
      Text011@1100525001 : TextConst 'ENU=Maintenance Scheme %1 of Object %2 contains Additional Activities, these will be deleted, continue?';
      Text012@1100525002 : TextConst 'ENU=Cannot rename object %1, there are %2 present';
      Text013@1100528900 : TextConst 'ENU=%1 must have a positive value';
      DoNotUpdateServicePackageOnServiceContract@1100528603 : Boolean;
      StartingDateMaintenance@1100529600 : Date;

    PROCEDURE FillDefaultMaintScheme@1100526200();
    VAR
      StdObjectMaintenanceScheme@1100526200 : Record 11071703;
      ContractObjectMaintenance@1100526206 : Record 11071692;
      ServiceContract@1100528601 : Record 11012812;
      StandardObject@1100525000 : Record 11020334;
      MaintenanceSchemeVersion@1100525001 : Record 11071743;
      ServiceObject@1100528602 : Record 11071691;
      LineNo@1100528600 : Integer;
      UpdateMaintenanceSchemes@1100528000 : Boolean;
    BEGIN
      IF DoNotCreateMaintenanceLines THEN
        EXIT;

      CALCFIELDS("Standard Object No.");
      IF StandardObject.GET("Standard Object No.") THEN
        IF NOT StandardObject."Copy Maintenance Schemes" THEN
          EXIT;
      IF ServiceObject.GET("Service Object No.") THEN
        IF ServiceObject."Quantity Based on" = ServiceObject."Quantity Based on"::"Contract Object" THEN
          EXIT;
      IF ServiceContract.GET("Service Contract No.") THEN
        IF (ServiceContract."Contract Form" = ServiceContract."Contract Form"::"Framework Contract") OR
           (ServiceContract."Contract Form" = ServiceContract."Contract Form"::"Location Contract") THEN
          EXIT;
      ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
      ContractObjectMaintenance.SETRANGE("Object No.", "Service Object No.");
      IF NOT GUIALLOWED OR DoNotConfirmMaintenanceSchemeCreation OR ContractObjectMaintenance.ISEMPTY THEN
        UpdateMaintenanceSchemes := TRUE
      ELSE
        UpdateMaintenanceSchemes := CONFIRM(Text003, TRUE, "Service Object No.", "Service Contract No.");
      IF UpdateMaintenanceSchemes THEN BEGIN
        IF ContractObjectMaintenance.FINDSET(TRUE) THEN
          REPEAT
            StdObjectMaintenanceScheme.SETRANGE("Object No.", "Standard Object No.");
            StdObjectMaintenanceScheme.SETFILTER("Service Package", '%1|%2', '', "Service Package");
            StdObjectMaintenanceScheme.SETRANGE(Subcontracting, Subcontracting);
            StdObjectMaintenanceScheme.SETRANGE("Maintenance Scheme", ContractObjectMaintenance."Maintenance Scheme");
            IF StdObjectMaintenanceScheme.ISEMPTY THEN BEGIN
              IF GUIALLOWED AND NOT DoNotConfirmMaintenanceSchemeCreation THEN BEGIN
                MaintenanceSchemeVersion.SETRANGE("Line Type", MaintenanceSchemeVersion."Line Type"::Object);
                MaintenanceSchemeVersion.SETRANGE(Code, ContractObjectMaintenance."Object No.");
                MaintenanceSchemeVersion.SETRANGE("Belongs to Scheme", ContractObjectMaintenance."Maintenance Scheme");
                IF NOT MaintenanceSchemeVersion.ISEMPTY THEN
                  IF NOT CONFIRM(Text011, TRUE, ContractObjectMaintenance."Maintenance Scheme", ContractObjectMaintenance."Object No.") THEN
                    ERROR('');
              END;
              ContractObjectMaintenance.SetSuppressMsgNonDeletedSCArr(TRUE);
              ContractObjectMaintenance.DELETE(TRUE);
            END;
          UNTIL ContractObjectMaintenance.NEXT = 0;

        StdObjectMaintenanceScheme.RESET;
        StdObjectMaintenanceScheme.SETRANGE("Object No.", "Standard Object No.");
        StdObjectMaintenanceScheme.SETFILTER("Service Package", '%1|%2', '', "Service Package");
        StdObjectMaintenanceScheme.SETRANGE(Subcontracting, Subcontracting);
        IF StdObjectMaintenanceScheme.FINDSET THEN BEGIN
          LineNo := DetLastLineNo;
          REPEAT
            InsertContractObjectsMaintRec(
              StdObjectMaintenanceScheme."Maintenance Scheme", StdObjectMaintenanceScheme."Service Package", LineNo);
          UNTIL StdObjectMaintenanceScheme.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InsertContractObjectsMaintRec@1100526212(IMaintSchemeCde@1100526203 : Code[20];IServPackageCde@1100526204 : Code[10];VAR ILineNoInt@1100526201 : Integer) : Boolean;
    VAR
      ContractObjectsMaintenance@1100526200 : Record 11071692;
    BEGIN
      ContractObjectsMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
      ContractObjectsMaintenance.SETRANGE("Object No.", "Service Object No.");
      ContractObjectsMaintenance.SETRANGE("Maintenance Scheme", IMaintSchemeCde);
      IF ContractObjectsMaintenance.ISEMPTY THEN BEGIN
        ContractObjectsMaintenance.INIT;
        ContractObjectsMaintenance."Service Contract No." := "Service Contract No.";
        ContractObjectsMaintenance."Line No." := ILineNoInt;
        ILineNoInt += 10000;
        ContractObjectsMaintenance."Object No." := "Service Object No.";
        ContractObjectsMaintenance.VALIDATE("Service Location No.", "Service Location No.");
        ContractObjectsMaintenance.Subcontracting := Subcontracting;
        ContractObjectsMaintenance.CALCFIELDS("Standard Object No.", "Object Group");
        ContractObjectsMaintenance."Service Package" := IServPackageCde;
        ContractObjectsMaintenance.VALIDATE("Maintenance Scheme", IMaintSchemeCde);
        ContractObjectsMaintenance.DetermineStartingDateMaintenanceOnOtherMaintenanceScheme;
        IF StartingDateMaintenance <> 0D THEN
          ContractObjectsMaintenance.VALIDATE("Starting Date Maintenance", StartingDateMaintenance);
        IF ContractObjectsMaintenance."Starting Date Maintenance" = 0D THEN
          ContractObjectsMaintenance.DetermineStartingDateMaintenanceByLastMaintDateConversion;
        IF (ContractObjectsMaintenance."Starting Date Maintenance" = 0D) AND ("Starting Date" <> 0D) THEN
          ContractObjectsMaintenance.VALIDATE("Starting Date Maintenance", "Starting Date");
        ContractObjectsMaintenance.VALIDATE("Object No.", "Service Object No.");
        ContractObjectsMaintenance."Service Category" := "Service Category (Contract)";
        ContractObjectsMaintenance.DetermineStartingDateMaintenanceOnOtherMaintenanceScheme;
        ContractObjectsMaintenance.INSERT(TRUE);
        EXIT(TRUE);
      END;
    END;

    PROCEDURE DetLastLineNo@1100526213() lvLineNoInt : Integer;
    VAR
      lvContrObjMaintRec@1100526200 : Record 11071692;
    BEGIN
      lvContrObjMaintRec.SETRANGE("Service Contract No.","Service Contract No.");
      IF lvContrObjMaintRec.FINDLAST THEN
        EXIT(lvContrObjMaintRec."Line No." + 10000)
      ELSE
        EXIT(10000);
    END;

    PROCEDURE UpdateMaintenanceDates@1100526202();
    VAR
      lvContrObjMaintRec@1100526200 : Record 11071692;
    BEGIN
      lvContrObjMaintRec.SETRANGE("Service Contract No.","Service Contract No.");
      lvContrObjMaintRec.SETRANGE("Object No.","Service Object No.");
      IF lvContrObjMaintRec.FINDSET THEN BEGIN
        REPEAT
          IF lvContrObjMaintRec."Next Maintenance Date" = 0D THEN
            lvContrObjMaintRec.VALIDATE("Next Maintenance Date","Starting Date");
          IF lvContrObjMaintRec."Starting Date Maintenance" = 0D THEN
            lvContrObjMaintRec.VALIDATE("Starting Date Maintenance","Starting Date");
          lvContrObjMaintRec.MODIFY;
        UNTIL lvContrObjMaintRec.NEXT = 0;
      END;
    END;

    PROCEDURE HasOverlapConflicts@1100526201() : Boolean;
    VAR
      ContractObject@1100526200 : Record 11071702;
      OrigContractObjEndingDate@1100526202 : Date;
      ContractObjEndingDate@1100526203 : Date;
    BEGIN
      CALCFIELDS("Service Contract Status");
      IF NOT ("Service Contract Status" IN [
        "Service Contract Status"::Order,
        "Service Contract Status"::"Invoicing Allowed"]) THEN
        EXIT;

      ContractObject.SETCURRENTKEY("Service Object No.", "Service Contract No.");
      ContractObject.SETRANGE("Service Object No.", "Service Object No.");
      ContractObject.SETFILTER("Service Contract No.", '<>%1', "Service Contract No.");
      ContractObject.SETFILTER("Service Contract Status", '%1|%2',
        ContractObject."Service Contract Status"::Order,
        ContractObject."Service Contract Status"::"Invoicing Allowed");

      IF NOT ContractObject.FINDFIRST THEN EXIT;

      REPEAT
        CALCFIELDS(Blocked);
        IF NOT Blocked THEN BEGIN
          OrigContractObjEndingDate := GetContractObjectEndingDate;
          ContractObjEndingDate := ContractObject.GetContractObjectEndingDate;
          IF ((OrigContractObjEndingDate = 0D) OR (ContractObject."Starting Date" <= OrigContractObjEndingDate)) AND
             ((ContractObjEndingDate = 0D) OR (ContractObjEndingDate >= "Starting Date")) THEN
            EXIT(TRUE);
        END;
      UNTIL ContractObject.NEXT = 0;
    END;

    PROCEDURE GetContractObjectEndingDate@1100526215() : Date;
    VAR
      ServiceContract@1100526201 : Record 11012812;
    BEGIN
      IF "Ending Date" <> 0D THEN
        EXIT("Ending Date");

      ServiceContract.GET("Service Contract No.");
      EXIT(ServiceContract."Ending Date");
    END;

    PROCEDURE ContractDiscountExists@1100409000(IServicePackage@1100409000 : Code[10]) : Boolean;
    VAR
      ServiceContractDiscountTerm@1100409001 : Record 11012828;
    BEGIN
      ServiceContractDiscountTerm.SETRANGE("Service Contract No.", "Service Contract No.");
      ServiceContractDiscountTerm.SETRANGE("Service Package", IServicePackage);
      EXIT(NOT ServiceContractDiscountTerm.ISEMPTY);
    END;

    PROCEDURE GetPrefServiceLocationNo@1100528301(IWithErrorCheck@1100528300 : Boolean) : Code[20];
    VAR
      ServiceObject@1100528301 : Record 11071691;
    BEGIN
      IF ServiceObject.GET("Service Object No.") THEN
        EXIT(ServiceObject.GetPrefServiceLocationNo(IWithErrorCheck));
    END;

    PROCEDURE SetDoNotCreateMaintenanceLines@1100525000(IDoNotCreateMaintenanceLines@1100525000 : Boolean);
    BEGIN
      DoNotCreateMaintenanceLines := IDoNotCreateMaintenanceLines;
    END;

    PROCEDURE GetObjectInvAmountPerYear@1100525001(ISeekDate@1100525000 : Date) RevenuePerYear : Decimal;
    VAR
      ServiceContract@1100525001 : Record 11012812;
      ContractObject@1100525002 : Record 11071702;
      MaintenanceInvoiceMgt@1100525003 : Codeunit 11012828;
      RecordFound@1100525004 : Boolean;
    BEGIN
      IF NOT ServiceContract.GET("Service Contract No.") THEN
        EXIT;
      IF ContractObject.GET("Service Contract No.", "Service Object No.") THEN
        RevenuePerYear :=
          MaintenanceInvoiceMgt.GetSCObjectPriceLCYAgreemByDat(
            ContractObject."Service Contract No.", ContractObject."Service Object No.", ISeekDate, RecordFound);
        IF NOT RecordFound THEN
          RevenuePerYear := ContractObject.GetMaintSalesRateLCY(ISeekDate);
      EXIT(RevenuePerYear);
    END;

    LOCAL PROCEDURE GetAgreementDS@1100525006(VAR OServiceMaintAgreement@1100525000 : Record 11071973) : Boolean;
    VAR
      ServiceMaintAgreement@1100525001 : Record 11071973;
      Customer@1100525002 : Record 18;
      ServiceObject@1100525006 : Record 11071691;
      ObjectSpecificationMgt@1100525004 : Codeunit 11012848;
      YearOfManufacture@1100525003 : Integer;
      YearOfManufactureDate@1100525005 : Date;
      YearOfManufactureFound@1100525007 : Boolean;
      AgePeriod@1100525008 : DateFormula;
      DummyPeriod@1100525009 : DateFormula;
    BEGIN
      OServiceMaintAgreement.RESET;
      OServiceMaintAgreement.MARKEDONLY(TRUE);

      IF "S&M Agreement Level" = "S&M Agreement Level"::Object THEN
        EXIT;
      CALCFIELDS("Object Group", "Contract Group");
      IF "Object Group" = '' THEN
        EXIT;

      ServiceMaintAgreement.SETRANGE(Type, ServiceMaintAgreement.Type::Contract);
      ServiceMaintAgreement.SETRANGE(Code, "Service Contract No.");
      IF ServiceMaintAgreement.ISEMPTY THEN BEGIN
        CALCFIELDS("Customer No.");
        ServiceMaintAgreement.SETRANGE(Type, ServiceMaintAgreement.Type::Customer);
        ServiceMaintAgreement.SETRANGE(Code, "Customer No.");
      END;
      IF ServiceMaintAgreement.ISEMPTY THEN BEGIN
        IF Customer.GET("Customer No.") THEN BEGIN
          ServiceMaintAgreement.SETRANGE(Type, ServiceMaintAgreement.Type::"Customer Price Group");
          ServiceMaintAgreement.SETRANGE(Code, Customer."Customer Price Group");
        END;
      END;
      IF ServiceMaintAgreement.ISEMPTY THEN
        EXIT;

      IF EVALUATE(YearOfManufacture, ObjectSpecificationMgt.GetYearOfManufactureValue("Service Object No.")) THEN
        YearOfManufactureDate := DMY2DATE(1, 1, YearOfManufacture)
      ELSE
        YearOfManufactureDate := TODAY;

      ServiceMaintAgreement.SETFILTER("Contract Group", '%1|%2', '', "Contract Group");
      IF ServiceMaintAgreement.FINDSET THEN
        REPEAT
          ServiceObject.RESET;
          ServiceObject.SETRANGE("No.", "Service Object No.");
          IF ServiceMaintAgreement."Object Group" <> '' THEN
            ServiceObject.SETRANGE("Object Group", ServiceMaintAgreement."Object Group");
          IF ServiceMaintAgreement."Standard Object Filter" <> '' THEN
            ServiceObject.SETFILTER("Standard Object No.", ServiceMaintAgreement."Standard Object Filter");
          IF ServiceMaintAgreement."Object Brand Code Filter" <> '' THEN
            ServiceObject.SETFILTER(ServiceObject."Brand Code", ServiceMaintAgreement."Object Brand Code Filter");
          IF ServiceMaintAgreement."Object Model Code Filter" <> '' THEN
            ServiceObject.SETFILTER(ServiceObject."Model Code", ServiceMaintAgreement."Object Model Code Filter");
          IF ServiceMaintAgreement."Object Type Filter" <> '' THEN
            ServiceObject.SETFILTER("Type Description", ServiceMaintAgreement."Object Type Filter");
          IF ServiceMaintAgreement.Discipline <> '' THEN
            ServiceObject.SETRANGE(Discipline, ServiceMaintAgreement.Discipline);
          IF ServiceMaintAgreement."Trade Speciality" <> '' THEN
            ServiceObject.SETRANGE("Trade Speciality", ServiceMaintAgreement."Trade Speciality");
          YearOfManufactureFound := FALSE;
          IF (FORMAT(ServiceMaintAgreement."Age Period") <> '') AND (YearOfManufactureDate <> 0D) THEN BEGIN
            IF CALCDATE(ServiceMaintAgreement."Age Period", TODAY) < TODAY THEN BEGIN
              IF YearOfManufactureDate > CALCDATE(ServiceMaintAgreement."Age Period", TODAY) THEN
                YearOfManufactureFound := TRUE;
            END ELSE BEGIN
              NegateDateFormula(ServiceMaintAgreement."Age Period", AgePeriod);
              IF YearOfManufactureDate <= CALCDATE(AgePeriod, TODAY) THEN
                YearOfManufactureFound := TRUE;
            END;
          END ELSE
            YearOfManufactureFound := TRUE;
          IF NOT ServiceObject.ISEMPTY AND YearOfManufactureFound THEN
            ServiceMaintAgreement.MARK(TRUE);

        UNTIL ServiceMaintAgreement.NEXT = 0;
      ServiceMaintAgreement.MARKEDONLY(TRUE);
      ServiceMaintAgreement.SETFILTER("Age Period", '<>%1', DummyPeriod);
      IF ServiceMaintAgreement.ISEMPTY THEN
        ServiceMaintAgreement.SETRANGE("Age Period");
      ServiceMaintAgreement.SETCURRENTKEY(Type, Code, "Standard Object Filter");
      OServiceMaintAgreement.COPY(ServiceMaintAgreement);
    END;

    PROCEDURE DetermineAgreement@1100528600(VAR OServiceMaintAgreement@1100528600 : Record 11071973) : Boolean;
    VAR
      ServiceMaintAgreement@1100528601 : Record 11071973;
    BEGIN
      GetAgreementDS(ServiceMaintAgreement);
      IF ServiceMaintAgreement.ISEMPTY THEN
        EXIT(FALSE);
      ServiceMaintAgreement.FINDLAST;
      OServiceMaintAgreement := ServiceMaintAgreement;
      EXIT(TRUE);
    END;

    PROCEDURE ShowAgreementLines@1100528603();
    VAR
      ServiceMaintAgreement@1100528600 : Record 11071973;
      ServiceMaintAgreementsPAGE@1100525000 : Page 11126460;
    BEGIN
      GetAgreementDS(ServiceMaintAgreement);
      ServiceMaintAgreementsPAGE.EDITABLE(FALSE);
      ServiceMaintAgreementsPAGE.SETTABLEVIEW(ServiceMaintAgreement);
      ServiceMaintAgreementsPAGE.RUNMODAL;
    END;

    PROCEDURE HandleSMAgreement@1100528604(IDeleteMaintSchemes@1100528604 : Boolean;IShowMessages@1100528608 : Boolean);
    VAR
      ServiceMaintAgreement@1100528600 : Record 11071973;
      SMAgreementMaintScheme@1100528601 : Record 11071975;
      ContractObjectMaintenance@1100528602 : Record 11071692;
      LineNo@1100528603 : Integer;
      IsInserted@1100528607 : Boolean;
    BEGIN
      IF "S&M Agreement Level" <> "S&M Agreement Level"::Object THEN
        "S&M Agreement Level" := "S&M Agreement Level"::" ";
      IF NOT DetermineAgreement(ServiceMaintAgreement) THEN
        EXIT;
      IF ("Ending Date" <> 0D) AND ("Ending Date" < TODAY) THEN
        EXIT;
      "S&M Agreement Level" := ServiceMaintAgreement.Type +1;
      IF (ServiceMaintAgreement."Service Package" <> '') AND ("Service Package" <> ServiceMaintAgreement."Service Package") THEN BEGIN
        DoNotConfirmMaintenanceSchemeCreation := TRUE;
        VALIDATE("Service Package", ServiceMaintAgreement."Service Package");
        DoNotConfirmMaintenanceSchemeCreation := FALSE;
      END;

      SMAgreementMaintScheme.SETRANGE(Type, ServiceMaintAgreement.Type);
      SMAgreementMaintScheme.SETRANGE(Code, ServiceMaintAgreement.Code);
      SMAgreementMaintScheme.SETRANGE("Agreement Line No.", ServiceMaintAgreement."Line No.");
      SMAgreementMaintScheme.SETFILTER("Index Date", '<=%1', TODAY);
      SMAgreementMaintScheme.SETFILTER("Ending Date", '%1|>=%2', 0D, TODAY);
      IF NOT SMAgreementMaintScheme.ISEMPTY THEN BEGIN
        IF IDeleteMaintSchemes THEN BEGIN
          ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
          ContractObjectMaintenance.SETRANGE("Object No.", "Service Object No.");
          ContractObjectMaintenance.SetSuppressMsgNonDeletedSCArr(NOT IShowMessages);
          IF ContractObjectMaintenance.FINDSET THEN
            REPEAT
              SMAgreementMaintScheme.SETRANGE("Maintenance Scheme", ContractObjectMaintenance."Maintenance Scheme");
              IF SMAgreementMaintScheme.ISEMPTY THEN
                ContractObjectMaintenance.DELETE(TRUE);
            UNTIL ContractObjectMaintenance.NEXT = 0;
          SMAgreementMaintScheme.SETRANGE("Maintenance Scheme");
        END;
      END;

      LineNo := 10000;
      IF SMAgreementMaintScheme.FINDSET THEN
        REPEAT
          IsInserted := InsertContractObjectsMaintRec(SMAgreementMaintScheme."Maintenance Scheme", ServiceMaintAgreement."Service Package", LineNo);
          IF ContractObjectMaintenance.GET("Service Contract No.", "Service Object No.", SMAgreementMaintScheme."Maintenance Scheme") THEN
            IF (FORMAT(SMAgreementMaintScheme."Maintenance Interval") <> '') AND
               (SMAgreementMaintScheme."Maintenance Interval" <> ContractObjectMaintenance."Maintenance Interval")
            THEN
              ContractObjectMaintenance.VALIDATE("Maintenance Interval", SMAgreementMaintScheme."Maintenance Interval");
            IF IsInserted THEN BEGIN
              ContractObjectMaintenance."Starting Date Maintenance" := 0D;
              ContractObjectMaintenance.DetermineStartingDateMaintenanceOnOtherMaintenanceScheme;
              IF ContractObjectMaintenance."Starting Date Maintenance" = 0D THEN
                ContractObjectMaintenance.DetermineStartingDateMaintenanceByLastMaintDateConversion;
              IF ContractObjectMaintenance."Starting Date Maintenance" = 0D THEN
                ContractObjectMaintenance.VALIDATE("Starting Date Maintenance", "Starting Date");
            END;
            ContractObjectMaintenance.MODIFY(TRUE);
        UNTIL SMAgreementMaintScheme.NEXT = 0;
    END;

    PROCEDURE GetMaintSalesRateLCY@1100528601(IIndexDate@1100528600 : Date) Rate : Decimal;
    VAR
      ServiceContract@1100528605 : Record 11012812;
      ServiceMaintAgreement@1100528604 : Record 11071973;
      GetMaintenanceSalesAndCost@1100528602 : Codeunit 11012827;
      PriceFound@1100528603 : Boolean;
    BEGIN
      IF IIndexDate = 0D THEN BEGIN
        IF ServiceContract.GET("Service Contract No.") AND (ServiceContract."Invoice Type" = ServiceContract."Invoice Type"::Dynamic) THEN BEGIN
          CALCFIELDS("Invoice Interval");
          IF ("Invoiced Until" <> 0D) AND (FORMAT("Invoice Interval") <> '')THEN
            IIndexDate := CALCDATE("Invoice Interval", "Invoiced Until") +1
          ELSE
            IIndexDate := "Invoice From";
          IF ("Ending Date" <> 0D) AND (IIndexDate > "Ending Date") THEN
            IIndexDate := "Ending Date";
        END;
      END;
      IF IIndexDate = 0D THEN
        IIndexDate := TODAY();

      IF DetermineAgreement(ServiceMaintAgreement) THEN
        Rate := ServiceMaintAgreement.GetServiceRate(IIndexDate, PriceFound);
      IF NOT PriceFound THEN BEGIN
        CALCFIELDS("Standard Object No.");
        CALCFIELDS("Object Group", "Customer No.");
        Rate := GetMaintenanceSalesAndCost.GetRate("Customer No.", IIndexDate, "Object Group", "Customer Object Group", "Service Package");
      END;
    END;

    PROCEDURE GetMaintSalesRate@1100528602(IIndexDate@1100528600 : Date) Rate : Decimal;
    VAR
      ServiceContract@1100528605 : Record 11012812;
      GetMaintenanceSalesAndCost@1100528601 : Codeunit 11012827;
    BEGIN
      Rate := GetMaintSalesRateLCY(IIndexDate);
      IF NOT ServiceContract.GET("Service Contract No.") THEN
        ServiceContract.INIT;
      Rate := GetMaintenanceSalesAndCost.CalcServiceRateFromLCY(Rate, ServiceContract."Currency Code");
    END;

    PROCEDURE GetPreferredBuildingCode@1100528605() : Code[20];
    VAR
      ServiceObject@1100528600 : Record 11071691;
    BEGIN
      IF ServiceObject.GET("Service Object No.") THEN
        EXIT(ServiceObject.GetPreferredBuildingCode);
    END;

    PROCEDURE NegateDateFormula@1100525005(IDateFormula@1100525000 : DateFormula;VAR ODateFormula@1100525001 : DateFormula);
    VAR
      DateFormulaText@1100525002 : Text;
    BEGIN
      DateFormulaText := FORMAT(IDateFormula);
      IF DateFormulaText = '' THEN
        EXIT;
      DateFormulaText := DELCHR(DateFormulaText, '<', '+-');
      EVALUATE(ODateFormula, STRSUBSTNO('-%1', DateFormulaText));
    END;

    PROCEDURE ShowContractArrangements@1100528606();
    VAR
      ServiceContractArrangement@1100528600 : Record 11071705;
    BEGIN
      ServiceContractArrangement.SETCURRENTKEY(
        "Service Contract No.", "Object No.", "Maintenance Scheme", "Maintenance Year", "Maintenance Week", "Maintenance Day");
      ServiceContractArrangement.FILTERGROUP(7);
      ServiceContractArrangement.SETRANGE("Service Contract No.", "Service Contract No.");
      ServiceContractArrangement.SETRANGE("Object No.", "Service Object No.");
      ServiceContractArrangement.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Service Contract Arrangements", ServiceContractArrangement);
    END;

    LOCAL PROCEDURE ValidateEndingDate@1100528607();
    VAR
      ServiceContract@1100528601 : Record 11012812;
      ServiceObject@1100528600 : Record 11071691;
      MaintenanceProposalLine@1100528602 : Record 11071696;
    BEGIN
      ServiceContract.GET("Service Contract No.");
      IF "Ending Date" <> 0D THEN BEGIN
        TESTFIELD("Starting Date");
        IF "Starting Date" > "Ending Date" THEN
          FIELDERROR("Ending Date", STRSUBSTNO(Text001, FIELDCAPTION("Starting Date")));
        IF (ServiceContract."Ending Date" <> 0D) AND ("Ending Date" > ServiceContract."Ending Date") THEN
          FIELDERROR("Ending Date", Text007);
        IF ServiceObject.GET("Service Object No.") THEN
          IF ServiceObject."Ending Date" <> 0D THEN
            IF "Ending Date" > ServiceObject."Ending Date" THEN
              IF CurrFieldNo = FIELDNO("Ending Date") THEN
                FIELDERROR("Ending Date", Text010)
              ELSE
                "Ending Date" := ServiceObject."Ending Date";

        MaintenanceProposalLine.SETCURRENTKEY("Service Contract No.", "Object No.");
        MaintenanceProposalLine.SETRANGE("Service Contract No.", "Service Contract No.");
        MaintenanceProposalLine.SETRANGE("Object No.", "Service Object No.");
        MaintenanceProposalLine.SETRANGE("Service Order No.", '');
        MaintenanceProposalLine.SETFILTER("Theoretical Maintenance Date", '>%1', "Ending Date");
        MaintenanceProposalLine.MODIFYALL(Expired, TRUE, TRUE);
      END ELSE BEGIN
        IF ServiceContract."Ending Date" <> 0D THEN
          IF CurrFieldNo = FIELDNO("Ending Date") THEN
            FIELDERROR("Ending Date", Text007)
          ELSE
            "Ending Date" := ServiceContract."Ending Date";
        IF ServiceObject.GET("Service Object No.") THEN
          IF ServiceObject."Ending Date" <> 0D THEN
            IF ("Ending Date" = 0D) OR ("Ending Date" > ServiceObject."Ending Date") THEN
              IF CurrFieldNo = FIELDNO("Ending Date") THEN
                FIELDERROR("Ending Date", Text010)
              ELSE
                "Ending Date" := ServiceObject."Ending Date";
      END;
    END;

    PROCEDURE SetDoNotConfirmMaintenanceSchemeCreation@1100528000(IDoNotConfirmMaintenanceSchemeCreation@1100528000 : Boolean);
    BEGIN
      DoNotConfirmMaintenanceSchemeCreation := IDoNotConfirmMaintenanceSchemeCreation;
    END;

    PROCEDURE SetDoNotUpdateServicePackageOnServiceContract@1100528611(IDoNotUpdateServicePackageOnServiceContract@1100528600 : Boolean);
    BEGIN
      DoNotUpdateServicePackageOnServiceContract := IDoNotUpdateServicePackageOnServiceContract;
    END;

    LOCAL PROCEDURE UpdateServicePackageOnServiceContract@1100528608(Action@1100528604 : 'Insert,Modify,Delete');
    VAR
      ServiceSetup@1100528606 : Record 11012800;
      ServiceContract@1100528600 : Record 11012812;
      ServiceObject@1100528601 : Record 11071691;
      ContractObject@1100528603 : Record 11071702;
      ServicePackageCode@1100528602 : Code[10];
    BEGIN
      IF DoNotUpdateServicePackageOnServiceContract THEN
        EXIT;
      ServiceSetup.GET;
      IF NOT ServiceSetup."Update Serv. Package Contract" THEN
        EXIT;
      IF NOT ServiceContract.GET("Service Contract No.") THEN
        EXIT;
      IF NOT ServiceObject.GET("Service Object No.") THEN
        EXIT;
      IF Action <> Action::Delete THEN BEGIN
        ServicePackageCode := "Service Package";
        IF ServicePackageCode = '' THEN
          EXIT;
      END;
      ContractObject.SETRANGE("Service Contract No.", "Service Contract No.");
      ContractObject.SETFILTER("Service Object No.", '<>%1', "Service Object No.");
      IF Action = Action::Delete THEN
        IF ContractObject.FINDFIRST THEN BEGIN
          ServicePackageCode := ContractObject."Service Package";
          IF ServicePackageCode = '' THEN
            EXIT;
        END;
      IF NOT ContractObject.ISEMPTY THEN BEGIN
        ContractObject.SETFILTER("Service Package", '<>%1', ServicePackageCode);
        IF NOT ContractObject.ISEMPTY THEN
          ServicePackageCode := '';
      END;
      IF (ServicePackageCode <> '') AND (ServiceContract."Service Package" <> ServicePackageCode) THEN BEGIN
        ServiceContract.VALIDATE("Service Package", ServicePackageCode);
        ServiceContract.MODIFY(TRUE);
      END;
    END;

    PROCEDURE GetServiceSalesDiscount@1100528908(ISeekDate@1100528602 : Date;VAR OFoundServiceSalesDiscount@1100528600 : Record 11072237;VAR OPeriodEnd@1100528606 : Date) : Boolean;
    VAR
      ServiceSalesDiscount@1100528601 : Record 11072237;
      PeriodEnd@1100528603 : Date;
      LoopType@1100528604 : 'With Object,Without Object';
      EmptyDateFormula@1100528605 : DateFormula;
    BEGIN
      CLEAR(OFoundServiceSalesDiscount);
      ServiceSalesDiscount.SETRANGE(Type, ServiceSalesDiscount.Type::Contract);
      ServiceSalesDiscount.SETRANGE(Code, "Service Contract No.");
      ServiceSalesDiscount.SETRANGE("Discount/Surcharge", ServiceSalesDiscount."Discount/Surcharge"::Discount);
      ServiceSalesDiscount.SETFILTER("Starting Date", '<>%1&<=%2', 0D, ISeekDate);
      ServiceSalesDiscount.SETFILTER("Ending Date", '%1|>=%2', 0D, ISeekDate);
      ServiceSalesDiscount.SETFILTER(Period, '<>%1', EmptyDateFormula);
      IF ServiceSalesDiscount.ISEMPTY THEN
        EXIT(FALSE);
      ServiceSalesDiscount.SETRANGE("Object No.", "Service Object No.");
      FOR LoopType := LoopType::"With Object" TO LoopType::"Without Object" DO BEGIN
        IF ServiceSalesDiscount.FIND('+') THEN
          REPEAT
            PeriodEnd := CALCDATE(ServiceSalesDiscount.Period, "Invoice From") -1;
            IF (ISeekDate <= PeriodEnd) AND (OFoundServiceSalesDiscount.Percentage <= ServiceSalesDiscount.Percentage) THEN BEGIN
              OFoundServiceSalesDiscount := ServiceSalesDiscount;
              OPeriodEnd := PeriodEnd;
            END;
          UNTIL ServiceSalesDiscount.NEXT(-1) = 0;
        IF OFoundServiceSalesDiscount.Code <> '' THEN
          EXIT(TRUE);
        ServiceSalesDiscount.SETRANGE("Object No.", '');
      END;
      EXIT(OFoundServiceSalesDiscount.Code <> '');
    END;

    LOCAL PROCEDURE UpdateReferenceNorms@1100528609();
    VAR
      ReferenceNorm@1100528601 : Record 11012856;
      ReferenceNorm2@1100528600 : Record 11012856;
    BEGIN
      CALCFIELDS("Standard Object No.");
      IF "Standard Object No." = '' THEN
        EXIT;
      ReferenceNorm.SETRANGE("Service Contract No.", '');
      ReferenceNorm.SETRANGE("Service Location No.", '');
      ReferenceNorm.SETRANGE("Customer No.", '');
      ReferenceNorm.SETRANGE("Standard Object No.", "Standard Object No.");
      ReferenceNorm.SETRANGE("Object No.", '');
      IF ReferenceNorm.FINDSET THEN BEGIN
        REPEAT
          ReferenceNorm2 := ReferenceNorm;
          ReferenceNorm2."Service Contract No." := "Service Contract No.";
          ReferenceNorm2."Object No." := "Service Object No.";
          ReferenceNorm2."Standard Object No." := '';
          IF ReferenceNorm2.INSERT THEN;
        UNTIL ReferenceNorm.NEXT = 0;
      END;
    END;

    PROCEDURE GetQuantity@1100528610(ISeekDate@1100528602 : Date) : Integer;
    VAR
      ServiceObject@1100528600 : Record 11071691;
      ContractAmountsbyDate@1100528601 : Record 11071708;
    BEGIN
      IF CURRENTCOMPANY <> COMPANYNAME THEN
        ServiceObject.CHANGECOMPANY(CURRENTCOMPANY);
      IF NOT ServiceObject.GET("Service Object No.") THEN
        EXIT(0);
      CASE ServiceObject."Quantity Based on" OF
        ServiceObject."Quantity Based on"::"Service Object":
          EXIT(ServiceObject.Quantity);
        ServiceObject."Quantity Based on"::"Contract Object":
          BEGIN
            IF CURRENTCOMPANY <> COMPANYNAME THEN
              ContractAmountsbyDate.CHANGECOMPANY(CURRENTCOMPANY);
            ContractAmountsbyDate.SETRANGE(Type, ContractAmountsbyDate.Type::"Price Agreement");
            ContractAmountsbyDate.SETRANGE("Service Contract No.", "Service Contract No.");
            ContractAmountsbyDate.SETRANGE("Service Object No.", "Service Object No.");
            ContractAmountsbyDate.SETRANGE("Installment Line No.", 0);
            ContractAmountsbyDate.SETFILTER("Index Date", '<=%1', ISeekDate);
            ContractAmountsbyDate.SETFILTER("Ending Date", '%1|>=%2', 0D, ISeekDate);
            IF ContractAmountsbyDate.FINDLAST THEN
              EXIT(ContractAmountsbyDate.Quantity);
          END;
      END;
    END;

    PROCEDURE GetNextIndexDate@1100528900() : Date;
    VAR
      ContractAmountsbyDate@1100528901 : Record 11071708;
      LastIndexDate@1100528900 : Date;
    BEGIN
      LastIndexDate := "Last Index Date";
      IF LastIndexDate <> 0D THEN
        EXIT(CALCDATE("Index Interval", LastIndexDate));
      LastIndexDate := "Index Starting Date";
      IF LastIndexDate = 0D THEN
        EXIT;
      IF FORMAT("Index Interval") = '' THEN
        EXIT;
      IF NOT ContractAmountsbyDate.GET(ContractAmountsbyDate.Type::"Price Agreement", "Service Contract No.", "Service Object No.", 0, LastIndexDate) THEN
        EXIT(LastIndexDate)
      ELSE
        EXIT(CALCDATE("Index Interval", LastIndexDate));
    END;

    LOCAL PROCEDURE TestServiceContractInvoiceMethodDynamic@1100528902();
    VAR
      ServiceContract@1100528900 : Record 11012812;
    BEGIN
      ServiceContract.GET("Service Contract No.");
      ServiceContract.TESTFIELD("Invoice Type", ServiceContract."Invoice Type"::Dynamic);
    END;

    LOCAL PROCEDURE GetServiceCategoryContract@1100529600();
    VAR
      ServicePackage@1100529600 : Record 11012806;
      ServiceCategory@1100529601 : Record 11071985;
    BEGIN
      IF ("Service Package" = '') OR ("Service Contract No." = '') OR ("Service Category (Contract)" <> '') THEN
        EXIT;
      IF NOT ServicePackage.GET("Service Package") THEN
        EXIT;
      IF ServicePackage."Service Category" = '' THEN
        EXIT;
      ServiceCategory.SETRANGE("Service Contract No.", "Service Contract No.");
      ServiceCategory.SETRANGE(Code, ServicePackage."Service Category");
      IF NOT ServiceCategory.ISEMPTY THEN
        "Service Category (Contract)" := ServicePackage."Service Category";
    END;

    PROCEDURE SetStartingDateMaintenance@1100529601(NewStartingDateMaintenance@1100529600 : Date);
    BEGIN
      StartingDateMaintenance := NewStartingDateMaintenance;
    END;

    BEGIN
    END.
  }
}

