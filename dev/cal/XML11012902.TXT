OBJECT XMLport 11012902 Import Investm. Prop. Cashflow
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Import Investment Proposal Cashflow;
    Direction=Import;
    TextEncoding=WINDOWS;
    OnPostXMLport=BEGIN
                    UpdateProjectDevelopment;
                  END;

    Format=Variable Text;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{A1B517BD-3A8E-4413-8050-1D3E07BF754D}];  ;Root                ;Element ;Text     }

    { [{5B4D37FB-D180-47AD-B033-42EC67ABEF63}];1 ;Integer             ;Element ;Table   ;
                                                  SourceTable=Table2000000026;
                                                  SourceTableView=SORTING(Field1);
                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 ImportLine;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{02523D8C-1668-47B8-9C01-E88CC85BE7F2}];2 ;ImportCostObject    ;Element ;Text     }

    { [{E3FB2FC8-90FB-4E60-8A65-3B73DB955098}];2 ;ImportDescription   ;Element ;Text     }

    { [{1745EC1F-7C2B-4BF5-A25B-A380160CE1FA}];2 ;ImportType          ;Element ;Text     }

    { [{AB1A2A92-2CFC-42D2-8EE4-17FD3738AB00}];2 ;ImportAmount        ;Element ;Text     }

    { [{2AD62D5B-0015-433D-897C-F1A557FA4049}];2 ;ImportDate          ;Element ;Text     }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525003;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525001;2;Field  ;
                  CaptionML=[ENU=Phase;
                             SVE=Fas];
                  SourceExpr=PhaseCode;
                  OnValidate=BEGIN
                               ValidatePhaseCode;
                             END;

                  OnLookup=BEGIN
                             LookupPhaseCode;
                           END;
                            }

      { 1100525002;2;Field  ;
                  CaptionML=[ENU=Reference Date;
                             SVE=Referensdatum];
                  SourceExpr=ReferenceDate }

    }
  }
  CODE
  {
    VAR
      Importcashflow@1100525000 : TextConst 'ENU=Import Cashflow';
      ProjectNo@1100525001 : Code[20];
      PhaseCode@1100525002 : Code[10];
      ReferenceDate@1100525003 : Date;
      VersionCode@1100525004 : Code[10];

    PROCEDURE SetProjectNo@1100525007(CurrentProjectNo@1100525000 : Code[20]);
    BEGIN
      ProjectNo := CurrentProjectNo;
    END;

    LOCAL PROCEDURE ValidatePhaseCode@1100525008();
    VAR
      ProjectDevelopmentPhase@1100525000 : Record 11012911;
    BEGIN
      IF PhaseCode = '' THEN
        EXIT;

      ProjectDevelopmentPhase.GET(ProjectNo,PhaseCode);
    END;

    LOCAL PROCEDURE LookupPhaseCode@1100525009();
    VAR
      ProjectDevelopmentPhase@1100525000 : Record 11012911;
    BEGIN
      ProjectDevelopmentPhase.SETRANGE("Project No.",ProjectNo);
      IF PAGE.RUNMODAL(0,ProjectDevelopmentPhase) = ACTION::LookupOK THEN BEGIN
        PhaseCode := ProjectDevelopmentPhase.Phase;
        ValidatePhaseCode;
      END;
    END;

    LOCAL PROCEDURE ImportLine@1100525001();
    BEGIN
      IF VersionCode = '' THEN BEGIN
        GetNewVersionCode;
        InsertInvestmentProposal;
      END;
      InsertInvestmentProposalRubricLine;
      InsertInvestmentProposalBudget;
    END;

    LOCAL PROCEDURE GetNewVersionCode@1100525000();
    BEGIN
      VersionCode := GetProjectDevelopmentVersionCode;
      IF VersionCode = '' THEN
        VersionCode := 'CASHF-01'
      ELSE
        VersionCode := INCSTR(VersionCode);
    END;

    LOCAL PROCEDURE GetProjectDevelopmentVersionCode@1100525006() : Code[10];
    VAR
      ProjectDevelopment@1100525000 : Record 11012915;
    BEGIN
      ProjectDevelopment.GET(ProjectNo);
      EXIT(ProjectDevelopment."Last Imported Cashflow");
    END;

    LOCAL PROCEDURE InsertInvestmentProposal@1100525002();
    VAR
      InvestmentProposal@1100525000 : Record 11012912;
    BEGIN
      InvestmentProposal.VALIDATE("Project No.",ProjectNo);
      InvestmentProposal.VALIDATE(Version,VersionCode);
      InvestmentProposal.VALIDATE(Description,Importcashflow + ' ' + FORMAT(TODAY));
      InvestmentProposal.VALIDATE("Version Date",TODAY);
      InvestmentProposal.VALIDATE(Phase,PhaseCode);
      InvestmentProposal.VALIDATE("Reference Date",ReferenceDate);
      InvestmentProposal.INSERT(TRUE);
    END;

    LOCAL PROCEDURE InsertInvestmentProposalRubricLine@1100525003();
    VAR
      InvestmProposalRubricLine@1100525000 : Record 11012913;
    BEGIN
      IF InvestmProposalRubricLine.GET(ProjectNo,VersionCode,ImportCostObject) THEN
        EXIT;

      InvestmProposalRubricLine.VALIDATE("Project No.",ProjectNo);
      InvestmProposalRubricLine.VALIDATE(Version,VersionCode);
      InvestmProposalRubricLine.VALIDATE("Rubric Code",ImportCostObject);
      InvestmProposalRubricLine.VALIDATE(Description,ImportDescription);
      IF ImportType = 'Cost' THEN
        InvestmProposalRubricLine.VALIDATE(Type,InvestmProposalRubricLine.Type::Costs)
      ELSE
        InvestmProposalRubricLine.VALIDATE(Type,InvestmProposalRubricLine.Type::Revenues);
      InvestmProposalRubricLine.VALIDATE("Cost Object Filter",ImportCostObject);
      InvestmProposalRubricLine.INSERT(TRUE);
    END;

    LOCAL PROCEDURE InsertInvestmentProposalBudget@1100525013();
    VAR
      InvestmentProposalBudget@1100525000 : Record 11012914;
      AmountDec@1100525001 : Decimal;
      Date@1100525002 : Date;
    BEGIN
      InvestmentProposalBudget.VALIDATE("Project No.",ProjectNo);
      InvestmentProposalBudget.VALIDATE(Version,VersionCode);
      InvestmentProposalBudget.VALIDATE("Rubric Code",ImportCostObject);
      InvestmentProposalBudget.VALIDATE(Description,ImportDescription);
      EVALUATE(Date,ImportDate);
      InvestmentProposalBudget.VALIDATE(Date,Date);
      EVALUATE(AmountDec,ImportAmount);
      InvestmentProposalBudget.VALIDATE("Unit Price",AmountDec);
      InvestmentProposalBudget.INSERT(TRUE);
    END;

    LOCAL PROCEDURE UpdateProjectDevelopment@1100525005();
    VAR
      ProjectDevelopment@1100525002 : Record 11012915;
    BEGIN
      IF VersionCode = '' THEN
        EXIT;
      ProjectDevelopment.GET(ProjectNo);
      ProjectDevelopment.VALIDATE("Last Imported Cashflow",VersionCode);
      ProjectDevelopment.MODIFY(TRUE);
    END;

    BEGIN
    END.
  }
}

