OBJECT Page 11229424 Built SC Control Reportings
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=ServicevertragsÅberwachungsberichte (erstellt);
               ENU=Built Service Contract Control Reportings;
               NLD=Opgebouwde servicecontractbewakingsrapportages];
    InsertAllowed=No;
    SourceTable=Table11020579;
    PageType=List;
    UsageCategory=Tasks;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             BuildControlReporting;
           END;

    OnDeleteRecord=VAR
                     ServiceContractControl@1100528600 : Record 11071762;
                     CreateServiceControl@1100528601 : Codeunit 11012845;
                     ServiceContrPreclDetail@1100528500 : Record 11071767;
                   BEGIN
                     IF Bool1 THEN BEGIN
                       ServiceContractControl.SETRANGE("Reporting Date", Date1);
                       ServiceContractControl.SETRANGE("Save Contract Control", FALSE);
                       IF ServiceContractControl.FINDSET THEN
                         REPEAT
                           IF ServiceContractControl.IsControlUnusedInPreclosure(ServiceContrPreclDetail) THEN
                             ServiceContractControl.DELETE(TRUE);
                         UNTIL ServiceContractControl.NEXT = 0;
                     END ELSE BEGIN
                       ServiceContractControl.SETRANGE("Reporting Date", Date1);
                       ServiceContractControl.DELETEALL(TRUE);
                       CreateServiceControl.RemoveFinanceControlLines(Date1);
                     END;
                   END;

  }
  CONTROLS
  {
    { 1100528600;0;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100528602;2;Field  ;
                CaptionML=[DEU=Berichtsdatum;
                           ENU=Reporting Date;
                           NLD=Rapportdatum;
                           NOR=Rapporteringsdato;
                           SVE=Rapporteringsdatum];
                SourceExpr=Date1;
                Editable=FALSE }

    { 1100528603;2;Field  ;
                CaptionML=[DEU=Anz. öberwachungen;
                           ENU=No. of Controls;
                           NLD=Aantal bewakingen];
                SourceExpr=Int1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownNoOfControls;
                            END;
                             }

    { 1100528604;2;Field  ;
                CaptionML=[DEU=Anzahl ArbeitsvertrÑge;
                           ENU=No. of Contracts;
                           NLD=Aantal contracten;
                           NOR=Antall kontrakter;
                           SVE=Antal kontrakt];
                SourceExpr=Int2;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownNoOfContracts;
                            END;
                             }

    { 1100528605;2;Field  ;
                CaptionML=[DEU=VertragsÅberwachung speichern;
                           ENU=Save Contract Control;
                           NLD=Bewaar contractbewaking];
                SourceExpr=Bool1;
                OnValidate=VAR
                             ServiceContractControl@1100528600 : Record 11071762;
                           BEGIN
                             ServiceContractControl.SETRANGE("Reporting Date", Date1);
                             IF ServiceContractControl.FINDSET(TRUE) THEN
                               REPEAT
                                 ServiceContractControl.VALIDATE("Save Contract Control", Bool1);
                                 ServiceContractControl.MODIFY;
                               UNTIL ServiceContractControl.NEXT = 0;
                           END;
                            }

  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'DEU=Fortschritt: @1@@@@@@@;ENU=Progress: @1@@@@@@@;NLD=Voortgang: @1@@@@@@@';

    LOCAL PROCEDURE BuildControlReporting@1100528600();
    VAR
      ServiceContractControl@1100528600 : Record 11071762;
      TmpServiceContractControl@1100528601 : TEMPORARY Record 11071762;
      ServiceControlCustomer@1100528602 : Record 11071763;
      ProgressDialog@1100525002 : Dialog;
      RecordCount@1100525001 : Integer;
      RecordCounter@1100525000 : Integer;
    BEGIN
      RESET;
      DELETEALL;

      IF ServiceContractControl.FINDSET THEN BEGIN
        ProgressDialog.OPEN(Text000);
        RecordCount := ServiceContractControl.COUNT;
        REPEAT
          INIT;
          "Key Code1" := FORMAT(ServiceContractControl."Reporting Date", 0, 9);
          Date1 := ServiceContractControl."Reporting Date";
          ServiceContractControl.SETCURRENTKEY("Reporting Date");
          ServiceContractControl.SETRANGE("Reporting Date", ServiceContractControl."Reporting Date");
          Int1 := ServiceContractControl.COUNT;
          ServiceContractControl.SETCURRENTKEY("Save Contract Control");
          ServiceContractControl.SETRANGE("Save Contract Control", TRUE);
          Bool1 := NOT ServiceContractControl.ISEMPTY;
          ServiceContractControl.SETRANGE("Save Contract Control");
          ServiceContractControl.SETRANGE("Reporting Date");
          IF INSERT THEN;
          TmpServiceContractControl."Reporting Date" := ServiceContractControl."Reporting Date";
          TmpServiceContractControl."Service Contract No." := ServiceContractControl."Service Contract No.";
          IF TmpServiceContractControl.INSERT THEN;
          RecordCounter += 1;
          ProgressDialog.UPDATE(1, ROUND(RecordCounter / RecordCount * 10000, 1));
        UNTIL ServiceContractControl.NEXT = 0;
        ProgressDialog.CLOSE;
      END;

      IF ServiceControlCustomer.FINDSET THEN
        REPEAT
          INIT;
          "Key Code1" := FORMAT(ServiceControlCustomer."Reporting Date", 0, 9);
          Date1 := ServiceControlCustomer."Reporting Date";
          IF INSERT THEN;
        UNTIL ServiceControlCustomer.NEXT = 0;

      IF FINDSET THEN
        REPEAT
          TmpServiceContractControl.SETRANGE("Reporting Date", Date1);
          Int2 := TmpServiceContractControl.COUNT;
          MODIFY;
        UNTIL NEXT = 0;

      IF FINDFIRST THEN;
    END;

    LOCAL PROCEDURE DrillDownNoOfControls@1100528601();
    VAR
      ServiceContractControl@1100528600 : Record 11071762;
    BEGIN
      ServiceContractControl.SETRANGE("Reporting Date", Date1);
      PAGE.RUNMODAL(0, ServiceContractControl);
    END;

    LOCAL PROCEDURE DrillDownNoOfContracts@1100528602();
    VAR
      ServiceContractControl@1100528600 : Record 11071762;
      ServiceContract@1100528601 : Record 11012812;
    BEGIN
      ServiceContractControl.SETRANGE("Reporting Date", Date1);
      IF ServiceContractControl.FINDSET THEN
        REPEAT
          ServiceContract."No." := ServiceContractControl."Service Contract No.";
          ServiceContract.MARK(TRUE);
        UNTIL ServiceContractControl.NEXT = 0;
      ServiceContract.MARKEDONLY(TRUE);
      PAGE.RUNMODAL(0, ServiceContract);
    END;

    BEGIN
    END.
  }
}

