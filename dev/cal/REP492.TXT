OBJECT Report 492 Copy Purchase Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Purchase Document;
               NOR=Kopier kjõpsdokument;
               SVE=Kopiera inkîpsdokument];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  PurchSetup.GET;
                  CopyDocMgt.SetProperties(
                    //IncludeHeader,RecalculateLines,FALSE,FALSE,FALSE,PurchSetup."Exact Cost Reversing Mandatory",FALSE); //**4PS.o
                    IncludeHeader,RecalculateLines,FALSE,FALSE,FALSE,PurchSetup."Exact Cost Reversing Mandatory",FALSE,CopyShortcutdimension,FALSE); //**4PS.n
                  CopyDocMgt.SetArchDocVal(DocNoOccurrence,DocVersionNo);

                  OnPreReportOnBeforeCopyPurchaseDoc(CopyDocMgt);

                  CopyDocMgt.SetIncludeOrgInvInfo(FALSE);
                  CopyDocMgt.CopyPurchDoc(DocType,DocNo,PurchHeader);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DocNo <> '' THEN BEGIN
                     CASE DocType OF
                       DocType::Quote:
                         IF FromPurchHeader.GET(FromPurchHeader."Document Type"::Quote,DocNo) THEN;
                       DocType::"Blanket Order":
                         IF FromPurchHeader.GET(FromPurchHeader."Document Type"::"Blanket Order",DocNo) THEN;
                       DocType::Order:
                         IF FromPurchHeader.GET(FromPurchHeader."Document Type"::Order,DocNo) THEN;
                       DocType::Invoice:
                         IF FromPurchHeader.GET(FromPurchHeader."Document Type"::Invoice,DocNo) THEN;
                       DocType::"Return Order":
                         IF FromPurchHeader.GET(FromPurchHeader."Document Type"::"Return Order",DocNo) THEN;
                       DocType::"Credit Memo":
                         IF FromPurchHeader.GET(FromPurchHeader."Document Type"::"Credit Memo",DocNo) THEN;
                       DocType::"Posted Receipt":
                         IF FromPurchRcptHeader.GET(DocNo) THEN
                           FromPurchHeader.TRANSFERFIELDS(FromPurchRcptHeader);
                       DocType::"Posted Invoice":
                         IF FromPurchInvHeader.GET(DocNo) THEN
                           FromPurchHeader.TRANSFERFIELDS(FromPurchInvHeader);
                       DocType::"Posted Return Shipment":
                         IF FromReturnShptHeader.GET(DocNo) THEN
                           FromPurchHeader.TRANSFERFIELDS(FromReturnShptHeader);
                       DocType::"Posted Credit Memo":
                         IF FromPurchCrMemoHeader.GET(DocNo) THEN
                           FromPurchHeader.TRANSFERFIELDS(FromPurchCrMemoHeader);
                       DocType::"Arch. Order":
                         IF FromPurchHeaderArchive.GET(FromPurchHeaderArchive."Document Type"::Order,DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromPurchHeader.TRANSFERFIELDS(FromPurchHeaderArchive);
                       DocType::"Arch. Quote":
                         IF FromPurchHeaderArchive.GET(FromPurchHeaderArchive."Document Type"::Quote,DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromPurchHeader.TRANSFERFIELDS(FromPurchHeaderArchive);
                       DocType::"Arch. Blanket Order":
                         IF FromPurchHeaderArchive.GET(FromPurchHeaderArchive."Document Type"::"Blanket Order",DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromPurchHeader.TRANSFERFIELDS(FromPurchHeaderArchive);
                       DocType::"Arch. Return Order":
                         IF FromPurchHeaderArchive.GET(FromPurchHeaderArchive."Document Type"::"Return Order",DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromPurchHeader.TRANSFERFIELDS(FromPurchHeaderArchive);
                     END;
                     IF FromPurchHeader."No." = '' THEN
                       DocNo := '';
                   END;
                   ValidateDocNo;

                   OnAfterOpenPage;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 3   ;2   ;Field     ;
                  Name=DocumentType;
                  CaptionML=[ENU=Document Type;
                             NOR=Bilagstype;
                             SVE=Dokumenttyp];
                  ToolTipML=[ENU=Specifies the type of document that is processed by the report or batch job.;
                             NOR=Angir typen dokument som behandles av rapporten eller kjõrselen.;
                             SVE=Anger dokumenttypen som rapporten eller buntjobbet bearbetar.];
                  OptionCaptionML=[ENU=Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Receipt,Posted Invoice,Posted Return Shipment,Posted Credit Memo,Arch. Quote,Arch. Order,Arch. Blanket Order,Arch. Return Order;
                                   NOR=Tilbud,Rammebestilling,Ordre,Faktura,Ordreretur,Kreditnota,Bokfõrt mottak,Bokfõrt faktura,Bokfõrt returforsendelse,Bokfõrt kreditnota,Ark.tilbud,Ark.ordre,Ark.rammebestilling,Ark.ordreretur;
                                   SVE=Offert,Avropsorder,Order,Faktura,Returorder,Kreditnota,Bokfîrd inleverans,Bokfîrd faktura,Bokfîrd returutleverans,Bokfîrd kreditnota,Arkiverad offert,Arkiverad order,Arkiverad avropsorder,Arkiverad returorder];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocType;
                  OnValidate=BEGIN
                               DocNo := '';
                               ValidateDocNo;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[ENU=Document No.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[ENU=Specifies the number of the document that is processed by the report or batch job.;
                             NOR=Angir nummeret for dokumentet som behandles av rapporten eller kjõrselen.;
                             SVE=Anger antalet dokument som rapporten eller buntjobbet bearbetar.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocNo;
                  OnValidate=BEGIN
                               ValidateDocNo;
                             END;

                  OnLookup=BEGIN
                             LookupDocNo;
                           END;
                            }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Doc. No. Occurrence;
                             NOR=Forekomst av bilagsnr.;
                             SVE=Dok.nr fîrekomst];
                  ToolTipML=[ENU=Specifies the number of times the No. value has been used in the number series.;
                             NOR=Angir hvor mange ganger verdien Nr. er brukt i nummerserien.];
                  ApplicationArea=#Basic,#Suite;
                  BlankZero=Yes;
                  SourceExpr=DocNoOccurrence;
                  Editable=FALSE }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Version No.;
                             NOR=Versjonsnr.;
                             SVE=Versionsnr];
                  ToolTipML=[ENU=Specifies the version of the document to be copied.;
                             NOR=Angir versjonen av dokumentet som skal kopieres.];
                  ApplicationArea=#Basic,#Suite;
                  BlankZero=Yes;
                  SourceExpr=DocVersionNo;
                  Editable=FALSE }

      { 5   ;2   ;Field     ;
                  Name=BuyfromVendorNo;
                  CaptionML=[ENU=Buy-from Vendor No.;
                             NOR=Kjõp fra-leverandõrnr.;
                             SVE=Inkîpsleverantîrsnr];
                  ToolTipML=[ENU=Specifies the vendor according to the values in the Document No. and Document Type fields.;
                             NOR=Angir leverandõren i henhold til verdiene i feltene Dokumentnr. og Dokumenttype.;
                             SVE=Anger leverantîren enligt vÑrdena i fÑlten Dokumentnr och Dokumenttyp.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=FromPurchHeader."Buy-from Vendor No.";
                  Editable=FALSE }

      { 7   ;2   ;Field     ;
                  Name=BuyfromVendorName;
                  CaptionML=[ENU=Buy-from Vendor Name;
                             NOR=Kjõp fra-leverandõrnavn;
                             SVE=Inkîpsnamn];
                  ToolTipML=[ENU=Specifies the vendor according to the values in the Document No. and Document Type fields.;
                             NOR=Angir leverandõren i henhold til verdiene i feltene Dokumentnr. og Dokumenttype.;
                             SVE=Anger leverantîren enligt vÑrdena i fÑlten Dokumentnr och Dokumenttyp.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=FromPurchHeader."Buy-from Vendor Name";
                  Editable=FALSE }

      { 1   ;2   ;Field     ;
                  Name=IncludeHeader_Options;
                  CaptionML=[ENU=Include Header;
                             NOR=Ta med hode;
                             SVE=Ta med huvud];
                  ToolTipML=[ENU=Specifies if you also want to copy the information from the document header. When you copy quotes, if the posting date field of the new document is empty, the work date is used as the posting date of the new document.;
                             NOR=Angir om du ogsÜ vil kopiere informasjonen fra dokumenthodet. NÜr du kopierer tilbud, brukes arbeidsdatoen som bokfõringsdato for det nye dokumentet hvis feltet Bokfõringsdato i det nye dokumentet er tomt.;
                             SVE=Anger om du ocksÜ vill kopiera informationen frÜn dokumenthuvudet. Om du kopierar en offert, och fÑltet Bokfîringsdatum i det nya dokumentet Ñr tomt, anvÑnds arbetsdatumet som bokfîringsdatum fîr det nya dokumentet.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=IncludeHeader;
                  OnValidate=BEGIN
                               ValidateIncludeHeader;
                             END;
                              }

      { 4   ;2   ;Field     ;
                  Name=RecalculateLines;
                  CaptionML=[ENU=Recalculate Lines;
                             NOR=Gjenberegn linjer;
                             SVE=BerÑkna om rader];
                  ToolTipML=[ENU=Specifies that lines are recalculate and inserted on the purchase document you are creating. The batch job retains the item numbers and item quantities but recalculates the amounts on the lines based on the vendor information on the new document header. In this way, the batch job accounts for item prices and discounts that are specifically linked to the vendor on the new header.;
                             NOR=Angir at linjer blir omberegnet og satt inn i kjõpsdokumentet du oppretter. Kjõrselen beholder varenumre og varemengde, men omberegner belõpene pÜ linjene pÜ bakgrunn av leverandõrinformasjonen i det nye dokumenthodet. PÜ denne mÜten tar kjõrselen med varepriser og -rabatter som er spesielt knyttet til leverandõren i det nye hodet.;
                             SVE=Anger att rader berÑknas om och infogas i inkîpsdokumentet som du skapar. Batch-jobbet behÜller artikelnumren och artikelantalen men berÑknar om beloppen pÜ raderna med hjÑlp av leverantîrsinformationen i det nya dokumenthuvudet. PÜ sÜ vis redovisar batch-jobbet artikelpriser och rabatter som Ñr knutna till leverantîren, i det nya huvudet.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=RecalculateLines;
                  OnValidate=BEGIN
                               IF (DocType = DocType::"Posted Receipt") OR (DocType = DocType::"Posted Return Shipment") THEN
                                 RecalculateLines := TRUE;
                             END;
                              }

      { 1100527550;2;Field  ;
                  Name=CopyShortcutdimension;
                  CaptionML=ENU=Copy Shortcut Dimension;
                  SourceExpr=CopyShortcutdimension }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PurchHeader@1007 : Record 38;
      FromPurchHeader@1010 : Record 38;
      FromPurchRcptHeader@1012 : Record 120;
      FromPurchInvHeader@1014 : Record 122;
      FromReturnShptHeader@1008 : Record 6650;
      FromPurchCrMemoHeader@1016 : Record 124;
      FromPurchHeaderArchive@1028 : Record 11072057;
      PurchSetup@1003 : Record 312;
      CopyDocMgt@1033 : Codeunit 6620;
      DocType@1024 : 'Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Receipt,Posted Invoice,Posted Return Shipment,Posted Credit Memo,Arch. Quote,Arch. Order,Arch. Blanket Order,Arch. Return Order';
      DocNo@1025 : Code[20];
      IncludeHeader@1026 : Boolean;
      RecalculateLines@1027 : Boolean;
      Text000@1002 : TextConst 'ENU=The price information may not be reversed correctly, if you copy a %1. If possible, copy a %2 instead or use %3 functionality.;NOR=Prisinformasjonen tilbakefõres kanskje ikke pÜ riktig mÜte hvis du kopierer %1. Kopier om mulig %2 i stedet, eller bruk %3-funksjonalitet.;SVE=Prisinformationen kanske inte Üterfîrs korrekt om du kopierar en/ett %1. Om det Ñr mîjligt bîr du kopiera en/ett %2 i stÑllet eller anvÑnda funktionen %3.';
      Text001@1001 : TextConst 'ENU=Undo Receipt;NOR=Angre mottak;SVE=èterstÑll inleverans';
      Text002@1000 : TextConst 'ENU=Undo Return Shipment;NOR=Angre returforsendelse;SVE=èterstÑll returutleverans';
      Text003@1004 : TextConst 'ENU=Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Receipt,Posted Invoice,Posted Return Shipment,Posted Credit Memo;NOR=Forespõrsel,Rammebestilling,Bestilling,Faktura,Bestillingsretur,Kreditnota,Bokfõrt mottaksseddel,Bokfõrt faktura,Bokfõrt returforsendelse,Bokfõrt kreditnota;SVE=Offert,Avropsorder,Order,Faktura,Returorder,Kreditnota,Bokfîrd inleverans,Bokfîrd faktura,Bokfîrd returutleverans,Bokfîrd kreditnota';
      DocNoOccurrence@1031 : Integer;
      DocVersionNo@1032 : Integer;
      CopyShortcutdimension@1100527550 : Boolean;

    [External]
    PROCEDURE SetPurchHeader@2(VAR NewPurchHeader@1000 : Record 38);
    BEGIN
      NewPurchHeader.TESTFIELD("No.");
      PurchHeader := NewPurchHeader;
    END;

    LOCAL PROCEDURE ValidateDocNo@1();
    VAR
      DocType2@1000 : 'Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Receipt,Posted Invoice,Posted Return Shipment,Posted Credit Memo';
    BEGIN
      IF DocNo = '' THEN BEGIN
        FromPurchHeader.INIT;
        DocNoOccurrence := 0;
        DocVersionNo := 0;
      END ELSE
        IF DocNo <> FromPurchHeader."No." THEN BEGIN
          FromPurchHeader.INIT;
          CASE DocType OF
            DocType::Quote,
            DocType::"Blanket Order",
            DocType::Order,
            DocType::Invoice,
            DocType::"Return Order",
            DocType::"Credit Memo":
              FromPurchHeader.GET(CopyDocMgt.PurchHeaderDocType(DocType),DocNo);
            DocType::"Posted Receipt":
              BEGIN
                FromPurchRcptHeader.GET(DocNo);
                FromPurchHeader.TRANSFERFIELDS(FromPurchRcptHeader);
                IF PurchHeader."Document Type" IN
                   [PurchHeader."Document Type"::"Return Order",PurchHeader."Document Type"::"Credit Memo"]
                THEN BEGIN
                  DocType2 := DocType2::"Posted Invoice";
                  MESSAGE(Text000,SELECTSTR(1 + DocType,Text003),SELECTSTR(1 + DocType2,Text003),Text001);
                END;
              END;
            DocType::"Posted Invoice":
              BEGIN
                FromPurchInvHeader.GET(DocNo);
                CheckVendLegdEntryInProposal(FromPurchInvHeader); //**4PS.n Call C001905
                FromPurchHeader.TRANSFERFIELDS(FromPurchInvHeader);
              END;
            DocType::"Posted Return Shipment":
              BEGIN
                FromReturnShptHeader.GET(DocNo);
                FromPurchHeader.TRANSFERFIELDS(FromReturnShptHeader);
                IF PurchHeader."Document Type" IN
                   [PurchHeader."Document Type"::Order,PurchHeader."Document Type"::Invoice]
                THEN BEGIN
                  DocType2 := DocType2::"Posted Credit Memo";
                  MESSAGE(Text000,SELECTSTR(1 + DocType,Text003),SELECTSTR(1 + DocType2,Text003),Text002);
                END;
              END;
            DocType::"Posted Credit Memo":
              BEGIN
                FromPurchCrMemoHeader.GET(DocNo);
                FromPurchHeader.TRANSFERFIELDS(FromPurchCrMemoHeader);
              END;
            DocType::"Arch. Quote",
            DocType::"Arch. Order",
            DocType::"Arch. Blanket Order",
            DocType::"Arch. Return Order":
              BEGIN
                IF NOT FromPurchHeaderArchive.GET(
                     CopyDocMgt.ArchPurchHeaderDocType(DocType),DocNo,DocNoOccurrence,DocVersionNo)
                THEN BEGIN
                  FromPurchHeaderArchive.SETRANGE("No.",DocNo);
                  IF FromPurchHeaderArchive.FINDLAST THEN BEGIN
                    DocNoOccurrence := FromPurchHeaderArchive."Doc. No. Occurrence";
                    DocVersionNo := FromPurchHeaderArchive."Version No.";
                  END;
                END;
                FromPurchHeader.TRANSFERFIELDS(FromPurchHeaderArchive);
              END;
          END;
        END;
      FromPurchHeader."No." := '';

      IncludeHeader :=
        (DocType IN [DocType::"Posted Invoice",DocType::"Posted Credit Memo"]) AND
        ((DocType = DocType::"Posted Credit Memo") <>
         (PurchHeader."Document Type" = PurchHeader."Document Type"::"Credit Memo")) AND
        (PurchHeader."Buy-from Vendor No." IN [FromPurchHeader."Buy-from Vendor No.",'']);

      OnBeforeValidateIncludeHeader(IncludeHeader,DocType);
      ValidateIncludeHeader;
    END;

    LOCAL PROCEDURE LookupDocNo@3();
    BEGIN
      CASE DocType OF
        DocType::Quote,
        DocType::"Blanket Order",
        DocType::Order,
        DocType::Invoice,
        DocType::"Return Order",
        DocType::"Credit Memo":
          LookupPurchDoc;
        DocType::"Posted Receipt":
          LookupPostedReceipt;
        DocType::"Posted Invoice":
          LookupPostedInvoice;
        DocType::"Posted Return Shipment":
          LookupPostedReturn;
        DocType::"Posted Credit Memo":
          LookupPostedCrMemo;
        DocType::"Arch. Quote",
        DocType::"Arch. Order",
        DocType::"Arch. Blanket Order",
        DocType::"Arch. Return Order":
          LookupPurchArchive;
      END;
      ValidateDocNo;
    END;

    LOCAL PROCEDURE LookupPurchDoc@6();
    BEGIN
      OnBeforeLookupPurchDoc(FromPurchHeader,PurchHeader);

      FromPurchHeader.FILTERGROUP := 0;
      FromPurchHeader.SETRANGE("Document Type",CopyDocMgt.PurchHeaderDocType(DocType));
      IF PurchHeader."Document Type" = CopyDocMgt.PurchHeaderDocType(DocType) THEN
        FromPurchHeader.SETFILTER("No.",'<>%1',PurchHeader."No.");
      FromPurchHeader.FILTERGROUP := 2;
      FromPurchHeader."Document Type" := CopyDocMgt.PurchHeaderDocType(DocType);
      FromPurchHeader."No." := DocNo;
      IF (DocNo = '') AND (PurchHeader."Buy-from Vendor No." <> '') THEN
        IF FromPurchHeader.SETCURRENTKEY("Document Type","Buy-from Vendor No.") THEN BEGIN
          FromPurchHeader."Buy-from Vendor No." := PurchHeader."Buy-from Vendor No.";
          IF FromPurchHeader.FIND('=><') THEN;
        END;
      IF PAGE.RUNMODAL(0,FromPurchHeader) = ACTION::LookupOK THEN
        DocNo := FromPurchHeader."No.";
    END;

    LOCAL PROCEDURE LookupPurchArchive@7();
    BEGIN
      FromPurchHeaderArchive.RESET;
      FromPurchHeaderArchive.FILTERGROUP := 0;
      FromPurchHeaderArchive.SETRANGE("Document Type",CopyDocMgt.ArchPurchHeaderDocType(DocType));
      FromPurchHeaderArchive.FILTERGROUP := 2;
      FromPurchHeaderArchive."Document Type" := CopyDocMgt.ArchPurchHeaderDocType(DocType);
      FromPurchHeaderArchive."No." := DocNo;
      FromPurchHeaderArchive."Doc. No. Occurrence" := DocNoOccurrence;
      FromPurchHeaderArchive."Version No." := DocVersionNo;
      IF (DocNo = '') AND (PurchHeader."Sell-to Customer No." <> '') THEN
        IF FromPurchHeaderArchive.SETCURRENTKEY("Document Type","Sell-to Customer No.") THEN BEGIN
          FromPurchHeaderArchive."Sell-to Customer No." := PurchHeader."Sell-to Customer No.";
          IF FromPurchHeaderArchive.FIND('=><') THEN;
        END;
      IF PAGE.RUNMODAL(0,FromPurchHeaderArchive) = ACTION::LookupOK THEN BEGIN
        DocNo := FromPurchHeaderArchive."No.";
        DocNoOccurrence := FromPurchHeaderArchive."Doc. No. Occurrence";
        DocVersionNo := FromPurchHeaderArchive."Version No.";
        RequestOptionsPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE LookupPostedReceipt@8();
    BEGIN
      OnBeforeLookupPostedReceipt(FromPurchRcptHeader,PurchHeader);

      FromPurchRcptHeader."No." := DocNo;
      IF (DocNo = '') AND (PurchHeader."Buy-from Vendor No." <> '') THEN
        IF FromPurchRcptHeader.SETCURRENTKEY("Buy-from Vendor No.") THEN BEGIN
          FromPurchRcptHeader."Buy-from Vendor No." := PurchHeader."Buy-from Vendor No.";
          IF FromPurchRcptHeader.FIND('=><') THEN;
        END;
      IF PAGE.RUNMODAL(0,FromPurchRcptHeader) = ACTION::LookupOK THEN
        DocNo := FromPurchRcptHeader."No.";
    END;

    LOCAL PROCEDURE LookupPostedInvoice@9();
    BEGIN
      OnBeforeLookupPostedInvoice(FromPurchInvHeader,PurchHeader);

      FromPurchInvHeader."No." := DocNo;
      IF (DocNo = '') AND (PurchHeader."Buy-from Vendor No." <> '') THEN
        IF FromPurchInvHeader.SETCURRENTKEY("Buy-from Vendor No.") THEN BEGIN
          FromPurchInvHeader."Buy-from Vendor No." := PurchHeader."Buy-from Vendor No.";
          IF FromPurchInvHeader.FIND('=><') THEN;
        END;
      FromPurchInvHeader.FILTERGROUP(2);
      FromPurchInvHeader.SETRANGE("Prepayment Invoice",FALSE);
      FromPurchInvHeader.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,FromPurchInvHeader) = ACTION::LookupOK THEN
        DocNo := FromPurchInvHeader."No.";
    END;

    LOCAL PROCEDURE LookupPostedCrMemo@10();
    BEGIN
      OnBeforeLookupPostedCrMemo(FromPurchCrMemoHeader,PurchHeader);

      FromPurchCrMemoHeader."No." := DocNo;
      IF (DocNo = '') AND (PurchHeader."Buy-from Vendor No." <> '') THEN
        IF FromPurchCrMemoHeader.SETCURRENTKEY("Buy-from Vendor No.") THEN BEGIN
          FromPurchCrMemoHeader."Buy-from Vendor No." := PurchHeader."Buy-from Vendor No.";
          IF FromPurchCrMemoHeader.FIND('=><') THEN;
        END;
      FromPurchCrMemoHeader.FILTERGROUP(2);
      FromPurchCrMemoHeader.SETRANGE("Prepayment Credit Memo",FALSE);
      FromPurchCrMemoHeader.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,FromPurchCrMemoHeader) = ACTION::LookupOK THEN
        DocNo := FromPurchCrMemoHeader."No.";
    END;

    LOCAL PROCEDURE LookupPostedReturn@11();
    BEGIN
      FromReturnShptHeader."No." := DocNo;
      IF (DocNo = '') AND (PurchHeader."Buy-from Vendor No." <> '') THEN
        IF FromReturnShptHeader.SETCURRENTKEY("Buy-from Vendor No.") THEN BEGIN
          FromReturnShptHeader."Buy-from Vendor No." := PurchHeader."Buy-from Vendor No.";
          IF FromReturnShptHeader.FIND('=><') THEN;
        END;
      IF PAGE.RUNMODAL(0,FromReturnShptHeader) = ACTION::LookupOK THEN
        DocNo := FromReturnShptHeader."No.";
    END;

    LOCAL PROCEDURE ValidateIncludeHeader@4();
    BEGIN
      RecalculateLines :=
        (DocType IN [DocType::"Posted Receipt",DocType::"Posted Return Shipment"]) OR NOT IncludeHeader;
    END;

    [External]
    PROCEDURE InitializeRequest@5(NewDocType@1001 : Option;NewDocNo@1000 : Code[20];NewIncludeHeader@1002 : Boolean;NewRecalcLines@1003 : Boolean);
    BEGIN
      DocType := NewDocType;
      DocNo := NewDocNo;
      IncludeHeader := NewIncludeHeader;
      RecalculateLines := NewRecalcLines;
    END;

    [Integration(DEFAULT,TRUE)]
    LOCAL PROCEDURE OnAfterOpenPage@12();
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeLookupPurchDoc@14(VAR FromPurchaseHeader@1000 : Record 38;PurchaseHeader@1001 : Record 38);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeLookupPostedReceipt@15(VAR PurchRcptHeader@1000 : Record 120;PurchaseHeader@1001 : Record 38);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeLookupPostedInvoice@16(VAR FromPurchInvHeader@1000 : Record 122;PurchaseHeader@1001 : Record 38);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeLookupPostedCrMemo@17(VAR FromPurchCrMemoHdr@1000 : Record 124;PurchaseHeader@1001 : Record 38);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeValidateIncludeHeader@1016(VAR DoIncludeHeader@1000 : Boolean;DocType@1001 : Option);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnPreReportOnBeforeCopyPurchaseDoc@13(VAR CopyDocumentMgt@1000 : Codeunit 6620);
    BEGIN
    END;

    PROCEDURE CheckVendLegdEntryInProposal@1100525000(FromPurchInvHeader@1100525001 : Record 122);
    VAR
      VendLedgEntry@1100525000 : Record 25;
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

