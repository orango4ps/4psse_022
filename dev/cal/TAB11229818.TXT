OBJECT Table 11229818 Project Portal Metadata
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Standard Data", FALSE);
               InsertAdditionalDataRec;
             END;

    OnModify=BEGIN
               IF (NOT "Standard Data") THEN
                 Mandatory := FALSE;
             END;

    OnDelete=VAR
               MetadataTranslationPPT@1100528300 : Record 11229819;
             BEGIN
               TESTFIELD("Standard Data", FALSE);
               MetadataTranslationPPT.SETRANGE("Subsite Type", "Subsite Type");
               MetadataTranslationPPT.SETRANGE("Metadata Code", Code);
               MetadataTranslationPPT.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=ENU=Project Portal Metadata;
    LookupPageID=Page11229193;
    DrillDownPageID=Page11229193;
  }
  FIELDS
  {
    { 5   ;   ;Subsite Type        ;Option        ;CaptionML=ENU=Subsite Type;
                                                   OptionCaptionML=ENU=Project,Service Location;
                                                   OptionString=Project,ServLoc;
                                                   Editable=No }
    { 10  ;   ;Code                ;Text30        ;TableRelation=IF (Standard Data=CONST(No)) "Additional Data (PPT)".Code WHERE (Subsite Type=FIELD(Subsite Type));
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Standard Data       ;Boolean       ;CaptionML=ENU=Standard Data;
                                                   Editable=No }
    { 30  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                UpdateDefaultTranslationDescription;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Show in View        ;Boolean       ;OnValidate=BEGIN
                                                                CASE Code OF
                                                                  'SCDocumentType':
                                                                    FIELDERROR("Show in View");
                                                                END;
                                                                CheckModifyViewAllowed;
                                                              END;

                                                   CaptionML=ENU=Show in View }
    { 50  ;   ;Mandatory           ;Boolean       ;OnValidate=BEGIN
                                                                CheckModifyViewAllowed;
                                                                IF (Mandatory) THEN
                                                                  "Show in View" := TRUE;
                                                              END;

                                                   CaptionML=[ENU=Mandatory;
                                                              NOR=Obligatorisk;
                                                              SVE=Obligatoriskt] }
    { 60  ;   ;Position            ;Integer       ;OnValidate=BEGIN
                                                                IF (Position > 0) THEN BEGIN
                                                                  CheckModifyViewAllowed;
                                                                  CheckPosition;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Position;
                                                              NOR=Posisjon;
                                                              SVE=Position];
                                                   MinValue=0;
                                                   MaxValue=99;
                                                   BlankZero=Yes }
    { 70  ;   ;Searchable          ;Boolean       ;OnValidate=BEGIN
                                                                CheckModifySearchAllowed;
                                                              END;

                                                   CaptionML=ENU=Show in Search Center }
    { 80  ;   ;Refiner Code        ;Text30        ;OnValidate=BEGIN
                                                                IF ("Refiner Code" <> '') THEN BEGIN
                                                                  TESTFIELD(Searchable, TRUE);
                                                                  CASE Code OF
                                                                    'FileType',
                                                                    'Modified',
                                                                    'ModifiedBy':
                                                                      FIELDERROR(Searchable);
                                                                  END;
                                                                  CheckModifySearchAllowed;
                                                                  CheckRefinerCode;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Refiner Code }
    { 90  ;   ;Refiner Description ;Text50        ;OnValidate=BEGIN
                                                                CheckRefinerDescription;
                                                                UpdateDefaultTranslationRefiner;
                                                              END;

                                                   CaptionML=ENU=Refiner Description }
    { 100 ;   ;Refiner Type        ;Option        ;OnValidate=BEGIN
                                                                CheckModifyAllowed;
                                                              END;

                                                   CaptionML=ENU=Refiner Type;
                                                   OptionCaptionML=ENU=" ,Text,,Value,DateTime";
                                                   OptionString=[ ,Text,,Value,DateTime] }
    { 110 ;   ;Refiner Position    ;Integer       ;OnValidate=BEGIN
                                                                IF ("Refiner Position" > 0) THEN BEGIN
                                                                  CheckModifySearchAllowed;
                                                                  CheckRefinerPosition;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Refiner Position;
                                                   MinValue=0;
                                                   MaxValue=99;
                                                   BlankZero=Yes }
    { 200 ;   ;View Allowed        ;Boolean       ;CaptionML=ENU=View Allowed;
                                                   Editable=No }
    { 210 ;   ;Search Allowed      ;Boolean       ;CaptionML=ENU=Search Allowed;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Subsite Type,Code                       ;Clustered=Yes }
    {    ;Show in View,Position                    }
    {    ;Searchable,Refiner Position              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Standard Data,Description           }
  }
  CODE
  {

    PROCEDURE CreateStandardCodes@1100528300(SubsiteType@1100528300 : Option);
    BEGIN
      //DeleteMetadata(0, 'SCContactPerson');

      CreateStandardCode(SubsiteType, 'Author', FALSE, TRUE, FALSE, 'Created by', 'Aangemaakt door', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'Created', FALSE, TRUE, FALSE, 'Created on', 'Aangemaakt op', 0, '', 4, 0);
      CreateStandardCode(SubsiteType, 'CheckoutUser', FALSE, TRUE, FALSE, 'Checked Out User', 'Uitgechecked door', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'DocIcon', FALSE, TRUE, FALSE, 'Document Icon', 'Document icoon', 1, '', 1, 0);
      CreateStandardCode(SubsiteType, 'FileType', FALSE, FALSE, TRUE, 'FileType', 'Bestandtype', 0, 'FileType', 1, 1);
      CreateStandardCode(SubsiteType, 'LinkFilename', FALSE, TRUE, FALSE, 'Name', 'Naam', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'Modified', FALSE, TRUE, TRUE, 'Modified', 'Gewijzigd', 0, 'LastModifiedTime', 4, 2);
      CreateStandardCode(SubsiteType, 'Editor', FALSE, TRUE, FALSE, 'Modified By', 'Gewijzigd door', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCBody', FALSE, TRUE, TRUE, 'Body', 'Inhoud', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCCc', FALSE, TRUE, TRUE, 'CC', 'CC', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCRelatedCompany', FALSE, TRUE, TRUE, 'Related Company', 'Relatie', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCContactPerson', FALSE, TRUE, TRUE, 'Contact Person', 'Contactpersoon', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCDescription', FALSE, TRUE, FALSE, 'Description', 'Omschrijving', 3, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCDocumentDate', FALSE, TRUE, TRUE, 'Document Date', 'Documentdatum', 0, '', 4, 0);
      CreateStandardCode(SubsiteType, 'SCDocumentId', FALSE, TRUE, FALSE, 'Navision Document Id', 'Navision document-Id', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCDocumentNo', FALSE, TRUE, FALSE, 'Document Number', 'Documentnummer', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCDocumentStatus', FALSE, TRUE, TRUE, 'Document Status', 'Documentstatus', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCDocumentType', TRUE, TRUE, TRUE, 'Document Type', 'Documentsoort', 2, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCDocumentVersion', FALSE, TRUE, TRUE, 'Document Version', 'Document Versie', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCElementCode', FALSE, TRUE, TRUE, 'Element Code', 'Elementcode', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCFrom', FALSE, TRUE, TRUE, 'From', 'Van', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCModificationDate', FALSE, TRUE, FALSE, 'Change Date', 'Wijzigingsdatum', 0, '', 4, 0);
      CreateStandardCode(SubsiteType, 'SCSiteStatus', FALSE, TRUE, TRUE, 'Project Status', 'Projectstatus', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCReceivedOn', FALSE, TRUE, TRUE, 'Received on', 'Ontvangen op', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCRemark', FALSE, TRUE, FALSE, 'Remark', 'Opmerking', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCTo', FALSE, TRUE, TRUE, 'To', 'Naar', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, '_UIVersionString', FALSE, TRUE, FALSE, 'Version', 'Versie', 0, '', 0, 0);
      CreateStandardCode(SubsiteType, 'SCModifiedByRelation', FALSE, TRUE, TRUE, 'Modified By Relation', 'Gewijzigd door relatie', 0, '', 1, 0);
      CreateStandardCode(SubsiteType, 'SCPlotNo', FALSE, TRUE, TRUE, 'Plots', 'Bouwnummers', 0, '', 1, 0);
    END;

    LOCAL PROCEDURE CreateStandardCode@1100528302(SubsiteType@1100528304 : Option;MetadataCode@1100528301 : Text[30];MandatoryField@1100525006 : Boolean;ViewAllowed@1100525003 : Boolean;RefinerAllowed@1100525004 : Boolean;DescriptionENU@1100528302 : Text[50];DescriptionNLD@1100528303 : Text[50];Position@1100525002 : Integer;RefinerCode@1100525005 : Text[30];RefinerType@1100525001 : Option;RefinerPosition@1100525000 : Integer);
    VAR
      SharePointSetup4PS@1100528305 : Record 11012730;
      ProjectPortalMetadata@1100528300 : Record 11229818;
    BEGIN
      SharePointSetup4PS.GET;
      IF (ProjectPortalMetadata.GET(SubsiteType, MetadataCode)) THEN BEGIN
        IF (SharePointSetup4PS."Language ID" = 1043) THEN BEGIN
          CreateDefaultTranslationDescription(SubsiteType, 0, MetadataCode, 1033, DescriptionENU);
          CreateDefaultTranslationDescription(SubsiteType, 1, MetadataCode, 1033, DescriptionENU);
          ProjectPortalMetadata.VALIDATE(Description, DescriptionNLD);
          ProjectPortalMetadata.VALIDATE("Refiner Description", DescriptionNLD);
        END ELSE BEGIN
          CreateDefaultTranslationDescription(SubsiteType, 0, MetadataCode, 1043, DescriptionNLD);
          CreateDefaultTranslationDescription(SubsiteType, 1, MetadataCode, 1043, DescriptionNLD);
          ProjectPortalMetadata.VALIDATE(Description, DescriptionENU);
          ProjectPortalMetadata.VALIDATE("Refiner Description", DescriptionENU);
        END;
        ProjectPortalMetadata.Mandatory := MandatoryField;
        ProjectPortalMetadata."View Allowed" := ViewAllowed;
        ProjectPortalMetadata."Search Allowed" := RefinerAllowed;
        ProjectPortalMetadata."Show in View" := (Position > 0);
        ProjectPortalMetadata.Position := Position;
        ProjectPortalMetadata."Refiner Code" := RefinerCode;
        ProjectPortalMetadata."Refiner Type" := RefinerType;
        ProjectPortalMetadata.Searchable := (RefinerPosition > 0);
        ProjectPortalMetadata.MODIFY;
        EXIT;
      END;

      ProjectPortalMetadata.INIT;
      ProjectPortalMetadata."Subsite Type" := SubsiteType;
      ProjectPortalMetadata.Code := MetadataCode;
      ProjectPortalMetadata."Standard Data" := TRUE;
      ProjectPortalMetadata.INSERT;

      ProjectPortalMetadata.Mandatory := MandatoryField;
      ProjectPortalMetadata."View Allowed" := ViewAllowed;
      ProjectPortalMetadata."Search Allowed" := RefinerAllowed;
      IF (SharePointSetup4PS."Language ID" = 1043) THEN BEGIN
        CreateDefaultTranslationDescription(SubsiteType, 0, MetadataCode, 1033, DescriptionENU);
        CreateDefaultTranslationDescription(SubsiteType, 1, MetadataCode, 1033, DescriptionENU);
        ProjectPortalMetadata.VALIDATE(Description, DescriptionNLD);
        ProjectPortalMetadata.VALIDATE("Refiner Description", DescriptionNLD);
      END ELSE BEGIN
        CreateDefaultTranslationDescription(SubsiteType, 0, MetadataCode, 1043, DescriptionNLD);
        CreateDefaultTranslationDescription(SubsiteType, 1, MetadataCode, 1043, DescriptionNLD);
        ProjectPortalMetadata.VALIDATE(Description, DescriptionENU);
        ProjectPortalMetadata.VALIDATE("Refiner Description", DescriptionENU);
      END;
      ProjectPortalMetadata."Show in View" := (Position > 0);
      ProjectPortalMetadata.Position := Position;
      ProjectPortalMetadata."Refiner Code" := RefinerCode;
      ProjectPortalMetadata."Refiner Type" := RefinerType;
      ProjectPortalMetadata.Searchable := (RefinerPosition > 0);
      ProjectPortalMetadata.VALIDATE("Refiner Position", RefinerPosition);
      ProjectPortalMetadata.MODIFY;
    END;

    LOCAL PROCEDURE CreateDefaultTranslationDescription@1100528310(iSubsiteType@1100528301 : Option;iType@1100528302 : Option;iCode@1100528303 : Text[30];iLanguageId@1100528305 : Integer;iDescription@1100528306 : Text[50]);
    VAR
      MetadataTranslationPPT@1100528300 : Record 11229819;
    BEGIN
      IF (NOT MetadataTranslationPPT.GET(iSubsiteType, iType, iCode, iLanguageId)) THEN BEGIN
        MetadataTranslationPPT."Subsite Type" := iSubsiteType;
        MetadataTranslationPPT.Type := iType;
        MetadataTranslationPPT."Metadata Code" := iCode;
        MetadataTranslationPPT."Language Id" := iLanguageId;
        MetadataTranslationPPT.INSERT;
      END;
      MetadataTranslationPPT.Description := iDescription;
      MetadataTranslationPPT.MODIFY;
    END;

    LOCAL PROCEDURE CheckPosition@1100525013();
    VAR
      ProjectPortalMetadata@1100528300 : Record 11229818;
    BEGIN
      ProjectPortalMetadata.SETRANGE("Subsite Type", "Subsite Type");
      ProjectPortalMetadata.SETFILTER(Code, '<>%1', Code);
      ProjectPortalMetadata.SETRANGE(Position, Position);
      IF (ProjectPortalMetadata.FINDFIRST) THEN
        FIELDERROR(Position);
    END;

    LOCAL PROCEDURE CheckRefinerCode@1100528304();
    VAR
      ProjectPortalMetadata@1100528301 : Record 11229818;
    BEGIN
      IF ("Refiner Code" = '') THEN
        EXIT;
      ProjectPortalMetadata.SETRANGE("Subsite Type", "Subsite Type");
      ProjectPortalMetadata.SETFILTER(Code, '<>%1', Code);
      ProjectPortalMetadata.SETRANGE("Refiner Code", "Refiner Code");
      IF (ProjectPortalMetadata.FINDFIRST) THEN
        FIELDERROR("Refiner Code");
    END;

    LOCAL PROCEDURE CheckRefinerDescription@1100528305();
    VAR
      ProjectPortalMetadata@1100528300 : Record 11229818;
    BEGIN
      IF ("Refiner Description" = '') THEN
        EXIT;
      ProjectPortalMetadata.SETRANGE("Subsite Type", "Subsite Type");
      ProjectPortalMetadata.SETFILTER(Code, '<>%1', Code);
      ProjectPortalMetadata.SETRANGE("Refiner Description", "Refiner Description");
      IF (ProjectPortalMetadata.FINDFIRST) THEN
        FIELDERROR("Refiner Description");
    END;

    LOCAL PROCEDURE CheckRefinerPosition@1100528306();
    VAR
      ProjectPortalMetadata@1100528300 : Record 11229818;
    BEGIN
      ProjectPortalMetadata.SETRANGE("Subsite Type", "Subsite Type");
      ProjectPortalMetadata.SETFILTER(Code, '<>%1', Code);
      ProjectPortalMetadata.SETRANGE("Refiner Position", "Refiner Position");
      IF (ProjectPortalMetadata.FINDFIRST) THEN
        FIELDERROR("Refiner Position");
    END;

    LOCAL PROCEDURE CheckModifyAllowed@1100525001();
    BEGIN
      TESTFIELD("Standard Data", FALSE);
    END;

    LOCAL PROCEDURE CheckModifyViewAllowed@1100525007();
    BEGIN
      TESTFIELD("View Allowed", TRUE);
    END;

    LOCAL PROCEDURE CheckModifySearchAllowed@1100525000();
    BEGIN
      TESTFIELD("Search Allowed", TRUE);
    END;

    LOCAL PROCEDURE UpdateDefaultTranslationDescription@1100525010();
    VAR
      SharePointSetup4PS@1100528304 : Record 11012730;
      MetadataTranslationPPT@1100528300 : Record 11229819;
    BEGIN
      SharePointSetup4PS.GET;
      IF (NOT MetadataTranslationPPT.GET("Subsite Type", 0, Code, SharePointSetup4PS."Language ID")) THEN BEGIN
        MetadataTranslationPPT."Subsite Type" := "Subsite Type";
        MetadataTranslationPPT.Type := MetadataTranslationPPT.Type::Description;
        MetadataTranslationPPT."Metadata Code" := Code;
        MetadataTranslationPPT."Language Id" := SharePointSetup4PS."Language ID";
        MetadataTranslationPPT.INSERT;
      END;
      MetadataTranslationPPT.Description := Description;
      MetadataTranslationPPT.MODIFY;
    END;

    LOCAL PROCEDURE UpdateDefaultTranslationRefiner@1100525012();
    VAR
      SharePointSetup4PS@1100528301 : Record 11012730;
      MetadataTranslationPPT@1100528300 : Record 11229819;
    BEGIN
      SharePointSetup4PS.GET;
      IF (NOT MetadataTranslationPPT.GET("Subsite Type", 1, Code, SharePointSetup4PS."Language ID")) THEN BEGIN
        MetadataTranslationPPT."Subsite Type" := "Subsite Type";
        MetadataTranslationPPT.Type := MetadataTranslationPPT.Type::Refiner;
        MetadataTranslationPPT."Metadata Code" := Code;
        MetadataTranslationPPT."Language Id" := SharePointSetup4PS."Language ID";
        MetadataTranslationPPT.INSERT;
      END;
      MetadataTranslationPPT.Description := "Refiner Description";
      MetadataTranslationPPT.MODIFY;
    END;

    PROCEDURE UpdateSearchCenter@1100525009();
    VAR
      DocumentPortalProject@1100525000 : Codeunit 11012948;
    BEGIN
      DocumentPortalProject.UpdateSearchCenter;
    END;

    LOCAL PROCEDURE InsertAdditionalDataRec@1100525002();
    VAR
      AdditionalDataPPT@1100525000 : Record 11229820;
    BEGIN
      IF (AdditionalDataPPT.GET("Subsite Type", Code)) THEN BEGIN
        VALIDATE(Description, AdditionalDataPPT.Description);
        VALIDATE("Refiner Description", AdditionalDataPPT.Description);
        "View Allowed" := TRUE;
        "Search Allowed" := TRUE;
        CASE AdditionalDataPPT.Type OF
          AdditionalDataPPT.Type::Option:
            "Refiner Type" := "Refiner Type"::Text;
          ELSE
            "Refiner Type" := AdditionalDataPPT.Type + 1;
        END;
      END;
    END;

    LOCAL PROCEDURE DeleteMetadata@1100529900(SubsiteType@1100529901 : Option;CodeIn@1100529902 : Text);
    VAR
      ProjectPortalMetadata@1100529900 : Record 11229818;
      MetadataTranslationPPT@1100529903 : Record 11229819;
    BEGIN
      IF (ProjectPortalMetadata.GET(SubsiteType, CodeIn)) THEN  BEGIN
        MetadataTranslationPPT.SETRANGE("Subsite Type", ProjectPortalMetadata."Subsite Type");
        MetadataTranslationPPT.SETRANGE("Metadata Code", ProjectPortalMetadata.Code);
        MetadataTranslationPPT.DELETEALL;
        ProjectPortalMetadata.DELETE;
      END;
    END;

    BEGIN
    END.
  }
}

