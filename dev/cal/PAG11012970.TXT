OBJECT Page 11012970 KOMO Installations by Plot
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=KOMO Installationen pro Baunr.;
               ENU=KOMO Installations by Plot;
               NLD=KOMO Installatie per bouwnr.];
    SourceTable=Table11012968;
    PageType=List;
    AutoSplitKey=Yes;
    ActionList=ACTIONS
    {
      { 1100525017;  ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100525018;1 ;Action    ;
                      CaptionML=[DEU=Belege;
                                 ENU=Documents;
                                 NLD=Documenten;
                                 NOR=Dokumenter;
                                 SVE=Dokument];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Documents;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentFilters@1100525001 : Codeunit 11012432;
                                 RecRef@1100525000 : RecordRef;
                               BEGIN
                                 RecRef.GETTABLE(Rec);
                                 DocumentFilters.CreateTempFilterAndExecute1(RecRef, '', FALSE, TRUE);
                               END;
                                }
      { 1100525019;1 ;Action    ;
                      CaptionML=[DEU=Abweichungen;
                                 ENU=Diviations;
                                 NLD=Afwijkingen];
                      RunObject=Page 11012971;
                      RunPageView=SORTING(Project No.,Plot No.,Line No.,Deviation);
                      RunPageLink=Project No.=FIELD(Project No.),
                                  Plot No.=FIELD(Plot No.),
                                  Line No.=FIELD(Line No.);
                      Image=Change }
      { 1100525020;1 ;Action    ;
                      CaptionML=[DEU=Messapparatur;
                                 ENU=Measuring Equipment;
                                 NLD=Meetapparatuur;
                                 NOR=MÜleutstyr;
                                 SVE=MÑtutrustning];
                      RunObject=Page 11012972;
                      RunPageView=SORTING(Project No.,Plot No.,Line No.,Measuring Equip.);
                      RunPageLink=Project No.=FIELD(Project No.),
                                  Plot No.=FIELD(Plot No.),
                                  Line No.=FIELD(Line No.);
                      Image=UnitOfMeasure }
      { 1100525021;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525022;1 ;Action    ;
                      CaptionML=[DEU=Erzeugen Installationen nach Baunr.;
                                 ENU=Generate Installations by Plot;
                                 NLD=Genereren installatie per bouwnummer;
                                 NOR=Generer installasjoner etter tegning;
                                 SVE=Generera inst. efter plats];
                      Image=CreateRating;
                      OnAction=VAR
                                 KOMOInstallationbyPlot@1100525000 : Record 11012968;
                                 GenerateInstallationsbyPlot@1100525001 : Report 11012960;
                               BEGIN
                                 KOMOInstallationbyPlot.SETRANGE("Project No.", "Project No.");
                                 COPYFILTER("Plot No.", KOMOInstallationbyPlot."Plot No.");
                                 GenerateInstallationsbyPlot.SETTABLEVIEW(KOMOInstallationbyPlot);
                                 GenerateInstallationsbyPlot.RUNMODAL;
                               END;
                                }
      { 1100525023;1 ;Action    ;
                      CaptionML=[DEU=Installationen freigeben;
                                 ENU=Release Installations;
                                 NLD=Vrijgeven installaties];
                      Image=ReleaseDoc;
                      OnAction=BEGIN
                                 OptionNoRelease := STRMENU(Text002);

                                 IF (OptionNoRelease > 0) THEN
                                   ReleaseInstallations;
                               END;
                                }
      { 1100525024;1 ;Action    ;
                      CaptionML=[DEU=Word-Dok&umente generieren;
                                 ENU=Generate Word Doc&uments;
                                 NLD=Genereren Word doc&umenten];
                      Image=CreateDocuments;
                      OnAction=VAR
                                 GenerateKOMOWordDocuments@1100525000 : Page 11012977;
                               BEGIN
                                 GenerateKOMOWordDocuments.FillDefaults("Project No.", GETFILTER("Plot No."));
                                 GenerateKOMOWordDocuments.RUNMODAL;
                               END;
                                }
      { 1100525025;1 ;Action    ;
                      CaptionML=[DEU=Inspektionsseite;
                                 ENU=Inspection Page;
                                 NLD=Controleverklaring];
                      Image=CheckList;
                      OnAction=VAR
                                 Plot@1100525000 : Record 11012500;
                                 Contact@1100525001 : Record 5050;
                                 DocumentTemplate@1100525002 : Record 11012401;
                                 DocumentAction@1100525003 : Code[20];
                               BEGIN
                                 IF ("Project No." = '') OR ("Plot No." = '') OR (Installation = '') THEN
                                   EXIT;
                                 IF (NOT Plot.GET("Project No.", "Plot No.")) THEN
                                   EXIT;
                                 DocumentAction := "Project No." + '-' + "Plot No.";

                                 IF (Plot.Buyer = '') THEN BEGIN
                                   Plot.TESTFIELD(Principal);
                                   IF Contact.GetContactByCustomer(Plot.Principal, Contact, TRUE) THEN
                                     EditDocuments(
                                       STRSUBSTNO('%1', DocumentTemplate.Type::"KOMO Inspection"), Contact."No.", '', DocumentAction, FALSE, '');
                                 END ELSE BEGIN
                                   IF (Contact.GET(Plot.Buyer)) THEN BEGIN
                                     IF (Contact.IsContactPerson) THEN
                                       EditDocuments(
                                         STRSUBSTNO('%1', DocumentTemplate.Type::"KOMO Inspection"), Contact."Company No.", Contact."No.", DocumentAction, FALSE, '')
                                     ELSE
                                       EditDocuments(
                                         STRSUBSTNO('%1', DocumentTemplate.Type::"KOMO Inspection"), Contact."No.", '', DocumentAction, FALSE, '');
                                   END;
                                 END;
                               END;
                                }
      { 1100525026;1 ;Action    ;
                      CaptionML=[DEU=Beendete Seite;
                                 ENU=Finished Page;
                                 NLD=Gereedmelding];
                      Image=Change;
                      OnAction=VAR
                                 Plot@1100525000 : Record 11012500;
                                 Contact@1100525001 : Record 5050;
                                 DocumentTemplate@1100525002 : Record 11012401;
                                 DocumentAction@1100525003 : Code[20];
                               BEGIN
                                 IF ("Project No." = '') OR ("Plot No." = '') OR (Installation = '') THEN
                                   EXIT;
                                 IF (NOT Plot.GET("Project No.", "Plot No.")) THEN
                                   EXIT;
                                 DocumentAction := "Plot No.";

                                 IF (Plot.Buyer = '') THEN BEGIN
                                   Plot.TESTFIELD(Principal);
                                   IF Contact.GetContactByCustomer(Plot.Principal, Contact, TRUE) THEN
                                     EditDocuments(
                                       STRSUBSTNO('%1', DocumentTemplate.Type::"KOMO Ready"), Contact."No.", '', DocumentAction, FALSE, '');
                                 END ELSE BEGIN
                                   IF (Contact.GET(Plot.Buyer)) THEN BEGIN
                                     IF (Contact.IsContactPerson) THEN
                                       EditDocuments(
                                         STRSUBSTNO('%1', DocumentTemplate.Type::"KOMO Ready"), Contact."Company No.", Contact."No.", DocumentAction, FALSE, '')
                                     ELSE
                                       EditDocuments(
                                         STRSUBSTNO('%1', DocumentTemplate.Type::"KOMO Ready"), Contact."No.", '', DocumentAction, FALSE, '');
                                   END;
                                 END;
                               END;
                                }
      { 1100525027;1 ;Action    ;
                      CaptionML=[DEU=Belege öbersicht;
                                 ENU=Document List;
                                 NLD=Documentoverzicht;
                                 NOR=Dokumentliste;
                                 SVE=Dokumentlista];
                      Image=Documents;
                      OnAction=VAR
                                 DocumentProperties@1100525001 : Record 11012746;
                                 DocumentTemplate@1100525002 : Record 11012401;
                                 Plot@1100525003 : Record 11012500;
                                 Contact@1100525004 : Record 5050;
                                 ContactBusinessRelation@1100525005 : Record 5054;
                                 DocumentLinkManagement@1100525006 : Codeunit 11012401;
                                 RecRef@1100525007 : RecordRef;
                                 TemplateFilter@1100525008 : Text;
                                 PlotContact@1100525009 : Code[20];
                               BEGIN
                                 //This function shows all KOMO documents related to the project
                                 //there is a small "but": the documentgroup of all buyers must be the same because
                                 //there must be a hard filter on this group
                                 IF (NOT Plot.GET("Project No.","Plot No.")) THEN
                                   EXIT;

                                 PlotContact := '';
                                 IF (Plot.Buyer = '') THEN BEGIN
                                   PlotContact := Plot.Principal;
                                   ContactBusinessRelation.SETCURRENTKEY("Link to Table", "No.");
                                   ContactBusinessRelation.SETRANGE("Link to Table", ContactBusinessRelation."Link to Table"::Customer);
                                   ContactBusinessRelation.SETRANGE("No.", PlotContact);
                                   IF (NOT ContactBusinessRelation.FINDFIRST) THEN
                                     ERROR(Text004, PlotContact);
                                 END ELSE
                                   ContactBusinessRelation."Contact No." := Plot.Buyer;
                                 IF (ContactBusinessRelation."Contact No." = '') THEN
                                   EXIT;

                                 Contact.GET(ContactBusinessRelation."Contact No.");
                                 Contact.TESTFIELD("Document Group");

                                 DocumentTemplate.SETRANGE("Document Group", Contact."Document Group");
                                 DocumentTemplate.SETFILTER(Type, '%1|%2', DocumentTemplate.Type::"KOMO Inspection", DocumentTemplate.Type::"KOMO Ready");
                                 IF (DocumentTemplate.FINDSET) THEN BEGIN
                                   REPEAT
                                     IF TemplateFilter <> '' THEN
                                       TemplateFilter += '|';
                                     TemplateFilter += DocumentTemplate."Template Code";
                                   UNTIL (DocumentTemplate.NEXT = 0);
                                 END;

                                 DocumentProperties.RESET;
                                 DocumentProperties.SETRANGE("Internal Company", COMPANYNAME);
                                 DocumentProperties.SETRANGE("Document Group", Contact."Document Group");
                                 DocumentProperties.SETFILTER("Document Template", TemplateFilter);
                                 DocumentProperties.SETCURRENTKEY("Document Group","Document Template");

                                 RecRef.GETTABLE(Plot);
                                 DocumentLinkManagement.OpenDocuments2(DocumentProperties, RecRef, '', FALSE, TRUE)
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;0;Container;
                ContainerType=ContentArea }

    { 1100525001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100525002;2;Field  ;
                SourceExpr="Project No." }

    { 1100525003;2;Field  ;
                SourceExpr="Plot No." }

    { 1100525004;2;Field  ;
                SourceExpr=Discipline }

    { 1100525005;2;Field  ;
                SourceExpr="Discipline Description";
                Visible=false }

    { 1100525006;2;Field  ;
                SourceExpr=Installation }

    { 1100525007;2;Field  ;
                SourceExpr="Installation Description";
                Visible=false }

    { 1100525008;2;Field  ;
                SourceExpr=Inspection }

    { 1100525009;2;Field  ;
                SourceExpr="Inspection Description";
                Visible=false }

    { 1100525010;2;Field  ;
                SourceExpr=Inspector }

    { 1100525011;2;Field  ;
                SourceExpr="Inspector Name" }

    { 1100525012;2;Field  ;
                SourceExpr="Measuring Equip. 1" }

    { 1100525013;2;Field  ;
                SourceExpr="Inspection Date" }

    { 1100525014;2;Field  ;
                SourceExpr="Date Released" }

    { 1100525015;2;Field  ;
                SourceExpr="Measure Equipment";
                Visible=false }

    { 1100525016;2;Field  ;
                SourceExpr=Deviations }

  }
  CODE
  {
    VAR
      Text000@1100525004 : TextConst 'DEU=nicht zulÑssig, nicht vorhanden;ENU=not allowed, not present;NLD=niet toegestaan, niet aanwezig';
      Text002@1100525002 : TextConst 'DEU=Freigabe, wenn keine Abweichungen, Freigabe, wenn Abweichungen akzeptiert wurden, Beide;ENU=Release if no deviations,Release if deviations accepted,Both;NLD=Vrijgeven als geen afwijkingen,Vrijgeven als afwijkingen geaccepteerd,Beide';
      Text004@1100525000 : TextConst 'DEU=Es muss zuerst ein Kontakt mit Debitor %1 verknÅpft werden.;ENU=First Associate a Prospect to Customer %1.;NLD=Er moet eerst een relatie worden gekoppeld aan klant %1.;NOR=Tilknytt fõrst et prospekt til kunde %1.;SVE=Associera fîrst en tilltÑnkt kund till kund %1.';
      OptionNoRelease@1100525005 : Integer;

    LOCAL PROCEDURE ReleaseInstallations@1100525003();
    VAR
      KOMOInstallationbyPlot@1100525000 : Record 11012968;
      Release@1100525001 : Boolean;
    BEGIN
      KOMOInstallationbyPlot.COPY(Rec);

      IF (NOT KOMOInstallationbyPlot.FINDSET) THEN
        EXIT;
      REPEAT
        Release := FALSE;
        IF (KOMOInstallationbyPlot."Date Released" = 0D) AND (KOMOInstallationbyPlot."Inspection Date" <> 0D) THEN BEGIN
          KOMOInstallationbyPlot.CALCFIELDS(Deviations, "Final Inspection");
          IF KOMOInstallationbyPlot."Final Inspection" THEN BEGIN
            CASE OptionNoRelease OF
              1:  BEGIN //No Deviations
                IF NOT KOMOInstallationbyPlot.Deviations THEN
                  Release := TRUE;
              END;
              2:  BEGIN //Accepted Deviations
                IF KOMOInstallationbyPlot.Deviations THEN BEGIN
                  IF CheckDeviationsAccepted(KOMOInstallationbyPlot."Project No.", KOMOInstallationbyPlot."Plot No.", KOMOInstallationbyPlot."Line No.") THEN
                    Release := TRUE;
                END;
              END;
              3: BEGIN  //Both
                IF NOT KOMOInstallationbyPlot.Deviations THEN
                  Release := TRUE
                ELSE BEGIN
                  IF CheckDeviationsAccepted(KOMOInstallationbyPlot."Project No.", KOMOInstallationbyPlot."Plot No.", KOMOInstallationbyPlot."Line No.") THEN
                    Release := TRUE;
                END;
              END;
            END;
            IF Release THEN BEGIN
              KOMOInstallationbyPlot."Date Released" := TODAY;
              KOMOInstallationbyPlot.MODIFY;
            END;
          END;
        END;
      UNTIL (KOMOInstallationbyPlot.NEXT = 0);
    END;

    LOCAL PROCEDURE CheckMeasureEquip@1100525005(IMeasureEquip@1100525000 : Code[10]);
    VAR
      MeasuringEqpbyInstallation@1100525001 : Record 11012967;
    BEGIN
      IF (IMeasureEquip = '') THEN
        EXIT;

      IF (NOT MeasuringEqpbyInstallation.GET(Installation, IMeasureEquip)) THEN BEGIN
        MeasuringEqpbyInstallation.Installation := Installation;
        MeasuringEqpbyInstallation."Measuring Equip." := IMeasureEquip;
        MeasuringEqpbyInstallation.FIELDERROR("Measuring Equip.", Text000);
      END;
    END;

    LOCAL PROCEDURE CheckDeviationsAccepted@1100525006(IProjectNo@1100525002 : Code[20];IPlotNo@1100525001 : Code[20];ILineNo@1100525000 : Integer) : Boolean;
    VAR
      DeviationbyPlotInstallation@1100525003 : Record 11012969;
    BEGIN
      DeviationbyPlotInstallation.SETRANGE("Project No.", IProjectNo);
      DeviationbyPlotInstallation.SETRANGE("Plot No.", IPlotNo);
      DeviationbyPlotInstallation.SETRANGE("Line No.", ILineNo);
      DeviationbyPlotInstallation.SETRANGE("Date Accepted", 0D);
      IF (DeviationbyPlotInstallation.FINDFIRST) THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

