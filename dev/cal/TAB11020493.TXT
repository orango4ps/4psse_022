OBJECT Table 11020493 Plant Order Receipt Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               PlantOrderReceivedQuantity@1100529000 : Record 11020494;
             BEGIN
               PlantOrderReceivedQuantity.SETRANGE("Plant Order No.", "Plant Order No.");
               PlantOrderReceivedQuantity.SETRANGE("Plant Order Line No.", "Plant Order Line No.");
               PlantOrderReceivedQuantity.DELETEALL;
             END;

    CaptionML=ENU=Plant Order Receipt Line;
  }
  FIELDS
  {
    { 10  ;   ;Plant Order No.     ;Code20        ;TableRelation="Plant Order";
                                                   OnLookup=VAR
                                                              PlantOrder@1100529000 : Record 11012556;
                                                            BEGIN
                                                              PlantOrder.FILTERGROUP(4);
                                                              PlantOrder.SETRANGE("No.", "Plant Order No.");
                                                              PlantOrder.FILTERGROUP(0);
                                                              PAGE.RUNMODAL(PAGE::"Posted Plant Order List", PlantOrder);
                                                            END;

                                                   CaptionML=[ENU=Plant Order No.;
                                                              NOR=Maskinordrenummer;
                                                              SVE=Maskinordernummer];
                                                   Editable=No }
    { 20  ;   ;Plant Order Line No.;Integer       ;TableRelation="Plant Order Line"."Line No." WHERE (Plant Order No.=FIELD(Plant Order No.));
                                                   OnLookup=VAR
                                                              PlantOrderLine@1100529000 : Record 11012557;
                                                            BEGIN
                                                              PlantOrderLine.FILTERGROUP(4);
                                                              PlantOrderLine.SETRANGE("Plant Order No.", "Plant Order No.");
                                                              PlantOrderLine.FILTERGROUP(0);
                                                              PlantOrderLine."Plant Order No." := "Plant Order No.";
                                                              PlantOrderLine."Line No." := "Plant Order Line No.";
                                                              PAGE.RUNMODAL(PAGE::"Plant Order Line List", PlantOrderLine);
                                                            END;

                                                   CaptionML=[ENU=Plant Order Line No.;
                                                              NOR=Maskinordre, Radnr.;
                                                              SVE=Maskinorder, radnr];
                                                   Editable=No }
    { 25  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=Plant,,Item;
                                                   OptionString=Plant,,Item;
                                                   Editable=No }
    { 30  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   Editable=No }
    { 35  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr];
                                                   Editable=No }
    { 40  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr];
                                                   Editable=No }
    { 50  ;   ;Description         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order Line".Description WHERE (Plant Order No.=FIELD(Plant Order No.),
                                                                                                            Line No.=FIELD(Plant Order Line No.)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 51  ;   ;Description 2       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order Line"."Description 2" WHERE (Plant Order No.=FIELD(Plant Order No.),
                                                                                                                Line No.=FIELD(Plant Order Line No.)));
                                                   CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2];
                                                   Editable=No }
    { 55  ;   ;Rental Type         ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order Line"."Rental Type" WHERE (Plant Order No.=FIELD(Plant Order No.),
                                                                                                              Line No.=FIELD(Plant Order Line No.)));
                                                   CaptionML=[ENU=Rental Type;
                                                              NOR=Leietype;
                                                              SVE=Uthyrningstyp];
                                                   OptionCaptionML=[ENU=" ,Rental,Sales,Buy Back,Buy Back and Sales";
                                                                    NOR=" ,Leie,Salg,Tilbakekjõp,Tilbakekjõp og salg";
                                                                    SVE=" ,Uthyrning,FîrsÑljning,èterkîp,èterkîp och fîrsÑljning"];
                                                   OptionString=[ ,Rental,Sales,Buy Back,Buy Back and Sales];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 60  ;   ;Bulk                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Bulk WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Bulk;
                                                              NOR=Bulk;
                                                              SVE=Bulk];
                                                   Editable=No }
    { 70  ;   ;Mobilization Rate Code;Code10      ;OnLookup=VAR
                                                              RateCode@1100529001 : Record 11020502;
                                                              PlantOrderReceipt@1100529002 : Record 11020492;
                                                            BEGIN
                                                              PlantOrderReceipt.GET("Plant Order No.");
                                                              RateCode.LookupPlantRateCode(PlantOrderReceipt."To Location","Mobilization Rate Code",FALSE,'',2);
                                                            END;

                                                   CaptionML=ENU=Mobilization Rate Code;
                                                   Editable=No }
    { 80  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 90  ;   ;To Receive          ;Decimal       ;OnValidate=VAR
                                                                PlantOrderReceipt@1100529000 : Record 11020492;
                                                              BEGIN
                                                                IF "To Receive" <> 0 THEN BEGIN
                                                                  PlantOrderReceipt.GET("Plant Order No.");
                                                                  PlantOrderReceipt.TESTFIELD("Receipt Date");
                                                                  CheckToReceive;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=To Receive;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 100 ;   ;Received            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Order Received Quantity".Received WHERE (Plant Order No.=FIELD(Plant Order No.),
                                                                                                                   Plant Order Line No.=FIELD(Plant Order Line No.)));
                                                   CaptionML=[ENU=Received;
                                                              NOR=Mottatt;
                                                              SVE=Inlevererad];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 110 ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=MÜleenhet;
                                                              SVE=MÜttenhet];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Plant Order No.,Plant Order Line No.    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529000 : TextConst 'ENU=must be 0, is already completely received';
      Text002@1100529001 : TextConst 'ENU=may only be 1 (or 0) for unique plant';
      Text003@1100529002 : TextConst 'ENU=may not be greater than %1';

    PROCEDURE GetExternal@1100485001() : Boolean;
    VAR
      PlantType@1100485000 : Record 11012551;
      PlantNumber@1100485001 : Record 11012552;
    BEGIN
      IF Type = Type::Plant THEN BEGIN
        IF ("Plant No." <> '') AND ("Plant No." <> '0') THEN BEGIN
          IF PlantNumber.GET("Plant Type", "Plant No.") THEN
            EXIT(PlantNumber.External);
        END;
        IF PlantType.GET("Plant Type") THEN
          EXIT(PlantType.External);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE CheckToReceive@1100529000();
    VAR
      MaxToReceive@1100529000 : Decimal;
    BEGIN
      IF "To Receive" <> 0 THEN BEGIN
        IF "To Receive" < 0 THEN
          FIELDERROR("To Receive");
        CALCFIELDS(Received, Bulk);
        MaxToReceive := Quantity - Received;
        IF MaxToReceive <= 0 THEN
          FIELDERROR("To Receive", Text001);
        IF (Type = Type::Plant) AND (NOT Bulk) AND ("To Receive" <> 1) THEN
          FIELDERROR("To Receive", Text002);
        IF "To Receive" > MaxToReceive THEN
          FIELDERROR("To Receive", STRSUBSTNO(Text003, MaxToReceive));
      END;
    END;

    PROCEDURE ShowReceivedQty@1100529001(ReceiptNo@1100529001 : Code[20];ReceiptLineNo@1100529002 : Integer);
    VAR
      PlantOrderReceivedQuantity@1100529000 : Record 11020494;
    BEGIN
      IF ReceiptNo = '' THEN
        EXIT;
      PlantOrderReceivedQuantity.FILTERGROUP(4);
      PlantOrderReceivedQuantity.SETRANGE("Plant Order No.", ReceiptNo);
      PlantOrderReceivedQuantity.FILTERGROUP(0);
      IF ReceiptLineNo <> 0 THEN
        PlantOrderReceivedQuantity.SETRANGE("Plant Order Line No.", ReceiptLineNo);
      PlantOrderReceivedQuantity."Plant Order No." := ReceiptNo;
      PlantOrderReceivedQuantity."Plant Order Line No." := ReceiptLineNo;
      PAGE.RUNMODAL(PAGE::"Plant Order ReceivedQuantities", PlantOrderReceivedQuantity);
    END;

    PROCEDURE FillToReceive@1100529002(VAR PlantOrderReceiptLine@1100529000 : Record 11020493);
    VAR
      PlantOrderReceipt@1100529001 : Record 11020492;
    BEGIN
      PlantOrderReceipt.GET("Plant Order No.");
      IF PlantOrderReceipt.Status >= PlantOrderReceipt.Status::Finished THEN
        PlantOrderReceipt.FIELDERROR(Status);
      PlantOrderReceipt.TESTFIELD("Receipt Date");
      PlantOrderReceiptLine.SETCURRENTKEY("Plant Order No.","Plant Order Line No.");
      PlantOrderReceiptLine.SETRANGE("Plant Order No.", "Plant Order No.");
      PlantOrderReceiptLine.FINDSET(TRUE,FALSE);
      REPEAT
        PlantOrderReceiptLine.CALCFIELDS(Received);
        PlantOrderReceiptLine."To Receive" := PlantOrderReceiptLine.Quantity - PlantOrderReceiptLine.Received;
        PlantOrderReceiptLine.MODIFY;
      UNTIL PlantOrderReceiptLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

