OBJECT Codeunit 11072008 Job Task-Indent
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00 (1003);
  }
  PROPERTIES
  {
    TableNo=11072600;
    OnRun=BEGIN
            TESTFIELD("Job No.");
            IF NOT
               CONFIRM(
                 Text000 +
                 Text001 +
                 Text002 +
                 Text003,TRUE)
            THEN
              EXIT;
            JT := Rec;
            Indent("Job No.");
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Mit dieser Funktion kann die EinrÅckung fÅr alle Projektaufgaben aktualisiert werden.;ENU=This function updates the indentation of all the Job Tasks.;NLD=Met deze functie werkt u de indentatie van alle projecttaken bij.;NOR=Denne funksjonen oppdaterer innrykket for alle prosjektaktiviteter.;SVE=Den hÑr funktionen uppdaterar indraget fîr alla projektaktiviteter.';
      Text001@1001 : TextConst 'DEU="Alle Projektaufgaben zwischen einer Von-Summe und der entsprechenden Bis-Summe werden um eine Ebene eingerÅckt. ";ENU="All Job Tasks between a Begin-Total and the matching End-Total are indented one level. ";NLD="Voor alle projecttaken is de inspringing van een begintotaal en het bijbehorende eindtotaal hetzelfde. ";NOR=Alle prosjektaktiviteter mellom Fra-summer og matchende Til-summer rykker inn ett nivÜ.;SVE="Alla projektaktiviteter mellan frÜn-summor och motsvarande till-summor dras in en nivÜ. "';
      Text002@1002 : TextConst 'DEU=Die ZusammenzÑhlung fÅr Bis-Summen ist ebenfalls aktualisiert.;ENU=The Totaling for each End-total is also updated.;NLD=Bovendien wordt voor ieder Eindtotaal het veld Samentelling bijgewerkt.;NOR=Summeringen for hver sluttsum er ogsÜ oppdatert.;SVE=Summeringen fîr varje slutsumma Ñr ocksÜ uppdaterad.';
      Text003@1003 : TextConst 'DEU=\\Mîchten Sie die Projektaufgaben einrÅcken?;ENU=\\Do you want to indent the Job Tasks?;NLD=\\Wilt u de projecttaken laten inspringen?;NOR=\\Vil du opprette innrykk for prosjektaktivitetene?;SVE=\\Vill du skapa indrag fîr projektaktiviteterna?';
      Text004@1004 : TextConst 'DEU=EinrÅcken der Projektaufgaben #1##########.;ENU=Indenting the Job Tasks #1##########.;NLD=Inspringen van projecttaken #1##########.;NOR=Lager innrykk for prosjektaktiviteter #1##########.;SVE=Skapar indrag fîr projektaktiviteter #1##########.';
      Text005@1005 : TextConst 'DEU=Bis-Summe %1 hat keine passende Von-Summe.;ENU=End-Total %1 is missing a matching Begin-Total.;NLD=Er is geen overeenkomstig begintotaal voor eindtotaal %1.;NOR=Til- sum %1 mangler en matchende Fra-sum.;SVE=Till-summa %1 saknar en korresponderande FrÜn-summa.';
      ArrayExceededErr@1010 : TextConst '@@@="%1 = A number bigger than 1";DEU=Sie kînnen nur %1 Ebenen fÅr Projektaufgaben des Typs Von-Summe einrÅcken.;ENU=You can only indent %1 levels for job tasks of the type Begin-Total.;NLD=U kunt slechts %1 niveaus inspringen voor projecttaken van het type begintotaal.';
      JT@1006 : Record 11072600;
      Window@1007 : Dialog;
      JTNo@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    [External]
    PROCEDURE Indent@1(JobNo@1000 : Code[20]);
    BEGIN
      Window.OPEN(Text004);
      JT.SETRANGE("Job No.",JobNo);
      WITH JT DO
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(1,"Job Task No.");

            IF "Job Task Type" = "Job Task Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "Job Task No.");
              Totaling := JTNo[i] + '..' + "Job Task No.";
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Job Task Type" = "Job Task Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              IF i > ARRAYLEN(JTNo) THEN
                ERROR(ArrayExceededErr,ARRAYLEN(JTNo));
              JTNo[i] := "Job Task No.";
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

