OBJECT Table 11072345 DPT Subsite Doc Type ServLoc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CheckRecordChangeAllowed;
             END;

    OnDelete=VAR
               DPTRespPersPermProject@1100528303 : Record 11072332;
               DPTAuthorityPermProject@1100528304 : Record 11072334;
             BEGIN
               CheckRecordChangeAllowed;
               DPTRespPersPermProject.SETRANGE("Project No.", "Service Location No.");
               DPTRespPersPermProject.SETRANGE("Subsite Code", "Subsite Code");
               DPTRespPersPermProject.SETRANGE("Document Type", "Document Type");
               DPTRespPersPermProject.DELETEALL(TRUE);

               DPTAuthorityPermProject.SETRANGE("Project No.", "Service Location No.");
               DPTAuthorityPermProject.SETRANGE("Subsite Code", "Subsite Code");
               DPTAuthorityPermProject.SETRANGE("Document Type", "Document Type");
               DPTAuthorityPermProject.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=ENU=DPT Document Type per Subsite (Service);
    LookupPageID=Page11130620;
    DrillDownPageID=Page11130620;
  }
  FIELDS
  {
    { 10  ;   ;Service Location No.;Code20        ;TableRelation="Service Location".No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Service Location Description");
                                                              END;

                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 11  ;   ;Service Location Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Service Location Description;
                                                              NOR=Servicelokasjonbeskrivelse;
                                                              SVE=Serviceplatsbeskrivning];
                                                   Editable=No }
    { 20  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Subsite ServLoc".Code WHERE (Service Location No.=FIELD(Service Location No.));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Subsite Description", "Subsite Structure");
                                                              END;

                                                   CaptionML=ENU=Subsite Code;
                                                   NotBlank=Yes }
    { 21  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite ServLoc".Description WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 22  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite ServLoc"."Subsite Structure" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 30  ;   ;Document Type       ;Code10        ;TableRelation="Document Type".Type;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Document Type Description");
                                                              END;

                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Bilagstype;
                                                              SVE=Dokumenttyp];
                                                   NotBlank=Yes }
    { 31  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 60  ;   ;Synchronized On     ;DateTime      ;CaptionML=ENU=Synchronized On;
                                                   Editable=No }
    { 90  ;   ;Use Document CheckIn/-Out;Boolean  ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                UpdateSubsiteStructure
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out }
  }
  KEYS
  {
    {    ;Service Location No.,Subsite Code,Document Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528300 : TextConst 'ENU=Rename is not allowed.;NOR=Gi nytt navn ikke tillatt;SVE=Att ge nytt namn „r inte till†tet.';

    PROCEDURE CreateAllDocumentTypeLinks@1100528304(ServiceLocationNo@1100528303 : Code[20]);
    VAR
      ServiceLocation@1100528304 : Record 11012801;
      DPTDefaultSubsiteDocType@1100528300 : Record 11229942;
      DPTSubsiteServLoc@1100528301 : Record 11072344;
      DPTSubsiteDocTypeServLoc@1100528302 : Record 11072345;
    BEGIN
      DPTSubsiteServLoc.SETRANGE("Service Location No.", ServiceLocationNo);
      IF (NOT DPTSubsiteServLoc.FINDSET) THEN
        EXIT;

      REPEAT
        DPTDefaultSubsiteDocType.SETRANGE("Source Type", DPTDefaultSubsiteDocType."Source Type"::ServiceLocation);
        DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", DPTSubsiteServLoc.Code);
        IF (DPTDefaultSubsiteDocType.FINDSET) THEN BEGIN
          REPEAT
            IF (NOT DPTSubsiteDocTypeServLoc.GET(ServiceLocationNo, DPTSubsiteServLoc.Code, DPTDefaultSubsiteDocType."Document Type")) THEN BEGIN
              DPTSubsiteDocTypeServLoc.INIT;
              DPTSubsiteDocTypeServLoc."Service Location No." := ServiceLocationNo;
              DPTSubsiteDocTypeServLoc."Subsite Code" := DPTDefaultSubsiteDocType."Subsite Code";
              DPTSubsiteDocTypeServLoc."Document Type" := DPTDefaultSubsiteDocType."Document Type";
              DPTSubsiteDocTypeServLoc."Use Document CheckIn/-Out" := DPTDefaultSubsiteDocType."Use Document CheckIn/-Out";
              DPTSubsiteDocTypeServLoc.INSERT(TRUE);
            END;
          UNTIL (DPTDefaultSubsiteDocType.NEXT = 0);
        END;
      UNTIL (DPTSubsiteServLoc.NEXT = 0);
    END;

    PROCEDURE ToggleUseDocumentCheckInOut@1100528300(NewValue@1100528300 : Boolean);
    BEGIN
      IF (NOT FINDSET(TRUE)) THEN
        EXIT;

      REPEAT
        "Use Document CheckIn/-Out" := NewValue;
        MODIFY(TRUE);
      UNTIL (NEXT = 0);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528302();
    BEGIN
    END;

    LOCAL PROCEDURE CheckRecordChangeAllowed@1100528301();
    VAR
      DPTInterface@1100528300 : Codeunit 11012451;
    BEGIN
      TESTFIELD("Synchronized On", 0DT);
      DPTInterface.SetDPTType(2);
      DPTInterface.CheckDocumentsLinkedToSubsiteDocumentType("Service Location No.", "Subsite Code", "Document Type");
    END;

    LOCAL PROCEDURE UpdateSubsiteStructure@1100528303();
    VAR
      DPTSubsiteDocTypeServLoc@1100528300 : Record 11072345;
    BEGIN
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure"<> "Subsite Structure"::OneDocLibrary) THEN
        EXIT;

      DPTSubsiteDocTypeServLoc.SETRANGE("Service Location No.", "Service Location No.");
      DPTSubsiteDocTypeServLoc.SETRANGE("Subsite Code", "Subsite Code");
      DPTSubsiteDocTypeServLoc.SETFILTER("Document Type", '<>%1', "Document Type");
      DPTSubsiteDocTypeServLoc.MODIFYALL("Use Document CheckIn/-Out", "Use Document CheckIn/-Out", TRUE);
    END;

    BEGIN
    END.
  }
}

