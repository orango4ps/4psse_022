OBJECT Codeunit 1217 Pre-map Incoming Purch. Doc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    TableNo=1220;
    OnRun=VAR
            BuyFromVendorNo@1000 : Code[20];
            PayToVendorNo@1001 : Code[20];
            ParentRecNo@1002 : Integer;
            CurrRecNo@1004 : Integer;
          BEGIN
            ParentRecNo := 0;
            FindDistinctRecordNos(TempIntegerHeaderRecords,"Entry No.",DATABASE::"Purchase Header",ParentRecNo);
            IF NOT TempIntegerHeaderRecords.FINDSET THEN
              EXIT;

            REPEAT
              CurrRecNo := TempIntegerHeaderRecords.Number;

              ValidateCompanyInfo("Entry No.",CurrRecNo);
              ValidateCurrency("Entry No.",CurrRecNo);
              SetDocumentType("Entry No.",ParentRecNo,CurrRecNo);

              CorrectHeaderData("Entry No.",CurrRecNo);
              BuyFromVendorNo := FindBuyFromVendor("Entry No.",CurrRecNo);
              PayToVendorNo := FindPayToVendor("Entry No.",CurrRecNo);
              FindInvoiceToApplyTo("Entry No.",CurrRecNo);

              PersistHeaderData("Entry No.",CurrRecNo,BuyFromVendorNo,PayToVendorNo);

              ProcessLines("Entry No.",CurrRecNo,BuyFromVendorNo);
            UNTIL TempIntegerHeaderRecords.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      InvalidCompanyInfoGLNErr@1000 : TextConst '@@@="%1 = GLN (13 digit number)";ENU=The customer''s GLN %1 on the incoming document does not match the GLN in the Company Information window.;NOR=Kunde-GLN %1 i det inng†ende dokumentet, samsvarer ikke med GLN i vinduet Selskapsopplysninger.;SVE=Kundens GLN %1 i det inkommande dokumentet matchar inte GLN i f”nstret med f”retagsinformation.';
      InvalidCompanyInfoVATRegNoErr@1001 : TextConst '@@@=%1 VAT Registration Number (format could be AB###### or ###### or AB##-##-###);ENU=The customer''s VAT registration number %1 on the incoming document does not match the VAT Registration No. in the Company Information window.;NOR=Organisasjonsnummeret %1 for kunden i det inng†ende dokumentet, samsvarer ikke med organisasjonsnummeret i vinduet Selskapsopplysninger.;SVE=Kundens momsregistreringsnummer %1 i det inkommande dokumentet matchar inte momsregistreringsnumret i f”nstret med f”retagsinformation.';
      CurrencyCodeMissingErr@1003 : TextConst 'ENU=The currency code is missing on the incoming document.;NOR=Valutakoden mangler i det innkommende dokumentet.;SVE=Valutakoden saknas i det inkommande dokumentet.';
      CurrencyCodeDifferentErr@1004 : TextConst '@@@=%1 currency code (e.g. GBP), %2 the document currency code (e.g. DKK);ENU=The currency code %1 must not be different from the currency code %2 on the incoming document.;NOR=Valutakoden %1 kan ikke v‘re forskjellig fra valutakoden %2 i det innkommende dokumentet.;SVE=Valutakoden %1 f†r inte skilja sig fr†n valutakoden %2 i det inkommande dokumentet.';
      ItemCurrencyCodeDifferentErr@1005 : TextConst '@@@=%1 Invoice line currency code (e.g. GBP), %2 invoice line no. (e.g. 2), %3 document currency code (e.g. DKK);ENU=The currency code %1 on invoice line no. %2 must not be different from the currency code %3 on the incoming document.;NOR=Valutakoden %1 p† fakturalinje %2 kan ikke v‘re forskjellig fra valutakoden %3 i det innkommende dokumentet.;SVE=Valutakoden %1 p† fakturarad %2 f†r inte skilja sig fr†n valutakoden %3 i det inkommande dokumentet.';
      BuyFromVendorNotFoundErr@1007 : TextConst '@@@=%1 Vendor name (e.g. London Postmaster), %2 Vendor''s GLN (13 digit number), %3 Vendor''s VAT Registration Number;ENU=Cannot find buy-from vendor ''%1'' based on the vendor''s GLN %2 or VAT registration number %3 on the incoming document. Make sure that a card for the vendor exists with the corresponding GLN or VAT Registration No.;NOR=Finner ikke kj›p fra-leverand›ren %1 basert p† leverand›r-GLN %2 eller organisasjonsnummeret %3 i det inng†ende dokumentet. Kontroller at det finnes et kort for leverand›ren med tilsvarende GLN eller organisasjonsnummer.;SVE=Det g†r inte att hitta ink”psleverant”r %1 utifr†n leverant”rens GLN %2 eller momsregistreringsnummer %3 p† det inkommande dokumentet. Kontrollera att det finns ett kort f”r leverant”ren med motsvarande GLN eller momsregistreringsnummer.';
      PayToVendorNotFoundErr@1008 : TextConst '@@@=%1 Vendor name (e.g. London Postmaster), %2 Vendor''s GLN (13 digit number), %3 Vendor''s VAT Registration Number;ENU=Cannot find pay-to vendor ''%1'' based on the vendor''s GLN %2 or VAT registration number %3 on the incoming document. Make sure that a card for the vendor exists with the corresponding GLN or VAT Registration No.;NOR=Finner ikke betal til-leverand›ren %1 basert p† leverand›r-GLN %2 eller organisasjonsnummeret %3 i det inng†ende dokumentet. Kontroller at det finnes et kort for leverand›ren med tilsvarende GLN eller organisasjonsnummer.;SVE=Det g†r inte att hitta betalningsleverant”ren %1 utifr†n leverant”rens GLN %2 eller momsregistreringsnummer %3 p† det inkommande dokumentet. Kontrollera att det finns ett kort f”r leverant”ren med motsvarande GLN eller momsregistreringsnummer.';
      ItemNotFoundErr@1009 : TextConst '@@@=%1 Vendor item name (e.g. Bicycle - may be another language),%2 Vendor''''s number,%3 Vendor''''s item number, %4 item bar code (GTIN);ENU=Cannot find item ''%1'' based on the vendor %2 item number %3 or GTIN %4 on the incoming document. Make sure that a card for the item exists with the corresponding item cross reference or GTIN.;NOR=Finner ikke varen %1 basert p† leverand›ren %2 og varenummeret %3 eller GTIN %4, i det inng†ende dokumentet. Kontroller at det finnes et kort for varen med tilh›rende varekryssreferanse eller GTIN.;SVE=Det g†r inte att hitta artikel %1 utifr†n leverant”r %2s artikelnummer %3 eller GTIN %4 p† det inkommande dokumentet. Kontrollera att det finns ett kort f”r artikeln med motsvarande artikelreferens eller GTIN.';
      ItemNotFoundByGTINErr@1021 : TextConst '@@@=%1 Vendor item name (e.g. Bicycle - may be another language),%2 item bar code (GTIN);ENU=Cannot find item ''%1'' based on GTIN %2 on the incoming document. Make sure that a card for the item exists with the corresponding GTIN.;NOR=Finner ikke varen %1 basert p† GTIN %2 i det inng†ende dokumentet. Kontroller at det finnes et kort for varen med tilh›rende GTIN.;SVE=Det g†r inte att hitta artikel %1 utifr†n GTIN %2 p† det inkommande dokumentet. Kontrollera att det finns ett kort f”r artikeln med motsvarande GTIN.';
      ItemNotFoundByVendorItemNoErr@1022 : TextConst '@@@=%1 Vendor item name (e.g. Bicycle - may be another language),%2 Vendor''''s number,%3 Vendor''''s item number;ENU=Cannot find item ''%1'' based on the vendor %2 item number %3 on the incoming document. Make sure that a card for the item exists with the corresponding item cross reference.;NOR=Finner ikke varen %1 basert p† leverand›ren %2 og varenummeret %3 i det inng†ende dokumentet. Kontroller at det finnes et kort for varen med tilh›rende varekryssreferanse.;SVE=Det g†r inte att hitta artikel %1 utifr†n leverant”r %2s artikelnummer %3 p† det inkommande dokumentet. Kontrollera att det finns ett kort f”r artikeln med motsvarande artikelreferens.';
      UOMNotFoundErr@1011 : TextConst '@@@=%1 International Standard Code or Code or Description for Unit of Measure;ENU=Cannot find unit of measure %1. Make sure that the unit of measure exists.;NOR=Finner ikke m†leenheten %1. Kontroller at m†leenheten finnes.;SVE=Det g†r inte att hitta m†ttenheten %1. Kontrollera att den finns.';
      UOMMissingErr@1010 : TextConst '@@@=%1 document line number (e.g. 2);ENU=Cannot find a unit of measure code on the incoming document line %1.;NOR=Finner inne en m†leenhetskode for linje %1 i det inng†ende dokumentet.;SVE=Det g†r inte att hitta en m†ttenhetskod p† inkommande dokumentrad %1.';
      UOMConflictWithCrossRefErr@1014 : TextConst '@@@=%1 imported unit code, %2 document line number (e.g. 2), %3 Item Cross Reference unit code;ENU=Unit of measure %1 on incoming document line %2 does not match unit of measure %3 in the item cross reference.  Make sure that a card for the item with the specified unit of measure exists with the corresponding item cross reference.;NOR=M†leenheten %1 p† innkommende dokumentlinje %2 samsvarer ikke med m†leenheten %3 i varekryssreferansen. Kontroller at det finnes et kort for varen med den angitte m†leenheten med tilsvarende varekryssreferanse.;SVE=M†ttenheten %1 p† inkommande dokumentraden %2 motsvarar inte m†ttenheten %3 i artikelkorsreferensen. Kontrollera att ett kort f”r artikeln med angiven m†ttenhet finns med motsvarande artikelkorsreferens.';
      UOMConflictWithItemErr@1015 : TextConst '@@@=%1 imported unit code, %2 document line number (e.g. 2), %3 Item unit code;ENU=Unit of measure %1 on incoming document line %2 does not match purchase unit of measure %3 on the item card.  Make sure that a card for the item with the specified unit of measure exists with the corresponding item cross reference.;NOR=M†leenheten %1 p† innkommende dokumentlinje %2 samsvarer ikke med m†leenheten %3 for kj›p p† varekortet. Kontroller at det finnes et kort for varen med den angitte m†leenheten med tilsvarende varekryssreferanse.;SVE=M†ttenheten %1 p† inkommande dokumentraden %2 motsvarar inte ink”psm†ttenheten %3 p† artikelkortet. Kontrollera att ett kort f”r artikeln med angiven m†ttenhet finns med motsvarande artikelkorsreferens.';
      UOMConflictCrossRefWithItemErr@1028 : TextConst '@@@=%1 item cross reference unit code;ENU=Unit of measure %1 in the item cross reference is not in the list of units of measure for the corresponding item. Make sure that a unit of measure of item cross reference is in the list of units of measure for the corresponding item.;NOR=M†leenheten %1 i varekryssreferansen finnes ikke i listen over m†leenheter for tilsvarende vare. Kontroller at det finnes en m†leenhet for varekryssreferansen i listen over m†leenheter for den tilsvarende varen.;SVE=M†ttenheten %1 p† listan med artikelns korsreferens finns inte p† listan med m†ttenheter f”r motsvarande artikel. Kontrollera att en m†ttenhet f”r artikelkorsreferensen finns p† listan med enheter f”r motsvarande artikel.';
      NotSpecifiedUnitOfMeasureTxt@1018 : TextConst 'ENU=<NONE>;NOR=<INGEN>;SVE=<INGEN>';
      MissingCompanyInfoSetupErr@1002 : TextConst 'ENU=You must fill either GLN or VAT Registration No. in the Company Information window.;NOR=Du m† fylle ut GLN- eller organisasjonsnummer i enten vinduet Selskapsopplysninger.;SVE=Du m†ste fylla i GLN eller momsregistreringsnummer i f”nstret med f”retagsinformation.';
      VendorNotFoundByNameAndAddressErr@1013 : TextConst 'ENU=Cannot find vendor based on the vendor''s name ''%1'' and street name ''%2'' on the incoming document. Make sure that a card for the vendor exists with the corresponding name.;NOR=Finner ikke leverand›ren basert p† leverandnavnet %1 og gatenavnet %2 i det inng†ende dokumentet. Kontroller at det finnes et kort for leverand›ren med tilsvarende navnet.;SVE=Det g†r inte att hitta leverant”ren utifr†n leverant”rens namn %1 och postadress %2 p† det inkommande dokumentet. Kontrollera att det finns ett kort f”r leverant”ren med motsvarande namn.';
      InvalidCompanyInfoNameErr@1016 : TextConst '@@@="%1 = customer name";ENU=The customer name ''%1'' on the incoming document does not match the name in the Company Information window.;NOR=Kundenavnet %1 i det innkommende dokumentet samsvarer ikke med navnet i vinduet Selskapsopplysninger.;SVE=Kundens namn %1 i det inkommande dokumentet matchar inte namnet i f”nstret med f”retagsinformation.';
      InvalidCompanyInfoAddressErr@1017 : TextConst '@@@="%1 = customer address, street name";ENU=The customer''s address ''%1'' on the incoming document does not match the Address in the Company Information window.;NOR=Kundeadressen %1 i det inng†ende dokumentet samsvarer ikke med adressen i vinduet Selskapsopplysninger.;SVE=Kundens adress %1 i det inkommande dokumentet matchar inte adressen i f”nstret med f”retagsinformation.';
      TempIntegerHeaderRecords@1006 : TEMPORARY Record 2000000026;
      TempIntegerLineRecords@1019 : TEMPORARY Record 2000000026;
      FieldMustHaveAValueErr@1020 : TextConst '@@@=%1 - field caption;ENU=You must specify a value for field ''%1''.;NOR=Du m† angi en verdi for feltet %1.;SVE=Du m†ste ange ett v„rde f”r f„lt %1.';
      DocumentTypeUnknownErr@1023 : TextConst '@@@=%1 - Column Definitions (page caption),%2 - Data Exchange Definition (page caption),%3 - invoice (option caption),%4 - credit memo (option caption),%5 - Constant (field name),%6 - Document Type (field caption),%7 - Purchase Header (table caption);ENU=You must make a new entry in the %1 of the %2 window, and enter ''%3'' or ''%4'' in the %5 field. Then, you must map it to the %6 field in the %7 table.;NOR=Du m† opprette en ny post i %1 i vinduet %2, og angi %3 eller %4 i %5-feltet. Deretter m† du tilordne det til %6-feltet i %7-tabellen.;SVE=Du m†ste skapa en ny post i %1 i f”nstret %2 och ange %3 eller %4 i f„ltet %5. Sedan m†ste du mappa den till f„ltet %6 i tabellen %7.';
      YouMustFirstPostTheRelatedInvoiceErr@1024 : TextConst '@@@=%1 - vendor invoice no.,%2 posted purchase invoice no.;ENU=The incoming document references invoice %1 from the vendor. You must post related purchase invoice %2 before you create a new purchase document from this incoming document.;NOR=Det inng†ende dokumentet refererer til fakturaen %1 for leverand›ren. Du m† bokf›re den relaterte kj›psfakturaen %2 f›r du kan opprette et nytt kj›psdokument fra dette inng†ende dokumentet.;SVE=Den inkommande dokumentet refererar till faktura %1 fr†n leverant”ren. Du m†ste bokf”ra relaterad ink”psfaktura %2 innan du kan skapa ett nytt ink”psdokument fr†n det h„r inkommande dokumentet.';
      UnableToFindRelatedInvoiceErr@1025 : TextConst '@@@=%1 - vendor invoice no.;ENU=The incoming document references invoice %1 from the vendor, but no purchase invoice exists for %1.;NOR=Det inng†ende dokumentet refererer til fakturaen %1 for leverand›ren, men det finnes ingen kj›psfaktura for %1.;SVE=Den inkommande dokumentet refererar till faktura %1 fr†n leverant”ren, men ingen ink”psfaktura finns f”r %1.';
      UnableToFindTotalAmountErr@1026 : TextConst 'ENU=The incoming document has no total amount excluding VAT.;NOR=Det inng†ende dokumentet har ikke totalt bel›p eks. mva.;SVE=Det inkommande dokumentet har inget totalbelopp exklusive moms.';
      UnableToFindAppropriateAccountErr@1012 : TextConst '@@@=%1 - arbitrary text;ENU=Cannot find an appropriate G/L account for the line with description ''%1''. Choose the Map Text to Account button, and then map the core part of ''%1'' to the relevant G/L account.;NOR=Finner ikke en passende finanskonto for linjen med beskrivelsen %1. Klikk knappen Tilordne tekst til konto, og tilordne hoveddelen av %1 til den relevante finanskontoen.;SVE=Det gick inte att hitta ett l„mpligt redovisningskonto f”r raden med beskrivningen %1. Tryck p† knappen Mappa text till konto och mappa sedan huvuddelen av %1 till relevant redovisningskonto.';

    LOCAL PROCEDURE ValidateCompanyInfo@15(EntryNo@1006 : Integer;RecordNo@1002 : Integer);
    VAR
      IntermediateDataImport@1000 : Record 1214;
      CompanyInformation@1001 : Record 79;
      DataExch@1003 : Record 1220;
      IncomingDocument@1007 : Record 130;
      IncomingDocumentAttachment@1008 : Record 133;
      GLN@1004 : Text;
      VatRegNo@1005 : Text;
      VatRegNoFound@1009 : Boolean;
    BEGIN
      // for OCRed invoices, we don't check the buyer's information
      DataExch.GET(EntryNo);
      IncomingDocument.GET(DataExch."Incoming Entry No.");
      IF IncomingDocument.GetGeneratedFromOCRAttachment(IncomingDocumentAttachment) THEN
        EXIT;

      CompanyInformation.GET;
      WITH IntermediateDataImport DO BEGIN
        IF FindEntry(EntryNo,DATABASE::"Company Information",CompanyInformation.FIELDNO("VAT Registration No."),0,RecordNo) THEN
          VatRegNo := Value;

        SETRANGE("Field ID",CompanyInformation.FIELDNO(GLN));
        IF FINDFIRST THEN
          GLN := Value;

        IF (GLN = '') AND (VatRegNo = '') THEN BEGIN
          ValidateCompanyInfoByNameAndAddress(EntryNo,RecordNo);
          EXIT;
        END;

        IF (CompanyInformation.GLN = '') AND (CompanyInformation."VAT Registration No." = '') THEN
          LogErrorMessage(EntryNo,CompanyInformation,CompanyInformation.FIELDNO(GLN),MissingCompanyInfoSetupErr);

        IF CompanyInformation.GLN <> '' THEN BEGIN
          SETFILTER(Value,STRSUBSTNO('<>%1&<>%2',CompanyInformation.GLN,''''''));
          IF FINDLAST THEN
            LogErrorMessage(EntryNo,CompanyInformation,CompanyInformation.FIELDNO(GLN),
              STRSUBSTNO(InvalidCompanyInfoGLNErr,GLN));
        END;

        IF CompanyInformation."VAT Registration No." <> '' THEN BEGIN
          SETRANGE("Field ID",CompanyInformation.FIELDNO("VAT Registration No."));
          SETFILTER(Value,STRSUBSTNO('<>%1',''''''));

          IF FINDSET THEN BEGIN
            REPEAT
              VatRegNoFound := ExtractVatRegNo(Value,'') = ExtractVatRegNo(CompanyInformation."VAT Registration No.",'');
            UNTIL (NEXT = 0) OR VatRegNoFound;
            IF NOT VatRegNoFound THEN
              LogErrorMessage(EntryNo,CompanyInformation,CompanyInformation.FIELDNO("VAT Registration No."),
                STRSUBSTNO(InvalidCompanyInfoVATRegNoErr,VatRegNo));
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateCompanyInfoByNameAndAddress@29(EntryNo@1006 : Integer;RecordNo@1002 : Integer);
    VAR
      IntermediateDataImport@1000 : Record 1214;
      CompanyInfo@1001 : Record 79;
      RecordMatchMgt@1011 : Codeunit 1251;
      ImportedAddress@1004 : Text;
      ImportedName@1005 : Text;
      CompanyName@1003 : Text;
      CompanyAddr@1007 : Text;
      NameNearness@1008 : Integer;
      AddressNearness@1009 : Integer;
    BEGIN
      CompanyInfo.GET;
      CompanyName := CompanyInfo.Name;
      CompanyAddr := CompanyInfo.Address;
      WITH IntermediateDataImport DO BEGIN
        IF FindEntry(EntryNo,DATABASE::"Company Information",CompanyInfo.FIELDNO(Name),0,RecordNo) THEN
          ImportedName := Value;

        NameNearness := RecordMatchMgt.CalculateStringNearness(CompanyName,ImportedName,MatchThreshold,NormalizingFactor);

        SETRANGE("Field ID",CompanyInfo.FIELDNO(Address));
        IF FINDFIRST THEN
          ImportedAddress := Value;

        AddressNearness := RecordMatchMgt.CalculateStringNearness(CompanyAddr,ImportedAddress,MatchThreshold,NormalizingFactor);

        IF (ImportedName <> '') AND (NameNearness < RequiredNearness) THEN
          LogErrorMessage(EntryNo,CompanyInfo,CompanyInfo.FIELDNO(Name),STRSUBSTNO(InvalidCompanyInfoNameErr,ImportedName));

        IF (ImportedAddress <> '') AND (AddressNearness < RequiredNearness) THEN
          LogErrorMessage(EntryNo,CompanyInfo,CompanyInfo.FIELDNO(Address),STRSUBSTNO(InvalidCompanyInfoAddressErr,ImportedAddress));
      END;
    END;

    LOCAL PROCEDURE ValidateCurrency@16(EntryNo@1004 : Integer;RecordNo@1008 : Integer);
    VAR
      IntermediateDataImport@1005 : Record 1214;
      PurchaseHeader@1001 : Record 38;
      PurchaseLine@1003 : Record 39;
      GLSetup@1007 : Record 98;
      DocumentCurrency@1002 : Text;
      IsLCY@1006 : Boolean;
    BEGIN
      GLSetup.GET;
      IF GLSetup."LCY Code" = '' THEN
        LogErrorMessage(EntryNo,GLSetup,GLSetup.FIELDNO("LCY Code"),
          STRSUBSTNO(FieldMustHaveAValueErr,GLSetup.FIELDCAPTION("LCY Code")));

      WITH IntermediateDataImport DO BEGIN
        DocumentCurrency := GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Currency Code"),0,RecordNo);
        IF DocumentCurrency = '' THEN BEGIN
          LogSimpleErrorMessage(EntryNo,CurrencyCodeMissingErr);
          EXIT;
        END;

        IsLCY := DocumentCurrency = GLSetup."LCY Code";
        // If LCY Currency wont be in Currency table
        IF IsLCY THEN BEGIN
          // Update Document Currency
          Value := '';
          MODIFY;
        END;

        // Ensure the currencies all match the same document currency
        SETRANGE("Field ID",PurchaseHeader.FIELDNO("Tax Area Code"));
        SETFILTER(Value,'<>%1',DocumentCurrency);
        IF FINDFIRST THEN
          LogSimpleErrorMessage(EntryNo,STRSUBSTNO(CurrencyCodeDifferentErr,Value,DocumentCurrency));

        // Clear the additional currency values on header
        SETRANGE(Value);
        DELETEALL;

        // check currency on the lines
        SETRANGE("Table ID",DATABASE::"Purchase Line");
        SETRANGE("Field ID",PurchaseLine.FIELDNO("Currency Code"));
        SETRANGE("Record No.");
        SETRANGE("Parent Record No.",RecordNo);
        SETFILTER(Value,'<>%1',DocumentCurrency);
        IF FINDFIRST THEN
          LogSimpleErrorMessage(EntryNo,STRSUBSTNO(ItemCurrencyCodeDifferentErr,Value,"Record No.",DocumentCurrency));

        // Clear the additional currency values on lines
        SETRANGE(Value);
        DELETEALL;
      END;
    END;

    LOCAL PROCEDURE ProcessLines@24(EntryNo@1000 : Integer;HeaderRecordNo@1003 : Integer;VendorNo@1001 : Code[20]);
    VAR
      DataExch@1002 : Record 1220;
      IncomingDocument@1004 : Record 130;
    BEGIN
      DataExch.GET(EntryNo);
      WITH IncomingDocument DO BEGIN
        GET(DataExch."Incoming Entry No.");
        IF "Document Type" = "Document Type"::Journal THEN
          EXIT;
      END;

      FindDistinctRecordNos(TempIntegerLineRecords,EntryNo,DATABASE::"Purchase Line",HeaderRecordNo);
      IF NOT TempIntegerLineRecords.FINDSET THEN BEGIN
        InsertLineForTotalDocumentAmount(EntryNo,HeaderRecordNo,1,VendorNo);
        EXIT;
      END;

      REPEAT
        ProcessLine(EntryNo,HeaderRecordNo,TempIntegerLineRecords.Number,VendorNo);
      UNTIL TempIntegerLineRecords.NEXT = 0;
    END;

    LOCAL PROCEDURE CorrectHeaderData@33(EntryNo@1001 : Integer;RecordNo@1000 : Integer);
    VAR
      Vendor@1002 : Record 23;
      VendorBankAccount@1004 : Record 288;
      IncomingDocument@1009 : Record 130;
      DataExch@1010 : Record 1220;
      PurchaseHeader@1012 : Record 38;
      GLEntry@1003 : Record 17;
    BEGIN
      DataExch.GET(EntryNo);
      IncomingDocument.GET(DataExch."Incoming Entry No.");
      IF IncomingDocument."OCR Data Corrected" THEN BEGIN
        CorrectHeaderField(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Buy-from Vendor Name"),RecordNo,
          IncomingDocument."Vendor Name");
        CorrectHeaderField(EntryNo,DATABASE::Vendor,Vendor.FIELDNO("VAT Registration No."),RecordNo,
          IncomingDocument."Vendor VAT Registration No.");
        CorrectHeaderField(EntryNo,DATABASE::"Vendor Bank Account",VendorBankAccount.FIELDNO(IBAN),RecordNo,
          IncomingDocument."Vendor IBAN");
        CorrectHeaderField(EntryNo,DATABASE::"Vendor Bank Account",VendorBankAccount.FIELDNO("Bank Account No."),RecordNo,
          IncomingDocument."Vendor Bank Account No.");
        CorrectHeaderField(EntryNo,DATABASE::"Vendor Bank Account",VendorBankAccount.FIELDNO("Bank Branch No."),RecordNo,
          IncomingDocument."Vendor Bank Branch No.");
        CorrectHeaderField(EntryNo,DATABASE::Vendor,Vendor.FIELDNO("Phone No."),RecordNo,
          IncomingDocument."Vendor Phone No.");
        CorrectHeaderField(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Vendor Invoice No."),RecordNo,
          IncomingDocument."Vendor Invoice No.");
        CorrectHeaderField(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Document Date"),RecordNo,
          IncomingDocument."Document Date");
        CorrectHeaderField(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Due Date"),RecordNo,
          IncomingDocument."Due Date");
        CorrectCurrencyCode(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Currency Code"),RecordNo,
          IncomingDocument."Currency Code");
        CorrectHeaderField(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO(Amount),RecordNo,
          IncomingDocument."Amount Excl. VAT");
        CorrectHeaderField(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Amount Including VAT"),RecordNo,
          IncomingDocument."Amount Incl. VAT");
        CorrectHeaderField(EntryNo,DATABASE::"G/L Entry",GLEntry.FIELDNO("VAT Amount"),RecordNo,
          IncomingDocument."VAT Amount");
        CorrectHeaderField(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Vendor Order No."),RecordNo,
          IncomingDocument."Order No.");
      END;
    END;

    LOCAL PROCEDURE CorrectHeaderField@39(EntryNo@1001 : Integer;TableID@1002 : Integer;FieldID@1003 : Integer;RecordNo@1000 : Integer;IncomingDocumentValue@1004 : Variant);
    VAR
      IntermediateDataImport@1005 : Record 1214;
      ExistingValue@1006 : Text;
      CorrectedValue@1007 : Text[250];
    BEGIN
      ExistingValue := IntermediateDataImport.GetEntryValue(EntryNo,TableID,FieldID,0,RecordNo);
      CorrectedValue := COPYSTR(FORMAT(IncomingDocumentValue,0,9),1,MAXSTRLEN(CorrectedValue));
      IF CorrectedValue <> ExistingValue THEN
        IntermediateDataImport.InsertOrUpdateEntry(EntryNo,TableID,FieldID,0,RecordNo,CorrectedValue);
    END;

    LOCAL PROCEDURE CorrectCurrencyCode@26(EntryNo@1001 : Integer;TableID@1002 : Integer;FieldID@1003 : Integer;RecordNo@1000 : Integer;IncomingDocumentValue@1004 : Variant);
    VAR
      IntermediateDataImport@1005 : Record 1214;
      GeneralLedgerSetup@1008 : Record 98;
      ExistingValue@1006 : Text;
      CorrectedValue@1007 : Text[250];
    BEGIN
      ExistingValue := IntermediateDataImport.GetEntryValue(EntryNo,TableID,FieldID,0,RecordNo);
      CorrectedValue := COPYSTR(FORMAT(IncomingDocumentValue,0,9),1,MAXSTRLEN(CorrectedValue));
      GeneralLedgerSetup.GET;
      IF (CorrectedValue <> ExistingValue) AND ((CorrectedValue <> GeneralLedgerSetup."LCY Code") OR (ExistingValue <> '')) THEN
        IntermediateDataImport.InsertOrUpdateEntry(EntryNo,TableID,FieldID,0,RecordNo,CorrectedValue);
    END;

    LOCAL PROCEDURE PersistHeaderData@34(EntryNo@1001 : Integer;RecordNo@1000 : Integer;BuyFromVendorNo@1012 : Code[20];PayToVendorNo@1013 : Code[20]);
    VAR
      IntermediateDataImport@1002 : Record 1214;
      DataExch@1003 : Record 1220;
      IncomingDocument@1004 : Record 130;
      PurchaseHeader@1005 : Record 38;
      VendorBankAccount@1010 : Record 288;
      Vendor@1011 : Record 23;
      GLEntry@1014 : Record 17;
      GeneralLedgerSetup@1016 : Record 98;
      AmountInclVAT@1006 : Decimal;
      AmountExclVAT@1007 : Decimal;
      VATAmount@1015 : Decimal;
      TextValue@1008 : Text[250];
      Date@1009 : Date;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        DataExch.GET(EntryNo);
        IncomingDocument.GET(DataExch."Incoming Entry No.");

        IF PayToVendorNo <> '' THEN
          IncomingDocument.VALIDATE("Vendor No.",PayToVendorNo)
        ELSE
          IncomingDocument.VALIDATE("Vendor No.",BuyFromVendorNo);

        EVALUATE(
          TextValue,GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Buy-from Vendor Name"),0,RecordNo));
        IncomingDocument.VALIDATE("Vendor Name",COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Vendor Name")));

        TextValue := GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Amount Including VAT"),0,RecordNo);
        IF TextValue <> '' THEN
          EVALUATE(AmountInclVAT,TextValue,9);
        IncomingDocument.VALIDATE("Amount Incl. VAT",AmountInclVAT);

        TextValue := GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO(Amount),0,RecordNo);
        IF TextValue <> '' THEN
          EVALUATE(AmountExclVAT,TextValue,9);
        IncomingDocument.VALIDATE("Amount Excl. VAT",AmountExclVAT);

        TextValue := GetEntryValue(EntryNo,DATABASE::"G/L Entry",GLEntry.FIELDNO("VAT Amount"),0,RecordNo);
        IF TextValue <> '' THEN
          EVALUATE(VATAmount,TextValue,9);
        IncomingDocument.VALIDATE("VAT Amount",VATAmount);

        IF GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Document Type"),0,RecordNo) =
           FORMAT(PurchaseHeader."Document Type"::Invoice,0,9)
        THEN
          EVALUATE(TextValue,GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Vendor Invoice No."),0,RecordNo))
        ELSE
          EVALUATE(
            TextValue,GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Vendor Cr. Memo No."),0,RecordNo));

        IncomingDocument.VALIDATE("Vendor Invoice No.",COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Vendor Invoice No.")));

        EVALUATE(TextValue,GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Vendor Order No."),0,RecordNo));
        IncomingDocument.VALIDATE("Order No.",COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Order No.")));

        EVALUATE(Date,GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Document Date"),0,RecordNo),9);
        IncomingDocument.VALIDATE("Document Date",Date);

        EVALUATE(Date,GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Due Date"),0,RecordNo),9);
        IncomingDocument.VALIDATE("Due Date",Date);

        EVALUATE(TextValue,GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Currency Code"),0,RecordNo));
        GeneralLedgerSetup.GET;
        IF (TextValue <> '') OR (IncomingDocument."Currency Code" <> GeneralLedgerSetup."LCY Code") THEN
          IncomingDocument."Currency Code" := COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Currency Code"));

        EVALUATE(TextValue,GetEntryValue(EntryNo,DATABASE::Vendor,Vendor.FIELDNO("VAT Registration No."),0,RecordNo));
        IncomingDocument.VALIDATE("Vendor VAT Registration No.",
          COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Vendor VAT Registration No.")));

        EVALUATE(TextValue,GetEntryValue(EntryNo,DATABASE::"Vendor Bank Account",VendorBankAccount.FIELDNO(IBAN),0,RecordNo));
        IncomingDocument.VALIDATE("Vendor IBAN",COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Vendor IBAN")));

        EVALUATE(
          TextValue,GetEntryValue(EntryNo,DATABASE::"Vendor Bank Account",VendorBankAccount.FIELDNO("Bank Branch No."),0,RecordNo));
        IncomingDocument.VALIDATE("Vendor Bank Branch No.",COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Vendor Bank Branch No.")));

        EVALUATE(
          TextValue,GetEntryValue(EntryNo,DATABASE::Vendor,Vendor.FIELDNO("Phone No."),0,RecordNo));
        IncomingDocument.VALIDATE("Vendor Phone No.",COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Vendor Phone No.")));

        EVALUATE(
          TextValue,GetEntryValue(EntryNo,DATABASE::"Vendor Bank Account",VendorBankAccount.FIELDNO("Bank Account No."),0,RecordNo));
        IncomingDocument.VALIDATE("Vendor Bank Account No.",
          COPYSTR(TextValue,1,MAXSTRLEN(IncomingDocument."Vendor Bank Account No.")));

        IncomingDocument.MODIFY;
      END;
    END;

    LOCAL PROCEDURE FindBuyFromVendor@17(EntryNo@1003 : Integer;RecordNo@1006 : Integer) : Code[20];
    VAR
      IntermediateDataImport@1000 : Record 1214;
      PurchaseHeader@1001 : Record 38;
      Vendor@1002 : Record 23;
      EmptyVendor@1009 : Record 23;
      IncomingDocument@1011 : Record 130;
      DataExch@1012 : Record 1220;
      GLN@1004 : Text;
      BuyFromName@1005 : Text;
      BuyFromAddress@1008 : Text;
      BuyFromPhoneNo@1013 : Text;
      VatRegNo@1007 : Text;
      VendorId@1014 : Text;
      VendorNo@1010 : Code[20];
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        VendorId := GetEntryValue(EntryNo,DATABASE::Vendor,Vendor.FIELDNO(Id),0,RecordNo);
        VendorNo := FindVendorById(EntryNo,RecordNo,PurchaseHeader.FIELDNO("Buy-from Vendor No."),VendorId);
        IF VendorNo <> '' THEN
          EXIT(VendorNo);

        BuyFromPhoneNo := GetEntryValue(EntryNo,DATABASE::Vendor,Vendor.FIELDNO("Phone No."),0,RecordNo);

        IF FindEntry(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Buy-from Vendor Name"),0,RecordNo) THEN
          BuyFromName := Value;

        SETRANGE("Field ID",PurchaseHeader.FIELDNO("Buy-from Address"));
        IF FINDFIRST THEN
          BuyFromAddress := Value;

        // Lookup GLN
        SETRANGE("Field ID",PurchaseHeader.FIELDNO("Buy-from Vendor No."));
        IF FINDFIRST THEN
          IF Value <> '' THEN BEGIN
            GLN := Value;
            Vendor.SETRANGE(GLN,Value);
            IF Vendor.FINDFIRST THEN BEGIN
              InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",
                PurchaseHeader.FIELDNO("Buy-from Vendor No."),0,RecordNo,Vendor."No.");
              EXIT(Vendor."No.");
            END;
          END;

        Vendor.RESET;
        VatRegNo := '';

        // Lookup VAT Reg No
        SETRANGE("Table ID",DATABASE::Vendor);
        SETRANGE("Field ID",Vendor.FIELDNO("VAT Registration No."));

        IF FINDFIRST THEN BEGIN
          IF (Value = '') AND (GLN = '') THEN BEGIN
            VendorNo := FindVendorByBankAccount(EntryNo,RecordNo,PurchaseHeader.FIELDNO("Buy-from Vendor No."));
            IF VendorNo <> '' THEN
              EXIT(VendorNo);
            VendorNo := FindVendorByPhoneNo(EntryNo,RecordNo,PurchaseHeader.FIELDNO("Buy-from Vendor No."),BuyFromPhoneNo);
            IF VendorNo <> '' THEN
              EXIT(VendorNo);
            EXIT(FindVendorByNameAndAddress(EntryNo,RecordNo,BuyFromName,BuyFromAddress,
                PurchaseHeader.FIELDNO("Buy-from Vendor No.")));
          END;
          VatRegNo := Value;
          IF Value <> '' THEN BEGIN
            Vendor.SETFILTER("VAT Registration No.",
              STRSUBSTNO('*%1',COPYSTR(Value,STRLEN(Value))));
            IF Vendor.FINDSET THEN
              REPEAT
                IF ExtractVatRegNo(Vendor."VAT Registration No.",Vendor."Country/Region Code") =
                   ExtractVatRegNo(Value,Vendor."Country/Region Code")
                THEN BEGIN
                  InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",
                    PurchaseHeader.FIELDNO("Buy-from Vendor No."),0,RecordNo,Vendor."No.");

                  EXIT(Vendor."No.");
                END;
              UNTIL Vendor.NEXT = 0;
          END;
        END;

        IF (VatRegNo = '') AND (GLN = '') THEN BEGIN
          VendorNo := FindVendorByBankAccount(EntryNo,RecordNo,PurchaseHeader.FIELDNO("Buy-from Vendor No."));
          IF VendorNo <> '' THEN
            EXIT(VendorNo);
          VendorNo := FindVendorByPhoneNo(EntryNo,RecordNo,PurchaseHeader.FIELDNO("Buy-from Vendor No."),BuyFromPhoneNo);
          IF VendorNo <> '' THEN
            EXIT(VendorNo);
          EXIT(FindVendorByNameAndAddress(EntryNo,RecordNo,BuyFromName,BuyFromAddress,
              PurchaseHeader.FIELDNO("Buy-from Vendor No.")));
        END;

        DataExch.GET(EntryNo);
        IncomingDocument.GET(DataExch."Incoming Entry No.");
        IF IncomingDocument."Document Type" <> IncomingDocument."Document Type"::Journal THEN
          LogErrorMessage(EntryNo,EmptyVendor,EmptyVendor.FIELDNO(Name),
            STRSUBSTNO(BuyFromVendorNotFoundErr,BuyFromName,GLN,VatRegNo));
        EXIT('');
      END;
    END;

    LOCAL PROCEDURE FindPayToVendor@8(EntryNo@1006 : Integer;RecordNo@1007 : Integer) : Code[20];
    VAR
      IntermediateDataImport@1000 : Record 1214;
      PurchaseHeader@1001 : Record 38;
      Vendor@1002 : Record 23;
      EmptyVendor@1009 : Record 23;
      IncomingDocument@1010 : Record 130;
      DataExch@1011 : Record 1220;
      GLN@1003 : Text;
      VatRegNo@1004 : Text;
      PayToName@1005 : Text;
      PayToAddress@1008 : Text;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        IF FindEntry(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Pay-to Name"),0,RecordNo) THEN
          PayToName := Value;

        SETRANGE("Field ID",PurchaseHeader.FIELDNO("Pay-to Address"));
        IF FINDFIRST THEN
          PayToAddress := Value;

        SETRANGE("Field ID",PurchaseHeader.FIELDNO("VAT Registration No."));
        IF FINDFIRST THEN
          VatRegNo := Value;

        SETRANGE("Field ID",PurchaseHeader.FIELDNO("Pay-to Vendor No."));
        IF FINDFIRST THEN
          GLN := Value;

        IF (VatRegNo = '') AND (GLN = '') THEN BEGIN
          IF PayToName <> '' THEN
            EXIT(FindVendorByNameAndAddress(EntryNo,RecordNo,PayToName,PayToAddress,PurchaseHeader.FIELDNO("Pay-to Vendor No.")));
          EXIT;
        END;

        // Lookup GLN
        IF GLN <> '' THEN BEGIN
          Vendor.SETRANGE(GLN,GLN);
          IF Vendor.FINDFIRST THEN BEGIN
            InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",
              PurchaseHeader.FIELDNO("Pay-to Vendor No."),0,RecordNo,Vendor."No.");

            EXIT(Vendor."No.");
          END;
        END;

        Vendor.RESET;

        // Lookup VAT Reg No
        Vendor.SETFILTER("VAT Registration No.",STRSUBSTNO('*%1',COPYSTR(VatRegNo,STRLEN(VatRegNo))));
        IF Vendor.FINDSET THEN
          REPEAT
            IF ExtractVatRegNo(Vendor."VAT Registration No.",Vendor."Country/Region Code") =
               ExtractVatRegNo(VatRegNo,Vendor."Country/Region Code")
            THEN BEGIN
              InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",
                PurchaseHeader.FIELDNO("Pay-to Vendor No."),0,RecordNo,Vendor."No.");

              EXIT(Vendor."No.");
            END;
          UNTIL Vendor.NEXT = 0;

        DataExch.GET(EntryNo);
        IncomingDocument.GET(DataExch."Incoming Entry No.");
        IF IncomingDocument."Document Type" <> IncomingDocument."Document Type"::Journal THEN
          LogErrorMessage(EntryNo,EmptyVendor,EmptyVendor.FIELDNO(Name),
            STRSUBSTNO(PayToVendorNotFoundErr,PayToName,GLN,VatRegNo));
        EXIT('');
      END;
    END;

    LOCAL PROCEDURE FindVendorByNameAndAddress@4(EntryNo@1006 : Integer;RecordNo@1007 : Integer;VendorName@1008 : Text;VendorAddress@1003 : Text;FieldID@1009 : Integer) : Code[20];
    VAR
      IntermediateDataImport@1000 : Record 1214;
      Vendor@1002 : Record 23;
      EmptyVendor@1013 : Record 23;
      IncomingDocument@1005 : Record 130;
      DataExch@1004 : Record 1220;
      RecordMatchMgt@1001 : Codeunit 1251;
      NameNearness@1011 : Integer;
      AddressNearness@1012 : Integer;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        IF Vendor.FINDSET THEN
          REPEAT
            NameNearness := RecordMatchMgt.CalculateStringNearness(VendorName,Vendor.Name,MatchThreshold,NormalizingFactor);
            IF VendorAddress = '' THEN
              AddressNearness := RequiredNearness
            ELSE
              AddressNearness := RecordMatchMgt.CalculateStringNearness(VendorAddress,Vendor.Address,MatchThreshold,NormalizingFactor);
            IF (NameNearness >= RequiredNearness) AND (AddressNearness >= RequiredNearness) THEN BEGIN
              InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",FieldID,0,RecordNo,Vendor."No.");
              EXIT(Vendor."No.");
            END;
          UNTIL Vendor.NEXT = 0;

        DataExch.GET(EntryNo);
        IncomingDocument.GET(DataExch."Incoming Entry No.");
        IF IncomingDocument."Document Type" <> IncomingDocument."Document Type"::Journal THEN
          LogErrorMessage(EntryNo,EmptyVendor,EmptyVendor.FIELDNO(Name),
            STRSUBSTNO(VendorNotFoundByNameAndAddressErr,VendorName,VendorAddress));
        EXIT('');
      END;
    END;

    LOCAL PROCEDURE FindVendorByBankAccount@23(EntryNo@1006 : Integer;RecordNo@1007 : Integer;FieldID@1009 : Integer) : Code[20];
    VAR
      IntermediateDataImport@1000 : Record 1214;
      VendorBankAccount@1002 : Record 288;
      VendorNo@1003 : Code[20];
      VendorIBAN@1004 : Code[50];
      VendorBankBranchNo@1005 : Text[20];
      VendorBankAccountNo@1008 : Text[30];
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        IF FindEntry(EntryNo,DATABASE::"Vendor Bank Account",VendorBankAccount.FIELDNO(IBAN),0,RecordNo) THEN
          VendorIBAN := COPYSTR(Value,1,MAXSTRLEN(VendorIBAN));

        SETRANGE("Field ID",VendorBankAccount.FIELDNO("Bank Branch No."));
        IF FINDFIRST THEN
          VendorBankBranchNo := COPYSTR(Value,1,MAXSTRLEN(VendorBankBranchNo));

        SETRANGE("Field ID",VendorBankAccount.FIELDNO("Bank Account No."));
        IF FINDFIRST THEN
          VendorBankAccountNo := COPYSTR(Value,1,MAXSTRLEN(VendorBankAccountNo));

        IF VendorIBAN <> '' THEN BEGIN
          VendorBankAccount.SETRANGE(IBAN,VendorIBAN);
          IF VendorBankAccount.FINDFIRST THEN
            VendorNo := VendorBankAccount."Vendor No.";
        END;

        IF (VendorNo = '') AND (VendorBankBranchNo <> '') AND (VendorBankAccountNo <> '') THEN BEGIN
          VendorBankAccount.RESET;
          VendorBankAccount.SETRANGE("Bank Branch No.",VendorBankBranchNo);
          VendorBankAccount.SETRANGE("Bank Account No.",VendorBankAccountNo);
          IF VendorBankAccount.FINDFIRST THEN
            VendorNo := VendorBankAccount."Vendor No.";
        END;

        IF VendorNo <> '' THEN BEGIN
          InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",FieldID,0,RecordNo,VendorNo);
          EXIT(VendorNo);
        END;

        EXIT('');
      END;
    END;

    LOCAL PROCEDURE FindVendorByPhoneNo@35(EntryNo@1006 : Integer;RecordNo@1007 : Integer;FieldID@1009 : Integer;PhoneNo@1001 : Text) : Code[20];
    VAR
      IntermediateDataImport@1000 : Record 1214;
      Vendor@1002 : Record 23;
      RecordMatchMgt@1003 : Codeunit 1251;
      PhoneNoNearness@1010 : Integer;
    BEGIN
      IF PhoneNo = '' THEN
        EXIT('');

      PhoneNo := DELCHR(PhoneNo,'=',DELCHR(PhoneNo,'=','0123456789'));
      WITH IntermediateDataImport DO BEGIN
        IF Vendor.FINDSET THEN
          REPEAT
            PhoneNoNearness := RecordMatchMgt.CalculateStringNearness(PhoneNo,Vendor."Phone No.",MatchThreshold,NormalizingFactor);
            IF PhoneNoNearness >= RequiredNearness THEN BEGIN
              InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",FieldID,0,RecordNo,Vendor."No.");
              EXIT(Vendor."No.");
            END;
          UNTIL Vendor.NEXT = 0;

        EXIT('');
      END;
    END;

    LOCAL PROCEDURE FindVendorById@44(EntryNo@1006 : Integer;RecordNo@1007 : Integer;FieldID@1009 : Integer;VendorIdText@1001 : Text) : Code[20];
    VAR
      IntermediateDataImport@1000 : Record 1214;
      Vendor@1002 : Record 23;
      VendorId@1003 : GUID;
    BEGIN
      IF VendorIdText = '' THEN
        EXIT('');

      IF NOT EVALUATE(VendorId,VendorIdText,9) THEN
        EXIT('');

      Vendor.SETRANGE(Id,VendorId);
      IF NOT Vendor.FINDFIRST THEN
        EXIT('');

      IntermediateDataImport.InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",FieldID,0,RecordNo,Vendor."No.");
      EXIT(Vendor."No.");
    END;

    LOCAL PROCEDURE FindInvoiceToApplyTo@21(EntryNo@1006 : Integer;RecordNo@1007 : Integer);
    VAR
      IntermediateDataImport@1000 : Record 1214;
      PurchaseHeader@1001 : Record 38;
      PurchInvHeader@1003 : Record 122;
      VendorInvoiceNo@1002 : Text;
      AppliesToDocTypeAsInteger@1004 : Integer;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        VendorInvoiceNo := GetEntryValue(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Applies-to Doc. No."),0,RecordNo);
        IF VendorInvoiceNo = '' THEN
          EXIT;

        // Find a posted purchase invoice that has the specified Vendor Invoice No.
        PurchInvHeader.SETRANGE("Vendor Invoice No.",VendorInvoiceNo);
        IF PurchInvHeader.FINDFIRST THEN BEGIN
          AppliesToDocTypeAsInteger := PurchaseHeader."Applies-to Doc. Type"::Invoice;
          InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",
            PurchaseHeader.FIELDNO("Applies-to Doc. Type"),0,RecordNo,FORMAT(AppliesToDocTypeAsInteger));
          InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",
            PurchaseHeader.FIELDNO("Applies-to Doc. No."),0,RecordNo,PurchInvHeader."No.");
          EXIT;
        END;

        // No posted purchase invoice has the specified Vendor Invoice No.
        // This is an error - the user first needs to post the related invoice before importing this document.
        // If we can find an unposted invoice with this Vendor Invoice No. we will link to it in the error message.
        PurchaseHeader.SETRANGE("Vendor Invoice No.",VendorInvoiceNo);
        IF PurchaseHeader.FINDFIRST THEN BEGIN
          LogErrorMessage(EntryNo,PurchaseHeader,PurchaseHeader.FIELDNO("No."),
            STRSUBSTNO(YouMustFirstPostTheRelatedInvoiceErr,VendorInvoiceNo,PurchaseHeader."No."));
          EXIT;
        END;

        // No purchase invoice (posted or not) has the specified Vendor Invoice No.
        // This is an error - the user needs to create and post the related invoice before importing this document.
        LogErrorMessage(
          EntryNo,PurchInvHeader,PurchInvHeader.FIELDNO("No."),STRSUBSTNO(UnableToFindRelatedInvoiceErr,VendorInvoiceNo));
      END;
    END;

    LOCAL PROCEDURE ProcessLine@18(EntryNo@1002 : Integer;HeaderRecordNo@1005 : Integer;RecordNo@1004 : Integer;VendorNo@1003 : Code[20]);
    VAR
      ImportedUnitCode@1000 : Code[10];
    BEGIN
      // Lines with 0 quantity are "empty/description only" lines
      IF IsDescriptionOnlyLine(EntryNo,HeaderRecordNo,RecordNo) THEN BEGIN
        CleanDescriptionOnlyLine(EntryNo,HeaderRecordNo,RecordNo);
        EXIT;
      END;

      // Lookup Cross Ref, then GTIN/Bar Code, else G/L Account
      IF ResolveUnitOfMeasureFromDataImport(ImportedUnitCode,EntryNo,HeaderRecordNo,RecordNo) THEN
        IF NOT FindItemCrossReferenceForLine(ImportedUnitCode,EntryNo,HeaderRecordNo,RecordNo,VendorNo) THEN
          IF NOT FindItemForLine(ImportedUnitCode,EntryNo,HeaderRecordNo,RecordNo) THEN
            IF NOT FindGLAccountForLine(EntryNo,HeaderRecordNo,RecordNo,VendorNo) THEN
              LogErrorIfItemNotFound(EntryNo,HeaderRecordNo,RecordNo,VendorNo);

      ValidateLineDiscount(EntryNo,HeaderRecordNo,RecordNo);
    END;

    LOCAL PROCEDURE InsertLineForTotalDocumentAmount@22(EntryNo@1002 : Integer;HeaderRecordNo@1005 : Integer;RecordNo@1004 : Integer;VendorNo@1003 : Code[20]);
    VAR
      PurchaseLine@1000 : Record 39;
      PurchaseHeader@1007 : Record 38;
      Vendor@1001 : Record 23;
      IntermediateDataImport@1006 : Record 1214;
      LineDescription@1009 : Text[250];
    BEGIN
      IF NOT Vendor.GET(VendorNo) THEN
        EXIT;

      WITH IntermediateDataImport DO BEGIN
        LineDescription := GetEntryValue(
            EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Buy-from Vendor Name"),0,HeaderRecordNo);
        IF LineDescription = '' THEN
          LineDescription := Vendor.Name;
        InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Line",
          PurchaseLine.FIELDNO(Description),HeaderRecordNo,RecordNo,LineDescription);
        InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Line",
          PurchaseLine.FIELDNO(Quantity),HeaderRecordNo,RecordNo,'1');
        InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Line",
          PurchaseLine.FIELDNO("Direct Unit Cost"),HeaderRecordNo,RecordNo,GetTotalAmountExclVAT(EntryNo,HeaderRecordNo));
        FindGLAccountForLine(EntryNo,HeaderRecordNo,RecordNo,VendorNo);
      END;
    END;

    LOCAL PROCEDURE GetTotalAmountExclVAT@25(EntryNo@1002 : Integer;HeaderRecordNo@1005 : Integer) : Text[250];
    VAR
      PurchaseHeader@1000 : Record 38;
      IntermediateDataImport@1006 : Record 1214;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        IF NOT FindEntry(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO(Amount),0,HeaderRecordNo) THEN BEGIN
          LogSimpleErrorMessage(EntryNo,UnableToFindTotalAmountErr);
          EXIT('');
        END;
        EXIT(Value);
      END;
    END;

    LOCAL PROCEDURE FindItemForLine@5(ImportedUnitCode@1007 : Code[10];EntryNo@1004 : Integer;HeaderNo@1006 : Integer;RecordNo@1005 : Integer) : Boolean;
    VAR
      IntermediateDataImport@1000 : Record 1214;
      PurchaseLine@1001 : Record 39;
      Item@1002 : Record 27;
      GTIN@1003 : Text;
    BEGIN
      IF NOT IntermediateDataImport.FindEntry(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("No."),HeaderNo,RecordNo) THEN
        EXIT(FALSE);

      GTIN := IntermediateDataImport.Value;
      IF GTIN = '' THEN
        EXIT(FALSE);

      Item.SETRANGE(GTIN,GTIN);
      IF NOT Item.FINDFIRST THEN
        EXIT(FALSE);

      IntermediateDataImport.Value := Item."No.";
      IntermediateDataImport.MODIFY;

      IntermediateDataImport.InsertOrUpdateEntry(
        EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO(Type),HeaderNo,RecordNo,FORMAT(PurchaseLine.Type::Item,0,9));

      ResolveUnitOfMeasureFromItem(Item,ImportedUnitCode,EntryNo,HeaderNo,RecordNo);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE FindItemCrossReferenceForLine@2(ImportedUnitCode@1006 : Code[10];EntryNo@1007 : Integer;HeaderNo@1008 : Integer;RecordNo@1001 : Integer;VendorNo@1000 : Code[20]) : Boolean;
    VAR
      IntermediateDataImport@1002 : Record 1214;
      PurchaseLine@1003 : Record 39;
      ItemCrossReference@1004 : Record 5717;
      Vendor@1005 : Record 23;
    BEGIN
      IF NOT Vendor.GET(VendorNo) THEN
        EXIT(FALSE);

      IF NOT IntermediateDataImport.FindEntry(
           EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("Cross-Reference No."),HeaderNo,RecordNo)
      THEN
        EXIT(FALSE);

      ItemCrossReference.SETRANGE("Cross-Reference Type",ItemCrossReference."Cross-Reference Type"::Vendor);
      ItemCrossReference.SETRANGE("Cross-Reference Type No.",VendorNo);
      ItemCrossReference.SETRANGE(
        "Cross-Reference No.",COPYSTR(IntermediateDataImport.Value,1,MAXSTRLEN(ItemCrossReference."Cross-Reference No.")));

      IF NOT FindMatchingItemCrossReference(ItemCrossReference,ImportedUnitCode) THEN
        EXIT(FALSE);

      IntermediateDataImport.InsertOrUpdateEntry(
        EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("No."),HeaderNo,RecordNo,FORMAT(ItemCrossReference."Item No.",0,9));
      IntermediateDataImport.InsertOrUpdateEntry(
        EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO(Type),HeaderNo,RecordNo,FORMAT(PurchaseLine.Type::Item,0,9));

      ResolveUnitOfMeasureFromItemCrossReference(ItemCrossReference,ImportedUnitCode,EntryNo,HeaderNo,RecordNo);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE FindMatchingItemCrossReference@46(VAR ItemCrossReference@1000 : Record 5717;ImportedUnitCode@1001 : Code[10]) : Boolean;
    BEGIN
      IF NOT ItemCrossReference.FINDFIRST THEN
        EXIT(FALSE);

      ItemCrossReference.SETRANGE("Unit of Measure",ImportedUnitCode);
      IF ItemCrossReference.FINDSET THEN
        REPEAT
          IF ItemCrossReference.HasValidUnitOfMeasure THEN
            EXIT(TRUE);
        UNTIL ItemCrossReference.NEXT = 0;

      ItemCrossReference.SETRANGE("Unit of Measure",'');
      IF ItemCrossReference.FINDSET THEN
        REPEAT
          IF ItemCrossReference.HasValidUnitOfMeasure THEN
            EXIT(TRUE);
        UNTIL ItemCrossReference.NEXT = 0;

      ItemCrossReference.SETRANGE("Unit of Measure");
      EXIT(ItemCrossReference.FINDFIRST);
    END;

    LOCAL PROCEDURE IsDescriptionOnlyLine@14(EntryNo@1007 : Integer;HeaderRecordNo@1006 : Integer;RecordNo@1005 : Integer) : Boolean;
    VAR
      IntermediateDataImport@1003 : Record 1214;
      PurchaseLine@1001 : Record 39;
      Qty@1002 : Decimal;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        IF NOT FindEntry(EntryNo,DATABASE::"Purchase Line",
             PurchaseLine.FIELDNO(Quantity),HeaderRecordNo,RecordNo)
        THEN
          EXIT(TRUE);

        EVALUATE(Qty,Value,9);
        IF Qty = 0 THEN
          EXIT(TRUE);

        EXIT(FALSE);
      END;
    END;

    LOCAL PROCEDURE CleanDescriptionOnlyLine@6(EntryNo@1004 : Integer;HeaderRecordNo@1002 : Integer;RecordNo@1000 : Integer);
    VAR
      IntermediateDataImport@1003 : Record 1214;
      PurchaseLine@1001 : Record 39;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO(Type),
          HeaderRecordNo,RecordNo,FORMAT(PurchaseLine.Type::" ",0,9));

        SETRANGE("Data Exch. No.",EntryNo);
        SETRANGE("Table ID",DATABASE::"Purchase Line");
        SETRANGE("Parent Record No.",HeaderRecordNo);
        SETRANGE("Record No.",RecordNo);
        SETFILTER("Field ID",'<>%1&<>%2&<>%3',
          PurchaseLine.FIELDNO(Type),PurchaseLine.FIELDNO(Description),PurchaseLine.FIELDNO("Description 2"));
        DELETEALL;
      END;
    END;

    LOCAL PROCEDURE LogErrorIfItemNotFound@10(EntryNo@1005 : Integer;HeaderRecordNo@1004 : Integer;RecordNo@1003 : Integer;VendorNo@1001 : Code[20]) : Boolean;
    VAR
      IntermediateDataImport@1000 : Record 1214;
      PurchaseLine@1002 : Record 39;
      Item@1012 : Record 27;
      GTIN@1006 : Text[250];
      ItemName@1007 : Text[250];
      VendorItemNo@1008 : Text[250];
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        GTIN := GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("No."),
            HeaderRecordNo,RecordNo);

        VendorItemNo := GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("Cross-Reference No."),
            HeaderRecordNo,RecordNo);

        ItemName := GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO(Description),
            HeaderRecordNo,RecordNo);

        IF (GTIN <> '') AND (VendorItemNo <> '') THEN BEGIN
          LogErrorMessage(EntryNo,Item,Item.FIELDNO("No."),
            STRSUBSTNO(ItemNotFoundErr,ItemName,VendorNo,VendorItemNo,GTIN));
          EXIT(FALSE);
        END;

        IF GTIN <> '' THEN BEGIN
          LogErrorMessage(EntryNo,Item,Item.FIELDNO("No."),
            STRSUBSTNO(ItemNotFoundByGTINErr,ItemName,GTIN));
          EXIT(FALSE);
        END;

        IF VendorItemNo <> '' THEN BEGIN
          LogErrorMessage(EntryNo,Item,Item.FIELDNO("No."),
            STRSUBSTNO(ItemNotFoundByVendorItemNoErr,ItemName,VendorNo,VendorItemNo));
          EXIT(FALSE);
        END;

        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE FindGLAccountForLine@20(EntryNo@1005 : Integer;HeaderRecordNo@1004 : Integer;RecordNo@1003 : Integer;VendorNo@1009 : Code[20]) : Boolean;
    VAR
      IntermediateDataImport@1000 : Record 1214;
      PurchaseLine@1002 : Record 39;
      GLAccountNo@1006 : Code[20];
      LineDescription@1008 : Text[250];
      LineDirectUnitCostTxt@1001 : Text;
      LineDirectUnitCost@1007 : Decimal;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        LineDescription := GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO(Description),HeaderRecordNo,RecordNo);
        LineDirectUnitCostTxt :=
          GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("Direct Unit Cost"),HeaderRecordNo,RecordNo);
        IF LineDirectUnitCostTxt <> '' THEN
          EVALUATE(LineDirectUnitCost,LineDirectUnitCostTxt,9);
        GLAccountNo := FindAppropriateGLAccount(EntryNo,HeaderRecordNo,LineDescription,LineDirectUnitCost,VendorNo);

        IF GLAccountNo <> '' THEN BEGIN
          InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("No."),
            HeaderRecordNo,RecordNo,GLAccountNo);
          InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO(Type),
            HeaderRecordNo,RecordNo,FORMAT(PurchaseLine.Type::"G/L Account",0,9));
        END;
      END;
      EXIT(GLAccountNo <> '');
    END;

    LOCAL PROCEDURE InsertOrUpdateUnitOfMeasureCode@58(EntryNo@1002 : Integer;HeaderNo@1003 : Integer;RecordNo@1004 : Integer;UnitCode@1000 : Code[10]);
    VAR
      PurchaseLine@1005 : Record 39;
      IntermediateDataImport@1001 : Record 1214;
    BEGIN
      IntermediateDataImport.InsertOrUpdateEntry(
        EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("Unit of Measure Code"),HeaderNo,RecordNo,UnitCode);
    END;

    LOCAL PROCEDURE ResolveUnitOfMeasureFromItemCrossReference@67(VAR ItemCrossReference@1003 : Record 5717;ImportedUnitCode@1002 : Code[10];EntryNo@1004 : Integer;HeaderNo@1007 : Integer;RecordNo@1000 : Integer) : Boolean;
    VAR
      Item@1001 : Record 27;
      ResolvedUnitCode@1005 : Code[10];
    BEGIN
      ResolvedUnitCode := ItemCrossReference."Unit of Measure";
      IF ResolvedUnitCode = '' THEN BEGIN
        Item.GET(ItemCrossReference."Item No.");
        EXIT(ResolveUnitOfMeasureFromItem(Item,ImportedUnitCode,EntryNo,HeaderNo,RecordNo));
      END;

      IF (ImportedUnitCode <> '') AND (ImportedUnitCode <> ResolvedUnitCode) THEN BEGIN
        LogErrorMessage(EntryNo,ItemCrossReference,ItemCrossReference.FIELDNO("Unit of Measure"),
          STRSUBSTNO(UOMConflictWithCrossRefErr,ImportedUnitCode,RecordNo,UnitCodeToString(ResolvedUnitCode)));
        EXIT(FALSE);
      END;

      IF NOT ItemCrossReference.HasValidUnitOfMeasure THEN BEGIN
        LogErrorMessage(EntryNo,ItemCrossReference,ItemCrossReference.FIELDNO("Unit of Measure"),
          STRSUBSTNO(UOMConflictCrossRefWithItemErr,UnitCodeToString(ResolvedUnitCode)));
        EXIT(FALSE);
      END;

      InsertOrUpdateUnitOfMeasureCode(EntryNo,HeaderNo,RecordNo,ResolvedUnitCode);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ResolveUnitOfMeasureFromItem@74(VAR Item@1001 : Record 27;ImportedUnitCode@1002 : Code[10];EntryNo@1004 : Integer;HeaderNo@1006 : Integer;RecordNo@1000 : Integer) : Boolean;
    VAR
      ResolvedUnitCode@1003 : Code[10];
    BEGIN
      ResolvedUnitCode := Item."Purch. Unit of Measure";
      IF ResolvedUnitCode = '' THEN
        ResolvedUnitCode := Item."Base Unit of Measure";

      IF (ImportedUnitCode <> '') AND (ImportedUnitCode <> ResolvedUnitCode) THEN BEGIN
        LogErrorMessage(EntryNo,Item,Item.FIELDNO("Base Unit of Measure"),
          STRSUBSTNO(UOMConflictWithItemErr,ImportedUnitCode,RecordNo,UnitCodeToString(ResolvedUnitCode)));
        EXIT(FALSE);
      END;

      InsertOrUpdateUnitOfMeasureCode(EntryNo,HeaderNo,RecordNo,ResolvedUnitCode);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ResolveUnitOfMeasureFromDataImport@42(VAR ImportedUnitCode@1004 : Code[10];EntryNo@1002 : Integer;HeaderNo@1005 : Integer;RecordNo@1006 : Integer) : Boolean;
    VAR
      PurchaseLine@1007 : Record 39;
      UnitOfMeasure@1000 : Record 204;
      IntermediateDataImport@1001 : Record 1214;
      ImportedUnitString@1003 : Text;
    BEGIN
      IF NOT IntermediateDataImport.FindEntry(
           EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("Unit of Measure Code"),HeaderNo,RecordNo)
      THEN BEGIN
        LogSimpleErrorMessage(EntryNo,STRSUBSTNO(UOMMissingErr,RecordNo));
        EXIT(FALSE);
      END;

      ImportedUnitString := IntermediateDataImport.Value;
      IF ImportedUnitString = '' THEN BEGIN
        ImportedUnitCode := '';
        InsertOrUpdateUnitOfMeasureCode(EntryNo,HeaderNo,RecordNo,ImportedUnitCode);
        EXIT(TRUE);
      END;

      UnitOfMeasure.SETRANGE(Code,COPYSTR(ImportedUnitString,1,MAXSTRLEN(UnitOfMeasure.Code)));
      IF UnitOfMeasure.FINDFIRST THEN BEGIN
        ImportedUnitCode := UnitOfMeasure.Code;
        InsertOrUpdateUnitOfMeasureCode(EntryNo,HeaderNo,RecordNo,ImportedUnitCode);
        EXIT(TRUE);
      END;

      UnitOfMeasure.SETRANGE(Code);
      UnitOfMeasure.SETRANGE(
        "International Standard Code",COPYSTR(ImportedUnitString,1,MAXSTRLEN(UnitOfMeasure."International Standard Code")));
      IF UnitOfMeasure.FINDFIRST THEN BEGIN
        ImportedUnitCode := UnitOfMeasure.Code;
        InsertOrUpdateUnitOfMeasureCode(EntryNo,HeaderNo,RecordNo,ImportedUnitCode);
        EXIT(TRUE);
      END;

      UnitOfMeasure.SETRANGE("International Standard Code");
      UnitOfMeasure.SETRANGE(Description,ImportedUnitString);
      IF UnitOfMeasure.FINDFIRST THEN BEGIN
        ImportedUnitCode := UnitOfMeasure.Code;
        InsertOrUpdateUnitOfMeasureCode(EntryNo,HeaderNo,RecordNo,ImportedUnitCode);
        EXIT(TRUE);
      END;

      LogErrorMessage(EntryNo,UnitOfMeasure,UnitOfMeasure.FIELDNO(Code),STRSUBSTNO(UOMNotFoundErr,ImportedUnitString));
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE UnitCodeToString@99(UnitCode@1000 : Code[10]) : Text;
    BEGIN
      IF UnitCode <> '' THEN
        EXIT(UnitCode);
      EXIT(NotSpecifiedUnitOfMeasureTxt);
    END;

    LOCAL PROCEDURE ValidateLineDiscount@12(EntryNo@1004 : Integer;HeaderRecordNo@1005 : Integer;RecordNo@1000 : Integer);
    VAR
      PurchaseLine@1002 : Record 39;
      IntermediateDataImport@1001 : Record 1214;
      LineDirectUnitCostTxt@1003 : Text;
      LineQuantityTxt@1006 : Text;
      LineAmountTxt@1007 : Text;
      LineDirectUnitCost@1008 : Decimal;
      LineAmount@1009 : Decimal;
      LineQuantity@1010 : Decimal;
      LineDiscountAmount@1011 : Decimal;
    BEGIN
      WITH IntermediateDataImport DO BEGIN
        IF GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("Line Discount Amount"),HeaderRecordNo,RecordNo) <> ''
        THEN
          EXIT;

        // if no discount amount has been specified, calculate it based on quantity, direct unit cost and line extension amount
        LineDirectUnitCostTxt :=
          GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("Direct Unit Cost"),HeaderRecordNo,RecordNo);
        IF LineDirectUnitCostTxt <> '' THEN
          EVALUATE(LineDirectUnitCost,LineDirectUnitCostTxt,9);
        LineQuantityTxt :=
          GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO(Quantity),HeaderRecordNo,RecordNo);
        IF LineQuantityTxt <> '' THEN
          EVALUATE(LineQuantity,LineQuantityTxt,9);
        LineAmountTxt :=
          GetEntryValue(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO(Amount),HeaderRecordNo,RecordNo);
        IF LineAmountTxt <> '' THEN
          EVALUATE(LineAmount,LineAmountTxt,9);
        LineDiscountAmount := (LineQuantity * LineDirectUnitCost) - LineAmount;

        InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Line",PurchaseLine.FIELDNO("Line Discount Amount"),
          HeaderRecordNo,RecordNo,FORMAT(LineDiscountAmount,0,9));

        MODIFY;
      END;
    END;

    LOCAL PROCEDURE ExtractVatRegNo@1(VatRegNo@1000 : Text;CountryRegionCode@1002 : Text) : Text;
    VAR
      CompanyInformation@1001 : Record 79;
    BEGIN
      IF CountryRegionCode = '' THEN BEGIN
        CompanyInformation.GET;
        CountryRegionCode := CompanyInformation."Country/Region Code";
      END;
      VatRegNo := UPPERCASE(VatRegNo);
      VatRegNo := DELCHR(VatRegNo,'=',DELCHR(VatRegNo,'=','ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'));
      IF STRPOS(VatRegNo,UPPERCASE(CountryRegionCode)) = 1 THEN
        VatRegNo := DELSTR(VatRegNo,1,STRLEN(CountryRegionCode));
      EXIT(VatRegNo);
    END;

    LOCAL PROCEDURE FindDistinctRecordNos@3(VAR TempInteger@1000 : TEMPORARY Record 2000000026;DataExchEntryNo@1003 : Integer;TableID@1001 : Integer;ParentRecNo@1004 : Integer);
    VAR
      IntermediateDataImport@1002 : Record 1214;
      CurrRecNo@1005 : Integer;
    BEGIN
      CurrRecNo := -1;
      CLEAR(TempInteger);
      TempInteger.DELETEALL;

      WITH IntermediateDataImport DO BEGIN
        SETRANGE("Data Exch. No.",DataExchEntryNo);
        SETRANGE("Table ID",TableID);
        SETRANGE("Parent Record No.",ParentRecNo);
        SETCURRENTKEY("Record No.");
        IF NOT FINDSET THEN
          EXIT;

        REPEAT
          IF CurrRecNo <> "Record No." THEN BEGIN
            CurrRecNo := "Record No.";
            CLEAR(TempInteger);
            TempInteger.Number := CurrRecNo;
            TempInteger.INSERT;
          END;
        UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE LogErrorMessage@7(EntryNo@1004 : Integer;RelatedRec@1001 : Variant;FieldNo@1002 : Integer;Message@1003 : Text);
    VAR
      ErrorMessage@1000 : Record 700;
      DataExch@1005 : Record 1220;
      IncomingDocument@1006 : Record 130;
    BEGIN
      DataExch.GET(EntryNo);
      IncomingDocument.GET(DataExch."Incoming Entry No.");

      ErrorMessage.SetContext(IncomingDocument);
      ErrorMessage.LogMessage(RelatedRec,FieldNo,ErrorMessage."Message Type"::Error,Message);
    END;

    LOCAL PROCEDURE LogSimpleErrorMessage@19(EntryNo@1004 : Integer;Message@1003 : Text);
    VAR
      ErrorMessage@1000 : Record 700;
      DataExch@1005 : Record 1220;
      IncomingDocument@1006 : Record 130;
    BEGIN
      DataExch.GET(EntryNo);
      IncomingDocument.GET(DataExch."Incoming Entry No.");

      ErrorMessage.SetContext(IncomingDocument);
      ErrorMessage.LogSimpleMessage(ErrorMessage."Message Type"::Error,Message);
    END;

    LOCAL PROCEDURE SetDocumentType@11(EntryNo@1000 : Integer;ParentRecNo@1001 : Integer;CurrRecNo@1002 : Integer);
    VAR
      IntermediateDataImport@1004 : Record 1214;
      PurchaseHeader@1003 : Record 38;
      DataExch@1006 : Record 1220;
      DataExchDef@1007 : Record 1222;
      DocumentType@1005 : Text[250];
    BEGIN
      DataExch.GET(EntryNo);
      DataExchDef.GET(DataExch."Data Exch. Def Code");
      WITH IntermediateDataImport DO BEGIN
        IF NOT FindEntry(EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Document Type"),ParentRecNo,CurrRecNo) THEN
          LogErrorMessage(EntryNo,DataExchDef,DataExchDef.FIELDNO(Code),
            ConstructDocumenttypeUnknownErr);

        CASE UPPERCASE(Value) OF
          GetDocumentTypeOptionString(PurchaseHeader."Document Type"::Invoice),
          GetDocumentTypeOptionCaption(PurchaseHeader."Document Type"::Invoice):
            DocumentType := FORMAT(PurchaseHeader."Document Type"::Invoice,0,9);
          GetDocumentTypeOptionString(PurchaseHeader."Document Type"::"Credit Memo"),
          GetDocumentTypeOptionCaption(PurchaseHeader."Document Type"::"Credit Memo"),
          'CREDIT NOTE':
            DocumentType := FORMAT(PurchaseHeader."Document Type"::"Credit Memo",0,9);
          ELSE
            LogErrorMessage(EntryNo,DataExchDef,DataExchDef.FIELDNO(Code),
              ConstructDocumenttypeUnknownErr);
        END;
      END;

      IntermediateDataImport.InsertOrUpdateEntry(EntryNo,DATABASE::"Purchase Header",
        PurchaseHeader.FIELDNO("Document Type"),ParentRecNo,CurrRecNo,
        DocumentType);
    END;

    [External]
    PROCEDURE GetDocumentTypeOptionString@37(OptionIndex@1000 : Integer) : Text[250];
    VAR
      PurchaseHeader@1001 : Record 38;
      PurchaseHeaderRecRef@1003 : RecordRef;
      DocumentTypeFieldRef@1002 : FieldRef;
    BEGIN
      PurchaseHeaderRecRef.OPEN(DATABASE::"Purchase Header");
      DocumentTypeFieldRef := PurchaseHeaderRecRef.FIELD(PurchaseHeader.FIELDNO("Document Type"));
      EXIT(UPPERCASE(SELECTSTR(OptionIndex + 1,DocumentTypeFieldRef.OPTIONSTRING)));
    END;

    [External]
    PROCEDURE GetDocumentTypeOptionCaption@27(OptionIndex@1000 : Integer) : Text[250];
    VAR
      PurchaseHeader@1001 : Record 38;
      PurchaseHeaderRecRef@1003 : RecordRef;
      DocumentTypeFieldRef@1002 : FieldRef;
    BEGIN
      PurchaseHeaderRecRef.OPEN(DATABASE::"Purchase Header");
      DocumentTypeFieldRef := PurchaseHeaderRecRef.FIELD(PurchaseHeader.FIELDNO("Document Type"));
      EXIT(UPPERCASE(SELECTSTR(OptionIndex + 1,DocumentTypeFieldRef.OPTIONCAPTION)));
    END;

    [External]
    PROCEDURE ConstructDocumenttypeUnknownErr@13() : Text;
    VAR
      PurchaseHeader@1002 : Record 38;
      DataExchColumnDef@1003 : Record 1223;
      DataExchColDefPart@1001 : Page 1216;
      DataExchDefCard@1000 : Page 1210;
    BEGIN
      EXIT(STRSUBSTNO(DocumentTypeUnknownErr,
          DataExchColDefPart.CAPTION,
          DataExchDefCard.CAPTION,
          GetDocumentTypeOptionCaption(PurchaseHeader."Document Type"::Invoice),
          GetDocumentTypeOptionCaption(PurchaseHeader."Document Type"::"Credit Memo"),
          DataExchColumnDef.FIELDCAPTION(Constant),
          PurchaseHeader.FIELDCAPTION("Document Type"),
          PurchaseHeader.TABLECAPTION));
    END;

    [External]
    PROCEDURE FindAppropriateGLAccount@28(EntryNo@1003 : Integer;HeaderRecordNo@1006 : Integer;LineDescription@1001 : Text[250];LineDirectUnitCost@1000 : Decimal;VendorNo@1009 : Code[20]) : Code[20];
    VAR
      PurchasesPayablesSetup@1002 : Record 312;
      TextToAccountMapping@1005 : Record 1251;
      PurchaseHeader@1007 : Record 38;
      IntermediateDataImport@1012 : Record 1214;
      DocumentTypeTxt@1013 : Text;
      DocumentType@1004 : Option;
      DefaultGLAccount@1008 : Code[20];
      CountOfResult@1010 : Integer;
    BEGIN
      DocumentTypeTxt := IntermediateDataImport.GetEntryValue(
          EntryNo,DATABASE::"Purchase Header",PurchaseHeader.FIELDNO("Document Type"),0,HeaderRecordNo);
      IF NOT EVALUATE(DocumentType,DocumentTypeTxt) THEN
        EXIT('');

      CountOfResult := TextToAccountMapping.SearchEnteriesInText(TextToAccountMapping,LineDescription,VendorNo);
      IF CountOfResult = 1 THEN
        EXIT(FindCorrectAccountFromMapping(TextToAccountMapping,LineDirectUnitCost,DocumentType));
      IF CountOfResult > 1 THEN BEGIN
        LogErrorMessage(EntryNo,TextToAccountMapping,TextToAccountMapping.FIELDNO("Mapping Text"),
          STRSUBSTNO(UnableToFindAppropriateAccountErr,LineDescription));
        EXIT('');
      END;

      IF VendorNo <> '' THEN BEGIN
        CountOfResult := TextToAccountMapping.SearchEnteriesInText(TextToAccountMapping,LineDescription,'');
        IF CountOfResult = 1 THEN
          EXIT(FindCorrectAccountFromMapping(TextToAccountMapping,LineDirectUnitCost,DocumentType));
        IF CountOfResult > 1 THEN BEGIN
          LogErrorMessage(EntryNo,TextToAccountMapping,TextToAccountMapping.FIELDNO("Mapping Text"),
            STRSUBSTNO(UnableToFindAppropriateAccountErr,LineDescription));
          EXIT('');
        END;
      END;

      // if you don't find any suggestion in Text-to-Account Mapping, then look in the Purchases & Payables table
      PurchasesPayablesSetup.GET;
      CASE DocumentType OF
        PurchaseHeader."Document Type"::Invoice:
          BEGIN
            IF LineDirectUnitCost >= 0 THEN
              DefaultGLAccount := PurchasesPayablesSetup."Debit Acc. for Non-Item Lines"
            ELSE
              DefaultGLAccount := PurchasesPayablesSetup."Credit Acc. for Non-Item Lines";
          END;
        PurchaseHeader."Document Type"::"Credit Memo":
          BEGIN
            IF LineDirectUnitCost >= 0 THEN
              DefaultGLAccount := PurchasesPayablesSetup."Credit Acc. for Non-Item Lines"
            ELSE
              DefaultGLAccount := PurchasesPayablesSetup."Debit Acc. for Non-Item Lines";
          END;
      END;
      IF DefaultGLAccount = '' THEN
        LogErrorMessage(EntryNo,TextToAccountMapping,TextToAccountMapping.FIELDNO("Mapping Text"),
          STRSUBSTNO(UnableToFindAppropriateAccountErr,LineDescription));
      EXIT(DefaultGLAccount)
    END;

    LOCAL PROCEDURE NormalizingFactor@30() : Integer;
    BEGIN
      EXIT(100)
    END;

    LOCAL PROCEDURE MatchThreshold@31() : Integer;
    BEGIN
      EXIT(4)
    END;

    LOCAL PROCEDURE RequiredNearness@32() : Integer;
    BEGIN
      EXIT(95)
    END;

    LOCAL PROCEDURE FindCorrectAccountFromMapping@40(TextToAccountMapping@1002 : Record 1251;LineDirectUnitCost@1000 : Decimal;DocumentType@1003 : Option) : Code[20];
    VAR
      PurchaseHeader@1004 : Record 38;
    BEGIN
      CASE DocumentType OF
        PurchaseHeader."Document Type"::Invoice:
          BEGIN
            IF (LineDirectUnitCost >= 0) AND (TextToAccountMapping."Debit Acc. No." <> '') THEN
              EXIT(TextToAccountMapping."Debit Acc. No.");
            IF (LineDirectUnitCost < 0) AND (TextToAccountMapping."Credit Acc. No." <> '') THEN
              EXIT(TextToAccountMapping."Credit Acc. No.");
          END;
        PurchaseHeader."Document Type"::"Credit Memo":
          BEGIN
            IF (LineDirectUnitCost >= 0) AND (TextToAccountMapping."Credit Acc. No." <> '') THEN
              EXIT(TextToAccountMapping."Credit Acc. No.");
            IF (LineDirectUnitCost < 0) AND (TextToAccountMapping."Debit Acc. No." <> '') THEN
              EXIT(TextToAccountMapping."Debit Acc. No.");
          END;
      END
    END;

    BEGIN
    END.
  }
}

