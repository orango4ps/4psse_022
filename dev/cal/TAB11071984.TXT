OBJECT Table 11071984 Debrief E-Mail Body Text
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Debrief E-Mail Body Text;
  }
  FIELDS
  {
    { 10  ;   ;Debrief Text Rubric Code;Code10    ;TableRelation="Debrief Text Rubric";
                                                   CaptionML= }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer] }
    { 40  ;   ;Text                ;Text250       ;CaptionML=[ENU=Text;
                                                              NOR=Tekst;
                                                              SVE=Ben„mning] }
    { 50  ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table),
                                                                                  ID=FILTER(79|11012823|11012886));
                                                   OnValidate=BEGIN
                                                                IF "Table No." <> xRec."Table No." THEN
                                                                  "Field No." := 0;

                                                                CALCFIELDS("Table Caption");
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr] }
    { 60  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Enabled=CONST(Yes),
                                                                                  Class=FILTER(Normal|FlowField));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Field Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              FieldTableManagement@1100525000 : Codeunit 11012372;
                                                            BEGIN
                                                              VALIDATE("Field No.", FieldTableManagement.LookupFieldNo("Table No.", "Field No."));
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr] }
    { 70  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Caption;
                                                              NOR=Tabelltittel;
                                                              SVE=Tabellrubrik];
                                                   Editable=No }
    { 80  ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              NOR=Felttekst;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Debrief Text Rubric Code,Line No.       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text002@1100526203 : TextConst 'ENU=******************************;NOR=******************************;SVE=******************************';

    PROCEDURE GetSubstituteResultTextLine@1100525000(DebriefText@1100525000 : Record 11012886;ServiceOrder@1100525001 : Record 11012823) : Text[250];
    VAR
      Pos@1100525002 : Integer;
      Substitute@1100525003 : Text[250];
      LenSubstitute@1100525004 : Integer;
    BEGIN
      Pos := STRPOS(Text, '%1');
      IF Pos <> 0 THEN BEGIN
        Substitute := GetSubstitute(DebriefText, ServiceOrder);
        LenSubstitute := STRLEN(Substitute);
        EXIT(
          COPYSTR(Text, 1, Pos - 1) +
          Substitute +
          COPYSTR(Text, Pos + 2, 250 - LenSubstitute - Pos + 1));
      END ELSE
        EXIT(Text);
    END;

    LOCAL PROCEDURE GetSubstitute@1210190004(DebriefText@1100525000 : Record 11012886;ServiceOrder@1100526201 : Record 11012823) @11012000 : Text;
    VAR
      CompanyInformation@1210190002 : Record 79;
      RecRef@1100525001 : RecordRef;
    BEGIN
      CASE "Table No." OF
        DATABASE::"Company Information":
          BEGIN
            CompanyInformation.GET;
            RecRef.GETTABLE(CompanyInformation);
            EXIT(AnyTableFieldValue(RecRef, "Field No."));
          END;
        DATABASE::"Service Order":
          BEGIN
            RecRef.GETTABLE(ServiceOrder);
            EXIT(AnyTableFieldValue(RecRef, "Field No."));
          END;
        DATABASE::"Debrief Text":
          BEGIN
            RecRef.GETTABLE(DebriefText);
            EXIT(AnyTableFieldValue(RecRef, "Field No."));
          END;
      END;
    END;

    LOCAL PROCEDURE AnyTableFieldValue@1210190010(RecRef@1100525005 : RecordRef;FieldNo@1210190001 : Integer) : Text[250];
    VAR
      FieldRec@1100525003 : Record 2000000041;
      FldRef@1100526200 : FieldRef;
      DecimalValue@1100525004 : Decimal;
      DateValue@1100525000 : Date;
      TimeValue@1100525001 : Time;
    BEGIN
      //copied from codeunit 110120401
      FldRef := RecRef.FIELD(FieldNo);
      IF NOT FieldRec.GET(RecRef.NUMBER, FieldNo) THEN
        EXIT(Text002);

      IF FieldRec.Class = FieldRec.Class::FlowField THEN
        FldRef.CALCFIELD;

      CASE FieldRec.Type OF
        FieldRec.Type::Decimal:
          BEGIN
            EVALUATE(DecimalValue, FORMAT(FldRef.VALUE));
            EXIT(FormatDecimal(DecimalValue));
          END;
        FieldRec.Type::Date:
          BEGIN
            DateValue := FldRef.VALUE;
            EXIT(FormatDate(DateValue));
          END;
        FieldRec.Type::Time:
          BEGIN
            TimeValue := FldRef.VALUE;
            EXIT(FormatTime(TimeValue));
          END;
      END;

      EXIT(FORMAT(FldRef));
    END;

    LOCAL PROCEDURE FormatDecimal@1210190009(DecimalValue@1210190000 : Decimal) : Text[30];
    BEGIN
      //copied from codeunit 110120401
      EXIT(FORMAT(DecimalValue, 0, '<Precision,2:><Standard format,0>'));
    END;

    LOCAL PROCEDURE FormatDate@1100528601(DateValue@1100528600 : Date) : Text[30];
    BEGIN
      EXIT(FORMAT(DateValue));
    END;

    LOCAL PROCEDURE FormatTime@1100528602(TimeValue@1100528600 : Time) : Text[30];
    BEGIN
      EXIT(FORMAT(TimeValue));
    END;

    BEGIN
    END.
  }
}

