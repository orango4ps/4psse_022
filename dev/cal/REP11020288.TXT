OBJECT Report 11020288 Copy Blkt.Scheme Plot to Plots
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Leeres Schema GrundstÅck zu anderen GrundstÅcken verschieben;
               ENU=Copy Blanket Scheme Plot to Other Plots;
               NLD=Afroepschema bouwnummer naar andere bouwnummers kopiâren];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  Proj@1100525000 : Record 11072003;
                  Plot2@1100525001 : Record 11012500;
                  Date@1100525003 : Record 2000000007;
                  FieldOK@1100525004 : Boolean;
                BEGIN
                  FieldOK := TRUE;
                  IF ("Blanket Scheme".GETFILTER("Project No.") = '') OR
                     (STRLEN("Blanket Scheme".GETFILTER("Project No.")) > MAXSTRLEN("Blanket Scheme"."Project No."))
                  THEN
                    FieldOK := FALSE
                  ELSE BEGIN
                    IF NOT Proj.GET("Blanket Scheme".GETFILTER("Project No.")) THEN
                      FieldOK := FALSE
                  END;
                  IF NOT FieldOK THEN
                    ERROR(Text001 + ' ' + Text003, "Blanket Scheme".FIELDCAPTION("Project No."));
                  //
                  FieldOK := TRUE;
                  IF ("Blanket Scheme".GETFILTER("Plot No.") = '') OR
                     (STRLEN("Blanket Scheme".GETFILTER("Plot No.")) > MAXSTRLEN("Blanket Scheme"."Plot No."))
                  THEN
                    FieldOK := FALSE
                  ELSE BEGIN
                    IF NOT Plot2.GET(Proj."No.", "Blanket Scheme".GETFILTER("Plot No.")) THEN
                      FieldOK := FALSE
                  END;
                  IF NOT FieldOK THEN
                    ERROR(Text001 + ' ' + Text003, "Blanket Scheme".FIELDCAPTION("Plot No."));
                  //
                  IF "Blanket Scheme".GETFILTER("Blanket Date") = '' THEN
                    ERROR(Text001 + ' ' + Text004);
                  IF Plot.GETFILTER("Plot No.") = '' THEN
                    ERROR(Text002 + ' ' + Text005);

                  Date.SETRANGE("Period Type", Date."Period Type"::Date);
                  Date.SETFILTER("Period Start", "Blanket Scheme".GETFILTER("Blanket Date"));
                  IF Date.FINDFIRST THEN BEGIN
                    IF (Date."Period Start" + ShiftNoOfDays) < TODAY THEN BEGIN
                      IF NOT CONFIRM(Text001 +' '+ Text006, FALSE, Date."Period Start", ShiftNoOfDays, Date."Period Start"+ShiftNoOfDays) THEN
                        ERROR('');
                    END;
                  END;
                END;

  }
  DATASET
  {
    { 1100525000;;DataItem;                  ;
               DataItemTable=Table11012066;
               DataItemTableView=SORTING(Document No.,Document Line No.,Line No.);
               ReqFilterHeadingML=[DEU=Kopieren von;
                                   ENU=Copy from;
                                   NLD=Kopiâren van;
                                   NOR=Kopier fra;
                                   SVE=Kopiera frÜn];
               OnAfterGetRecord=BEGIN
                                  MarkedPurchLine.FILTERGROUP(11);
                                  MarkedPurchLine.SETRANGE("Document Type", MarkedPurchLine."Document Type"::Order);
                                  MarkedPurchLine.SETRANGE("Document No.", "Document No.");
                                  MarkedPurchLine.SETRANGE("Line No.", "Document Line No.");
                                  MarkedPurchLine.FILTERGROUP(0);
                                  IF MarkedPurchLine.ISEMPTY THEN
                                    CurrReport.SKIP;
                                END;

               ReqFilterFields=Document No.,Project No.,Plot No.,Blanket Date }

    { 1100525001;1;DataItem;                 ;
               DataItemTable=Table11012500;
               DataItemTableView=SORTING(Project No.,Plot No.);
               ReqFilterHeadingML=[DEU=Kopieren in;
                                   ENU=Copy to;
                                   NLD=Kopierân naar;
                                   NOR=Kopier til;
                                   SVE=Kopiera till];
               OnPreDataItem=BEGIN
                               Plot.FILTERGROUP(7);
                               Plot.SETFILTER("Plot No.", '<>%1', "Blanket Scheme"."Plot No.");
                               Plot.FILTERGROUP(0);
                             END;

               OnAfterGetRecord=VAR
                                  BlanketScheme@1100525000 : Record 11012066;
                                BEGIN
                                  BlanketScheme := "Blanket Scheme";
                                  BlanketScheme."Line No." := 0;
                                  BlanketScheme."Receipts Created" := FALSE;
                                  BlanketScheme."Blanket Date" := BlanketScheme."Blanket Date" + ShiftNoOfDays;
                                  BlanketScheme.VALIDATE("Plot No.", Plot."Plot No.");
                                  BlanketScheme.Quantity := 0;  //xRec must be 0
                                  BlanketScheme.VALIDATE(Quantity, "Blanket Scheme".Quantity);
                                  BlanketScheme.INSERT(TRUE);
                                END;

               ReqFilterFields=Plot No.;
               DataItemLink=Project No.=FIELD(Project No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525002;0;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=Anzahl der Tage verschieben;
                             ENU=Shift Number of Days;
                             NLD=Aantal dagen verschuiven];
                  SourceExpr=ShiftNoOfDays }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      MarkedPurchLine@1100525001 : Record 39;
      ShiftNoOfDays@1100525000 : Integer;
      Text001@1100525002 : TextConst 'DEU=Kopieren von:;ENU=Copy from:;NLD=Kopieren van:';
      Text002@1100525003 : TextConst 'DEU=Kopieren zu:;ENU=Copy to:;NLD=Kopieren naar:';
      Text003@1100525004 : TextConst 'DEU=Kein oder unbekannt %1;ENU=No or unknown %1;NLD=Geen of onbekend %1';
      Text004@1100525005 : TextConst 'DEU=Kein Blankodatumsfilter;ENU=No Blanket Date Filter;NLD=Geen afroepdatumfilter';
      Text005@1100525006 : TextConst 'DEU=Kein GrundstÅckfilter;ENU=No Plot Filter;NLD=Geen bouwummerfilter';
      Text006@1100525007 : TextConst 'DEU=Erstes Datum aus Blankodatumsfilter %1 mit Verschiebung von %2 Tagen fÅhrt zu Daten ab %3. Dies liegt in der Vergangenheit, fortfahren?;ENU=First date from Blanket Date Filter %1 with shift of %2 days results in dates from %3. This is in the past, continue?;NLD=Eeste datum uit afroepdatumfilter %1 met verschuiving van %2 dagen resulteerd in datums vanaf %3. Dit ligt in verleden, doorgaan?';

    PROCEDURE InitSelections@1210190000(VAR PurchLine@1210190000 : Record 39);
    BEGIN
      MarkedPurchLine.COPY(PurchLine);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

