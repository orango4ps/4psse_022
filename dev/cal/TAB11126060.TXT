OBJECT Table 11126060 Proj. Hr. StMng. CostCode Sel.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ERROR('');  //* Only indirect insert allowed (so no manual insert by the user)
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=[ENU=Project Hour Acc. Site Manager - Cost Code Selection;
               NOR=Prosjekttimekonto byggeleder - kostnadskode utvalg;
               SVE=Arbetsledare, ack. projekttimmar Õ Val av kostnadskod];
  }
  FIELDS
  {
    { 10  ;   ;Year                ;Integer       ;CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   Editable=No }
    { 20  ;   ;Week                ;Integer       ;CaptionML=[ENU=Week;
                                                              NOR=Uke;
                                                              SVE=Vecka];
                                                   MinValue=1;
                                                   MaxValue=53;
                                                   Editable=No }
    { 30  ;   ;Receiving Company   ;Text50        ;TableRelation="Site Mngmnt. Hours Companies";
                                                   CaptionML=[ENU=Receiving Company;
                                                              NOR=Mottakselskap;
                                                              SVE=Mottagningsfîretag] }
    { 40  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projekt- nr];
                                                   Editable=No }
    { 50  ;   ;Cost Code           ;Code20        ;TableRelation="Cost Code";
                                                   CaptionML=[ENU=Cost Code;
                                                              NOR=Kostkode;
                                                              SVE=Kostnadskod];
                                                   Editable=No }
    { 60  ;   ;Cost Code Description;Text30       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cost Code".Description WHERE (Code=FIELD(Cost Code)));
                                                   CaptionML=[ENU=Cost Code Description;
                                                              NOR=Kostkode, Beskrivelse;
                                                              SVE=Kostnadskod, beskrivning];
                                                   Editable=No }
    { 80  ;   ;Select              ;Boolean       ;CaptionML=[ENU=Select;
                                                              NOR=Velg;
                                                              SVE=VÑlj] }
  }
  KEYS
  {
    {    ;Year,Week,Receiving Company,Project No.,Cost Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=Building %1 #2###############;NOR=Bygning %1 #2###############;SVE=Byggnad  %1 #2###############';
      Text002@1210190001 : TextConst 'ENU=In week %1-%2 already booked on %3 ''%4'' of %5 ''%6'', this must be modified/deleted first.;NOR=I uke %1-%2 er det allerede bestilt for %3''%4'' av %5 ''%6'', dette mÜ modifiseres/slettes fõrst.;SVE=I vecka %1-%2 redan bokad fîr %3 ''%4'' av %5 ''%6''. Detta mÜste fîrst Ñndras/tas bort.';
      SiteMgtHrSetup@1210190002 : Record 11012355;

    PROCEDURE BuildAllowedCostCodes@1100525003(YearNo@1100525002 : Integer;WeekNo@1100525003 : Integer;ReceivingCompany@1210190003 : Text[50];ProjectNo@1100525001 : Code[20]);
    VAR
      SiteMgtProjHrsHead@1210190001 : Record 11012362;
      CostCode@1210190004 : Record 11012083;
      ProjHrCostCodeSel@1210190002 : Record 11126060;
      Window@1210190000 : Dialog;
    BEGIN
      //* Build in a temporary table, so in the calling process (form) 'Rec' must be temporary.
      Window.OPEN(STRSUBSTNO(Text001, FIELDCAPTION("Cost Code")));
      RESET;
      DELETEALL;
      IF (YearNo = 0) OR (WeekNo = 0) OR (ReceivingCompany = '') OR (ProjectNo = '') THEN
        EXIT;
      IF NOT SiteMgtProjHrsHead.GET(YearNo, WeekNo, ReceivingCompany, ProjectNo) THEN
        EXIT;

      IF SiteMgtHrSetup.CheckPostingInWeekAllowed(YearNo, WeekNo, ReceivingCompany, FALSE) THEN BEGIN
        CostCode.RESET;
        CostCode.SETFILTER(Code, '<>%1', '');
        CostCode.SETRANGE(Blocked, FALSE);
        IF CostCode.FINDFIRST THEN BEGIN
          REPEAT
            Window.UPDATE(2, CostCode.Code);
            IF NOT GET(YearNo, WeekNo, ReceivingCompany, ProjectNo, CostCode.Code) THEN BEGIN
              INIT;
              Year := YearNo;
              Week := WeekNo;
              "Receiving Company" := ReceivingCompany;
              "Project No." := ProjectNo;
              "Cost Code" := CostCode.Code;
              IF ProjHrCostCodeSel.GET(Year, Week, "Receiving Company", "Project No.", "Cost Code") THEN
                Select := TRUE;
              INSERT;
            END;
          UNTIL CostCode.NEXT = 0;
        END;
      END;

      //* Insert already selected cost codes, if not present yet.
      ProjHrCostCodeSel.RESET;
      ProjHrCostCodeSel.SETRANGE(Year, YearNo);
      ProjHrCostCodeSel.SETRANGE(Week, WeekNo);
      ProjHrCostCodeSel.SETRANGE("Receiving Company", ReceivingCompany);
      ProjHrCostCodeSel.SETRANGE("Project No.", ProjectNo);
      IF ProjHrCostCodeSel.FINDSET THEN BEGIN
        REPEAT
          Window.UPDATE(2, ProjHrCostCodeSel."Cost Code");
          IF NOT GET(YearNo, WeekNo, ReceivingCompany, ProjectNo, ProjHrCostCodeSel."Cost Code") THEN BEGIN
            INIT;
            Year := YearNo;
            Week := WeekNo;
            "Receiving Company" := ReceivingCompany;
            "Project No." := ProjectNo;
            "Cost Code" := ProjHrCostCodeSel."Cost Code";
            Select := TRUE;
            INSERT;
          END;
        UNTIL ProjHrCostCodeSel.NEXT = 0;
      END;

      Window.CLOSE;
    END;

    PROCEDURE OnSelect@1210190002();
    VAR
      ProjHrCostCodeSel@1210190001 : Record 11126060;
      SiteMgtProjHrLine@1210190003 : Record 11012363;
    BEGIN
      //* Update from temporary table to 'normal' table
      IF Select THEN BEGIN
        IF NOT ProjHrCostCodeSel.GET(Year, Week, "Receiving Company", "Project No.", "Cost Code") THEN BEGIN
          ProjHrCostCodeSel := Rec;
          ProjHrCostCodeSel.INSERT;
        END ELSE BEGIN
          IF NOT ProjHrCostCodeSel.Select THEN BEGIN
            ProjHrCostCodeSel.Select := TRUE;
            ProjHrCostCodeSel.MODIFY;
          END;
        END;
      END ELSE BEGIN
        SiteMgtProjHrLine.SETRANGE(Year, Year);
        SiteMgtProjHrLine.SETRANGE(Week, Week);
        SiteMgtProjHrLine.SETRANGE("Receiving Company", "Receiving Company");
        SiteMgtProjHrLine.SETRANGE("Project No.", "Project No.");
        SiteMgtProjHrLine.SETRANGE("Cost Code", "Cost Code");
        SiteMgtProjHrLine.SETRANGE(Type, SiteMgtProjHrLine.Type::Indirect);  //* Cost Codes only for indirect
        IF NOT SiteMgtProjHrLine.ISEMPTY THEN
          ERROR(Text002, Year, Week, FIELDCAPTION("Cost Code"), "Cost Code", FIELDCAPTION("Project No."), "Project No.");
        IF ProjHrCostCodeSel.GET(Year, Week, "Receiving Company", "Project No.", "Cost Code") THEN
          ProjHrCostCodeSel.DELETE;
      END;
    END;

    BEGIN
    END.
  }
}

