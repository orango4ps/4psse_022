OBJECT Table 11125827 BIM Model IFC File
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=BIM Model IFC File;
  }
  FIELDS
  {
    { 10  ;   ;BIM Model No.       ;Code20        ;TableRelation="BIM Model";
                                                   CaptionML=[ENU=BIM Model No.;
                                                              SVE=BIM-modellnr.];
                                                   NotBlank=Yes }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;IFC File            ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("IFC File");
                                                                IfcFileExists();
                                                              END;

                                                   OnLookup=BEGIN
                                                              "IFC File" := SelectIcfFile("IFC File");
                                                            END;

                                                   CaptionML=ENU=IFC File }
    { 40  ;   ;Version             ;Text10        ;CaptionML=[ENU=Version;
                                                              NOR=Versjon;
                                                              SVE=Version] }
    { 50  ;   ;Version Date        ;Date          ;CaptionML=[ENU=Version Date;
                                                              NOR=Versjonsdato;
                                                              SVE=Versionsdatum] }
    { 60  ;   ;Status              ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("IFC File");
                                                                IF Status <> Status::Active THEN
                                                                  Load := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=ENU=Active,Expired;
                                                   OptionString=Active,Expired }
    { 70  ;   ;Load                ;Boolean       ;OnValidate=BEGIN
                                                                IF Load THEN
                                                                  TESTFIELD(Status, Status::Active);
                                                              END;

                                                   CaptionML=[ENU=Load;
                                                              NOR=Belastning;
                                                              SVE=Bel„ggning] }
    { 90  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
  }
  KEYS
  {
    {    ;BIM Model No.,Line No.                  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100529001 : TextConst 'ENU=Select IFC File';
      Text001@1100529000 : TextConst 'ENU=''%1'' not present';

    PROCEDURE SelectIcfFile@1100525000(FileName@1100525001 : Text) NewFileName : Text;
    VAR
      FileManagement@1100525000 : Codeunit 419;
    BEGIN
      NewFileName := FileManagement.OpenFileDialog(Text000, "IFC File", '(*.ifc)|*.ifc');
      IF NewFileName = '' THEN
        NewFileName := FileName;
    END;

    PROCEDURE IfcFileExists@1100525005();
    VAR
      FileMgt@1100525000 : Codeunit 419;
    BEGIN
      IF "IFC File" = '' THEN
        EXIT;
      IF NOT FileMgt.ClientFileExists("IFC File") THEN
        FIELDERROR("IFC File", STRSUBSTNO(Text001, "IFC File"));
    END;

    PROCEDURE RunViewer@1100525001(VAR BimModelIfcFileFilters@1100529000 : Record 11125827);
    VAR
      BimModel@1100525000 : Record 11125823;
      BimViewer@1100529001 : Page 11130583;
    BEGIN
      TESTFIELD("BIM Model No.");

      BimModel.FILTERGROUP(9);
      BimModel.SETRANGE("No.", "BIM Model No.");
      BimModel.FILTERGROUP(0);
      BimModel.GET("BIM Model No.");

      BimModelIfcFileFilters.SETRANGE("BIM Model No.", "BIM Model No.");
      BimModelIfcFileFilters.FINDFIRST;

      BimViewer.SETTABLEVIEW(BimModel);
      BimViewer.SETRECORD(BimModel);
      BimViewer.SetIfcFileSelection(BimModelIfcFileFilters, DATABASE::"BIM Model IFC File");
      BimViewer.RUNMODAL;
    END;

    PROCEDURE ShowDocuments@1100529400();
    VAR
      RecRef@1100525000 : RecordRef;
      DocumentFilters@1100525001 : Codeunit 11012432;
    BEGIN
      TESTFIELD("BIM Model No.");
      TESTFIELD("Line No.");
      RecRef.GETTABLE(Rec);
      DocumentFilters.CreateTempFilterAndExecute1(RecRef, '', FALSE, TRUE);
    END;

    PROCEDURE NumberOfDocumentLinks@1100525006() : Integer;
    VAR
      RecRef@1100529400 : RecordRef;
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
    BEGIN
      RecRef.GETTABLE(Rec);
      EXIT(DocumentLinkMgt.EntityNumberOfDocumentLinks(RecRef));
    END;

    BEGIN
    END.
  }
}

