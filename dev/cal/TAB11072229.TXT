OBJECT Table 11072229 Change Request Impact Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Change Request Impact Line;
    LookupPageID=Page11229223;
    DrillDownPageID=Page11229223;
  }
  FIELDS
  {
    { 20  ;   ;Change Request Code ;Code20        ;TableRelation="Change Request";
                                                   CaptionML=ENU=Change Request Code;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 40  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF (Type = Type::Group) THEN
                                                                  "Field Type" := "Field Type"::" ";
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Group,Line;
                                                                    NOR=Gruppe,Rad;
                                                                    SVE=Grupp,Rad];
                                                   OptionString=Group,Line }
    { 50  ;   ;Impact Code         ;Code20        ;TableRelation=IF (Type=CONST(Group)) "Change Mgt Impact Group"
                                                                 ELSE IF (Type=CONST(Line)) "Change Mgt Impact Line";
                                                   OnValidate=VAR
                                                                ChangeMgtImpactGroup@1100409001 : Record 11072226;
                                                                ChangeMgtImpactLine@1100409000 : Record 11072227;
                                                              BEGIN
                                                                CheckImpactGroupCode;

                                                                CASE Type OF
                                                                  Type::Group: BEGIN
                                                                    IF (ChangeMgtImpactGroup.GET("Impact Code")) THEN BEGIN
                                                                      Description := ChangeMgtImpactGroup.Description;
                                                                      "Field Type" := "Field Type"::" ";
                                                                      "Unit Of Measure" := '';
                                                                      VALIDATE(Value, '');
                                                                    END;
                                                                  END;
                                                                  Type::Line: BEGIN
                                                                    IF (ChangeMgtImpactLine.GET("Impact Code")) THEN BEGIN
                                                                      Description := ChangeMgtImpactLine.Description;
                                                                      "Field Type" := ChangeMgtImpactLine."Field Type";
                                                                      "Unit Of Measure" := ChangeMgtImpactLine."Unit Of Measure";
                                                                      VALIDATE(Value, '');
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ChangeMgtImpactGroup@1100409000 : Record 11072226;
                                                              ChangeMgtImpactLine@1100409001 : Record 11072227;
                                                            BEGIN
                                                              CASE Type OF
                                                                Type::Group: BEGIN
                                                                  IF (PAGE.RUNMODAL(0, ChangeMgtImpactGroup) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Impact Code", ChangeMgtImpactGroup.Code);
                                                                END;
                                                                Type::Line: BEGIN
                                                                  ChangeMgtImpactLine.SETFILTER("Impact Group Code", '%1|%2', '', GetImpactGroupCode);
                                                                  IF (PAGE.RUNMODAL(0, ChangeMgtImpactLine) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Impact Code", ChangeMgtImpactLine.Code);
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Impact Code }
    { 60  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 70  ;   ;Field Type          ;Option        ;OnValidate=BEGIN
                                                                IF ("Field Type" = 0) THEN
                                                                  TESTFIELD(Type, Type::Group);
                                                              END;

                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Felttype;
                                                              SVE=Field Type];
                                                   OptionCaptionML=[ENU="Date,Value,Text,Option, ";
                                                                    NOR="Dato,Verdi,Tekst,Alternativ, ";
                                                                    SVE="Datum,V„rde,Text,Alternativ, "];
                                                   OptionString=[Date,Value,Text,Option, ];
                                                   Editable=No }
    { 80  ;   ;Unit Of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit Of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet];
                                                   Editable=No }
    { 90  ;   ;Value               ;Text80        ;OnValidate=BEGIN
                                                                EvaluateValue;
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleValueLookup;
                                                            END;

                                                   CaptionML=[ENU=Value;
                                                              NOR=Verdi;
                                                              SVE=V„rde] }
    { 120 ;   ;Value (Option)      ;Code20        ;TableRelation="Change Mgt Impact Line Option";
                                                   OnValidate=BEGIN
                                                                Value := FORMAT("Value (Date)");
                                                              END;

                                                   CaptionML=ENU=Value (Option);
                                                   Editable=No }
    { 130 ;   ;Value (Date)        ;Date          ;OnValidate=BEGIN
                                                                Value := FORMAT("Value (Date)");
                                                              END;

                                                   CaptionML=[ENU=Value (Date);
                                                              NOR=Verdi (Dato);
                                                              SVE=Valuteringsdag];
                                                   Editable=No }
    { 140 ;   ;Value (Value)       ;Decimal       ;CaptionML=[ENU=Value (Value);
                                                              NOR=Verdi (Verdi);
                                                              SVE=V„rde (v„rde)];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 150 ;   ;Publish on Service Portal;Boolean  ;CaptionML=[ENU=Publish on Portal;
                                                              NOR=Publiser p† Serviceportal;
                                                              SVE=Publicera p† serviceportalen] }
  }
  KEYS
  {
    {    ;Change Request Code,Line No.            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100409001 : TextConst 'ENU=Lines with different %1 present.';
      Text001@1100528601 : TextConst 'ENU=Option Value %1 not found;NOR=Alternativverdi %1 ikke funnet;SVE=Alternativv„rde %1 hittades inte';
      Text002@1100528600 : TextConst 'ENU=t;NOR=t;SVE=t';
      Text003@1210190000 : TextConst 'ENU=not found.';
      Text004@1100409000 : TextConst 'ENU=No %1 line found.';

    PROCEDURE CheckImpactGroupCode@1100409000();
    VAR
      ChangeRequestImpactLine@1100409000 : Record 11072229;
      ChangeMgtImpactLine@1100409001 : Record 11072227;
      NextGroupLine@1100409002 : Integer;
    BEGIN
      CASE Type OF
        Type::Group: BEGIN
          NextGroupLine := 9999999;
          ChangeRequestImpactLine.SETRANGE("Change Request Code", "Change Request Code");
          ChangeRequestImpactLine.SETFILTER("Line No.", '>%1', "Line No.");
          ChangeRequestImpactLine.SETRANGE(Type, Type::Group);
          IF (ChangeRequestImpactLine.FINDLAST) THEN
            NextGroupLine := ChangeRequestImpactLine."Line No.";

          ChangeRequestImpactLine.SETFILTER("Line No.", '%1..%2', "Line No."+1, NextGroupLine-1);
          ChangeRequestImpactLine.SETRANGE(Type, Type::Line);
          ChangeRequestImpactLine.SETFILTER("Impact Code", '<>%1', "Impact Code");
          IF (ChangeRequestImpactLine.FINDFIRST) THEN
            ERROR(Text000, FIELDCAPTION("Impact Code"));
        END;
        Type::Line: BEGIN
          ChangeRequestImpactLine.SETRANGE("Change Request Code", "Change Request Code");
          ChangeRequestImpactLine.SETFILTER("Line No.", '<%1', "Line No.");
          ChangeRequestImpactLine.SETRANGE(Type, Type::Group);
          IF (NOT ChangeRequestImpactLine.FINDLAST) THEN
            ERROR(Text004, Type::Group);

          ChangeMgtImpactLine.GET("Impact Code");
          IF (ChangeMgtImpactLine."Impact Group Code" <> '') AND (ChangeMgtImpactLine."Impact Group Code" <> ChangeRequestImpactLine."Impact Code") THEN
            FIELDERROR("Impact Code", Text003);
        END;
      END;
    END;

    PROCEDURE GetImpactGroupCode@1100528601() : Code[10];
    VAR
      ChangeRequestImpactLine@1100528600 : Record 11072229;
      ChangeMgtImpactGroup@1100528900 : Record 11072226;
    BEGIN
      ChangeRequestImpactLine.SETRANGE("Change Request Code", "Change Request Code");
      ChangeRequestImpactLine.SETFILTER("Line No.", '<%1', "Line No.");
      ChangeRequestImpactLine.SETRANGE(Type, Type::Group);
      IF (ChangeRequestImpactLine.FINDLAST) THEN
        EXIT(ChangeRequestImpactLine."Impact Code");

      IF (ChangeMgtImpactGroup.GET("Impact Code")) THEN
        EXIT(ChangeMgtImpactGroup.Code);
    END;

    PROCEDURE EvaluateValue@1100528603();
    VAR
      ChangeMgtImpactLineOption@1100409000 : Record 11072228;
      TmpDate@1100528600 : Date;
      TmpDecimal@1100528601 : Decimal;
    BEGIN
      "Value (Date)" := 0D;
      "Value (Value)" := 0;
      "Value (Option)" := '';
      IF (Value = '') THEN
        EXIT;

      CASE "Field Type" OF
        "Field Type"::Date: BEGIN
          IF LOWERCASE(Value) = Text002 THEN BEGIN
            Value := FORMAT(TODAY);
            "Value (Date)" := TODAY;
          END ELSE BEGIN
            EVALUATE(TmpDate, Value);
            Value := FORMAT(TmpDate);
            "Value (Date)" := TmpDate;
          END;
        END;
        "Field Type"::Value: BEGIN
            EVALUATE(TmpDecimal, Value);
            Value := FORMAT(TmpDecimal);
            "Value (Value)" := TmpDecimal;
        END;
        "Field Type"::Option: BEGIN
          Value := CheckOptionValue;
          ChangeMgtImpactLineOption.SETRANGE("Impact Code", "Impact Code");
          ChangeMgtImpactLineOption.SETRANGE(Description, Value);
          IF (ChangeMgtImpactLineOption.FINDFIRST) THEN
            "Value (Option)" := ChangeMgtImpactLineOption.Code;
        END;
      END;
    END;

    PROCEDURE CheckOptionValue@1100528604() : Text[80];
    VAR
      ChangeMgtImpactLineOption@1100528600 : Record 11072228;
    BEGIN
      ChangeMgtImpactLineOption.SETRANGE("Impact Code", "Impact Code");
      ChangeMgtImpactLineOption.SETRANGE(Description, Value);
      IF (ChangeMgtImpactLineOption.FINDFIRST) THEN
        EXIT(ChangeMgtImpactLineOption.Description);
      ChangeMgtImpactLineOption.SETFILTER(Description, '@' + Value + '*');
      IF (ChangeMgtImpactLineOption.COUNT = 1) THEN BEGIN
        ChangeMgtImpactLineOption.FINDFIRST;
        EXIT(ChangeMgtImpactLineOption.Description);
      END;
      ERROR(Text001, Value);
    END;

    PROCEDURE HandleValueLookup@1100528605();
    VAR
      ChangeMgtImpactLineOption@1100528600 : Record 11072228;
    BEGIN
      IF ("Field Type" = "Field Type"::Option) THEN BEGIN
        ChangeMgtImpactLineOption.SETRANGE("Impact Code", "Impact Code");
        IF (PAGE.RUNMODAL(0, ChangeMgtImpactLineOption) = ACTION::LookupOK) THEN
          VALIDATE(Value, ChangeMgtImpactLineOption.Description);
      END;
    END;

    BEGIN
    END.
  }
}

