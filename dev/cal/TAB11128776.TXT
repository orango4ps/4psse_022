OBJECT Table 11128776 NEN2767 Cond. Meas. Header OBS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Service Object No.");
               TESTFIELD("Measurement Date");
             END;

    OnDelete=VAR
               NEN2767ConditionMeasLine@1100528600 : Record 11128777;
               LTMaintPlanTask@1100528601 : Record 11128781;
             BEGIN
               NEN2767ConditionMeasLine.SETRANGE("Service Object No.", "Service Object No.");
               NEN2767ConditionMeasLine.SETRANGE("Measurement Date", "Measurement Date");
               NEN2767ConditionMeasLine.DELETEALL(TRUE);

               LTMaintPlanTask.SETRANGE("Service Object No.", "Service Object No.");
               LTMaintPlanTask.SETRANGE("Condition Measurement Date", "Measurement Date");
               LTMaintPlanTask.MODIFYALL("Condition Measurement Date", 0D, TRUE);
             END;

    CaptionML=ENU=NEN2767 Condition Measurement OBSOLETE;
    LookupPageID=Page11128780;
    DrillDownPageID=Page11128780;
  }
  FIELDS
  {
    { 10  ;   ;Service Object No.  ;Code20        ;TableRelation=Service-Object;
                                                   OnValidate=VAR
                                                                ServiceObject@1100528600 : Record 11071691;
                                                              BEGIN
                                                                IF ServiceObject.GET("Service Object No.") THEN
                                                                  "Condition Measurement Method" := ServiceObject."Condition Meas. Method OLD";
                                                              END;

                                                   CaptionML=[ENU=Object No.;
                                                              NOR=Objektnr.;
                                                              SVE=Objektnr];
                                                   Editable=No }
    { 20  ;   ;Measurement Date    ;Date          ;OnValidate=VAR
                                                                NEN2767ConditionMeasLine@1100528600 : Record 11128777;
                                                                NEN2767ConditionMeasLine2@1100528601 : Record 11128777;
                                                              BEGIN
                                                                IF "Measurement Date" <> xRec."Measurement Date" THEN BEGIN
                                                                  NEN2767ConditionMeasLine.SETRANGE("Service Object No.", "Service Object No.");
                                                                  NEN2767ConditionMeasLine.SETRANGE("Measurement Date", xRec."Measurement Date");
                                                                  IF NEN2767ConditionMeasLine.FINDSET THEN
                                                                    REPEAT
                                                                      NEN2767ConditionMeasLine2 := NEN2767ConditionMeasLine;
                                                                      NEN2767ConditionMeasLine2."Measurement Date" := "Measurement Date";
                                                                      NEN2767ConditionMeasLine2.INSERT(TRUE);
                                                                      NEN2767ConditionMeasLine.DELETE(TRUE);
                                                                    UNTIL NEN2767ConditionMeasLine.NEXT = 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Measurement Date;
                                                              NOR=M†lingsdato] }
    { 30  ;   ;Condition Score     ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Max("NEN2767 Cond. Meas. Line OBS".Score WHERE (Service Object No.=FIELD(Service Object No.),
                                                                                                               Measurement Date=FIELD(Measurement Date)));
                                                   CaptionML=[ENU=Condition Score;
                                                              NOR=Poengsum for tilstand];
                                                   OptionCaptionML=[ENU=,1,2,3,4,5,6;
                                                                    NOR=,1,2,3,4,5,6;
                                                                    SVE=,1,2,3,4,5,6];
                                                   OptionString=,1,2,3,4,5,6;
                                                   Editable=No }
    { 35  ;   ;Desired Condition Score;Option     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Desired Condition Score" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Desired Condition Score;
                                                              NOR=nsket tilstandspoengsum;
                                                              SVE=™nskat tillst†ndspo„ng];
                                                   OptionCaptionML=[ENU=,1,2,3,4,5,6;
                                                                    NOR=,1,2,3,4,5,6;
                                                                    SVE=,1,2,3,4,5,6];
                                                   OptionString=,1,2,3,4,5,6;
                                                   Editable=No }
    { 40  ;   ;No Defects          ;Boolean       ;OnValidate=VAR
                                                                NEN2767ConditionMeasLine@1100528600 : Record 11128777;
                                                              BEGIN
                                                                NEN2767ConditionMeasLine.SETRANGE("Service Object No.", "Service Object No.");
                                                                NEN2767ConditionMeasLine.SETRANGE("Measurement Date", "Measurement Date");
                                                                IF NOT NEN2767ConditionMeasLine.ISEMPTY THEN
                                                                  ERROR(Text000);
                                                              END;

                                                   CaptionML=[ENU=No Defects;
                                                              NOR=Ingen defekter] }
    { 50  ;   ;Condition Measurement Method;Option;CaptionML=[ENU=Condition Mesurement Method;
                                                              NOR=Tilstand Í m†lingsmetode];
                                                   OptionCaptionML=[ENU=Scale,Repair Cost;
                                                                    NOR=Skala,Reparasjonskost;
                                                                    SVE=Skala,Reperationskostnad];
                                                   OptionString=Scale,Repair Cost;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Object No.,Measurement Date     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=Condition Measurement Lines present;NOR=Tilstandsm†lingsrader finnes';

    PROCEDURE NewerVersionExists@1100528600() : Boolean;
    VAR
      NEN2767ConditionMeasHeader@1100528600 : Record 11128776;
    BEGIN
      NEN2767ConditionMeasHeader := Rec;
      NEN2767ConditionMeasHeader.SETRECFILTER;
      NEN2767ConditionMeasHeader.SETFILTER("Measurement Date", '>%1', "Measurement Date");
      EXIT(NOT NEN2767ConditionMeasHeader.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

