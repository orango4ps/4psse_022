OBJECT XMLport 11012112 Imp. Fixed Assets
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Imp. Fixed Assets;
               SVE=Imp. anl„ggningstillg†ngar];
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    XmlPortID := 11012112;
                    MainTableID := 5600;
                    MainTableLookupPageID := 5601;
                    XMLPortRequestPageID := 11127324;
                  END;

    OnPreXMLport=VAR
                   ConversionXMLPort4PS@1100527700 : Record 11020640;
                 BEGIN
                   IF NOT currXMLport.IMPORTFILE THEN BEGIN
                     AddConversionFileFormatLine;
                     ConversionXMLPort4PS.UpdateFromXMLPort(XmlPortID,MainTableID,MainTableLookupPageID,XMLPortRequestPageID,TRUE,ConversionXMLPort."Journal Template Type"::"Fixed Assets");
                     COMMIT;
                     ERROR('');
                   END;

                   ConversionXMLPort.GET(XmlPortID);

                   FASetupRec.GET;
                   FASetupRec.TESTFIELD("Default Depr. Book");

                   ConvMgt.CheckComCde(CreateNewNosBln,MainTableID,CommentCde);

                   ConvMgt.CheckTemplateFilled(80,ConversionXMLPort."Journal Template Name");
                   ConvMgt.CheckBatchFilled(232,ConversionXMLPort."Journal Batch Name");

                   IF NOT PostingDateFromImportFile THEN
                     ConvMgt.CheckBalAccountFilled(AcquisitionBalAccNo);
                     ConvMgt.CheckBalAccountFilled(DepreciationBalAccNo);

                   ConvMgt.DeleteConComment(XmlPortID);
                   ConvMgt.CreateConComment(XmlPortID,STRSUBSTNO('Import File: %1.',currXMLport.FILENAME));
                   ConvMgt.CreateConComment(XmlPortID,'Start');
                 END;

    OnPostXMLport=BEGIN
                    ConvMgt.CreateConComment(XmlPortID,'Stop');
                    ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateNoOfLinesCommentTxt(MainTableID,ImpLineCounter));
                  END;

    Format=Variable Text;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{7B539476-C20A-46BC-A552-62FED55766AD}];  ;Root                ;Element ;Text     }

    { [{A667E51D-CE85-4A0D-946E-DE2CCC52ED51}];1 ;FixedAsset          ;Element ;Table   ;
                                                  SourceTable=Table5600;
                                                  SourceTableView=SORTING(Field1);
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 lvFADeprBookRec@1100525001 : Record 5612;
                                                                                 lvGenJnlLineRec@1100525000 : Record 81;
                                                                                 PostingDate@1100525002 : Date;
                                                                               BEGIN
                                                                                 IF A = '' THEN
                                                                                   currXMLport.SKIP;

                                                                                 ImpLineCounter := ImpLineCounter + 1;
                                                                                 CLEAR("Fixed Asset");

                                                                                 WITH "Fixed Asset" DO BEGIN
                                                                                   IF PostingDateFromImportFile AND (KDate <> 0D) THEN
                                                                                     PostingDate := KDate
                                                                                   ELSE
                                                                                     PostingDate := ConversionXMLPort."Posting Date";

                                                                                   IF CreateNewNosBln THEN
                                                                                     "No." := ''
                                                                                   ELSE
                                                                                     "No." := A;

                                                                                   SetSkipCreateDefFADeprBooks(TRUE);  //* Must not be done in OnInsert, is done below
                                                                                   INSERT(TRUE);
                                                                                   IF CreateNewNosBln THEN
                                                                                     ConvMgt.FillComment(CommentRec."Table Name"::"Fixed Asset","No.",CommentCde,A);

                                                                                   ImpLineCounter := ImpLineCounter + 1;

                                                                                   VALIDATE(Description,B);
                                                                                   VALIDATE("Global Dimension 1 Code",J);
                                                                                   VALIDATE("FA Class Code",H);
                                                                                   VALIDATE("FA Subclass Code",I);
                                                                                   //VALIDATE("FA Posting Group",G);
                                                                                   MODIFY(TRUE);

                                                                                   lvFADeprBookRec.VALIDATE("FA No.","Fixed Asset"."No.");
                                                                                   lvFADeprBookRec.VALIDATE("Depreciation Book Code",FASetupRec."Default Depr. Book");
                                                                                   lvFADeprBookRec.VALIDATE("FA Posting Group",G);
                                                                                   lvFADeprBookRec.VALIDATE("Depreciation Starting Date",CDate);
                                                                                   lvFADeprBookRec.VALIDATE("No. of Depreciation Years",DInt);
                                                                                   lvFADeprBookRec.INSERT(TRUE);

                                                                                   WITH lvGenJnlLineRec DO BEGIN

                                                                                     // Add Acquisition costs
                                                                                     ConvMgt.GenerateGenJnlLine(
                                                                                       ConversionXMLPort."Journal Template Name",
                                                                                       ConversionXMLPort."Journal Batch Name",
                                                                                       PostingDate,
                                                                                       Text001,
                                                                                       AcquisitionBalAccNo,
                                                                                       lvGenJnlLineRec);
                                                                                     VALIDATE("FA Posting Date",lvFADeprBookRec."Depreciation Starting Date");
                                                                                     VALIDATE("Account Type","Account Type"::"Fixed Asset");
                                                                                     VALIDATE("Account No.","No.");
                                                                                     VALIDATE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
                                                                                     VALIDATE("Bal. Account No.",AcquisitionBalAccNo);
                                                                                     VALIDATE(Amount,EDec);
                                                                                     MODIFY(TRUE);

                                                                                     // Add Depreciation costs
                                                                                     CLEAR(lvGenJnlLineRec);
                                                                                     ConvMgt.GenerateGenJnlLine(
                                                                                       ConversionXMLPort."Journal Template Name",
                                                                                       ConversionXMLPort."Journal Batch Name",
                                                                                       PostingDate,
                                                                                       Text001,
                                                                                       DepreciationBalAccNo,
                                                                                       lvGenJnlLineRec);
                                                                                     VALIDATE("FA Posting Date",PostingDate);
                                                                                     VALIDATE("Account Type","Account Type"::"Fixed Asset");
                                                                                     VALIDATE("Account No.","No.");
                                                                                     VALIDATE("FA Posting Type","FA Posting Type"::Depreciation);
                                                                                     VALIDATE(Amount,FDec*-1);
                                                                                     MODIFY(TRUE);
                                                                                   END;
                                                                                 END;

                                                                                 ClearFields;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{3AC77EC7-9326-4B76-BA5F-EA1F70D4DA67}];2 ;A                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=20 }

    { [{1B889F9B-B9E2-4749-96B0-D22BA1117049}];2 ;B                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{D76B46B3-243B-4FF7-8D1D-C7B64CCFA492}];2 ;C                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF C <> '' THEN
                                                                                    EVALUATE(CDate,C);
                                                                                END;

                                                  Width=50 }

    { [{11AAC24C-6612-4FE6-9973-FE5EF2EF2C43}];2 ;D                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF D <> '' THEN
                                                                                    EVALUATE(DInt,D);
                                                                                END;

                                                  Width=50 }

    { [{F0633544-507F-4C5E-A35B-A0DD95F11E63}];2 ;E                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF E <> '' THEN
                                                                                    EVALUATE(EDec,E);
                                                                                END;

                                                  Width=50 }

    { [{98E7308F-19D5-4AF6-89FB-48DB78E52124}];2 ;F                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF F <> '' THEN
                                                                                    EVALUATE(FDec,F);
                                                                                END;

                                                  Width=50 }

    { [{23B2F6EB-78F9-48FB-BC70-858F6A53A74C}];2 ;G                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{007CC416-7129-43EA-90B0-193FFE5FCA68}];2 ;H                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{8F9BF873-E7E2-450A-B9C5-33703DAFE6BE}];2 ;I                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{F0890EEC-4257-4AFD-B88A-7442730D3214}];2 ;J                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{659442CF-3753-4298-9149-BE36CD412787}];2 ;K                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF K <> '' THEN
                                                                                    EVALUATE(KDate,K);
                                                                                END;

                                                  Width=50 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   RunIsImportRunBln := TRUE;
                   CreateNewNosBln := TRUE;
                 END;

    }
    CONTROLS
    {
      { 1100409000;;Container;
                  Name=Imp. Fixed Assets;
                  ContainerType=ContentArea }

      { 1100409001;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525000;2;Field  ;
                  CaptionML=[ENU=Create new Fixed Asset Nos;
                             SVE=Skapa nya anl„ggningstillg†ngsnr];
                  SourceExpr=CreateNewNosBln;
                  OnValidate=BEGIN
                               IF NOT CreateNewNosBln THEN
                                 CommentCde := '';
                             END;
                              }

      { 1100525001;2;Field  ;
                  Name=ComCde;
                  CaptionML=[ENU=Comment Code for storing old Fixed Asset Nos.;
                             SVE=Kommentarskod f”r lagring av gamla fasta tillg†ngsnr];
                  SourceExpr=CommentCde;
                  TableRelation="Comment Code" }

      { 1100525004;2;Field  ;
                  CaptionML=[ENU=Balance account Aquisition Costs;
                             SVE=Balanskonto anskaffningskostnader];
                  SourceExpr=AcquisitionBalAccNo;
                  TableRelation="G/L Account" }

      { 1100525005;2;Field  ;
                  CaptionML=[ENU=Balance account Depreciation Costs;
                             SVE=Balanskonto avskrivningskostnader];
                  SourceExpr=DepreciationBalAccNo;
                  TableRelation="G/L Account" }

      { 1100525006;2;Field  ;
                  Name=DateFromImport;
                  CaptionML=[ENU=Posting Date from Import File;
                             SVE=Bokf”ringsdatum fr†n importfil];
                  SourceExpr=PostingDateFromImportFile }

    }
  }
  CODE
  {
    VAR
      CommentRec@1100525033 : Record 97;
      ConvMgt@1100525032 : Codeunit 11020212;
      XmlPortID@1100525028 : Integer;
      MainTableID@1100525027 : Integer;
      MainTableLookupPageID@1100527701 : Integer;
      XMLPortRequestPageID@1100527700 : Integer;
      CreateNewNosBln@1100525026 : Boolean;
      ImpLineCounter@1100525025 : Integer;
      CommentCde@1100525024 : Code[10];
      ConversionFileFormat@1100525023 : Record 11020691;
      RunIsImportRunBln@1100525022 : Boolean;
      ConversionXMLPort@1100525002 : Record 11020640;
      PostingDateFromImportFile@1100525001 : Boolean;
      AcquisitionBalAccNo@1100525018 : Code[20];
      DepreciationBalAccNo@1100525017 : Code[20];
      FASetupRec@1100525011 : Record 5603;
      CDate@1100525008 : Date;
      DInt@1100525007 : Integer;
      EDec@1100525006 : Decimal;
      FDec@1100525005 : Decimal;
      KDate@1100525000 : Date;
      Text001@1100525036 : TextConst 'ENU=OPENING;SVE=™PPNAR';
      Text002@1100525035 : TextConst 'ENU=Acquisition Cost;SVE=Redov.integration-anskaff.kost';
      Text003@1100525030 : TextConst 'ENU=Cumulative Depreciation;SVE=Ackumulerad avskrivning';

    PROCEDURE AddConversionFileFormatLine@1100529901();
    VAR
      FADepriationBook@1100529900 : Record 5612;
      GenJnlLine@1100529901 : Record 81;
    BEGIN
      // This function stores the file format into table [Conversion File Format].
      // There is no other standard documentation; so please maintain this function in case of customization.
      //
      // Parameters: 1: [Dataport No.],
      //             2: [csv-Column],
      //             3: [NAV Table ID],
      //             4: [NAV Field Name],
      //             5: [Data Type of dataport field],
      //             6: [extra Comment].
      //
      WITH ConversionFileFormat DO BEGIN
        ConversionFileFormat.DeleteColumnLines(XmlPortID);
        InsertColumnLine(XmlPortID,'A',MainTableID,"Fixed Asset".FIELDNAME("No."),'Code20','OldOrNewTxt');
        InsertColumnLine(XmlPortID,'B',MainTableID,"Fixed Asset".FIELDNAME(Description),'Text30','');
        InsertColumnLine(XmlPortID,'C',5612,FADepriationBook.FIELDNAME("Depreciation Starting Date"),'Date','');
        InsertColumnLine(XmlPortID,'D',5612,FADepriationBook.FIELDNAME("No. of Depreciation Years"),'Decimal','');
        InsertColumnLine(XmlPortID,'E',81,GenJnlLine.FIELDNAME(Amount),'Decimal',Text002);
        InsertColumnLine(XmlPortID,'F',81,GenJnlLine.FIELDNAME(Amount),'Decimal',Text003);
        InsertColumnLine(XmlPortID,'G',MainTableID,"Fixed Asset".FIELDNAME("FA Posting Group"),'Code10','');
        InsertColumnLine(XmlPortID,'H',MainTableID,"Fixed Asset".FIELDNAME("FA Class Code"),'Code10','');
        InsertColumnLine(XmlPortID,'I',MainTableID,"Fixed Asset".FIELDNAME("FA Subclass Code"),'Code10','');
        InsertColumnLine(XmlPortID,'J',MainTableID,"Fixed Asset".FIELDNAME("Global Dimension 1 Code"),'Code20','');
        InsertColumnLine(XmlPortID,'K',81,GenJnlLine.FIELDNAME("Posting Date"),'Date','');
      END;
    END;

    PROCEDURE ClearFields@1100529902();
    BEGIN
      CLEAR("Fixed Asset");
      A := '';
      B := '';
      C := '';
      D := '';
      E := '';
      F := '';
      G := '';
      H := '';
      I := '';
      J := '';
      K := '';
      CDate := 0D;
      DInt := 0;
      EDec := 0;
      FDec := 0;
      KDate := 0D;
    END;

    PROCEDURE InitXMLPort@1100529500(iCreateNewNosBln@1100529504 : Boolean;iCommentCde@1100529503 : Code[10];iPostingDateFromImportFile@1100529502 : Boolean;iAcquisitionBalAccNo@1100529501 : Code[20];iDepreciationBalAccNo@1100529500 : Code[20]);
    BEGIN
      CreateNewNosBln := iCreateNewNosBln;
      CommentCde := iCommentCde;
      PostingDateFromImportFile := iPostingDateFromImportFile;
      AcquisitionBalAccNo := iAcquisitionBalAccNo;
      DepreciationBalAccNo := iDepreciationBalAccNo;
    END;

    BEGIN
    END.
  }
}

