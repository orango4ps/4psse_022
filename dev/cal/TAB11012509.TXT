OBJECT Table 11012509 Option Main Group
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Plot@1100528201 : Record 11012500;
               OptionMainGroup@1100528200 : Record 11012509;
             BEGIN
               IF ("Project No." <> '' ) AND ("Plot No." = '') THEN BEGIN
                 Plot.SETRANGE("Project No.", "Project No.");
                 IF Plot.FINDSET THEN
                   REPEAT
                     OptionMainGroup := Rec;
                     OptionMainGroup."Plot No." := Plot."Plot No.";
                     IF OptionMainGroup.INSERT THEN;
                   UNTIL Plot.NEXT = 0;
               END;
             END;

    OnModify=VAR
               OptionMainGroup@1100528200 : Record 11012509;
             BEGIN
               IF ("Project No." <> '' ) AND ("Plot No." = '') THEN BEGIN
                 IF "Closing Date" <> xRec."Closing Date" THEN BEGIN
                   OptionMainGroup.SETRANGE("Project No.", "Project No.");
                   OptionMainGroup.SETFILTER("Plot No.", '<>%1', '');
                   OptionMainGroup.SETRANGE(Code, Code);
                   OptionMainGroup.SETRANGE("Closing Date", xRec."Closing Date");
                   IF OptionMainGroup.FINDFIRST THEN BEGIN
                     IF CONFIRM(
                       Text003, TRUE,
                       FIELDCAPTION("Closing Date"), FIELDCAPTION("Plot No."),
                       FIELDCAPTION("Closing Date"), xRec."Closing Date") THEN
                       OptionMainGroup.MODIFYALL("Closing Date", "Closing Date");
                   END;
                 END;
                 IF Description <> xRec.Description THEN BEGIN
                   OptionMainGroup.RESET;
                   OptionMainGroup.SETRANGE("Project No.", "Project No.");
                   OptionMainGroup.SETFILTER("Plot No.", '<>%1', '');
                   OptionMainGroup.SETRANGE(Code, Code);
                   IF OptionMainGroup.FINDFIRST THEN
                     OptionMainGroup.MODIFYALL(Description, Description);
                 END;
               END;
             END;

    OnDelete=VAR
               lvBuyerMgtTextRec@1210190000 : Record 11012511;
               OptionMainGroup@1100528200 : Record 11012509;
               Option@1100528201 : Record 11012502;
             BEGIN
               IF ("Project No." <> '' )  THEN BEGIN
                 Option.SETRANGE("Main Group", Code);
                 Option.SETRANGE("Project No.", "Project No.");
                 IF "Plot No." <> '' THEN
                   Option.SETRANGE("Plot No.","Plot No.");

                 IF Option.FINDFIRST THEN
                   IF Option."Plot No." <> '' THEN
                     ERROR(
                       Text001, TABLECAPTION, Option.TABLECAPTION,
                       Option.FIELDCAPTION("Project No."), Option."Project No.",
                       Option.FIELDCAPTION("Plot No."), Option."Plot No.",
                       Option.FIELDCAPTION("Main Group"), Option."Main Group")
                   ELSE
                         ERROR(
                     Text002, TABLECAPTION, Option.TABLECAPTION,
                     Option.FIELDCAPTION("Project No."), Option."Project No.",
                     Option.FIELDCAPTION("Main Group"), Option."Main Group");

                 IF "Plot No." = '' THEN BEGIN
                   OptionMainGroup.SETRANGE("Project No.");
                   OptionMainGroup.SETFILTER("Plot No.", '<>%1', '');
                   OptionMainGroup.SETRANGE(Code, Code);
                   OptionMainGroup.DELETEALL(TRUE);
                 END;

               END;

               lvBuyerMgtTextRec.SETRANGE(Table, lvBuyerMgtTextRec.Table::"Option Main Group");
               lvBuyerMgtTextRec.SETRANGE("Project No.", "Project No.");
               lvBuyerMgtTextRec.SETRANGE("Plot No.","Plot No.");
               lvBuyerMgtTextRec.SETRANGE("Option Main Group", Code);
               lvBuyerMgtTextRec.DELETEALL;
             END;

    CaptionML=[ENU=Option Main Group;
               NOR=Alternativhovedgruppe;
               SVE=Alternativhuvudgrupp];
    LookupPageID=Page11012543;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 20  ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                OptionMainGroup@1100528200 : Record 11012509;
                                                              BEGIN
                                                                IF ("Project No." <> '') AND (Description = '') THEN BEGIN
                                                                  IF "Plot No." <>'' THEN BEGIN
                                                                    IF OptionMainGroup.GET("Project No.",'',Code) THEN
                                                                      Description := OptionMainGroup.Description;
                                                                  END;
                                                                  IF (Description = '') AND OptionMainGroup.GET('','',Code) THEN
                                                                    Description := OptionMainGroup.Description;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Closing Date        ;Date          ;OnValidate=VAR
                                                                Option@1210190000 : Record 11012502;
                                                              BEGIN
                                                                IF ("Closing Date" <> xRec."Closing Date") AND ("Project No." <> '' ) THEN BEGIN
                                                                  Option.SETRANGE("Main Group", Code);
                                                                  Option.SETRANGE("Project No.", "Project No.");
                                                                  IF "Plot No." <> '' THEN
                                                                    Option.SETRANGE("Plot No.","Plot No.");
                                                                  IF (NOT Option.ISEMPTY) THEN
                                                                    MESSAGE(Text000, TABLECAPTION, FIELDCAPTION("Closing Date"), Option.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Closing Date;
                                                              NOR=Sluttdato;
                                                              SVE=Avslutsdatum] }
    { 50  ;   ;Plot No.            ;Code10        ;TableRelation=Plot."Plot No." WHERE (Project No.=FIELD(Project No.));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Plot No.;
                                                              SVE=Ritningnr] }
    { 70  ;   ;Block No.           ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Plot."Block No." WHERE (Project No.=FIELD(Project No.),
                                                                                              Plot No.=FIELD(Plot No.)));
                                                   CaptionML=[ENU=Block No.;
                                                              SVE=Sp„rrnr];
                                                   Editable=No }
    { 80  ;   ;Floor No.           ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Plot."Floor No." WHERE (Project No.=FIELD(Project No.),
                                                                                              Plot No.=FIELD(Plot No.)));
                                                   CaptionML=[ENU=Floor No.;
                                                              SVE=V†ning nr];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.,Plot No.,Code               ;Clustered=Yes }
    {    ;Description                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description                         }
  }
  CODE
  {
    VAR
      Text000@1100525001 : TextConst 'ENU=This %1 is already used, changing the %2 will not effect the %3 lines.;NOR=Dette %1 er allerede brukt, † endre %2 p†virker ikke %3-radene.;SVE=Detta %1 har redan anv„nts, att „ndra %2 p†verkar inte %3-raderna.';
      Text001@1100528200 : TextConst 'ENU=The %1 is in use for %2 -  %3 %4, %5 %6, %7 %8.';
      Text002@1100528201 : TextConst 'ENU=The %1 is in use for %2 -  %3 %4, %5 %6.';
      Text003@1100528202 : TextConst 'ENU="Modify the %1 per %2  with %3 %4 too? "';

    BEGIN
    END.
  }
}

