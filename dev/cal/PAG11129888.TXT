OBJECT Page 11129888 Copy External Documents
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Externe Belege kopieren;
               ENU=Copy External Documents;
               NLD=Kopieer externe documenten];
    OnInit=BEGIN
             SourceDocEnabled := FALSE;
             TargetDocEnabled := FALSE;
             CopyEnabled := FALSE;
           END;

    ActionList=ACTIONS
    {
      { 1100529709;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100529706;1 ;Action    ;
                      Name=Copy;
                      CaptionML=[DEU=Kopieren;
                                 ENU=Copy;
                                 NLD=Kopieer;
                                 NOR=KOPIER;
                                 SVE=Kopiera];
                      Promoted=Yes;
                      Enabled=CopyEnabled;
                      Image=Copy;
                      OnAction=BEGIN
                                 CopyFunction;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100529700;;Container;
                ContainerType=ContentArea }

    { 1100529719;1;Group  ;
                Name=MoveOrCopy;
                CaptionML=[DEU=Kopieren oder Verschieben;
                           ENU=Copy or Move;
                           NLD=Kopieer of verplaats];
                GroupType=Group }

    { 1100529718;2;Field  ;
                Name=MoveDoc;
                CaptionML=[DEU=Verschieben Belege;
                           ENU=Move Documents;
                           NLD=Verplaats documenten;
                           NOR=Flytt dokument;
                           SVE=Flytta dokument];
                SourceExpr=MoveDoc }

    { 1100529701;1;Group  ;
                Name=CopyFromAndCopyTo;
                CaptionML=[DEU=Kopieren von und Kopieren zu;
                           ENU=Copy From and Copy To;
                           NLD=Kopieer van en kopieer naar];
                GroupType=GridLayout }

    { 1100529708;2;Group  ;
                Name=Copy From;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy From;
                           NLD=Kopieer van;
                           NOR=Kopier fra;
                           SVE=Kopiera frÜn];
                GroupType=Group }

    { 1100529703;3;Field  ;
                Name=LinkName;
                Lookup=Yes;
                CaptionML=[DEU=VerknÅpfungsname;
                           ENU=Link Name;
                           NLD=Koppelingnaam;
                           NOR=Lenkenavn;
                           SVE=LÑnknamn];
                SourceExpr=PossibleDocumentLink.GetLinkName(tmpSourceDocumentLink."Table No.",tmpSourceDocumentLink."Filter Expression");
                Editable=FALSE;
                OnLookup=VAR
                           PossibleDocumentLink@1199198001 : Record 11012748;
                         BEGIN
                           IF PossibleDocumentLink.SelectPossibleDocLinkFromList(PossibleDocumentLink, PossibleDocumentLink) THEN BEGIN
                             tmpSourceDocumentLink.VALIDATE("Table No.", PossibleDocumentLink."Table No.");
                             tmpSourceDocumentLink."Filter Expression" := PossibleDocumentLink."Filter Expression";
                           END;

                           SourceDocEnabled := FALSE;
                           CopyEnabled := FALSE;
                           SourceNumberOfDocuments := 0;

                           CurrPage.UPDATE;
                         END;
                          }

    { 1100529705;3;Field  ;
                Name=LinkWith;
                Lookup=Yes;
                CaptionML=[DEU=VerknÅpfung mit;
                           ENU=Link with;
                           NLD=Koppeling met;
                           NOR=Linke med;
                           SVE=LÑnka med];
                SourceExpr=DocumentLinkManagement.GetLongDescription(tmpSourceDocumentLink);
                Editable=FALSE;
                RowSpan=2;
                OnLookup=VAR
                           RID@1100529701 : RecordID;
                         BEGIN
                           DocumentLinkManagement.SetTable(tmpSourceDocumentLink, TRUE);

                           SourceNumberOfDocuments := 0;
                           SourceDocEnabled := FALSE;
                           CopyEnabled := FALSE;

                           CLEAR(SourceLinkRecRef);
                           SourceLinkRecRef.OPEN(tmpSourceDocumentLink."Table No.");

                           IF FORMAT(tmpSourceDocumentLink."Record ID") <> '' THEN BEGIN
                             RID := tmpSourceDocumentLink."Record ID";
                             SourceDocEnabled := SourceLinkRecRef.GET(RID);
                             IF SourceDocEnabled THEN BEGIN
                               CurrPage.DocumentList.PAGE.SetFiltersWhitDocumentLinks(SourceLinkRecRef);
                               SourceNumberOfDocuments := DocumentLinkManagement.EntityNumberOfDocumentLinks(SourceLinkRecRef);
                               CopyEnabled := SourceDocEnabled AND TargetDocEnabled;
                             END
                           END;

                           CurrPage.UPDATE
                         END;
                          }

    { 1100529712;3;Field  ;
                Name=TableNo;
                CaptionML=[DEU=Tabellennr.;
                           ENU=Table No.;
                           NLD=Tabelnr.;
                           NOR=Tabellnr.;
                           SVE=Tabellnr];
                SourceExpr=tmpSourceDocumentLink."Table No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1100529715;3;Field  ;
                Name=RecordID;
                CaptionML=[DEU=Datensatz-ID;
                           ENU=Record ID;
                           NLD=Regel ID;
                           NOR=Post-ID;
                           SVE=Post-ID];
                SourceExpr=FORMAT(tmpSourceDocumentLink."Record ID");
                Editable=FALSE }

    { 1100529711;3;Field  ;
                Name=Documents;
                CaptionML=[DEU=Anzahl der Belege;
                           ENU=Number Of Documents;
                           NLD=Documenten];
                SourceExpr=SourceNumberOfDocuments;
                Editable=FALSE;
                OnDrillDown=VAR
                              DocumentFilters@1100525000 : Codeunit 11012432;
                            BEGIN
                              IF NOT SourceDocEnabled THEN
                                EXIT;
                              DocumentFilters.CreateTempFilterAndExecute1(SourceLinkRecRef, '', FALSE, FALSE);
                              SourceNumberOfDocuments := DocumentLinkManagement.EntityNumberOfDocumentLinks(SourceLinkRecRef);
                              CurrPage.UPDATE;
                            END;
                             }

    { 1100529714;2;Group  ;
                Name=Copy To;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy To;
                           NLD=Kopieer naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 1100529713;3;Field  ;
                Name=LinkName_To;
                CaptionML=[DEU=VerknÅpfungsname;
                           ENU=Link Name;
                           NLD=Koppelingnaam;
                           NOR=Lenkenavn;
                           SVE=LÑnknamn];
                SourceExpr=PossibleDocumentLink.GetLinkName(tmpTargetDocumentLink."Table No.",tmpTargetDocumentLink."Filter Expression");
                Editable=FALSE;
                OnLookup=BEGIN
                           IF PossibleDocumentLink.SelectPossibleDocLinkFromList(PossibleDocumentLink, PossibleDocumentLink) THEN BEGIN
                             tmpTargetDocumentLink.VALIDATE("Table No.", PossibleDocumentLink."Table No.");
                             tmpTargetDocumentLink."Filter Expression" := PossibleDocumentLink."Filter Expression";
                           END;

                           TargetDocEnabled := FALSE;
                           CopyEnabled := FALSE;

                           CurrPage.UPDATE;
                         END;
                          }

    { 1100529710;3;Field  ;
                Name=LinkWith_To;
                CaptionML=[DEU=VerknÅpfung mit;
                           ENU=Link with;
                           NLD=Koppeling met;
                           NOR=Linke med;
                           SVE=LÑnka med];
                SourceExpr=DocumentLinkManagement.GetLongDescription(tmpTargetDocumentLink);
                Editable=FALSE;
                OnLookup=VAR
                           RID@1100529701 : RecordID;
                         BEGIN
                           DocumentLinkManagement.SetTable(tmpTargetDocumentLink, TRUE);

                           TargetNumberOfDocuments := 0;
                           TargetDocEnabled := FALSE;
                           CopyEnabled := FALSE;

                           CLEAR(TargetLinkRecRef);
                           TargetLinkRecRef.OPEN(tmpTargetDocumentLink."Table No.");

                           IF FORMAT(tmpTargetDocumentLink."Record ID") <> '' THEN BEGIN
                             RID := tmpTargetDocumentLink."Record ID";
                             TargetDocEnabled := TargetLinkRecRef.GET(RID);
                              IF TargetDocEnabled THEN BEGIN
                               TargetNumberOfDocuments := DocumentLinkManagement.EntityNumberOfDocumentLinks(TargetLinkRecRef);
                               CopyEnabled := SourceDocEnabled AND TargetDocEnabled;
                             END
                           END;

                           CurrPage.UPDATE
                         END;
                          }

    { 1100529707;3;Field  ;
                Name=TableNo_To;
                CaptionML=[DEU=Tabellennr.;
                           ENU=Table No.;
                           NLD=Tabelnr.;
                           NOR=Tabellnr;
                           SVE=Tabellnr];
                SourceExpr=tmpTargetDocumentLink."Table No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1100529702;3;Field  ;
                Name=RecordID_To;
                CaptionML=[DEU=Datensatz-ID;
                           ENU=Record ID;
                           NLD=Regel ID;
                           NOR=Post-ID;
                           SVE=Post-ID];
                SourceExpr=FORMAT(tmpTargetDocumentLink."Record ID");
                Editable=FALSE }

    { 1100529716;3;Field  ;
                Name=Documents_To;
                CaptionML=[DEU=Anzahl der Belege;
                           ENU=Number Of Documents;
                           NLD=Documenten];
                SourceExpr=TargetNumberOfDocuments;
                Editable=FALSE;
                OnDrillDown=VAR
                              DocumentFilters@1100525000 : Codeunit 11012432;
                            BEGIN
                              IF NOT TargetDocEnabled THEN
                                EXIT;
                              DocumentFilters.CreateTempFilterAndExecute1(TargetLinkRecRef, '', FALSE, FALSE);
                              TargetNumberOfDocuments := DocumentLinkManagement.EntityNumberOfDocumentLinks(TargetLinkRecRef);
                              CurrPage.UPDATE;
                            END;
                             }

    { 1100529704;1;Part   ;
                Name=DocumentList;
                CaptionML=[DEU=Belege;
                           ENU=Documents;
                           NLD=Documenten;
                           NOR=Dokument;
                           SVE=Dokument];
                PagePartID=Page11072476;
                Visible=SourceDocEnabled;
                Editable=FALSE;
                PartType=Page }

  }
  CODE
  {
    VAR
      tmpSourceDocumentLink@1100529700 : TEMPORARY Record 11012747;
      SourceLinkRecRef@1100529704 : RecordRef;
      SourceDocEnabled@1100529705 : Boolean INDATASET;
      SourceNumberOfDocuments@1100529709 : Integer;
      PossibleDocumentLink@1100529701 : Record 11012748;
      DocumentLinkManagement@1100529702 : Codeunit 11012401;
      tmpTargetDocumentLink@1100529708 : TEMPORARY Record 11012747;
      TargetLinkRecRef@1100529707 : RecordRef;
      TargetDocEnabled@1100529706 : Boolean INDATASET;
      TargetNumberOfDocuments@1100529710 : Integer;
      CopyEnabled@1100529703 : Boolean INDATASET;
      MoveDoc@1100529711 : Boolean;
      Text000@1100529713 : TextConst 'DEU=Mîchten Sie die Belege kopieren/verschieben?;ENU=Do you want to copy/move the documents?;NLD=Wil u de documenten kopieren/verplaatsen?';
      Text001@1100529712 : TextConst 'DEU=Die Belege werden kopiert/verschoben;ENU=The documents are copied/moved;NLD=Kopiâren/Verplaatsen van de documenten is voltooid';
      DocumentProperties@1100527300 : Record 11012746;

    LOCAL PROCEDURE CopyFunction@1100529706();
    BEGIN
      IF NOT CONFIRM(Text000) THEN
        EXIT;

      DocumentProperties.RESET;
      CurrPage.DocumentList.PAGE.GetSelectedRecord(DocumentProperties);
      IF DocumentProperties.FINDSET THEN
        REPEAT
          DocumentLinkManagement.SetSelectedDocuments(DocumentProperties."No.");
        UNTIL DocumentProperties.NEXT = 0;

      IF MoveDoc THEN
        DocumentLinkManagement.ReplaceDocLink(SourceLinkRecRef,TargetLinkRecRef)
      ELSE
        DocumentLinkManagement.CopyDocLinks(SourceLinkRecRef,TargetLinkRecRef);

      SourceNumberOfDocuments := DocumentLinkManagement.EntityNumberOfDocumentLinks(SourceLinkRecRef);
      TargetNumberOfDocuments := DocumentLinkManagement.EntityNumberOfDocumentLinks(TargetLinkRecRef);

      MESSAGE(Text001);
    END;

    PROCEDURE SetSource@1100529700(RID@1100529701 : RecordID);
    VAR
      DocumentLink@1100529700 : Record 11012747;
    BEGIN
      IF FORMAT(RID) <> '' THEN BEGIN
        tmpSourceDocumentLink.VALIDATE("Table No.", RID.TABLENO);

        DocumentLink.SETCURRENTKEY("Record ID");
        DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RID));
        IF DocumentLink.FINDFIRST THEN
          tmpSourceDocumentLink."Filter Expression" := DocumentLink."Filter Expression";

        tmpSourceDocumentLink."Record ID" := RID;

        SourceLinkRecRef.OPEN(tmpSourceDocumentLink."Table No.");
        SourceDocEnabled := SourceLinkRecRef.GET(RID);

        IF SourceDocEnabled THEN BEGIN
          CurrPage.DocumentList.PAGE.SetFiltersWhitDocumentLinks(SourceLinkRecRef);
          SourceNumberOfDocuments := DocumentLinkManagement.EntityNumberOfDocumentLinks(SourceLinkRecRef);
        END

      END;
    END;

    PROCEDURE SetTarget@1100529707(RID@1100529701 : RecordID);
    VAR
      DocumentLink@1100529700 : Record 11012747;
    BEGIN
      IF FORMAT(RID) <> '' THEN BEGIN
        tmpTargetDocumentLink.VALIDATE("Table No.", RID.TABLENO);

        DocumentLink.SETCURRENTKEY("Record ID");
        DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RID));
        IF DocumentLink.FINDFIRST THEN
          tmpTargetDocumentLink."Filter Expression" := DocumentLink."Filter Expression";

        tmpTargetDocumentLink."Record ID" := RID;

        TargetLinkRecRef.OPEN(tmpTargetDocumentLink."Table No.");
        TargetDocEnabled := TargetLinkRecRef.GET(RID);

        IF TargetDocEnabled THEN
          TargetNumberOfDocuments := DocumentLinkManagement.EntityNumberOfDocumentLinks(TargetLinkRecRef);
      END;
    END;

    BEGIN
    END.
  }
}

