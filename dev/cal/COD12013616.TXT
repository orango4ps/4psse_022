OBJECT Codeunit 12013616 EX User Dimension Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF406000;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SeparationValue@1100285001 : Text[30];

    PROCEDURE OpenUserDimTable@1100285000(ExUserCompany@1100285000 : Record 12013641);
    VAR
      EXUserDimSelection@1100285001 : Page 12013670;
    BEGIN
      CLEAR(EXUserDimSelection);
      EXUserDimSelection.SetEXUser(ExUserCompany."User ID");
      EXUserDimSelection.LOOKUPMODE := TRUE;
      EXUserDimSelection.RUNMODAL;
    END;

    PROCEDURE PopulateTempTable@1100285001(VAR TempDimensionValue@1100285006 : TEMPORARY Record 349;ExUserCode@1100285005 : Code[50]);
    VAR
      TempDimensionValue2@1100285004 : TEMPORARY Record 349;
      DimensionValue@1100285008 : Record 349;
      EXUser@1100285007 : Record 12013641;
      TempDimValue@1100285000 : Code[250];
      UserDimValueCode@1100285001 : ARRAY [999] OF Code[20];
      UserDimCode@1100285009 : Code[20];
      i@1100285002 : Integer;
      TempPos@1100285003 : Integer;
    BEGIN
      SeparationValue := '|';

      IF ExUserCode = '' THEN BEGIN
        EXUser.FINDFIRST;
        ExUserCode := EXUser."User ID";
      END
      ELSE
        EXUser.GET(ExUserCode);

      TempDimensionValue.RESET;
      TempDimensionValue.DELETEALL;

      CLEAR(UserDimValueCode);
      TempDimensionValue2.RESET;
      TempDimensionValue2.DELETEALL;

      IF EXUser."Dimension 1" <> '' THEN BEGIN
        i := 1;
        TempDimValue := UPPERCASE(EXUser."Dimension 1");
        REPEAT
          TempPos := STRPOS(TempDimValue,SeparationValue);
          IF TempPos > 0 THEN BEGIN
            UserDimValueCode[i] := COPYSTR(TempDimValue,1,TempPos-1);
            TempDimValue := DELSTR(TempDimValue,1,TempPos);
          END
          ELSE BEGIN
            UserDimValueCode[i] := COPYSTR(TempDimValue,1);
            TempDimValue := '';
          END;

          IF UserDimCode = '' THEN
            UserDimCode := COPYSTR(UserDimValueCode[i],1, STRPOS(UserDimValueCode[i],':')-1);
          UserDimValueCode[i] := COPYSTR(UserDimValueCode[i],STRPOS(UserDimValueCode[i],':')+1);
          TempDimensionValue2."Dimension Code" := UserDimCode;
          TempDimensionValue2.Code := UserDimValueCode[i];
          TempDimensionValue2.INSERT;

          i += 1;
        UNTIL TempDimValue = '';
      END;

      IF UserDimCode <> '' THEN
        DimensionValue.SETRANGE("Dimension Code", UserDimCode);
      IF DimensionValue.FINDSET THEN
        REPEAT
          TempDimensionValue.INIT;
          TempDimensionValue.TRANSFERFIELDS(DimensionValue);

          TempDimensionValue2.SETRANGE("Dimension Code", DimensionValue."Dimension Code");
          TempDimensionValue2.SETRANGE(Code, DimensionValue.Code);
          TempDimensionValue.Blocked := TempDimensionValue2.FIND('-');
          TempDimensionValue.INSERT;
        UNTIL DimensionValue.NEXT = 0;
    END;

    PROCEDURE UpdateUser@1100285002(VAR TempDimensionValue@1100285001 : TEMPORARY Record 349;ExUserCode@1100285002 : Code[50]);
    VAR
      EXUser@1100285000 : Record 12013641;
      WebSetting@1100285004 : Record 12057071;
      RecRef@1100285003 : RecordRef;
    BEGIN
      EXUser.GET(ExUserCode);

      SeparationValue := '|';

      EXUser."Dimension 1" := '';

      TempDimensionValue.SETRANGE(Blocked, TRUE);
      IF TempDimensionValue.FIND('-') THEN BEGIN
        EXUser."Dimension 1" := TempDimensionValue."Dimension Code" + ':' +
                                TempDimensionValue.Code + SeparationValue;
        IF TempDimensionValue.NEXT <> 0 THEN
          REPEAT
            EXUser."Dimension 1" := EXUser."Dimension 1"  + TempDimensionValue.Code + SeparationValue;
          UNTIL TempDimensionValue.NEXT = 0;

        EXUser."Dimension 1" := COPYSTR(EXUser."Dimension 1",1,STRLEN(EXUser."Dimension 1")-STRLEN(SeparationValue));
      END;

      EXUser.MODIFY;

      RecRef.GETTABLE(EXUser);
      WebSetting.TableChange(RecRef);

      TempDimensionValue.RESET;
    END;

    BEGIN
    END.
  }
}

