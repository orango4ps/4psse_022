OBJECT Page 11129903 Anonymous Object fields
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Felder ³Anonymes ObjektÊ;
               ENU=Anonymous Object fields;
               NLD=Anonimiseren object velden];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table2000000041;
    DataCaptionExpr=PageCaption;
    PageType=List;
    OnOpenPage=BEGIN
                 FILTERGROUP(2);
                 SETRANGE(Class,Class::Normal);
                 FILTERGROUP(0);
                 PageCaption := FORMAT(TableNo) + ' ' + TableName;
               END;

    OnAfterGetRecord=BEGIN
                       GetRec;
                       TransFromRec;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           GetRec;
                           TransFromRec;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 1100528300;2;Field  ;
                SourceExpr=TableNo;
                Editable=false }

    { 1100528301;2;Field  ;
                SourceExpr=TableName;
                Editable=false }

    { 2   ;2   ;Field     ;
                Lookup=No;
                CaptionML=[DEU=Nr.;
                           ENU=No.;
                           NLD=Nr.;
                           NOR=Nr.;
                           SVE=Nr];
                SourceExpr="No.";
                Editable=FALSE }

    { 1100528302;2;Field  ;
                SourceExpr=FieldName;
                Editable=false }

    { 4   ;2   ;Field     ;
                DrillDown=No;
                CaptionML=[DEU=Feldbezeichnung;
                           ENU=Field Caption;
                           NLD=Veldbijschrift;
                           NOR=Felttekst;
                           SVE=F„ltrubrik];
                SourceExpr="Field Caption";
                Visible=false;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=Change;
                CaptionML=[DEU=Žndern;
                           ENU=Change;
                           NLD=Verander];
                SourceExpr=Change;
                OnValidate=VAR
                             ValueString@1100525000 : Text[50];
                           BEGIN
                             IF Change = TRUE THEN BEGIN
                               ValueString := '##################################################';
                               Field.RESET;
                               Field.SETRANGE(TableNo,AnonymousObjectField."Table No.");
                               Field.SETRANGE("No.",AnonymousObjectField."Field No.");
                               IF Field.FINDFIRST THEN BEGIN
                                 Value := COPYSTR(ValueString,1,Field.Len);
                               END;

                             END ELSE BEGIN
                               Value := '';
                             END;
                             UpdateRec;
                           END;
                            }

    { 1100525000;2;Field  ;
                Name=Value;
                CaptionML=[DEU=Wert;
                           ENU=Value;
                           NLD=Waarde;
                           NOR=Verdi;
                           SVE=V„rde];
                SourceExpr=Value;
                OnValidate=BEGIN
                             Field.RESET;
                             Field.SETRANGE(TableNo,AnonymousObjectField."Table No.");
                             Field.SETRANGE("No.",AnonymousObjectField."Field No.");
                             IF Field.FINDFIRST THEN BEGIN
                               IF STRLEN(Value) > Field.Len THEN
                                 ERROR(Text001,Field.Len);
                             END;
                             UpdateRec;
                           END;
                            }

  }
  CODE
  {
    VAR
      Change@1001 : Boolean;
      Value@1100525001 : Text[50];
      PageCaption@1002 : Text[250];
      AnonymousObjectField@1100525000 : Record 11229428;
      Field@1100525002 : Record 2000000041;
      Text001@1100525003 : TextConst 'DEU=Der Wert kann nur %1 lang sein.;ENU=The value can only be %1 long.;NLD=De waarde mag maar %1 tekens zijn.';

    LOCAL PROCEDURE UpdateRec@18();
    BEGIN
      GetRec;
      TransToRec;
      WITH AnonymousObjectField DO
        IF NOT (Change) THEN BEGIN
          IF DELETE THEN;
        END ELSE
          IF NOT MODIFY THEN
            INSERT;
    END;

    LOCAL PROCEDURE GetRec@2();
    BEGIN
      IF NOT AnonymousObjectField.GET(TableNo,"No.") THEN BEGIN
        AnonymousObjectField.INIT;
        AnonymousObjectField."Table No." := TableNo;
        AnonymousObjectField."Field No." := "No.";
      END;
    END;

    LOCAL PROCEDURE TransFromRec@5();
    BEGIN
      Change := AnonymousObjectField.Change;
      Value := AnonymousObjectField.Value;
    END;

    LOCAL PROCEDURE TransToRec@6();
    BEGIN
      AnonymousObjectField.Change := Change;
      AnonymousObjectField.Value := Value;
    END;

    BEGIN
    END.
  }
}

