OBJECT Report 8613 Create Item Journal Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Artikel Buch.-Blattzeilen erstellen;
               ENU=Create Item Journal Lines;
               NLD=Artikeldagboekregels maken;
               NOR=Opprett varekladdelinjer;
               SVE=Skapa artikeljournalrader];
    ApplicationArea=#Basic,#Suite;
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   MESSAGE(Text004);
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table27;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               CheckJournalTemplate;
                               CheckBatchName;
                               CheckPostingDate;

                               ItemJnlLine.SETRANGE("Journal Template Name",JournalTemplate);
                               ItemJnlLine.SETRANGE("Journal Batch Name",BatchName);
                               IF ItemJnlLine.FINDLAST THEN
                                 LineNo := ItemJnlLine."Line No." + 10000
                               ELSE
                                 LineNo := 10000;

                               ItemJnlBatch.GET(JournalTemplate,BatchName);
                               IF TemplateCode <> '' THEN
                                 StdItemJnl.GET(JournalTemplate,TemplateCode);
                             END;

               OnAfterGetRecord=VAR
                                  StdItemJnlLine@1000 : Record 753;
                                BEGIN
                                  ItemJnlLine.INIT;
                                  IF GetStandardJournalLine THEN BEGIN
                                    Initialize(StdItemJnl,ItemJnlBatch.Name);

                                    StdItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
                                    StdItemJnlLine.SETRANGE("Standard Journal Code",StdItemJnl.Code);
                                    IF StdItemJnlLine.FINDSET THEN
                                      REPEAT
                                        CopyItemJnlFromStdJnl(StdItemJnlLine,ItemJnlLine);
                                        ItemJnlLine.VALIDATE("Entry Type",EntryTypes);
                                        ItemJnlLine.VALIDATE("Item No.","No.");

                                        IF PostingDate <> 0D THEN
                                          ItemJnlLine.VALIDATE("Posting Date",PostingDate);

                                        IF DocumentDate <> 0D THEN BEGIN
                                          ItemJnlLine.VALIDATE("Posting Date",DocumentDate);
                                          ItemJnlLine."Posting Date" := PostingDate;
                                        END;

                                        IF NOT ItemJnlLine.INSERT(TRUE) THEN
                                          ItemJnlLine.MODIFY(TRUE);
                                      UNTIL StdItemJnlLine.NEXT = 0;
                                  END ELSE BEGIN
                                    ItemJnlLine.VALIDATE("Journal Template Name",ItemJnlLine.GETFILTER("Journal Template Name"));
                                    ItemJnlLine.VALIDATE("Journal Batch Name",BatchName);
                                    ItemJnlLine."Line No." := LineNo;
                                    LineNo := LineNo + 10000;

                                    ItemJnlLine.VALIDATE("Entry Type",EntryTypes);
                                    ItemJnlLine.VALIDATE("Item No.","No.");

                                    IF PostingDate <> 0D THEN
                                      ItemJnlLine.VALIDATE("Posting Date",PostingDate);

                                    IF DocumentDate <> 0D THEN BEGIN
                                      ItemJnlLine.VALIDATE("Posting Date",DocumentDate);
                                      ItemJnlLine."Posting Date" := PostingDate;
                                    END;

                                    IF (ItemJnlLine."Document No." = '') AND (DocumentNo <> '') THEN
                                      ItemJnlLine.VALIDATE("Document No.",DocumentNo);

                                    IF NOT ItemJnlLine.INSERT(TRUE) THEN
                                      ItemJnlLine.MODIFY(TRUE);
                                  END;
                                END;

               ReqFilterFields=No.,Statistics Group,Vendor No.,Blocked }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PostingDate = 0D THEN
                     PostingDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Postenart;
                             ENU=Entry Type;
                             NLD=Boekingssoort;
                             NOR=Posttype;
                             SVE=Transaktionstyp];
                  ToolTipML=[DEU=Gibt die Postenart an.;
                             ENU=Specifies the entry type.;
                             NLD=Hiermee wordt het boekingssoort opgegeven.;
                             NOR=Angir posttypen.;
                             SVE=Anger transaktionstypen.];
                  OptionCaptionML=[DEU=Einkauf,Verkauf,Zugang,Abgang;
                                   ENU=Purchase,Sale,Positive Adjmt.,Negative Adjmt.;
                                   NLD=Inkoop,Verkoop,Pos. correctie,Neg. correctie;
                                   NOR=Kjõp,Salg,Oppjustering,Nedjustering;
                                   SVE=Inkîp,FîrsÑljning,Positiv just.,Negativ just.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=EntryTypes }

      { 1   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Dokumentnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt die Standardbelegnummer der Buch.-Blattzeile an.;
                             ENU=Specifies the default document number of the journal line.;
                             NLD=Hiermee wordt het standaarddocumentnummer van de dagboekregel opgegeven.;
                             NOR=Angir standard dokumentnummer pÜ kladdelinjen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentNo }

      { 4   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  ToolTipML=[DEU=Legt das Datum fÅr die Buchung dieser Stapelverarbeitung fest. StandardmÑ·ig wird das Arbeitsdatum eingegeben, Sie kînnen es allerdings Ñndern.;
                             ENU=Specifies the date for the posting of this batch job. By default, the working date is entered, but you can change it.;
                             NLD=Hiermee wordt de datum opgegeven voor de boeking van deze batchverwerking. Standaard wordt de werkdatum ingevuld in dit veld, maar u kunt deze wijzigen.;
                             NOR=Angir datoen for bokfõring av denne kjõrselen. Som standard oppgis arbeidsdatoen, men du kan endre den.;
                             SVE=Anger datumet fîr bokfîringen av detta batch-jobb. Arbetsdatumet anges standard, men du kan Ñndra det.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               CheckPostingDate;
                             END;
                              }

      { 6   ;2   ;Field     ;
                  CaptionML=[DEU=Belegdatum;
                             ENU=Document Date;
                             NLD=Documentdatum;
                             NOR=Bilagsdato;
                             SVE=Dokumentdatum];
                  ToolTipML=[DEU=Legt das Belegdatum fest, das in die erstellten DatensÑtze eingefÅgt wird.;
                             ENU=Specifies the document date that will be inserted on the created records.;
                             NLD=Hiermee wordt de documentdatum opgegeven die wordt ingevoegd in de gemaakte records.;
                             NOR=Angir dokumentdatoen som skal settes inn i de opprettede postene.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentDate }

      { 12  ;2   ;Field     ;
                  CaptionML=[DEU=Buch.-Blattvorlage;
                             ENU=Journal Template;
                             NLD=Dagboeksjabloon;
                             NOR=Kladdemal;
                             SVE=Journalmall];
                  ToolTipML=[DEU=Gibt die Buch.-Blattvorlage an, auf der das Artikel Buch.-Blatt basiert.;
                             ENU=Specifies the journal template that the item journal is based on.;
                             NLD=Hiermee wordt de dagboeksjabloon opgegeven waarop het artikeldagboek is gebaseerd.;
                             NOR=Angir kladdemalen som varekladden er basert pÜ.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=JournalTemplate;
                  TableRelation="Item Journal Template".Name;
                  OnValidate=BEGIN
                               CheckJournalTemplate;
                             END;

                  OnLookup=VAR
                             ItemJnlTemplate@1001 : Record 82;
                             ItemJnlTemplates@1000 : Page 102;
                           BEGIN
                             ItemJnlTemplate.SETRANGE(Type,ItemJnlTemplate.Type::Item);
                             ItemJnlTemplate.SETRANGE(Recurring,FALSE);
                             ItemJnlTemplates.SETTABLEVIEW(ItemJnlTemplate);

                             ItemJnlTemplates.LOOKUPMODE := TRUE;
                             ItemJnlTemplates.EDITABLE := FALSE;
                             IF ItemJnlTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               ItemJnlTemplates.GETRECORD(ItemJnlTemplate);
                               JournalTemplate := ItemJnlTemplate.Name;
                             END;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  CaptionML=[DEU=Buch.-Blattname;
                             ENU=Batch Name;
                             NLD=Batchnaam;
                             NOR=Bunkenavn;
                             SVE=Journalnamn];
                  ToolTipML=[DEU=Gibt den Namen des Buch.-Blattnamens an, eines personalisierten Buch.-Blattlayouts, auf dem die Erfassung basiert.;
                             ENU=Specifies the name of the journal batch, a personalized journal layout, that the journal is based on.;
                             NLD=Hiermee wordt de naam opgegeven van de dagboekbatch, een aangepaste dagboeklay-out, waarop het dagboek is gebaseerd.;
                             NOR=Angir navnet pÜ kladden, et tilpasset kladdoppsett, som kladden er basert pÜ.;
                             SVE=Anger namnet pÜ journalen, en anpassad journallayout, som journalen Ñr baserad pÜ.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=BatchName;
                  OnValidate=BEGIN
                               CheckBatchName;
                             END;

                  OnLookup=VAR
                             ItemJnlBatches@1000 : Page 262;
                           BEGIN
                             IF JournalTemplate <> '' THEN BEGIN
                               ItemJnlBatch.SETRANGE("Journal Template Name",JournalTemplate);
                               ItemJnlBatches.SETTABLEVIEW(ItemJnlBatch);
                             END;

                             ItemJnlBatches.LOOKUPMODE := TRUE;
                             ItemJnlBatches.EDITABLE := FALSE;
                             IF ItemJnlBatches.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               ItemJnlBatches.GETRECORD(ItemJnlBatch);
                               BatchName := ItemJnlBatch.Name;
                             END;
                           END;
                            }

      { 10  ;2   ;Field     ;
                  CaptionML=[DEU=Standard Artikel Buch.-Blatt;
                             ENU=Standard Item Journal;
                             NLD=Standaardartikeldagboek;
                             NOR=Standard varekladd;
                             SVE=Standardartikeljournal];
                  ToolTipML=[DEU=Gibt das Standard Artikel Buch.-Blatt an, das die Stapelverarbeitung verwendet.;
                             ENU=Specifies the standard item journal that the batch job uses.;
                             NLD=Hiermee wordt het standaardartikeldagboek opgegeven dat door de batchverwerking wordt gebruikt.;
                             NOR=Angir standard varekladd som kjõrselen bruker.];
                  ApplicationArea=#Suite;
                  SourceExpr=TemplateCode;
                  TableRelation="Standard Item Journal".Code;
                  OnLookup=VAR
                             StdItemJnl1@1001 : Record 752;
                             StdItemJnls@1000 : Page 753;
                           BEGIN
                             IF JournalTemplate <> '' THEN BEGIN
                               StdItemJnl1.SETRANGE("Journal Template Name",JournalTemplate);
                               StdItemJnls.SETTABLEVIEW(StdItemJnl1);
                             END;

                             StdItemJnls.LOOKUPMODE := TRUE;
                             StdItemJnls.EDITABLE := FALSE;
                             IF StdItemJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               StdItemJnls.GETRECORD(StdItemJnl1);
                               TemplateCode := StdItemJnl1.Code;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      StdItemJnl@1007 : Record 752;
      ItemJnlBatch@1006 : Record 233;
      LastItemJnlLine@1012 : Record 83;
      ItemJnlLine@1011 : Record 83;
      UOMMgt@1010 : Codeunit 5402;
      EntryTypes@1000 : 'Purchase,Sale,Positive Adjmt.,Negative Adjmt.';
      PostingDate@1001 : Date;
      DocumentDate@1002 : Date;
      BatchName@1003 : Code[10];
      TemplateCode@1004 : Code[20];
      LineNo@1008 : Integer;
      JournalTemplate@1015 : Text[10];
      Text001@1018 : TextConst 'DEU=Der Artikel Buch.-Blattvorlagenname ist leer.;ENU=Item Journal Template name is blank.;NLD=Naam artikeldagboeksjabloon is leeg.;NOR=Navn pÜ varekladdemal er tomt.';
      Text002@1017 : TextConst 'DEU=Der Artikel Buch.-Blattname ist leer.;ENU=Item Journal Batch name is blank.;NLD=Naam artikeldagboekbatch is leeg.;NOR=Navn pÜ varekladd er tomt.';
      Text004@1005 : TextConst 'DEU=Die Artikel Buch.-Blattzeilen wurden erfolgreich erstellt.;ENU=Item Journal lines are successfully created.;NLD=Artikeldagboekregels zijn gemaakt.;NOR=Varekladdelinjer er opprettet.';
      PostingDateIsEmptyErr@1009 : TextConst 'DEU=Das Buchungsdatum ist leer.;ENU=The Posting Date is empty.;NLD=Boekingsdatum is leeg.;NOR=Bokfõringsdatoen er tom.';
      DocumentNo@1020 : Code[20];

    LOCAL PROCEDURE GetStandardJournalLine@3() : Boolean;
    VAR
      StdItemJnlLine@1000 : Record 753;
    BEGIN
      IF TemplateCode = '' THEN
        EXIT;
      StdItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
      StdItemJnlLine.SETRANGE("Standard Journal Code",StdItemJnl.Code);
      EXIT(StdItemJnlLine.FINDFIRST);
    END;

    [External]
    PROCEDURE Initialize@2(StdItemJnl@1000 : Record 752;JnlBatchName@1001 : Code[10]);
    BEGIN
      ItemJnlLine."Journal Template Name" := StdItemJnl."Journal Template Name";
      ItemJnlLine."Journal Batch Name" := JnlBatchName;
      ItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
      ItemJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      LastItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
      LastItemJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      IF LastItemJnlLine.FINDLAST THEN;
    END;

    LOCAL PROCEDURE CopyItemJnlFromStdJnl@7(StdItemJnlLine@1000 : Record 753;VAR ItemJnlLine@1001 : Record 83);
    BEGIN
      ItemJnlLine.INIT;
      ItemJnlLine."Line No." := 0;
      ItemJnlLine.SetUpNewLine(LastItemJnlLine);
      IF LastItemJnlLine."Line No." <> 0 THEN
        ItemJnlLine."Line No." := LastItemJnlLine."Line No." + 10000
      ELSE
        ItemJnlLine."Line No." := 10000;

      ItemJnlLine.TRANSFERFIELDS(StdItemJnlLine,FALSE);

      IF (ItemJnlLine."Item No." <> '') AND (ItemJnlLine."Unit Amount" = 0) THEN
        ItemJnlLine.RecalculateUnitAmount;

      IF (ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Output) AND
         (ItemJnlLine."Value Entry Type" <> ItemJnlLine."Value Entry Type"::Revaluation)
      THEN
        ItemJnlLine."Invoiced Quantity" := 0
      ELSE
        ItemJnlLine."Invoiced Quantity" := ItemJnlLine.Quantity;
      ItemJnlLine.TESTFIELD("Qty. per Unit of Measure");
      ItemJnlLine."Invoiced Qty. (Base)" :=
        ROUND(ItemJnlLine."Invoiced Quantity" * ItemJnlLine."Qty. per Unit of Measure",UOMMgt.QtyRndPrecision);

      ItemJnlLine.INSERT(TRUE);

      LastItemJnlLine := ItemJnlLine;
    END;

    [External]
    PROCEDURE InitializeRequest@1(EntryTypesFrom@1000 : Option;PostingDateFrom@1001 : Date;DocumentDateFrom@1002 : Date);
    BEGIN
      EntryTypes := EntryTypesFrom;
      PostingDate := PostingDateFrom;
      DocumentDate := DocumentDateFrom;
    END;

    [External]
    PROCEDURE InitializeRequestTemplate@4(JournalTemplateFrom@1000 : Text[10];BatchNameFrom@1001 : Code[10];TemplateCodeFrom@1002 : Code[20]);
    BEGIN
      JournalTemplate := JournalTemplateFrom;
      BatchName := BatchNameFrom;
      TemplateCode := TemplateCodeFrom;
    END;

    [External]
    PROCEDURE SetDefaultDocumentNo@5(NewDocumentNo@1000 : Code[20]);
    BEGIN
      DocumentNo := NewDocumentNo;
    END;

    LOCAL PROCEDURE CheckPostingDate@24();
    BEGIN
      IF PostingDate = 0D THEN
        ERROR(PostingDateIsEmptyErr);
    END;

    LOCAL PROCEDURE CheckBatchName@32();
    BEGIN
      IF BatchName = '' THEN
        ERROR(Text002);
    END;

    LOCAL PROCEDURE CheckJournalTemplate@33();
    BEGIN
      IF JournalTemplate = '' THEN
        ERROR(Text001);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

