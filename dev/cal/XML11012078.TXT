OBJECT XMLport 11012078 Import GL Entries DBS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries DBS;
               SVE=Importera redovisningstransaktioner DBS];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::DBS);
                    HumanResourcesSetup.TESTFIELD("Exchange Salary Application");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");
                    HumanResourcesSetup.TESTFIELD("Pathname Salary Backup Files");

                    currXMLport.FILENAME(HumanResourcesSetup."Pathname Files from Salary");
                  END;

    OnPreXMLport=BEGIN
                   GenJnlLine.LOCKTABLE;
                   GenJnlLine.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   GenJnlLine.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF GenJnlLine.FIND('+') THEN
                     LineCounter := GenJnlLine."Line No." + 10000
                   ELSE
                     LineCounter := 10000;

                   FileMgt.SplitFileAbsPathName(currXMLport.FILENAME,DummyTxt,SelectedFileName);

                   JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template");
                   JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch");
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{C0A2561D-0010-4577-9884-054C5D8F8954}];  ;Root                ;Element ;Text     }

    { [{0B09E32A-A560-420B-8D76-49E3757F2350}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(CompNo);
                                                                              CLEAR(DocNo);
                                                                              CLEAR(PostDate);
                                                                              CLEAR(AccountCde);
                                                                              CLEAR(AmntTxt);
                                                                              CLEAR(DescTxt);
                                                                              CLEAR(DimensionCde);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 RecRef@1100528400 : RecordRef;
                                                                               BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN //With statement added because this code has been copied from dataport.

                                                                                 "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                 "Journal Batch Name" := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                 "Line No." := LineCounter;
                                                                                 LineCounter := LineCounter + 10000;

                                                                                 "Document No." := DocNo;
                                                                                 "Account Type" := "Account Type"::"G/L Account";
                                                                                 "Posting Date" := PostDate;
                                                                                 "Source Code"   := JnlTemplRec."Source Code";
                                                                                 IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                   "Reason Code" := JnlBatchRec."Reason Code"
                                                                                 ELSE
                                                                                   "Reason Code" := JnlTemplRec."Reason Code";

                                                                                 IF CompNo <> '' THEN BEGIN
                                                                                   CompanyData.SETRANGE("Company ID", CompNo);
                                                                                   IF CompanyData.FINDFIRST THEN
                                                                                     IF CompanyData."Company Name" <> COMPANYNAME THEN
                                                                                       "Receiving Company" := CompanyData."Company Name";
                                                                                 END;

                                                                                 "Account No." := DELCHR(AccountCde, '<', '0');
                                                                                 "Shortcut Dimension 1 Code" := DELCHR(DimensionCde, '<', '0');

                                                                                 VALIDATE("Account No.");
                                                                                 VALIDATE("Shortcut Dimension 1 Code");

                                                                                 Amount := ConvertTextToValue(AmntTxt);
                                                                                 VALIDATE(Amount);
                                                                                 "System-Created Entry" := TRUE;
                                                                                 Description := COPYSTR(DescTxt,1,MAXSTRLEN(Description));

                                                                                 "Gen. Posting Type" := "Gen. Posting Type"::" ";
                                                                                 "Gen. Bus. Posting Group" := '';
                                                                                 "Gen. Prod. Posting Group"  := '';
                                                                                 "VAT Bus. Posting Group" := '';
                                                                                 VALIDATE("VAT Prod. Posting Group", ''); //This clears VAT Amounts
                                                                                 "Origin Salary Application" := TRUE;

                                                                                 RecRef.GETTABLE("Gen. Journal Line");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 INSERT;

                                                                                 currXMLport.SKIP;

                                                                                 END; //Belongs to With statement.
                                                                               END;
                                                                                }

    { [{2279E25C-7373-44FE-8E7F-50ADD0EA6071}];2 ;CompNo              ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=5 }

    { [{8231EB70-DD4E-4974-95CE-7C8B3F29D3A0}];2 ;Filler1             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=29 }

    { [{68DB4C82-60C3-4C30-BAF4-E51225C085B8}];2 ;DocNo               ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{704F81E7-5B70-4570-BE86-03EC798B493F}];2 ;Filler2             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{30519003-0A35-4A85-A6AB-C7CB1A07F37C}];2 ;PostDateTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  EVALUATE(PostDate, PostDateTxt);
                                                                                END;

                                                  Width=8 }

    { [{19276242-C8B3-4E7A-B2A1-2E4E916708C4}];2 ;Filler3             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=30 }

    { [{E161D3AC-A908-4BA9-A642-84B56259F26F}];2 ;AccountCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=11 }

    { [{D184AEBE-53BD-46AD-9C8F-3D507F2B5279}];2 ;AmntTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=11 }

    { [{169AD3F3-7C89-447C-84C9-3FB25C12C145}];2 ;DescTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=30 }

    { [{4AF69175-9225-405A-B468-3BD56017C432}];2 ;Filler4             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=136 }

    { [{AA5C2E27-23FD-4485-98B5-E04A9DD2E75A}];2 ;DimensionCde        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=7 }

    { [{6445F1BC-547B-42CB-9975-421D1DDDA673}];2 ;Filler5             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=65 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      HumanResourcesSetup@1100528420 : Record 5218;
      GenJnlLine@1100528419 : Record 81;
      JnlTemplRec@1100528418 : Record 80;
      JnlBatchRec@1100528417 : Record 232;
      CompanyData@1100528416 : Record 11020674;
      FileMgt@1100528410 : Codeunit 419;
      XMLportStatusDialogMgt@1100528411 : Codeunit 11130006;
      LineCounter@1100528415 : Integer;
      PostDate@1100528413 : Date;
      DummyTxt@1100528408 : Text[250];
      OldFile@1100528404 : Text[250];
      NewFile@1100528403 : Text[250];
      SelectedFileName@1100528402 : Text[250];

    PROCEDURE ConvertTextToValue@1210190001(QuantityText@1210190000 : Text[11]) Value@1210190003 : Decimal;
    VAR
      ValueQuantityText@1210190001 : Text[11];
    BEGIN
      IF QuantityText = '' THEN
        EXIT(0);

      ValueQuantityText := DELCHR(QuantityText,'=','.-'); //remove dots and sign
      EVALUATE(Value,DELCHR(ValueQuantityText,'=','.'));
      Value := Value/100;
      IF STRPOS(QuantityText, '-') > 0 THEN
        Value := -Value;
    END;

    BEGIN
    {
      CC: Dataport 11012352

      4PS01 JD 29-08-2007 Call C-010871, for support CALBI, ALBI will not be supported right anymore
    }
    END.
  }
}

