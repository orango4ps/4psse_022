OBJECT Codeunit 11012866 Create Customer Central Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WebServiceURL@1100525004 : Record 11020659;
      WebServiceManagement@1100525000 : Codeunit 11012266;
      CustomerTagName@1100525003 : Text;
      CustomerXPath@1100525002 : Text;
      CustomerNamespace@1100525001 : Text;
      Text000@1100525005 : TextConst 'DEU=Unternehmen %1 hat keine verknÅpfte Zentralgesellschaft. Erstellung Debitor (zentral verwaltet) nicht zulÑssig.;ENU=Company %1 has no linked Central Company. Creating Customer (Central Managed) not allowed.;NLD=Bedrijf %1 heeft geen gekoppeld centraal bedrijf. Klant aanmaken (centraal beheer) niet mogelijk.';
      Text001@1100527300 : TextConst 'DEU=Debitoren werden zentral verwaltet, EinfÅgung nicht erlaubt.;ENU=Customers are managed centrally, insert is not allowed.;NLD=Klanten worden centraal beheerd, invoer is niet toegestaan.';

    PROCEDURE CreateCustomerInCentralCompany@1100525001(VAR ICustomer@1100525000 : Record 18;ICustomerTemplateCode@1100525001 : Code[10]);
    VAR
      Namespaces@1100525006 : ARRAY [10,2] OF Text[100];
    BEGIN
      TestIfInDecentralCompany;
      InitGlobalVars;

      WebServiceManagement.PrepareSOAPRequest(CustomerNamespace, 'Create');
      Namespaces[1, 1] := 'cu';
      Namespaces[1, 2] := CustomerNamespace;
      WebServiceManagement.AddRequestSelectionNamespaces(Namespaces);
      WebServiceManagement.AppendSOAPParameter(CustomerNamespace, CustomerTagName, '');

      FillCustomerFieldsInSoapRequest(ICustomer, ICustomerTemplateCode);
      WebServiceManagement.SendSOAPRequest2('Create', WebServiceURL.URL, WebServiceURL.Login, WebServiceURL.Password, TRUE);
      WebServiceManagement.AddResponseSelectionNamespaces(Namespaces);

      ICustomer."No." :=
        WebServiceManagement.GetResponseValueText('.//cu:' + CustomerTagName + '/cu:No', TRUE);
      ICustomer.FIND('=');
    END;

    PROCEDURE TestIfInDecentralCompany@1100525000();
    VAR
      CentralDataManagement@1100525000 : Codeunit 11012010;
    BEGIN
      //IF NOT CentralDataManagement.CentralActiveLocalInput THEN //C016112.o
      IF NOT CentralDataManagement.CentralActiveLocalInput(DATABASE::"Marketing Setup") THEN //C016112.n
        ERROR(Text000, COMPANYNAME);
    END;

    LOCAL PROCEDURE InitGlobalVars@1100525004();
    BEGIN
      CustomerTagName := 'CustomerSCW';
      CustomerXPath := './/cu:Create/cu:' + CustomerTagName;
      IF NOT WebServiceURL.GET(WebServiceURL.Type::"Service Call Wizard", 'CREATECUST') THEN
        ERROR(Text001);
      WebServiceURL.TESTFIELD("Target Namespace");
      CustomerNamespace := WebServiceURL."Target Namespace"; //urn:microsoft-dynamics-schemas/page/customerscw
    END;

    LOCAL PROCEDURE FillCustomerFieldsInSoapRequest@1100528402(ICustomer@1100528403 : Record 18;ICustomerTemplateCode@1100525000 : Code[10]);
    BEGIN
      FillCustomerFieldInSoapRequest('Customer_Template_Code', ICustomerTemplateCode);
      FillCustomerFieldInSoapRequest('Name', ICustomer.Name);
      FillCustomerFieldInSoapRequest('Name_2', ICustomer."Name 2");
      FillCustomerFieldInSoapRequest('Address', ICustomer.Address);
      FillCustomerFieldInSoapRequest('Address_2', ICustomer."Address 2");
      FillCustomerFieldInSoapRequest('City', ICustomer.City);
      FillCustomerFieldInSoapRequest('Post_Code', ICustomer."Post Code");
      FillCustomerFieldInSoapRequest('Country_Region_Code', ICustomer."Country/Region Code");
      FillCustomerFieldInSoapRequest('Phone_No', ICustomer."Phone No.");
    END;

    LOCAL PROCEDURE FillCustomerFieldInSoapRequest@1100528410(FieldName@1100528400 : Text[50];Value@1100528401 : Text[250]);
    BEGIN
      WebServiceManagement.AppendParameter(CustomerXPath, CustomerNamespace, FieldName, Value);
    END;

    BEGIN
    END.
  }
}

