OBJECT Codeunit 12013595 Exflow-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF500000;
  }
  PROPERTIES
  {
    TableNo=12013588;
    Permissions=TableData 36=m,
                TableData 37=m,
                TableData 39=imd,
                TableData 49=imd,
                TableData 93=imd,
                TableData 94=imd,
                TableData 110=imd,
                TableData 111=imd,
                TableData 120=imd,
                TableData 121=imd,
                TableData 122=imd,
                TableData 123=imd,
                TableData 124=imd,
                TableData 125=imd,
                TableData 223=imd,
                TableData 357=imd,
                TableData 359=imd,
                TableData 6507=ri,
                TableData 6508=rid,
                TableData 6650=imd,
                TableData 6651=imd;
    OnRun=VAR
            ExPurchDocHeader@1100285004 : Record 12013587;
            ExFlowSetup@1100285001 : Record 12013601;
            ExflowWorkFlowMgt@1100285003 : Codeunit 12013593;
            ExFlowSEFunctions@1100285000 : Codeunit 12013598;
            ExDimMgt@1100285002 : Codeunit 12013605;
          BEGIN
            IF "OCR Imported Line" THEN BEGIN
              IF "OCR Line is Text Only" THEN
                EXIT;
            END ELSE IF Type = Type::" " THEN
              EXIT;

            TESTFIELD("Document Type");
            TESTFIELD("Buy-from Vendor No.");

            ExFlowSetup.GET;
            ExPurchDocHeader.GET("Inbound Document No.");

            IF ExFlowSetup."Order Applies-to" <> ExFlowSetup."Order Applies-to"::"Order not used" THEN
              CheckOrderMatch(Rec,ExFlowSetup);

            CheckAlways(Rec);

            IF "Test Unit Costs" THEN
              IF NOT ExflowWorkFlowMgt.PurchPriceOK(Rec) THEN
                IF "Exflow-Created Entry" = "Exflow-Created Entry"::DiffLine THEN
                  ERROR(EXF119)
                ELSE
                  ERROR(EXF002);

            IF ExFlowSetup."Check if no Approvers" THEN
              CheckProposalExist(Rec,ExPurchDocHeader);

            IF ExFlowSetup."Check Posting Setup" THEN
              CheckPostingSetup(Rec);

            IF ExFlowSetup."Check Dimensions" THEN
              ExDimMgt.ImpLineCCDocDimToTempDocDim(ExPurchDocHeader,Rec);

            IF ExFlowSetup."Automatic Reg. of Inwards" = ExFlowSetup."Automatic Reg. of Inwards"::"Values from Lines" THEN
              ExFlowSEFunctions.TestInwSetup(Rec);

            IF "Check Order Line (OCR)" AND ExFlowSetup."Require Accept UOM Mismatch" THEN
              IF "Potential UOM Mismatch" AND (NOT "Accept Potential UOM Mismatch") THEN
                ERROR(EXF123);
          END;

  }
  CODE
  {
    VAR
      EXF001@1100285002 : TextConst 'ENU=Line with Order No %1 cannot be matched against a receipt line.;SVE=Raden med order nr %1 kan ej matchas mot en inleveransrad.';
      EXF002@1100285001 : TextConst 'ENU=Line difference between the invoice and the order unit cost!;SVE=Raddifferens mellan fakturan och orderns ink”pspris!';
      EXF100@1100285003 : TextConst 'ENU=Approvers Missing;SVE=Attestanter saknas';
      EXF104@1100285015 : TextConst 'ENU=Product posting group %1 does not exist!;SVE=Produktbokf”ringsmall %1 finns inte!';
      EXF105@1100285014 : TextConst 'ENU=VAT Product posting group %1 does not exist!;SVE=Moms produktbokf”ringsmall %1 finns inte!';
      EXF106@1100285013 : TextConst 'ENU=G/L Account %1 does not exist!;SVE=Konto %1 finns inte!';
      EXF107@1100285012 : TextConst 'ENU=Account No. %1 is blocked;SVE=Konto %1 „r sp„rrat';
      EXF108@1100285011 : TextConst 'ENU=Item %1 does not exist!;SVE=Artikel %1 finns inte!';
      EXF109@1100285010 : TextConst 'ENU="Item No. %1 is blocked. ";SVE=Artikel %1 „r sp„rrad!';
      EXF110@1100285009 : TextConst 'ENU=Fixed asset %1 does not exist!;SVE=Anl„ggningstillg†ng %1 finns inte!';
      EXF111@1100285008 : TextConst 'ENU=Fixed Asset %1 is blocked.;SVE=Anl. tillg†ng %1 „r sp„rrad!';
      EXF113@1100285006 : TextConst 'ENU=Line type must be %1.;SVE=Radtyp m†ste vara %1.';
      EXF114@1100285005 : TextConst 'ENU=No. must be %1.;SVE=Nr m†ste vara %1.';
      EXF115@1100285004 : TextConst 'ENU=Quantity must be less than or equal to %1.;SVE=Antal m†ste vara lika med eller mindre „n %1.';
      EXF116@1100285016 : TextConst 'ENU=Unsupported Document Type: %1;SVE=Denna dokumenttyp st”ds ej: %1';
      EXF117@1100285017 : TextConst 'ENU=Line cannot be matched against the Order No. %1.;SVE=Raden kan ej matchas mot order numret %1.';
      EXF118@1100285018 : TextConst 'ENU=Line requires an Order No.!;SVE=Raden kr„ver ett order nr.!';
      EXF119@1100285019 : TextConst 'ENU=The difference lines exceeds the maximum tolerance!;SVE=Differensraderna ”verstiger den maximala toleransen!';
      EXF120@1100285020 : TextConst 'ENU=Line cannot be matched against an Order Line.;SVE=Raden kan ej matchas mot en orderrad.';
      EXF121@1100285021 : TextConst 'ENU=Document %1, Line %2, contains Gen. Bus. Posting Group (%3) and Gen. Prod. Posting Group (%4). \There is however no setup in General Posting Setup for this combination.;SVE=Dokument %1, rad %2, inneh†ller Gen. R”relse. Mall (%3) och Gen. Prod Mall (%4)\Denna kombination finns ej uppsatt i Bokf”ringsinst„llningar';
      EXF122@1100285007 : TextConst 'ENU=Document %1, Line %2, contains VAT Bus. Posting Group (%3) and VAT Prod. Posting Group (%4). \There is however no setup in VAT Posting Setup for this combination.;SVE=Dokument %1, rad %2, inneh†ller Moms. R”relse. Mall (%3) och Moms. Prod. Mall (%4)\Denna kombination finns ej uppsatt i Bokf”ringsinst„llningar';
      EXF123@1100285022 : TextConst 'ENU=Accept of Potential unit of measure must be set to Yes.;SVE=Acceptens av m”jlig enhetskonflikt m†ste vara satt till Ja.';
      EXF124@1100285023 : TextConst 'ENU=Quantity must be less than or equal to %1, Line %2;SVE=Antal m†ste vara lika med eller mindre „n %1, Rad %2';
      EXF125@1100285000 : TextConst 'ENU=Dummy item %1 can not be used;SVE=L†tsas artikel %1 f†r ej anv„ndas';

    PROCEDURE CheckOrderMatch@1100285007(ExPurchDocLine@1100285001 : Record 12013588;ExFlowSetup@1100285000 : Record 12013601);
    BEGIN
      WITH ExPurchDocLine DO BEGIN
        //Check always
        IF ("Order No." <> '') AND ("Order Line No." = 0) THEN
          ERROR(EXF120);

        IF ("Order No." = '') AND ("Order No. (Import)" <> '') THEN
          ERROR(EXF118);

        IF ExFlowSetup."Disable Over Invoicing" AND ("Order Line No." <> 0) AND ("Document Type" = "Document Type"::Invoice) THEN
          CheckOverInvoicing(ExPurchDocLine,ExFlowSetup);

        IF "Receipt No. Mandatory" THEN BEGIN
          IF "Order No." = '' THEN
            ERROR(EXF117,"Order No. (Import)");

          IF "Outstanding Quantity (Order)" > 0 THEN
            ERROR(EXF001,"Order No.");
        END;

        IF "Receipt No. Mandatory" OR (ExFlowSetup."Order Applies-to" = ExFlowSetup."Order Applies-to"::"Receipts Lines") THEN
          CASE "Document Type" OF
            "Document Type"::Invoice :
              BEGIN
                IF ("Order No." <> '') AND ("Receipt No." = '') THEN
                  ERROR(EXF001, "Order No.");
              END;
            "Document Type"::"Credit Memo" :
              BEGIN
                IF ("Order No." <> '') AND ("Return Shipment No." = '') THEN
                  ERROR(EXF001, "Order No.");
              END;
            ELSE
              ERROR(EXF116);
          END;
      END;
    END;

    PROCEDURE CheckProposalExist@1100285002(ExfPurchDocLine@1100285002 : Record 12013588;ExPurchDocHeader@1100285006 : Record 12013587);
    VAR
      Proposals@1100285001 : Record 12013615;
      ExFlowSetup@1100285000 : Record 12013601;
      ExflowWorkflowMgt@1100285003 : Codeunit 12013593;
      CheckProposal@1100285004 : Boolean;
    BEGIN
      ExFlowSetup.GET;

      IF NOT ExfPurchDocLine.Approval THEN
        EXIT;

      IF NOT ExflowWorkflowMgt.CheckAutoApprovalAmt(ExfPurchDocLine) THEN BEGIN
        CheckProposal := TRUE;
        IF ExFlowSetup."Dont check Apps for PO-matched" THEN
          CheckProposal := NOT ExflowWorkflowMgt.CheckAutoApprovalOrder(ExfPurchDocLine,ExFlowSetup,ExPurchDocHeader);

        IF CheckProposal THEN BEGIN
          Proposals.RESET;
          Proposals.SETRANGE("Entry No.",ExfPurchDocLine."Inbound Document No.");
          Proposals.SETRANGE("Line No.",ExfPurchDocLine."Line No.");
          IF Proposals.ISEMPTY THEN
            ERROR(EXF100);
        END;
      END;
    END;

    PROCEDURE CheckAlways@1100285006(ExfPurchDocLine@1100285001 : Record 12013588);
    VAR
      Vend@1100285004 : Record 23;
      GenProductPostingGroup@1100285005 : Record 251;
      VATProductPostingGroup@1100285006 : Record 324;
      Account@1100285009 : Record 15;
      Item@1100285008 : Record 27;
      Fixed@1100285007 : Record 5600;
      PurchRcptLine@1100285013 : Record 121;
      LineAdvOptions@1100285014 : Record 12013596;
      ReturnShptLine@1100285000 : Record 6651;
      ExFlowSetup@1100285002 : Record 12013601;
    BEGIN
      ExFlowSetup.GET;
      Vend.GET(ExfPurchDocLine."Pay-to Vendor No.");

      // Check "Gen. Prod. Posting Group" and "VAT Prod. Posting Group"
      IF NOT GenProductPostingGroup.GET(ExfPurchDocLine."Gen. Prod. Posting Group") THEN
        ERROR(EXF104,
              ExfPurchDocLine."Gen. Prod. Posting Group",
              ExfPurchDocLine."Inbound Document No.",
              ExfPurchDocLine."Line No.");

      IF ExfPurchDocLine."VAT Prod. Posting Group" <> '' THEN
        IF NOT VATProductPostingGroup.GET(ExfPurchDocLine."VAT Prod. Posting Group") THEN
          ERROR(EXF105,
                ExfPurchDocLine."VAT Prod. Posting Group",
                ExfPurchDocLine."Inbound Document No.",
                ExfPurchDocLine."Line No.");

      // Check No.
      IF ExfPurchDocLine."No." <> '' THEN
        BEGIN
          CASE ExfPurchDocLine.Type OF
            ExfPurchDocLine.Type::"G/L Account" :
              BEGIN
                IF NOT Account.GET(ExfPurchDocLine."No.") THEN
                  ERROR(EXF106,ExfPurchDocLine."No.",ExfPurchDocLine."Inbound Document No.",ExfPurchDocLine."Line No.")
                ELSE
                  IF Account.Blocked THEN
                    ERROR(EXF107,ExfPurchDocLine."No.",ExfPurchDocLine."Inbound Document No.",ExfPurchDocLine."Line No.");
              END;

            ExfPurchDocLine.Type::Item :
              BEGIN
                IF NOT Item.GET(ExfPurchDocLine."No.") THEN
                  ERROR(EXF108,ExfPurchDocLine."No.",ExfPurchDocLine."Inbound Document No.",ExfPurchDocLine."Line No.")
                ELSE
                  IF Item.Blocked THEN
                    ERROR(EXF109,ExfPurchDocLine."No.",ExfPurchDocLine."Inbound Document No.",ExfPurchDocLine."Line No.");
              END;

            ExfPurchDocLine.Type::"Fixed Asset" :
              BEGIN
                IF NOT Fixed.GET(ExfPurchDocLine."No.") THEN
                  ERROR(EXF110,ExfPurchDocLine."No.",ExfPurchDocLine."Inbound Document No.",ExfPurchDocLine."Line No.")
                ELSE
                 IF Fixed.Blocked THEN
                   ERROR(EXF111,ExfPurchDocLine."No.",ExfPurchDocLine."Inbound Document No.",ExfPurchDocLine."Line No.");
              END;
          END;

          LineAdvOptions.RESET;
          LineAdvOptions.SETRANGE(Type, ExfPurchDocLine.Type);
          LineAdvOptions.SETFILTER("No.", '%1|%2', '', ExfPurchDocLine."No.");
          LineAdvOptions.SETFILTER("Minimum Amount", '%1|<=%2', 0, ExfPurchDocLine.Amount);
          LineAdvOptions.SETRANGE("Purchase Order Mandatory", TRUE);
          IF LineAdvOptions.FINDLAST THEN
            IF (ExfPurchDocLine."Order No." = '') THEN
              ERROR(EXF118);
        END;

      // Receipt
      IF ExfPurchDocLine."Receipt No." <> '' THEN
        BEGIN
          PurchRcptLine.GET(ExfPurchDocLine."Receipt No.",ExfPurchDocLine."Receipt Line No.");
          IF ExfPurchDocLine.Type <> PurchRcptLine.Type THEN
            ERROR(EXF113,
                  PurchRcptLine.Type,
                  ExfPurchDocLine."Inbound Document No.",
                  ExfPurchDocLine."Line No.");
          IF ExfPurchDocLine."No." <> PurchRcptLine."No." THEN
            ERROR(EXF114,
                  PurchRcptLine."No.",
                  ExfPurchDocLine."Inbound Document No.",
                  ExfPurchDocLine."Line No.");
          IF ExfPurchDocLine.Quantity > PurchRcptLine."Qty. Rcd. Not Invoiced" THEN
            ERROR(EXF115,
                  PurchRcptLine."Qty. Rcd. Not Invoiced",
                  ExfPurchDocLine."Inbound Document No.",
                  ExfPurchDocLine."Line No.");
        END;

      // Return Shipment
      IF ExfPurchDocLine."Return Shipment No." <> '' THEN
        BEGIN
          ReturnShptLine.GET(ExfPurchDocLine."Return Shipment No.",ExfPurchDocLine."Return Shipment Line No.");
          IF ExfPurchDocLine.Type <> ReturnShptLine.Type THEN
            ERROR(EXF113,
                  ReturnShptLine.Type,
                  ExfPurchDocLine."Inbound Document No.",
                  ExfPurchDocLine."Line No.");
          IF ExfPurchDocLine."No." <> ReturnShptLine."No." THEN
            ERROR(EXF114,
                  ReturnShptLine."No.",
                  ExfPurchDocLine."Inbound Document No.",
                  ExfPurchDocLine."Line No.");
          IF ExfPurchDocLine.Quantity > ReturnShptLine."Return Qty. Shipped Not Invd." THEN
            ERROR(EXF115,
                  ReturnShptLine."Return Qty. Shipped Not Invd.",
                  ExfPurchDocLine."Inbound Document No.",
                  ExfPurchDocLine."Line No.");
        END;

      IF ExFlowSetup."Dummy Item OCR Line" <> '' THEN
        IF ExfPurchDocLine.Type = ExfPurchDocLine.Type::Item THEN
          IF ExFlowSetup."Dummy Item OCR Line" = ExfPurchDocLine."No." THEN
            ERROR(EXF125,ExfPurchDocLine."No.");
    END;

    PROCEDURE CheckPostingSetup@1100285000(ExfPurchDocLine@1100285001 : Record 12013588);
    VAR
      GenPostingSetup@1100285002 : Record 252;
      VATPostingSetup@1100285000 : Record 325;
      Vend@1100285003 : Record 23;
    BEGIN
      IF ExfPurchDocLine.Type = 0 THEN
        EXIT;

      WITH ExfPurchDocLine DO BEGIN
        Vend.GET("Pay-to Vendor No.");

        TESTFIELD("Gen. Prod. Posting Group");
        IF NOT GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group") THEN
          ERROR(EXF121,
                "Inbound Document No.",
                "Line No.",
                "Gen. Bus. Posting Group",
                "Gen. Prod. Posting Group");

        TESTFIELD("VAT Prod. Posting Group");
        IF NOT VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group") THEN
          ERROR(EXF122,
                "Inbound Document No.",
                "Line No.",
                "VAT Bus. Posting Group",
                "VAT Prod. Posting Group");
      END;
    END;

    PROCEDURE CheckOverInvoicing@1100285001(ExfPurchDocLine@1100285000 : Record 12013588;ExFlowSetup@1100285004 : Record 12013601);
    VAR
      PurchOrderLine@1100285001 : Record 39;
      ExfPurchDocLine2@1100285003 : Record 12013588;
      TotQuant@1100285002 : Decimal;
    BEGIN
      ExfPurchDocLine2.RESET;
      ExfPurchDocLine2.SETRANGE("Inbound Document No.",ExfPurchDocLine."Inbound Document No.");
      ExfPurchDocLine2.SETRANGE("Order No.",ExfPurchDocLine."Order No.");
      ExfPurchDocLine2.SETRANGE("Order Line No.",ExfPurchDocLine."Order Line No.");
      IF ExfPurchDocLine2.FINDSET THEN
        REPEAT
          TotQuant := TotQuant + ExfPurchDocLine2.Quantity;
        UNTIL ExfPurchDocLine2.NEXT = 0;

      PurchOrderLine.GET(PurchOrderLine."Document Type"::Order,ExfPurchDocLine."Order No.",ExfPurchDocLine."Order Line No.");
      IF ExFlowSetup."Order Applies-to" = ExFlowSetup."Order Applies-to"::"Receipts Lines" THEN BEGIN
        IF TotQuant > PurchOrderLine."Qty. Rcd. Not Invoiced" THEN
          ERROR(EXF124,
                PurchOrderLine."Qty. Rcd. Not Invoiced",
                ExfPurchDocLine."Line No.");
      END
      ELSE
        IF ExFlowSetup."Order Applies-to" = ExFlowSetup."Order Applies-to"::"Purch Order Lines" THEN
          IF TotQuant > PurchOrderLine.Quantity THEN
            ERROR(EXF124,
                  PurchOrderLine.Quantity,
                  ExfPurchDocLine."Line No.");
    END;

    BEGIN
    END.
  }
}

