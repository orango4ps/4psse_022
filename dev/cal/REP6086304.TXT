OBJECT Report 6086304 Import Statement Transactions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW17.00.00.2.60;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Bankkontoabst.-Zeilen vorschl.;
               ENU=Suggest Bank Acc. Recon. Lines;
               NLD=Stel bankrekeningreconciliatieregels voor];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1   ;    ;DataItem;BankStatTrans       ;
               DataItemTable=Table6086330;
               DataItemTableView=SORTING(Bank Account Type,Bank Account No.,Bank Statement Transaction,Reconciled);
               OnPreDataItem=BEGIN
                               IF EndDate = 0D THEN
                                 ERROR(Text000);

                               BankAccReconLine.FilterBankRecLines(BankAccRecon);
                               IF NOT BankAccReconLine.FINDLAST THEN BEGIN
                                 BankAccReconLine."Bank Account Type" := BankAccRecon."Bank Account Type";
                                 BankAccReconLine."Bank Account No." := BankAccRecon."Bank Account No.";
                                 BankAccReconLine."Statement No." := BankAccRecon."Statement No.";
                                 BankAccReconLine."Statement Line No." := 0;
                               END;

                               BankStatTrans.RESET;
                               BankStatTrans.SETCURRENTKEY("Card No.","Posting Date");
                               BankStatTrans.SETRANGE("Bank Account Type",BankAccRecon."Bank Account Type");
                               BankStatTrans.SETRANGE("Bank Account No.",BankAccRecon."Bank Account No.");
                               BankStatTrans.SETRANGE("Posting Date",StartDate,EndDate);
                               BankStatTrans.SETRANGE("Bank Statement Transaction",TRUE);
                               BankStatTrans.SETFILTER("Entry No.",'>%1',LowestEntryNo);
                               BankStatTrans.SETRANGE(Reconciled,FALSE);
                             END;

               OnAfterGetRecord=BEGIN
                                  EnterBankTransactionLine(BankStatTrans);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             SVE=Alternativ] }

      { 5   ;2   ;Group     ;
                  CaptionML=[DEU=Auszug Periode;
                             ENU=Statement Period;
                             NLD=Afschriftperiode;
                             SVE=Rapportperiod] }

      { 1   ;3   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[DEU="";
                             ENU=Starting Date;
                             SVE=Startdatum];
                  SourceExpr=StartDate }

      { 3   ;3   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             SVE=Slutdatum];
                  SourceExpr=EndDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Geben Sie das Enddatum ein.;ENU=Enter the Ending Date.;NLD=Voer de einddatum in.';
      BankAccRecon@1004 : Record 6086374;
      BankAccReconLine@1005 : Record 6086375;
      BankAccSetStmtNo@1006 : Codeunit 6086393;
      LowestEntryNo@6086300 : Integer;
      StartDate@1008 : Date;
      EndDate@1009 : Date;

    PROCEDURE SetStmt@1(VAR BankAccRecon2@1000 : Record 6086374);
    BEGIN
      BankAccRecon := BankAccRecon2;
      EndDate := BankAccRecon."Statement Date";
    END;

    LOCAL PROCEDURE EnterBankTransactionLine@5(VAR BankTransaction@1000 : Record 6086330);
    BEGIN
      BankAccReconLine.INIT;
      BankAccReconLine."Statement Line No." := BankAccReconLine."Statement Line No." + 10000;
      BankAccReconLine."Transaction Date" := BankTransaction."Document Date";
      BankAccReconLine."Value Date" := BankTransaction."Document Date";
      BankAccReconLine.Description := COPYSTR(BankTransaction."Business Name",1,MAXSTRLEN(BankAccReconLine.Description));
      BankAccReconLine.VALIDATE("Statement Amount",BankTransaction."Bank-Currency Amount");
      BankAccReconLine.Type := BankAccReconLine.Type::"Bank Transaction Entry";
      BankAccReconLine."Bank Transaction Entry No." := BankTransaction."Entry No.";
      BankAccReconLine.INSERT;
    END;

    PROCEDURE SetLowestEntryNo@1160040000(EntryNo@1160040000 : Integer);
    BEGIN
      LowestEntryNo := EntryNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

