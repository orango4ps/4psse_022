OBJECT Table 11126148 Plant Maint. Proposal Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               PlantSetup.GET;
               PlantSetup.TESTFIELD("Service Company");
               IF PlantSetup."Service Company" <> CURRENTCOMPANY THEN
                 "Service Company" := PlantSetup."Service Company"
               ELSE
                 "Service Company" := '';
             END;

    OnModify=BEGIN
               TESTFIELD("Service Order No.", '');
             END;

    OnDelete=BEGIN
               TESTFIELD("Service Order No.", '');
             END;

    CaptionML=ENU=Plant Maintenance Proposal Line;
  }
  FIELDS
  {
    { 10  ;   ;Maintenance Proposal No.;Code20    ;TableRelation="Plant Maint. Proposal Header".No.;
                                                   CaptionML=ENU=Maintenance Proposal No.;
                                                   Editable=No }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              SVE=Radnr] }
    { 30  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type".Code;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Blocked (Plant Type)");
                                                                TESTFIELD("Blocked (Plant Type)", FALSE);
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Plant Type;
                                                              SVE=Maskintyp];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Plant Description", "Inspection No.", "Blocked (Plant No.)");
                                                                TESTFIELD("Blocked (Plant No.)", FALSE);
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Plant No.;
                                                              SVE=Maskinnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 41  ;   ;Plant Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=[ENU=Plant Description;
                                                              SVE=Maskinbeskrivning];
                                                   Editable=No }
    { 42  ;   ;Inspection No.      ;Code30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number"."Inspection No." WHERE (Plant Type=FIELD(Plant Type),
                                                                                                             No.=FIELD(Plant No.)));
                                                   CaptionML=[ENU=Inspection No.;
                                                              SVE=Inspektionsnr];
                                                   Editable=No }
    { 43  ;   ;Blocked (Plant Type);Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Blocked WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Blocked (Plant Type);
                                                              SVE=SpÑrrad (Maskintyp)];
                                                   Editable=No }
    { 44  ;   ;Blocked (Plant No.) ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Blocked WHERE (Plant Type=FIELD(Plant Type),
                                                                                                    No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Blocked (Plant No.);
                                                   Editable=No }
    { 50  ;   ;Maintenance Scheme  ;Code20        ;TableRelation="Plant Stnd. Maintenance Scheme";
                                                   OnValidate=VAR
                                                                PlantStndMaintenanceScheme@1100529000 : Record 11072673;
                                                              BEGIN
                                                                CALCFIELDS("Maintenance Scheme Description");
                                                                IF "Maintenance Scheme" <> '' THEN BEGIN
                                                                  PlantStndMaintenanceScheme.GET("Maintenance Scheme");
                                                                  IF PlantStndMaintenanceScheme."Order Kind" <> '' THEN
                                                                    "Order Kind" := PlantStndMaintenanceScheme."Order Kind";
                                                                  IF PlantStndMaintenanceScheme."Order Type" <> '' THEN
                                                                    "Order Type" := PlantStndMaintenanceScheme."Order Type";
                                                                  IF PlantStndMaintenanceScheme."Cost Component" <> '' THEN
                                                                    "Cost Component" := PlantStndMaintenanceScheme."Cost Component";
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Maintenance Scheme;
                                                              SVE=UnderhÜllsschema];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 51  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Stnd. Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme)));
                                                   CaptionML=[ENU=Maintenance Scheme Description;
                                                              SVE=Beskrivning av underhÜllsschema];
                                                   Editable=No }
    { 60  ;   ;Maint. Arrangement Line No.;Integer;TableRelation="Plant Maintenance Arrangement"."Line No." WHERE (Plant Type=FIELD(Plant Type),
                                                                                                                   Plant No.=FIELD(Plant No.),
                                                                                                                   Maintenance Scheme=FIELD(Maintenance Scheme));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Maint. Arrangement Line No.;
                                                   Editable=No }
    { 70  ;   ;Inspection Type     ;Code20        ;TableRelation="Plant Type Inspection Type"."Inspection Type" WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Inspection Type Description");
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Inspection Type;
                                                              SVE=Inspektionstyp];
                                                   Editable=No }
    { 71  ;   ;Inspection Type Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Inspection Type".Description WHERE (Code=FIELD(Inspection Type)));
                                                   CaptionML=ENU=Inspection Type Description;
                                                   Editable=No }
    { 80  ;   ;Interval Maintenance;DateFormula   ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Interval Maintenance;
                                                   Editable=No }
    { 90  ;   ;Meter               ;Code10        ;TableRelation="Plant Meter Type"."Plant Meter" WHERE (Plant Type=FIELD(Plant Type),
                                                                                                         Plant No.=FIELD(Plant No.));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Meter;
                                                   Editable=No }
    { 95  ;   ;Meter Reading Type  ;Code10        ;TableRelation="Plant Meter Type"."Plant Meter Type" WHERE (Plant Type=FIELD(Plant Type),
                                                                                                              Plant No.=FIELD(Plant No.),
                                                                                                              Plant Meter=FIELD(Meter));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Meter Reading Type;
                                                   Editable=No }
    { 96  ;   ;Unit of Measure (Meter);Code10     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Meter Reading Type"."Unit of Measure" WHERE (Code=FIELD(Meter Reading Type)));
                                                   CaptionML=ENU=Unit of Measure (Meter);
                                                   Editable=No }
    { 100 ;   ;Interval Meter Reading;Decimal     ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Interval Meter Reading;
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 110 ;   ;Specific Meter Reading;Decimal     ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Interval (Meter Reading);
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   Description=Total;
                                                   Editable=No }
    { 120 ;   ;Total Meter Reading Maint.;Decimal ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Total Meter Reading Maint.;
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 200 ;   ;Starting Date Maintenance;Date     ;OnValidate=VAR
                                                                DoConfirm@1100529000 : Boolean;
                                                              BEGIN
                                                                IF "Starting Date Maintenance" <> xRec."Starting Date Maintenance" THEN BEGIN
                                                                  "Ending Date Maintenance" := "Starting Date Maintenance";
                                                                  "Starting Time Maintenance" := 0T;
                                                                  "Ending Time Maintenance" := 0T;
                                                                END;
                                                                TestStartEndDateTimes;

                                                                "Execution Year" := DATE2DWY("Starting Date Maintenance",3);
                                                                "Execution Week" := DATE2DWY("Starting Date Maintenance",2);
                                                                "Execution Day" := DATE2DWY("Starting Date Maintenance",1) - 1;
                                                                IF "Starting Date Maintenance" = 0D THEN
                                                                  EXIT;
                                                                CASE "Maintenance Day" OF
                                                                  "Maintenance Day"::Monday.."Maintenance Day"::Sunday:
                                                                    DoConfirm := ("Execution Day" + 1 <> "Maintenance Day");
                                                                  "Maintenance Day"::Weekday:
                                                                    DoConfirm := ("Execution Day" IN ["Execution Day"::Saturday, "Execution Day"::Sunday]);
                                                                  "Maintenance Day"::Weekend:
                                                                    DoConfirm := ("Execution Day" IN ["Execution Day"::Monday.."Execution Day"::Friday]);
                                                                END;
                                                                IF DoConfirm THEN BEGIN
                                                                  IF NOT CONFIRM(Text000, FALSE, FIELDCAPTION("Starting Date Maintenance"), "Maintenance Day") THEN
                                                                    ERROR('');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Starting Date Maintenance;
                                                              SVE=Startdatum, underhÜll] }
    { 201 ;   ;Starting Time Maintenance;Time     ;OnValidate=VAR
                                                                StartingDateTime@1100529000 : DateTime;
                                                                EndingDateTime@1100528600 : DateTime;
                                                              BEGIN
                                                                IF "Starting Time Maintenance" <> 0T THEN BEGIN
                                                                  TESTFIELD("Starting Date Maintenance");
                                                                  StartingDateTime := CREATEDATETIME("Starting Date Maintenance", "Starting Time Maintenance");
                                                                  EndingDateTime := CREATEDATETIME("Ending Date Maintenance", "Ending Time Maintenance");
                                                                  IF EndingDateTime < StartingDateTime THEN BEGIN
                                                                    "Ending Date Maintenance" := DT2DATE(StartingDateTime);
                                                                    "Ending Time Maintenance" := DT2TIME(StartingDateTime);
                                                                  END;
                                                                END;
                                                                TestStartEndDateTimes;
                                                              END;

                                                   CaptionML=[ENU=Starting Time Maintenance;
                                                              SVE=Starttid fîr underhÜll] }
    { 205 ;   ;Ending Date Maintenance;Date       ;OnValidate=BEGIN
                                                                TestStartEndDateTimes;
                                                              END;

                                                   CaptionML=[ENU=Ending Date Maintenance;
                                                              SVE=Slutdatum fîr underhÜll] }
    { 206 ;   ;Ending Time Maintenance;Time       ;OnValidate=BEGIN
                                                                IF "Ending Time Maintenance" <> 0T THEN
                                                                  TESTFIELD("Ending Date Maintenance");
                                                                TestStartEndDateTimes;
                                                              END;

                                                   CaptionML=[ENU=Ending Time Maintenance;
                                                              SVE=Sluttid fîr underhÜll] }
    { 210 ;   ;Execution Year      ;Integer       ;CaptionML=[ENU=Year;
                                                              SVE=èr];
                                                   Editable=No }
    { 211 ;   ;Execution Week      ;Integer       ;CaptionML=[ENU=Week;
                                                              SVE=Vecka];
                                                   Editable=No }
    { 212 ;   ;Execution Day       ;Option        ;CaptionML=[ENU=Day;
                                                              SVE=Dag];
                                                   OptionCaptionML=[ENU=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday;
                                                                    SVE=MÜndag,Tisdag,Onsdag,Torsdag,Fredag,Lîrdag,Sîndag];
                                                   OptionString=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday;
                                                   Editable=No }
    { 220 ;   ;New Maintenance Date;Date          ;OnValidate=VAR
                                                                NewExecutionDay@1100526200 : Option;
                                                                DoConfirm@1100529000 : Boolean;
                                                              BEGIN
                                                                IF "New Maintenance Date" = 0D THEN BEGIN
                                                                  "New Theoretical Maint. Date" := 0D;
                                                                  EXIT;
                                                                END;
                                                                NewExecutionDay := DATE2DWY("New Maintenance Date",1) - 1;
                                                                CASE "Maintenance Day" OF
                                                                  "Maintenance Day"::Monday.."Maintenance Day"::Sunday:
                                                                    DoConfirm := (NewExecutionDay + 1 <> "Maintenance Day");
                                                                  "Maintenance Day"::Weekday:
                                                                    DoConfirm := (NewExecutionDay IN ["Execution Day"::Saturday, "Execution Day"::Sunday]);
                                                                  "Maintenance Day"::Weekend:
                                                                    DoConfirm :=  (NewExecutionDay IN ["Execution Day"::Monday.."Execution Day"::Friday]);
                                                                END;
                                                                IF DoConfirm THEN BEGIN
                                                                  IF NOT CONFIRM(Text000, FALSE, FIELDCAPTION("New Maintenance Date"), "Maintenance Day") THEN
                                                                    ERROR('');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=New Maintenance Date;
                                                              SVE=Nytt underhÜllsdatum] }
    { 230 ;   ;Theoretical Maintenance Date;Date  ;CaptionML=[ENU=Theoretical Maintenance Date;
                                                              SVE=Teoretiskt underhÜllsdatum];
                                                   Editable=No }
    { 235 ;   ;New Theoretical Maint. Date;Date   ;CaptionML=[ENU=New Theoretical Maintenance Date;
                                                              SVE=Nytt teoretiskt underhÜllsdatum];
                                                   Editable=No }
    { 240 ;   ;Maintenance Day     ;Option        ;CaptionML=[ENU=Maintenance Day;
                                                              SVE=UnderhÂllsdag];
                                                   OptionCaptionML=[ENU=" ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Weekday,Weekend";
                                                                    SVE=" ,MÂndag,Tisdag,Onsdag,Torsdag,Fredag,Lˆrdag,Sˆndag,Veckodag,Helg"];
                                                   OptionString=[ ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Weekday,Weekend] }
    { 250 ;   ;Firm Planned        ;Boolean       ;CaptionML=[ENU=Firm Planned;
                                                              SVE=Fast planerad] }
    { 260 ;   ;Cancelled           ;Boolean       ;OnValidate=VAR
                                                                PlantMaintenanceArrangement@1100528601 : Record 11126146;
                                                              BEGIN
                                                                TESTFIELD("Service Order No.", '');
                                                                IF Cancelled <> xRec.Cancelled THEN BEGIN
                                                                  PlantMaintenanceArrangement.SETRANGE("Plant Type", "Plant Type");
                                                                  PlantMaintenanceArrangement.SETRANGE("Plant No.", "Plant No.");
                                                                  PlantMaintenanceArrangement.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
                                                                  PlantMaintenanceArrangement.SETRANGE("Line No.", "Maint. Arrangement Line No.");
                                                                  IF NOT Cancelled THEN BEGIN
                                                                    IF PlantMaintenanceArrangement.FINDFIRST THEN
                                                                      PlantMaintenanceArrangement.TESTFIELD(Cancelled, FALSE)
                                                                    ELSE
                                                                      ERROR(Text002, PlantMaintenanceArrangement.TABLECAPTION);
                                                                  END ELSE BEGIN
                                                                    IF PlantMaintenanceArrangement.FINDFIRST THEN BEGIN
                                                                      IF NOT PlantMaintenanceArrangement.Cancelled THEN BEGIN
                                                                        PlantMaintenanceArrangement.Cancelled := TRUE;  // No validate
                                                                        PlantMaintenanceArrangement.MODIFY;
                                                                      END;
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Cancelled;
                                                              SVE=Annullerad] }
    { 270 ;   ;Comment             ;Text100       ;CaptionML=[ENU=Comment;
                                                              SVE=Kommentar] }
    { 300 ;   ;Resource Type       ;Option        ;OnValidate=BEGIN
                                                                IF "Resource Type" <> xRec."Resource Type" THEN BEGIN
                                                                  "Resource No." := '';
                                                                  CALCFIELDS("Resource Name");
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Resource Type;
                                                              SVE=Resurstyp];
                                                   OptionCaptionML=[ENU=" ,Employee,Subcontractor";
                                                                    SVE=" ,AnstÑlld,Underentreprenîr"];
                                                   OptionString=[ ,Employee,Subcontractor] }
    { 310 ;   ;Resource No.        ;Code20        ;TableRelation="Resource (WOP)" WHERE (Type=FIELD(Resource Type));
                                                   OnValidate=VAR
                                                                ResourceMgtWorkOrderPlan@1100528600 : Codeunit 11229277;
                                                              BEGIN
                                                                IF "Service Company" = '' THEN
                                                                  ResourceMgtWorkOrderPlan.TestResourceWithinCompanyPlanGroup("Resource No.");
                                                                CALCFIELDS("Resource Name");
                                                              END;

                                                   OnLookup=VAR
                                                              ResourceWOP@1100529000 : Record 11229278;
                                                              ResourceCompany@1100529001 : Text;
                                                            BEGIN
                                                              TESTFIELD("Resource Type");
                                                              ResourceCompany := COMPANYNAME;
                                                              IF "Service Company" <> '' THEN
                                                                ResourceCompany := "Service Company";
                                                              ResourceWOP.SETCURRENTKEY(Type, Company, "No.");
                                                              ResourceWOP.FILTERGROUP(7);
                                                              ResourceWOP.SETRANGE(Type, "Resource Type");
                                                              ResourceWOP.FILTERGROUP(0);
                                                              ResourceWOP.SETRANGE(Company, ResourceCompany);
                                                              ResourceWOP.Type := "Resource Type";
                                                              ResourceWOP.Company := ResourceCompany;
                                                              ResourceWOP."No." := "Resource No.";
                                                              IF "Resource No." <> '' THEN BEGIN
                                                                IF ResourceWOP.GET("Resource No.") THEN BEGIN
                                                                  IF ResourceWOP.Company <> ResourceCompany THEN
                                                                    ResourceWOP.SETFILTER(Company, '%1|%2', ResourceCompany, ResourceWOP.Company);
                                                                END;
                                                              END;
                                                              IF PAGE.RUNMODAL(0, ResourceWOP) = ACTION::LookupOK THEN BEGIN
                                                                IF ("Service Order No." = '') AND (ResourceWOP."No." <> '')  AND (ResourceWOP."No." <> "Resource No.") THEN
                                                                  VALIDATE("Resource No.", ResourceWOP."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Resource;
                                                              SVE=Resurs] }
    { 311 ;   ;Resource Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Name WHERE (No.=FIELD(Resource No.)));
                                                   CaptionML=[ENU=Resource Name;
                                                              SVE=Resursnamn];
                                                   Editable=No }
    { 320 ;   ;Service Order Description;Text50   ;CaptionML=[ENU=Service Order Description;
                                                              SVE=Serviceorder beskrivning] }
    { 330 ;   ;Service Company     ;Text30        ;TableRelation=Company;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Service Company;
                                                   Editable=No }
    { 340 ;   ;Order Kind          ;Code10        ;TableRelation=IF (Service Company=CONST()) "Service Order Indication".Code WHERE (Indication=CONST(Kind));
                                                   OnValidate=BEGIN
                                                                CheckOrderIndication(0, "Order Kind");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupOrderIndication(0, "Order Kind");
                                                            END;

                                                   CaptionML=[ENU=Order Kind;
                                                              SVE=Typ av order] }
    { 350 ;   ;Order Type          ;Code10        ;TableRelation=IF (Service Company=CONST()) "Service Order Indication".Code WHERE (Indication=CONST(Type));
                                                   OnValidate=BEGIN
                                                                CheckOrderIndication(1, "Order Type");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupOrderIndication(1, "Order Type");
                                                            END;

                                                   CaptionML=[ENU=Order Type;
                                                              SVE=Ordertyp] }
    { 360 ;   ;Service Order No.   ;Code20        ;TableRelation=IF (Service Company=CONST()) "Service Order";
                                                   OnLookup=VAR
                                                              ServiceOrder@1100529000 : Record 11012823;
                                                            BEGIN
                                                              IF ("Service Company" <> '') OR ("Service Order No." = '') THEN
                                                                EXIT;
                                                              IF ServiceOrder.GET("Service Order No.") THEN
                                                                PAGE.RUN(PAGE::"Service-Order List", ServiceOrder);
                                                            END;

                                                   CaptionML=[ENU=Service Order No.;
                                                              SVE=Serviceordernr];
                                                   Editable=No }
    { 370 ;   ;Cost Component      ;Code10        ;TableRelation="Cost Component Plant".Code;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Cost Component Description");
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Cost Component;
                                                              SVE=Kostnadsdel] }
    { 371 ;   ;Cost Component Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cost Component Plant".Description WHERE (Code=FIELD(Cost Component)));
                                                   CaptionML=[ENU=Cost Component Description;
                                                              SVE=Kostnadsdel, beskrivning];
                                                   Editable=Yes }
  }
  KEYS
  {
    {    ;Maintenance Proposal No.,Line No.       ;Clustered=Yes }
    {    ;Plant Type,Plant No.,Maintenance Scheme,Maint. Arrangement Line No. }
    {    ;Service Order No.                        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100526200 : TextConst 'ENU=%1 is not a %2, continue?;SVE=%1 Ñr inte ett %2, fortsÑtta?';
      Text001@1100528601 : TextConst 'ENU=%1 and %2 must occur before %3 en %4;SVE=%1 och %2 mÜste intrÑffa fîre %3 och %4';
      Text002@1100528602 : TextConst 'ENU=No %1 exists for this Proposal Line';
      PlantSetup@1100529000 : Record 11012550;

    LOCAL PROCEDURE CheckOrderIndication@1100529001(IndicationType@1100529001 : Option;IndicationCode@1100529002 : Code[10]);
    VAR
      ServiceOrderIndication@1100529000 : Record 11012837;
      CostComponentPlant@1100529003 : Record 11012575;
    BEGIN
      IF IndicationCode <> '' THEN BEGIN
        IF "Service Company" <> '' THEN
          ServiceOrderIndication.CHANGECOMPANY("Service Company");
        ServiceOrderIndication.GET(IndicationType, IndicationCode);
        IF ("Order Kind" <> xRec."Order Kind") AND (ServiceOrderIndication."Cost Component Plant" <> '') AND
           (IndicationType = ServiceOrderIndication.Indication::Kind )
        THEN BEGIN
          IF CostComponentPlant.GET(ServiceOrderIndication."Cost Component Plant") THEN
            VALIDATE("Cost Component", ServiceOrderIndication."Cost Component Plant");
        END;
      END;
    END;

    LOCAL PROCEDURE LookupOrderIndication@1100529000(IndicationType@1100529001 : Option;IndicationCode@1100529002 : Code[10]);
    VAR
      ServiceOrderIndication@1100529000 : Record 11012837;
    BEGIN
      IF "Service Company" <> '' THEN
        ServiceOrderIndication.CHANGECOMPANY("Service Company");
      ServiceOrderIndication.FILTERGROUP(7);
      ServiceOrderIndication.SETRANGE(Indication, IndicationType);
      ServiceOrderIndication.FILTERGROUP(0);
      ServiceOrderIndication.Indication := IndicationType;
      ServiceOrderIndication.Code := IndicationCode;
      IF "Service Order No." <> '' THEN
        PAGE.RUNMODAL(0, ServiceOrderIndication)
      ELSE BEGIN
        IF PAGE.RUNMODAL(0, ServiceOrderIndication) = ACTION::LookupOK THEN BEGIN
          IF (ServiceOrderIndication.Code <> '') AND (ServiceOrderIndication.Code <> IndicationCode) THEN BEGIN
            CASE IndicationType OF
              ServiceOrderIndication.Indication::Kind:
                VALIDATE("Order Kind", ServiceOrderIndication.Code);
              ServiceOrderIndication.Indication::Type:
                VALIDATE("Order Type", ServiceOrderIndication.Code);
            END;
          END;
        END;
      END;
    END;

    PROCEDURE ReplaceMaintenanceDate@1100526202(VAR PlantMaintProposalLine2@1100529000 : Record 11126148);
    VAR
      PlantMaintProposalLine@1100529001 : Record 11126148;
    BEGIN
      PlantMaintProposalLine.COPY(PlantMaintProposalLine2);
      PlantMaintProposalLine.FILTERGROUP(11);
      PlantMaintProposalLine.SETRANGE("Service Order No.", '');
      PlantMaintProposalLine.FILTERGROUP(0);
      PlantMaintProposalLine.SETFILTER("New Maintenance Date", '<>%1', 0D);
      IF PlantMaintProposalLine.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          PlantMaintProposalLine.VALIDATE("Starting Date Maintenance", PlantMaintProposalLine."New Maintenance Date");
          PlantMaintProposalLine.VALIDATE("Ending Date Maintenance", PlantMaintProposalLine."New Maintenance Date");
          IF PlantMaintProposalLine."New Theoretical Maint. Date" <> 0D THEN
            PlantMaintProposalLine."Theoretical Maintenance Date" := PlantMaintProposalLine."New Theoretical Maint. Date";
          PlantMaintProposalLine."New Maintenance Date" := 0D;
          PlantMaintProposalLine."New Theoretical Maint. Date" := 0D;
          PlantMaintProposalLine.MODIFY(TRUE);
        UNTIL PlantMaintProposalLine.NEXT = 0;
      END;
    END;

    PROCEDURE TestStartEndDateTimes@1100528600();
    VAR
      StartDateTime@1100528600 : DateTime;
      EndDateTime@1100528601 : DateTime;
    BEGIN
      IF ("Starting Date Maintenance" = 0D) AND ("Ending Date Maintenance" <> 0D) THEN
        TESTFIELD("Starting Date Maintenance");

      StartDateTime := CREATEDATETIME("Starting Date Maintenance", "Starting Time Maintenance");
      EndDateTime := CREATEDATETIME("Ending Date Maintenance", "Ending Time Maintenance");

      IF StartDateTime > EndDateTime THEN
        ERROR(Text001, FIELDCAPTION("Starting Date Maintenance"), FIELDCAPTION("Starting Time Maintenance"),
          FIELDCAPTION("Ending Date Maintenance"), FIELDCAPTION("Ending Time Maintenance"));
    END;

    PROCEDURE CalcNewMaintenanceDates@1100526201(VAR NewTheoreticalMaintDate@1100529000 : Date;VAR NewMaintenanceDate@1100526200 : Date;DateFormula@1100526201 : DateFormula;MaintenanceDay@1100526202 : ' ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Weekday,Weekend');
    VAR
      DayNo@1100526205 : Integer;
    BEGIN
      NewMaintenanceDate := NewTheoreticalMaintDate;
      IF NewTheoreticalMaintDate = 0D THEN
        EXIT;
      IF FORMAT(DateFormula) <> '' THEN BEGIN
        NewTheoreticalMaintDate := CALCDATE(DateFormula, NewTheoreticalMaintDate);
        NewMaintenanceDate := NewTheoreticalMaintDate;
      END;
      IF MaintenanceDay = MaintenanceDay::" " THEN
        EXIT;
      DayNo := DATE2DWY(NewMaintenanceDate, 1);
      CASE MaintenanceDay OF
        MaintenanceDay::Monday..MaintenanceDay::Sunday:
          BEGIN
            IF MaintenanceDay - DayNo > 3 THEN
              NewMaintenanceDate := CALCDATE('<-1W>', NewMaintenanceDate);
            IF MaintenanceDay - DayNo < -3 THEN
              NewMaintenanceDate := CALCDATE('<+1W>', NewMaintenanceDate);
          END;
        MaintenanceDay::Weekday:
          BEGIN
            MaintenanceDay := DayNo;
            IF DayNo = 6 THEN
              MaintenanceDay := 5;
            IF DayNo = 7 THEN BEGIN
              MaintenanceDay := 1;
              NewMaintenanceDate := CALCDATE('<+1W>', NewMaintenanceDate);
            END;
          END;
        MaintenanceDay::Weekend:
          BEGIN
            MaintenanceDay := DayNo;
            IF DayNo IN [1..3] THEN BEGIN
              NewMaintenanceDate := CALCDATE('<-1W>', NewMaintenanceDate);
              MaintenanceDay := 7;
            END;
            IF DayNo IN [4..5] THEN BEGIN
              MaintenanceDay := 6;
            END;
          END;
      END;
      NewMaintenanceDate := DWY2DATE(MaintenanceDay, DATE2DWY(NewMaintenanceDate, 2), DATE2DWY(NewMaintenanceDate, 3));
    END;

    PROCEDURE DrillDownServiceOrder@1100529002(CardMode@1100529000 : Boolean);
    VAR
      ServiceOrder@1100529001 : Record 11012823;
    BEGIN
      IF "Service Company" <> '' THEN
        EXIT;
      ServiceOrder.SETRANGE("No.", "Service Order No.");
      IF CardMode AND ("Service Order No." <> '') THEN BEGIN
        IF ServiceOrder.GET("Service Order No.") THEN BEGIN
          PAGE.RUNMODAL(PAGE::"Service Order (General)", ServiceOrder);
          EXIT;
        END;
      END;
      PAGE.RUNMODAL(0, ServiceOrder);
    END;

    BEGIN
    {
      Copied from service, Table 'Maintenance Proposal Line' (T11071696).
    }
    END.
  }
}

