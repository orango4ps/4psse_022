OBJECT Codeunit 5507 Graph Mgt - Sales Credit Memo
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 114=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE ProcessComplexTypes@1(VAR SalesCrMemoEntityBuffer@1000 : Record 5507;SellToAddressJSON@1001 : Text);
    BEGIN
      ParseSellToCustomerAddressFromJSON(SellToAddressJSON,SalesCrMemoEntityBuffer);
    END;

    [External]
    PROCEDURE ParseSellToCustomerAddressFromJSON@4(SellToAddressJSON@1000 : Text;VAR SalesCrMemoEntityBuffer@1002 : Record 5507);
    VAR
      GraphMgtComplexTypes@1003 : Codeunit 5468;
      RecRef@1001 : RecordRef;
    BEGIN
      IF SellToAddressJSON <> '' THEN
        WITH SalesCrMemoEntityBuffer DO BEGIN
          RecRef.GETTABLE(SalesCrMemoEntityBuffer);
          GraphMgtComplexTypes.ApplyPostalAddressFromJSON(SellToAddressJSON,RecRef,
            FIELDNO("Sell-to Address"),FIELDNO("Sell-to Address 2"),FIELDNO("Sell-to City"),FIELDNO("Sell-to County"),
            FIELDNO("Sell-to Country/Region Code"),FIELDNO("Sell-to Post Code"));
          RecRef.SETTABLE(SalesCrMemoEntityBuffer);
        END;
    END;

    [External]
    PROCEDURE ParseBillToCustomerAddressFromJSON@10(BillToAddressJSON@1000 : Text;VAR SalesCrMemoEntityBuffer@1002 : Record 5507);
    VAR
      GraphMgtComplexTypes@1003 : Codeunit 5468;
      RecRef@1001 : RecordRef;
    BEGIN
      IF BillToAddressJSON <> '' THEN
        WITH SalesCrMemoEntityBuffer DO BEGIN
          RecRef.GETTABLE(SalesCrMemoEntityBuffer);
          GraphMgtComplexTypes.ApplyPostalAddressFromJSON(BillToAddressJSON,RecRef,
            FIELDNO("Bill-to Address"),FIELDNO("Bill-to Address 2"),FIELDNO("Bill-to City"),FIELDNO("Bill-to County"),
            FIELDNO("Bill-to Country/Region Code"),FIELDNO("Bill-to Post Code"));
          RecRef.SETTABLE(SalesCrMemoEntityBuffer);
        END;
    END;

    [External]
    PROCEDURE SellToCustomerAddressToJSON@5(SalesCrMemoEntityBuffer@1000 : Record 5507) JSON : Text;
    VAR
      GraphMgtComplexTypes@1001 : Codeunit 5468;
    BEGIN
      WITH SalesCrMemoEntityBuffer DO
        GraphMgtComplexTypes.GetPostalAddressJSON("Sell-to Address","Sell-to Address 2",
          "Sell-to City","Sell-to County","Sell-to Country/Region Code","Sell-to Post Code",JSON);
    END;

    [External]
    PROCEDURE BillToCustomerAddressToJSON@7(SalesCrMemoEntityBuffer@1000 : Record 5507) JSON : Text;
    VAR
      GraphMgtComplexTypes@1001 : Codeunit 5468;
    BEGIN
      WITH SalesCrMemoEntityBuffer DO
        GraphMgtComplexTypes.GetPostalAddressJSON("Bill-to Address","Bill-to Address 2",
          "Bill-to City","Bill-to County","Bill-to Country/Region Code","Bill-to Post Code",JSON);
    END;

    [External]
    PROCEDURE UpdateIntegrationRecordIds@34(OnlyRecordsWithoutID@1007 : Boolean);
    VAR
      DummySalesCrMemoEntityBuffer@1001 : Record 5507;
      DummyCustomer@1005 : Record 18;
      GraphMgtGeneralTools@1008 : Codeunit 5465;
      SalesHeaderRecordRef@1000 : RecordRef;
      SalesCrMemoHeaderRecordRef@1002 : RecordRef;
      CustomerRecordRef@1004 : RecordRef;
    BEGIN
      CustomerRecordRef.OPEN(DATABASE::Customer);
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        CustomerRecordRef,DummyCustomer.FIELDNO(Id),TRUE);

      SalesHeaderRecordRef.OPEN(DATABASE::"Sales Header");
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        SalesHeaderRecordRef,DummySalesCrMemoEntityBuffer.FIELDNO(Id),OnlyRecordsWithoutID);

      SalesCrMemoHeaderRecordRef.OPEN(DATABASE::"Sales Cr.Memo Header");
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        SalesCrMemoHeaderRecordRef,DummySalesCrMemoEntityBuffer.FIELDNO(Id),OnlyRecordsWithoutID);
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    PROCEDURE HandleApiSetup@2();
    VAR
      GraphMgtSalCrMemoBuf@1001 : Codeunit 5508;
    BEGIN
      UpdateIntegrationRecordIds(FALSE);
      GraphMgtSalCrMemoBuf.UpdateBufferTableRecords;
    END;

    BEGIN
    END.
  }
}

