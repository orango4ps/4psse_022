OBJECT Table 11124908 Resource Request Item Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetRequestHeader();
               ResourceRequest.TESTFIELD(Status, ResourceRequest.Status::Open);
               Posted := FALSE;
               "Plant Order No" := '';
               "Collective List No.":= '';
             END;

    OnModify=BEGIN
               GetRequestHeader();
               ResourceRequest.TESTFIELD(Status, ResourceRequest.Status::Open);
               TESTFIELD(Posted, FALSE);
             END;

    OnDelete=BEGIN
               GetRequestHeader();
               ResourceRequest.TESTFIELD(Status, ResourceRequest.Status::Open);
               TESTFIELD(Posted, FALSE);
             END;

    CaptionML=ENU=Resource Request Item Line;
  }
  FIELDS
  {
    { 10  ;   ;Request No.         ;Code20        ;TableRelation="Resource Request";
                                                   CaptionML=[ENU=Request No.;
                                                              NOR=Foresp›rselnr.;
                                                              SVE=Nr f”r f”rfr†gan];
                                                   NotBlank=Yes }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 40  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=VAR
                                                                Item@1100528301 : Record 27;
                                                                UserSetupMgt@1100528300 : Codeunit 5700;
                                                              BEGIN
                                                                IF ("Item No." = '') AND ("Item No." <> xRec."Item No.") THEN
                                                                  "Cost Object" := '';
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  Item.GET("Item No.");
                                                                  IF ("Item No." <> xRec."Item No.") THEN BEGIN
                                                                    IF "Location Code" = '' THEN
                                                                      "Location Code" := UserSetupMgt.GetLocation(0,'',UserSetupMgt.GetSalesFilter);
                                                                    Description := Item.Description;
                                                                    "Description 2" := Item."Description 2";
                                                                    "Cost Object" := Item."Global Dimension 2 Code";
                                                                    VALIDATE("Unit of Measure", Item."Sales Unit of Measure");
                                                                    GetUnitPrice(FIELDNO("Item No."));
                                                                    VALIDATE("Unit Price");
                                                                    CheckInventoryWarning;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr] }
    { 45  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=CONST(Material));
                                                   OnValidate=VAR
                                                                DimensionValue@1100527200 : Record 349;
                                                                DimensionMgt@1100527201 : Codeunit 408;
                                                              BEGIN
                                                                IF ("Item No." = '') AND ("Cost Object" <> '') THEN BEGIN
                                                                  DimensionMgt.GetDimValueRec(2, "Cost Object", DimensionValue, TRUE, '');
                                                                  IF ("Cost Object" <> xRec."Cost Object") THEN BEGIN
                                                                    "Location Code" := '';
                                                                    Description := DimensionValue.Name;
                                                                    "Description 2" := '';
                                                                    VALIDATE("Unit of Measure", DimensionValue."Unit of Measure");
                                                                    VALIDATE("Unit Price", DimensionValue."Sales Price");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   CaptionClass='1,1,2' }
    { 50  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 51  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 60  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  IF "Location Code" <> xRec."Location Code" THEN BEGIN
                                                                    GetUnitPrice(FIELDNO("Location Code"));
                                                                    VALIDATE("Unit Price");
                                                                    CheckInventoryWarning;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Plasseringkode;
                                                              SVE=Lagerst„llekod] }
    { 70  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=VAR
                                                                Item@1100528301 : Record 27;
                                                                UnitOfMeasureMgt@1100528300 : Codeunit 5402;
                                                              BEGIN
                                                                IF "Item No." = '' THEN
                                                                  "Qty. per Unit of Measure" := 1
                                                                ELSE BEGIN
                                                                  Item.GET("Item No.");
                                                                  "Qty. per Unit of Measure" := UnitOfMeasureMgt.GetQtyPerUnitOfMeasure(Item, "Unit of Measure");
                                                                  IF ("Unit of Measure" <> xRec."Unit of Measure") AND (CurrFieldNo <> FIELDNO("Item No.")) THEN BEGIN
                                                                    GetUnitPrice(FIELDNO("Unit of Measure"));
                                                                    VALIDATE("Unit Price");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=M†ttenhet] }
    { 75  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              NOR=Antall per M†leenhet;
                                                              SVE=Antal per enhet];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 80  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  IF Quantity <> xRec.Quantity THEN
                                                                    GetUnitPrice(FIELDNO(Quantity));
                                                                  CheckInventoryWarning;
                                                                END;
                                                                VALIDATE("Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 90  ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                Amount := ROUND(Quantity * "Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              NOR=Pris;
                                                              SVE=Pris];
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 100 ;   ;Amount              ;Decimal       ;OnValidate=VAR
                                                                GLSetup@1100528300 : Record 98;
                                                              BEGIN
                                                                IF (Amount <> 0) THEN
                                                                  TESTFIELD(Quantity);
                                                                IF Quantity <> 0 THEN BEGIN
                                                                  "Unit Price" := Amount / Quantity
                                                                END ELSE
                                                                  "Unit Price" := Amount;
                                                                GLSetup.GET;
                                                                "Unit Price" := ROUND("Unit Price",GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 110 ;   ;Posted              ;Boolean       ;CaptionML=[ENU=Posted;
                                                              NOR=Bokf›rt;
                                                              SVE=Bokf”rd];
                                                   Editable=No }
    { 111 ;   ;Plant Order No      ;Code20        ;TableRelation="Plant Order";
                                                   OnLookup=VAR
                                                              PlantOrder@1100528300 : Record 11012556;
                                                            BEGIN
                                                              IF "Plant Order No" <> '' THEN BEGIN
                                                                IF PlantOrder.GET("Plant Order No") THEN BEGIN
                                                                  IF NOT PlantOrder.Posted THEN
                                                                    PAGE.RUNMODAL(0, PlantOrder)
                                                                  ELSE
                                                                    PAGE.RUNMODAL(PAGE::"Posted Plant Order List", PlantOrder);
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Plant Order No.;
                                                              NOR=Maskinordrenummer;
                                                              SVE=Maskinordernummer];
                                                   Editable=No }
    { 120 ;   ;Collective List No. ;Code20        ;TableRelation="Resource Req. Collective-List";
                                                   CaptionML=[ENU=Collective List No.;
                                                              NOR=Samlingsfaktura nr.;
                                                              SVE=Samlingsfaktura nr.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Request No.,Line No.                    ;SumIndexFields=Amount,Quantity;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Collective List No.,Request No.          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ResourceRequest@11012002 : Record 11124901;
      Text001@1100525002 : TextConst 'ENU=%1 (%2) is greater than inventory (%3), %4 ''%5'' %6 ''%7''.';

    PROCEDURE GetRequestHeader@5();
    BEGIN
      ResourceRequest.GET("Request No.");
    END;

    PROCEDURE GetUnitPrice@1100485000(CurrFieldNo@1100485001 : Integer);
    VAR
      ItemJournalLine@1100485000 : Record 83;
      Customer@1100485008 : Record 18;
      SalesPriceCalcMgtCU@1100528300 : Codeunit 7000;
      ItemJnlFieldNo@1100485003 : Integer;
    BEGIN
      IF "Item No." = '' THEN
        EXIT;
      GetRequestHeader();
      IF ResourceRequest."Source Type" = ResourceRequest."Source Type"::Customer THEN BEGIN
        IF NOT Customer.GET(ResourceRequest."Source No.") THEN
          Customer.INIT;
      END;
      ItemJournalLine."Item No." := "Item No.";
      ItemJournalLine."Location Code" := "Location Code";
      ItemJournalLine."Posting Date" :=TODAY;
      ItemJournalLine.Quantity := ABS(Quantity);
      ItemJournalLine."Unit Amount" := "Unit Price";
      ItemJournalLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
      ItemJournalLine."Unit of Measure Code" := "Unit of Measure";
      CASE CurrFieldNo OF
        FIELDNO("Item No."):
          ItemJnlFieldNo := ItemJournalLine.FIELDNO("Item No.");
        FIELDNO("Location Code"):
          ItemJnlFieldNo := ItemJournalLine.FIELDNO("Location Code");
        FIELDNO("Unit of Measure"):
          ItemJnlFieldNo := ItemJournalLine.FIELDNO("Unit of Measure Code");
        FIELDNO(Quantity):
          ItemJnlFieldNo := ItemJournalLine.FIELDNO(Quantity);
      END;
      SalesPriceCalcMgtCU.FindPlantOrderItemLinePrice(ItemJournalLine, ItemJnlFieldNo, Customer."No.", Customer."Customer Price Group");
      "Unit Price" := ItemJournalLine."Unit Amount";
    END;

    PROCEDURE GetInventory@1100525001(ItemInventByLocation@1100525000 : Boolean) : Decimal;
    VAR
      Item@1100525003 : Record 27;
    BEGIN
      IF "Item No." = '' THEN
        EXIT(0);
      IF NOT Item.GET("Item No.") THEN
        EXIT(0);
      IF ItemInventByLocation THEN
        Item.SETRANGE("Location Filter", "Location Code");
      Item.CALCFIELDS(Inventory);
      IF ("Qty. per Unit of Measure" <> 0) THEN
        Item.Inventory := ROUND(Item.Inventory / "Qty. per Unit of Measure", 0.00001);
      EXIT(Item.Inventory);
    END;

    PROCEDURE CheckInventoryWarning@1100525002();
    VAR
      InventQty@1100525000 : Decimal;
    BEGIN
      IF (Quantity <= 0) OR ("Item No." = '') THEN
        EXIT;
      InventQty := GetInventory(TRUE);
      IF (Quantity > InventQty) THEN BEGIN
        MESSAGE(Text001, FIELDCAPTION(Quantity), Quantity, InventQty,
          FIELDCAPTION("Item No."), "Item No.", FIELDCAPTION("Location Code"), "Location Code");
      END;
    END;

    PROCEDURE SetLineNo@1100527200();
    VAR
      ResourceRequestItemLine@1100527200 : Record 11124908;
    BEGIN
      IF ResourceRequestItemLine.FINDLAST THEN
        "Line No." := ResourceRequestItemLine."Line No." + 1
      ELSE
        "Line No." := 1;
    END;

    BEGIN
    END.
  }
}

