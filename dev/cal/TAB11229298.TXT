OBJECT Table 11229298 Web Service Set
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD("Is Standard", FALSE);
             END;

    OnModify=BEGIN
               TESTFIELD("Is Standard", FALSE);
             END;

    OnDelete=VAR
               WebServiceSetLine@1100525000 : Record 11229309;
             BEGIN
               TESTFIELD("Is Standard", FALSE);
               WebServiceSetLine.SETRANGE("Web Service Set Code", Code);
               WebServiceSetLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=ENU=Web Service Set;
    LookupPageID=Page11126625;
    DrillDownPageID=Page11126625;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Is Standard         ;Boolean       ;CaptionML=ENU=Is Standard;
                                                   Editable=No }
    { 40  ;   ;Web Service Lines   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Service Set Line" WHERE (Web Service Set Code=FIELD(Code)));
                                                   CaptionML=ENU=Web Service Lines;
                                                   Editable=No }
    { 41  ;   ;Not Published Lines ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Service Set Line" WHERE (Web Service Set Code=FIELD(Code),
                                                                                                   Published=CONST(No)));
                                                   CaptionML=ENU=Not Published Lines;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE TogglePublishWebService@1100525000(VAR WebServiceSet@1100525001 : Record 11229298;Publish@1100525005 : Boolean);
    VAR
      WebServiceSetLine@1100525002 : Record 11229309;
      PossibleWebService@1100525004 : Record 11229317;
      WebService@1100525003 : Record 2000000076;
      TenantWebService@1100527400 : Record 2000000168;
      WebServiceManagement@1100527401 : Codeunit 9750;
      PermissionManager@1100527402 : Codeunit 9002;
    BEGIN
      IF (NOT WebServiceSet.FINDSET) THEN
        EXIT;
      REPEAT
        WebServiceSetLine.SETRANGE("Web Service Set Code", WebServiceSet.Code);
        WebServiceSetLine.SETFILTER("Service Name", '<>%1', '');
        IF (WebServiceSetLine.FINDSET) THEN BEGIN
          REPEAT
            IF (NOT Publish) THEN BEGIN
              IF (IsLastService(WebServiceSetLine)) THEN BEGIN
                IF PermissionManager.SoftwareAsAService THEN BEGIN
                  IF (TenantWebService.GET(WebServiceSetLine."Object Type", WebServiceSetLine."Service Name")) THEN
                    TenantWebService.DELETE;
                 END ELSE BEGIN
                   IF (WebService.GET(WebServiceSetLine."Object Type", WebServiceSetLine."Service Name")) THEN
                     WebService.DELETE;
                END;
              END;
            END ELSE BEGIN
              PossibleWebService.GET(WebServiceSetLine."Object Type", WebServiceSetLine."Service Name");
              IF PermissionManager.SoftwareAsAService THEN BEGIN
                WebServiceManagement.CreateTenantWebService(
                  PossibleWebService."Object Type", PossibleWebService."Object Id",
                  PossibleWebService."Service Name", Publish);
              END ELSE BEGIN
                WebServiceManagement.CreateWebService(
                  PossibleWebService."Object Type", PossibleWebService."Object Id",
                  PossibleWebService."Service Name", Publish);
              END;
            END;
            WebServiceSetLine.Published := Publish;
            WebServiceSetLine.MODIFY;
          UNTIL (WebServiceSetLine.NEXT = 0);
        END;
      UNTIL (WebServiceSet.NEXT = 0);
    END;

    PROCEDURE ShowWebService@1100525001(VAR WebServiceSet@1100525001 : Record 11229298);
    VAR
      WebServiceSetLine@1100525003 : Record 11229309;
      WebServiceSetLinesList@1100525002 : Page 11126626;
      WebServiceSetFilter@1100525004 : Text;
    BEGIN
      IF (NOT WebServiceSet.FINDSET) THEN
        EXIT;
      REPEAT
        IF (WebServiceSetFilter = '') THEN
          WebServiceSetFilter := WebServiceSet.Code
        ELSE
          WebServiceSetFilter += STRSUBSTNO('|%1', WebServiceSet.Code);
      UNTIL (WebServiceSet.NEXT = 0);
      WebServiceSetLine.SETFILTER("Web Service Set Code", WebServiceSetFilter);
      WebServiceSetLinesList.SETTABLEVIEW(WebServiceSetLine);
      WebServiceSetLinesList.RUNMODAL;
    END;

    LOCAL PROCEDURE IsLastService@1100525002(iWebServiceSetLine@1100525000 : Record 11229309) : Boolean;
    VAR
      WebServiceSetLine@1100525002 : Record 11229309;
    BEGIN
      WebServiceSetLine.SETFILTER("Web Service Set Code", '<>%1', iWebServiceSetLine."Web Service Set Code");
      WebServiceSetLine.SETRANGE("Object Type", iWebServiceSetLine."Object Type");
      WebServiceSetLine.SETRANGE("Service Name", iWebServiceSetLine."Service Name");
      WebServiceSetLine.SETRANGE(Published, TRUE);
      EXIT(NOT WebServiceSetLine.FINDSET);
    END;

    BEGIN
    END.
  }
}

