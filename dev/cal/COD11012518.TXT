OBJECT Codeunit 11012518 Anomymous data
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 110=rimd,
                TableData 112=rimd,
                TableData 114=rimd,
                TableData 122=rimd,
                TableData 124=rimd,
                TableData 297=rimd,
                TableData 304=rimd,
                TableData 6660=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'DEU=Mîchten Sie die Daten anonymisieren?;ENU=Would you anonymous the data?;NLD=Wilt u de gegevens anonimiseren?';
      Text002@1100528500 : TextConst 'DEU=Sind Sie sicher? Diese énderung kann nicht rÅckgÑngig gemacht werden.;ENU=Are you sure? This change can not be undone.;NLD=Weet u het zeker? Gegevens kunnen niet meer worden teruggedraaid.';

    PROCEDURE Initialize@1100525001();
    VAR
      AnonymousObject@1100525001 : Record 11229427;
      AnonymousObjectField@1100525000 : Record 11229428;
    BEGIN
      AnonymousObject.DELETEALL;
      AnonymousObjectField.DELETEALL;

      InsertTable(18);
      InsertTable(23);
      InsertTable(156);
      InsertTable(287);
      InsertTable(288);
      InsertTable(5200);
      InsertTable(5050);
      InsertTable(97);
      InsertTable(5201);
      InsertTable(5205);
      InsertTable(5208);
      InsertTable(5216);
      InsertTable(5219);
      InsertTable(5211);
      InsertTable(5207);
      InsertTable(38);
      InsertTable(122);
      InsertTable(124);
      InsertTable(36);
      InsertTable(112);
      InsertTable(114);
      InsertTable(295);
      InsertTable(302);
      InsertTable(297);
      InsertTable(304);
      InsertTable(6660);
      InsertTable(110);
      InsertTable(11071819);
      InsertTable(11012801);
      InsertTable(11071737);
      InsertTable(11012822);
      InsertTable(11125777);
      InsertTable(11012554);
      InsertTable(11012590);
      InsertTable(11071688);
      InsertTable(11012011);
      InsertTable(11012450);

      EnableFields;
    END;

    LOCAL PROCEDURE InsertTable@1100525002(TableNo@1100525000 : Integer);
    VAR
      AnonymousObject@1100525001 : Record 11229427;
    BEGIN
      AnonymousObject.INIT;
      AnonymousObject.Type := AnonymousObject.Type::Table;
      AnonymousObject.VALIDATE(ID, TableNo);
      AnonymousObject.INSERT;
    END;

    LOCAL PROCEDURE EnableFields@1100525006();
    VAR
      AnonymousObject@1100525000 : Record 11229427;
      AnonymousObjectField@1100525001 : Record 11229428;
      Field@1100525002 : Record 2000000041;
      i@1100525003 : Integer;
      ValueString@1100525004 : Text[50];
    BEGIN
      ValueString := '##################################################';
      AnonymousObject.RESET;
      AnonymousObject.SETRANGE(Type,AnonymousObject.Type::Table);
      IF AnonymousObject.FINDFIRST THEN REPEAT
        Field.RESET;
        Field.SETRANGE(TableNo,AnonymousObject.ID);
        Field.SETRANGE(Type,Field.Type::Text);
        Field.SETRANGE(Enabled,TRUE);
        IF Field.FINDSET THEN REPEAT
          AnonymousObjectField."Table No." := Field.TableNo;
          AnonymousObjectField."Field No." := Field."No.";
          AnonymousObjectField.Change := TRUE;
          AnonymousObjectField.Value := COPYSTR(ValueString,1,Field.Len);
          AnonymousObjectField.INSERT;
        UNTIL Field.NEXT=0;
      UNTIL AnonymousObject.NEXT=0;
    END;

    PROCEDURE Anonymous@1100525012();
    VAR
      Answer@1100525000 : Boolean;
      Answer1@1100528500 : Boolean;
      AnonymousObjectField@1100525001 : Record 11229428;
      FieldRef@1100525002 : FieldRef;
      RecRef@1100525003 : RecordRef;
      FieldCurrent@1100525005 : Integer;
      WindowDialog@1100525004 : Dialog;
      TotalAnonymousObjectField@1100525006 : Integer;
    BEGIN
      Answer := CONFIRM(Text001,FALSE);
      IF Answer = FALSE THEN
        EXIT;

      Answer1 := CONFIRM(Text002,FALSE);
      IF Answer1 = FALSE THEN
        EXIT;

      AnonymousObjectField.SETRANGE(Change,TRUE);
      WindowDialog.OPEN(
          'Table fields being processed...' +
          '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
      WindowDialog.UPDATE(1,0);
      FieldCurrent := 0;
      TotalAnonymousObjectField := AnonymousObjectField.COUNT;

      IF AnonymousObjectField.FINDSET THEN
        REPEAT
          FieldCurrent += 1;
          WindowDialog.UPDATE(1,ROUND(FieldCurrent / TotalAnonymousObjectField * 10000, 1));
          RecRef.OPEN(AnonymousObjectField."Table No.");
          IF RecRef.FINDSET THEN
            REPEAT
              FieldRef := RecRef.FIELD(AnonymousObjectField."Field No.");
              FieldRef.VALUE(AnonymousObjectField.Value);
              RecRef.MODIFY;
            UNTIL RecRef.NEXT=0;
          RecRef.CLOSE;
        UNTIL AnonymousObjectField.NEXT=0;
    END;

    BEGIN
    END.
  }
}

