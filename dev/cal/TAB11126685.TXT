OBJECT Table 11126685 Plot-Rental Unit Relation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD("Company (Rental Unit)", COMPANYNAME);
               TESTFIELD("Company (Plot)");
               TESTFIELD("Project No. (Plot)");
             END;

    OnModify=BEGIN
               TESTFIELD("Company (Rental Unit)", COMPANYNAME);
             END;

    OnDelete=VAR
               PlotRentalUnitRelationLine@1100528800 : Record 11126686;
             BEGIN
               TESTFIELD("Company (Rental Unit)", COMPANYNAME);

               PlotRentalUnitRelationLine.SETRANGE("Company (Rental Unit)", "Company (Rental Unit)");
               PlotRentalUnitRelationLine.SETRANGE("Company (Plot)", "Company (Plot)");
               PlotRentalUnitRelationLine.SETRANGE("Project No. (Plot)", "Project No. (Plot)");
               PlotRentalUnitRelationLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               PlotRentalUnitRelationLine@1100528800 : Record 11126686;
             BEGIN
               TESTFIELD("Company (Rental Unit)", COMPANYNAME);
               TESTFIELD("Company (Plot)");
               TESTFIELD("Project No. (Plot)");

               PlotRentalUnitRelationLine.SETRANGE("Company (Rental Unit)", "Company (Rental Unit)");
               PlotRentalUnitRelationLine.SETRANGE("Company (Plot)", xRec."Company (Plot)");
               PlotRentalUnitRelationLine.SETRANGE("Project No. (Plot)", xRec."Project No. (Plot)");
               IF PlotRentalUnitRelationLine.FINDSET THEN
                 ERROR(Text000);
             END;

    CaptionML=ENU=Plot-Rental Unit Relation;
  }
  FIELDS
  {
    { 10  ;   ;Company (Rental Unit);Text30       ;TableRelation=Company;
                                                   CaptionML=ENU=Company (Rental Unit);
                                                   NotBlank=Yes;
                                                   Description=Only created in current company;
                                                   Editable=No }
    { 20  ;   ;Company (Plot)      ;Text30        ;TableRelation=Company;
                                                   OnValidate=BEGIN
                                                                IF "Company (Plot)" <> xRec."Company (Plot)" THEN
                                                                  "Project No. (Plot)" := '';
                                                              END;

                                                   CaptionML=ENU=Company (Plot);
                                                   NotBlank=Yes }
    { 30  ;   ;Project No. (Plot)  ;Code20        ;OnValidate=VAR
                                                                Project@1100528800 : Record 11072003;
                                                              BEGIN
                                                                Project.CHANGECOMPANY("Company (Plot)");
                                                                Project.GET("Project No. (Plot)");
                                                              END;

                                                   OnLookup=VAR
                                                              Project@1100528800 : Record 11072003;
                                                            BEGIN
                                                              Project.CHANGECOMPANY("Company (Plot)");
                                                              Project."No." := "Project No. (Plot)";
                                                              IF PAGE.RUNMODAL(0, Project) = ACTION::LookupOK THEN
                                                                VALIDATE("Project No. (Plot)", Project."No.");
                                                            END;

                                                   CaptionML=ENU=Project No. (Plot);
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;Company (Rental Unit),Company (Plot),Project No. (Plot);
                                                   Clustered=Yes }
    {    ;Company (Plot),Project No. (Plot)        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528800 : TextConst 'ENU=Not allowed, already lines present';

    PROCEDURE InitNewRecord@1100528802();
    BEGIN
      "Company (Rental Unit)" := COMPANYNAME;
      IF "Company (Plot)" = '' THEN
        "Company (Plot)" := COMPANYNAME;
    END;

    PROCEDURE ProjectDescription@1100529000() : Text;
    VAR
      Project@1100529000 : Record 11072003;
    BEGIN
      IF "Company (Plot)" <> COMPANYNAME THEN
        Project.CHANGECOMPANY("Company (Plot)");
      IF Project.GET("Project No. (Plot)") THEN
        EXIT(Project.Description);
      EXIT('');
    END;

    PROCEDURE GetPlotNumbers@1100528804();
    VAR
      Plot@1100528803 : Record 11012500;
      PlotRentalUnitRelationLine@1100528802 : Record 11126686;
    BEGIN
      TESTFIELD("Company (Rental Unit)", COMPANYNAME);
      TESTFIELD("Company (Plot)");
      TESTFIELD("Project No. (Plot)");
      Plot.CHANGECOMPANY("Company (Plot)");
      Plot.SETRANGE("Project No.", "Project No. (Plot)");
      Plot.SETFILTER("Plot No.", '<>%1', '');
      IF Plot.FINDSET THEN BEGIN
        REPEAT
          IF NOT PlotRentalUnitRelationLine.GET("Company (Rental Unit)", "Company (Plot)", "Project No. (Plot)", Plot."Plot No.") THEN BEGIN
            PlotRentalUnitRelationLine.INIT;
            PlotRentalUnitRelationLine."Company (Rental Unit)" := "Company (Rental Unit)";
            PlotRentalUnitRelationLine."Company (Plot)" := "Company (Plot)";
            PlotRentalUnitRelationLine."Project No. (Plot)" := "Project No. (Plot)";
            PlotRentalUnitRelationLine."Plot No." := Plot."Plot No.";
            PlotRentalUnitRelationLine.INSERT(TRUE);
          END;
        UNTIL Plot.NEXT = 0;
      END;
    END;

    BEGIN
    {
      Copied from T11072219, Now for plot and rental unit, split in header and line table
    }
    END.
  }
}

