OBJECT Table 11012000 Budget Rate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Budget Rate;
               NOR=Budsjettsats;
               SVE=Budgetavgift];
    LookupPageID=Page11020265;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Rate                ;Decimal       ;OnValidate=BEGIN
                                                                IF Rate <> xRec.Rate THEN
                                                                  "Last Date Modified" := TODAY;
                                                              END;

                                                   CaptionML=[ENU=Rate;
                                                              NOR=Avgift;
                                                              SVE=Avgift];
                                                   AutoFormatType=2 }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Sales Rate          ;Decimal       ;CaptionML=[ENU=Sales Rate;
                                                              NOR=Salgsrate;
                                                              SVE=F”rs„ljningsavgift];
                                                   AutoFormatType=2 }
    { 5   ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum] }
    { 10  ;   ;Project Filter      ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Project Filter;
                                                              NOR=Prosjektfilter;
                                                              SVE=Projektfilter] }
    { 30  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 40  ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 50  ;   ;Rate Type           ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("Apply as Estimate Rate", TRUE);
                                                                IF "Rate Type" = "Rate Type"::Hours THEN
                                                                  "Team Size" := 0;
                                                                IF "Rate Type" = "Rate Type"::Minuts THEN
                                                                  IF "Team Size" = 0 THEN
                                                                    "Team Size" := 1;
                                                              END;

                                                   CaptionML=[ENU=Rate Type;
                                                              NOR=Avgiftstype;
                                                              SVE=Avgiftstyp];
                                                   OptionCaptionML=[ENU=Hours,Minuts;
                                                                    NOR=Timer,Minutter;
                                                                    SVE=Timmar,Minuter];
                                                   OptionString=Hours,Minuts }
    { 60  ;   ;Team Size           ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Apply as Estimate Rate", TRUE);
                                                                IF "Rate Type" = "Rate Type"::Hours THEN
                                                                  TESTFIELD("Team Size", 0);
                                                              END;

                                                   CaptionML=[ENU=Team Size;
                                                              NOR=Teamst›rrelse;
                                                              SVE=Teamstorlek] }
    { 70  ;   ;Apply as Estimate Rate;Boolean     ;InitValue=Yes;
                                                   CaptionML=ENU=Apply as Estimate Rate }
    { 80  ;   ;Estimate Rate       ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Apply as Estimate Rate", TRUE);
                                                              END;

                                                   CaptionML=[ENU=Estimate Rate;
                                                              NOR=Kalkyle Í Sats;
                                                              SVE=Kalkyl Í avgift];
                                                   AutoFormatType=2 }
    { 90  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=CONST(Labor),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Apply as Estimate Rate", TRUE);
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   CaptionClass='1,1,2' }
  }
  KEYS
  {
    {    ;Code,Starting Date                      ;Clustered=Yes }
    {    ;Starting Date,Code                       }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Starting Date,Description,Rate      }
  }
  CODE
  {

    PROCEDURE ProjectRate@1100485000(ProjCde@1100485000 : Code[20];RateCde@1100485004 : Code[10];RefDate@1100485002 : Date;VAR IORate@1100485005 : Decimal) : Boolean;
    VAR
      ProjBudgetRateRec@1100485001 : Record 11020231;
    BEGIN
      ProjBudgetRateRec.SETRANGE("Project No.", ProjCde);
      ProjBudgetRateRec.SETRANGE("Rate Code", RateCde);
      ProjBudgetRateRec.SETRANGE("Starting Date", 0D, RefDate);
      ProjBudgetRateRec.SETFILTER("Ending Date",'%1|>=%2',0D, RefDate);
      IF ProjBudgetRateRec.FINDLAST THEN BEGIN
        IORate := ProjBudgetRateRec."Rate (LCY)";
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE StandardRate@1100485002(RateCde@1100485004 : Code[10];RefDate@1100485002 : Date;VAR IORate@1100485005 : Decimal) : Boolean;
    BEGIN
      SETRANGE(Code, RateCde);
      SETRANGE("Starting Date", 0D, RefDate);
      SETFILTER("Ending Date",'%1|>=%2',0D, RefDate);
      IF FINDLAST THEN BEGIN
        IORate := Rate;
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE SynchroniseEstimateRate@1100525000();
    VAR
      BudgetRate@1100525000 : Record 11012000;
    BEGIN
      IF BudgetRate.FINDSET THEN BEGIN
        REPEAT
          IF BudgetRate."Apply as Estimate Rate" = FALSE THEN BEGIN
            BudgetRate."Rate Type" := BudgetRate."Rate Type"::Hours;
            BudgetRate."Estimate Rate" := 0;
            BudgetRate."Team Size" := 0;
            BudgetRate."Cost Object" := '';
          END ELSE BEGIN
            BudgetRate."Estimate Rate" := BudgetRate.Rate;
            IF BudgetRate."Rate Type" = BudgetRate."Rate Type"::Minuts THEN
              BudgetRate."Estimate Rate" := ROUND(BudgetRate."Estimate Rate" / 60, 0.01);
          END;
          BudgetRate.MODIFY;
        UNTIL BudgetRate.NEXT = 0;
      END;
    END;

    PROCEDURE RateDescription@1100409000(RateCode@1100409003 : Code[10];StartDate@1100409000 : Date;EndDate@1100409001 : Date) : Text[50];
    VAR
      BudgetRate@1100409002 : Record 11012000;
    BEGIN
      IF RateCode <> '' THEN BEGIN
        BudgetRate.SETRANGE(Code, RateCode);
        IF EndDate <> 0D THEN
          BudgetRate.SETRANGE("Starting Date", 0D, EndDate)
        ELSE BEGIN
          IF StartDate <> 0D THEN
            BudgetRate.SETRANGE("Starting Date", 0D, StartDate);
        END;
        BudgetRate.SETFILTER("Ending Date",'%1|>=%2',0D, StartDate);
        IF BudgetRate.FINDLAST THEN
          EXIT(BudgetRate.Description)
        ELSE BEGIN
          BudgetRate.RESET;
          BudgetRate.SETRANGE(Code, RateCode);
          IF BudgetRate.FINDLAST THEN
            EXIT(BudgetRate.Description);
        END;
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

