OBJECT Table 6085580 Template Field
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.07,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Type,Code,Field Name;
    OnInsert=VAR
               Template@1000000000 : Record 6085579;
             BEGIN
               IF "Language Code" = '' THEN BEGIN
                 Template.GET("Template No.");
                 "Language Code" := Template."Language Code";
               END;

               "Source Table Filter GUID" := CREATEGUID;
               SetOrderRecords;
             END;

    OnDelete=VAR
               TemplField@161024012 : Record 6085580;
               Rule@1000000002 : Record 6085583;
               Caption@1000000000 : Record 6085581;
               Translation@1000000001 : Record 6085582;
               DataTransl@1160040001 : Record 6085584;
               RecIDMgt@1160040000 : Codeunit 6085604;
             BEGIN
               IF ("G/L Account Field Code" <> '') AND ("G/L Account Field Code" <> Code) THEN BEGIN
                 TemplField.SETRANGE("Template No.","Template No.");
                 TemplField.SETRANGE(Type,Type);
                 TemplField.SETRANGE("G/L Account Field Code","G/L Account Field Code");
                 IF TemplField.COUNT = 1 THEN
                   IF TemplField.GET("Template No.",Type,"G/L Account Field Code") THEN
                     TemplField.DELETE(TRUE);
               END;

               Rule.SETRANGE("Template No.","Template No.");
               Rule.SETRANGE(Type,Type);
               Rule.SETRANGE(Code,Code);
               Rule.DELETEALL(TRUE);

               Caption.SETRANGE("Template No.","Template No.");
               Caption.SETRANGE(Type,Type);
               Caption.SETRANGE(Code,Code);
               Caption.DELETEALL(TRUE);

               Translation.SETRANGE("Template No.","Template No.");
               Translation.SETRANGE(Type,Type);
               Translation.SETRANGE(Code,Code);
               Translation.DELETEALL(TRUE);

               DataTransl.SETRANGE("Template No.","Template No.");
               DataTransl.SETRANGE(Type,Type);
               DataTransl.SETRANGE("Field Code",Code);
               DataTransl.DELETEALL(TRUE);

               RecIDMgt.DeleteTableFilter("Source Table Filter GUID");
             END;

    CaptionML=[DEU=Vorlagenfeld;
               ENU=Template Field;
               NLD=Sjabloonveld];
    LookupPageID=Page6085602;
    DrillDownPageID=Page6085602;
  }
  FIELDS
  {
    { 1   ;   ;Template No.        ;Code20        ;TableRelation=Template;
                                                   CaptionML=[DEU=Vorlagennr.;
                                                              ENU=Template No.;
                                                              NLD=Sjabloonnr.;
                                                              NOR=Malnr.;
                                                              SVE=Mallnr];
                                                   NotBlank=Yes }
    { 2   ;   ;Type                ;Option        ;CaptionML=[DEU=Art;
                                                              ENU=Type;
                                                              NLD=Soort;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[DEU=Kopf,Zeile;
                                                                    ENU=Header,Line;
                                                                    NLD=Koptekst,Regel;
                                                                    NOR=Tittel,Rad;
                                                                    SVE=Huvud,Rad];
                                                   OptionString=Header,Line }
    { 3   ;   ;Code                ;Code20        ;CaptionML=[DEU=Code;
                                                              ENU=Code;
                                                              NLD=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 4   ;   ;Data Type           ;Option        ;OnValidate=VAR
                                                                GLSetup@161024012 : Record 98;
                                                                Template@161024013 : Record 6085579;
                                                              BEGIN
                                                                SetFixedValue('');

                                                                IF "Data Type" <> "Data Type"::Lookup THEN
                                                                  VALIDATE("Source Table No.",0);

                                                                IF "Data Type" = "Data Type"::Number THEN BEGIN
                                                                  GLSetup.GET;
                                                                  VALIDATE("Decimal Places",GLSetup."Amount Decimal Places");
                                                                  VALIDATE("Blank Zero",TRUE);
                                                                END;

                                                                IF "Data Type" = "Data Type"::Date THEN BEGIN
                                                                  Template.GET("Template No.");
                                                                  "Date Format" := Template."Date Format" + 1;
                                                                END;

                                                                IF NOT ("Data Type" IN ["Data Type"::Text,"Data Type"::Date]) THEN
                                                                  "Enable Rule Generation" := FALSE;
                                                              END;

                                                   CaptionML=[DEU=Datentyp;
                                                              ENU=Data Type;
                                                              NLD=Gegevenssoort;
                                                              NOR=Datatype;
                                                              SVE=Datatyp];
                                                   OptionCaptionML=[DEU=Text,Zahl,Datum,Boolesch,,,,,Suche;
                                                                    ENU=Text,Number,Date,Boolean,,,,,Lookup;
                                                                    NLD=Tekst,Nummer,Datum,Boolean,,,,,Lookup];
                                                   OptionString=Text,Number,Date,Boolean,,,,,Lookup }
    { 5   ;   ;Required            ;Boolean       ;CaptionML=[DEU=Erforderlich;
                                                              ENU=Required;
                                                              NLD=Vereist;
                                                              SVE=REQUIRED] }
    { 6   ;   ;Search for Value    ;Boolean       ;CaptionML=[DEU=Suchen nach Wert;
                                                              ENU=Search for Value;
                                                              NLD=Zoek waarde] }
    { 7   ;   ;Caption Is Part Of Value;Boolean   ;CaptionML=[DEU=Beschriftung ist Teil des Werts;
                                                              ENU=Caption Is Part Of Value;
                                                              NLD=Kop is deel van waarde] }
    { 8   ;   ;Insert on new Templates;Boolean    ;InitValue=Yes;
                                                   CaptionML=[DEU=EinfÅgen auf neuen Vorlagen;
                                                              ENU=Insert on new Templates;
                                                              NLD=Voeg toe aan nieuwe sjablonen] }
    { 9   ;   ;Number Has Decimals ;Boolean       ;CaptionML=[DEU=Zahl hat Dezimalstellen;
                                                              ENU=Number Has Decimals;
                                                              NLD=Getal heeft decimalen] }
    { 10  ;   ;Caption Offset X    ;Integer       ;CaptionML=[DEU=Beschriftung Offset X;
                                                              ENU=Caption Offset X;
                                                              NLD=Caption Offset X] }
    { 11  ;   ;Caption Offset Y    ;Integer       ;CaptionML=[DEU=Beschriftung Offset Y;
                                                              ENU=Caption Offset Y;
                                                              NLD=Caption Offset Y] }
    { 12  ;   ;Fixed Value (Text)  ;Text250       ;CaptionML=[DEU=Fester Wert (Text);
                                                              ENU=Fixed Value (Text);
                                                              NLD=Vaste waarde (Tekst)] }
    { 13  ;   ;Fixed Value (Decimal);Decimal      ;CaptionML=[DEU=Fester Wert (Dezimal);
                                                              ENU=Fixed Value (Decimal);
                                                              NLD=Vaste waarde (Decimaal)] }
    { 14  ;   ;Fixed Value (Date)  ;Date          ;CaptionML=[DEU=Fester Wert (Datum);
                                                              ENU=Fixed Value (Date);
                                                              NLD=Vaste waarde (Datum)] }
    { 15  ;   ;Fixed Value (Lookup);Code20        ;TableRelation=IF (Lookup Table=CONST(Vendor)) Vendor
                                                                 ELSE IF (Lookup Table=CONST(Contact)) Contact
                                                                 ELSE IF (Lookup Table=CONST(Customer)) Customer
                                                                 ELSE IF (Lookup Table=CONST(Job)) Job
                                                                 ELSE IF (Lookup Table=CONST(Item)) Item
                                                                 ELSE IF (Lookup Table=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Lookup Table=CONST(Employee)) Employee
                                                                 ELSE IF (Lookup Table=CONST(Dimension Value)) "Dimension Value".Code WHERE (Dimension Code=FIELD(Lookup Rel. Code));
                                                   CaptionML=[DEU=Fester Wert (Suche);
                                                              ENU=Fixed Value (Lookup);
                                                              NLD=Vaste waarde (opzoeken)] }
    { 17  ;   ;Validation Dateformula From;DateFormula;
                                                   CaptionML=[DEU=Validierung Datumsformel ab;
                                                              ENU=Validation Dateformula From;
                                                              NLD=Validatie datumformule Van] }
    { 18  ;   ;Validation Dateformula To;DateFormula;
                                                   CaptionML=[DEU=Validierung Datumsformel bis;
                                                              ENU=Validation Dateformula To;
                                                              NLD=Validatie datumformule Naar] }
    { 19  ;   ;Never use Global Captions;Boolean  ;InitValue=Yes;
                                                   CaptionML=[DEU=Niemals globale Beschriftungen verwenden;
                                                              ENU=Never use Global Captions;
                                                              NLD=Gebruik nooit globale captions] }
    { 20  ;   ;Decimal Places      ;Code10        ;CaptionML=[DEU=Dezimalstellen;
                                                              ENU=Decimal Places;
                                                              NLD=Aantal decimalen] }
    { 21  ;   ;Blank Zero          ;Boolean       ;CaptionML=[DEU=Leer Null;
                                                              ENU=Blank Zero;
                                                              NLD=Blank zero] }
    { 22  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[DEU=Sprachcode;
                                                              ENU=Language Code;
                                                              NLD=Taal;
                                                              NOR=SprÜkkode;
                                                              SVE=SprÜkkod] }
    { 23  ;   ;Enable Rule Generation;Boolean     ;OnValidate=BEGIN
                                                                IF "Enable Rule Generation" THEN
                                                                  IF NOT ("Data Type" IN ["Data Type"::Text,"Data Type"::Date]) THEN
                                                                    ERROR(EnableRuleActivateErr,FIELDCAPTION("Enable Rule Generation"),"Data Type"::Text,"Data Type"::Date);
                                                              END;

                                                   CaptionML=[DEU=Regelerstellung aktivieren;
                                                              ENU=Enable Rule Generation;
                                                              NLD=Regelgenereren aan] }
    { 24  ;   ;Multiline           ;Boolean       ;CaptionML=[DEU=Multiline;
                                                              ENU=Multiline;
                                                              NLD=Multiline] }
    { 25  ;   ;Default Page Source ;Option        ;CaptionML=[DEU=Standard-Seite Quelle;
                                                              ENU=Default Page Source;
                                                              NLD=Standaard paginabron];
                                                   OptionCaptionML=[DEU=Erste Seite,Letzte Seite;
                                                                    ENU=First Page,Last Page;
                                                                    NLD=Eerste pagina,Laatste pagina];
                                                   OptionString=First Page,Last Page }
    { 26  ;   ;Auto Update Caption ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU=Beschriftung automatisch aktualisieren;
                                                              ENU=Auto Update Caption;
                                                              NLD=Automatisch naam bijwerken] }
    { 27  ;   ;Typical Field Width ;Decimal       ;CaptionML=[DEU=Typische Feldbreite;
                                                              ENU=Typical Field Width;
                                                              NLD=Standaard veldbreedte];
                                                   DecimalPlaces=0:5 }
    { 28  ;   ;Auto Update Field Width;Boolean    ;InitValue=Yes;
                                                   CaptionML=[DEU=Feldbreite automatisch aktualisieren;
                                                              ENU=Auto Update Field Width;
                                                              NLD=Automatisch veldbreedte bijwerken] }
    { 29  ;   ;Typical Field Height;Decimal       ;CaptionML=[DEU=Typische Feldhîhe;
                                                              ENU=Typical Field Height;
                                                              NLD=Standaard veldhoogte];
                                                   DecimalPlaces=0:5 }
    { 30  ;   ;Auto Update Field Height;Boolean   ;InitValue=Yes;
                                                   CaptionML=[DEU=Feldhîhe automatisch aktualisieren;
                                                              ENU=Auto Update Field Height;
                                                              NLD=Automatisch veldhoogte bijwerken] }
    { 31  ;   ;Sort Order          ;Integer       ;CaptionML=[DEU=Reihenfolge;
                                                              ENU=Sort Order;
                                                              NLD=Sorteervolgorde;
                                                              NOR=Sorteringsrekkefõlge;
                                                              SVE=Sorteringsordning] }
    { 32  ;   ;Field Name          ;Text50        ;CaptionML=[DEU=Feldname;
                                                              ENU=Field Name;
                                                              NLD=Veldnaam;
                                                              NOR=Feltnavn;
                                                              SVE=FÑltnamn] }
    { 33  ;   ;Stop Lines Recognition;Option      ;CaptionML=[DEU=Stoppzeilenerkennung;
                                                              ENU=Stop Lines Recognition;
                                                              NLD=Stop regelherkenning];
                                                   OptionCaptionML=[DEU=" ,Wenn Beschriftung auf derselben Zeile,Wenn Wert auf derselben Zeile,Wenn Beschriftung oder Wert auf derselben Zeile,Wenn Beschriftung auf derselben Zeile (weiter auf der nÑchsten Seite),Wenn Wert auf derselben Zeile (weiter auf der nÑchsten Seite), Wenn Beschriftung oder Wert auf derselben Zeile (weiter auf der nÑchsten Seite)";
                                                                    ENU=" ,If Caption is on same line,If Value is on same line,If Caption or Value is on same line,If Caption is on same line (continue on next page),If Value is on same line (continue on next page),If Caption or Value is on same line (continue on next page)";
                                                                    NLD=" ,Indien bijschrift op dezelfde lijn is,Indien waarde op dezelfde lijn is,indien bijschrift of waarde op dezelfde lijn is,Indien bijschrift op dezelfde lijn is (verder gaan op volgende pagina),Indien waarde op dezelfde lijn is (verder gaan op volgende pagina),indien bijschrift of waarde op dezelfde lijn is (verder gaan op volgende pagina)ist (weiter auf nÑchster Seite)"];
                                                   OptionString=[ ,If Caption is on same line,If Value is on same line,If Caption or Value is on same line,If Caption is on same line (continue on next page),If Value is on same line (continue on next page),If Caption or Value is on same line (continue on next page)] }
    { 34  ;   ;Make Absolute Number;Boolean       ;CaptionML=[DEU=Nummer absolut machen;
                                                              ENU=Make Absolute Number;
                                                              NLD=Maak absoluut nummer] }
    { 37  ;   ;Lookup Table        ;Option        ;CaptionML=[DEU=Suchtabelle;
                                                              ENU=Lookup Table;
                                                              NLD=Opzoeken tabel];
                                                   OptionCaptionML=[DEU=" ,Kreditor,Kontakt,Debitor,Projekt,Artikel,Anlage,Mitarbeiter,Dimensionswert";
                                                                    ENU=" ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee,Dimension Value";
                                                                    NLD=" ,Leverancier,Contact,Klant,Project,Artikel,Vast activum,Werknemer,Dimensiewaarde"];
                                                   OptionString=[ ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee,Dimension Value] }
    { 38  ;   ;Lookup Rel. Code    ;Code20        ;TableRelation=IF (Lookup Table=CONST(Dimension Value)) Dimension;
                                                   CaptionML=[DEU=Beziehungscode;
                                                              ENU=Relation Code;
                                                              NLD=Relatiecode;
                                                              SVE=Kontaktnr] }
    { 39  ;   ;Codeunit ID: Is OK  ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Ist OK;
                                                              ENU=Codeunit ID: Is OK;
                                                              NLD=Codeunit-id: Is OK];
                                                   BlankZero=Yes }
    { 41  ;   ;Codeunit ID: Lookup ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Lookup;
                                                              ENU=Codeunit ID: Lookup;
                                                              NLD=Codeunit-id: Lookup];
                                                   BlankZero=Yes }
    { 43  ;   ;Codeunit ID: Validate;Integer      ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Validieren;
                                                              ENU=Codeunit ID: Validate;
                                                              NLD=Codeunit-id: Valideer];
                                                   BlankZero=Yes }
    { 45  ;   ;Formula             ;Code250       ;OnValidate=VAR
                                                                CaptureMgt@1160040000 : Codeunit 6085576;
                                                                WebClientManagement@6085574 : Codeunit 6085662;
                                                              BEGIN
                                                                IF Formula = '' THEN
                                                                  EXIT;

                                                                IF ("Data Type" = "Data Type"::Number) AND NOT WebClientManagement.IsWebClient THEN
                                                                  CaptureMgt.ValidateNumberFormula("Template No.",Formula);

                                                                "Search for Value" := FALSE;
                                                              END;

                                                   CaptionML=[DEU=Formel;
                                                              ENU=Formula;
                                                              NLD=Formule;
                                                              NOR=Formel;
                                                              SVE=Formel] }
    { 46  ;   ;G/L Account Field Code;Code20      ;TableRelation="Template Field".Code WHERE (Template No.=FIELD(Template No.),
                                                                                              Type=CONST(Header),
                                                                                              Data Type=CONST(Text));
                                                   OnLookup=VAR
                                                              TemplateField@1160040000 : Record 6085580;
                                                            BEGIN
                                                              TemplateField.SETRANGE("Template No.","Template No.");
                                                              TemplateField.SETRANGE(Type,TemplateField.Type::Header);
                                                              TemplateField.SETRANGE("Data Type",TemplateField."Data Type"::Text);
                                                              IF PAGE.RUNMODAL(0,TemplateField) = ACTION::LookupOK THEN
                                                                "G/L Account Field Code" := TemplateField.Code;
                                                            END;

                                                   CaptionML=[DEU=Sachkonto-Feldcode;
                                                              ENU=G/L Account Field Code;
                                                              NLD=Grootboek veldcode] }
    { 47  ;   ;Transfer Amount to Document;Option ;CaptionML=[DEU=Betrag zu Beleg Åbertragen;
                                                              ENU=Transfer Amount to Document;
                                                              NLD=Verplaats bedrag naar document];
                                                   OptionCaptionML=[DEU=" ,Wenn Zeilen nicht anerkannt,Immer";
                                                                    ENU=" ,If lines are not recognised,Always";
                                                                    NLD=" ,Als regels niet herkend worden,Altijd"];
                                                   OptionString=[ ,If lines are not recognised,Always] }
    { 48  ;   ;Subtract from Amount Field;Code20  ;TableRelation="Template Field".Code WHERE (Template No.=FIELD(Template No.),
                                                                                              Type=FIELD(Type),
                                                                                              Transfer Amount to Document=FILTER(<>' '));
                                                   OnValidate=BEGIN
                                                                IF "Subtract from Amount Field" = Code THEN
                                                                  ERROR(Text003);
                                                              END;

                                                   CaptionML=[DEU=Von Betragsfeld abziehen (bei Registrierung);
                                                              ENU=Subtract from Amount Field (on registration);
                                                              NLD=Aftrekken van Bedrag (bij registratie)] }
    { 49  ;   ;Codeunit ID: Capture Value;Integer ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Erfassungswert;
                                                              ENU=Codeunit ID: Capture Value;
                                                              NLD=Codeunit-id: Capture Waarde];
                                                   BlankZero=Yes }
    { 51  ;   ;Purch. Alloc. G/L Account No.;Code20;
                                                   TableRelation="G/L Account";
                                                   CaptionML=[DEU=Eink.zuordn. Sachkontonr.;
                                                              ENU=Purch. Alloc. G/L Account No.;
                                                              NLD=Inkoopallocatie Grootboekrekeningnummer] }
    { 52  ;   ;Date Format         ;Option        ;CaptionML=[DEU=Datumsformat;
                                                              ENU=Date Format;
                                                              NLD=Datumformaat];
                                                   OptionCaptionML=[DEU=" ,Tag / Monat / Jahr,Monat / Tag / Jahr,Jahr / Tag / Monat,Jahr / Monat / Tag,Tag / Jahr / Monat,Monat / Jahr / Tag";
                                                                    ENU=" ,Day / Month / Year,Month / Day / Year,Year / Day / Month,Year / Month / Day,Day / Year / Month,Month / Year / Day";
                                                                    NLD=" ,Dag / Maand / Jaar,Maand / Dag / Jaar,Jaar / Dag / Maand,Jaar / Maand / Dag,Dag / Jaar / Maand,Maand / Jaar / Dag"];
                                                   OptionString=[ ,Day / Month / Year,Month / Day / Year,Year / Day / Month,Year / Month / Day,Day / Year / Month,Month / Year / Day] }
    { 53  ;   ;Source Table No.    ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=VAR
                                                                DCMgt@1160040001 : Codeunit 6085603;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Table No." <> 0 THEN
                                                                  TESTFIELD("Data Type","Data Type"::Lookup);

                                                                DCMgt.IsLogActive2("Source Table No.",TRUE);

                                                                IF "Source Table No." = xRec."Source Table No." THEN
                                                                  EXIT;

                                                                RecIDMgt.CheckDocValue(Type = Type::Line,Code,"Template No.",FIELDCAPTION("Source Table No."));
                                                                RecIDMgt.DeleteTableFilter("Source Table Filter GUID");
                                                                CLEAR("Source Table Filter GUID");
                                                                "Fixed Value (Rec. ID Tree ID)" := 0;
                                                                "Source Field No." := RecIDMgt.GetFirstKeyField("Source Table No.");
                                                              END;

                                                   CaptionML=[DEU=Herkunftstabellennr.;
                                                              ENU=Source Table No.;
                                                              NLD=Brontabelnr.];
                                                   BlankZero=Yes }
    { 55  ;   ;Source Table Filter GUID;GUID      ;CaptionML=[DEU=Herkunftstabellenfilter-GUID;
                                                              ENU=Source Table Filter GUID;
                                                              NLD=Brontabelfilter-GUID] }
    { 56  ;   ;Source Field No.    ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;

                                                                IF xRec."Source Field No." > 0 THEN
                                                                  RecIDMgt.CheckDocValue(Type = Type::Line,Code,"Template No.",FIELDCAPTION("Source Field No."));

                                                                IF NOT RecIDMgt.PartOfKey("Source Table No.","Source Field No.") THEN
                                                                  ERROR(Text005,FIELDCAPTION("Source Field No."),FIELDCAPTION("Source Table No."));
                                                              END;

                                                   CaptionML=[DEU=Quellfeldnr.;
                                                              ENU=Source Field No.;
                                                              NLD=Bronveldnr.;
                                                              NOR=Kildefeltnr.;
                                                              SVE=KÑllfÑltsnr];
                                                   BlankZero=Yes }
    { 57  ;   ;Destination Header Field No.;Integer;
                                                   TableRelation=Field.No.;
                                                   OnValidate=VAR
                                                                Template@1160040001 : Record 6085579;
                                                                Field@161024013 : Record 2000000041;
                                                                DocCat@161024012 : Record 6085575;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF ("Destination Header Field No." = xRec."Destination Header Field No.") OR ("Destination Header Field No." = 0) THEN
                                                                  EXIT;

                                                                Template.GET("Template No.");
                                                                DocCat.GET(Template."Category Code");
                                                                DocCat.TESTFIELD("Destination Header Table No.");

                                                                Field.GET(DocCat."Destination Header Table No.","Destination Header Field No.");
                                                                Field.TESTFIELD(Enabled);
                                                                Field.TESTFIELD(Class,Field.Class::Normal);
                                                              END;

                                                   CaptionML=[DEU=Zielkopffeldnr.;
                                                              ENU=Destination Header Field No.;
                                                              NLD=Doelveldnr. (kop)];
                                                   BlankZero=Yes }
    { 58  ;   ;No. of Source Table Filters;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Table Filter Field" WHERE (Table Filter GUID=FIELD(Source Table Filter GUID)));
                                                   CaptionML=[DEU=Anz. der Quelltabellenfilter;
                                                              ENU=No. of Source Table Filters;
                                                              NLD=Aantal brontabelfilters];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 59  ;   ;Fixed Value (Rec. ID Tree ID);Integer;
                                                   TableRelation="Record ID Tree";
                                                   CaptionML=[DEU=Fester Wert (Auf.-ID Baum-ID);
                                                              ENU=Fixed Value (Rec. ID Tree ID);
                                                              NLD=Vaste waarde (Record-id Boom-id)] }
    { 60  ;   ;Destination Line Field No.;Integer ;TableRelation=Field.No.;
                                                   OnValidate=VAR
                                                                Template@1160040001 : Record 6085579;
                                                                Field@161024013 : Record 2000000041;
                                                                DocCat@161024012 : Record 6085575;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF ("Destination Line Field No." = xRec."Destination Line Field No.") OR ("Destination Line Field No." = 0) THEN
                                                                  EXIT;

                                                                Template.GET("Template No.");
                                                                DocCat.GET(Template."Category Code");
                                                                DocCat.TESTFIELD("Destination Line Table No.");

                                                                Field.GET(DocCat."Destination Line Table No.","Destination Line Field No.");
                                                                Field.TESTFIELD(Enabled);
                                                                Field.TESTFIELD(Class,Field.Class::Normal);
                                                              END;

                                                   CaptionML=[DEU=Zielzeilenfeldnr.;
                                                              ENU=Destination Line Field No.;
                                                              NLD=Doelveldnr. (regel)];
                                                   BlankZero=Yes }
    { 61  ;   ;Delete Blanks       ;Boolean       ;CaptionML=[DEU=Leerzeichen lîschen;
                                                              ENU=Delete Blanks;
                                                              NLD=Blanco's Verwijderen] }
    { 62  ;   ;Caption Mandatory   ;Boolean       ;CaptionML=[DEU=Beschriftung erforderlich;
                                                              ENU=Caption Mandatory;
                                                              NLD=Bijschrift Verplicht] }
    { 63  ;   ;Use as Doc. Separator;Boolean      ;OnValidate=BEGIN
                                                                IF NOT "Use as Doc. Separator" THEN
                                                                  EXIT;

                                                                TESTFIELD(Type,Type::Header);
                                                              END;

                                                   CaptionML=[DEU=Als Belegtrenner verwenden;
                                                              ENU=Use as Document Separator;
                                                              NLD=Gebruik als documentscheiding] }
    { 64  ;   ;Fixed Value (Boolean);Boolean      ;CaptionML=[DEU=Fester Wert (Boolesch);
                                                              ENU=Fixed Value (Boolean);
                                                              NLD=Vaste waarde (Boolean)] }
    { 65  ;   ;Offset DPI          ;Integer       ;CaptionML=[DEU=Offset DPI;
                                                              ENU=Offset DPI;
                                                              NLD=Offset DPI] }
    { 66  ;   ;Typical Field DPI   ;Integer       ;CaptionML=[DEU=Typisches Feld DPI;
                                                              ENU=Typical Field DPI;
                                                              NLD=Typisch veld DPI] }
    { 67  ;   ;Transfer Blank Values;Boolean      ;CaptionML=[DEU=Leere Werte Åbertragen;
                                                              ENU=Transfer Blank Values;
                                                              NLD=Lege waarden overzetten] }
  }
  KEYS
  {
    {    ;Template No.,Type,Code                  ;Clustered=Yes }
    {    ;Template No.,Type,Sort Order             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'DEU=Es wird nicht empfohlen, Leerzeichen als Trennzeichen in Datumsvalidierungsregeln zu verwenden, da diese vor der Evaluierung gelîscht werden. Verwenden Sie stattdessen - oder / als Trennzeichen.;ENU=It is not recommended to use space as a seperator in Date Validation Rules as they will be deleted before evaluation. Use a seperator line - or / instead.;NLD="Het is niet aan te bevelen om een spatie te gebruiken als scheidingsteken in Date Validation Rules want deze worden verwijderd. Gebruik een scheidingslijn - of / . "';
      Text002@161024012 : TextConst 'DEU=Mîchten Sie auch das Feld ''%1'' lîschen?;ENU=Do you also want to delete the field ''%1''?;NLD=Wilt u ook veld ''%1'' verwijderen?';
      Text003@161024013 : TextConst 'DEU=Das Feld kann nicht von sich selbst abgezogen werden;ENU=The field cannot be subtracted from itself;NLD=Het veld kan niet zichzelf worden afgetrokken.';
      Text004@1160040000 : TextConst 'DEU=Der Datensatz liegt nicht innerhalb der Filter von %1.;ENU=The record is not within the filters of the %1.;NLD=Het record voldoet niet aan de filters van %1.';
      Text005@1160040001 : TextConst 'DEU=%1 ist nicht Teil des PrimÑrschlÅssels fÅr von %2.;ENU=The %1 is not part of the primary key of the %2.;NLD=%1 is geen onderdeel van de primaire sleutel van %2.';
      Text006@1160040002 : TextConst 'DEU=Feld in %1;ENU=Field in %1;NLD=Veld in %1';
      Text007@1160040011 : TextConst 'DEU=Zielkopffeld;ENU=Destination Header Field;NLD=Doelveld (kop)';
      Text008@1160040010 : TextConst 'DEU=Zielzeilenfeld;ENU=Destination Line Field;NLD=Doelveld (regel)';
      EnableRuleActivateErr@6085573 : TextConst 'DEU=%1 kann nur eingetragen werden, wenn %2 in %3 eingegeben worden ist und wenn ein %4 fÅr %5 erstellt worden ist.;ENU=%1 can only be activated for Date Type %2 or %3.;NLD=%1 kan alleen worden geactiveerd voor datumtype %2 of %3.';

    PROCEDURE Clone@161024012(FromCompany@1160040002 : Text[30];FromField@1160040003 : Record 6085580;ToTemplate@161024013 : Record 6085579;CreatedFromMasterTemplate@6085581 : Boolean);
    VAR
      ToField@1000000002 : Record 6085580;
      FromFieldRule@1000000010 : Record 6085583;
      ToFieldRule@1000000009 : Record 6085583;
      FromFieldCapt@1000000003 : Record 6085581;
      ToFieldCaption@1000000004 : Record 6085581;
      FromFieldTransl@1000000005 : Record 6085582;
      ToFieldTransl@1000000006 : Record 6085582;
      FromTableFilterFields@1160040000 : Record 6085762;
      ToTableFilterFields@1160040001 : Record 6085762;
      NoOfFromFieldRule@6085573 : Integer;
    BEGIN
      ToField := FromField;
      ToField."Template No." := ToTemplate."No.";
      IF ToField.FIND('=') THEN
        EXIT;

      ToField.INSERT(TRUE);

      IF FromCompany <> '' THEN BEGIN
        FromFieldRule.CHANGECOMPANY(FromCompany);
        FromFieldCapt.CHANGECOMPANY(FromCompany);
        FromFieldTransl.CHANGECOMPANY(FromCompany);
        FromTableFilterFields.CHANGECOMPANY(FromCompany);
      END;

      FromFieldRule.SETRANGE("Template No.",FromField."Template No.");
      FromFieldRule.SETRANGE(Type,FromField.Type);
      FromFieldRule.SETRANGE(Code,FromField.Code);
      IF FromFieldRule.FINDSET THEN BEGIN
        NoOfFromFieldRule := FromFieldRule.COUNT;

        REPEAT
          ToFieldRule := FromFieldRule;
          ToFieldRule."Template No." := ToField."Template No.";
          IF (NoOfFromFieldRule > 1) AND CreatedFromMasterTemplate AND FromField."Enable Rule Generation" THEN
            ToFieldRule."Created from Master Template" := TRUE
          ELSE
            ToFieldRule."Created from Master Template" := FALSE;
          ToFieldRule.INSERT(TRUE);
        UNTIL FromFieldRule.NEXT = 0;
      END;

      FromFieldCapt.SETRANGE("Template No.",FromField."Template No.");
      FromFieldCapt.SETRANGE(Type,FromField.Type);
      FromFieldCapt.SETRANGE(Code,FromField.Code);
      IF FromFieldCapt.FINDSET THEN
        REPEAT
          ToFieldCaption := FromFieldCapt;
          ToFieldCaption."Template No." := ToField."Template No.";
          ToFieldCaption.INSERT(TRUE);
        UNTIL FromFieldCapt.NEXT = 0;

      FromFieldTransl.SETRANGE("Template No.",FromField."Template No.");
      FromFieldTransl.SETRANGE(Type,FromField.Type);
      FromFieldTransl.SETRANGE(Code,FromField.Code);
      IF FromFieldTransl.FINDSET THEN
        REPEAT
          ToFieldTransl := FromFieldTransl;
          ToFieldTransl."Template No." := ToField."Template No.";
          ToFieldTransl.INSERT(TRUE);
        UNTIL FromFieldTransl.NEXT = 0;

      FromTableFilterFields.SETRANGE("Table Filter GUID",FromField."Source Table Filter GUID");
      IF FromTableFilterFields.FINDSET THEN
        REPEAT
          ToTableFilterFields := FromTableFilterFields;
          ToTableFilterFields."Table Filter GUID" := ToField."Source Table Filter GUID";
          ToTableFilterFields.INSERT(TRUE);
        UNTIL FromTableFilterFields.NEXT = 0;
    END;

    PROCEDURE SetFixedValue@1000000000(Value@1000000000 : Text[250]);
    VAR
      RecIDMgt@161024013 : Codeunit 6085604;
      DCMgt@1160040000 : Codeunit 6085603;
      CaptureMgt@1160040001 : Codeunit 6085576;
      RecID@161024012 : Text[30];
    BEGIN
      IF ("Source Table No." <> 0) OR ("Data Type" = "Data Type"::Lookup) THEN BEGIN
        "Fixed Value (Text)" := '';
        IF Value <> '' THEN
          DCMgt.IsLogActive("Source Table No.",TRUE);
        "Fixed Value (Rec. ID Tree ID)" := RecIDMgt.SetKeyValueWithGUID("Source Table No.","Source Field No.",0,
          "Source Table Filter GUID",Value,TRUE);
      END;

      CASE "Data Type" OF
        "Data Type"::Text:
          EVALUATE("Fixed Value (Text)",Value);
        "Data Type"::Number:
          IF Value = '' THEN
            "Fixed Value (Decimal)" := 0
          ELSE
            EVALUATE("Fixed Value (Decimal)",Value);
        "Data Type"::Date:
          IF Value = 'TODAY' THEN
            EVALUATE("Fixed Value (Text)",Value)
          ELSE BEGIN
            "Fixed Value (Text)" := '';
            EVALUATE("Fixed Value (Date)",Value);
          END;
        "Data Type"::Boolean:
          IF Value = '' THEN
            "Fixed Value (Boolean)" := FALSE
          ELSE
            CaptureMgt.ParseBoolean(Value,"Fixed Value (Boolean)");
      END;

      IF Value <> '' THEN
        "Search for Value" := FALSE;
    END;

    PROCEDURE GetFixedValue@1000000001() : Text[250];
    VAR
      RecIDMgt@161024012 : Codeunit 6085604;
    BEGIN
      IF ("Source Table No." <> 0) OR ("Data Type" = "Data Type"::Lookup) THEN
        EXIT(RecIDMgt.GetKeyValue("Fixed Value (Rec. ID Tree ID)","Source Field No."));

      CASE "Data Type" OF
        "Data Type"::Text:
          EXIT(FORMAT("Fixed Value (Text)"));
        "Data Type"::Date:
          IF "Fixed Value (Date)" <> 0D THEN
            EXIT(FORMAT("Fixed Value (Date)"))
          ELSE
            EXIT("Fixed Value (Text)");
        "Data Type"::Number:
          IF "Fixed Value (Decimal)" <> 0 THEN
            EXIT(FORMAT("Fixed Value (Decimal)"));
        "Data Type"::Boolean:
          EXIT(FORMAT("Fixed Value (Boolean)"));
      END;
    END;

    PROCEDURE FormulaOnLookup@161024013(VAR Text@161024012 : Text[1024]) : Boolean;
    VAR
      TemplField@161024013 : Record 6085580;
    BEGIN
      TemplField.FILTERGROUP(2);
      TemplField.SETCURRENTKEY("Template No.",Type,"Sort Order");
      TemplField.SETRANGE("Template No.","Template No.");
      TemplField.SETRANGE(Type,Type);
      TemplField.SETFILTER(Code,'<>%1',Code);
      IF "Data Type" = "Data Type"::Number THEN
        TemplField.SETRANGE("Data Type","Data Type");
      TemplField.FILTERGROUP(0);
      IF STRLEN(Text) <= MAXSTRLEN(TemplField.Code) THEN
        IF TemplField.GET("Template No.",Type,Text) THEN;

      IF PAGE.RUNMODAL(0,TemplField) = ACTION::LookupOK THEN BEGIN
        Text := TemplField.Code;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE GetDestFieldCaption@1160040000(FieldType@1160040002 : 'Header,Line') FieldCap : Text[250];
    VAR
      Template@1160040001 : Record 6085579;
      DocCat@1160040000 : Record 6085575;
      AllObjWithCaption@1160040004 : Record 2000000058;
      RecIDMgt@1160040003 : Codeunit 6085604;
    BEGIN
      IF Template.GET("Template No.") THEN
        DocCat.GET(Template."Category Code");

      IF FieldType = FieldType::Header THEN BEGIN
        FieldCap := RecIDMgt.GetObjectCaption(AllObjWithCaption."Object Type"::Table,DocCat."Destination Header Table No.");
        IF FieldCap = '' THEN
          FieldCap := Text007
        ELSE
          FieldCap := STRSUBSTNO(Text006,FieldCap);
      END ELSE BEGIN
        FieldCap := RecIDMgt.GetObjectCaption(AllObjWithCaption."Object Type"::Table,DocCat."Destination Line Table No.");
        IF FieldCap = '' THEN
          FieldCap := Text008
        ELSE
          FieldCap := STRSUBSTNO(Text006,FieldCap);
      END;
    END;

    LOCAL PROCEDURE SetOrderRecords@6085573();
    VAR
      SingleInstanceStorage@6085574 : Codeunit 6085789;
      TemplateField@6085573 : Record 6085580;
      TemplateField2@6085576 : Record 6085580;
      i@6085575 : Integer;
    BEGIN
      IF "Sort Order" <> 0 THEN
        EXIT;

      i := SingleInstanceStorage.GetActiveTemplFieldForP6085585;
      IF i = 0 THEN BEGIN
        TemplateField.SETCURRENTKEY("Template No.",Type,"Sort Order");
        TemplateField.SETRANGE("Template No.","Template No.");
        TemplateField.SETRANGE(Type,Type);
        IF TemplateField.FINDLAST THEN
          "Sort Order" := TemplateField."Sort Order" + 1
        ELSE
          "Sort Order" := 1;
      END ELSE BEGIN
        TemplateField.SETRANGE("Template No.","Template No.");
        TemplateField.SETRANGE(Type,Type);
        TemplateField.SETFILTER("Sort Order",'>%1',i);
        IF TemplateField.FINDSET(TRUE,FALSE) THEN
          REPEAT
            TemplateField2.GET(TemplateField."Template No.",TemplateField.Type,TemplateField.Code);
            TemplateField2."Sort Order" := TemplateField2."Sort Order" + 1;
            TemplateField2.MODIFY;
          UNTIL TemplateField.NEXT = 0;

        "Sort Order" := i + 1;
      END;
      SingleInstanceStorage.SetActiveTemplFieldForP6085585("Sort Order");
    END;

    BEGIN
    {
      4PS, Job relation changed from table 167 to 11072003
    }
    END.
  }
}

