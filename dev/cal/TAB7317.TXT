OBJECT Table 7317 Warehouse Receipt Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               WhseRcptHeader@1000 : Record 7316;
               OrderStatus@1001 : Option;
             BEGIN
               IF (Quantity <> "Qty. Outstanding") AND ("Qty. Outstanding" <> 0) THEN
                 IF NOT CONFIRM(Text004,FALSE,TABLECAPTION,"Line No.") THEN
                   ERROR(Text003);

               WhseRcptHeader.GET("No.");
               OrderStatus := WhseRcptHeader.GetHeaderStatus("Line No.");
               IF OrderStatus <> WhseRcptHeader."Document Status" THEN BEGIN
                 WhseRcptHeader.VALIDATE("Document Status",OrderStatus);
                 WhseRcptHeader.MODIFY;
               END;
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[ENU=Warehouse Receipt Line;
               NOR=Lagermottakslinje];
    LookupPageID=Page7342;
    DrillDownPageID=Page7342;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 3   ;   ;Source Type         ;Integer       ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   Editable=No }
    { 4   ;   ;Source Subtype      ;Option        ;CaptionML=[ENU=Source Subtype;
                                                              NOR=Kildeundertype;
                                                              SVE=Ursprungssubtyp];
                                                   OptionCaptionML=[ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    NOR=0,1,2,3,4,5,6,7,8,9,10;
                                                                    SVE=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10;
                                                   Editable=No }
    { 6   ;   ;Source No.          ;Code20        ;CaptionML=[ENU=Source No.;
                                                              NOR=Kildenr.;
                                                              SVE=Ursprungsnr];
                                                   Editable=No }
    { 7   ;   ;Source Line No.     ;Integer       ;CaptionML=[ENU=Source Line No.;
                                                              NOR=Kildelinjenr.;
                                                              SVE=Ursprungsradnr];
                                                   Editable=No }
    { 9   ;   ;Source Document     ;Option        ;CaptionML=[ENU=Source Document;
                                                              NOR=Kildedokument;
                                                              SVE=K„lldokument];
                                                   OptionCaptionML=[ENU=,Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer;
                                                                    NOR=,Ordre,,,Ordreretur,Bestilling,,,Bestillingsretur,Inng†ende overf.];
                                                   OptionString=,Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer;
                                                   Editable=No }
    { 10  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod];
                                                   Editable=No }
    { 11  ;   ;Shelf No.           ;Code20        ;CaptionML=[ENU=Shelf No.;
                                                              NOR=Hyllenr.;
                                                              SVE=Hyllnr];
                                                   Description=4PS 10 -> 20 }
    { 12  ;   ;Bin Code            ;Code20        ;TableRelation=IF (Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code))
                                                                 ELSE IF (Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                  Zone Code=FIELD(Zone Code));
                                                   OnValidate=VAR
                                                                Bin@1000 : Record 7354;
                                                                WhseIntegrationMgt@1001 : Codeunit 7317;
                                                              BEGIN
                                                                IF xRec."Bin Code" <> "Bin Code" THEN
                                                                  IF "Bin Code" <> '' THEN BEGIN
                                                                    GetLocation("Location Code");
                                                                    WhseIntegrationMgt.CheckBinTypeCode(DATABASE::"Warehouse Receipt Line",
                                                                      FIELDCAPTION("Bin Code"),
                                                                      "Location Code",
                                                                      "Bin Code",0);
                                                                    IF Location."Directed Put-away and Pick" THEN BEGIN
                                                                      Bin.GET("Location Code","Bin Code");
                                                                      "Zone Code" := Bin."Zone Code";
                                                                      CheckBin(FALSE);
                                                                    END;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Bin Code;
                                                              NOR=Hyllekode;
                                                              SVE=Lagerplatskod] }
    { 13  ;   ;Zone Code           ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF xRec."Zone Code" <> "Zone Code" THEN BEGIN
                                                                  IF "Zone Code" <> '' THEN BEGIN
                                                                    GetLocation("Location Code");
                                                                    Location.TESTFIELD("Directed Put-away and Pick");
                                                                  END;
                                                                  "Bin Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Zone Code;
                                                              NOR=Sonekode;
                                                              SVE=Zonkod] }
    { 14  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr];
                                                   Editable=No }
    { 15  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. (Base)" := CalcBaseQty(Quantity);
                                                                InitOutstandingQtys;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 16  ;   ;Qty. (Base)         ;Decimal       ;CaptionML=[ENU=Qty. (Base);
                                                              NOR=Ant. (l.enh.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 19  ;   ;Qty. Outstanding    ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Outstanding (Base)" := CalcBaseQty("Qty. Outstanding");
                                                                VALIDATE("Qty. to Receive","Qty. Outstanding");
                                                              END;

                                                   CaptionML=[ENU=Qty. Outstanding;
                                                              NOR=Restantall];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 20  ;   ;Qty. Outstanding (Base);Decimal    ;CaptionML=[ENU=Qty. Outstanding (Base);
                                                              NOR=Restantall (lagerenhet)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 21  ;   ;Qty. to Receive     ;Decimal       ;OnValidate=VAR
                                                                WMSMgt@1000 : Codeunit 7302;
                                                                IsHandled@1001 : Boolean;
                                                              BEGIN
                                                                IsHandled := FALSE;
                                                                OnBeforeValidateQtyToReceive(Rec,IsHandled);
                                                                IF NOT IsHandled THEN
                                                                  IF "Qty. to Receive" > "Qty. Outstanding" THEN
                                                                    ERROR(Text002,"Qty. Outstanding");

                                                                GetLocation("Location Code");
                                                                IF Location."Directed Put-away and Pick" THEN BEGIN
                                                                  WMSMgt.CalcCubageAndWeight(
                                                                    "Item No.","Unit of Measure Code","Qty. to Receive",Cubage,Weight);

                                                                  IF (CurrFieldNo <> 0) AND ("Qty. to Receive" > 0) THEN
                                                                    CheckBin(TRUE);
                                                                END;

                                                                "Qty. to Cross-Dock" := 0;
                                                                "Qty. to Cross-Dock (Base)" := 0;
                                                                "Qty. to Receive (Base)" := CalcBaseQty("Qty. to Receive");

                                                                Item.CheckSerialNoQty("Item No.",FIELDCAPTION("Qty. to Receive (Base)"),"Qty. to Receive (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Receive;
                                                              NOR=Motta (antall);
                                                              SVE=Ant. att inlevereras];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 22  ;   ;Qty. to Receive (Base);Decimal     ;OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Receive","Qty. to Receive (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Receive (Base);
                                                              NOR=Antall som skal mottas (l.enh);
                                                              SVE=Ant. att inlevereras (bas)];
                                                   DecimalPlaces=0:5 }
    { 23  ;   ;Qty. Received       ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Received (Base)" := CalcBaseQty("Qty. Received");
                                                              END;

                                                   CaptionML=[ENU=Qty. Received;
                                                              NOR=Mottatt ant.];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 24  ;   ;Qty. Received (Base);Decimal       ;CaptionML=[ENU=Qty. Received (Base);
                                                              NOR=Ant. mottatt (lagerenhet);
                                                              SVE=Inlevererat antal (bas)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 29  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              NOR=Enhetskode;
                                                              SVE=Enhetskod];
                                                   Editable=No }
    { 30  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              NOR=Antall per enhet;
                                                              SVE=Antal per enhet];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 31  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              NOR=Variantkode;
                                                              SVE=Variantkod];
                                                   Editable=No }
    { 32  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 33  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2];
                                                   Editable=No }
    { 34  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=" ,Partially Received,Completely Received";
                                                                    NOR=" ,Delvis mottatt,Fullstendig mottatt"];
                                                   OptionString=[ ,Partially Received,Completely Received];
                                                   Editable=No }
    { 35  ;   ;Sorting Sequence No.;Integer       ;CaptionML=[ENU=Sorting Sequence No.;
                                                              NOR=Sorteringsrekkef›lgenr.];
                                                   Editable=No }
    { 36  ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              NOR=Forfallsdato;
                                                              SVE=F”rfallodatum] }
    { 37  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 38  ;   ;Cubage              ;Decimal       ;CaptionML=[ENU=Cubage;
                                                              NOR=Kubikkinnhold];
                                                   DecimalPlaces=0:5 }
    { 39  ;   ;Weight              ;Decimal       ;CaptionML=[ENU=Weight;
                                                              NOR=Vekt;
                                                              SVE=Vikt];
                                                   DecimalPlaces=0:5 }
    { 48  ;   ;Not upd. by Src. Doc. Post.;Boolean;CaptionML=[ENU=Not upd. by Src. Doc. Post.;
                                                              NOR=Ikke oppdt. av kildedok.bokf.];
                                                   Editable=No }
    { 49  ;   ;Posting from Whse. Ref.;Integer    ;CaptionML=[ENU=Posting from Whse. Ref.;
                                                              NOR=Bokf›ring fra lagerref.;
                                                              SVE=Bokf. fr†n dist.lager ref.];
                                                   Editable=No }
    { 50  ;   ;Qty. to Cross-Dock  ;Decimal       ;OnValidate=BEGIN
                                                                CrossDockMgt.GetUseCrossDock(UseCrossDock,"Location Code","Item No.");
                                                                IF NOT UseCrossDock THEN
                                                                  ERROR(Text006,Item.TABLECAPTION,Location.TABLECAPTION);
                                                                IF "Qty. to Cross-Dock" > "Qty. to Receive" THEN
                                                                  ERROR(
                                                                    Text005,
                                                                    "Qty. to Receive");

                                                                "Qty. to Cross-Dock (Base)" := CalcBaseQty("Qty. to Cross-Dock");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Cross-Dock;
                                                              NOR=Ant. som skal kryssoverf.];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 51  ;   ;Qty. to Cross-Dock (Base);Decimal  ;OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Cross-Dock","Qty. to Cross-Dock (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Cross-Dock (Base);
                                                              NOR=Ant. som skal kryssoverf. (l.enh)];
                                                   DecimalPlaces=0:5 }
    { 52  ;   ;Cross-Dock Zone Code;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code),
                                                                                  Cross-Dock Bin Zone=CONST(Yes));
                                                   CaptionML=[ENU=Cross-Dock Zone Code;
                                                              NOR=Kryssoverf›ringssone - kode] }
    { 53  ;   ;Cross-Dock Bin Code ;Code20        ;TableRelation=IF (Cross-Dock Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                      Cross-Dock Bin=CONST(Yes))
                                                                                                                      ELSE IF (Cross-Dock Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                                                                                  Zone Code=FIELD(Cross-Dock Zone Code),
                                                                                                                                                                                  Cross-Dock Bin=CONST(Yes));
                                                   CaptionML=[ENU=Cross-Dock Bin Code;
                                                              NOR=Hyllekode for kryssoverf›ring;
                                                              SVE=Direktutlevns lagerplatskod] }
  }
  KEYS
  {
    {    ;No.,Line No.                            ;Clustered=Yes }
    {    ;Source Type,Source Subtype,Source No.,Source Line No.;
                                                   SumIndexFields=Qty. Outstanding (Base);
                                                   MaintainSIFTIndex=No }
    {    ;No.,Source Type,Source Subtype,Source No.,Source Line No.;
                                                   MaintainSQLIndex=No }
    {    ;No.,Sorting Sequence No.                ;MaintainSQLIndex=No }
    {    ;No.,Shelf No.                           ;MaintainSQLIndex=No }
    {    ;No.,Item No.                            ;MaintainSQLIndex=No }
    {    ;No.,Source Document,Source No.          ;MaintainSQLIndex=No }
    {    ;No.,Due Date                            ;MaintainSQLIndex=No }
    {    ;No.,Bin Code                            ;MaintainSQLIndex=No }
    {    ;Item No.,Location Code,Variant Code     ;SumIndexFields=Qty. Outstanding (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Bin Code,Location Code                  ;SumIndexFields=Cubage,Weight;
                                                   MaintainSIFTIndex=No }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Location@1002 : Record 14;
      Item@1012 : Record 27;
      Bin@1009 : Record 7354;
      CrossDockMgt@1005 : Codeunit 5780;
      UOMMgt@1000 : Codeunit 5402;
      UseCrossDock@1006 : Boolean;
      Text001@1022 : TextConst 'ENU=You cannot rename a %1.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn p† %1.';
      Text002@1021 : TextConst 'ENU=You cannot handle more than the outstanding %1 units.;NOR=Du kan ikke h†ndtere mer enn de utest†ende %1 enhetene.';
      Text003@1001 : TextConst 'ENU=Cancelled.;NOR=Avbrutt.;SVE=Avbruten.';
      Text004@1004 : TextConst 'ENU=%1 %2 is not completely received.\Do you really want to delete the %1?;NOR=%1 %2 er ikke fullstendig mottatt.\Er du sikker p† at du vil slette %1?';
      Text005@1008 : TextConst 'ENU=You cannot Cross-Dock  more than the %1 units to be received.;NOR=Du kan ikke kryssoverf›re mer enn de %1 enhetene som skal mottas.';
      Text006@1007 : TextConst 'ENU=Cross-Docking is disabled for this %1 and/or %2;NOR=Kryssoverf›ring er deaktivert for %1 og/eller %2';
      IgnoreErrors@1003 : Boolean;
      ErrorOccured@1010 : Boolean;

    [External]
    PROCEDURE InitNewLine@19(DocNo@1000 : Code[20]);
    BEGIN
      RESET;
      "No." := DocNo;
      SETRANGE("No.","No.");
      LOCKTABLE;
      IF FINDLAST THEN;

      INIT;
      SetIgnoreErrors;
      "Line No." := "Line No." + 10000;
    END;

    LOCAL PROCEDURE CalcBaseQty@14(Qty@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(Qty * "Qty. per Unit of Measure",UOMMgt.QtyRndPrecision));
    END;

    [External]
    PROCEDURE AutofillQtyToReceive@10(VAR WhseReceiptLine@1000 : Record 7317);
    BEGIN
      WITH WhseReceiptLine DO BEGIN
        IF FIND('-') THEN
          REPEAT
            VALIDATE("Qty. to Receive","Qty. Outstanding");
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE DeleteQtyToReceive@11(VAR WhseReceiptLine@1000 : Record 7317);
    BEGIN
      WITH WhseReceiptLine DO BEGIN
        IF FIND('-') THEN
          REPEAT
            VALIDATE("Qty. to Receive",0);
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE GetItem@15();
    BEGIN
      IF Item."No." <> "Item No." THEN
        Item.GET("Item No.");
    END;

    [External]
    PROCEDURE GetLineStatus@1() : Integer;
    BEGIN
      IF "Qty. Outstanding" = 0 THEN
        Status := Status::"Completely Received"
      ELSE
        IF Quantity = "Qty. Outstanding" THEN
          Status := Status::" "
        ELSE
          Status := Status::"Partially Received";

      EXIT(Status);
    END;

    LOCAL PROCEDURE GetLocation@2(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        Location.GetLocationSetup(LocationCode,Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE GetBin@4(LocationCode@1000 : Code[10];BinCode@1001 : Code[20]);
    BEGIN
      GetLocation(LocationCode);
      IF NOT Location."Bin Mandatory" THEN
        CLEAR(Bin)
      ELSE
        IF (Bin."Location Code" <> LocationCode) OR
           (Bin.Code <> BinCode)
        THEN
          Bin.GET(LocationCode,BinCode);
    END;

    LOCAL PROCEDURE CheckBin@3(CalledFromQtytoReceive@1002 : Boolean);
    VAR
      BinContent@1001 : Record 7302;
      DeductCubage@1006 : Decimal;
      DeductWeight@1005 : Decimal;
    BEGIN
      IF CalledFromQtytoReceive THEN BEGIN
        DeductCubage := xRec.Cubage;
        DeductWeight := xRec.Weight;
      END;

      IF BinContent.GET(
           "Location Code","Bin Code",
           "Item No.","Variant Code","Unit of Measure Code")
      THEN BEGIN
        IF NOT BinContent.CheckIncreaseBinContent(
             "Qty. to Receive",xRec."Qty. to Receive",
             DeductCubage,DeductWeight,Cubage,Weight,FALSE,IgnoreErrors)
        THEN
          ErrorOccured := TRUE;
      END ELSE BEGIN
        GetBin("Location Code","Bin Code");
        IF NOT Bin.CheckIncreaseBin(
             "Bin Code","Item No.","Qty. to Receive",
             DeductCubage,DeductWeight,Cubage,Weight,FALSE,IgnoreErrors)
        THEN
          ErrorOccured := TRUE;
      END;
      IF ErrorOccured THEN
        "Bin Code" := '';
    END;

    [External]
    PROCEDURE OpenItemTrackingLines@6500();
    VAR
      PurchaseLine@1000 : Record 39;
      SalesLine@1001 : Record 37;
      TransferLine@1002 : Record 5741;
      ReservePurchLine@1003 : Codeunit 99000834;
      ReserveSalesLine@1004 : Codeunit 99000832;
      ReserveTransferLine@1005 : Codeunit 99000836;
      SecondSourceQtyArray@1007 : ARRAY [3] OF Decimal;
      Direction@1006 : 'Outbound,Inbound';
      IsHandled@1008 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeOpenItemTrackingLines(Rec,IsHandled);
      IF IsHandled THEN
        EXIT;

      TESTFIELD("No.");
      TESTFIELD("Qty. (Base)");

      GetItem;
      Item.TESTFIELD("Item Tracking Code");

      SecondSourceQtyArray[1] := DATABASE::"Warehouse Receipt Line";
      SecondSourceQtyArray[2] := "Qty. to Receive (Base)";
      SecondSourceQtyArray[3] := 0;

      CASE "Source Type" OF
        DATABASE::"Purchase Line":
          BEGIN
            IF PurchaseLine.GET("Source Subtype","Source No.","Source Line No.") THEN
              ReservePurchLine.CallItemTracking2(PurchaseLine,SecondSourceQtyArray);
          END;
        DATABASE::"Sales Line":
          BEGIN
            IF SalesLine.GET("Source Subtype","Source No.","Source Line No.") THEN
              ReserveSalesLine.CallItemTracking2(SalesLine,SecondSourceQtyArray);
          END;
        DATABASE::"Transfer Line":
          BEGIN
            Direction := Direction::Inbound;
            IF TransferLine.GET("Source No.","Source Line No.") THEN
              ReserveTransferLine.CallItemTracking2(TransferLine,Direction,SecondSourceQtyArray);
          END
      END;

      OnAfterOpenItemTrackingLines(Rec,SecondSourceQtyArray);
    END;

    [External]
    PROCEDURE SetIgnoreErrors@6();
    BEGIN
      IgnoreErrors := TRUE;
    END;

    [External]
    PROCEDURE HasErrorOccured@7() : Boolean;
    BEGIN
      EXIT(ErrorOccured);
    END;

    [External]
    PROCEDURE InitOutstandingQtys@8();
    BEGIN
      VALIDATE("Qty. Outstanding",Quantity - "Qty. Received");
      "Qty. Outstanding (Base)" := "Qty. (Base)" - "Qty. Received (Base)";
    END;

    [External]
    PROCEDURE GetWhseRcptLine@9(ReceiptNo@1001 : Code[20];SourceType@1002 : Integer;SourceSubType@1003 : Option;SourceNo@1004 : Code[20];SourceLineNo@1005 : Integer) : Boolean;
    BEGIN
      SETRANGE("No.",ReceiptNo);
      SetSourceFilter(SourceType,SourceSubType,SourceNo,SourceLineNo,FALSE);
      IF FINDFIRST THEN
        EXIT(TRUE);
    END;

    [External]
    PROCEDURE SetItemData@29(ItemNo@1000 : Code[20];ItemDescription@1001 : Text[100];ItemDescription2@1006 : Text[50];LocationCode@1002 : Code[10];VariantCode@1003 : Code[10];UoMCode@1005 : Code[10];QtyPerUoM@1004 : Decimal);
    BEGIN
      "Item No." := ItemNo;
      Description := ItemDescription;
      "Description 2" := ItemDescription2;
      "Location Code" := LocationCode;
      "Variant Code" := VariantCode;
      "Unit of Measure Code" := UoMCode;
      "Qty. per Unit of Measure" := QtyPerUoM;
    END;

    [External]
    PROCEDURE SetSource@5(SourceType@1003 : Integer;SourceSubType@1002 : Option;SourceNo@1001 : Code[20];SourceLineNo@1000 : Integer);
    VAR
      WhseMgt@1004 : Codeunit 5775;
    BEGIN
      "Source Type" := SourceType;
      "Source Subtype" := SourceSubType;
      "Source No." := SourceNo;
      "Source Line No." := SourceLineNo;
      "Source Document" := WhseMgt.GetSourceDocument("Source Type","Source Subtype");
    END;

    [External]
    PROCEDURE SetSourceFilter@42(SourceType@1004 : Integer;SourceSubType@1003 : Option;SourceNo@1002 : Code[20];SourceLineNo@1001 : Integer;SetKey@1005 : Boolean);
    VAR
      WhseManagement@1000 : Codeunit 5775;
    BEGIN
      WhseManagement.SetSourceFilterForWhseRcptLine(Rec,SourceType,SourceSubType,SourceNo,SourceLineNo,SetKey);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterOpenItemTrackingLines@12(VAR WarehouseReceiptLine@1000 : Record 7317;SecondSourceQtyArray@1001 : ARRAY [3] OF Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeOpenItemTrackingLines@16(VAR WarehouseReceiptLine@1000 : Record 7317;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeValidateQtyToReceive@13(VAR WarehouseReceiptLine@1000 : Record 7317;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

