OBJECT Table 11126059 Proj. Hr. StMng. CostObj. Sel.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ERROR('');  //* Only indirect insert allowed (so no manual insert by the user)
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=[ENU=Project Hour Acc. Site Manager - Cost Object Selection;
               NOR=Prosjekttimekonto byggeleder - kostnadsobjekt utvalg;
               SVE=Arbetsledare, ack. projekttimmar Õ Val av kostnadsobjekt];
  }
  FIELDS
  {
    { 10  ;   ;Year                ;Integer       ;CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   Editable=No }
    { 20  ;   ;Week                ;Integer       ;CaptionML=[ENU=Week;
                                                              NOR=Uke;
                                                              SVE=Vecka];
                                                   MinValue=1;
                                                   MaxValue=53;
                                                   Editable=No }
    { 30  ;   ;Receiving Company   ;Text50        ;TableRelation="Site Mngmnt. Hours Companies";
                                                   CaptionML=[ENU=Receiving Company;
                                                              NOR=Mottakselskap;
                                                              SVE=Mottagningsfîretag];
                                                   Editable=No }
    { 40  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projekt- nr];
                                                   Editable=No }
    { 50  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(Labor),
                                                                                               Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   Editable=No;
                                                   CaptionClass='1,1,2' }
    { 55  ;   ;Cost Type           ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value"."Cost Type" WHERE (Global Dimension No.=FILTER(2),
                                                                                                           Code=FIELD(Cost Object)));
                                                   CaptionML=[ENU=Cost Type;
                                                              NOR=Kostnadstype;
                                                              SVE=Kostnadstyp];
                                                   OptionCaptionML=[ENU=Labor,,Subcontracting,Plant,Sundry;
                                                                    NOR=Arbeid,,,Maskin,Diverse];
                                                   OptionString=Labor,,Subcontracting,Plant,Sundry;
                                                   Editable=No }
    { 60  ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Global Dimension No.=FILTER(2),
                                                                                                    Code=FIELD(Cost Object)));
                                                   CaptionML=[ENU=Cost Object Description;
                                                              NOR=Kostnadsobjekt, Beskrivelse;
                                                              SVE=Kostnadsobjekt, beskrivning];
                                                   Editable=No;
                                                   CaptionClass='1,11012000,2, , ' + Text000 }
    { 80  ;   ;Select              ;Boolean       ;CaptionML=[ENU=Select;
                                                              NOR=Velg;
                                                              SVE=VÑlj] }
  }
  KEYS
  {
    {    ;Year,Week,Receiving Company,Project No.,Cost Object;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SiteMgtHrSetup@1210190002 : Record 11012355;
      GLSetup@1100525000 : Record 98;
      Text001@1210190000 : TextConst 'ENU=Building %1 #2###############;NOR=Bygning %1 #2###############;SVE=Byggnad  %1 #2###############';
      Text002@1210190001 : TextConst 'ENU=In week %1-%2 already booked on %3 ''%4'' of %5 ''%6'', this must be modified/deleted first.;NOR=I uke %1-%2 er det allerede bestilt for %3''%4'' av %5 ''%6'', dette mÜ modifiseres/slettes fõrst.;SVE=I vecka %1-%2 redan bokad fîr %3 ''%4'' av %5 ''%6''. Detta mÜste fîrst Ñndras/tas bort.';
      Text000@1100527350 : TextConst 'ENU=Description;SVE=Beskrivning';

    PROCEDURE BuildAllowedCostObjects@1100525003(YearNo@1100525002 : Integer;WeekNo@1100525003 : Integer;ReceivingCompany@1210190003 : Text[50];ProjectNo@1100525001 : Code[20]);
    VAR
      SiteMgtProjHrsHead@1210190001 : Record 11012362;
      BudgetLine@1100525000 : Record 11012001;
      DimVal@1210190002 : Record 349;
      ProjHrCostObjSel@1100525004 : Record 11126059;
      Window@1210190000 : Dialog;
    BEGIN
      //* Build in a temporary table, so in the calling process (form) 'Rec' must be temporary.
      Window.OPEN(STRSUBSTNO(Text001, FIELDCAPTION("Cost Object")));
      RESET;
      DELETEALL;
      IF (YearNo = 0) OR (WeekNo = 0) OR (ReceivingCompany = '') OR (ProjectNo = '') THEN
        EXIT;
      IF NOT SiteMgtProjHrsHead.GET(YearNo, WeekNo, ReceivingCompany, ProjectNo) THEN
        EXIT;

      IF SiteMgtHrSetup.CheckPostingInWeekAllowed(YearNo, WeekNo, ReceivingCompany, FALSE) THEN BEGIN
        BudgetLine.CHANGECOMPANY(ReceivingCompany);
        BudgetLine.SETCURRENTKEY("Project No.", "Cost Type", "Cost Object");
        BudgetLine.SETRANGE("Project No.", ProjectNo);
        BudgetLine.SETRANGE("Budget Summary Line", FALSE);  //* If only Summary lines, handle as no budget
        IF NOT BudgetLine.ISEMPTY THEN BEGIN
          BudgetLine.SETFILTER("Cost Type", '%1|%2|%3|%4',
            BudgetLine."Cost Type"::Labor,BudgetLine."Cost Type"::Subcontracting,
            BudgetLine."Cost Type"::Plant,BudgetLine."Cost Type"::Sundry); //dp01010
          BudgetLine.SETFILTER("Cost Object", '<>%1', '');
          IF BudgetLine.FINDFIRST THEN BEGIN
            REPEAT
              Window.UPDATE(2, BudgetLine."Cost Object");
              IF NOT GET(YearNo, WeekNo, ReceivingCompany, ProjectNo, BudgetLine."Cost Object") THEN BEGIN
                INIT;
                Year := YearNo;
                Week := WeekNo;
                "Receiving Company" := ReceivingCompany;
                "Project No." := ProjectNo;
                "Cost Object" := BudgetLine."Cost Object";
                IF ProjHrCostObjSel.GET(Year, Week, "Receiving Company", "Project No.", "Cost Object") THEN
                  Select := TRUE;
                INSERT;
              END;
              BudgetLine.FILTERGROUP(9);
              BudgetLine.SETRANGE("Cost Type", BudgetLine."Cost Type");
              BudgetLine.SETRANGE("Cost Object", BudgetLine."Cost Object");
              BudgetLine.FINDLAST;  //* Read last record of Cost Object, so on 'UNTIL BudgetLine.NEXT' the next Cost Object
              BudgetLine.SETRANGE("Cost Object");  //* Reset filters
              BudgetLine.SETRANGE("Cost Type");    //* in filtergroup 9
              BudgetLine.FILTERGROUP(0);
            UNTIL BudgetLine.NEXT = 0;
          END;
        END ELSE BEGIN
          GLSetup.GET;
          GLSetup.TESTFIELD("Global Dimension 2 Code");
          DimVal.RESET;
          DimVal.SETRANGE("Dimension Code", GLSetup."Global Dimension 2 Code");
          DimVal.SETFILTER(Code, '<>%1', '');
          DimVal.SETFILTER("Cost Type", '%1|%2|%3|%4',
            DimVal."Cost Type"::Labor,DimVal."Cost Type"::Subcontracting,
            DimVal."Cost Type"::Plant,DimVal."Cost Type"::Sundry);  //dp01010
          DimVal.SETRANGE(Blocked, FALSE);
          IF DimVal.FINDFIRST THEN BEGIN
            REPEAT
              Window.UPDATE(2, DimVal.Code);
              IF NOT GET(YearNo, WeekNo, ReceivingCompany, ProjectNo, DimVal.Code) THEN BEGIN
                INIT;
                Year := YearNo;
                Week := WeekNo;
                "Receiving Company" := ReceivingCompany;
                "Project No." := ProjectNo;
                "Cost Object" := DimVal.Code;
                IF ProjHrCostObjSel.GET(Year, Week, "Receiving Company", "Project No.", "Cost Object") THEN
                  Select := TRUE;
                INSERT;
              END;
            UNTIL DimVal.NEXT = 0;
          END;
        END;
      END;

      //* Insert already selected cost objects, if not present yet.
      ProjHrCostObjSel.RESET;
      ProjHrCostObjSel.SETRANGE(Year, YearNo);
      ProjHrCostObjSel.SETRANGE(Week, WeekNo);
      ProjHrCostObjSel.SETRANGE("Receiving Company", ReceivingCompany);
      ProjHrCostObjSel.SETRANGE("Project No.", ProjectNo);
      IF ProjHrCostObjSel.FINDSET THEN BEGIN
        REPEAT
          Window.UPDATE(2, ProjHrCostObjSel."Cost Object");
          IF NOT GET(YearNo, WeekNo, ReceivingCompany, ProjectNo, ProjHrCostObjSel."Cost Object") THEN BEGIN
            INIT;
            Year := YearNo;
            Week := WeekNo;
            "Receiving Company" := ReceivingCompany;
            "Project No." := ProjectNo;
            "Cost Object" := ProjHrCostObjSel."Cost Object";
            Select := TRUE;
            INSERT;
          END;
        UNTIL ProjHrCostObjSel.NEXT = 0;
      END;

      Window.CLOSE;
    END;

    PROCEDURE OnSelect@1210190002();
    VAR
      ProjHrCostObjSel@1210190001 : Record 11126059;
      SiteMgtProjHrLine@1210190002 : Record 11012363;
    BEGIN
      //* Update from temporary table to 'normal' table
      IF Select THEN BEGIN
        IF NOT ProjHrCostObjSel.GET(Year, Week, "Receiving Company", "Project No.", "Cost Object") THEN BEGIN
          ProjHrCostObjSel := Rec;
          ProjHrCostObjSel.INSERT;
        END ELSE BEGIN
          IF NOT ProjHrCostObjSel.Select THEN BEGIN
            ProjHrCostObjSel.Select := TRUE;
            ProjHrCostObjSel.MODIFY;
          END;
        END;
      END ELSE BEGIN
        SiteMgtProjHrLine.SETRANGE(Year, Year);
        SiteMgtProjHrLine.SETRANGE(Week, Week);
        SiteMgtProjHrLine.SETRANGE("Receiving Company", "Receiving Company");
        SiteMgtProjHrLine.SETRANGE("Project No.", "Project No.");
        SiteMgtProjHrLine.SETRANGE("Cost Object", "Cost Object");
        IF NOT SiteMgtProjHrLine.ISEMPTY THEN
          ERROR(Text002, Year, Week, FIELDCAPTION("Cost Object"), "Cost Object", FIELDCAPTION("Project No."), "Project No.");
        IF ProjHrCostObjSel.GET(Year, Week, "Receiving Company", "Project No.", "Cost Object") THEN
          ProjHrCostObjSel.DELETE;
      END;
    END;

    BEGIN
    END.
  }
}

