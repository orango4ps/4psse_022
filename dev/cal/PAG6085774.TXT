OBJECT Page 6085774 Req. Approval Comment
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.5.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Genehmigungsanmerkung;
               ENU=Approval Comment;
               NLD=Goedkeuringsopmerking];
    LinksAllowed=No;
    PageType=Card;
    OnQueryClosePage=VAR
                       AppvlReasonCode@1160040000 : Record 6085773;
                     BEGIN
                       IF (CloseAction = ACTION::LookupOK) AND (Comment = '') THEN
                         CASE Type OF
                           AppvlReasonCode.Type::Reject:
                             ERROR(InstructRejectTxt);
                           AppvlReasonCode.Type::"Put on Hold":
                             ERROR(InstructPutOnHoldTxt);
                         END;

                       EXIT(TRUE);
                     END;

  }
  CONTROLS
  {
    { 1160040002;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1160040001;1;Group  ;
                Name=Generelt;
                CaptionML=[DEU=Allgemein;
                           ENU=General;
                           NLD=Algemeen];
                GroupType=Group }

    { 1160040006;2;Field  ;
                Name=DocumentTitle;
                CaptionML=[DEU=Beleg;
                           ENU=Document;
                           NLD=Document];
                SourceExpr=DocumentTitle;
                Editable=FALSE }

    { 1160040004;2;Field  ;
                Name=Comment;
                CaptionML=[DEU=Bemerkung;
                           ENU=Comment;
                           NLD=Opmerking];
                SourceExpr=Comment }

    { 1160040005;1;Part   ;
                Name=PrevApprovalCmt;
                CaptionML=[DEU=FrÅhrere Bemerkungen;
                           ENU=Previous Comments;
                           NLD=Eerdere opmerkingen];
                PagePartID=Page6085771;
                PartType=Page }

  }
  CODE
  {
    VAR
      Comment@1160040004 : Text[1024];
      DocumentTitle@1160040008 : Text[1024];
      TableID@1160040002 : Integer;
      DocType@1160040001 : Integer;
      Type@1160040000 : Integer;
      InstructRejectTxt@1160040007 : TextConst 'DEU=Sie mÅssen eine Bemerkung angeben, wenn Sie einen Beleg ablehnen.;ENU=You must specify a comment when you reject a document.;NLD=U dient een opmerking op te geven als u een document weigert.';
      InstructPutOnHoldTxt@1160040003 : TextConst 'DEU=Sie mÅssen eine Bemerkung angeben, wenn Sie einen Beleg auf Abwarten einstellen.;ENU=You must specify a comment when you put a document on hold.;NLD=U dient een opmerking op te geven als u een document op Afwachten zet.';

    PROCEDURE SetParam@1160040004(NewDocumentTitle@1160040006 : Text[250];NewTableID@1160040001 : Integer;NewDocType@1160040000 : Integer;NewDocumentNo@1160040004 : Code[20];NewType@1160040002 : Integer);
    VAR
      AppvlReasonCode@1160040003 : Record 6085773;
      AppvlCmtLine@1160040005 : Record 455;
    BEGIN
      DocumentTitle := NewDocumentTitle;
      TableID := NewTableID;
      DocType := NewDocType;
      Type := NewType;

      AppvlCmtLine.SETCURRENTKEY("Table ID","Document Type","Document No.");
      AppvlCmtLine.ASCENDING(FALSE);
      AppvlCmtLine.FILTERGROUP(4);
      AppvlCmtLine.SETRANGE("Table ID",TableID);
      AppvlCmtLine.SETRANGE("Document Type",DocType);
      AppvlCmtLine.SETRANGE("Document No.",NewDocumentNo);
      AppvlCmtLine.FILTERGROUP(0);
      CurrPage.PrevApprovalCmt.PAGE.SETTABLEVIEW(AppvlCmtLine);
    END;

    PROCEDURE GetValues@1160040013(VAR NewComment@1160040001 : Text[1024]);
    BEGIN
      NewComment := Comment;
    END;

    BEGIN
    END.
  }
}

