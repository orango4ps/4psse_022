OBJECT Codeunit 11012262 RegExp Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      RegExp@1100525003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Regex";
      Matches@1100525002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.MatchCollection";

    PROCEDURE RegExpFindMatches@1100525008(Line@1100525008 : Text;RegExpPattern@1100525010 : Text) : Boolean;
    BEGIN
      RegExp := RegExp.Regex(RegExpPattern);
      EXIT(MatchLine(Line));
    END;

    PROCEDURE Replace@1100528300(Line@1100525008 : Text;RegExpPattern@1100525010 : Text;ReplaceText@1100528300 : Text) : Text;
    BEGIN
      RegExp := RegExp.Regex(RegExpPattern);
      //EXIT(RegExp.Replace(Line, ReplaceText));
      //replace all patterns
      WHILE (RegExpFindMatches(Line, RegExpPattern)) DO BEGIN
        Line := RegExp.Replace(Line, ReplaceText);
      END;
      EXIT(Line);
    END;

    PROCEDURE GetSubMatchFirstMatch@1100525002(SubMatchNo@1100525000 : Integer) : Text;
    BEGIN
      EXIT(GetSubMatch(0, SubMatchNo));
    END;

    LOCAL PROCEDURE GetSubMatch@1100525017(MatchNo@1100525000 : Integer;SubMatchNo@1100525001 : Integer) : Text;
    VAR
      Groups@1100525003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.GroupCollection";
      Match@1100525002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Match";
    BEGIN
      Match := Matches.Item(MatchNo);
      Groups := Match.Groups;
      EXIT(FORMAT(Groups.Item(SubMatchNo + 1))); //First group always contains entire match, so it's not a sub match.
    END;

    PROCEDURE GetSubMatchCountFirstMatch@1100528700() : Integer;
    BEGIN
      EXIT(GetSubMatchCount(0));
    END;

    LOCAL PROCEDURE GetSubMatchCount@1100525016(MatchNo@1100525000 : Integer) : Integer;
    VAR
      Groups@1100525001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.GroupCollection";
      Match@1100525002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Match";
    BEGIN
      Match := Matches.Item(MatchNo);
      Groups := Match.Groups;
      EXIT(Groups.Count - 1); //First group always contains entire match, so it's not a sub match.
    END;

    LOCAL PROCEDURE MatchLine@1100525000(Line@1100525008 : Text) : Boolean;
    BEGIN
      Matches := RegExp.Matches(Line);
      EXIT(MatchCount > 0);
    END;

    LOCAL PROCEDURE MatchCount@1100525011() : Integer;
    BEGIN
      EXIT(Matches.Count);
    END;

    PROCEDURE IsValidBase64String@1100528602(IBase64String@1100528600 : Text) : Boolean;
    VAR
      RegEx@1100528601 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Regex";
    BEGIN
      IF STRLEN(IBase64String) MOD 4 = 0 THEN BEGIN
        IBase64String := CONVERTSTR(IBase64String, '-_', '+/');
        IF RegEx.IsMatch(IBase64String, '^[a-zA-Z0-9\+/]+={0,3}$') THEN
          EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

