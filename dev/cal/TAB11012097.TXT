OBJECT Table 11012097 Document Filter Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF Company = '' THEN
                 Company := COMPANYNAME;
             END;

    CaptionML=[ENU=Document Filter Line;
               NOR=Dokumentfilterrad;
               SVE=Dokumentfilterrad];
  }
  FIELDS
  {
    { 10  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              NOR=Bruker-ID;
                                                              SVE=Anv„ndar-ID] }
    { 20  ;   ;Document Filter     ;Code10        ;TableRelation="Document Filter"."Document Filter" WHERE (User ID=FIELD(User ID));
                                                   CaptionML=[ENU=Document Filter;
                                                              NOR=Filter;
                                                              SVE=Filtrera] }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer] }
    { 35  ;   ;Company             ;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=[ENU=Company;
                                                              NOR=Selskap;
                                                              SVE=F”retag];
                                                   NotBlank=Yes }
    { 40  ;   ;Filter Table No.    ;Integer       ;TableRelation="Possible Document Link"."Table No.";
                                                   OnValidate=BEGIN
                                                                IF "Filter Table No." <> xRec."Filter Table No." THEN
                                                                  "Filter Expression" := '';
                                                              END;

                                                   CaptionML=[ENU=Filter Table No.;
                                                              NOR=Filtrer tabellnr.;
                                                              SVE=Filtrera tabellnr];
                                                   Editable=No }
    { 50  ;   ;Filter Table Filter Expression;Text100;
                                                   CaptionML=[ENU=Filter Table Filter Expression;
                                                              NOR=Filtrer tabellfilteruttrykk;
                                                              SVE=Filtrera tabellfilteruttryck];
                                                   Editable=No }
    { 60  ;   ;Link Field No.      ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Filter Table No.));
                                                   OnValidate=BEGIN
                                                                IF "Link Field No." <> xRec."Link Field No." THEN
                                                                  "Filter Expression" := '';
                                                              END;

                                                   CaptionML=[ENU=Link Field No.;
                                                              NOR=Linkfeltnr.;
                                                              SVE=L„nkf„lt nr] }
    { 80  ;   ;Filter Expression   ;Text250       ;OnLookup=VAR
                                                              DocumentLinkManagement@1100528708 : Codeunit 11012401;
                                                              DocumentFilters@1100528709 : Codeunit 11012432;
                                                              DocumentLink@1100528707 : Record 11012747;
                                                              RID@1100528706 : RecordID;
                                                              RecRef@1100528705 : RecordRef;
                                                              FieldRef@1100528704 : FieldRef;
                                                              OptionFieldTools@1100528703 : Codeunit 11012271;
                                                              ReadableOption@1100528701 : Text[250];
                                                              FieldRec@1100528700 : Record 2000000041;
                                                            BEGIN
                                                              DocumentFilters.GetRecRefWithDocFilLineFilters(Rec, RecRef);

                                                              DocumentLink.INIT;
                                                              DocumentLink."Internal Company" := Company;
                                                              DocumentLink."Table No." := "Filter Table No.";
                                                              IF RecRef.NUMBER <> 0 THEN BEGIN
                                                                DocumentLink."Filter Expression" := RecRef.GETVIEW;
                                                                RecRef.CLOSE;
                                                              END;

                                                              DocumentLinkManagement.SetTable(DocumentLink, TRUE);

                                                              IF FORMAT(DocumentLink."Record ID") <> '' THEN BEGIN
                                                                RID := DocumentLink."Record ID";
                                                                RecRef.OPEN(RID.TABLENO, FALSE, DocumentLink."Internal Company");
                                                                IF RecRef.GET(RID) THEN BEGIN
                                                                  FieldRef := RecRef.FIELD("Link Field No.");
                                                                  FieldRec.GET(RecRef.NUMBER, FieldRef.NUMBER);
                                                                  IF FieldRec.Type = FieldRec.Type::Option THEN BEGIN
                                                                    IF OptionFieldTools.OptionValueToReadableOption2(FieldRef, ReadableOption) THEN
                                                                      "Filter Expression" := "Filter Expression" + ReadableOption;
                                                                  END ELSE
                                                                    "Filter Expression" := "Filter Expression" + FORMAT(FieldRef.VALUE);
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Filter Expression;
                                                              NOR=Filteruttrykk;
                                                              SVE=Filteruttryck] }
    { 90  ;   ;Show in Document List;Boolean      ;CaptionML=[ENU=Show in Document List;
                                                              NOR=Vis i dokumentliste;
                                                              SVE=Visa i dokumentlista] }
  }
  KEYS
  {
    {    ;User ID,Document Filter,Line No.        ;Clustered=Yes }
    {    ;User ID,Document Filter,Company,Filter Table No.,Filter Table Filter Expression }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528700 : TextConst 'ENU=%1 not found!;NOR=%1 ikke funnet;SVE=%1 hittades inte';

    PROCEDURE GetNextFreeLineNo@1100528702() : Integer;
    VAR
      DocumentFilterLine@1100528700 : Record 11012097;
    BEGIN
      DocumentFilterLine.SETRANGE("User ID", "User ID");
      DocumentFilterLine.SETRANGE("Document Filter", "Document Filter");
      IF NOT DocumentFilterLine.FINDLAST THEN
        EXIT(10000)
      ELSE
        EXIT(DocumentFilterLine."Line No." + 10000);
    END;

    PROCEDURE GetLinkField@1199198000(TableNo@1199198001 : Integer;FieldNo@1199198002 : Integer) : Text[80];
    VAR
      Field@1100530000 : Record 2000000041;
    BEGIN
      IF Field.GET(TableNo, FieldNo) THEN
        EXIT(Field."Field Caption");
    END;

    PROCEDURE GetLinkFieldFromText@1100528700(LinkField@1100528700 : Text[250]) : Integer;
    VAR
      Field@1100528701 : Record 2000000041;
      i@1100528704 : Integer;
      Cont@1100528702 : Boolean;
      FieldTableManagement@1100529200 : Codeunit 11012372;
    BEGIN
      IF "Filter Table No." = 0 THEN
        EXIT(0);

      Field.FILTERGROUP(10);
      Field.SETRANGE(TableNo, "Filter Table No.");
      Field.FILTERGROUP(0);

      IF NOT EVALUATE(Field."No.", LinkField) THEN BEGIN
        IF Field.FINDSET THEN BEGIN
          Cont := TRUE;
          REPEAT
            IF UPPERCASE(Field."Field Caption") = UPPERCASE(LinkField) THEN BEGIN
              Field.MARK(TRUE);
              Cont := FALSE;
            END ELSE IF STRPOS(UPPERCASE(Field."Field Caption"), UPPERCASE(LinkField)) = 1 THEN
              Field.MARK(TRUE);
          UNTIL (Field.NEXT = 0) OR (Cont = FALSE);
        END;
        Field.MARKEDONLY(TRUE);
      END ELSE
        Field.SETFILTER("No.", LinkField);

      i := Field.COUNT;

      CASE TRUE OF
        i = 0:
          BEGIN
            ERROR(Text001, Field.FIELDCAPTION("No."));
          END;
        i = 1:
          BEGIN
            Field.FINDFIRST;
            EXIT(Field."No.");
          END;
        i > 1:
          BEGIN
            EXIT(FieldTableManagement.LookupFieldNo(Field.TableNo,Field."No."));
          END;
      END;
    END;

    PROCEDURE GetDocumentLinkCompanyFilter@1100528400() : Text[30];
    VAR
      DocumentMgtSetup@1100528400 : Record 11071831;
    BEGIN
      DocumentMgtSetup.GET;
      IF DocumentMgtSetup."Show Only Own Documents" THEN
        EXIT(Company)
      ELSE
        EXIT('');
    END;

    BEGIN
    END.
  }
}

