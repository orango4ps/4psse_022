OBJECT Report 11125366 IFC Object - Create Pr.Objects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=IFC-Objekt - Projektobjekte erstellen;
               ENU=IFC Object - Create Project Objects;
               NLD=IFC object - Projectobjecten aanmaken];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Selected := FALSE;
                 END;

    OnPreReport=BEGIN
                  IF (CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::All) THEN BEGIN
                    ValidateItemNo(FALSE);
                    ValidateCostObject;
                    ValidatePlotNo;
                    Selected := TRUE;
                  END;
                  IF (CreateFor = CreateFor::Link) THEN
                    Selected := TRUE;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF CurrentLinked THEN
                     CreateFor := CreateFor::Selection;
                   SetEditable;
                   DeterminePropertyActions;
                 END;

      OnQueryClosePage=BEGIN
                         IF (CloseAction = ACTION::OK) AND ((CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::All)) THEN BEGIN
                           ValidateItemNo(FALSE);
                           ValidateCostObject;
                         END;
                       END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525007;2;Field  ;
                  CaptionML=[DEU=Erstellen fÅr;
                             ENU=Create for;
                             NLD=Aanmaken voor];
                  OptionCaptionML=[DEU=Aktuell,Selektion,Verbinden,Alle;
                                   ENU=Current,Selection,Link,All;
                                   NLD=Huidige,Selectie,Koppelen,Alle];
                  SourceExpr=CreateFor;
                  Editable=CreateForEditable;
                  OnValidate=BEGIN
                               SetEditable;
                               IF CreateFor = CreateFor::All THEN BEGIN
                                 ObjectIfcType := '';
                                 PartOfIfcName := '';
                               END;
                             END;
                              }

      { 1100525001;2;Field  ;
                  CaptionML=[DEU=Artikelnr.;
                             ENU=Item No.;
                             NLD=Artikelnr.;
                             NOR=Artikkelnr;
                             SVE=Artikelnr];
                  SourceExpr=ItemNo;
                  TableRelation=Item;
                  Editable=DefaultFieldsEditable;
                  OnValidate=BEGIN
                               ValidateItemNo(TRUE);
                             END;
                              }

      { 1100529000;2;Field  ;
                  CaptionML=[NOR=Kostnadsobjekt.;
                             SVE=Kostnadsobjekt.];
                  SourceExpr=CostObject;
                  CaptionClass='1,1,2';
                  TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                  Editable=DefaultFieldsEditable;
                  OnValidate=BEGIN
                               ValidateCostObject;
                             END;
                              }

      { 1100529002;2;Field  ;
                  CaptionML=[DEU=Baunr.;
                             ENU=Plot No.;
                             NLD=Bouwnr.;
                             NOR=Tegningnr;
                             SVE=Ritningnr];
                  SourceExpr=PlotNo;
                  OnValidate=BEGIN
                               ValidatePlotNo;
                             END;

                  OnLookup=VAR
                             Plot@1100529000 : Record 11012500;
                           BEGIN
                             Plot.FILTERGROUP(9);
                             Plot.SETRANGE("Project No.", ProjectNo);
                             Plot.FILTERGROUP(0);
                             Plot."Plot No." := PlotNo;
                             IF PAGE.RUNMODAL(0, Plot) = ACTION::LookupOK THEN
                               PlotNo := Plot."Plot No.";
                           END;
                            }

      { 1100525003;2;Field  ;
                  CaptionML=[DEU=(Teil des) Name(ns);
                             ENU=(Part of) Name;
                             NLD=(Deel van) naam];
                  SourceExpr=PartOfIfcName;
                  Editable=SelectionFieldsEditable }

      { 1100529001;2;Field  ;
                  CaptionML=[DEU=Eigenschaftenaktionen;
                             ENU=Property Actions;
                             NLD=Eigenschap acties];
                  OptionCaptionML=[DEU=Keine,Kopieren,Filtern,Kopieren und filtern;
                                   ENU=None,Copy,Filter,Copy & Filter;
                                   NLD=Geen,Kopiâren,Filteren,Kopiâren & filteren];
                  SourceExpr=PropertyActions;
                  Editable=PropertyActionsEditable;
                  OnAssistEdit=BEGIN
                                 CopyBaseIfcPropertyForProjectObject(TmpIfcObjectProperty);
                                 TmpIfcObjectProperty.Bool4 := TRUE;
                                 TmpIfcObjectProperty.Bool5 := (CreateFor <> CreateFor::Current);
                                 TmpIfcObjectProperty.Int1 := DATABASE::"Project Object";
                                 PAGE.RUNMODAL(PAGE::"IFC Object Properties", TmpIfcObjectProperty);
                                 DeterminePropertyActions;
                               END;
                                }

      { 1100529401;2;Field  ;
                  CaptionML=[DEU=Bildpositionsfilter;
                             ENU=Picture Position Filter;
                             NLD=Afbeeldingpositiefilter];
                  SourceExpr=PicturePositionFilterCaption;
                  Editable=FALSE;
                  HideValue=PicturePositionFilterHideValue;
                  OnAssistEdit=VAR
                                 SelectPicturePositions@1100529400 : Page 11130594;
                               BEGIN
                                 IF PicturePositionFilterHideValue THEN
                                   EXIT;
                                 SelectPicturePositions.SetSelection(PicturePositions);
                                 IF SelectPicturePositions.RUNMODAL = ACTION::OK THEN BEGIN
                                   SelectPicturePositions.GetSelection(PicturePositions);
                                   PicturePositionFilterCaption := SelectPicturePositions.CreatePicturePositionFilterCaption(PicturePositions);
                                   IF STRPOS(PicturePositions, '1') = 0 THEN
                                     EXIT;
                                 END;
                               END;
                                }

      { 1100525005;1;Group  ;
                  CaptionML=[DEU=Aktuell;
                             ENU=Current;
                             NLD=Huidig;
                             SVE=Current];
                  Editable=true;
                  GroupType=Group }

      { 1100525004;2;Field  ;
                  CaptionML=[DEU=IFC-Typ;
                             ENU=IFC Type;
                             NLD=IFC type];
                  SourceExpr=ObjectIfcType;
                  Editable=ObjectIfcTypeEditable;
                  QuickEntry=true }

      { 1100525006;2;Field  ;
                  Lookup=No;
                  CaptionML=[DEU=Projektnr.;
                             ENU=Project No.;
                             NLD=Projectnr.;
                             NOR=Prosjektnr.;
                             SVE=Projektnr];
                  SourceExpr=ProjectNo;
                  TableRelation=Job;
                  Editable=FALSE;
                  QuickEntry=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TmpIfcObjectProperty@1100529008 : TEMPORARY Record 11020579;
      ItemNo@1100525000 : Code[20];
      CostObject@1100529002 : Code[20];
      ProjectNo@1100525004 : Code[20];
      PlotNo@1100529005 : Code[10];
      ObjectIfcType@1100525002 : Text;
      PartOfIfcName@1100525003 : Text;
      CurrentLinked@1100529000 : Boolean;
      Selected@1100525001 : Boolean;
      CreateForEditable@1100529001 : Boolean INDATASET;
      DefaultFieldsEditable@1100529003 : Boolean INDATASET;
      SelectionFieldsEditable@1100525006 : Boolean INDATASET;
      PropertyActionsEditable@1100529004 : Boolean INDATASET;
      PicturePositionFilterHideValue@1100529401 : Boolean INDATASET;
      CreateFor@1100525005 : 'Current,Selection,Link,All';
      PropertyActions@1100529009 : 'None,Copy,Filter,Copy&Filter';
      PicturePositions@1100529402 : Code[10];
      PicturePositionFilterCaption@1100529400 : Text;
      ObjectIfcTypeEditable@1100527350 : Boolean INDATASET;

    PROCEDURE SetSelection@1100525000(IfcType@1100525001 : Text;IfcName@1100525000 : Text;ProjectNo2@1100529000 : Code[20];CurrentLinked2@1100529001 : Boolean;VAR TmpIfcObjectProperty2@1100529002 : TEMPORARY Record 11020579);
    BEGIN
      IF CreateFor = CreateFor::All THEN BEGIN
        ObjectIfcType := '';
        PartOfIfcName := '';
      END ELSE BEGIN
        ObjectIfcType := IfcType;
        PartOfIfcName := IfcName;
      END;
      ProjectNo := ProjectNo2;
      CurrentLinked := CurrentLinked2;
      TmpIfcObjectProperty.COPY(TmpIfcObjectProperty2, TRUE);
    END;

    PROCEDURE GetSelection@1100525001(VAR CreateFor2@1100525000 : Integer;VAR PartOfName@1100525001 : Text;VAR ItemNo2@1100525002 : Code[20];VAR CostObject2@1100529000 : Code[20];VAR PlotNo2@1100529003 : Code[10];VAR PropertyName2@1100529002 : Text;VAR PropertyValue2@1100529001 : Text;VAR PicturePositions2@1100529400 : Code[10]);
    BEGIN
      CreateFor2 := 0;
      PartOfName := '';
      ItemNo2 := '';
      CostObject2 := '';
      PlotNo2 := '';
      PropertyName2 := '';
      PropertyValue2 := '';
      PicturePositions2 := PicturePositions;
      IF Selected THEN BEGIN
        CreateFor2 := CreateFor + 1;
        PartOfName := PartOfIfcName;
        ItemNo2 := ItemNo;
        CostObject2 := CostObject;
        PlotNo2 := PlotNo;
        IF CreateFor = CreateFor::Current THEN BEGIN
          TmpIfcObjectProperty.RESET;
          TmpIfcObjectProperty.MODIFYALL(Bool2, FALSE);  //Tmp table contains all properties of current IFC object, so no further filtering needed
        END ELSE BEGIN
          TmpIfcObjectProperty.RESET;
          TmpIfcObjectProperty.SETRANGE(Bool2, TRUE);
          TmpIfcObjectProperty.SETFILTER(Text5, '<>%1', '');
          IF TmpIfcObjectProperty.FINDFIRST THEN BEGIN
            PropertyName2 := TmpIfcObjectProperty.Text1;
            PropertyValue2 := TmpIfcObjectProperty.Text5;
          END ELSE BEGIN
            TmpIfcObjectProperty.SETRANGE(Text5);
            IF TmpIfcObjectProperty.FINDFIRST THEN
              PropertyName2 := TmpIfcObjectProperty.Text1;
          END;
          TmpIfcObjectProperty.RESET;
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateItemNo@1100525002(FromField@1100529000 : Boolean);
    VAR
      Item@1100525000 : Record 27;
    BEGIN
      IF ItemNo <> '' THEN BEGIN
        Item.GET(ItemNo);
        IF FromField AND (Item."Global Dimension 2 Code" <> '') THEN
          CostObject := Item."Global Dimension 2 Code";
      END;
    END;

    LOCAL PROCEDURE ValidateCostObject@1100529001();
    VAR
      DimVal@1100525000 : Record 349;
      DimMgt@1100529000 : Codeunit 408;
    BEGIN
      IF CostObject <> '' THEN
        DimMgt.GetDimValueRec(2, CostObject, DimVal, TRUE, '');
    END;

    LOCAL PROCEDURE ValidatePlotNo@1100529002();
    VAR
      Plot@1100525000 : Record 11012500;
    BEGIN
      IF PlotNo <> '' THEN
        Plot.GET(ProjectNo, PlotNo);
    END;

    LOCAL PROCEDURE SetEditable@1100525003();
    BEGIN
      CreateForEditable := (NOT CurrentLinked);
      SelectionFieldsEditable := ((CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::Link) OR (CreateFor = CreateFor::All));
      DefaultFieldsEditable := ((CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::All));
      PropertyActionsEditable := FALSE; // Only editable via lookup if SelectionFieldsEditable
      PicturePositionFilterHideValue := (CreateFor = CreateFor::Link);
      ObjectIfcTypeEditable := (CreateFor = CreateFor::All);
    END;

    LOCAL PROCEDURE DeterminePropertyActions@1100529000();
    VAR
      TmpProperty@1100529002 : TEMPORARY Record 11020579;
      CopySelected@1100529001 : Boolean;
      FilterSelected@1100529000 : Boolean;
    BEGIN
      TmpProperty.COPY(TmpIfcObjectProperty, TRUE);
      TmpProperty.RESET;
      TmpProperty.SETRANGE(Bool1, TRUE);
      CopySelected := (NOT TmpProperty.ISEMPTY);
      TmpProperty.SETRANGE(Bool1);
      TmpProperty.SETRANGE(Bool2, TRUE);
      FilterSelected := (NOT TmpProperty.ISEMPTY);
      TmpProperty.SETRANGE(Bool2);
      PropertyActions := PropertyActions::None;
      IF CopySelected AND FilterSelected THEN
        PropertyActions := PropertyActions::"Copy&Filter"
      ELSE BEGIN
        IF CopySelected THEN
          PropertyActions := PropertyActions::Copy;
        IF FilterSelected THEN
          PropertyActions := PropertyActions::Filter;
      END;
    END;

    PROCEDURE GetIfcObjectTypeForAll@1100527350(VAR IfcObjectType2@1100527350 : Text[50]);
    BEGIN
      IF CreateFor = CreateFor::All THEN
        IfcObjectType2 :=  ObjectIfcType;
    END;

    LOCAL PROCEDURE CopyBaseIfcPropertyForProjectObject@1100527352(VAR TmpIfcObjectProperty@1100527350 : TEMPORARY Record 11020579);
    VAR
      BaseIfcPropertyForProjectObject@1100527353 : Record 11125828;
      TmpIfcObjectProperty2@1100527352 : TEMPORARY Record 11020579;
      KeyCode@1100527351 : Text[10];
    BEGIN
      TmpIfcObjectProperty2.COPY(TmpIfcObjectProperty, TRUE);
      BaseIfcPropertyForProjectObject.SETRANGE("Table No.", 11229311);
      IF BaseIfcPropertyForProjectObject.FINDSET THEN BEGIN
        REPEAT
          TmpIfcObjectProperty2.RESET;
          TmpIfcObjectProperty2.SETRANGE(Text1, BaseIfcPropertyForProjectObject.Property);
          IF NOT TmpIfcObjectProperty2.FINDFIRST THEN BEGIN
            KeyCode := FORMAT(TmpIfcObjectProperty.COUNT + 1);
            TmpIfcObjectProperty.INIT;
            TmpIfcObjectProperty."Key Code1" := COPYSTR('0000000000', 1, 10-STRLEN(TmpIfcObjectProperty."Key Code1")) + KeyCode;
            TmpIfcObjectProperty.Text1 := BaseIfcPropertyForProjectObject.Property;
            TmpIfcObjectProperty.Bool1 := TRUE;
            TmpIfcObjectProperty.Bool2 := FALSE;
            TmpIfcObjectProperty.Int1 := 11229311;
            TmpIfcObjectProperty.Int2 := 1;
            TmpIfcObjectProperty.INSERT;
            END;
          UNTIL BaseIfcPropertyForProjectObject.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

