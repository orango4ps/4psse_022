OBJECT Page 11125056 Assembly Objects SM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Montagespeicher Objekt;
               ENU=Assembly Objects SM;
               NLD=Assemblageobjecten SO;
               NOR=Kapslingsobjekt SM;
               SVE=Kapslingsobjekt SM];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11020596;
    PageType=List;
    OnOpenPage=VAR
                 Project@1100528300 : Record 11072003;
               BEGIN
                 IF CentralDataManagement.CentralActive THEN
                   CentralDataMgtActive := TRUE;

                 FrmTypeEditable := TRUE;
                 FrmCompany := COMPANYNAME;
                 Project.GET("Project No.");
                 SetCustomerFilter;
                 SetProjectAddressData;

                 OldFrmCode := FrmCode;
                 OldFrmType := FrmType;
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528317;1 ;Action    ;
                      CaptionML=[DEU=Process;
                                 ENU=Process;
                                 NLD=Verwerken;
                                 NOR=Process;
                                 SVE=Process];
                      Promoted=Yes;
                      Image=Post;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Process;
                               END;
                                }
      { 1100525005;1 ;Action    ;
                      CaptionML=[DEU=Objekt mit Standardwert ausfÅllen;
                                 ENU=Fill Object with default Value;
                                 NLD=Object vullen met standaard waarde];
                      Image=Action;
                      OnAction=BEGIN
                                 FillDefaultObject;
                                 CurrPage.UPDATE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528316;1;Group  ;
                CaptionML=[DEU=Optionen;
                           ENU=Options;
                           NLD=Opties;
                           NOR=Alternativer;
                           SVE=Alternativ] }

    { 1100528200;2;Field  ;
                CaptionML=[DEU=Ziel-Unternehmen;
                           ENU=Target Company;
                           NLD=Doelbedrijf];
                SourceExpr=FrmCompany;
                TableRelation=Company;
                OnValidate=BEGIN
                             FrmTypeEditable := TRUE;
                             IF (COMPANYNAME <> FrmCompany) THEN BEGIN
                               IF NOT CheckSharedData THEN BEGIN
                                 FrmTypeEditable := FALSE;
                                 FrmType := FrmType::Customer;
                               END;
                             END;
                             FrmCode := '';
                             SetProjectAddressData;
                           END;
                            }

    { 1100528318;2;Field  ;
                CaptionML=[DEU=Art Auftraggeber;
                           ENU=Principal Type;
                           NLD=Soort opdrachtgever;
                           NOR=Type;
                           SVE=Radtyp];
                OptionCaptionML=[DEU=Debitor,Auftraggeber;
                                 ENU=Customer,Principal;
                                 NLD=Klant,Opdrachtgever;
                                 NOR=Kunde,Oppdragsgiver;
                                 SVE=Kund,Uppdragsgivare];
                SourceExpr=FrmType;
                Enabled=FrmTypeEditable;
                Editable=FrmTypeEditable;
                OnValidate=BEGIN
                             FrmCode := '';
                             SetProjectAddressData;
                           END;
                            }

    { 1100528319;2;Field  ;
                CaptionML=[DEU=Debitor/Auftraggeber;
                           ENU=Customer/Principal;
                           NLD=Klant/Opdrachtgever];
                SourceExpr=FrmCode;
                OnValidate=BEGIN
                             IF (ValidateFrmCode) THEN BEGIN
                               SetNewAddressData;
                               ResetAddressData;
                             END;
                           END;

                OnLookup=VAR
                           Customer@1100528300 : Record 18;
                           ProjectPrincipal@1100528301 : Record 11012005;
                         BEGIN
                           Customer.CHANGECOMPANY(FrmCompany);
                           CASE FrmType OF
                             FrmType::Customer: BEGIN
                               IF FrmTypeEditable THEN
                                 Customer.SETFILTER("No.", CustomerFilter);
                               IF (PAGE.RUNMODAL(0, Customer) = ACTION::LookupOK) THEN
                                 FrmCode := Customer."No.";
                             END;
                             FrmType::Principal: BEGIN
                               ProjectPrincipal.FILTERGROUP(9);
                               ProjectPrincipal.SETRANGE("Project No.", "Project No.");
                               ProjectPrincipal.FILTERGROUP(0);
                               IF (PAGE.RUNMODAL(0, ProjectPrincipal) = ACTION::LookupOK) THEN
                                 FrmCode := ProjectPrincipal.Principal;
                             END;
                           END;
                           IF (ValidateFrmCode) THEN BEGIN
                             SetNewAddressData;
                             ResetAddressData;
                           END;
                         END;
                          }

    { 1100528324;2;Field  ;
                CaptionML=[DEU=Name;
                           ENU=Name;
                           NLD=Naam;
                           NOR=Name;
                           SVE=Namn];
                SourceExpr=GetFrmName }

    { 1100528330;2;Field  ;
                CaptionML=[DEU=Lagerort;
                           ENU=Location;
                           NLD=Locatie;
                           NOR=Plassering;
                           SVE=LagerstÑlle];
                SourceExpr=FrmLocation;
                TableRelation="Service Location";
                OnValidate=BEGIN
                             SetLocationAddressData;
                             ValidateFrmLocation;
                           END;

                OnLookup=VAR
                           ServiceLocation@1100525000 : Record 11012801;
                         BEGIN
                           ServiceLocation.CHANGECOMPANY(FrmCompany);
                           IF (FrmType = FrmType::Customer) THEN
                             ServiceLocation.SETRANGE("Customer No.", FrmCode);
                           IF (PAGE.RUNMODAL(0, ServiceLocation) = ACTION::LookupOK) THEN
                             FrmLocation := ServiceLocation."No.";
                           SetLocationAddressData;
                           ValidateFrmLocation;
                         END;
                          }

    { 1100528320;2;Field  ;
                CaptionML=[DEU=Adresse;
                           ENU=Address;
                           NLD=Adres;
                           NOR=Address;
                           SVE=Adress];
                SourceExpr=FrmAddress;
                OnValidate=VAR
                             Address2@1100525004 : Text[50];
                             CountryRegionCode@1100525003 : Code[10];
                             PhoneNo@1100525002 : Text[30];
                             FaxNo@1100525001 : Text[30];
                           BEGIN
                           END;
                            }

    { 1100528321;2;Field  ;
                CaptionML=[DEU=PLZ-Code;
                           ENU=Post Code;
                           NLD=Postcode;
                           NOR=Postnr.;
                           SVE=Postnr];
                SourceExpr=FrmPostCode;
                TableRelation="Post Code".Code;
                OnValidate=VAR
                             PostCode@1100525000 : Record 225;
                             CountryDesc@1100525001 : Text[30];
                             CountryRegionCode@1100525002 : Code[10];
                           BEGIN
                             PostCode.ValidatePostCode(FrmCity, FrmPostCode, CountryDesc, CountryRegionCode, TRUE);
                           END;
                            }

    { 1100528329;2;Field  ;
                CaptionML=[DEU=Ort;
                           ENU=City;
                           NLD=Plaats;
                           NOR=Poststed;
                           SVE=Ort];
                SourceExpr=FrmCity;
                TableRelation="Post Code".City }

    { 1100528332;2;Field  ;
                CaptionML=[DEU=Startdatum;
                           ENU=Start Date;
                           NLD=Ingangsdatum;
                           NOR=Startdato;
                           SVE=Startdatum];
                SourceExpr=FrmStartDate }

    { 1100525000;2;Field  ;
                CaptionML=[DEU=Art;
                           ENU=Type;
                           NLD=Type;
                           NOR=Radtype;
                           SVE=Typ];
                OptionCaptionML=[DEU=Vertragsvorlage,Vertrag;
                                 ENU=Contract Template,Contract;
                                 NLD=Contractsjabloon,Contract;
                                 NOR=Kontraktmal,Kontrakt;
                                 SVE=Kontraktmall,Kontrakt];
                SourceExpr=FrmContractType;
                OnValidate=BEGIN
                             FrmContractTypeCode := '';
                           END;
                            }

    { 1100525001;2;Field  ;
                CaptionML=[DEU=Vertragsvorlage/Vertrag;
                           ENU=Contract Template/Contract;
                           NLD=Contractsjabloon/Contract];
                SourceExpr=FrmContractTypeCode;
                OnValidate=VAR
                             ServiceContract@1100525001 : Record 11012812;
                             ServiceContractTemplate@1100525000 : Record 11071754;
                           BEGIN
                             ServiceContractTemplate.CHANGECOMPANY(FrmCompany);
                             ServiceContract.CHANGECOMPANY(FrmCompany);
                             IF (FrmContractTypeCode <> '') THEN BEGIN
                               CASE FrmContractType OF
                                 FrmContractType::"Contract Template": ServiceContractTemplate.GET(FrmContractTypeCode);
                                 FrmContractType::Contract: ServiceContract.GET(FrmContractTypeCode);
                               END;
                             END;
                           END;

                OnLookup=VAR
                           ServiceContract@1100528300 : Record 11012812;
                           ServiceContractTemplate@1100528301 : Record 11071754;
                         BEGIN
                           ServiceContractTemplate.CHANGECOMPANY(FrmCompany);
                           ServiceContract.CHANGECOMPANY(FrmCompany);
                           CASE FrmContractType OF
                             FrmContractType::"Contract Template": BEGIN
                               IF (PAGE.RUNMODAL(0, ServiceContractTemplate) = ACTION::LookupOK) THEN
                                 FrmContractTypeCode := ServiceContractTemplate.Code;
                             END;
                             FrmContractType::Contract: BEGIN
                               ServiceContract.FILTERGROUP(9);
                               ServiceContract.SETRANGE(Blocked, FALSE);
                               ServiceContract.SETFILTER(Status, '<%1', ServiceContract.Status::Expired);
                               ServiceContract.FILTERGROUP(0);
                               IF (FrmType = FrmType::Customer) AND (FrmCode <> '') THEN
                                 ServiceContract.SETRANGE("Customer No.", FrmCode);
                               IF (PAGE.RUNMODAL(0, ServiceContract) = ACTION::LookupOK) THEN
                                 FrmContractTypeCode := ServiceContract."No.";
                             END;
                           END;

                           IF (FrmContractTypeCode <> '') THEN BEGIN
                             CASE FrmContractType OF
                               FrmContractType::"Contract Template": ServiceContractTemplate.GET(FrmContractTypeCode);
                               FrmContractType::Contract: ServiceContract.GET(FrmContractTypeCode);
                             END;
                           END;
                         END;
                          }

    { 1100525002;2;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=GetFrmContractTypeName }

    { 1100528300;1;Group  ;
                GroupType=Repeater }

    { 1100528301;2;Field  ;
                SourceExpr="Project No.";
                Editable=FALSE }

    { 1100528303;2;Field  ;
                SourceExpr="Yard No.";
                Editable=FALSE }

    { 1100528305;2;Field  ;
                SourceExpr="Entity Type";
                Editable=FALSE }

    { 1100528307;2;Field  ;
                SourceExpr="System No.";
                Editable=FALSE }

    { 1100528309;2;Field  ;
                SourceExpr="Entity No.";
                Editable=FALSE }

    { 1100528311;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE }

    { 1100528322;2;Field  ;
                SourceExpr=Quantity;
                Editable=FALSE }

    { 1210190000;2;Field  ;
                SourceExpr="Qty. Service Objects" }

    { 1100525003;2;Field  ;
                SourceExpr="Standard Object No.";
                OnValidate=VAR
                             StandardObject@1100528200 : Record 11020334;
                           BEGIN
                             StandardObject.CHANGECOMPANY(FrmCompany);
                             StandardObject.GET("Standard Object No.");
                             StandardObject.TESTFIELD(Expired, FALSE);
                             "Target Company" := FrmCompany;
                           END;

                OnLookup=VAR
                           StandardObject@1100528200 : Record 11020334;
                         BEGIN
                           StandardObject.CHANGECOMPANY(FrmCompany);
                           IF (PAGE.RUNMODAL(0, StandardObject) = ACTION::LookupOK) THEN BEGIN
                             "Target Company" := FrmCompany;
                             VALIDATE("Standard Object No.", StandardObject."No.");
                           END;
                         END;
                          }

    { 1100525004;2;Field  ;
                SourceExpr="Object No." }

    { 1100527700;2;Field  ;
                SourceExpr="Identification Number";
                Visible=false }

  }
  CODE
  {
    VAR
      FrmType@1100528300 : 'Customer,Principal';
      FrmCode@1100528302 : Code[20];
      FrmContractType@1100525000 : 'Contract Template,Contract';
      FrmContractTypeCode@1100528306 : Code[20];
      FrmLocation@1100528308 : Code[20];
      Text000@1100528301 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified.;NLD=%1 moet ingevuld worden.;NOR=%1 mÜ spesifiseres.;SVE=%1 mÜste specificeras.';
      FrmStartDate@1100528311 : Date;
      Text001@1100528312 : TextConst 'DEU=Startdatum;ENU=Start Date;NLD=Ingangsdatum;NOR=Startdato;SVE=Startdatum';
      Text002@1100525001 : TextConst 'DEU=Mîchten Sie die Adressendaten Ñndern: \\Alt: \%1 \%2 \\ Neu: \%3 \%4;ENU="Do you want to change Address Data: \\Old: \%1 \%2  \\New: \%3 \%4 ";NLD=Wilt u de adresgegevens wijzigen: \\Oud: \%1 \%2 \\Nieuw: \%3 \%4;SVE="Kontrakt "';
      Text003@1210190000 : TextConst 'DEU=%1 %2 existiert nicht.;ENU=%1 ''%2'' does not exist.;NLD=%1 ''%2'' bestaat niet.;NOR=%1 ''%2'' finnes ikke.;SVE=%1 %2 finns inte.';
      FrmAddress@1100525010 : Text[100];
      FrmPostCode@1100525009 : Code[20];
      FrmCity@1100525008 : Text[30];
      CustomerFilter@1100525007 : Code[1024];
      OldFrmCode@1100525006 : Code[20];
      OldFrmType@1100525005 : Option;
      NewFrmAddress@1100525004 : Text[100];
      NewFrmPostCode@1100525003 : Code[20];
      NewFrmCity@1100525002 : Text[30];
      FrmCompany@1100528202 : Text[50];
      FrmTypeEditable@1100528200 : Boolean INDATASET;
      CentralDataManagement@1100528201 : Codeunit 11012010;
      CentralDataMgtActive@1100528203 : Boolean;

    PROCEDURE Process@1100528302();
    VAR
      ServiceObjectMgt@1100528300 : Codeunit 11012808;
    BEGIN
      IF NOT ((ValidateFrmCode) AND (ValidateFrmLocation)) THEN
        EXIT;
      IF (FrmStartDate = 0D) THEN
        ERROR(Text000, Text001);

      ServiceObjectMgt.CreateFromAYNO(Rec, FrmCompany, FrmCode, FrmLocation, FrmContractType, FrmContractTypeCode,
        FrmStartDate, FrmAddress, FrmPostCode, FrmCity);

      CurrPage.UPDATE;
    END;

    PROCEDURE FillDefaultObject@1100525001();
    VAR
      ServiceObjectMgt@1100525000 : Codeunit 11012808;
    BEGIN
      ServiceObjectMgt.FillAYNOObjectDefaults(Rec);
    END;

    PROCEDURE GetFrmName@1100528300() : Text[100];
    VAR
      Customer@1100528300 : Record 18;
      ProjectPrincipal@1100528301 : Record 11012005;
    BEGIN
      Customer.CHANGECOMPANY(FrmCompany);
      CASE FrmType OF
        FrmType::Customer: BEGIN
          IF (Customer.GET(FrmCode)) THEN
            EXIT(Customer.Name);
        END;
        FrmType::Principal: BEGIN
          IF (ProjectPrincipal.GET("Project No.", FrmCode)) THEN BEGIN
            ProjectPrincipal.CALCFIELDS("Principal Name");
            EXIT(ProjectPrincipal."Principal Name");
          END;
        END;
      END;

      EXIT('');
    END;

    PROCEDURE GetFrmContractTypeName@1100525000() : Text[50];
    VAR
      ServiceContract@1100528300 : Record 11012812;
      ServiceContractTemplate@1100528301 : Record 11071754;
    BEGIN
      ServiceContractTemplate.CHANGECOMPANY(FrmCompany);
      ServiceContract.CHANGECOMPANY(FrmCompany);
      CASE FrmContractType OF
        FrmContractType::"Contract Template": BEGIN
          IF (ServiceContractTemplate.GET(FrmContractTypeCode)) THEN
            EXIT(ServiceContractTemplate.Description);
        END;
        FrmContractType::Contract: BEGIN
          IF (ServiceContract.GET(FrmContractTypeCode)) THEN BEGIN
            EXIT(ServiceContract.Description);
          END;
        END;
      END;

      EXIT('');
    END;

    PROCEDURE ValidateFrmCode@1210190000() : Boolean;
    VAR
      Customer@1210190002 : Record 18;
      ProjectPrincipal@1210190001 : Record 11012005;
    BEGIN
      IF (FrmCode = '') THEN
        EXIT(TRUE);
      Customer.CHANGECOMPANY(FrmCompany);
      CASE FrmType OF
        FrmType::Customer: BEGIN
          IF (NOT Customer.GET(FrmCode)) THEN BEGIN
            MESSAGE(Text003, Customer.TABLECAPTION, FrmCode);
            EXIT(FALSE);
          END;
        END;
        FrmType::Principal: BEGIN
          IF (NOT ProjectPrincipal.GET("Project No.", FrmCode)) THEN BEGIN
            MESSAGE(Text003, ProjectPrincipal.TABLECAPTION, FrmCode);
            EXIT(FALSE);
          END;
        END;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE ValidateFrmLocation@1100525002() : Boolean;
    VAR
      ServiceLocation@1100525000 : Record 11012801;
    BEGIN
      ServiceLocation.CHANGECOMPANY(FrmCompany);
      IF (FrmLocation = '') THEN
        EXIT(TRUE);

      IF (NOT ServiceLocation.GET(FrmLocation)) THEN BEGIN
        MESSAGE(Text003, ServiceLocation.TABLECAPTION, FrmCode);
        EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE SetLocationAddressData@1100525003();
    VAR
      ServiceLocation@1100525000 : Record 11012801;
    BEGIN
      ServiceLocation.CHANGECOMPANY(FrmCompany);
      FrmPostCode := '';
      FrmPostCode := '';
      FrmCity := '';

      IF (NOT ServiceLocation.GET(FrmLocation)) THEN
        EXIT;

      FrmAddress := ServiceLocation.Address;
      FrmPostCode := ServiceLocation."Post Code";
      FrmCity := ServiceLocation.City;
    END;

    PROCEDURE SetProjectAddressData@1100525004();
    VAR
      Project@1100525000 : Record 11072003;
    BEGIN
      FrmAddress := '';
      FrmPostCode := '';
      FrmCity := '';

      IF (NOT Project.GET("Project No.")) THEN
        EXIT;

      FrmAddress := Project.Address;
      FrmPostCode := Project."Post Code";
      FrmCity := Project.City;
    END;

    PROCEDURE SetCustomerFilter@1100525005();
    VAR
      ProjectPrincipal@1100525000 : Record 11012005;
    BEGIN
      ProjectPrincipal.SETRANGE("Project No.", "Project No.");
      IF (NOT ProjectPrincipal.FINDSET) THEN
        EXIT;

      REPEAT
        IF (STRLEN(CustomerFilter) + STRLEN(ProjectPrincipal.Principal) + 1 <= MAXSTRLEN(CustomerFilter)) THEN BEGIN
          IF (CustomerFilter = '') THEN
            CustomerFilter := ProjectPrincipal.Principal
          ELSE
            CustomerFilter := CustomerFilter + '|' + ProjectPrincipal.Principal;
        END;
      UNTIL (ProjectPrincipal.NEXT = 0);
    END;

    PROCEDURE SetNewAddressData@1100525006();
    VAR
      Customer@1100525000 : Record 18;
    BEGIN
      Customer.CHANGECOMPANY(FrmCompany);
      NewFrmAddress := FrmAddress;
      NewFrmPostCode := FrmPostCode;
      NewFrmCity := FrmCity;

      IF (FrmCode = '') THEN
        EXIT;

      Customer.GET(FrmCode);
      NewFrmAddress := Customer.Address;
      NewFrmPostCode := Customer."Post Code";
      NewFrmCity := Customer.City;
    END;

    PROCEDURE ResetAddressData@1100525007();
    BEGIN
      IF ((OldFrmType <> FrmType) OR (OldFrmCode <> FrmCode)) AND
         ((NewFrmAddress <> FrmAddress) OR (NewFrmPostCode <> FrmPostCode) OR (NewFrmCity <> FrmCity))
      THEN BEGIN
        IF (CONFIRM(Text002, TRUE,
             FrmAddress, (FrmPostCode + '  ' + FrmCity),
             NewFrmAddress, (NewFrmPostCode + '  ' + NewFrmCity)))
        THEN BEGIN
          FrmAddress := NewFrmAddress;
          FrmPostCode := NewFrmPostCode;
          FrmCity := NewFrmCity;
        END;
      END;
    END;

    LOCAL PROCEDURE CheckSharedData@1100528205() : Boolean;
    VAR
      CompanyRelationsCentralMgt@1100528200 : Record 11012059;
      CompanyRelationsCentralMgt2@1100528201 : Record 11012059;
    BEGIN
      IF NOT CentralDataMgtActive THEN
        EXIT(FALSE);
      IF NOT CompanyRelationsCentralMgt.GET(COMPANYNAME) THEN
        EXIT(FALSE);
      IF NOT CompanyRelationsCentralMgt2.GET(FrmCompany) THEN
        EXIT(FALSE);
      IF CompanyRelationsCentralMgt."Company Contact Data" = '' THEN
        EXIT(FALSE);
      IF CompanyRelationsCentralMgt."Company Contact Data" <> CompanyRelationsCentralMgt2."Company Contact Data" THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

