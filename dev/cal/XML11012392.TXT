OBJECT XMLport 11012392 Import KPD-Budget GWW
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import KPD-Budget GWW;
               SVE=Import KPD-Budget GWW];
    Direction=Import;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnPreXMLport=BEGIN
                   ProjSetup.GET;
                   GLSetup.GET;

                   LineCounter := 0;
                   LineNo := 0;
                 END;

    OnPostXMLport=VAR
                    lvSumSheetRec@1100525000 : Record 11012064;
                  BEGIN
                    lvSumSheetRec."Project No." := ProjectCde;
                    lvSumSheetRec."Contract No." := ExtensionCde;
                    lvSumSheetRec."Plot No." := PlotCde;
                    lvSumSheetRec."House Model" := HouseModelCde;
                    lvSumSheetRec."Main Group" := MainGroupCde;
                    lvSumSheetRec.Group := GroupCde;
                    lvSumSheetRec."Sub Group" := SubGroupCde;
                    lvSumSheetRec.Option := OptionCde;
                    IF OptionCde <> '' THEN BEGIN
                      lvSumSheetRec.ReCalcOptionSummaryLines();
                      lvSumSheetRec.UpdateOptBudgetLines();
                    END ELSE BEGIN
                      lvSumSheetRec.ReCalcSummaryLines(AdjustmentCde);
                      lvSumSheetRec.UpdateBudgetLines(TRUE,AdjustmentCde);
                    END;

                    CheckUpdateOption();

                    IF TotalImportAmnt <> TotalCalcAmnt THEN
                      StoreMessage(0,'',STRSUBSTNO(Text999,TotalImportAmnt,TotalCalcAmnt,TotalCalcAmnt-TotalImportAmnt));
                  END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{7E26F539-CADE-4F90-8EF6-7288C5E35BFB}];  ;Root                ;Element ;Text     }

    { [{DA94C704-109C-4273-83BC-03BE9C08307C}];1 ;BudgetLine          ;Element ;Table   ;
                                                  SourceTable=Table11012001;
                                                  SourceTableView=SORTING(Field1,Field31,Field33,Field38,Field14);
                                                  CalcFields=Field19;
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Budget Line".INIT;

                                                                              CLEAR(LineTxt);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 lvProjTenderPartRec@1100525001 : Record 11012264;
                                                                                 SpecialUnitRec@1100525000 : Record 11012392;
                                                                               BEGIN
                                                                                 WITH "Budget Line" DO BEGIN

                                                                                 LineNo += 1;

                                                                                 RecordType := CheckSeparator(COPYSTR(LineTxt,1,3));

                                                                                 IF (RecordType = '321') THEN BEGIN
                                                                                   InsertTextLine;
                                                                                   currXMLport.SKIP;
                                                                                 END;

                                                                                 IF (RecordType < '302') OR (RecordType > '304') THEN
                                                                                   currXMLport.SKIP;

                                                                                 "Project No." := ProjectCde;
                                                                                 "Extension Contract" := ExtensionCde;
                                                                                 Adjustment := AdjustmentCde;
                                                                                 Option := OptionCde;

                                                                                 FillMainProject;
                                                                                 GetVersionDate;
                                                                                 UpdateExtContractStatus;

                                                                                 IF NOT ProjectChecked THEN
                                                                                   CheckProject;

                                                                                 LineCounter := LineCounter + 10000;
                                                                                 "Line No." := LineCounter;
                                                                                 "Department Code" := ProjRec."Global Dimension 1 Code";

                                                                                 RecRef.GETTABLE("Budget Line");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 IF (RecordType = '302') OR (RecordType = '303') OR (RecordType = '304') THEN BEGIN
                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,18,17));
                                                                                   "Tender Paragraph" := COPYSTR(FieldTxt,1,MAXSTRLEN("Tender Paragraph"));
                                                                                   GWWMgtCU.FormatTenderElement("Tender Paragraph");
                                                                                 END;

                                                                                 IF (RecordType = '303') OR (RecordType = '304') THEN BEGIN
                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,36,17));
                                                                                   "Tender Paragraph Part" := COPYSTR(FieldTxt,1,MAXSTRLEN("Tender Paragraph Part"));
                                                                                   GWWMgtCU.FormatTenderElementPart("Tender Paragraph Part");
                                                                                 END;

                                                                                 IF RecordType = '302' THEN BEGIN
                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,36,50));
                                                                                   Description := COPYSTR(FieldTxt,1,MAXSTRLEN(Description));
                                                                                   Description := DELCHR(Description, '>', ' ');

                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,123,12));
                                                                                   IF EVALUATE(Quantity,FieldTxt) THEN;

                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,136,4));
                                                                                   "Unit of Measure" := COPYSTR(FieldTxt,1,MAXSTRLEN("Unit of Measure"));
                                                                                   CheckUnitOfMeasure();

                                                                                   InsertTenderElement;
                                                                                 END;

                                                                                 IF RecordType = '303' THEN BEGIN
                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,54,30));
                                                                                   Description := COPYSTR(FieldTxt,1,MAXSTRLEN(Description));
                                                                                   Description := DELCHR(Description, '>', ' ');

                                                                                   InsertTenderPart;
                                                                                 END;

                                                                                 IF RecordType = '304' THEN BEGIN
                                                                                   IF DELCHR("Tender Paragraph Part", '=', '0') = '' THEN BEGIN
                                                                                     IF NOT lvProjTenderPartRec.GET("Project No.", "Tender Paragraph", "Tender Paragraph Part") THEN
                                                                                       "Tender Paragraph Part" := '';
                                                                                   END;

                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,72,30));
                                                                                   Description := COPYSTR(FieldTxt,1,MAXSTRLEN(Description));
                                                                                   Description := DELCHR(Description, '>', ' ');

                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,54,17));
                                                                                   "Cost Object" := COPYSTR(FieldTxt,1,MAXSTRLEN("Cost Object"));
                                                                                   IF CheckCostObject("Cost Object") THEN BEGIN
                                                                                     "Cost Type" := DimValRec."Cost Type";
                                                                                      IF NOT DimValRec.Blocked THEN  //C-026977
                                                                                        "Purchase Action" := DimValRec."Purchase Action";
                                                                                   END;

                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,103,4));
                                                                                   "Unit of Measure" := COPYSTR(FieldTxt,1,MAXSTRLEN("Unit of Measure"));
                                                                                   CheckUnitOfMeasure();

                                                                                   IF "Cost Type" = "Cost Type"::Labor THEN BEGIN
                                                                                     FieldTxt := CheckSeparator(COPYSTR(LineTxt,124,13));
                                                                                     IF EVALUATE(Hours,FieldTxt) THEN;

                                                                                     IF SpecialUnitRec.GET("Unit of Measure") THEN
                                                                                       "Production Rate" := 1
                                                                                     ELSE BEGIN
                                                                                       FieldTxt := CheckSeparator(COPYSTR(LineTxt,110,13));
                                                                                       IF EVALUATE("Production Rate",FieldTxt) THEN;
                                                                                     END;

                                                                                     Quantity := ROUND(Hours * "Production Rate", 0.00001);
                                                                                     IF "Production Rate" <> 0 THEN
                                                                                       Norm := 1 / "Production Rate"
                                                                                     ELSE
                                                                                       Norm := 1;
                                                                                     Norm := ROUND(Norm, 0.00001);
                                                                                   END ELSE BEGIN
                                                                                     FieldTxt := CheckSeparator(COPYSTR(LineTxt,124,13));
                                                                                     IF EVALUATE(Quantity,FieldTxt) THEN;
                                                                                   END;

                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,138,20));
                                                                                   IF EVALUATE(PriceDec,FieldTxt) THEN;

                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,159,22));
                                                                                   IF EVALUATE(LineTotal,FieldTxt) THEN;

                                                                                   FieldTxt := CheckSeparator(COPYSTR(LineTxt,182,17));
                                                                                   Element := COPYSTR(FieldTxt,1,MAXSTRLEN(Element));
                                                                                   FillElement(Element);
                                                                                   FillElementPhaseCodeBasedOnElement;

                                                                                   PriceDec := ROUND(PriceDec,GLSetup."Unit-Amount Rounding Precision");
                                                                                   LineTotal := ROUND(LineTotal,GLSetup."Amount Rounding Precision");
                                                                                   TotalImportAmnt += LineTotal;

                                                                                   IF "Cost Type" = "Cost Type"::Labor THEN
                                                                                     "Rate (LCY)" := PriceDec
                                                                                   ELSE
                                                                                     "Price (LCY)" := PriceDec;

                                                                                   CalcTotals;
                                                                                   "Cost Component" := DefaultCostComponent("Cost Object", "Project No.");
                                                                                   FillDefaultFscType();
                                                                                   TotalCalcAmnt += "Amount (LCY)";

                                                                                   INSERT;
                                                                                   UpdateCostControlStatus(0);
                                                                                 END;

                                                                                 currXMLport.SKIP;

                                                                                 END;
                                                                               END;
                                                                                }

    { [{05812762-DB0B-4725-84E2-74736C87538B}];2 ;LineTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1024 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      ProjSetup@1100525031 : Record 315;
      GLSetup@1100525030 : Record 98;
      ProjRec@1100525029 : Record 11072003;
      ProjElemRec@1100525028 : Record 11012010;
      BudgetRec@1100525027 : Record 11012001;
      UnitRec@1100525023 : Record 204;
      DimValRec@1100525022 : Record 349;
      RecRef@1100525035 : RecordRef;
      XMLportStatusDialogMgt@1100525036 : Codeunit 11130006;
      DimMgt@1100525021 : Codeunit 408;
      GWWMgtCU@1100525020 : Codeunit 11012163;
      FieldTxt@1100525018 : Text[250];
      LineTotal@1100525017 : Decimal;
      LineCounter@1100525015 : Integer;
      RecordType@1100525014 : Text[30];
      ProjectChecked@1100525013 : Boolean;
      PriceDec@1100525012 : Decimal;
      LineNo@1100525011 : Integer;
      TotalImportAmnt@1100525010 : Decimal;
      TotalCalcAmnt@1100525009 : Decimal;
      ProjectCde@1100525008 : Code[20];
      AdjustmentCde@1100525007 : Code[10];
      ExtensionCde@1100525006 : Code[10];
      HouseModelCde@1100525005 : Code[20];
      MainGroupCde@1100525004 : Code[20];
      GroupCde@1100525003 : Code[20];
      SubGroupCde@1100525002 : Code[20];
      OptionCde@1100525001 : Code[20];
      PlotCde@1100525000 : Code[20];
      Text000@1100525034 : TextConst 'ENU=%1 %2 not present;SVE=%1 %2 finns inte';
      Text012@1100525032 : TextConst 'ENU=No ELement present (Element is mandatory);SVE=Det finns inget element (element „r obligatoriskt)';
      Text013@1210190000 : TextConst 'ENU=Cost Object %1 is blocked;SVE=Kostnadsobjekt %1 „r blockerad';
      Text999@1100525019 : TextConst 'ENU=The Total of the read in Budget %1 differs from the Calculated Total %2. The Difference is %3.;SVE=Totalt f”r l„s i budgeten %1 skiljer sig fr†n det ber„knade totala %2 Skillnaden „r %3.';

    PROCEDURE DetermineSelection@1(lvProject@11012000 : Code[20];lvAdjustment@11012001 : Code[10];lvExtension@11012002 : Code[10];lvHouseModel@1210190003 : Code[20];lvMainGroup@1210190002 : Code[20];lvGroup@1210190001 : Code[20];lvSubGroup@1210190000 : Code[20];lvOption@11012003 : Code[20];lvPlotCde@1100485000 : Code[20]);
    BEGIN
      ProjectCde := lvProject;
      AdjustmentCde := lvAdjustment;
      ExtensionCde := lvExtension;
      PlotCde := lvPlotCde;
      HouseModelCde := lvHouseModel;
      MainGroupCde := lvMainGroup;
      GroupCde := lvGroup;
      SubGroupCde := lvSubGroup;
      OptionCde := lvOption;
    END;

    PROCEDURE StoreMessage@5(LineNo@1000000000 : Integer;"Line Description"@11012000 : Text[250];ErrorText@11012001 : Text[250]);
    VAR
      ErrorRec@1100525000 : Record 11012051;
      lvError@11012002 : Integer;
    BEGIN
      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"Import Budget");
        IF FINDLAST THEN
          lvError := "Line No." + 1
        ELSE
          lvError := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"Import Budget";
        "Line No." := lvError;
        "Project No." := ProjRec."No.";
        "Error message" := ErrorText;
        Description := COPYSTR(FIELDCAPTION("Line No.") + ': ' + FORMAT(LineNo) + ' - '+ "Line Description",1,50);
        INSERT;
      END;
    END;

    PROCEDURE CheckProject@1000000004();
    BEGIN
      WITH "Budget Line" DO BEGIN
        BudgetRec.SETRANGE("Project No.", "Project No.");
        IF BudgetRec.FINDLAST THEN
          LineCounter := BudgetRec."Line No.";

        IF NOT ProjRec.GET("Project No.") THEN BEGIN
          StoreMessage(LineNo,'',STRSUBSTNO(Text000,FIELDCAPTION("Project No."),"Project No."));
          currXMLport.BREAK;
        END;
      END;
      ProjectChecked := TRUE;
    END;

    PROCEDURE CheckCostObject@1210190007(lCostObject@1210190000 : Code[20]) Exist : Boolean;
    BEGIN
      WITH "Budget Line" DO BEGIN
        DimMgt.GetDimValueRec(2, "Cost Object", DimValRec, FALSE, '');
        IF DimValRec.Code = '' THEN BEGIN
          StoreMessage(LineNo,Description,STRSUBSTNO(Text000,FIELDCAPTION("Cost Object"),"Cost Object"));
          EXIT(FALSE);
        END;
        IF DimValRec.Blocked THEN
          StoreMessage(LineNo,Description, STRSUBSTNO(Text013,"Cost Object"));  // Not 'EXIT(FALSE)' if blocked!
      END;

      EXIT(TRUE);
    END;

    PROCEDURE CheckUnitOfMeasure@1100525002();
    BEGIN
      WITH "Budget Line" DO BEGIN
        IF "Unit of Measure" = '' THEN
          EXIT;
        IF NOT UnitRec.GET("Unit of Measure") THEN
          StoreMessage(LineNo,Description, STRSUBSTNO(Text000, FIELDCAPTION("Unit of Measure"), "Unit of Measure"));
      END;
    END;

    PROCEDURE FillElement@1000000000(lElement@1210190000 : Code[20]) : Decimal;
    VAR
      pos1@1210190001 : Integer;
      ElementFormat@1210190003 : Codeunit 11012022;
      lChapterCde@1210190005 : Code[20];
      lParGraphCde@1210190006 : Code[20];
      lLevel@1210190007 : Integer;
    BEGIN
      IF lElement <> '' THEN BEGIN
        pos1 := STRPOS(lElement, '.');
        IF (pos1 = 2) OR ((pos1 = 0) AND (STRLEN(lElement) = 1)) THEN
          lElement := '0' + lElement;
        ElementFormat.FormatElemExtended(2,lElement,lChapterCde,lParGraphCde,lLevel);
        CASE lLevel OF
          1:BEGIN
              IF NOT ProjElemRec.GET(ProjectCde,lChapterCde) THEN
                InsertElement(lChapterCde);
            END;
          2:BEGIN
              IF NOT ProjElemRec.GET(ProjectCde,lChapterCde) THEN
                InsertElement(lChapterCde);
              IF NOT ProjElemRec.GET(ProjectCde,lParGraphCde) THEN
                InsertElement(lParGraphCde);
            END;
          3:BEGIN
              IF NOT ProjElemRec.GET(ProjectCde,lChapterCde) THEN
                InsertElement(lChapterCde);
              IF NOT ProjElemRec.GET(ProjectCde,lParGraphCde) THEN
                InsertElement(lParGraphCde);
              IF NOT ProjElemRec.GET(ProjectCde,lElement) THEN
                InsertElement(lElement);
             END;
        END;
        "Budget Line".Element := lElement;
        "Budget Line".Chapter := lChapterCde;
        "Budget Line".Paragraph := lParGraphCde;
      END ELSE BEGIN
        IF ProjSetup."Posting Element Mandatory" THEN
          StoreMessage(LineNo,"Budget Line".Description,Text012);
      END;
    END;

    PROCEDURE InsertElement@1210190012(lElementCde@1210190000 : Code[20]);
    BEGIN
      IF ProjectCde <> '' THEN BEGIN
        ProjElemRec.INIT;
        ProjElemRec.VALIDATE("Project No.", ProjectCde);
        ProjElemRec.VALIDATE(Element,lElementCde);
        ProjElemRec.INSERT(TRUE);
      END;
    END;

    PROCEDURE InsertTenderElement@1000000016();
    VAR
      ProjTenderElemRec@1100525000 : Record 11012263;
    BEGIN
      WITH "Budget Line" DO BEGIN
        ProjTenderElemRec.INIT;
        ProjTenderElemRec."Project No." := "Project No.";
        ProjTenderElemRec.VALIDATE("Tender Paragraph", "Tender Paragraph");
        ProjTenderElemRec.VALIDATE(Level);
        ProjTenderElemRec.VALIDATE(Description, Description);
        ProjTenderElemRec.Quantity := Quantity;
        ProjTenderElemRec."Unit of Measure Code" := "Unit of Measure";
        IF ProjTenderElemRec.INSERT THEN;
      END;
    END;

    PROCEDURE InsertTenderPart@1100525000();
    VAR
      ProjTenderPartRec@1100525000 : Record 11012264;
    BEGIN
      WITH "Budget Line" DO BEGIN
        ProjTenderPartRec.INIT;
        ProjTenderPartRec."Project No." := "Project No.";
        ProjTenderPartRec."Tender Paragraph" := "Tender Paragraph";
        ProjTenderPartRec."Tender Paragraph Part" := "Tender Paragraph Part";
        ProjTenderPartRec.VALIDATE(Description, Description);
        IF ProjTenderPartRec.INSERT THEN;
      END;
    END;

    PROCEDURE InsertTextLine@1100525004();
    VAR
      ProjTenderElemTextRec@1100525000 : Record 11012269;
      TenderElem@1100525002 : Text[250];
      ElemText@1100525003 : Text[250];
      LastLino@1100525001 : Integer;
    BEGIN
      WITH ProjTenderElemTextRec DO BEGIN
        TenderElem := CheckSeparator(COPYSTR(LineTxt,5,12));
        ElemText := CheckSeparator(COPYSTR(LineTxt,54,80));

        SETRANGE("Project No.", ProjectCde);
        SETRANGE("Tender Paragraph", TenderElem);
        SETRANGE("Source Type", "Source Type"::" ");  //db, 24-05-16: DP01086
        IF FINDLAST THEN
          LastLino := "Text Line No.";

        INIT;
        "Project No." := ProjectCde;
        "Tender Paragraph" := COPYSTR(TenderElem,1,MAXSTRLEN("Tender Paragraph"));
        "Source Type" := "Source Type"::" ";  //db, 24-05-16: DP01086
        "Text Line No." := LastLino + 10000;
        Text := COPYSTR(ElemText,1,MAXSTRLEN(Text));
        IF INSERT THEN;
      END;
    END;

    PROCEDURE CheckUpdateOption@1100485000();
    VAR
      lvDummyOptionSurchRec@1100485001 : Record 11012512;
      lvBuyerMgtCU@1100485000 : Codeunit 11012500;
    BEGIN
      IF OptionCde = '' THEN
        EXIT;

      lvBuyerMgtCU.BmCalcSurchargesUpdateOption(
        ProjectCde, PlotCde, HouseModelCde, MainGroupCde, GroupCde, SubGroupCde, OptionCde, FALSE, lvDummyOptionSurchRec);
    END;

    PROCEDURE DefaultCostComponent@1100485001(lvCostObject@1100485000 : Code[20];lvProject@1100485001 : Code[20]) lvCostComponent : Code[20];
    VAR
      lvDimValRec@1100485002 : Record 349;
    BEGIN
      lvCostComponent := '';
      IF lvCostObject <> '' THEN BEGIN
        DimMgt.GetDimValueRec(2, lvCostObject, lvDimValRec, FALSE, lvProject);
        IF NOT lvDimValRec.Blocked THEN
          lvCostComponent := lvDimValRec."Cost Component";
      END;
    END;

    PROCEDURE CheckSeparator@1100525001(FieldTxt@1100525000 : Text[250]) : Text[250];
    VAR
      lvText000@1100525001 : TextConst 'ENU=Wrong Import Format;SVE=Fel importformat';
    BEGIN
      IF STRPOS(FieldTxt, '|') <> 0 THEN ERROR(lvText000);
      EXIT(FieldTxt);
    END;

    BEGIN
    END.
  }
}

