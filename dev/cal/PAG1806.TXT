OBJECT Page 1806 Exchange Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Office Add-In Setup;
               NOR=Installasjonsprogram for Office-tillegg;
               SVE=Inst„llningar f”r Office-till„gg];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table1610;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    ShowFilter=No;
    OnInit=VAR
             User@1000 : Record 2000000120;
           BEGIN
             User.SETRANGE("User Name",USERID);
             IF User.FINDFIRST THEN
               Email := User."Authentication Email";

             LoadTopBanners;
             CredentialsRequired := (Email = '') OR ExchangeAddinSetup.CredentialsRequired(Email);
             EmailIsHostedO365 := TRUE;
           END;

    OnOpenPage=BEGIN
                 ShowIntroStep;
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1000 : Record 1803;
                     BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF AssistedSetup.GetStatus(PAGE::"Exchange Setup Wizard") = AssistedSetup.Status::"Not Completed" THEN
                           IF NOT CONFIRM(NAVNotSetUpQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 9       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 12      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 NOR=Tilbake;
                                 SVE=Bak†t];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 NOR=Neste;
                                 SVE=N„sta];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 IF Step = Step::Credentials THEN
                                   ValidateCredentials;
                                 NextStep(FALSE);
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 NOR=Fullf›r;
                                 SVE=Slutf”r];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=VAR
                                 AssistedSetup@1004 : Record 1803;
                               BEGIN
                                 DeployToExchange;
                                 AssistedSetup.SetStatus(PAGE::"Exchange Setup Wizard",AssistedSetup.Status::Completed);
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Control 1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 2   ;1   ;Group     ;
                Name=Step1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=IntroVisible;
                GroupType=Group }

    { 3   ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=[ENU=Welcome to Office Add-In Setup;
                           NOR=Velkommen til installasjonsprogrammet for Office-tillegg;
                           SVE=V„lkommen till Inst„llningar f”r Office-till„gg];
                GroupType=Group }

    { 4   ;3   ;Group     ;
                Name=Para1.1.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=You can set up Office add-ins in Exchange or Exchange Online to complete business tasks without leaving your Outlook inbox.;
                                     NOR=Du kan konfigurere Office-tillegg i Exchange eller Exchange Online for † fullf›re forretningsoppgaver uten † g† ut av Outlook-innboksen.;
                                     SVE=Du kan st„lla in Office-till„gg i Exchange eller Exchange Online om du vill utf”ra aff„rsuppgifter utan att l„mna inkorgen i Outlook.] }

    { 13  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=[ENU=Let's go!;
                           NOR=La oss sette i gang!;
                           SVE=S„tt ig†ng!];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Next so you can set up Office add-ins.;
                                     NOR=Velg Neste slik at du kan installere Office-tillegg.;
                                     SVE=V„lj N„sta om du vill st„lla in Office-till„gg.] }

    { 16  ;1   ;Group     ;
                Name=Step2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=DeploymentModeVisible;
                GroupType=Group }

    { 17  ;2   ;Group     ;
                Name=Para2.1;
                CaptionML=[ENU=Do you want to set up the add-ins for your organization or only for you?;
                           NOR=Vil du installere tilleggene for organisasjonen eller bare for deg?;
                           SVE=Vill du konfigurera till„ggen f”r din organisation eller enbart f”r dig?];
                GroupType=Group }

    { 18  ;3   ;Field     ;
                Name=DeploymentMode;
                CaptionML=[ENU=Set up for:;
                           NOR=Installer for:;
                           SVE=Konfigurera f”r:];
                OptionCaptionML=[ENU=My Mailbox,My Organization;
                                 NOR=Min postboks,Min organisasjon;
                                 SVE=Min postl†da,Min organisation];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DeploymentMode }

    { 25  ;1   ;Group     ;
                Name=Step2.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=O365Visible;
                GroupType=Group }

    { 31  ;2   ;Field     ;
                Name=UseO365;
                DrillDown=Yes;
                CaptionML=[ENU=Is your email hosted in Office 365?;
                           NOR=Blir e-posten din driftet i Office 365?;
                           SVE=Har din e-post Office 365 som v„rd?];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=EmailIsHostedO365 }

    { 5   ;1   ;Group     ;
                Name=Step3;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=CredentialsVisible;
                GroupType=Group }

    { 6   ;2   ;Group     ;
                Name=Step3.1;
                CaptionML=[ENU=Provide your credentials for Exchange or Exchange Online.;
                           NOR=Angi legitimasjonen din for Exchange eller Exchange Online.;
                           SVE=Ange autentiseringsuppgifter f”r Exchange eller Exchange Online.];
                Visible=NOT OnPremOrgDeploy;
                GroupType=Group }

    { 7   ;3   ;Field     ;
                Name=Email;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Email;
                           NOR=E-post;
                           SVE=E-post];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Email;
                OnValidate=BEGIN
                             CredentialsValidated := FALSE;
                           END;
                            }

    { 8   ;3   ;Field     ;
                Name=Password;
                ExtendedDatatype=Masked;
                CaptionML=[ENU=Password;
                           NOR=Passord;
                           SVE=L”senord];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Password;
                OnValidate=BEGIN
                             CredentialsValidated := FALSE;
                           END;
                            }

    { 20  ;3   ;Group     ;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=DeploymentMode = DeploymentMode::Organization;
                GroupType=Group }

    { 19  ;4   ;Field     ;
                Name=Administrator;
                CaptionML=[ENU=You must specify credentials for an administrative account.;
                           NOR=Du m† angi legitimasjon for en administratorkonto.;
                           SVE=Du m†ste ange beh”righeter f”r ett administrativt konto.];
                ApplicationArea=#Basic,#Suite;
                Style=Attention;
                StyleExpr=TRUE }

    { 26  ;2   ;Group     ;
                Name=Step3.2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=OnPremOrgDeploy;
                GroupType=Group }

    { 27  ;3   ;Field     ;
                Name=ExchangeUserName;
                CaptionML=[ENU=Exchange administrator user name;
                           NOR=Brukernavn for Exchange-administrator;
                           SVE=Anv„ndarnamn f”r Exchange-administrat”r];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=UserName }

    { 28  ;3   ;Field     ;
                Name=ExchangePassword;
                ExtendedDatatype=Masked;
                CaptionML=[ENU=Exchange administrator password;
                           NOR=Passord for Exchange-administrator;
                           SVE=L”senord f”r Exchange-administrat”r];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Password }

    { 29  ;3   ;Field     ;
                Name=ExchangeEndpoint;
                CaptionML=[ENU=Exchange PowerShell endpoint;
                           NOR=Endepunkt for Exchange PowerShell;
                           SVE=Slutpunkt f”r Exchange PowerShell];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PSEndpoint }

    { 21  ;1   ;Group     ;
                Name=Step4;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=EmailVisible;
                GroupType=Group }

    { 22  ;2   ;Group     ;
                Name=Para4.1;
                CaptionML=[ENU=Receive a sample email message to evaluate the add-ins;
                           NOR=Motta en pr›ve-e-postmelding for † evaluere tilleggene;
                           SVE=Ta emot exempelmeddelanden via e-post f”r att utv„rdera till„ggen];
                GroupType=Group }

    { 24  ;3   ;Group     ;
                Name=Para4.1.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=We can send you a sample email message from a contact in this evaluation company so that you can try out the Outlook add-in experience. To have a sample email sent to your inbox, select the checkbox.;
                                     NOR=Vi kan sende deg en pr›ve-e-postmelding fra en kontakt i dette evalueringsselskapet, slik at du kan pr›ve ut Outlook-tillegg. Merk av i avmerkingsboksen for † f† tilsendt pr›ve-e-post til innboksen.;
                                     SVE=Vi kan skicka ett exempelmeddelande via e-post till dig fr†n en kontakt i det h„r fiktiva f”retaget, s† att du kan testa Outlook-till„gget. Markera kryssrutan om du vill att ett exempel p† e-postmeddelanden ska skickas till din inkorg.] }

    { 23  ;3   ;Field     ;
                Name=SetupEmails;
                CaptionML=[ENU=Send sample email message;
                           NOR=Send pr›ve-e-postmelding;
                           SVE=Skicka exempel p† e-postmeddelande];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SetupEmails }

    { 14  ;1   ;Group     ;
                Name=Step5;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=DoneVisible;
                GroupType=Group }

    { 15  ;2   ;Group     ;
                CaptionML=[ENU=That's it!;
                           NOR=Det var det!;
                           SVE=Klart!];
                Visible=NOT OnPremOrgDeploy;
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Finish to enable Office add-ins in Exchange Online.;
                                     NOR=Velg Fullf›r for † aktivere Office-tillegg i Exchange Online.;
                                     SVE=V„lj Slutf”r f”r att aktivera Office-till„ggen i Exchange Online.] }

    { 30  ;2   ;Group     ;
                CaptionML=[ENU=That's it!;
                           NOR=Det var det!;
                           SVE=Klart!];
                Visible=OnPremOrgDeploy;
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Finish to enable Office add-ins in Exchange.;
                                     NOR=Velg Fullf›r for † aktivere Office-tillegg i Exchange.;
                                     SVE=V„lj Slutf”r f”r att aktivera Office-till„ggen i Exchange.] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record 9400;
      MediaRepositoryDone@1041 : Record 9400;
      MediaResourcesStandard@1033 : Record 2000000182;
      MediaResourcesDone@1032 : Record 2000000182;
      ExchangeAddinSetup@1018 : Codeunit 5323;
      ClientTypeManagement@1034 : Codeunit 4030;
      Step@1008 : 'Intro,DeploymentMode,UseO365,Credentials,Email,Done';
      Email@1000 : Text[80];
      UserName@1020 : Text[80];
      Password@1001 : Text[30];
      PSEndpoint@1021 : Text[250];
      EmailIsHostedO365@1025 : Boolean;
      DeploymentMode@1012 : 'User,Organization';
      BackEnabled@1002 : Boolean;
      NextEnabled@1003 : Boolean;
      FinishEnabled@1004 : Boolean;
      TopBannerVisible@1042 : Boolean;
      IntroVisible@1005 : Boolean;
      DeploymentModeVisible@1011 : Boolean;
      O365Visible@1024 : Boolean;
      CredentialsVisible@1006 : Boolean;
      EmailVisible@1013 : Boolean;
      DoneVisible@1007 : Boolean;
      NAVNotSetUpQst@1010 : TextConst 'ENU=No Office add-ins have been set up.\\Are you sure that you want to exit?;NOR=Det er ikke installert noen Office-tillegg.\\Er du sikker p† at du vil avslutte?;SVE=Inget Office-till„gg har st„llts in.\\Vill du avsluta?';
      SetupEmails@1015 : Boolean;
      CredentialsRequired@1019 : Boolean;
      CredentialsValidated@1017 : Boolean;
      EmailPasswordMissingErr@1009 : TextConst 'ENU=Please enter a valid email address and password.;NOR=Angi en gyldig e-postadresse og et gyldig passord.;SVE=Ange en giltig e-postadress och ett l”senord.';
      SkipDeployment@1016 : Boolean;
      UsernamePasswordMissingErr@1031 : TextConst 'ENU=Please enter a valid domain username and password.;NOR=Angi et gyldig brukernavn og passord for domene.;SVE=Ange ett giltigt anv„ndarnamn och l”senord f”r dom„nen.';
      SampleEndpointTxt@1022 : TextConst '@@@={Locked};ENU=http://mail.cronus.com/PowerShell;NOR=http://mail.cronus.com/PowerShell;SVE=http://mail.cronus.com/PowerShell';
      OnPremOrgDeploy@1023 : Boolean;
      ConnectingMsg@1014 : TextConst 'ENU=Connecting to Exchange.;NOR=Kobler til Exchange.;SVE=Ansluter till Exchange.';
      DeployAccountMsg@1026 : TextConst 'ENU=Deploying add-ins for your account.;NOR=Tar i bruk tillegg for kontoen.;SVE=Distribuerar till„gg f”r ditt konto.';
      DeployOrgMsg@1027 : TextConst 'ENU=Deploying add-ins for your organization.;NOR=Tar i bruk tillegg for organisasjonen.;SVE=Distribuerar till„gg f”r din organisation.';
      DeploySampleMailMsg@1028 : TextConst 'ENU=Deploying sample emails to your mailbox.;NOR=Tar i bruk pr›ve-e-postmeldinger i postboksen.;SVE=Distribuerar exempel p† e-postmeddelanden till din postl†da.';
      DeployAddInMsg@1029 : TextConst '@@@=%1 is the name of an Office add-in.;ENU=Deploying %1.;NOR=Tar i bruk %1.;SVE=Distribuerar %1.';
      ProgressTemplateMsg@1030 : TextConst '@@@={Locked};ENU=#1##########\@2@@@@@@@@@@;NOR=#1##########\@2@@@@@@@@@@;SVE=#1##########\@2@@@@@@@@@@';

    LOCAL PROCEDURE NextStep@1003(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      CASE Step OF
        Step::Intro:
          ShowIntroStep;
        Step::DeploymentMode:
          ShowDeploymentModeStep;
        Step::UseO365:
          ShowO365Step(Backwards);
        Step::Credentials:
          ShowCredentialsStep(Backwards);
        Step::Email:
          ShowEmailStep(Backwards);
        Step::Done:
          ShowDoneStep;
      END;

      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ShowIntroStep@1001();
    BEGIN
      ResetWizardControls;
      IntroVisible := TRUE;
      BackEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowDeploymentModeStep@1007();
    BEGIN
      ResetWizardControls;
      NextEnabled := TRUE;
      DeploymentModeVisible := TRUE;
      OnPremOrgDeploy := FALSE;
    END;

    LOCAL PROCEDURE ShowO365Step@1042(Backwards@1000 : Boolean);
    BEGIN
      ResetWizardControls;
      NextEnabled := TRUE;
      O365Visible := TRUE;

      IF DeploymentMode <> DeploymentMode::Organization THEN
        NextStep(Backwards);
    END;

    LOCAL PROCEDURE ShowCredentialsStep@1009(Backwards@1000 : Boolean);
    BEGIN
      ResetWizardControls;
      CredentialsVisible := TRUE;
      OnPremOrgDeploy := (DeploymentMode = DeploymentMode::Organization) AND (NOT EmailIsHostedO365);

      IF OnPremOrgDeploy AND (PSEndpoint = '') THEN
        PSEndpoint := SampleEndpointTxt;

      IF NOT NeedCredentials THEN
        NextStep(Backwards);
    END;

    LOCAL PROCEDURE ShowEmailStep@1023(Backwards@1001 : Boolean);
    BEGIN
      ResetWizardControls;
      NextEnabled := TRUE;
      EmailVisible := TRUE;

      IF (NOT ExchangeAddinSetup.SampleEmailsAvailable) OR OnPremOrgDeploy THEN
        NextStep(Backwards);
    END;

    LOCAL PROCEDURE ShowDoneStep@1011();
    BEGIN
      ResetWizardControls;
      DoneVisible := TRUE;
      NextEnabled := FALSE;
      FinishEnabled := TRUE;
    END;

    LOCAL PROCEDURE ResetWizardControls@1010();
    BEGIN
      // Buttons
      BackEnabled := TRUE;
      NextEnabled := TRUE;
      FinishEnabled := FALSE;

      // Tabs
      IntroVisible := FALSE;
      DeploymentModeVisible := FALSE;
      CredentialsVisible := FALSE;
      EmailVisible := FALSE;
      DoneVisible := FALSE;
      O365Visible := FALSE;
    END;

    LOCAL PROCEDURE DeployToExchange@1043();
    VAR
      OfficeAddin@1000 : Record 1610;
      AddinDeploymentHelper@1001 : Codeunit 1640;
      ProgressWindow@1002 : Dialog;
      Progress@1003 : Integer;
    BEGIN
      IF SkipDeployment THEN
        EXIT;
      ProgressWindow.OPEN(ProgressTemplateMsg);
      ProgressWindow.UPDATE(1,ConnectingMsg);
      ProgressWindow.UPDATE(2,3000);

      IF NOT OnPremOrgDeploy THEN
        IF NeedCredentials THEN
          ExchangeAddinSetup.InitializeServiceWithCredentials(Email,Password);

      IF DeploymentMode = DeploymentMode::User THEN BEGIN
        ProgressWindow.UPDATE(1,DeployAccountMsg);
        ProgressWindow.UPDATE(2,6000);
        ExchangeAddinSetup.DeployAddins(OfficeAddin);
      END ELSE
        IF OfficeAddin.GetAddins THEN BEGIN
          Progress := 4000;
          ProgressWindow.UPDATE(1,DeployOrgMsg);
          ProgressWindow.UPDATE(2,Progress);
          IF OnPremOrgDeploy THEN BEGIN
            AddinDeploymentHelper.SetManifestDeploymentCustomEndpoint(PSEndpoint);
            AddinDeploymentHelper.SetManifestDeploymentCredentials(UserName,Password);
          END ELSE BEGIN
            AddinDeploymentHelper.SetManifestDeploymentCustomEndpoint(PSEndpoint);
            AddinDeploymentHelper.SetManifestDeploymentCredentials(Email,Password);
          END;
          REPEAT
            Progress += 1000;
            ProgressWindow.UPDATE(1,STRSUBSTNO(DeployAddInMsg,OfficeAddin.Name));
            ProgressWindow.UPDATE(2,Progress);
            AddinDeploymentHelper.DeployManifest(OfficeAddin);
          UNTIL OfficeAddin.NEXT = 0;
        END;

      IF SetupEmails THEN BEGIN
        ProgressWindow.UPDATE(1,DeploySampleMailMsg);
        ProgressWindow.UPDATE(2,9000);
        ExchangeAddinSetup.DeploySampleEmails(Email);
      END;

      ProgressWindow.UPDATE(2,10000);
      ProgressWindow.CLOSE;
    END;

    LOCAL PROCEDURE ValidateCredentials@1013();
    BEGIN
      IF SkipDeployment THEN
        EXIT;

      IF OnPremOrgDeploy THEN BEGIN
        IF (UserName = '') OR (Password = '') OR (PSEndpoint = '') THEN
          ERROR(UsernamePasswordMissingErr);
      END ELSE BEGIN
        IF NeedCredentials AND NOT CredentialsValidated THEN BEGIN
          IF (Email = '') OR (Password = '') THEN
            ERROR(EmailPasswordMissingErr);
          ExchangeAddinSetup.InitializeServiceWithCredentials(Email,Password);
        END;
        CredentialsValidated := TRUE;
      END;
    END;

    [External]
    PROCEDURE SkipDeploymentToExchange@1002(Skip@1000 : Boolean);
    BEGIN
      SkipDeployment := Skip;
    END;

    LOCAL PROCEDURE NeedCredentials@1017() : Boolean;
    BEGIN
      EXIT((DeploymentMode = DeploymentMode::Organization) OR CredentialsRequired);
    END;

    LOCAL PROCEDURE LoadTopBanners@1040();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    BEGIN
    END.
  }
}

