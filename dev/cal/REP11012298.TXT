OBJECT Report 11012298 Block Pur. Vendor based on Ass
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kreditor fr Einkauf blockieren aufgrund Beurteilung (Auswahl);
               ENU=Block Vendor for Purchase based on Assessment (Selection);
               NLD=Leveancier voor inkoop blokkeren  n.a.v. beoordeling (selectie);
               NOR=Blokker leverand›r for innkj›p basert p† vurdering (utvalg)];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PurchSetup.GET;
                   PurchSetup.TESTFIELD("Max. Score Assessment");
                 END;

    OnPreReport=BEGIN
                  ValidateStartDateAss();
                  ValidateEndDateAss();
                  ValidateMinNeededScore();
                END;

    OnPostReport=BEGIN
                   IF NOT TmpVendor.FINDFIRST THEN
                     ERROR(Text006)
                   ELSE
                     PAGE.RUN(PAGE::"Block Pur. Vendor based on Ass", TmpVendor);
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 2089;    ;DataItem;                    ;
               DataItemTable=Table23;
               DataItemTableView=SORTING(No.)
                                 WHERE(Block Purchase Order=CONST(No));
               OnAfterGetRecord=BEGIN
                                  ReadVendorAssessment();
                                  IF (NoOfAssess > 0) AND (ResultScore < MinNeededScore) THEN BEGIN
                                    TmpVendor := Vendor;
                                    TmpVendor."Prepayment %" := NoOfAssess;
                                    TmpVendor."Budgeted Amount" := ResultScore;
                                    TmpVendor.INSERT;
                                  END;
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF (EndDateAss = 0D) THEN
                     EndDateAss := TODAY;
                   IF (MinNeededScore = 0) THEN
                     MinNeededScore := 1;

                   GetScoreDescription();
                 END;

    }
    CONTROLS
    {
      { 1100409000;0;Container;
                  ContainerType=ContentArea }

      { 1100409001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100409003;2;Field  ;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  NotBlank=Yes;
                  SourceExpr=StartDateAss;
                  OnValidate=BEGIN
                               ValidateStartDateAss();
                             END;
                              }

      { 1100409004;2;Field  ;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  NotBlank=Yes;
                  SourceExpr=EndDateAss;
                  OnValidate=BEGIN
                               ValidateEndDateAss();
                             END;
                              }

      { 1100409005;2;Field  ;
                  CaptionML=[DEU=Mindest ben”tigter Endstand;
                             ENU=Minimal Needed Score;
                             NLD=Minimaal benodigde score;
                             NOR=Minste poengsum n›dvendig;
                             SVE=L„gsta po„ng som beh”vs];
                  NotBlank=Yes;
                  SourceExpr=MinNeededScore;
                  MinValue=1;
                  OnValidate=BEGIN
                               ValidateMinNeededScore();
                             END;

                  OnLookup=BEGIN
                             ScoreRec.Score := ROUND(MinNeededScore,1);
                             IF PAGE.RUNMODAL(0, ScoreRec) = ACTION::LookupOK THEN BEGIN
                               IF (ScoreRec.Score >= 1) AND (ScoreRec.Score <= PurchSetup."Max. Score Assessment")  THEN BEGIN
                                 MinNeededScore := ScoreRec.Score;
                                 GetScoreDescription();
                               END;
                             END;
                           END;
                            }

      { 1100409006;2;Field  ;
                  CaptionML=[DEU=Ergebnisbeschreibung;
                             ENU=ScoreDescription;
                             NLD=Omschrijving score;
                             NOR=Poengsumbeskrivelse];
                  SourceExpr=ScoreDescription;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PurchSetup@1210190010 : Record 312;
      ScoreRec@1210190012 : Record 11012294;
      TmpVendor@1100525000 : TEMPORARY Record 23;
      StartDateAss@1100485000 : Date;
      EndDateAss@1100485001 : Date;
      NoOfAssess@1210190007 : Integer;
      TotalScore@1210190006 : Decimal;
      MaxScore@1210190005 : Decimal;
      AverageScore@1210190004 : Decimal;
      ResultScore@1210190003 : Decimal;
      MinNeededScore@1100485002 : Decimal;
      Text001@1100485005 : TextConst 'DEU=Datum darf nicht leer sein.;ENU=Date may not be empty.;NLD=Datum mag niet leeg zijn.;NOR=Dato kan ikke st† tom.;SVE=Datumet f†r inte vara tomt.';
      Text002@1100485006 : TextConst 'DEU=Datum darf nicht gr”sser als das heutige Datum sein.;ENU=Date may not be greater than the current date.;NLD=Datum mag niet groter zijn dan de huidige datum.;NOR=Dato kan ikke v‘re senere enn gjeldende dato.;SVE=Datumet f†r inte infalla senare „n dagens datum.';
      Text003@1100485007 : TextConst 'DEU=Startdatum darf nicht gr”sser als das Enddatum sein.;ENU=Starting Date may not be greater than the Ending Date.;NLD=Begindatum mag niet groter zijn dan de einddatum.;NOR=Startdato kan ikke v‘re senere enn sluttdato.;SVE=Startdatumet f†r inte infalla senare „n slutdatumet.';
      Text004@1100485008 : TextConst 'DEU=Enddatum darf nicht kleiner als das Startdatum sein.;ENU=Ending Date may not be less than the Starting Date.;NLD=Einddatum mag niet kleinerr zijn dan de begindatum.;NOR=Sluttdato kan ikke inntreffe f›r startdato.;SVE=Slutdatum f†r inte infalla f”re startdatum.';
      Text005@1100485009 : TextConst 'DEU=Der h”chst zul„ssiger Wert ist %1;ENU=The maximum permitted value %1.;NLD=De maximaal toegestane waarde is %1.;NOR=Maksimalt tilatt verdi %1.;SVE=Det h”gsta till†tna v„rdet „r %1.';
      ScoreDescription@1100485014 : Text[50];
      Text006@1100525001 : TextConst 'DEU=Keine zu blockierenden Kreditoren gefunden.;ENU=No vendors to block founded.;NLD=Geen te blokkeren leveranciers gevonden.;NOR=Fant ingen leverand›rer † blokkere.';

    PROCEDURE ReadVendorAssessment@1100485000();
    VAR
      VendorAssessmentVendor@1100485001 : Record 11012299;
      VendorAssessmentLineNew@1100485000 : Record 11072420;
    BEGIN
      NoOfAssess := 0;
      TotalScore := 0;
      MaxScore := 0;
      AverageScore := 0;
      ResultScore := 0;
      ScoreRec.INIT;

      VendorAssessmentVendor.SETCURRENTKEY("Vendor No.", Date);
      VendorAssessmentVendor.SETRANGE("Vendor No.", Vendor."No.");
      VendorAssessmentVendor.SETRANGE(Date, StartDateAss, EndDateAss);
      IF VendorAssessmentVendor.FINDSET(FALSE, FALSE) THEN BEGIN
        REPEAT
          VendorAssessmentLineNew.SETRANGE("Vendor No.", VendorAssessmentVendor."Vendor No.");
          VendorAssessmentLineNew.SETRANGE(Date, VendorAssessmentVendor.Date);
          VendorAssessmentLineNew.SETFILTER("Weight Factor", '<>%1', 0);
          VendorAssessmentLineNew.SETFILTER(Score, '<>%1', 0);
          IF VendorAssessmentLineNew.FINDSET(FALSE, FALSE) THEN BEGIN
            NoOfAssess := NoOfAssess + 1;
            REPEAT
              TotalScore := TotalScore + (VendorAssessmentLineNew."Weight Factor" * VendorAssessmentLineNew.Score);
              MaxScore := MaxScore + (VendorAssessmentLineNew."Weight Factor" * PurchSetup."Max. Score Assessment");
            UNTIL VendorAssessmentLineNew.NEXT = 0;
          END;
        UNTIL VendorAssessmentVendor.NEXT = 0;
      END;

      IF (NoOfAssess > 0) THEN BEGIN
        AverageScore := TotalScore / NoOfAssess;
        IF MaxScore <> 0 THEN
          ResultScore := ROUND((TotalScore / MaxScore * PurchSetup."Max. Score Assessment"), 0.01);
        IF NOT ScoreRec.GET(ROUND(ResultScore,1)) THEN
          ScoreRec.INIT;
      END;
    END;

    PROCEDURE ValidateStartDateAss@1100485002();
    BEGIN
      IF (StartDateAss = 0D) THEN
        ERROR(Text001);
      IF (StartDateAss > TODAY) THEN
        ERROR(Text002);
      IF (StartDateAss > EndDateAss) AND (EndDateAss <> 0D) THEN
        ERROR(Text003);
    END;

    PROCEDURE ValidateEndDateAss@1100485003();
    BEGIN
      IF (EndDateAss = 0D) THEN
        ERROR(Text001);
      IF (EndDateAss > TODAY) THEN
        ERROR(Text002);
      IF (EndDateAss < StartDateAss) THEN
        ERROR(Text004);
    END;

    PROCEDURE ValidateMinNeededScore@1100485004();
    BEGIN
      IF (MinNeededScore > PurchSetup."Max. Score Assessment") THEN
        ERROR(Text005, PurchSetup."Max. Score Assessment");

      GetScoreDescription();
    END;

    PROCEDURE GetScoreDescription@1100485005();
    BEGIN
      ScoreDescription := '';
      IF ScoreRec.GET(ROUND(MinNeededScore,1)) THEN
        ScoreDescription := ScoreRec.Assessment;
    END;

    BEGIN
    {
      //* Not for deblock for the following reasons:
      //* - There can be another reason to block a vendor then the assessment
      //* - When a vendor is blocked for purchase, no business will be done anymore with this vendor,
      //*   so also no assessments.
    }
    END.
  }
  RDLDATA
  {
  }
}

