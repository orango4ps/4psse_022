OBJECT Table 11012747 Document Link
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               SetDefaultCompany;
               SetDPTFields;
               UpdateProjectPortal(1);
               ProjectSiteMgt.LogDocumentLink(1, Rec);
             END;

    OnModify=VAR
               DocumentProperties@1100409001 : Record 11012746;
             BEGIN
               ProjectSiteMgt.LogDocumentLink(2, Rec);
             END;

    OnDelete=BEGIN
               UpdateProjectPortal(3);
               ProjectSiteMgt.LogDocumentLink(3, Rec);
             END;

    CaptionML=[ENU=Document Link;
               NOR=Dokumentlink;
               SVE=Dokumentl„nk];
  }
  FIELDS
  {
    { 10  ;   ;Document No.        ;Code20        ;TableRelation="Document Properties".No.;
                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Dokumentnr.;
                                                              SVE=Dokumentnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Seq. No.            ;Integer       ;CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   Editable=No }
    { 30  ;   ;Table No.           ;Integer       ;TableRelation="Possible Document Link"."Table No.";
                                                   OnValidate=BEGIN
                                                                IF xRec."Table No." <> 0 THEN BEGIN
                                                                  CLEAR("Record ID");
                                                                  CLEAR("Filter Expression");
                                                                  Source := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr] }
    { 40  ;   ;Filter Expression   ;Text100       ;CaptionML=[ENU=Filter Expression;
                                                              NOR=Filteruttrykk;
                                                              SVE=Filteruttryck];
                                                   Editable=No }
    { 45  ;   ;Internal Company    ;Text50        ;TableRelation=Company;
                                                   CaptionML=[ENU=Company;
                                                              NOR=Selskap;
                                                              SVE=F”retag];
                                                   Editable=No }
    { 50  ;   ;Record ID           ;RecordID      ;CaptionML=[ENU=Record ID;
                                                              NOR=Post-ID;
                                                              SVE=Post-ID];
                                                   Editable=No }
    { 60  ;   ;Source              ;Boolean       ;OnValidate=VAR
                                                                DocumentLink@1199198000 : Record 11012747;
                                                              BEGIN
                                                                IF NOT xRec.Source THEN BEGIN
                                                                  DocumentLink.SETRANGE("Document No.", "Document No.");
                                                                  DocumentLink.SETRANGE(Source, TRUE);
                                                                  DocumentLink.SETRANGE("Internal Company", "Internal Company");
                                                                  IF DocumentLink.FINDFIRST THEN BEGIN
                                                                    DocumentLink.Source := FALSE;
                                                                    DocumentLink.MODIFY;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Source;
                                                              NOR=Source;
                                                              SVE=K„lla] }
    { 501 ;   ;D3 Document Field Link ID;Integer  ;CaptionML=ENU=D3 Document Field Link ID }
  }
  KEYS
  {
    {    ;Document No.,Seq. No.                   ;Clustered=Yes }
    {    ;Record ID,Table No.,Internal Company,Source }
    {    ;Table No.                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProjectSiteMgt@1100528300 : Codeunit 11012282;

    PROCEDURE HandleRenameOfDocumentLink@1100528700(OldRID@1100528700 : RecordID;NewRID@1100528701 : RecordID);
    VAR
      DocumentLink@1100528702 : Record 11012747;
    BEGIN
      DocumentLink.SETCURRENTKEY("Record ID");
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', OldRID));
      WHILE DocumentLink.FINDFIRST DO BEGIN
        DocumentLink."Record ID" := NewRID;
        DocumentLink.MODIFY;
      END;
    END;

    PROCEDURE UpdateProjectPortal@1100528300(Action@1100528305 : Option);
    VAR
      DocumentProperties@1100528301 : Record 11012746;
      Job@1100528300 : Record 11072003;
      SharepointIntegration@1100528306 : Codeunit 11020210;
      DocumentPortalProject@1100528304 : Codeunit 11012948;
      ProjectElement@1100528307 : Code[20];
      RecRef@1100528302 : RecordRef;
      InstallationManagement@1100527800 : Codeunit 11012268;
    BEGIN
      IF (NOT InstallationManagement.LicensePermissionForCodeUnit(CODEUNIT::"Document Portal Project")) THEN
        EXIT;

      IF (NOT SharepointIntegration.IsProjectPortalActive) THEN
        EXIT;

      ProjectElement := '';
      CASE "Table No." OF
        DATABASE::Job: BEGIN
          IF (NOT RecRef.GET("Record ID")) THEN
            EXIT;
          RecRef.SETTABLE(Job);
        END;
        ELSE
          EXIT;
      END;

      DocumentProperties.GET("Document No.");
      IF NOT (DocumentProperties.HasStorageURI) THEN
        EXIT;

      IF (Job.IsJobInProjectPortal) THEN BEGIN
        CASE Action OF
          1: BEGIN //Add
            IF ((DocumentProperties."Project Portal Subsite" = '') OR (DocumentProperties."Storage Type" =  DocumentProperties."Storage Type"::PPT)) THEN
              EXIT;
            DocumentPortalProject.AddDocument(Job, DocumentProperties, TRUE);
          END;
          3: //Delete
            IF (DocumentProperties."Storage Type" =  DocumentProperties."Storage Type"::PPT) THEN
              DocumentPortalProject.RemoveDocument(Job, DocumentProperties);
        END;
      END;
    END;

    PROCEDURE IsPortalLink@1100528701() : Boolean;
    VAR
      Job@1100528701 : Record 11072003;
      ServiceLocation@1100525000 : Record 11012801;
      RecRef@1100528700 : RecordRef;
    BEGIN
      CASE "Table No." OF
        DATABASE::Job: BEGIN
          IF (NOT RecRef.GET("Record ID")) THEN
            EXIT(FALSE);
          RecRef.SETTABLE(Job);
          EXIT((Job.IsJobInProjectPortal) OR (Job.IsDocumentPortalDualProject));
        END;
        DATABASE::"Service Location": BEGIN
          IF (NOT RecRef.GET("Record ID")) THEN
            EXIT(FALSE);
          RecRef.SETTABLE(ServiceLocation);
          EXIT(ServiceLocation.IsDocumentPortalServiceLocation);
        END;
      END;
      EXIT(FALSE);
    END;

    PROCEDURE SetDPTFields@1100525000();
    VAR
      DocumentProperties@1100525000 : Record 11012746;
    BEGIN
      IF (NOT IsPortalLink) THEN
        EXIT;
      IF (NOT DocumentProperties.GET("Document No.")) THEN
        EXIT;
      DocumentProperties.SetDPTFields("Record ID");
      DocumentProperties.MODIFY;
    END;

    PROCEDURE SetDefaultCompany@1100525001();
    BEGIN
      IF "Internal Company" = '' THEN
        "Internal Company" := COMPANYNAME;
    END;

    PROCEDURE SetDocumentPropertiesShowInControl@1100528301();
    VAR
      DocumentProperties@1100528300 : Record 11012746;
    BEGIN
      IF ("Document No." <> '') AND ("Table No." = DATABASE::Contact) THEN BEGIN
        IF DocumentProperties.GET("Document No.") THEN BEGIN
          DocumentProperties.SetShowInControl(TRUE);
          DocumentProperties.MODIFY;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

