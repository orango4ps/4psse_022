OBJECT Table 11012591 Plant Cost Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Cost Level" = "Cost Level"::"Standard Cost" THEN
                 CheckMinAndMaxLine
               ELSE
                 TESTFIELD(Code);
               { 23-03-2010 Now also 'Transport' allowed, standard cost are inserted on TO (so not on PO)
               IF (Type = Type::Transport) THEN BEGIN
                 PlantSetupRec.GET;
                 IF PlantSetupRec."Transport Orders" THEN
                   FIELDERROR(Type, STRSUBSTNO(Text000, Type, PlantSetupRec.TABLECAPTION, PlantSetupRec.FIELDCAPTION("Transport Orders")));
                   Text000 was: ENU='%1' not allowed because %2 '%3' is on.;NLD='%1' niet toegestaan omdat de %2 '%3' aan staat.
               END;
               }
             END;

    OnDelete=VAR
               PlantCostLinePrice@1100529000 : Record 11072664;
             BEGIN
               //DP00241 (16930)
               PlantCostLinePrice.SETRANGE("Cost Level", "Cost Level");
               PlantCostLinePrice.SETRANGE(Code, Code);
               PlantCostLinePrice.SETRANGE("Cost Line No.", "Line No.");
               PlantCostLinePrice.DELETEALL;
             END;

    OnRename=VAR
               PlantCostLinePrice@1100529000 : Record 11072664;
             BEGIN
               //DP00241 (16930)
               PlantCostLinePrice.SETRANGE("Cost Level", "Cost Level");
               PlantCostLinePrice.SETRANGE(Code, Code);
               PlantCostLinePrice.SETRANGE("Cost Line No.", "Line No.");
               IF NOT PlantCostLinePrice.ISEMPTY THEN
                 ERROR(PlantCostLinePrice.TABLECAPTION);
             END;

    CaptionML=[ENU=Plant Cost Line;
               NOR=Maskinkostrad;
               SVE=Maskinkostnadsrad];
  }
  FIELDS
  {
    { 5   ;   ;Cost Level          ;Option        ;CaptionML=[ENU=Cost Level;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=Plant Type,Set,Location,Customer Price Group,Standard Cost;
                                                   OptionString=PlantType,Set,Location,CustomerPriceGroup,Standard Cost;
                                                   Description=Option must be the same as in T11072664;
                                                   Editable=No }
    { 10  ;   ;Code                ;Code20        ;TableRelation=IF (Cost Level=CONST(PlantType)) "Plant Type"
                                                                 ELSE IF (Cost Level=CONST(Set)) "Plant Set"
                                                                 ELSE IF (Cost Level=CONST(Location)) "Plant Location"
                                                                 ELSE IF (Cost Level=CONST(CustomerPriceGroup)) "Customer Price Group";
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer] }
    { 30  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                { 23-03-2010 Now also 'Transport' allowed, standard cost are inserted on TO (so not on PO)
                                                                IF (Type = Type::Transport) THEN BEGIN
                                                                  PlantSetupRec.GET;
                                                                  IF PlantSetupRec."Transport Orders" THEN
                                                                    FIELDERROR(Type, STRSUBSTNO(Text000, Type, PlantSetupRec.TABLECAPTION, PlantSetupRec.FIELDCAPTION("Transport Orders")));
                                                                END;
                                                                }
                                                                IF (Type = Type::Damage) AND ("Cost Level" <> "Cost Level"::PlantType) THEN
                                                                  FIELDERROR(Type);

                                                                IF (Type = Type::Transport) THEN BEGIN  // 23-03-2010 (new)
                                                                  PlantSetupRec.GET;
                                                                  IF PlantSetupRec."Transport Orders" THEN
                                                                    "Vendor No." := '';
                                                                END;

                                                                CheckMinAndMaxLine;

                                                                SaveCostLineRec := Rec;
                                                                INIT;
                                                                Type := SaveCostLineRec.Type;

                                                                //IF Type > Type::Text THEN BEGIN
                                                                //  DimValRec.SETFILTER("Global Dimension No.", '2');
                                                                //  DimValRec.SETRANGE(Plant, Type);
                                                                //  IF DimValRec.COUNT = 1 THEN BEGIN
                                                                //    DimValRec.FIND('-');
                                                                //    VALIDATE("No.", DimValRec.Code);
                                                                //  END;
                                                                //END;

                                                                //DP00241.n (C004697)
                                                                IF Type = Type::Damage THEN
                                                                  "Type Cost":= "Type Cost"::Removal;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Text,Hours,Transport,Sundry Costs,Damage;
                                                                    NOR=Tekst,Timer,Transport,Diversekoster,Skade;
                                                                    SVE=Text,Timmar,Transport,Diverse kostnader,Skada];
                                                   OptionString=Text,Hours,Transport,Sundry Costs,Damage }
    { 40  ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Text)) "Standard Text"
                                                                 ELSE IF (Type=CONST(Hours)) "Dimension Value".Code WHERE (Plant=CONST(Hours),
                                                                                                                           Global Dimension No.=CONST(2))
                                                                                                                           ELSE IF (Type=CONST(Transport)) "Dimension Value".Code WHERE (Plant=CONST(Transport),
                                                                                                                                                                                         Global Dimension No.=CONST(2))
                                                                                                                                                                                         ELSE IF (Type=CONST(Sundry Costs)) "Dimension Value".Code WHERE (Plant=CONST(Sundry),
                                                                                                                                                                                                                                                          Global Dimension No.=CONST(2))
                                                                                                                                                                                                                                                          ELSE IF (Type=CONST(Damage)) "Dimension Value".Code WHERE (Plant=CONST(Damage),
                                                                                                                                                                                                                                                                                                                     Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                IF "No." <> '' THEN
                                                                  IF Type = Type::Text THEN BEGIN
                                                                    StandTextRec.GET("No.");
                                                                    Description := StandTextRec.Description;
                                                                  END ELSE BEGIN
                                                                    DimMgtCU.GetDimValueRec(2, "No.", DimValRec, TRUE, '');
                                                                    IF NOT PlantDamageCodeBased THEN BEGIN
                                                                      Description := DimValRec.Name;
                                                                      "Unit of Measure" := DimValRec."Unit of Measure";
                                                                      //VALIDATE(Price, DimValRec."Sales Price");  //DP00241.o (16930)
                                                                    END;
                                                                    CheckMinAndMaxLine;
                                                                    GetStandardMinAndMaxValues;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 50  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 60  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF (Quantity <> 0) AND ((Type = Type::Text) OR (Type = Type::Damage)) THEN  //DP00241.c (C004697)
                                                                  FIELDERROR(Quantity);

                                                                //VALIDATE(Price);  //DP00241.o (16930)
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 70  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=BEGIN
                                                                IF ("Unit of Measure" <> '') AND (Type = Type::Text) THEN
                                                                  FIELDERROR("Unit of Measure");
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=M†ttenhet] }
    { 90  ;   ;Damage Code         ;Code10        ;TableRelation=IF (Type=CONST(Damage)) "Plant Type Damage Code".Code WHERE (Plant Type=FIELD(Code));
                                                   OnValidate=VAR
                                                                PlantTypeDamageCode@1100529000 : Record 11229920;
                                                              BEGIN
                                                                IF "Damage Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Type, Type::Damage);
                                                                  PlantTypeDamageCode.GET(Code, "Damage Code");
                                                                  Description := PlantTypeDamageCode.Description;
                                                                  "Unit of Measure" := PlantTypeDamageCode."Unit of Measure";
                                                                END ELSE BEGIN
                                                                  IF xRec."Damage Code" <> '' THEN BEGIN
                                                                    IF "No." <> '' THEN
                                                                      VALIDATE("No.");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Damage Code }
    { 100 ;   ;Type Cost           ;Option        ;OnValidate=BEGIN
                                                                //*C-025816
                                                                IF "Type Cost" = "Type Cost"::Removal THEN
                                                                  "Incl. Arrival Other Transfers" := FALSE;

                                                                //DP00241.n (C004697)
                                                                IF Type = Type::Damage THEN
                                                                  TESTFIELD("Type Cost", "Type Cost"::Removal);

                                                                CheckMinAndMaxLine;
                                                              END;

                                                   CaptionML=[ENU=Type Cost;
                                                              NOR=Typekost;
                                                              SVE=Typkostnad];
                                                   OptionCaptionML=[ENU=Arrival,Removal,Both;
                                                                    NOR=Ankomst,Fjerning,Begge;
                                                                    SVE=Ankomst,Borttagning,B†da];
                                                   OptionString=Arrival,Removal,Both }
    { 101 ;   ;Incl. Arrival Other Transfers;Boolean;
                                                   OnValidate=BEGIN
                                                                //*C-025816
                                                                IF "Incl. Arrival Other Transfers" THEN BEGIN
                                                                  IF ("Type Cost" = "Type Cost"::Removal) THEN
                                                                    TESTFIELD("Incl. Arrival Other Transfers", FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Incl. Arrival Other Transfers;
                                                              NOR=Inkludert Ankomst for andre Overf›ringer;
                                                              SVE=Inkl. ankomst f”r andra ”verf”ringar] }
    { 110 ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                IF (Type = Type::Transport) THEN BEGIN  // 23-03-2010 (new)
                                                                  PlantSetupRec.GET;
                                                                  IF PlantSetupRec."Transport Orders" THEN
                                                                    TESTFIELD("Vendor No.", '');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Vendor No. (Default);
                                                              NOR=Leverand›rnr (Standard);
                                                              SVE=Leverant”rsnr (standard)] }
    { 120 ;   ;Minimum Value       ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Minimum Value" <> 0) AND (Type = Type::Text) THEN
                                                                  FIELDERROR(Type);
                                                                IF ("Minimum Value" > "Maximum Value") AND ("Maximum Value" <> 0) THEN
                                                                  ERROR(Text002, FIELDCAPTION("Minimum Value"), FIELDCAPTION("Maximum Value"));
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Minimum Value;
                                                              SVE=Minimiv„rde];
                                                   MinValue=0 }
    { 130 ;   ;Maximum Value       ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Maximum Value" <> 0) AND (Type = Type::Text) THEN
                                                                  FIELDERROR(Type);
                                                                IF "Maximum Value" < "Minimum Value" THEN
                                                                  ERROR(Text003, FIELDCAPTION("Maximum Value"), FIELDCAPTION("Minimum Value"));
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Maximum Value;
                                                   MinValue=0 }
  }
  KEYS
  {
    {    ;Cost Level,Code,Line No.                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetupRec@1100485001 : Record 11012550;
      StandTextRec@11012003 : Record 7;
      DimValRec@11012004 : Record 349;
      SaveCostLineRec@11012005 : Record 11012591;
      DimMgtCU@1210190000 : Codeunit 408;
      Text001@1100525000 : TextConst 'ENU=Standard cost line ''%1'' for transportorder;NOR=Standardkostrad ''%1'' for transportordre;SVE=Standardkostnadsrad ''%1'' f”r transportorder';
      Text002@1100529601 : TextConst 'ENU=%1 must not be greater than %2.;SVE=%1 f†r inte vara st”rre „n %2.';
      Text003@1100529600 : TextConst 'ENU=%1 must not be less than %2.';
      Text004@1100529602 : TextConst 'ENU="Plant Standard Cost (%1= %2, %3 = %4, %5 = %6) already exists."';

    PROCEDURE GetCommentTransport@1100525000() : Text[100];
    BEGIN
      IF (Type = Type::Transport) THEN BEGIN
        PlantSetupRec.GET;
        IF PlantSetupRec."Transport Orders" THEN
          EXIT(STRSUBSTNO(Text001, Type));
      END;
      EXIT('');
    END;

    PROCEDURE CheckUpdateDefaultCostPrice@1100529001();
    VAR
      PlantCostLinePrice@1100529000 : Record 11072664;
      PlantTypeDamageCode@1100528901 : Record 11229920;
      LinePrice@1100528900 : Decimal;
    BEGIN
      //DP00241 (16930)
      IF (Type = Type::Text) OR (Code = '') OR ("Line No." = 0) THEN
        EXIT;

      IF NOT PlantDamageCodeBased THEN BEGIN
        IF "No." = '' THEN
          EXIT;
        DimMgtCU.GetDimValueRec(2, "No.", DimValRec, FALSE, '');
        IF DimValRec."Sales Price" = 0 THEN
          EXIT;
        LinePrice := DimValRec."Sales Price";
      END ELSE BEGIN
        IF NOT PlantTypeDamageCode.GET(Code, "Damage Code") THEN
          EXIT;
        IF PlantTypeDamageCode.Price = 0 THEN
          EXIT;
        LinePrice := PlantTypeDamageCode.Price;
      END;

      PlantCostLinePrice.SETRANGE("Cost Level", "Cost Level");
      PlantCostLinePrice.SETRANGE(Code, Code);
      PlantCostLinePrice.SETRANGE("Cost Line No.", "Line No.");
      IF PlantCostLinePrice.ISEMPTY THEN BEGIN
        PlantCostLinePrice.INIT;
        PlantCostLinePrice."Cost Level" := "Cost Level";
        PlantCostLinePrice.Code := Code;
        PlantCostLinePrice."Cost Line No." := "Line No.";
        PlantCostLinePrice."Starting Date" := 0D;
        PlantCostLinePrice.Price := LinePrice;
        PlantCostLinePrice.INSERT;
      END ELSE BEGIN
        IF ((NOT PlantDamageCodeBased) AND ("No." <> xRec."No.")) OR ((PlantDamageCodeBased) AND ("Damage Code" <> xRec."Damage Code"))THEN BEGIN
          IF PlantCostLinePrice.COUNT = 1 THEN BEGIN
            IF PlantCostLinePrice.FINDFIRST THEN BEGIN
              IF PlantCostLinePrice.Price <> DimValRec."Sales Price" THEN BEGIN
                PlantCostLinePrice.Price := LinePrice;
                PlantCostLinePrice.MODIFY;
              END;
            END;
          END;
        END;
      END;
    END;

    PROCEDURE RunCostLinePrices@1100529000();
    VAR
      PlantCostLinePrice@1100529000 : Record 11072664;
    BEGIN
      //DP00241 (16930)
      IF Type = Type::Text THEN
        FIELDERROR(Type);

      PlantCostLinePrice.FILTERGROUP(4);
      PlantCostLinePrice.SETRANGE("Cost Level", "Cost Level");
      PlantCostLinePrice.SETRANGE(Code, Code);
      PlantCostLinePrice.SETRANGE("Cost Line No.", "Line No.");
      PlantCostLinePrice.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Plant Cost Line Prices", PlantCostLinePrice);
    END;

    PROCEDURE GetCostLinePrice@1100529002(RefDate@1100529001 : Date) : Decimal;
    VAR
      PlantCostLinePrice@1100529000 : Record 11072664;
    BEGIN
      //DP00241 (16930)
      IF RefDate = 0D THEN
        RefDate := WORKDATE;
      PlantCostLinePrice.SETRANGE("Cost Level", "Cost Level");
      PlantCostLinePrice.SETRANGE(Code, Code);
      PlantCostLinePrice.SETRANGE("Cost Line No.", "Line No.");
      PlantCostLinePrice.SETRANGE("Starting Date", 0D, RefDate);
      IF PlantCostLinePrice.FINDLAST THEN
        EXIT(PlantCostLinePrice.Price);
    END;

    PROCEDURE GetAmount@1100529006(RefDate@1100529000 : Date) : Decimal;
    BEGIN
      //DP00241 (16930)
      EXIT(ROUND(Quantity * GetCostLinePrice(RefDate)));
    END;

    LOCAL PROCEDURE PlantDamageCodeBased@1100528900() : Boolean;
    BEGIN
      IF (Type = Type::Damage) AND ("Damage Code" <> '') THEN
        EXIT(TRUE);
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckMinAndMaxLine@1100529608();
    VAR
      PlantCostLine@1100529600 : Record 11012591;
    BEGIN
      IF "Cost Level" <> "Cost Level"::"Standard Cost" THEN
        EXIT;
      TESTFIELD(Code, '');
      IF Type = Type::Text THEN
        FIELDERROR(Type);

      PlantCostLine.SETRANGE("Cost Level", "Cost Level");
      PlantCostLine.SETRANGE(Code, '');
      PlantCostLine.SETRANGE(Type, Type);
      PlantCostLine.SETRANGE("No.", "No.");
      PlantCostLine.SETRANGE("Type Cost", "Type Cost");
      PlantCostLine.SETFILTER("Line No.", '<>%1', "Line No.");
      IF NOT PlantCostLine.ISEMPTY THEN
        ERROR(Text004,
              FIELDCAPTION(Type), FORMAT(Type),
              FIELDCAPTION("No."), FORMAT("No."),
              FIELDCAPTION("Type Cost"), FORMAT("Type Cost"));
    END;

    LOCAL PROCEDURE GetStandardMinAndMaxValues@1100529606();
    VAR
      PlantStandardCostLine@1100529600 : Record 11012591;
    BEGIN
      IF ("Cost Level" = "Cost Level"::"Standard Cost") OR (Type = Type::Text) THEN
        EXIT;

      IF NOT GetStandardMinAndMaxLine(PlantStandardCostLine, '') THEN
        EXIT;

      "Maximum Value" := PlantStandardCostLine."Maximum Value";
      IF (PlantStandardCostLine."Minimum Value" < "Maximum Value") OR ("Maximum Value" = 0) THEN
        "Minimum Value" := PlantStandardCostLine."Minimum Value";
    END;

    PROCEDURE GetStandardMinAndMaxLine@1100529600(VAR PlantStandardCostLine@1100529600 : Record 11012591;FilterCode@1100529601 : Code[20]) : Boolean;
    BEGIN
      PlantStandardCostLine.RESET;
      PlantStandardCostLine.SETRANGE("Cost Level", PlantStandardCostLine."Cost Level"::"Standard Cost");
      PlantStandardCostLine.SETRANGE(Code, FilterCode);
      PlantStandardCostLine.SETRANGE(Type, Type);
      PlantStandardCostLine.SETRANGE("No.", "No.");
      PlantStandardCostLine.SETRANGE("Type Cost", "Type Cost");
      IF PlantStandardCostLine.ISEMPTY THEN
        IF "Type Cost" IN ["Type Cost"::Arrival, "Type Cost"::Removal] THEN
          PlantStandardCostLine.SETRANGE("Type Cost", PlantStandardCostLine."Type Cost"::Both);
      IF PlantStandardCostLine.ISEMPTY THEN
        EXIT(FALSE);
      PlantStandardCostLine.FINDLAST;
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

