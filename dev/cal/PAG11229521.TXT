OBJECT Page 11229521 Copy Settlement Quantities
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Verrechnungsmengen kopieren;
               ENU=Copy Settlement Quantities;
               NLD=Verrekenbare hoeveelheden kopi‰ren];
    PageType=StandardDialog;
    OnInit=BEGIN
             ProjectFromEditable := TRUE;
             HouseModelFromVisible := TRUE;
             HouseModelToEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 HouseModelToEditable := TRUE;
                 HouseModelFromVisible := TRUE;
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190006;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopi‰ren van;
                           NOR=Kopier fra;
                           SVE=Kopiera fr†n];
                GroupType=Group }

    { 1210190004;2;Field  ;
                Name=ProjectFrom;
                CaptionML=[DEU=Projektnr.;
                           ENU=Project No.;
                           NLD=Projectnr.;
                           NOR=Prosjektnr;
                           SVE=Projektnr];
                SourceExpr=ProjectFrom;
                Editable=ProjectFromEditable;
                OnValidate=BEGIN
                             IF ProjectFrom <> '' THEN
                               ProjectRec.GET(ProjectFrom);
                           END;

                OnLookup=BEGIN
                           LookupProject(ProjectFrom);
                         END;
                          }

    { 1210190000;2;Field  ;
                Name=HouseModelFrom;
                CaptionML=[DEU=Wohnungsmodell;
                           ENU=House Model;
                           NLD=Woningmodel;
                           NOR=Husmodell;
                           SVE=Husmodell];
                SourceExpr=HouseModelFromFilter;
                Visible=HouseModelFromVisible;
                OnLookup=BEGIN
                           LookupHouseModelFilter(ProjectFrom, HouseModelFromFilter);
                         END;
                          }

    { 1210190021;2;Field  ;
                CaptionML=[DEU=Verrechnungsmenge;
                           ENU=Settlement Quantity;
                           NLD=Verrekenbare hoeveelheid];
                SourceExpr=SQFromFilter;
                OnLookup=BEGIN
                           LookupSQFilter(ProjectFrom, HouseModelFromFilter, SQFromFilter);
                         END;
                          }

    { 1210190009;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopi‰ren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 1210190007;2;Field  ;
                CaptionML=[DEU=Projektnr.;
                           ENU=Project No.;
                           NLD=Projectnr.;
                           NOR=Prosjektnr;
                           SVE=Projektnr];
                SourceExpr=ProjectTo;
                Editable=FALSE }

    { 1210190010;2;Field  ;
                Name=HouseModelTo;
                CaptionML=[DEU=Wohnungsmodell;
                           ENU=House Model;
                           NLD=Woningmodel;
                           NOR=Husmodell;
                           SVE=Husmodell];
                SourceExpr=HouseModelTo;
                Editable=HouseModelToEditable;
                OnLookup=BEGIN
                           LookupHouseModelFilter(ProjectTo, HouseModelTo);
                         END;
                          }

  }
  CODE
  {
    VAR
      HouseModelRec@1210190012 : Record 11012501;
      SettlementQuantity@1210190006 : Record 11072243;
      ProjectRec@1210190004 : Record 11072003;
      ProjectFrom@1210190000 : Code[20];
      ProjectTo@1210190002 : Code[20];
      HouseModelTo@1210190013 : Code[20];
      HouseModelFromFilter@1210190009 : Code[250];
      SQFromFilter@1210190001 : Code[250];
      HouseModelToEditable@19044251 : Boolean INDATASET;
      HouseModelFromVisible@19050165 : Boolean INDATASET;
      ProjectFromEditable@19049011 : Boolean INDATASET;
      CloseActionOK@1100528400 : Boolean;

    PROCEDURE SetDefaults@1210190001(IProjectTo@1210190005 : Code[20];IHouseModelTo@1210190006 : Code[20];IHouseModelFrom@1210190004 : Code[250];ISQFrom@1210190000 : Code[250]);
    BEGIN
      ProjectFrom := '';
      ProjectTo := IProjectTo;
      HouseModelTo := IHouseModelTo;
      HouseModelFromFilter := IHouseModelFrom;
      SQFromFilter := ISQFrom;
    END;

    PROCEDURE LookupProject@1210190009(VAR IOProjectNo@1210190000 : Code[20]);
    BEGIN
      WITH ProjectRec DO BEGIN
        RESET;
        SETRANGE("Housebuilding Project", TRUE);
        "No." := IOProjectNo;
        IF PAGE.RUNMODAL(PAGE::"Job List",ProjectRec) = ACTION::LookupOK THEN BEGIN
          IOProjectNo := "No.";
        END;
      END;
    END;

    PROCEDURE LookupHouseModelFilter@1210190010(IProjectNo@1210190001 : Code[20];VAR IOHouseModelFilter@1210190000 : Code[250]);
    BEGIN
      WITH HouseModelRec DO BEGIN
        RESET;
        IF IProjectNo <> '' THEN
          SETRANGE("Project No.", IProjectNo)
         ELSE
          SETRANGE("Project No.", '');
        IF IOHouseModelFilter <> '' THEN BEGIN
          SETFILTER(Model, IOHouseModelFilter);
          IF FIND('-') THEN;
          SETRANGE(Model);
        END;
        IF IProjectNo <> '' THEN BEGIN
          IF PAGE.RUNMODAL(PAGE::"House Models by Project List", HouseModelRec) = ACTION::LookupOK THEN
            IOHouseModelFilter := IOHouseModelFilter + Model;
        END ELSE BEGIN
          IF PAGE.RUNMODAL(PAGE::"House Model List", HouseModelRec) = ACTION::LookupOK THEN
            IOHouseModelFilter := IOHouseModelFilter + Model;
        END;
      END;
    END;

    PROCEDURE LookupSQFilter@1210190005(IProjectNo@1210190000 : Code[20];IHouseModelFilter@1210190001 : Code[250];VAR IOSQFilter@1210190005 : Code[250]);
    BEGIN
      WITH SettlementQuantity DO BEGIN
        RESET;
        IF ProjectFrom <> '' THEN
          SETRANGE("Project No.", IProjectNo)
         ELSE
          SETRANGE("Project No.", '');

        IF IHouseModelFilter <> '' THEN
          SETFILTER("House Model", IHouseModelFilter);

        IF IOSQFilter <> '' THEN BEGIN
          SETFILTER(Code, IOSQFilter);
          IF FINDSET(FALSE, FALSE) THEN;
          SETRANGE(Code);
        END;
        IF PAGE.RUNMODAL(PAGE::"Settlement Quantity List", SettlementQuantity) = ACTION::LookupOK THEN
          IOSQFilter := IOSQFilter + Code;
      END;
    END;

    LOCAL PROCEDURE CopyProjectSQ@1100528404();
    VAR
      lvSettlementQuantityManagement@1100528402 : Codeunit 11071690;
      lvLevelFrom@1100528401 : 'Standard,Project';
      lvLevelTo@1100528400 : 'Standard,Project';
    BEGIN
      IF (ProjectFrom = '') THEN
        lvLevelFrom := lvLevelFrom::Standard
      ELSE
        lvLevelFrom := lvLevelFrom::Project;

      IF (ProjectTo = '') THEN
        lvLevelTo := lvLevelTo::Standard
      ELSE
        lvLevelTo := lvLevelTo::Project;

      lvSettlementQuantityManagement.MMRCopySettlementQuantities(ProjectFrom,ProjectTo,
       HouseModelFromFilter, HouseModelTo, SQFromFilter)
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      CopyProjectSQ;
    END;

    PROCEDURE StdDialogRunModal@1100528402();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

