OBJECT Table 11012100 Delivered Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ComplaintSetup.GET;
                 ComplaintSetup.TESTFIELD("Delivered Project Nos.");
                 NoSeriesMgt.InitSeries(ComplaintSetup."Delivered Project Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               UpdateDeliverdObjects();
             END;

    OnDelete=VAR
               lDeliveredObjectRec@1210190000 : Record 11012101;
             BEGIN
               lDeliveredObjectRec.SETCURRENTKEY("Delivered Project","No.");
               lDeliveredObjectRec.SETRANGE("Delivered Project","No.");
               IF lDeliveredObjectRec.FIND('-') THEN
                 ERROR(Text001,"No.");
             END;

    CaptionML=[ENU=Delivered Project;
               NOR=Leverert prosjekt;
               SVE=Levererat projekt];
    LookupPageID=Page11012360;
    DrillDownPageID=Page11012359;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ComplaintSetup.GET;
                                                                  NoSeriesMgt.TestManual(ComplaintSetup."Delivered Project Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 20  ;   ;Name                ;Text100       ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 30  ;   ;Address             ;Text100       ;CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress] }
    { 40  ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2;
                                                              SVE=Adress 2] }
    { 50  ;   ;Postal Code         ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=VAR
                                                                PostcodeRec@1210190000 : Record 225;
                                                              BEGIN
                                                                IF PostcodeRec.GET("Postal Code") THEN
                                                                  City := PostcodeRec.City;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Postal Code;
                                                              NOR=Postnummer;
                                                              SVE=Postnummer] }
    { 60  ;   ;City                ;Text30        ;CaptionML=[ENU=City;
                                                              NOR=Poststed;
                                                              SVE=Ort] }
    { 70  ;   ;Territory Code      ;Code10        ;TableRelation=Territory;
                                                   CaptionML=[ENU=Territory Code;
                                                              NOR=Distriktskode;
                                                              SVE=Distriktskod] }
    { 80  ;   ;Type                ;Code10        ;TableRelation=Type;
                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp] }
    { 90  ;   ;Site Manager        ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Site Manager");
                                                              END;

                                                   CaptionML=[ENU=Site Manager;
                                                              NOR=Prosjektleder;
                                                              SVE=Arbetschef] }
    { 100 ;   ;Project Engineer    ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Project Engineer");
                                                              END;

                                                   CaptionML=[ENU=Project Engineer;
                                                              NOR=Prosjektingeni›r;
                                                              SVE=Projektingenj”r] }
    { 110 ;   ;Buyer Contact       ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Buyer Contact");
                                                              END;

                                                   CaptionML=[ENU=Buyer Contact;
                                                              NOR=Kundeansvarlig;
                                                              SVE=Kundansvarig] }
    { 120 ;   ;Project Manager     ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Project Manager");
                                                              END;

                                                   CaptionML=[ENU=Project Manager;
                                                              NOR=Prosjektleder;
                                                              SVE=Platschef] }
    { 125 ;   ;Responsible Person  ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Responsible Person");
                                                              END;

                                                   CaptionML=[ENU=Responsible Person;
                                                              NOR=Ansvarlig person;
                                                              SVE=Ansvarig person] }
    { 130 ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettet den;
                                                              SVE=Uppl„ggningsdatum];
                                                   Editable=No }
    { 140 ;   ;Original Project No.;Code20        ;TableRelation=Job.No.;
                                                   OnValidate=VAR
                                                                Job@1100529000 : Record 11072003;
                                                              BEGIN
                                                                IF ("Original Project No." <> '') AND ("Original Project No." <> xRec."Original Project No.") THEN BEGIN
                                                                  IF Job.GET("Original Project No.") THEN
                                                                    "Original Project Description" := Job.Description;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Original Project No.;
                                                              NOR=Originalt prosjektnr.;
                                                              SVE=Ursprungligt projektnr];
                                                   Editable=Yes }
    { 150 ;   ;Original Project Description;Text100;
                                                   CaptionML=[ENU=Original Project Description;
                                                              NOR=Opprinnelig prosjektbeskrivelse;
                                                              SVE=Ursprunglig projektbeskrivning];
                                                   Editable=Yes }
    { 200 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 210 ;   ;Name Project Manager;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Project Manager)));
                                                   CaptionML=[ENU=Name Project Manager;
                                                              NOR=Prosjektlederens navn;
                                                              SVE=Platschefens namn];
                                                   Editable=No }
    { 220 ;   ;Name Site Manager   ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Site Manager)));
                                                   CaptionML=[ENU=Name Site Manager;
                                                              NOR=Prosjektlederens navn;
                                                              SVE=Arbetsledarens namn];
                                                   Editable=No }
    { 230 ;   ;Name Buyer Contact  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Buyer Contact)));
                                                   CaptionML=[ENU=Name Buyer Contact;
                                                              NOR=Navn p† Kundekontakt;
                                                              SVE=Kundansvarigs namn];
                                                   Editable=No }
    { 240 ;   ;Name Project Engineer;Text100      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Project Engineer)));
                                                   CaptionML=[ENU=Name Project Engineer;
                                                              NOR=Prosjektingeni›rens navn;
                                                              SVE=Projektingenj”rens namn];
                                                   Editable=No }
    { 250 ;   ;Name Responsible Person;Text100    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Responsible Person)));
                                                   CaptionML=[ENU=Name Responsible Person;
                                                              NOR=Navn p† ansvarlig;
                                                              SVE=Namn p† ansvarig];
                                                   Editable=No }
    { 310 ;   ;Supervisor          ;Code20        ;TableRelation=Contact.No. WHERE (Type=CONST(Person));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name SuperVisor");
                                                              END;

                                                   CaptionML=[ENU=Supervisor;
                                                              NOR=Ekstern arbeidsleder;
                                                              SVE=Extern arbetsledare] }
    { 315 ;   ;Name SuperVisor     ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Supervisor)));
                                                   CaptionML=[ENU=Name Supervisor;
                                                              NOR=Arbeidslederens navn;
                                                              SVE=Namn extern arbetsledare];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Original Project No.                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ComplaintSetup@1210190002 : Record 11012108;
      NoSeriesMgt@1210190000 : Codeunit 396;
      Text001@1210190001 : TextConst 'ENU="There are still Delivered Objects related to Delivered Project: %1. ";NOR=Det finnes fortsatt leverte objekter relatert til levert prosjekt: %1;SVE="Det finns fortfarande levererade objekt som „r relaterade till levererat projekt: %1. "';
      Text002@1210190003 : TextConst 'ENU=''%1'' not present anymore, update not possible;NOR=''%1'' er ikke tilstede lenger, oppdatering ikke mulig;SVE=''%1'' inte l„ngre n„rvarande, uppdatera inte m”jligt';
      Text003@1210190004 : TextConst 'ENU=''%1'' is already linked to %2 ''%3'', update not possible;NOR=''%1'' er allerede linket til %2 1%3'', oppdatering ikke mulig;SVE=''%1'' „r redan l„nkad till %2 ''%3'', uppdatering inte m”jlig';
      Text004@1210190005 : TextConst 'ENU=Update: step;NOR=Oppdater: steg;SVE=Uppdatera: steg';

    PROCEDURE AssistEdit@2(OldDeliveredProjRec@1210190001 : Record 11012100) : Boolean;
    VAR
      lDeliveredProjRec@1210190000 : Record 11012100;
    BEGIN
      WITH lDeliveredProjRec DO BEGIN
        lDeliveredProjRec := Rec;
        ComplaintSetup.GET;
        ComplaintSetup.TESTFIELD("Delivered Project Nos.");
        IF NoSeriesMgt.SelectSeries(ComplaintSetup."Delivered Project Nos.",
                                    OldDeliveredProjRec."No. Series","No. Series") THEN BEGIN
          ComplaintSetup.GET;
          ComplaintSetup.TESTFIELD("Delivered Project Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := lDeliveredProjRec;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE SiteManagerName@1() : Text[100];
    VAR
      EmplRec@11012000 : Record 5200;
    BEGIN
      IF EmplRec.GET("Site Manager") THEN
        EXIT(EmplRec.FullName)
      ELSE
        EXIT('');
    END;

    PROCEDURE NameProjectManager@3() : Text[100];
    VAR
      EmplRec@11012000 : Record 5200;
    BEGIN
      IF EmplRec.GET("Project Manager") THEN
        EXIT(EmplRec.FullName)
      ELSE
        EXIT('');
    END;

    PROCEDURE ProjectEngineerName@5() : Text[100];
    VAR
      EmplRec@11012000 : Record 5200;
    BEGIN
      IF EmplRec.GET("Project Engineer") THEN
        EXIT(EmplRec.FullName)
      ELSE
        EXIT('');
    END;

    PROCEDURE NameBuyerContact@9() : Text[100];
    VAR
      EmplRec@11012000 : Record 5200;
    BEGIN
      IF EmplRec.GET("Buyer Contact") THEN
        EXIT(EmplRec.FullName)
      ELSE
        EXIT('');
    END;

    PROCEDURE UpdateDeliveredProject@1210190000();
    VAR
      Proj@1210190000 : Record 11072003;
      DeliveredProj@1210190001 : Record 11012100;
      DeliveredObj@1210190004 : Record 11012101;
      GenDelObjects@1210190002 : Codeunit 11012050;
      GenVendorDelProj@1210190003 : Codeunit 11012051;
      Window@1210190005 : Dialog;
    BEGIN
      //*33920
      TESTFIELD("No.");
      TESTFIELD("Original Project No.");
      IF NOT Proj.GET("Original Project No.") THEN
        FIELDERROR("Original Project No.", STRSUBSTNO(Text002, "Original Project No."));
      IF (Proj."Delivered Project" <> '') AND (Proj."Delivered Project" <> "No.") THEN BEGIN
        IF DeliveredProj.GET(Proj."Delivered Project") THEN
          FIELDERROR("Original Project No.",
            STRSUBSTNO(Text003, "Original Project No.", Proj.FIELDCAPTION("Delivered Project"), Proj."Delivered Project"))
        ELSE
          Proj."Delivered Project" := '';
      END;

      Window.OPEN(Text004 + '#1###');
      Window.UPDATE(1, 1);
      IF Proj."Delivered Project" = '' THEN BEGIN
        Proj."Delivered Project" := "No.";
        Proj.MODIFY;
      END;
      Window.UPDATE(1, 2);
      IF Proj."Delivered Object" <> '' THEN  BEGIN
        IF DeliveredObj.GET(Proj."Delivered Object") THEN BEGIN
          IF (DeliveredObj."Original Project" = "Original Project No.") AND
             (DeliveredObj."Delivered Project" = Proj."Delivered Project")
          THEN
            GenDelObjects.CopyPrincipals(Proj."No.",Proj."Delivered Object");
        END;
      END ELSE BEGIN
        DeliveredObj.SETCURRENTKEY("Delivered Project", "No.");
        DeliveredObj.SETRANGE("Delivered Project", "No.");
        DeliveredObj.SETRANGE("Original Project", "Original Project No.");
        DeliveredObj.SETRANGE("Original Plot no.", '');
        IF DeliveredObj.FINDFIRST THEN
          GenDelObjects.CopyPrincipals(Proj."No.",DeliveredObj."No.");
      END;
      Window.UPDATE(1, 3);
      GenDelObjects.CopyAuthorities(Proj."No.","No.");
      Window.UPDATE(1, 4);
      GenVendorDelProj.RUN(Proj);
      Window.CLOSE;
    END;

    LOCAL PROCEDURE UpdateDeliverdObjects@1100529000();
    VAR
      DeliveredProjectOld@1100529000 : Record 11012100;
      DeliveredObject@1100529001 : Record 11012101;
    BEGIN
      IF DeliveredProjectOld.GET("No.") THEN BEGIN
        IF ("Original Project No." <> DeliveredProjectOld."Original Project No.") OR
           ("Original Project Description" <> DeliveredProjectOld."Original Project Description")
        THEN BEGIN
          DeliveredObject.SETCURRENTKEY("Delivered Project", "No.");
          DeliveredObject.SETRANGE("Delivered Project", "No.");
          IF DeliveredObject.FINDSET(TRUE,FALSE) THEN BEGIN
            REPEAT
              DeliveredObject."Original Project" := "Original Project No.";
              DeliveredObject."Original Project Description" := "Original Project Description";
              DeliveredObject.MODIFY(TRUE);
            UNTIL DeliveredObject.NEXT = 0;;
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

