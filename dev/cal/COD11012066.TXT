OBJECT Codeunit 11012066 Project Info Pane Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'DEU=Projektadresse ist ge„ndert worden;ENU=Project Address changed;NLD=Projectadres is gewijzigd;NOR=Prosjektadressen er endret;SVE=Projektadressen har „ndrats';

    [External]
    PROCEDURE CountAppointments@1100485000(JobRec@1100485000 : Record 11072003;AppointmentType@1100485002 : 'General,Execution,Purchase') NoAppointments : Integer;
    VAR
      AppointmentRec@1100485001 : Record 11012081;
    BEGIN
      AppointmentRec.SETRANGE("Project No.",JobRec."No.");
      CASE AppointmentType OF
        AppointmentType::General:AppointmentRec.SETRANGE("Appointment Type",AppointmentRec."Appointment Type"::General);
        AppointmentType::Execution:AppointmentRec.SETRANGE("Appointment Type",AppointmentRec."Appointment Type"::Execution);
        AppointmentType::Purchase:AppointmentRec.SETRANGE("Appointment Type",AppointmentRec."Appointment Type"::Purchase);
      END;
      EXIT(AppointmentRec.COUNT);
    END;

    [External]
    PROCEDURE CountSettlementSheets@1100485001(JobRec@1100485001 : Record 11072003) NoSettlSheets : Integer;
    VAR
      SettlSheetRec@1100485000 : Record 11012021;
    BEGIN
      SettlSheetRec.SETRANGE("Project No.",JobRec."No.");
      EXIT(SettlSheetRec.COUNT);
    END;

    [External]
    PROCEDURE CountDocuments@1100485003(JobRec@1100485000 : Record 11072003) NoDocuments : Integer;
    VAR
      DocumentLink@1100525000 : Record 11012747;
      RecRef@1100525001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(JobRec);

      DocumentLink.SETCURRENTKEY("Record ID"); //mg, 05-07-11: M27801
      DocumentLink.SETRANGE("Table No.", DATABASE::Job);
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', FORMAT(RecRef.RECORDID())));
      DocumentLink.SETRANGE("Internal Company", COMPANYNAME);
      EXIT(DocumentLink.COUNT);
    END;

    [External]
    PROCEDURE CountProjectAddresses@1100485004(Jobrec@1100485000 : Record 11072003) NoOfAddresses : Integer;
    VAR
      AddressRec@1100485001 : Record 222;
    BEGIN
      AddressRec.SETRANGE("Customer No.",Jobrec."Bill-to Customer No.");
      AddressRec.SETRANGE("Project Address", TRUE);
      EXIT(AddressRec.COUNT);
    END;

    [External]
    PROCEDURE CountContacts@1100485005(JobRec@1100485002 : Record 11072003) NoOfContacts : Integer;
    VAR
      ContBusRel@1100485001 : Record 5054;
      Cont@1100485000 : Record 5050;
    BEGIN
      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
      ContBusRel.SETRANGE("No.",JobRec."Bill-to Customer No.");
      IF ContBusRel.FIND('-') THEN BEGIN
        Cont.SETRANGE(Type,Cont.Type::Person);
        Cont.SETRANGE("Company No.",ContBusRel."Contact No.");
        EXIT(Cont.COUNT);
      END ELSE
        EXIT(0);
    END;

    [External]
    PROCEDURE CountSalesInvoices@1100485007(JobRec@1100485001 : Record 11072003) NoOfInvoices : Integer;
    VAR
      SalesInvoiceHeader@1100485000 : Record 112;
    BEGIN
      SalesInvoiceHeader.SETCURRENTKEY("Sell-to Customer No.");
      SalesInvoiceHeader.SETRANGE("Sell-to Customer No.",JobRec."Bill-to Customer No.");
      EXIT(SalesInvoiceHeader.COUNT);
    END;

    [External]
    PROCEDURE CountCreditMemos@1100485008(JobRec@1100485001 : Record 11072003) NoOfCredMemos : Integer;
    VAR
      SalesCreditMemoHeader@1100485000 : Record 114;
    BEGIN
      SalesCreditMemoHeader.SETCURRENTKEY("Sell-to Customer No.");
      SalesCreditMemoHeader.SETRANGE("Sell-to Customer No.",JobRec."Bill-to Customer No.");
      EXIT(SalesCreditMemoHeader.COUNT);
    END;

    [External]
    PROCEDURE CountOperations@1100485012(JobRec@1100485000 : Record 11072003) NoOfOperations : Integer;
    VAR
      OperRec@1100485001 : Record 11012094;
    BEGIN
      OperRec.SETRANGE("Project No.", JobRec."No.");
      EXIT(OperRec.COUNT);
    END;

    [External]
    PROCEDURE CountProductionMotivation@1100525000(JobRec@1100525001 : Record 11072003) NoOfProdMot : Integer;
    VAR
      lvUnitPriceProdMot@1100525000 : Record 11020425;
    BEGIN
      lvUnitPriceProdMot.SETRANGE("Project No.",JobRec."No.");
      EXIT(lvUnitPriceProdMot.COUNT);
    END;

    [External]
    PROCEDURE LookUpSettlementSheets@1100485002(JobRec@1100485000 : Record 11072003);
    BEGIN
      LookUpSettlementSheetsWithFilt(JobRec, FALSE);
    END;

    [External]
    PROCEDURE LookUpSettlementSheetsWithFilt@1210190000(JobRec@1100485000 : Record 11072003;HardFilter@1210190000 : Boolean);
    VAR
      SettlSheetRec@1100485001 : Record 11012021;
      SettlSheetCardFrm@1100485002 : Page 11012046;
      SettlSheetListFrm@1100485003 : Page 11012047;
    BEGIN
      IF HardFilter THEN SettlSheetRec.FILTERGROUP(4);
      SettlSheetRec.SETRANGE("Project No.",JobRec."No.");
      IF HardFilter THEN SettlSheetRec.FILTERGROUP(0);
      IF SettlSheetRec.COUNT = 1 THEN BEGIN
        //*C-027523.sn
        SettlSheetRec.FINDFIRST;
        SettlSheetRec.SETRANGE(Principal, SettlSheetRec.Principal);
        SettlSheetRec.SETRANGE("Commission No.", SettlSheetRec."Commission No.");
        //*C-027523.en
        SettlSheetCardFrm.SETTABLEVIEW(SettlSheetRec);
        SettlSheetCardFrm.RUNMODAL;
      END ELSE BEGIN
        SettlSheetListFrm.SETTABLEVIEW(SettlSheetRec);
        SettlSheetListFrm.RUNMODAL;
      END;
    END;

    PROCEDURE LookupProjectAddr@10(VAR JobRec@1000 : Record 11072003);
    VAR
      ShipToAddr@1001 : Record 222;
    BEGIN
      WITH JobRec DO BEGIN
        ShipToAddr.SETRANGE("Customer No.","Bill-to Customer No.");
        ShipToAddr.SETRANGE("Project Address",TRUE);
        IF PAGE.RUNMODAL(0,ShipToAddr) = ACTION::LookupOK THEN BEGIN
          VALIDATE("Project Address Code",ShipToAddr.Code);
          MODIFY(TRUE);
          MESSAGE(Text000);
        END;
      END;
    END;

    [External]
    PROCEDURE LookupContacts@1100485006(JobRec@1100485000 : Record 11072003);
    VAR
      ContBusRel@1100485002 : Record 5054;
      Cont@1100485001 : Record 5050;
      ContFrm@1100485003 : Page 5052;
    BEGIN
      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
      ContBusRel.SETRANGE("No.",JobRec."Bill-to Customer No.");
      IF ContBusRel.FIND('-') THEN BEGIN
        Cont.SETRANGE("Company No.",ContBusRel."Contact No.");
        ContFrm.SETTABLEVIEW(Cont);
        ContFrm.RUNMODAL;
      END;
    END;

    [External]
    PROCEDURE LookupSalesInvoices@1100485009(JobRec@1100485001 : Record 11072003);
    VAR
      SalesInvoiceHeader@1100485000 : Record 112;
      SalesInvoiceForm@1100485002 : Page 143;
    BEGIN
      SalesInvoiceHeader.SETCURRENTKEY("Sell-to Customer No.");
      SalesInvoiceHeader.SETRANGE("Sell-to Customer No.",JobRec."Bill-to Customer No.");
      IF SalesInvoiceHeader.FIND('+') THEN BEGIN
        SalesInvoiceForm.SETTABLEVIEW(SalesInvoiceHeader);
        SalesInvoiceForm.RUNMODAL;
      END;
    END;

    [External]
    PROCEDURE LookupCreditMemos@1100485010(JobRec@1100485001 : Record 11072003);
    VAR
      SalesCreditMemoHeader@1100485000 : Record 114;
      SalesCreditMemoForm@1100485002 : Page 144;
    BEGIN
      SalesCreditMemoHeader.SETCURRENTKEY("Sell-to Customer No.");
      SalesCreditMemoHeader.SETRANGE("Sell-to Customer No.",JobRec."Bill-to Customer No.");
      IF SalesCreditMemoHeader.FIND('+') THEN BEGIN
        SalesCreditMemoForm.SETTABLEVIEW(SalesCreditMemoHeader);
        SalesCreditMemoForm.RUNMODAL;
      END;
    END;

    [External]
    PROCEDURE LookupOperations@1100485011(JobRec@1100485000 : Record 11072003);
    VAR
      OperRec@1100485001 : Record 11012094;
      OperationsBySmallProjectForm@1100485002 : Page 11012283;
    BEGIN
      OperRec.SETRANGE("Project No.", JobRec."No.");
      IF OperRec.FIND('-') THEN BEGIN
        OperationsBySmallProjectForm.SETTABLEVIEW(OperRec);
        OperationsBySmallProjectForm.RUNMODAL;
      END;
    END;

    [External]
    PROCEDURE LookupProdMot@1100525001(JobRec@1100485000 : Record 11072003);
    VAR
      lvUnitPriceProdMot@1100525000 : Record 11020425;
      UnitPriceProdMotForm@1100525001 : Page 11072520;
      UnitPriceProdMotListForm@1100525002 : Page 11072523;
    BEGIN
      lvUnitPriceProdMot.SETRANGE("Project No.", JobRec."No.");
      CASE lvUnitPriceProdMot.COUNT OF
        0:EXIT;
        1:BEGIN
          lvUnitPriceProdMot.FIND('-');
          UnitPriceProdMotForm.SETTABLEVIEW(lvUnitPriceProdMot);
          UnitPriceProdMotForm.RUNMODAL;
        END;
        ELSE BEGIN
          lvUnitPriceProdMot.FIND('-');
          UnitPriceProdMotListForm.SETTABLEVIEW(lvUnitPriceProdMot);
          UnitPriceProdMotListForm.RUNMODAL;
        END;
      END;
    END;

    [External]
    PROCEDURE LookupAppointmentsProject@1100525002(Job@1100525001 : Record 11072003;AppointmentType@1100525000 : Option);
    VAR
      AppointmentByProj@1100525002 : Record 11012081;
    BEGIN
      AppointmentByProj.FILTERGROUP(9);
      AppointmentByProj.SETRANGE("Project No.", Job."No.");
      AppointmentByProj.SETRANGE("Appointment Type", AppointmentType);
      AppointmentByProj.FILTERGROUP(0);
      CASE AppointmentType OF
        AppointmentByProj."Appointment Type"::General:
          PAGE.RUNMODAL(PAGE::"General Appntmnts  By Project", AppointmentByProj);
        AppointmentByProj."Appointment Type"::Execution:
          PAGE.RUNMODAL(PAGE::"Execut. Appntmnts  By Project", AppointmentByProj);
        AppointmentByProj."Appointment Type"::Purchase:
          PAGE.RUNMODAL(PAGE::"Purchase Appntmnts  By Project", AppointmentByProj);
      END;
    END;

    BEGIN
    END.
  }
}

