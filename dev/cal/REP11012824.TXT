OBJECT Report 11012824 Copy Consignment Roster
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Konsignationsdienstplan kopieren;
               ENU=Copy Consignment Roster;
               NLD=Consignatiedienstrooster kopi‰ren;
               NOR=Kopier forsendelsesgruppe;
               SVE=Kopiera konsignationsgrupp];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6953;    ;DataItem;                    ;
               DataItemTable=Table11012864;
               DataItemTableView=SORTING(Consignment Code,Start Date,Start Time);
               OnPreDataItem=BEGIN
                               Window.OPEN(Text001 + '\#1############# #2#############');

                               BaseEndDate := CALCDATE(BasePeriod, BaseDate);
                               BaseEndDate := CALCDATE('<-1D>', BaseEndDate); // Go back 1 day for selection of a period...
                               "Consignment Time".SETFILTER("Start Date", '%1..%2', BaseDate, BaseEndDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  ConsTimeTemp := "Consignment Time";
                                  ConsTimeTemp.INSERT;
                                END;

               OnPostDataItem=BEGIN
                                //Check watches per consigment time
                                ConsRosterRec.SETFILTER("Consignment Code", ConsTimeTemp."Consignment Code");
                                ConsRosterRec.SETRANGE("Start Date", ConsTimeTemp."Start Date");
                                IF ConsRosterRec.FIND('-') THEN BEGIN
                                  REPEAT
                                    WatchTemp.Code := ConsRosterRec."Watch Code";
                                    IF WatchTemp.INSERT THEN;
                                  UNTIL ConsRosterRec.NEXT = 0;
                                END;

                                //read consigment times for given interval
                                ConsTimeRec.COPY("Consignment Time");
                                ConsTimeRec.SETRANGE("Start Date", StartDate, EndDate);
                                IF ConsTimeRec.FIND('-') THEN BEGIN
                                  REPEAT
                                    //find 1e record in to copy cyclus
                                    ConsTimeTemp.SETRANGE("Start Day of Week", DATE2DWY(ConsTimeRec."Start Date", 1));
                                    IF ConsTimeTemp.FIND('-') THEN BEGIN
                                      //copy the assigned employees for all watches per consigment time
                                      IF WatchTemp.FIND('-') THEN BEGIN
                                        REPEAT
                                          IF ConsRosterRec.GET(WatchTemp.Code, ConsTimeTemp."Consignment Code",
                                            ConsTimeTemp."Start Date", ConsTimeTemp."Start Time") THEN BEGIN
                                            ConsRosterRec2."Watch Code" := ConsRosterRec."Watch Code";
                                            ConsRosterRec2."Consignment Code" := ConsTimeRec."Consignment Code";
                                            ConsRosterRec2."Start Date" := ConsTimeRec."Start Date";
                                            ConsRosterRec2."Start Time" := ConsTimeRec."Start Time";
                                            IF ConsRosterRec2.INSERT THEN;
                                            ConsRosterRec2."Employee No." := ConsRosterRec."Employee No.";
                                            ConsRosterRec2.MODIFY(TRUE);
                                          END;
                                        UNTIL WatchTemp.NEXT = 0;
                                      END;
                                      ConsTimeTemp2 := ConsTimeTemp;
                                      ConsTimeTemp2.INSERT;
                                      ConsTimeTemp.DELETE;
                                      //after deleting current record check in to copy cyclus if there are more:
                                      //if not, copy back buffer and repeat this till given end date
                                      ConsTimeTemp.SETRANGE("Start Day of Week");
                                      IF NOT ConsTimeTemp.FIND('-') THEN BEGIN
                                        IF ConsTimeTemp2.FIND('-') THEN BEGIN
                                          REPEAT
                                            ConsTimeTemp := ConsTimeTemp2;
                                            ConsTimeTemp.INSERT;
                                            ConsTimeTemp2.DELETE;
                                          UNTIL ConsTimeTemp2.NEXT = 0;
                                        END;
                                      END;
                                    END;
                                    Window.UPDATE(1, CREATEDATETIME(ConsRosterRec."Start Date", ConsRosterRec."Start Time"));
                                    Window.UPDATE(2, ConsRosterRec."Employee No.");
                                  UNTIL ConsTimeRec.NEXT = 0;
                                END;

                                WatchTemp.DELETEALL;
                              END;

               ReqFilterFields=Consignment Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525007;;Container;
                  ContainerType=ContentArea }

      { 1100525006;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525005;2;Group  ;
                  CaptionML=[DEU=Herkunft;
                             ENU=Source;
                             NLD=Herkomst;
                             NOR=Source;
                             SVE=K„lla];
                  GroupType=Group }

      { 1100525004;3;Field  ;
                  CaptionML=[DEU=Kopieren ab Datum;
                             ENU=Copy from date;
                             NLD=Kopieer vanaf datum;
                             NOR=Kompier fra dato;
                             SVE=Kopiera fr†n datum];
                  SourceExpr=BaseDate }

      { 1100525003;3;Field  ;
                  CaptionML=[DEU=Kopieren Periode;
                             ENU=Copy period;
                             NLD=Kopieer periode;
                             NOR=Kopier periode;
                             SVE=Kopiera period];
                  SourceExpr=BasePeriod }

      { 1100525002;2;Group  ;
                  CaptionML=[DEU=Bestimmung;
                             ENU=Target;
                             NLD=Bestemming;
                             NOR=M†l;
                             SVE=M†l];
                  GroupType=Group }

      { 1100525001;3;Field  ;
                  CaptionML=[DEU=Startdatum zu erzeugender Zeiten;
                             ENU=Start Date Times to Generate;
                             NLD=Startdatum te genereren tijden;
                             NOR=Tider for startdato som skal genereres;
                             SVE=Tider f”r startdatum som ska genereras];
                  SourceExpr=StartDate }

      { 1100525000;3;Field  ;
                  CaptionML=[DEU=Enddatum zu erzeugender Zeiten;
                             ENU=End Date Times to Generate;
                             NLD=Einddatum te genereren tijden;
                             NOR=Tider for sluttdato som skal genereres;
                             SVE=Tider f”r slutdatum som ska genereras];
                  SourceExpr=EndDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      BaseDate@1100485000 : Date;
      BaseEndDate@1100485004 : Date;
      BasePeriod@1100485001 : DateFormula;
      StartDate@1100485002 : Date;
      EndDate@1100485003 : Date;
      Text001@1100485006 : TextConst 'DEU=Besch„ftigt mit dem Erzeugen von Konsignationszeiten...;ENU=Generating Consignment Times...;NLD=Bezig met genereren van consignatietijden...;NOR=Genererer konsignasjonstid...;SVE=Genererar konsignationstider ...';
      Window@1100485007 : Dialog;
      ConsTimeRec@1100485008 : Record 11012864;
      ConsRosterRec@1100485009 : Record 11012865;
      ConsRosterRec2@1100485010 : Record 11012865;
      ConsTimeTemp@1100485011 : TEMPORARY Record 11012864;
      ConsTimeTemp2@1100485012 : TEMPORARY Record 11012864;
      WatchTemp@1100485005 : TEMPORARY Record 11012861;

    BEGIN
    {
      Example:
      Consignment Time 12 (East-Ned)

      Watch     Date/Engineer  Date/Engineer  Date/Engineer  Date/Engineer
      5 (east)  01-07-06/DB    02-07-06/PS    08-07-06/DB    09-07-06/PS
      8 (Gld)   01-07-06/JH    02-07-06/JH    08-07-06/JB    09-07-06/JB
                saterday wk26  sunday wk26    saterday wk27  sunday wk27

      Origin
      Startdate : 01-07-06
      Period    : 2w

      Destination
      Startdate : 15-07-06 (first next saterday weekend shift)
      Enddate   : 15-08-06 (tuesday week 33)

      Result:
      For all watches per consignment time a copy will be made of week 26 for 28, 30 and 32
      while week 27 will be copied to 29 and 31.
    }
    END.
  }
  RDLDATA
  {
  }
}

