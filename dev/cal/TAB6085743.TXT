OBJECT Table 6085743 Approval Group
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.50;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               SetPriority(Rec);
             END;

    OnModify=BEGIN
               SetPriority(Rec);
             END;

    OnDelete=VAR
               DtldAppvlEntry@161024013 : Record 6085744;
               AppvlGroupDim@161024012 : Record 6085751;
               ApprvlUser@1160040000 : Record 6085746;
             BEGIN
               DtldAppvlEntry.SETCURRENTKEY("Approval Group Code");
               DtldAppvlEntry.SETRANGE("Approval Group Code",Code);
               DtldAppvlEntry.SETRANGE(Approved,FALSE);
               IF NOT DtldAppvlEntry.ISEMPTY THEN
                 ERROR(Text001,TABLECAPTION);

               AppvlGroupDim.SETRANGE("Approval Group Code",Code);
               AppvlGroupDim.DELETEALL(TRUE);

               ApprvlUser.SETRANGE("Approval Group Code",Code);
               ApprvlUser.DELETEALL(TRUE);
             END;

    CaptionML=[DEU=Genehmigungsgruppen;
               ENU=Approval Groups;
               NLD=Goedkeuringsgroepen;
               SVE=Godk„nnandegrupper];
    LookupPageID=Page6085743;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[DEU=Code;
                                                              ENU=Code;
                                                              NLD=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Priority            ;Integer       ;CaptionML=[DEU=Priorit„t;
                                                              ENU=Priority;
                                                              NLD=Prioriteit;
                                                              NOR=Prioritet;
                                                              SVE=Prioritet] }
    { 5   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 9   ;   ;Four Eyes Approval  ;Option        ;CaptionML=[DEU=Vier-Augen-Genehmigung;
                                                              ENU=Four Eyes Approval;
                                                              NLD=Four Eyes goedkeuring];
                                                   OptionCaptionML=[DEU=" ,Rechnung,Rechnung Vollst- Betrag";
                                                                    ENU=" ,Invoice,Invoice Full Amount";
                                                                    NLD=" ,Factuur,Invoice Full Amount"];
                                                   OptionString=[ ,Invoice,Invoice Full Amount] }
    { 12  ;   ;First Entry Created by;Option      ;InitValue=Approver ID;
                                                   CaptionML=[DEU=Erster Posten erstellt von;
                                                              ENU=First Entry Created by;
                                                              NLD=Eerste regel gemaakt door];
                                                   OptionCaptionML=[DEU=" ,Genehmiger-ID,Eink„ufer";
                                                                    ENU=" ,Approver ID,Purchaser";
                                                                    NLD=" ,Fiatteur-id,Inkoper";
                                                                    SVE=" ,Godk„nnar-ID,Ink”pare"];
                                                   OptionString=[ ,Approver ID,Purchaser] }
    { 100 ;   ;Purchase Header Filter;TableFilter ;TableIDExpr=38;
                                                   CaptionML=[DEU=Einkaufskopffilter;
                                                              ENU=Purchase Header Filter;
                                                              NLD=Inkoopfilter] }
    { 101 ;   ;Purchaser Filter    ;TableFilter   ;TableIDExpr=13;
                                                   CaptionML=[DEU=Eink„uferfilter;
                                                              ENU=Purchaser Filter;
                                                              NLD=Inkoperfilter;
                                                              SVE=Ink”parfilter] }
    { 102 ;   ;Vendor Filter       ;TableFilter   ;TableIDExpr=23;
                                                   CaptionML=[DEU=Kreditorenfilter;
                                                              ENU=Vendor Filter;
                                                              NLD=Leverancierfilter;
                                                              NOR=Leverand›r filter;
                                                              SVE=Leverant”rsfilter] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Priority                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@161024012 : TextConst 'DEU=Ein oder mehrere Dokumente warten auf Genehmigung in diesem %1.;ENU=One or documents are pending approval in this %1.;NLD=Een of meer documenten staan afwachten in deze %1.';

    PROCEDURE SetPriority@1103308000(VAR AppvlGroup@1103308000 : Record 6085743);
    VAR
      AppvlGroup2@1103308001 : Record 6085743;
      AppvlGroup3@1103308002 : Record 6085743;
    BEGIN
      IF AppvlGroup.Priority = 0 THEN BEGIN
        AppvlGroup.Priority := 1;
        AppvlGroup2.SETCURRENTKEY(Priority);
        IF AppvlGroup2.FINDLAST THEN
          AppvlGroup.Priority := AppvlGroup2.Priority + 1;
      END;

      AppvlGroup2.RESET;
      AppvlGroup2.SETCURRENTKEY(Priority);
      AppvlGroup2.SETRANGE(Priority,AppvlGroup.Priority);
      AppvlGroup2.SETFILTER(Code,'<>%1',AppvlGroup.Code);
      IF NOT AppvlGroup2.ISEMPTY THEN BEGIN
        AppvlGroup3.Priority := AppvlGroup.Priority;
        AppvlGroup2.SETFILTER(Priority,'>=%1',AppvlGroup.Priority);
        IF AppvlGroup2.FINDSET(TRUE,TRUE) THEN
          REPEAT
            IF AppvlGroup2.Priority = AppvlGroup3.Priority THEN BEGIN
              AppvlGroup2.Priority += 1;
              AppvlGroup2.MODIFY;
              AppvlGroup3.Priority := AppvlGroup2.Priority;
            END;
          UNTIL AppvlGroup2.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

