OBJECT Codeunit 9999 Upgrade Tag Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 9999=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE HasUpgradeTag@1(Tag@1000 : Code[250]) : Boolean;
    VAR
      UpgradeTags@1002 : Record 9999;
    BEGIN
      EXIT(UpgradeTags.GET(Tag,COMPANYNAME));
    END;

    [External]
    PROCEDURE SetUpgradeTag@2(NewTag@1000 : Code[250]);
    BEGIN
      SetUpgradeTagForCompany(NewTag,COMPANYNAME);
    END;

    [External]
    PROCEDURE RegisterUpgradeTag@9(NewUpgradeTag@1001 : Text[250];VAR TempUpgradeTags@1000 : TEMPORARY Record 9999);
    BEGIN
      CLEAR(TempUpgradeTags);
      TempUpgradeTags.Tag := NewUpgradeTag;
      //TempUpgradeTags.INSERT; //**4PS.o
      IF TempUpgradeTags.INSERT THEN; //**4PS.n
    END;

    [EventSubscriber(Codeunit,2,OnCompanyInitialize)]
    LOCAL PROCEDURE EnsureUpgradeTagsAreRegisteredForNewCompany@7();
    VAR
      TempPerCompanyUpgradeTags@1000 : TEMPORARY Record 9999;
      TempPerDatabaseUpgradeTags@1002 : TEMPORARY Record 9999;
    BEGIN
      OnGetPerDatabaseUpgradeTags(TempPerDatabaseUpgradeTags);
      EnsurePerDatabaseUpgradeTagsExist(TempPerDatabaseUpgradeTags);

      OnGetPerCompanyUpgradeTags(TempPerCompanyUpgradeTags);
      EnsurePerCompanyUpgradeTagsExist(TempPerCompanyUpgradeTags);
    END;

    LOCAL PROCEDURE EnsurePerCompanyUpgradeTagsExist@3(VAR TempUpgradeTags@1000 : TEMPORARY Record 9999);
    BEGIN
      IF NOT TempUpgradeTags.FINDFIRST THEN
        EXIT;

      REPEAT
        IF NOT HasUpgradeTag(TempUpgradeTags.Tag) THEN
          SetUpgradeTag(TempUpgradeTags.Tag);
      UNTIL TempUpgradeTags.NEXT = 0;
    END;

    LOCAL PROCEDURE EnsurePerDatabaseUpgradeTagsExist@8(VAR TempUpgradeTags@1000 : TEMPORARY Record 9999);
    VAR
      UpgradeTags@1001 : Record 9999;
    BEGIN
      IF NOT TempUpgradeTags.FINDFIRST THEN
        EXIT;

      REPEAT
        IF NOT UpgradeTags.GET(TempUpgradeTags.Tag,'') THEN
          SetUpgradeTagForCompany(TempUpgradeTags.Tag,'');
      UNTIL TempUpgradeTags.NEXT = 0;
    END;

    LOCAL PROCEDURE SetUpgradeTagForCompany@4(NewTag@1000 : Code[250];CompanyName@1001 : Text[30]);
    VAR
      UpgradeTags@1002 : Record 9999;
    BEGIN
      UpgradeTags.VALIDATE(Tag,NewTag);
      UpgradeTags.VALIDATE("Tag Timestamp",CURRENTDATETIME);
      UpgradeTags.VALIDATE(Company,CompanyName);
      UpgradeTags.INSERT(TRUE);
    END;

    [Integration]
    LOCAL PROCEDURE OnGetPerCompanyUpgradeTags@5(VAR TempUpgradeTags@1000 : TEMPORARY Record 9999);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGetPerDatabaseUpgradeTags@6(VAR TempUpgradeTags@1000 : TEMPORARY Record 9999);
    BEGIN
    END;

    BEGIN
    {
      Format of the upgrade tag is:
      [CompanyPrefix]-[TFSID]-[Description]-[YYYYMMDD]

      Example:
      MS-29901-UpdateGLEntriesIntegrationRecordIDs-20161206
    }
    END.
  }
}

