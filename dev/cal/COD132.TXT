OBJECT Codeunit 132 Release Incoming Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=130;
    OnRun=BEGIN
            IF Status = Status::Released THEN
              EXIT;
            IF Status IN [Status::Created,Status::Posted] THEN
              ERROR(CanReleasedIfStatusErr,Status::"Pending Approval",Status::New,Status::Failed);

            OnCheckIncomingDocReleaseRestrictions;

            TESTFIELD(Posted,FALSE);

            IF NOT IsADocumentAttached THEN
              ERROR(NothingToReleaseErr,"Entry No.");

            Status := Status::Released;
            Released := TRUE;
            "Released Date-Time" := CURRENTDATETIME;
            "Released By User ID" := USERSECURITYID;

            MODIFY(TRUE);

            OnAfterReleaseIncomingDoc(Rec);
          END;

  }
  CODE
  {
    VAR
      NothingToReleaseErr@1001 : TextConst '@@@="%1 = Incoming Document Entry No";ENU=There is nothing to release for the incoming document number %1.;NOR=Det finnes ingenting † frigi for det inng†ende dokumentnummeret %1.;SVE=Det finns ingenting att frisl„ppa f”r inkommande dokument nummer %1.';
      DocReleasedWhenApprovedErr@1005 : TextConst 'ENU=This document can only be released when the approval process is complete.;NOR=Dette dokumentet kan f›rst frigis n†r godkjenningsprosessen er fullf›rt.;SVE=Det h„r dokumentet kan inte sl„ppas f”rr„n godk„nnandeprocessen har slutf”rts.';
      CancelOrCompleteToReopenDocErr@1003 : TextConst 'ENU=The approval process must be cancelled or completed to reopen this document.;NOR=Godkjenningsprosessen m† avbrytes eller fullf›res f›r du kan †pne dette dokumentet p† nytt.;SVE=Godk„nnandeprocessen m†ste avbrytas eller slutf”ras f”r att det h„r dokumentet ska kunna ”ppnas igen.';
      CanReleasedIfStatusErr@1000 : TextConst '@@@="%1 = status released, %2 = status pending approval";ENU=It is only possible to release the document when the status is %1, %2 or %3.;NOR=Du kan bare frigi dokumentet n†r statusen er %1, %2 eller %3.;SVE=Det g†r bara att sl„ppa dokumentet n„r statusen „r %1, %2 eller %3.';

    [External]
    PROCEDURE Reopen@1(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      WITH IncomingDocument DO BEGIN
        IF Status = Status::New THEN
          EXIT;
        ClearReleaseFields(IncomingDocument);
        Status := Status::New;

        MODIFY(TRUE);
      END;
    END;

    [External]
    PROCEDURE Reject@8(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      WITH IncomingDocument DO BEGIN
        TESTFIELD(Posted,FALSE);

        ClearReleaseFields(IncomingDocument);
        Status := Status::Rejected;

        MODIFY(TRUE);
      END;
    END;

    [External]
    PROCEDURE Fail@6(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      WITH IncomingDocument DO BEGIN
        IF Status = Status::Failed THEN
          EXIT;

        Status := Status::Failed;

        MODIFY(TRUE);
        COMMIT;

        OnAfterCreateDocFromIncomingDocFail(IncomingDocument);
      END;
    END;

    [External]
    PROCEDURE Create@7(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      WITH IncomingDocument DO BEGIN
        IF Status = Status::Created THEN
          EXIT;

        Status := Status::Created;

        MODIFY(TRUE);
        COMMIT;
        OnAfterCreateDocFromIncomingDocSuccess(IncomingDocument);
      END;
    END;

    [Internal]
    PROCEDURE PerformManualRelease@2(VAR IncomingDocument@1002 : Record 130);
    VAR
      ApprovalsMgmt@1000 : Codeunit 1535;
    BEGIN
      IF ApprovalsMgmt.IsIncomingDocApprovalsWorkflowEnabled(IncomingDocument) AND
         (IncomingDocument.Status = IncomingDocument.Status::New)
      THEN
        ERROR(DocReleasedWhenApprovedErr);

      CODEUNIT.RUN(CODEUNIT::"Release Incoming Document",IncomingDocument);
    END;

    [External]
    PROCEDURE PerformManualReopen@3(VAR IncomingDocument@1002 : Record 130);
    BEGIN
      IF IncomingDocument.Status = IncomingDocument.Status::"Pending Approval" THEN
        ERROR(CancelOrCompleteToReopenDocErr);

      Reopen(IncomingDocument);
    END;

    [External]
    PROCEDURE PerformManualReject@5(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      IF IncomingDocument.Status = IncomingDocument.Status::"Pending Approval" THEN
        ERROR(CancelOrCompleteToReopenDocErr);

      Reject(IncomingDocument);
    END;

    LOCAL PROCEDURE ClearReleaseFields@22(VAR IncomingDocument@1000 : Record 130);
    BEGIN
      IncomingDocument.Released := FALSE;
      IncomingDocument."Released Date-Time" := 0DT;
      CLEAR(IncomingDocument."Released By User ID");
    END;

    [Integration]
    [External]
    PROCEDURE OnAfterReleaseIncomingDoc@4(VAR IncomingDocument@1000 : Record 130);
    BEGIN
    END;

    [Integration]
    [External]
    PROCEDURE OnAfterCreateDocFromIncomingDocSuccess@9(VAR IncomingDocument@1000 : Record 130);
    BEGIN
    END;

    [Integration]
    [External]
    PROCEDURE OnAfterCreateDocFromIncomingDocFail@10(VAR IncomingDocument@1000 : Record 130);
    BEGIN
    END;

    BEGIN
    END.
  }
}

