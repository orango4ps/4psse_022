OBJECT Codeunit 11130010 Estimate Tree View Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE EditViewSubEstimates@1100528916(EstimateNo@1100525000 : Code[20];SubEstimateNo@1100525003 : Code[20]);
    VAR
      SubEstimate@1100525001 : Record 11072074;
      SubEstimatePage@1100525002 : Page 11125320;
    BEGIN
      FilterSubEstimates(EstimateNo, SubEstimateNo, SubEstimate);

      SubEstimatePage.SETTABLEVIEW(SubEstimate);
      SubEstimatePage.SETRECORD(SubEstimate);
      SubEstimatePage.RUN;
    END;

    PROCEDURE EditViewEstimateParts@1100528915(EstimateNo@1100525000 : Code[20];SubEstimateNo@1100525003 : Code[20];PartGroup@1100525004 : Code[10];Part@1100525005 : Code[20]);
    VAR
      EstimatePart@1100525001 : Record 11072073;
      EstimatePartPage@1100525002 : Page 11125318;
    BEGIN
      FilterEstimateParts(EstimateNo, SubEstimateNo, PartGroup, Part, EstimatePart);

      EstimatePartPage.SETTABLEVIEW(EstimatePart);
      EstimatePartPage.SETRECORD(EstimatePart);
      EstimatePartPage.RUN;
    END;

    PROCEDURE EditViewEstimateLines@1100528914(EstimateNo@1100525000 : Code[20];SubEstimateNo@1100525003 : Code[20];PartGroup@1100525004 : Code[10];Part@1100525005 : Code[20];LineNo@1100525006 : Integer;ShowSales@1100528901 : Boolean);
    VAR
      EstimateLine@1100525001 : Record 11072072;
      EstimateLineCostPage@1100525002 : Page 11125316;
      EstimateLineSalesPage@1100528900 : Page 11125327;
    BEGIN
      FilterEstimateLines(EstimateNo, SubEstimateNo, PartGroup, Part, LineNo, EstimateLine);

      IF ShowSales THEN BEGIN  //C033558
        EstimateLineSalesPage.SETTABLEVIEW(EstimateLine);
        EstimateLineSalesPage.SETRECORD(EstimateLine);
        EstimateLineSalesPage.RUN;
      END ELSE BEGIN
        EstimateLineCostPage.SETTABLEVIEW(EstimateLine);
        EstimateLineCostPage.SETRECORD(EstimateLine);
        EstimateLineCostPage.RUN;
      END;
    END;

    PROCEDURE EditViewEstimateLineTotals@1100528912(VAR EstimateLine@1100525000 : Record 11072072);
    VAR
      EstimateLine2@1100525001 : Record 11072072;
      EstimateLineTotalPage@1100525002 : Page 11125328;
    BEGIN
      EstimateLine2.COPY(EstimateLine);

      EstimateLineTotalPage.SETTABLEVIEW(EstimateLine2);
      EstimateLineTotalPage.SETRECORD(EstimateLine2);
      EstimateLineTotalPage.RUN;
    END;

    PROCEDURE EditViewEstLinesModelQty@1100528911(VAR EstimateLine@1100525000 : Record 11072072);
    VAR
      EstimateLine2@1100525001 : Record 11072072;
      EstimateModelQuantitiesPage@1100525002 : Page 11125329;
    BEGIN
      EstimateLine2.COPY(EstimateLine);

      EstimateModelQuantitiesPage.SETTABLEVIEW(EstimateLine2);
      EstimateModelQuantitiesPage.SETRECORD(EstimateLine2);
      EstimateModelQuantitiesPage.RUN;
    END;

    PROCEDURE EditViewRecipeLines@1100528910(VAR EstimateLine@1100525000 : Record 11072072;ShowSales@1100525004 : Boolean);
    VAR
      RecipeLine@1100525001 : Record 11072075;
      RecipeLinesCostPage@1100525002 : Page 11125323;
      RecipeLinesSalesPage@1100525003 : Page 11229774;
    BEGIN
      FilterRecipeLines(EstimateLine, RecipeLine);

      IF ShowSales THEN BEGIN  //db, 14-03-14
        RecipeLinesSalesPage.SETTABLEVIEW(RecipeLine);
        RecipeLinesSalesPage.SETRECORD(RecipeLine);
        RecipeLinesSalesPage.RUNMODAL;
      END ELSE BEGIN
        RecipeLinesCostPage.SETTABLEVIEW(RecipeLine);
        RecipeLinesCostPage.SETRECORD(RecipeLine);
        RecipeLinesCostPage.RUNMODAL;
      END;

      //db, 02-02-15: Calculate Part by update total recipe totals to estimate line and check relations with charge lines
      //db, 08-07-16: function can be skipped; total and surcharge over recipe header calculated properly
      //UpdateMgt.UpdateTotals(EstimateLine."Estimate No.", EstimateLine."Sub-Estimate No.", EstimateLine.Part, TRUE);  //C027611
    END;

    PROCEDURE EditViewRecipeLineTotals@1100528908(VAR RecipeLine@1100525000 : Record 11072075);
    VAR
      RecipeLine2@1100525001 : Record 11072075;
      RecipeLinesTotalPage@1100525002 : Page 11229775;
    BEGIN
      RecipeLine2.COPY(RecipeLine);

      RecipeLinesTotalPage.SETTABLEVIEW(RecipeLine2);
      RecipeLinesTotalPage.SETRECORD(RecipeLine2);
      RecipeLinesTotalPage.RUN;
    END;

    LOCAL PROCEDURE FilterSubEstimates@1100528906(EstimateNo@1100525000 : Code[20];SubEstimateNo@1100525003 : Code[20];VAR SubEstimate2@1100525004 : Record 11072074);
    VAR
      SubEstimate@1100525001 : Record 11072074;
    BEGIN
      SubEstimate2.SETCURRENTKEY("Estimate No.", "Sub-Estimate No.");

      SubEstimate.FILTERGROUP(4);  //db, 18-07-14: C016687
      SubEstimate2.SETRANGE("Estimate No.", EstimateNo);
      SubEstimate.FILTERGROUP(0);

      SubEstimate2."Estimate No." := EstimateNo;
      SubEstimate2."Sub-Estimate No." := SubEstimateNo;
      IF SubEstimate2.FIND('=><') THEN;
    END;

    LOCAL PROCEDURE FilterEstimateParts@1100528905(EstimateNo@1100525000 : Code[20];SubEstimateNo@1100525003 : Code[20];PartGroup@1100525004 : Code[10];Part@1100525005 : Code[20];VAR EstimatePart2@1100525006 : Record 11072073);
    BEGIN
      EstimatePart2.SETCURRENTKEY("Estimate No.", "Sub-Estimate No.", "Part Group", Part);

      EstimatePart2.FILTERGROUP(4);  //db, 18-07-14: C016687
      EstimatePart2.SETRANGE("Estimate No.", EstimateNo);
      EstimatePart2.SETRANGE("Sub-Estimate No.", SubEstimateNo);
      EstimatePart2.FILTERGROUP(0);

      EstimatePart2."Estimate No." := EstimateNo;
      EstimatePart2."Sub-Estimate No." := SubEstimateNo;
      EstimatePart2."Part Group" := PartGroup;
      EstimatePart2.Part := Part;
      IF EstimatePart2.FIND('=><') THEN;
    END;

    PROCEDURE FilterEstimateLines@1100528904(EstimateNo@1100525000 : Code[20];SubEstimateNo@1100525003 : Code[20];PartGroup@1100525004 : Code[10];Part@1100525005 : Code[20];LineNo@1100525001 : Integer;VAR EstimateLine2@1100525006 : Record 11072072);
    BEGIN
      EstimateLine2.SETCURRENTKEY("Estimate No.", "Sub-Estimate No.", "Part Group", Part);

      EstimateLine2.FILTERGROUP(4);  //db, 18-07-14: C016687
      EstimateLine2.SETRANGE("Estimate No.", EstimateNo);
      EstimateLine2.SETRANGE("Sub-Estimate No.", SubEstimateNo);
      EstimateLine2.SETRANGE("Part Group", PartGroup);
      EstimateLine2.SETRANGE(Part, Part);
      EstimateLine2.FILTERGROUP(0);

      EstimateLine2."Estimate No." := EstimateNo;
      EstimateLine2."Sub-Estimate No." := SubEstimateNo;
      EstimateLine2."Part Group" := PartGroup;
      EstimateLine2.Part := Part;
      EstimateLine2."Line No." := LineNo;
      IF EstimateLine2.FIND('=><') THEN;
    END;

    LOCAL PROCEDURE FilterRecipeLines@1100528903(EstimateLine2@1100525006 : Record 11072072;VAR RecipeLine2@1100525000 : Record 11072075);
    BEGIN
      EstimateLine2.TESTFIELD(EstimateLine2.Recipe);
      RecipeLine2.SETCURRENTKEY(
        "Estimate No.", "Sub-Estimate No.", "Part Group", Part, "Estimate Line",
        "Recipe Group", Recipe, "Line No.");

      RecipeLine2.FILTERGROUP(4);  //db, 18-07-14: C016687
      RecipeLine2.SETRANGE("Estimate No.", EstimateLine2."Estimate No.");
      RecipeLine2.SETRANGE("Sub-Estimate No.", EstimateLine2."Sub-Estimate No.");
      RecipeLine2.SETRANGE("Part Group", EstimateLine2."Part Group");
      RecipeLine2.SETRANGE(Part, EstimateLine2.Part);
      RecipeLine2.SETRANGE("Estimate Line", EstimateLine2."Line No.");
      RecipeLine2.SETRANGE("Recipe Group", EstimateLine2."Recipe Group");
      RecipeLine2.SETRANGE(Recipe, EstimateLine2.Recipe);
      RecipeLine2.FILTERGROUP(0);

      IF RecipeLine2.FIND('=><') THEN;
    END;

    PROCEDURE FilterEstimateLinesNextPart@1100528902(PartNavigationLevel@1100528000 : 'Part,Paragraph,Chapter';PartNavigationDirection@1100528001 : 'Previous,Next';VAR EstimateLine2@1100528002 : Record 11072072);
    VAR
      EstimatePart@1100528003 : Record 11072073;
    BEGIN
      EstimatePart.GET(
        EstimateLine2."Estimate No.",
        EstimateLine2."Sub-Estimate No.",
        EstimateLine2."Part Group",
        EstimateLine2.Part);

      IF PartNavigationDirection = PartNavigationDirection::Previous THEN BEGIN
        IF NOT EstimatePart.GetPreviousPart(PartNavigationLevel) THEN
          EXIT;
      END ELSE BEGIN
        IF NOT EstimatePart.GetNextPart(PartNavigationLevel) THEN
          EXIT;
      END;

      FilterEstimateLines(
        EstimatePart."Estimate No.",
        EstimatePart."Sub-Estimate No.",
        EstimatePart."Part Group",
        EstimatePart.Part,
        0,
        EstimateLine2);
    END;

    PROCEDURE FilterEstimatePartsNextSubEstimate@1100525011(PartNavigationLevel@1100528000 : 'Part,Paragraph,Chapter';PartNavigationDirection@1100528001 : 'Previous,Next';VAR EstimatePart2@1100528002 : Record 11072073);
    VAR
      SubEstimate@1100528003 : Record 11072074;
    BEGIN
      //DP00909
      SubEstimate.GET(
        EstimatePart2."Estimate No.",
        EstimatePart2."Sub-Estimate No.");

      IF PartNavigationDirection = PartNavigationDirection::Previous THEN BEGIN
        IF NOT SubEstimate.GetPreviousPart(PartNavigationLevel) THEN
          EXIT;
      END ELSE BEGIN
        IF NOT SubEstimate.GetNextPart(PartNavigationLevel) THEN
          EXIT;
      END;

      FilterEstimateParts(
        SubEstimate."Estimate No.",
        SubEstimate."Sub-Estimate No.",
        '',
        '',
        EstimatePart2);
    END;

    BEGIN
    {
      db, 18-07-14: C016687
      - toolsbug: Ctrl A selects all lines in bufferprocedure, despite using EstimateLine.Copy(Rec) and SetSelectionFilter: result filtergroup 8 or 9 ignored
      - use filtergroup 4 (PageLink) while interface between Part and Lines is based on ListPages and not Main/Subform as in classic version
      db, 24-11-14: C017535
      - allow calling other sessions; use Page.Run instead of Runmodal (except page RecipeLines).
      db, 04-09-15: C025240
      - toggle/apply-functions HideZeroLines removed.
    }
    END.
  }
}

