OBJECT Table 11012351 Cost Object Relation Buyer Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnModify=BEGIN
               VALIDATE("Construction Cost Object");
               VALIDATE("Development Cost Object");
             END;

    CaptionML=[ENU=Cost Object Relation Buyer Mgt;
               NOR=Kostnadsobjektrelasjon, kj›perh†ndtering;
               SVE=Kostnadsobjektrelation, k”parhant.];
  }
  FIELDS
  {
    { 10  ;   ;Construction Company;Text30        ;TableRelation=Company;
                                                   OnValidate=BEGIN
                                                                FillDevAndConstructCompany(ConstructionCompany, DevelopmentCompany);
                                                                IF ("Construction Company" <> ConstructionCompany) THEN
                                                                  ERROR(Text001, "Construction Company", FIELDCAPTION("Construction Company"));
                                                              END;

                                                   CaptionML=[ENU=Construction Company;
                                                              NOR=Konstruksjonsselskap;
                                                              SVE=Konstruktionsf”retag];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Construction Cost Object;Code20    ;OnValidate=VAR
                                                                DimensionValue@1210190000 : Record 349;
                                                              BEGIN
                                                                DimensionValue.CHANGECOMPANY("Construction Company");
                                                                DimensionValue.SETRANGE(Code, "Construction Cost Object");
                                                                DimensionValue.SETRANGE("Global Dimension No.", 2);
                                                                IF (NOT DimensionValue.FINDFIRST) THEN
                                                                  ERROR(Text002, FIELDCAPTION("Construction Cost Object"), "Construction Cost Object");
                                                              END;

                                                   OnLookup=VAR
                                                              DimensionValue@1210190000 : Record 349;
                                                            BEGIN
                                                              DimensionValue.CHANGECOMPANY("Construction Company");
                                                              DimensionValue.SETRANGE("Global Dimension No.", 2);
                                                              DimensionValue.Code := "Construction Cost Object";
                                                              IF (PAGE.RUNMODAL(0, DimensionValue) = ACTION::LookupOK) THEN
                                                                "Construction Cost Object" := DimensionValue.Code;

                                                              VALIDATE("Construction Cost Object");
                                                            END;

                                                   CaptionML=[ENU=Construction Cost Object;
                                                              NOR=Konstruksjon, Kostnadsobjekt;
                                                              SVE=Konstruktion, kostnadsobjekt];
                                                   NotBlank=Yes }
    { 30  ;   ;Development Company ;Text30        ;TableRelation=Company;
                                                   OnValidate=BEGIN
                                                                FillDevAndConstructCompany(ConstructionCompany, DevelopmentCompany);
                                                                IF ("Development Company" <> DevelopmentCompany) THEN
                                                                  ERROR(Text001, "Development Company", FIELDCAPTION("Development Company"));
                                                              END;

                                                   CaptionML=[ENU=Development Company;
                                                              NOR=Utvikling selskap;
                                                              SVE=Utvecklingsf”retag];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Development Cost Object;Code20     ;OnValidate=VAR
                                                                DimensionValue@1210190000 : Record 349;
                                                              BEGIN
                                                                IF ("Development Cost Object" = '') THEN
                                                                  EXIT;

                                                                DimensionValue.CHANGECOMPANY("Development Company");
                                                                DimensionValue.SETRANGE(Code, "Development Cost Object");
                                                                DimensionValue.SETRANGE("Global Dimension No.", 2);
                                                                IF (NOT DimensionValue.FINDFIRST) THEN
                                                                  ERROR(Text002, FIELDCAPTION("Development Cost Object"), "Development Cost Object");

                                                                CheckCostObjectType;
                                                              END;

                                                   OnLookup=VAR
                                                              DimensionValue@1210190000 : Record 349;
                                                            BEGIN
                                                              DimensionValue.CHANGECOMPANY("Development Company");
                                                              DimensionValue.SETRANGE("Global Dimension No.", 2);
                                                              DimensionValue.Code := "Development Cost Object";
                                                              IF (PAGE.RUNMODAL(0, DimensionValue) = ACTION::LookupOK) THEN
                                                                "Development Cost Object" := DimensionValue.Code;

                                                              VALIDATE("Development Cost Object");
                                                            END;

                                                   CaptionML=[ENU=Development Cost Object;
                                                              NOR=Utvikling Kostnadsobjekt;
                                                              SVE=Utvecklingskostnadsobjekt] }
  }
  KEYS
  {
    {    ;Construction Company,Construction Cost Object;
                                                   Clustered=Yes }
    {    ;Development Company,Development Cost Object }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DevelopmentCompany@1210190002 : Text[30];
      ConstructionCompany@1210190001 : Text[30];
      Text000@1210190000 : TextConst 'ENU=%1 and %2 can not be determined.\Your current company is no %1 or %2.;NOR=%1 og %2 kan ikke fastsettes.\Ditt aktuelle selskap er nr %1 eller %2.;SVE=%1 och %2 kan inte fastst„llas.\Ditt aktuella f”retag „r nr %1 eller %2.';
      Text001@1210190004 : TextConst 'ENU=%1 is no %2.;NOR=%1 er ikke %2.;SVE=%1 „r inte %2.';
      Text002@1210190005 : TextConst 'ENU=%1 ''%2'' does not exist.;NOR=%1 ''%2'' finnes ikke.;SVE=%1 %2 finns inte.';
      Text003@1100528300 : TextConst 'ENU=%1 can''t be %2.;NOR=%1 kan ikke v‘re %2.;SVE=%1 kan inte vara %2.';

    PROCEDURE GetDevelopmentDescription@1210190000() : Text[50];
    BEGIN
      EXIT(GetDescription("Development Company", "Development Cost Object"));
    END;

    PROCEDURE GetConstructionDescription@1210190001() : Text[50];
    BEGIN
      EXIT(GetDescription("Construction Company", "Construction Cost Object"));
    END;

    LOCAL PROCEDURE GetDescription@1210190002(CompanyName@1210190000 : Text[30];CostObject@1210190001 : Code[20]) : Text[50];
    VAR
      DimensionValue@1210190002 : Record 349;
    BEGIN
      DimensionValue.CHANGECOMPANY(CompanyName);
      DimensionValue.SETCURRENTKEY(Code, "Global Dimension No.");
      DimensionValue.SETRANGE(Code, CostObject);
      DimensionValue.SETRANGE("Global Dimension No.", 2);
      IF (NOT DimensionValue.FINDFIRST) THEN
        EXIT('');
      EXIT(DimensionValue.Name);
    END;

    PROCEDURE FillDevAndConstructCompany@1210190003(VAR ConstructionCompany@1210190001 : Text[30];VAR DevelopmentCompany@1100528300 : Text[30]);
    VAR
      JobsSetup@1210190000 : Record 315;
    BEGIN
      JobsSetup.GET;
      IF (JobsSetup."Company Type" = JobsSetup."Company Type"::"Construction Company") THEN BEGIN
        DevelopmentCompany := JobsSetup."Related Company";
        JobsSetup.CHANGECOMPANY(JobsSetup."Related Company");
        JobsSetup.GET;
        IF (JobsSetup."Company Type" = JobsSetup."Company Type"::"Development Company") THEN
          ConstructionCompany := JobsSetup."Related Company";
      END;
      IF (JobsSetup."Company Type" = JobsSetup."Company Type"::"Development Company") THEN BEGIN
        ConstructionCompany := JobsSetup."Related Company";
        JobsSetup.CHANGECOMPANY(JobsSetup."Related Company");
        JobsSetup.GET;
        IF (JobsSetup."Company Type" = JobsSetup."Company Type"::"Construction Company") THEN
          DevelopmentCompany := JobsSetup."Related Company";
      END;

      IF (DevelopmentCompany = '') OR (ConstructionCompany = '') THEN
        ERROR(Text000, FIELDCAPTION("Construction Company"), FIELDCAPTION("Development Company"));
    END;

    PROCEDURE CheckCostObjectType@1100528300();
    VAR
      ConstructionDimensionValue@1100528301 : Record 349;
      DevelopmentDimensionValue@1100528302 : Record 349;
    BEGIN
      ConstructionDimensionValue.CHANGECOMPANY("Construction Company");
      ConstructionDimensionValue.SETCURRENTKEY(Code, "Global Dimension No.");
      ConstructionDimensionValue.SETRANGE(Code, "Construction Cost Object");
      ConstructionDimensionValue.SETRANGE("Global Dimension No.", 2);
      IF (NOT ConstructionDimensionValue.FINDFIRST) THEN
        EXIT;

      DevelopmentDimensionValue.CHANGECOMPANY("Development Company");
      DevelopmentDimensionValue.SETCURRENTKEY(Code, "Global Dimension No.");
      DevelopmentDimensionValue.SETRANGE(Code, "Development Cost Object");
      DevelopmentDimensionValue.SETRANGE("Global Dimension No.", 2);
      IF (NOT DevelopmentDimensionValue.FINDFIRST) THEN
        EXIT;

      IF (ConstructionDimensionValue."Cost Type" = ConstructionDimensionValue."Cost Type"::Labor) THEN BEGIN
        IF (DevelopmentDimensionValue."Cost Type" = DevelopmentDimensionValue."Cost Type"::Revenue) THEN
          ERROR(Text003, DevelopmentDimensionValue.FIELDCAPTION("Cost Type"), DevelopmentDimensionValue."Cost Type");
      END ELSE BEGIN
        IF(DevelopmentDimensionValue."Cost Type" = DevelopmentDimensionValue."Cost Type"::Labor) THEN
          ERROR(Text003, DevelopmentDimensionValue.FIELDCAPTION("Cost Type"), DevelopmentDimensionValue."Cost Type");
      END;
    END;

    PROCEDURE ImportAllCostObjects@1100528305();
    VAR
      ConstructionDimensionValue@1100528300 : Record 349;
    BEGIN
      FillDevAndConstructCompany(ConstructionCompany, DevelopmentCompany);
      ConstructionDimensionValue.CHANGECOMPANY(ConstructionCompany);
      ConstructionDimensionValue.SETCURRENTKEY(Code, "Global Dimension No.");
      ConstructionDimensionValue.SETRANGE("Global Dimension No.", 2);
      IF (NOT ConstructionDimensionValue.FINDSET) THEN
        EXIT;

      REPEAT
        INIT;
        "Construction Company" := ConstructionCompany;
        "Construction Cost Object" := ConstructionDimensionValue.Code;
        "Development Company" := DevelopmentCompany;
        IF (INSERT) THEN;
      UNTIL (ConstructionDimensionValue.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

