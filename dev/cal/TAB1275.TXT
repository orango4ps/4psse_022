OBJECT Table 1275 Doc. Exch. Service Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    Permissions=TableData 1261=rimd;
    OnInsert=BEGIN
               TESTFIELD("Primary Key",'');
               LogTelemetryWhenServiceCreated;
             END;

    OnDelete=BEGIN
               DeletePassword("Consumer Secret");
               DeletePassword("Consumer Key");
               DeletePassword(Token);
               DeletePassword("Token Secret");
               DeletePassword("Doc. Exch. Tenant ID");
             END;

    CaptionML=[ENU=Doc. Exch. Service Setup;
               NOR=Oppsett av dokumentutvekslingstjeneste;
               SVE=Serviceinst„llningar f”r dok.v„x.];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              NOR=Prim‘rn›kkel;
                                                              SVE=Prim„rnyckel] }
    { 4   ;   ;Sign-up URL         ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Sign-up URL;
                                                              NOR=URL-adresse for registrering;
                                                              SVE=URL f”r registrering] }
    { 5   ;   ;Service URL         ;Text250       ;OnValidate=VAR
                                                                WebRequestHelper@1000 : Codeunit 1299;
                                                              BEGIN
                                                                IF "Service URL" <> '' THEN
                                                                  WebRequestHelper.IsSecureHttpUrl("Service URL");
                                                              END;

                                                   ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Service URL;
                                                              NOR=URL-adresse for tjeneste;
                                                              SVE=Servicesida] }
    { 6   ;   ;Sign-in URL         ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Sign-in URL;
                                                              NOR=URL-adresse for p†logging;
                                                              SVE=URL f”r inloggning] }
    { 7   ;   ;Consumer Key        ;GUID          ;TableRelation="Service Password".Key;
                                                   CaptionML=[ENU=Consumer Key;
                                                              NOR=Forbrukern›kkel;
                                                              SVE=Anv„ndarnyckel] }
    { 8   ;   ;Consumer Secret     ;GUID          ;CaptionML=[ENU=Consumer Secret;
                                                              NOR=Forbrukerhemmelighet;
                                                              SVE=Konsumenthemlighet];
                                                   Editable=No }
    { 9   ;   ;Token               ;GUID          ;CaptionML=[ENU=Token;
                                                              NOR=Token;
                                                              SVE=Token];
                                                   Editable=No }
    { 10  ;   ;Token Secret        ;GUID          ;CaptionML=[ENU=Token Secret;
                                                              NOR=Tokenhemmelighet;
                                                              SVE=Tokenhemlighet];
                                                   Editable=No }
    { 11  ;   ;Doc. Exch. Tenant ID;GUID          ;DataClassification=OrganizationIdentifiableInformation;
                                                   CaptionML=[ENU=Doc. Exch. Tenant ID;
                                                              NOR=ID for leietaker for dokumentutveksling;
                                                              SVE=Klientorganisations-ID f”r dok.v„x.];
                                                   Editable=No }
    { 12  ;   ;User Agent          ;Text30        ;DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=User Agent;
                                                              NOR=Brukeragent;
                                                              SVE=Anv„ndare];
                                                   NotBlank=Yes }
    { 20  ;   ;Enabled             ;Boolean       ;OnValidate=BEGIN
                                                                IF Enabled THEN BEGIN
                                                                  CheckConnection;
                                                                  ScheduleJobQueueEntries;
                                                                  LogTelemetryWhenServiceEnabled;
                                                                  IF CONFIRM(JobQEntriesCreatedQst) THEN
                                                                    ShowJobQueueEntry;
                                                                END ELSE BEGIN
                                                                  CancelJobQueueEntries;
                                                                  LogTelemetryWhenServiceDisabled;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Enabled;
                                                              NOR=Aktivert;
                                                              SVE=Aktiv] }
    { 21  ;   ;Log Web Requests    ;Boolean       ;CaptionML=[ENU=Log Web Requests;
                                                              NOR=Loggf›r webforesp›rsler;
                                                              SVE=Logga webbf”rfr†gan] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      JobQEntriesCreatedQst@1003 : TextConst 'ENU=A job queue entry for exchanging documents has been created.\\Do you want to open the Job Queue Entries window?;NOR=Det er opprettet en jobbk›post for utveksling av dokumenter.\\Vil du †pne vinduet Poster i jobbk›?;SVE=En jobbk”transaktion f”r byte av dokument har skapats.\\Vill du ”ppna f”nstret f”r jobbk”transaktioner?';
      DocExchServiceMgt@1002 : Codeunit 1410;
      DocExchServiceCreatedTxt@1005 : TextConst '@@@={Locked};ENU=The user started setting up document exchange service.;NOR=The user started setting up document exchange service.';
      DocExchServiceEnabledTxt@1004 : TextConst '@@@={Locked};ENU=The user enabled document exchange service.;NOR=The user enabled document exchange service.';
      DocExchServiceDisabledTxt@1001 : TextConst '@@@={Locked};ENU=The user disabled document exchange service.;NOR=The user disabled document exchange service.';
      TelemetryCategoryTok@1000 : TextConst '@@@={Locked};ENU=AL Document Exchange Service;NOR=AL Document Exchange Service';

    [External]
    PROCEDURE SavePassword@1(VAR PasswordKey@1001 : GUID;PasswordText@1000 : Text);
    VAR
      ServicePassword@1002 : Record 1261;
    BEGIN
      PasswordText := DELCHR(PasswordText,'=',' ');

      IF ISNULLGUID(PasswordKey) OR NOT ServicePassword.GET(PasswordKey) THEN BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.INSERT(TRUE);
        PasswordKey := ServicePassword.Key;
        MODIFY;
      END ELSE BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.MODIFY;
      END;
      COMMIT;
    END;

    [External]
    PROCEDURE GetPassword@2(PasswordKey@1001 : GUID) : Text;
    VAR
      ServicePassword@1000 : Record 1261;
    BEGIN
      ServicePassword.GET(PasswordKey);
      EXIT(ServicePassword.GetPassword);
    END;

    LOCAL PROCEDURE DeletePassword@4(PasswordKey@1000 : GUID);
    VAR
      ServicePassword@1001 : Record 1261;
    BEGIN
      IF ServicePassword.GET(PasswordKey) THEN
        ServicePassword.DELETE;
    END;

    [External]
    PROCEDURE HasPassword@3(PasswordKey@1001 : GUID) : Boolean;
    VAR
      ServicePassword@1000 : Record 1261;
    BEGIN
      IF NOT ServicePassword.GET(PasswordKey) THEN
        EXIT(FALSE);

      EXIT(ServicePassword.GetPassword <> '');
    END;

    [External]
    PROCEDURE SetURLsToDefault@5();
    BEGIN
      DocExchServiceMgt.SetURLsToDefault(Rec);
    END;

    [Internal]
    PROCEDURE CheckConnection@7();
    BEGIN
      DocExchServiceMgt.CheckConnection;
    END;

    LOCAL PROCEDURE ScheduleJobQueueEntries@10();
    VAR
      JobQueueEntry@1000 : Record 472;
      DummyRecId@1001 : RecordID;
    BEGIN
      JobQueueEntry.ScheduleRecurrentJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,
        CODEUNIT::"Doc. Exch. Serv.- Doc. Status",DummyRecId);
      JobQueueEntry.ScheduleRecurrentJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,
        CODEUNIT::"Doc. Exch. Serv. - Recv. Docs.",DummyRecId);
    END;

    LOCAL PROCEDURE CancelJobQueueEntries@11();
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      CancelJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,
        CODEUNIT::"Doc. Exch. Serv.- Doc. Status");
      CancelJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,
        CODEUNIT::"Doc. Exch. Serv. - Recv. Docs.");
    END;

    LOCAL PROCEDURE CancelJobQueueEntry@16(ObjType@1001 : Option;ObjID@1002 : Integer);
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      IF JobQueueEntry.FindJobQueueEntry(ObjType,ObjID) THEN
        JobQueueEntry.Cancel;
    END;

    [External]
    PROCEDURE ShowJobQueueEntry@8();
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      JobQueueEntry.SETRANGE("Object Type to Run",JobQueueEntry."Object Type to Run"::Codeunit);
      JobQueueEntry.SETFILTER("Object ID to Run",'%1|%2',
        CODEUNIT::"Doc. Exch. Serv.- Doc. Status",
        CODEUNIT::"Doc. Exch. Serv. - Recv. Docs.");
      IF JobQueueEntry.FINDFIRST THEN
        PAGE.RUN(PAGE::"Job Queue Entries",JobQueueEntry);
    END;

    LOCAL PROCEDURE LogTelemetryWhenServiceEnabled@19();
    BEGIN
      SENDTRACETAG('00008A9',TelemetryCategoryTok,VERBOSITY::Normal,DocExchServiceEnabledTxt,DATACLASSIFICATION::SystemMetadata);
      SENDTRACETAG('00008AA',TelemetryCategoryTok,VERBOSITY::Normal,"Service URL",DATACLASSIFICATION::SystemMetadata);
    END;

    LOCAL PROCEDURE LogTelemetryWhenServiceDisabled@18();
    BEGIN
      SENDTRACETAG('00008AB',TelemetryCategoryTok,VERBOSITY::Normal,DocExchServiceDisabledTxt,DATACLASSIFICATION::SystemMetadata);
      SENDTRACETAG('00008AC',TelemetryCategoryTok,VERBOSITY::Normal,"Service URL",DATACLASSIFICATION::SystemMetadata);
    END;

    LOCAL PROCEDURE LogTelemetryWhenServiceCreated@17();
    BEGIN
      SENDTRACETAG('00008AD',TelemetryCategoryTok,VERBOSITY::Normal,DocExchServiceCreatedTxt,DATACLASSIFICATION::SystemMetadata);
    END;

    BEGIN
    END.
  }
}

