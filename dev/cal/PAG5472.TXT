OBJECT Page 5472 Vendor Entity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=vendors;
               NOR=vendors;
               SVE=vendors];
    SourceTable=Table23;
    DelayedInsert=Yes;
    PageType=API;
    ChangeTrackingAllowed=Yes;
    EntitySetName=vendors;
    EntityName=vendor;
    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     Vendor@1002 : Record 23;
                     GraphMgtGeneralTools@1004 : Codeunit 5465;
                     RecRef@1003 : RecordRef;
                   BEGIN
                     Vendor.SETRANGE("No.","No.");
                     IF NOT Vendor.ISEMPTY THEN
                       INSERT;

                     INSERT(TRUE);

                     ProcessPostalAddress;
                     RecRef.GETTABLE(Rec);
                     GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,CURRENTDATETIME);
                     RecRef.SETTABLE(Rec);

                     MODIFY(TRUE);
                     SetCalculatedFields;
                     EXIT(FALSE);
                   END;

    OnModifyRecord=VAR
                     Vendor@1002 : Record 23;
                     GraphMgtGeneralTools@1001 : Codeunit 5465;
                   BEGIN
                     IF xRec.Id <> Id THEN
                       GraphMgtGeneralTools.ErrorIdImmutable;
                     Vendor.SETRANGE(Id,Id);
                     Vendor.FINDFIRST;

                     ProcessPostalAddress;

                     IF "No." = Vendor."No." THEN
                       MODIFY(TRUE)
                     ELSE BEGIN
                       Vendor.TRANSFERFIELDS(Rec,FALSE);
                       Vendor.RENAME("No.");
                       TRANSFERFIELDS(Vendor);
                     END;

                     SetCalculatedFields;
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 14  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id;
                           NOR=Id;
                           SVE=Id];
                ApplicationArea=#All;
                SourceExpr=Id;
                Editable=FALSE }

    { 13  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number;
                           NOR=Number;
                           SVE=Number];
                ApplicationArea=#All;
                SourceExpr="No." }

    { 12  ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=DisplayName;
                           NOR=DisplayName;
                           SVE=DisplayName];
                ApplicationArea=#All;
                SourceExpr=Name;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Name));
                           END;
                            }

    { 7   ;2   ;Field     ;
                Name=address;
                CaptionML=[@@@={Locked};
                           ENU=Address;
                           NOR=Address;
                           SVE=Address];
                ToolTipML=[ENU=Specifies the address for the vendor.;
                           NOR=Angir adressen for leverand›ren.;
                           SVE=Anger adressen till leverant”ren.];
                ApplicationArea=#All;
                SourceExpr=PostalAddressJSON;
                OnValidate=BEGIN
                             PostalAddressSet := TRUE;
                           END;

                ODataEDMType=POSTALADDRESS }

    { 5   ;2   ;Field     ;
                Name=phoneNumber;
                CaptionML=[@@@={Locked};
                           ENU=PhoneNumber;
                           NOR=PhoneNumber;
                           SVE=PhoneNumber];
                ApplicationArea=#All;
                SourceExpr="Phone No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Phone No."));
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=email;
                CaptionML=[@@@={Locked};
                           ENU=Email;
                           NOR=Email;
                           SVE=Email];
                ApplicationArea=#All;
                SourceExpr="E-Mail";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("E-Mail"));
                           END;
                            }

    { 3   ;2   ;Field     ;
                Name=website;
                CaptionML=[@@@={Locked};
                           ENU=Website;
                           NOR=Website;
                           SVE=Website];
                ApplicationArea=#All;
                SourceExpr="Home Page";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Home Page"));
                           END;
                            }

    { 15  ;2   ;Field     ;
                Name=taxRegistrationNumber;
                CaptionML=[@@@={Locked};
                           ENU=TaxRegistrationNumber;
                           NOR=TaxRegistrationNumber;
                           SVE=TaxRegistrationNumber];
                ApplicationArea=#All;
                SourceExpr="VAT Registration No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("VAT Registration No."));
                           END;
                            }

    { 16  ;2   ;Field     ;
                Name=currencyId;
                CaptionML=[@@@={Locked};
                           ENU=CurrencyId;
                           NOR=CurrencyId;
                           SVE=CurrencyId];
                ApplicationArea=#All;
                SourceExpr="Currency Id";
                OnValidate=BEGIN
                             IF "Currency Id" = BlankGUID THEN
                               "Currency Code" := ''
                             ELSE BEGIN
                               Currency.SETRANGE(Id,"Currency Id");
                               IF NOT Currency.FINDFIRST THEN
                                 ERROR(CurrencyIdDoesNotMatchACurrencyErr);

                               "Currency Code" := Currency.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;
                            }

    { 11  ;2   ;Field     ;
                Name=currencyCode;
                CaptionML=[@@@={Locked};
                           ENU=CurrencyCode;
                           NOR=CurrencyCode;
                           SVE=CurrencyCode];
                ApplicationArea=#All;
                SourceExpr=CurrencyCodeTxt;
                OnValidate=BEGIN
                             "Currency Code" :=
                               GraphMgtGeneralTools.TranslateCurrencyCodeToNAVCurrencyCode(
                                 LCYCurrencyCode,COPYSTR(CurrencyCodeTxt,1,MAXSTRLEN(LCYCurrencyCode)));

                             IF Currency.Code <> '' THEN BEGIN
                               IF Currency.Code <> "Currency Code" THEN
                                 ERROR(CurrencyValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Currency Code" = '' THEN
                               "Currency Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT Currency.GET("Currency Code") THEN
                                 ERROR(CurrencyCodeDoesNotMatchACurrencyErr);

                               "Currency Id" := Currency.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;
                            }

    { 17  ;2   ;Field     ;
                Name=irs1099Code;
                CaptionML=[@@@={Locked};
                           ENU=IRS1099Code;
                           NOR=IRS1099Code;
                           SVE=IRS1099Code];
                ApplicationArea=#All;
                SourceExpr=IRS1099Code }

    { 9   ;2   ;Field     ;
                Name=paymentTermsId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTermsId;
                           NOR=PaymentTermsId;
                           SVE=PaymentTermsId];
                ApplicationArea=#All;
                SourceExpr="Payment Terms Id";
                OnValidate=BEGIN
                             IF "Payment Terms Id" = BlankGUID THEN
                               "Payment Terms Code" := ''
                             ELSE BEGIN
                               PaymentTerms.SETRANGE(Id,"Payment Terms Id");
                               IF NOT PaymentTerms.FINDFIRST THEN
                                 ERROR(PaymentTermsIdDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Code" := PaymentTerms.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;
                            }

    { 10  ;2   ;Field     ;
                Name=paymentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentMethodId;
                           NOR=PaymentMethodId;
                           SVE=PaymentMethodId];
                ApplicationArea=#All;
                SourceExpr="Payment Method Id";
                OnValidate=BEGIN
                             IF "Payment Method Id" = BlankGUID THEN
                               "Payment Method Code" := ''
                             ELSE BEGIN
                               PaymentMethod.SETRANGE(Id,"Payment Method Id");
                               IF NOT PaymentMethod.FINDFIRST THEN
                                 ERROR(PaymentMethodIdDoesNotMatchAPaymentMethodErr);

                               "Payment Method Code" := PaymentMethod.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Method Id"));
                             RegisterFieldSet(FIELDNO("Payment Method Code"));
                           END;
                            }

    { 22  ;2   ;Field     ;
                Name=taxLiable;
                CaptionML=[@@@={Locked};
                           ENU=TaxLiable;
                           NOR=TaxLiable;
                           SVE=TaxLiable];
                ApplicationArea=#All;
                SourceExpr="Tax Liable";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Tax Liable"));
                           END;
                            }

    { 23  ;2   ;Field     ;
                Name=blocked;
                CaptionML=[@@@={Locked};
                           ENU=Blocked;
                           NOR=Blocked;
                           SVE=Blocked];
                ApplicationArea=#All;
                SourceExpr=Blocked;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Blocked));
                           END;
                            }

    { 24  ;2   ;Field     ;
                Name=balance;
                CaptionML=[@@@={Locked};
                           ENU=Balance;
                           NOR=Balance;
                           SVE=Balance];
                ApplicationArea=#All;
                SourceExpr="Balance (LCY)" }

    { 6   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime;
                           NOR=LastModifiedDateTime;
                           SVE=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time" }

    { 8   ;2   ;Part      ;
                Name=picture;
                CaptionML=[ENU=picture;
                           NOR=bilde;
                           SVE=bild];
                ApplicationArea=#All;
                SubPageLink=Id=FIELD(Id);
                PagePartID=Page5468;
                EntitySetName=picture;
                EntityName=picture;
                PartType=Page }

    { 18  ;2   ;Part      ;
                Name=defaultDimensions;
                CaptionML=[@@@={Locked};
                           ENU=DefaultDimensions;
                           NOR=DefaultDimensions;
                           SVE=DefaultDimensions];
                ApplicationArea=#All;
                SubPageLink=ParentId=FIELD(Id);
                PagePartID=Page5509;
                EntitySetName=defaultDimensions;
                EntityName=defaultDimensions;
                PartType=Page }

  }
  CODE
  {
    VAR
      Currency@1018 : Record 4;
      PaymentTerms@1011 : Record 3;
      PaymentMethod@1016 : Record 289;
      TempFieldSet@1003 : TEMPORARY Record 2000000041;
      GraphMgtGeneralTools@1006 : Codeunit 5465;
      LCYCurrencyCode@1007 : Code[10];
      CurrencyCodeTxt@1008 : Text;
      PostalAddressJSON@1004 : Text;
      IRS1099Code@1001 : Code[10];
      CurrencyValuesDontMatchErr@1021 : TextConst '@@@={Locked};ENU=The currency values do not match to a specific Currency.;NOR=The currency values do not match to a specific Currency.;SVE=The currency values do not match to a specific Currency.';
      CurrencyIdDoesNotMatchACurrencyErr@1020 : TextConst '@@@={Locked};ENU=The "currencyId" does not match to a Currency.;NOR=The "currencyId" does not match to a Currency.;SVE=The "currencyId" does not match to a Currency.';
      CurrencyCodeDoesNotMatchACurrencyErr@1019 : TextConst '@@@={Locked};ENU=The "currencyCode" does not match to a Currency.;NOR=The "currencyCode" does not match to a Currency.;SVE=The "currencyCode" does not match to a Currency.';
      PaymentTermsIdDoesNotMatchAPaymentTermsErr@1009 : TextConst '@@@={Locked};ENU=The "paymentTermsId" does not match to a Payment Terms.;NOR=The "paymentTermsId" does not match to a Payment Terms.;SVE=The "paymentTermsId" does not match to a Payment Terms.';
      PaymentMethodIdDoesNotMatchAPaymentMethodErr@1014 : TextConst '@@@={Locked};ENU=The "paymentMethodId" does not match to a Payment Method.;NOR=The "paymentMethodId" does not match to a Payment Method.;SVE=The "paymentMethodId" does not match to a Payment Method.';
      BlankGUID@1017 : GUID;
      PostalAddressSet@1022 : Boolean;

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      GraphMgtVendor@1000 : Codeunit 5472;
    BEGIN
      PostalAddressJSON := GraphMgtVendor.PostalAddressToJSON(Rec);
      CurrencyCodeTxt := GraphMgtGeneralTools.TranslateNAVCurrencyCodeToCurrencyCode(LCYCurrencyCode,"Currency Code");
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(Id);
      CLEAR(PostalAddressJSON);
      CLEAR(IRS1099Code);
      CLEAR(PostalAddressSet);
      TempFieldSet.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      IF TempFieldSet.GET(DATABASE::Vendor,FieldNo) THEN
        EXIT;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::Vendor;
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(TRUE);
    END;

    LOCAL PROCEDURE ProcessPostalAddress@5();
    VAR
      GraphMgtVendor@1000 : Codeunit 5472;
    BEGIN
      IF NOT PostalAddressSet THEN
        EXIT;

      GraphMgtVendor.UpdatePostalAddress(PostalAddressJSON,Rec);

      IF xRec.Address <> Address THEN
        RegisterFieldSet(FIELDNO(Address));

      IF xRec."Address 2" <> "Address 2" THEN
        RegisterFieldSet(FIELDNO("Address 2"));

      IF xRec.City <> City THEN
        RegisterFieldSet(FIELDNO(City));

      IF xRec."Country/Region Code" <> "Country/Region Code" THEN
        RegisterFieldSet(FIELDNO("Country/Region Code"));

      IF xRec."Post Code" <> "Post Code" THEN
        RegisterFieldSet(FIELDNO("Post Code"));

      IF xRec.County <> County THEN
        RegisterFieldSet(FIELDNO(County));
    END;

    BEGIN
    END.
  }
}

