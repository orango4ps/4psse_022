OBJECT Table 12013630 Web Company ID
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF513000;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               IF "Company Name" <> COMPANYNAME THEN
                 ERROR(EXF003, "Company Name", COMPANYNAME);

               ConvertCompToWebID;

               IF "Web enabled" THEN
                 SetWebID;

               RecRef.GETTABLE(Rec);
               WebSetting.TableChange(RecRef);
             END;

    OnModify=BEGIN
               // 2816 >>
               IF (Rec."Web enabled")  AND
                  (Rec."Web enabled" <> xRec."Web enabled") THEN
                 SetWebID
               ELSE IF (Rec."Web enabled") AND
                       (Rec."Web Company ID" <> xRec."Web Company ID") THEN
                 SetWebID
               ELSE IF (NOT Rec."Web enabled") AND
                       (Rec."Web enabled" <> xRec."Web enabled") THEN
                 ClearWebID("Company Name");
               // 2816 <<

               RecRef.GETTABLE(Rec);
               WebSetting.TableChange(RecRef);
             END;

    OnDelete=BEGIN
               ClearWebID("Company Name");
               RecRef.GETTABLE(Rec);
               WebSetting.TableChange(RecRef);
             END;

    CaptionML=[ENU=Web Company ID;
               SVE=Webb f”retagsID];
  }
  FIELDS
  {
    { 1   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=[ENU=Company Name;
                                                              SVE=F”retagsnamn];
                                                   Editable=No }
    { 5   ;   ;Web Company ID      ;BigInteger    ;CaptionML=[ENU=Web Company ID;
                                                              SVE=Webb f”retagsID];
                                                   Description=Used to make web IDs unique;
                                                   Editable=No }
    { 10  ;   ;Web enabled         ;Boolean       ;CaptionML=[ENU=Company is enabled for web;
                                                              SVE=F”retaget „r aktiverat f”r web] }
    { 50  ;   ;Test Mode (web)     ;Boolean       ;OnValidate=VAR
                                                                _WebCompID@1100285000 : Record 12013630;
                                                              BEGIN
                                                                _WebCompID.SETFILTER("Company Name", '<>%1', "Company Name");
                                                                _WebCompID.MODIFYALL("Test Mode (web)", "Test Mode (web)");
                                                              END;

                                                   CaptionML=[ENU=Test mode (web);
                                                              SVE=Testl„ge (webb)] }
    { 51  ;   ;Default Company     ;Boolean       ;Editable=Yes }
  }
  KEYS
  {
    {    ;Company Name                            ;Clustered=Yes }
    {    ;Web Company ID                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      WebSetting@1100285001 : Record 12057071;
      RecRef@1100285000 : RecordRef;
      EXF001@1100285002 : TextConst 'ENU=Company %1 now enabled for ExFlow Web;SVE=F”retag %1 „r nu aktiverat f”r ExFlow Webb';
      EXF002@1100285003 : TextConst 'ENU=Company %1 now disabled for ExFlow Web;SVE=F”retag %1 „r nu avaktiverat f”r ExFlow Webb';
      EXF003@1100285004 : TextConst 'ENU=Cannot enable Company %1 from Company %2;SVE=Kan inte aktivera f”retag %1 fr†n f”retag %2';

    LOCAL PROCEDURE ConvertCompToWebID@1100285000();
    VAR
      WebCompID2@1100285000 : Record 12013630;
      Prefix@1100285002 : BigInteger;
    BEGIN
      IF "Company Name" = '' THEN
        EXIT;

      Prefix := 1000000000;
      WebCompID2.RESET;
      WebCompID2.SETCURRENTKEY("Web Company ID");
      IF NOT WebCompID2.FINDLAST THEN
        "Web Company ID" := Prefix
      ELSE
        "Web Company ID" := WebCompID2."Web Company ID" + Prefix;
    END;

    PROCEDURE WebEnable@1100285001(CompName@1100285001 : Text[30];ShowDialog@1100285003 : Boolean);
    VAR
      WebCompID@1100285000 : Record 12013630;
      WebCompID2@1100285002 : Record 12013630;
    BEGIN
      IF NOT WebCompID.GET(CompName) THEN BEGIN
        WebCompID.LOCKTABLE;
        WebCompID.INIT;
        WebCompID."Company Name" := CompName;
        WebCompID."Web enabled" := TRUE;
        WebCompID2.SETRANGE("Test Mode (web)", TRUE);
        WebCompID."Test Mode (web)" := WebCompID2.FINDFIRST;
        WebCompID.INSERT(TRUE);
      END ELSE BEGIN
        WebCompID."Web enabled" := TRUE;
        WebCompID.MODIFY(TRUE);
      END;

      IF ShowDialog THEN
        MESSAGE(STRSUBSTNO(EXF001,CompName));
    END;

    PROCEDURE WebDisable@1100285003(CompName@1100285001 : Text[30];ShowDialog@1100285002 : Boolean);
    VAR
      WebCompID@1100285000 : Record 12013630;
    BEGIN
      IF WebCompID.GET(CompName) THEN BEGIN
        WebCompID."Web enabled" := FALSE;
        WebCompID.MODIFY(TRUE);
      END;

      IF ShowDialog THEN
        MESSAGE(STRSUBSTNO(EXF002,CompName));
    END;

    LOCAL PROCEDURE SetWebID@1100285002();
    VAR
      ExDoc@1100285000 : Record 12013608;
      ExDoc2@1100285001 : Record 12013608;
      PostedExDoc@1100285003 : Record 12013626;
      PostedExDoc2@1100285002 : Record 12013626;
      ExImageDoc@1100285008 : Record 12013612;
      ExAttachedFile@1100285009 : Record 12013643;
    BEGIN
      ExDoc.RESET;
      IF ExDoc.FINDSET(TRUE,TRUE) THEN
        REPEAT
          ExDoc2.GET(ExDoc.ID);
          ExDoc2."Web Company ID" := "Web Company ID";
          ExDoc2."Web ID" := ExDoc2.ID + ExDoc2."Web Company ID";
          ExDoc2.MODIFY;
          ExImageDoc.RESET;
          ExImageDoc.SETCURRENTKEY("Document ID");
          ExImageDoc.SETRANGE("Document ID",ExDoc2.ID);
          IF ExImageDoc.FINDFIRST THEN BEGIN
            ExImageDoc."Web ID" := ExDoc2."Web ID";
            ExImageDoc.MODIFY;
          END;
          ExAttachedFile.RESET;
          ExAttachedFile.SETCURRENTKEY("Document ID");
          ExAttachedFile.SETRANGE("Document ID",ExDoc2.ID);
          ExAttachedFile.MODIFYALL("Web ID",ExDoc2."Web ID");
        UNTIL ExDoc.NEXT = 0;

      PostedExDoc.RESET;
      IF PostedExDoc.FINDSET(TRUE,TRUE) THEN
        REPEAT
          PostedExDoc2.GET(PostedExDoc.ID);
          PostedExDoc2."Web Company ID" := "Web Company ID";
          PostedExDoc2."Web ID" := PostedExDoc2.ID + PostedExDoc2."Web Company ID";
          PostedExDoc2.MODIFY;
          ExImageDoc.RESET;
          ExImageDoc.SETCURRENTKEY("Document ID");
          ExImageDoc.SETRANGE("Document ID",PostedExDoc2.ID);
          IF ExImageDoc.FINDFIRST THEN BEGIN
            ExImageDoc."Web ID" := PostedExDoc2."Web ID";
            ExImageDoc.MODIFY;
          END;
          ExAttachedFile.RESET;
          ExAttachedFile.SETCURRENTKEY("Document ID");
          ExAttachedFile.SETRANGE("Document ID",PostedExDoc2.ID);
          ExAttachedFile.MODIFYALL("Web ID",PostedExDoc2."Web ID");
        UNTIL PostedExDoc.NEXT = 0;
    END;

    LOCAL PROCEDURE ClearWebID@1100285004(CompName@1100285004 : Text[100]);
    VAR
      ExDoc@1100285003 : Record 12013608;
      PostedExDoc@1100285002 : Record 12013626;
      ExImageDoc@1100285001 : Record 12013612;
      ExAttachedFile@1100285000 : Record 12013643;
      Company@1100285005 : Record 2000000006;
    BEGIN
      Company.RESET;
      Company.SETRANGE(Name,CompName);
      IF NOT Company.FINDFIRST THEN
        EXIT;

      ExDoc.CHANGECOMPANY(CompName);
      ExDoc.MODIFYALL("Web ID",0);
      PostedExDoc.CHANGECOMPANY(CompName);
      PostedExDoc.MODIFYALL("Web ID",0);
      ExImageDoc.CHANGECOMPANY(CompName);
      ExImageDoc.MODIFYALL("Web ID",0);
      ExAttachedFile.CHANGECOMPANY(CompName);
      ExAttachedFile.MODIFYALL("Web ID",0);
    END;

    BEGIN
    END.
  }
}

