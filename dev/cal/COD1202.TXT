OBJECT Codeunit 1202 Import Payroll Transaction
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1220=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FileMgt@1000 : Codeunit 419;
      ImportPayrollTransCap@1002 : TextConst 'ENU=Select Payroll Transaction;NOR=Velg l›nnstransaksjon;SVE=V„lj en l”netransaktion';
      FileFilterTxt@1001 : TextConst 'ENU="Text Files(*.txt;*.csv)|*.txt;*.csv";NOR="Tekstfiler (*.txt;*.csv)|*.txt;*.csv";SVE="Textfiler(*.txt;*.csv)|*.txt;*.csv"';
      FileFilterExtensionTxt@1004 : TextConst '@@@={Locked};ENU=txt,csv;NOR=txt,csv;SVE=txt,csv';
      ProcessingSetupErr@1003 : TextConst 'ENU=You must specify either a reading/writing XMLport or a reading/writing codeunit.;NOR=Du m† enten angi en XMLport for lesing/skriving eller en codeunit for lesing/skriving.;SVE=Ange antingen en XMLport f”r l„sning/skrivning eller en kodenhet f”r l„sning/skrivning.';

    [Internal]
    PROCEDURE SelectAndImportPayrollDataToGL@2(VAR GenJournalLine@1000 : Record 81;DataExchDefCode@1001 : Code[20]);
    VAR
      TempBlob@1002 : Record 99008535;
      FileName@1003 : Text;
    BEGIN
      FileName := FileMgt.BLOBImportWithFilter(TempBlob,ImportPayrollTransCap,'',FileFilterTxt,FileFilterExtensionTxt);
      IF FileName <> '' THEN
        ImportPayrollDataToGL(GenJournalLine,FileName,TempBlob,DataExchDefCode);
    END;

    PROCEDURE ImportPayrollDataToGL@18(GenJournalLine@1000 : Record 81;FileName@1005 : Text;TempBlob@1001 : Record 99008535;DataExchDefCode@1002 : Code[20]);
    VAR
      GenJournalLineTemplate@1060 : Record 81;
      DataExch@1003 : Record 1220;
      DataExchDef@1004 : Record 1222;
    BEGIN
      DataExchDef.GET(DataExchDefCode);
      IF (DataExchDef."Reading/Writing XMLport" <> 0) = (DataExchDef."Reading/Writing Codeunit" <> 0) THEN
        ERROR(ProcessingSetupErr);

      PrepareGenJournalLine(GenJournalLineTemplate,GenJournalLine);
      CreateDataExch(DataExch,FileName,TempBlob,DataExchDefCode);
      ImportPayrollFile(DataExch);
      MapDataToGenJournalLine(DataExch,GenJournalLineTemplate);
    END;

    LOCAL PROCEDURE CreateDataExch@9(VAR DataExch@1004 : Record 1220;FileName@1002 : Text;TempBlob@1000 : Record 99008535;DataExchDefCode@1001 : Code[20]);
    VAR
      Source@1003 : InStream;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(Source);
      DataExch.InsertRec(COPYSTR(FileName,1,MAXSTRLEN(DataExch."File Name")),Source,DataExchDefCode);
    END;

    LOCAL PROCEDURE ImportPayrollFile@11(DataExch@1000 : Record 1220);
    VAR
      DataExchDef@1006 : Record 1222;
      Source@1002 : InStream;
    BEGIN
      DataExch."File Content".CREATEINSTREAM(Source);
      DataExch.SETRANGE("Entry No.",DataExch."Entry No.");
      DataExchDef.GET(DataExch."Data Exch. Def Code");
      IF DataExchDef."Reading/Writing XMLport" <> 0 THEN
        XMLPORT.IMPORT(DataExchDef."Reading/Writing XMLport",Source,DataExch)
      ELSE
        IF DataExchDef."Reading/Writing Codeunit" <> 0 THEN
          CODEUNIT.RUN(DataExchDef."Reading/Writing Codeunit",DataExch);
    END;

    LOCAL PROCEDURE MapDataToGenJournalLine@14(DataExch@1000 : Record 1220;GenJournalLineTemplate@1001 : Record 81);
    VAR
      ProcessDataExch@1003 : Codeunit 1201;
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(GenJournalLineTemplate);
      ProcessDataExch.ProcessAllLinesColumnMapping(DataExch,RecRef);
    END;

    LOCAL PROCEDURE PrepareGenJournalLine@1060(VAR GenJournalLineTemplate@1060 : Record 81;GenJournalLine@1061 : Record 81);
    BEGIN
      WITH GenJournalLineTemplate DO BEGIN
        "Journal Template Name" := GenJournalLine."Journal Template Name";
        "Journal Batch Name" := GenJournalLine."Journal Batch Name";
        SetUpNewLine(GenJournalLine,GenJournalLine."Balance (LCY)",TRUE);
        "Account Type" := "Account Type"::"G/L Account";

        GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");
        GenJournalLine.SETRANGE("Journal Batch Name",GenJournalLine."Journal Batch Name");
        IF GenJournalLine.FINDLAST THEN BEGIN
          "Line No." := GenJournalLine."Line No.";
          "Document No." := INCSTR(GenJournalLine."Document No.");
        END ELSE
          "Document No." := GenJournalLine."Document No.";
      END;
    END;

    BEGIN
    END.
  }
}

