OBJECT Table 11071894 Budget Rate Read Order Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Budget Rate Read Order Line;
               NOR="Leseordrerad for Budsjettsats "];
  }
  FIELDS
  {
    { 10  ;   ;Budget Rate Type    ;Option        ;CaptionML=[ENU=Budget Rate Type;
                                                              NOR=Satstyper for budsjett];
                                                   OptionCaptionML=[ENU=Project,Service;
                                                                    NOR=Prosjekt,Service;
                                                                    SVE=Projekt,Service];
                                                   OptionString=Project,Service }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;Read Order          ;Integer       ;OnValidate=VAR
                                                                BudgetRateReadOrderLine@1100528600 : Record 11071894;
                                                              BEGIN
                                                                IF "Read Order" > 0 THEN BEGIN
                                                                  BudgetRateReadOrderLine.RESET;
                                                                  BudgetRateReadOrderLine.SETCURRENTKEY("Budget Rate Type", "Read Order");
                                                                  BudgetRateReadOrderLine.SETRANGE("Budget Rate Type", "Budget Rate Type");
                                                                  BudgetRateReadOrderLine.SETRANGE("Read Order", "Read Order");
                                                                  BudgetRateReadOrderLine.SETFILTER("Line No.",'<>%1', "Line No.");
                                                                  IF BudgetRateReadOrderLine.FINDFIRST THEN
                                                                    ERROR(Text001, FIELDCAPTION("Read Order"), "Read Order");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Read Order;
                                                              NOR=Les Ordre;
                                                              SVE=L„sorder];
                                                   BlankZero=Yes }
    { 40  ;   ;Table No.           ;Integer       ;CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr.;
                                                              SVE=Tabellnr] }
    { 50  ;   ;Budget Rate Table   ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Budget Rate Table;
                                                              NOR=Budsjettsatstabell];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Budget Rate Type,Line No.               ;Clustered=Yes }
    {    ;Budget Rate Type,Read Order              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485001 : TextConst 'ENU=%1 %2 may only be used once.;NOR=%1 %2 kan kun brukes en g†ng.;SVE=%1 %2 kan endast anv„ndas en g†ng.';
      Text002@1100485005 : TextConst 'ENU=Table %1 is not involved in a Budget Rate Read Order.;NOR=Tabell %1 er ikke involvert i en leseordre for budsjettsats';

    PROCEDURE CreateReadOrderLines@1100485000(Initialize@1100485000 : Boolean);
    VAR
      BudgetRateReadOrderLine@1100485003 : Record 11071894;
      ServiceSetup@1100528601 : Record 11012800;
      BudgetRateType@1100485001 : 'Project,Service';
      NewReadOrder@1100525000 : Integer;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      IF Initialize THEN
        BudgetRateReadOrderLine.DELETEALL
      ELSE
        Initialize := BudgetRateReadOrderLine.ISEMPTY;

      //Project
      BudgetRateType := BudgetRateType::Project;
      CreateReadOrderLine(BudgetRateType, DATABASE::"Budget Rate");
      CreateReadOrderLine(BudgetRateType, DATABASE::"Project Budget Rate");
      CreateReadOrderLine(BudgetRateType, DATABASE::"Department Budget Rate");

      IF Initialize THEN BEGIN
        NewReadOrder := 1;
        WriteInitialReadOrder(BudgetRateType, DATABASE::"Project Budget Rate", NewReadOrder);
        WriteInitialReadOrder(BudgetRateType, DATABASE::"Budget Rate", NewReadOrder);
      END;

      //Service
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(ServiceSetup) THEN BEGIN
        BudgetRateType := BudgetRateType::Service;
        CreateReadOrderLine(BudgetRateType, DATABASE::"Budget Rate");
        CreateReadOrderLine(BudgetRateType, DATABASE::"Department Budget Rate");
        IF Initialize THEN BEGIN
          NewReadOrder := 1;
          WriteInitialReadOrder(BudgetRateType, DATABASE::"Budget Rate", NewReadOrder);
        END;
      END;
    END;

    PROCEDURE CreateReadOrderLine@1100485001(IBudgetRateType@1100485001 : Option;ITableNo@1100485002 : Integer);
    VAR
      BudgetRateReadOrderLine@1100485000 : Record 11071894;
      LineNo@1100485004 : Integer;
    BEGIN
      BudgetRateReadOrderLine.SETRANGE("Budget Rate Type", IBudgetRateType);
      IF BudgetRateReadOrderLine.FINDLAST THEN
        LineNo := BudgetRateReadOrderLine."Line No." + 1
      ELSE
        LineNo := 1;

      BudgetRateReadOrderLine.SETRANGE("Table No.", ITableNo);
      IF BudgetRateReadOrderLine.FINDFIRST THEN
        EXIT;

      BudgetRateReadOrderLine.INIT;
      BudgetRateReadOrderLine."Budget Rate Type" := IBudgetRateType;
      BudgetRateReadOrderLine."Line No." := LineNo;
      BudgetRateReadOrderLine."Table No." := ITableNo;
      BudgetRateReadOrderLine.INSERT;
    END;

    PROCEDURE WriteInitialReadOrder@1100485008(BudgetRateType@1100485002 : Option;TableNo@1100485001 : Integer;VAR IONewReadOrder@1100485003 : Integer);
    VAR
      BudgetRateReadOrderLine@1100525000 : Record 11071894;
    BEGIN
      BudgetRateReadOrderLine.SETRANGE("Budget Rate Type", BudgetRateType);
      BudgetRateReadOrderLine.SETRANGE("Table No.", TableNo);
      BudgetRateReadOrderLine.FINDFIRST;
      IF BudgetRateReadOrderLine."Read Order" = 0 THEN BEGIN
        BudgetRateReadOrderLine."Read Order" := IONewReadOrder;
        BudgetRateReadOrderLine.MODIFY;
        IONewReadOrder += 1;
      END;
    END;

    PROCEDURE EditBudgetRate@1100485005();
    VAR
      BudgetRate@1100485020 : Record 11012000;
      ProjectBudgetRate@1100485019 : Record 11020231;
      DepartmentBudgetRate@1100528600 : Record 11071895;
    BEGIN
      CASE "Table No." OF
        DATABASE::"Budget Rate": PAGE.RUNMODAL(PAGE::"Budget Rates", BudgetRate);
        DATABASE::"Project Budget Rate": PAGE.RUNMODAL(PAGE::"Project Budget Rates", ProjectBudgetRate);
        DATABASE::"Department Budget Rate": PAGE.RUNMODAL(PAGE::"Department Budget Rates", DepartmentBudgetRate);
      END;
    END;

    PROCEDURE CheckBudgetRateInReadOrder@1100485004(ITableNo@1100485000 : Integer);
    VAR
      BudgetRateReadOrderLine@1100528600 : Record 11071894;
      RecRef@1100485002 : RecordRef;
    BEGIN
      BudgetRateReadOrderLine.RESET;
      BudgetRateReadOrderLine.SETFILTER("Read Order", '1..');
      BudgetRateReadOrderLine.SETRANGE("Table No.", ITableNo);
      IF BudgetRateReadOrderLine.ISEMPTY THEN BEGIN
        RecRef.OPEN(ITableNo);
        MESSAGE(Text002, RecRef.CAPTION);
      END;
    END;

    BEGIN
    END.
  }
}

