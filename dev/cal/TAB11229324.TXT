OBJECT Table 11229324 Linked Resource
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               LinkedResource@1100529000 : Record 11229324;
             BEGIN
               IF "Resource No." <> '' THEN BEGIN
                 LinkedResource.SETRANGE("Main Resource No.", "Main Resource No.");
                 LinkedResource.SETRANGE("Operates Plant", "Resource No.");
                 LinkedResource.MODIFYALL("Operates Plant", '');
               END;
             END;

    OnRename=VAR
               LinkedResource@1100529000 : Record 11229324;
             BEGIN
               IF xRec."Resource No." <> '' THEN BEGIN
                 LinkedResource.SETRANGE("Main Resource No.", "Main Resource No.");
                 LinkedResource.SETRANGE("Operates Plant", "Resource No.");
                 IF LinkedResource.FINDFIRST THEN
                   LinkedResource.TESTFIELD("Operates Plant", '');
               END;
             END;

    CaptionML=ENU=Linked Resource;
  }
  FIELDS
  {
    { 10  ;   ;Main Resource No.   ;Code20        ;TableRelation="Resource (WOP)";
                                                   OnValidate=BEGIN
                                                                ChecResourceNotLinkWOToWOLeadingResource("Main Resource No.");
                                                              END;

                                                   CaptionML=ENU=Main Resource No. }
    { 20  ;   ;Resource No.        ;Code20        ;TableRelation="Resource (WOP)";
                                                   OnValidate=VAR
                                                                LinkedResource@1100529000 : Record 11229324;
                                                              BEGIN
                                                                IF "Resource No." = "Main Resource No." THEN
                                                                  FIELDERROR("Resource No.", STRSUBSTNO(Text001, "Resource No.", FIELDCAPTION("Main Resource No.")));
                                                                LinkedResource.SETRANGE("Main Resource No.", "Resource No.");
                                                                IF NOT LinkedResource.ISEMPTY THEN
                                                                  FIELDERROR("Resource No.", STRSUBSTNO(Text002, "Resource No.", FIELDCAPTION("Main Resource No.")));
                                                                LinkedResource.RESET;
                                                                LinkedResource.SETCURRENTKEY("Resource No.");
                                                                LinkedResource.SETRANGE("Resource No.", "Resource No.");
                                                                LinkedResource.SETFILTER("Main Resource No.", '<>%1', "Main Resource No.");
                                                                IF LinkedResource.FINDFIRST THEN
                                                                  FIELDERROR("Resource No.", STRSUBSTNO(Text003, "Resource No.", FIELDCAPTION("Main Resource No."), LinkedResource."Main Resource No."));
                                                                LinkedResource.RESET;
                                                                LinkedResource.SETCURRENTKEY("Resource No.");
                                                                LinkedResource.SETRANGE("Resource No.", "Main Resource No.");
                                                                IF LinkedResource.FINDFIRST THEN
                                                                  FIELDERROR("Main Resource No.", STRSUBSTNO(Text004,
                                                                    "Main Resource No.", LinkedResource.TABLECAPTION, FIELDCAPTION("Main Resource No."), LinkedResource."Main Resource No."));
                                                                ChecResourceNotLinkWOToWOLeadingResource("Resource No.");

                                                                CALCFIELDS("Resource Name", "Resource Type", "Resource Company");
                                                                IF (NOT ("Resource Type" IN ["Resource Type"::Employee, "Resource Type"::Subcontractor])) AND ("Operates Plant" <> '') THEN
                                                                  "Operates Plant" := '';
                                                              END;

                                                   CaptionML=[ENU=Resource No.;
                                                              NOR=Ressursnr.;
                                                              SVE=Resursnr];
                                                   NotBlank=Yes }
    { 30  ;   ;Resource Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Name WHERE (No.=FIELD(Resource No.)));
                                                   CaptionML=[ENU=Resource Name;
                                                              NOR=Ressursnavn;
                                                              SVE=Resursnamn];
                                                   Editable=No }
    { 31  ;   ;Resource Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Type WHERE (No.=FIELD(Resource No.)));
                                                   CaptionML=[ENU=Resource Type;
                                                              NOR=Ressurstype;
                                                              SVE=Resurstyp];
                                                   OptionCaptionML=[ENU=" ,Employee,Subontractor,Plant";
                                                                    SVE=" ,Anst„lld,Underentrepren”r,Maskin"];
                                                   OptionString=[ ,Employee,Subcontractor,Plant];
                                                   Editable=No }
    { 32  ;   ;Resource Company    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Company WHERE (No.=FIELD(Resource No.)));
                                                   TableRelation=Company;
                                                   CaptionML=[ENU=Resource Company;
                                                              SVE=F”retag resurs];
                                                   Editable=No }
    { 40  ;   ;Chargeable (default);Boolean       ;CaptionML=ENU=Chargeable (default) }
    { 50  ;   ;Operates Plant      ;Code20        ;TableRelation="Resource (WOP)";
                                                   OnValidate=VAR
                                                                LinkedResource@1100529000 : Record 11229324;
                                                              BEGIN
                                                                IF "Operates Plant" <> '' THEN BEGIN
                                                                  TESTFIELD("Resource No.");
                                                                  CALCFIELDS("Resource Type");
                                                                  IF NOT ("Resource Type" IN ["Resource Type"::Employee, "Resource Type"::Subcontractor]) THEN
                                                                    FIELDERROR("Resource Type");
                                                                  IF "Operates Plant" = "Main Resource No." THEN BEGIN
                                                                    CALCFIELDS("Main Resource Type");
                                                                    TESTFIELD("Main Resource Type", "Main Resource Type"::Plant);
                                                                  END ELSE BEGIN
                                                                    LinkedResource.GET("Main Resource No.", "Operates Plant");
                                                                    LinkedResource.CALCFIELDS("Resource Type");
                                                                    LinkedResource.TESTFIELD("Resource Type", "Resource Type"::Plant);
                                                                    LinkedResource.TESTFIELD("Main Resource No.", "Main Resource No.");
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              LinkedResource@1100529000 : Record 11229324;
                                                              Resource@1100529001 : Record 11229278;
                                                            BEGIN
                                                              CALCFIELDS("Resource Type");
                                                              IF ("Main Resource No." = '') OR ("Resource No." = '') OR
                                                                 (NOT ("Resource Type" IN ["Resource Type"::Employee, "Resource Type"::Subcontractor]))
                                                              THEN
                                                                EXIT;
                                                              CALCFIELDS("Main Resource Type");
                                                              IF "Main Resource Type" = "Main Resource Type"::Plant THEN BEGIN
                                                                IF Resource.GET("Main Resource No.") THEN
                                                                  Resource.MARK(TRUE)
                                                              END;
                                                              LinkedResource.SETRANGE("Main Resource No.", "Main Resource No.");
                                                              LinkedResource.SETRANGE("Resource Type", "Resource Type"::Plant);
                                                              IF LinkedResource.FINDSET THEN BEGIN
                                                                REPEAT
                                                                  IF Resource.GET(LinkedResource."Resource No.") THEN
                                                                    Resource.MARK(TRUE)
                                                                UNTIL LinkedResource.NEXT = 0;
                                                              END;
                                                              Resource.MARKEDONLY(TRUE);
                                                              Resource."No." := "Operates Plant";
                                                              IF PAGE.RUNMODAL(0, Resource) = ACTION::LookupOK THEN
                                                                VALIDATE("Operates Plant", Resource."No.");
                                                            END;

                                                   CaptionML=ENU=Operates Plant }
    { 60  ;   ;Main Resource Type  ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Type WHERE (No.=FIELD(Main Resource No.)));
                                                   CaptionML=ENU=Main Resource Type;
                                                   OptionCaptionML=[ENU=" ,Employee,Subontractor,Plant";
                                                                    SVE=" ,Anst„lld,Underentrepren”r,Maskin"];
                                                   OptionString=[ ,Employee,Subcontractor,Plant];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Main Resource No.,Resource No.          ;Clustered=Yes }
    {    ;Resource No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529000 : TextConst 'ENU=(%1) may not be equal to %2';
      Text002@1100529001 : TextConst 'ENU=(%1) is a %2';
      Text003@1100529002 : TextConst 'ENU=(%1) is already present on %2 ''%3''';
      Text004@1100529003 : TextConst 'ENU=(%1) is already used as %2 on %3 ''%4''';

    LOCAL PROCEDURE ChecResourceNotLinkWOToWOLeadingResource@1100525009(ResourceNo@1100525001 : Code[20]);
    VAR
      Resource@1100525000 : Record 11229278;
    BEGIN
      IF ResourceNo <> '' THEN BEGIN
        IF Resource.GET(ResourceNo) THEN
          Resource.TESTFIELD("Link WO to WO Leading Resource", FALSE);
      END;
    END;

    BEGIN
    END.
  }
}

