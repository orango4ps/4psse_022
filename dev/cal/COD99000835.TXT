OBJECT Codeunit 99000835 Item Jnl. Line-Reserve
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 337=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Die reservierte Menge kann nicht grî·er als %1 sein;ENU=Reserved quantity cannot be greater than %1;NLD=Gereserveerd aantal kan niet groter dan %1 zijn.;NOR=Reservert antall kan ikke vëre stõrre enn %1;SVE=Reserverat antal kan inte vara stîrre Ñn %1';
      Text002@1001 : TextConst 'DEU=muss ausgefÅllt sein, wenn eine Menge reserviert ist.;ENU=must be filled in when a quantity is reserved;NLD=moet ingevuld worden als er reserveringen zijn.;NOR=mÜ fylles ut nÜr et antall reserveres;SVE=ska fyllas i nÑr ett antal Ñr reserverade';
      Text003@1002 : TextConst 'DEU=darf nicht ausgefÅllt sein, wenn eine Menge reserviert ist;ENU=must not be filled in when a quantity is reserved;NLD=mag niet ingevuld worden als er reserveringen zijn.;NOR=skal ikke fylles ut nÜr et antall reserveres;SVE=fÜr inte fyllas i nÑr ett antal Ñr reserverade';
      Text004@1003 : TextConst 'DEU=darf nicht geÑndert werden, wenn eine Menge reserviert ist;ENU=must not be changed when a quantity is reserved;NLD=mag niet gewijzigd worden als er reserveringen zijn.;NOR=skal ikke endres nÜr et antall reserveres;SVE=fÜr inte Ñndras nÑr ett antal Ñr reserverade';
      Text005@1004 : TextConst 'DEU=Codeunit wurde nicht korrekt initialisiert.;ENU=Codeunit is not initialized correctly.;NLD=Codeunit is niet correct geãnitialiseerd.;NOR=Kodeenheten er ikke riktig initialisert.;SVE=Kodmodul har inte initierats pÜ ett korrekt sÑtt.';
      ReservMgt@1006 : Codeunit 99000845;
      CreateReservEntry@1007 : Codeunit 99000830;
      ReservEngineMgt@1008 : Codeunit 99000831;
      Blocked@1009 : Boolean;
      SetFromType@1010 : ' ,Sales,Requisition Line,Purchase,Item Journal,BOM Journal,Item Ledger Entry,Service,Job';
      SetFromSubtype@1011 : Integer;
      SetFromID@1012 : Code[20];
      SetFromBatchName@1013 : Code[10];
      SetFromProdOrderLine@1014 : Integer;
      SetFromRefNo@1015 : Integer;
      SetFromVariantCode@1016 : Code[10];
      SetFromLocationCode@1017 : Code[10];
      SetFromSerialNo@1019 : Code[50];
      SetFromLotNo@1020 : Code[50];
      SetFromQtyPerUOM@1021 : Decimal;
      Text006@1022 : TextConst 'DEU=Sie kînnen fÅr die %1 %2 keine Artikelverfolgung angeben.;ENU=You cannot define item tracking on %1 %2;NLD=U kunt geen artikeltracering definiâren op %1 %2.;NOR=Du kan ikke definere varesporing i %1 %2;SVE=Du kan inte definiera artikelspÜrning pÜ %1 %2';
      DeleteItemTracking@1005 : Boolean;

    [External]
    PROCEDURE CreateReservation@4(VAR ItemJnlLine@1000 : Record 83;Description@1001 : Text[100];ExpectedReceiptDate@1002 : Date;Quantity@1006 : Decimal;QuantityBase@1003 : Decimal;ForSerialNo@1004 : Code[50];ForLotNo@1005 : Code[50]);
    VAR
      ShipmentDate@1007 : Date;
    BEGIN
      IF SetFromType = 0 THEN
        ERROR(Text005);

      ItemJnlLine.TESTFIELD("Item No.");
      ItemJnlLine.TESTFIELD("Posting Date");
      ItemJnlLine.CALCFIELDS("Reserved Qty. (Base)");
      IF ABS(ItemJnlLine."Quantity (Base)") <
         ABS(ItemJnlLine."Reserved Qty. (Base)") + QuantityBase
      THEN
        ERROR(
          Text000,
          ABS(ItemJnlLine."Quantity (Base)") - ABS(ItemJnlLine."Reserved Qty. (Base)"));

      ItemJnlLine.TESTFIELD("Location Code",SetFromLocationCode);
      ItemJnlLine.TESTFIELD("Variant Code",SetFromVariantCode);

      IF QuantityBase > 0 THEN
        ShipmentDate := ItemJnlLine."Posting Date"
      ELSE BEGIN
        ShipmentDate := ExpectedReceiptDate;
        ExpectedReceiptDate := ItemJnlLine."Posting Date";
      END;

      CreateReservEntry.CreateReservEntryFor(
        DATABASE::"Item Journal Line",
        ItemJnlLine."Entry Type",ItemJnlLine."Journal Template Name",
        ItemJnlLine."Journal Batch Name",0,ItemJnlLine."Line No.",ItemJnlLine."Qty. per Unit of Measure",
        Quantity,QuantityBase,ForSerialNo,ForLotNo);
      CreateReservEntry.CreateReservEntryFrom(
        SetFromType,SetFromSubtype,SetFromID,SetFromBatchName,SetFromProdOrderLine,SetFromRefNo,
        SetFromQtyPerUOM,SetFromSerialNo,SetFromLotNo);
      CreateReservEntry.CreateReservEntry(
        ItemJnlLine."Item No.",ItemJnlLine."Variant Code",ItemJnlLine."Location Code",
        Description,ExpectedReceiptDate,ShipmentDate);

      SetFromType := 0;
    END;

    [External]
    PROCEDURE CreateReservationSetFrom@7(TrackingSpecificationFrom@1000 : Record 336);
    BEGIN
      WITH TrackingSpecificationFrom DO BEGIN
        SetFromType := "Source Type";
        SetFromSubtype := "Source Subtype";
        SetFromID := "Source ID";
        SetFromBatchName := "Source Batch Name";
        SetFromProdOrderLine := "Source Prod. Order Line";
        SetFromRefNo := "Source Ref. No.";
        SetFromVariantCode := "Variant Code";
        SetFromLocationCode := "Location Code";
        SetFromSerialNo := "Serial No.";
        SetFromLotNo := "Lot No.";
        SetFromQtyPerUOM := "Qty. per Unit of Measure";
      END;
    END;

    [External]
    PROCEDURE FilterReservFor@9(VAR FilterReservEntry@1000 : Record 337;ItemJnlLine@1001 : Record 83);
    BEGIN
      FilterReservEntry.SetSourceFilter(
        DATABASE::"Item Journal Line",ItemJnlLine."Entry Type",ItemJnlLine."Journal Template Name",ItemJnlLine."Line No.",FALSE);
      FilterReservEntry.SetSourceFilter2(ItemJnlLine."Journal Batch Name",0);
      FilterReservEntry.SetTrackingFilterFromItemJnlLine(ItemJnlLine);
    END;

    [External]
    PROCEDURE Caption@27(ItemJnlLine@1001 : Record 83) CaptionText@1000 : Text[80];
    BEGIN
      CaptionText :=
        STRSUBSTNO(
          '%1 %2 %3',ItemJnlLine."Journal Template Name",ItemJnlLine."Journal Batch Name",
          ItemJnlLine."Item No.");
    END;

    [External]
    PROCEDURE FindReservEntry@1(ItemJnlLine@1000 : Record 83;VAR ReservEntry@1001 : Record 337) : Boolean;
    BEGIN
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      FilterReservFor(ReservEntry,ItemJnlLine);

      EXIT(ReservEntry.FINDLAST);
    END;

    [External]
    PROCEDURE ReservEntryExist@11(ItemJnlLine@1000 : Record 83) : Boolean;
    VAR
      ReservEntry@1001 : Record 337;
      NSReservEntry@1100528600 : Record 11071900;
    BEGIN
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      FilterReservFor(ReservEntry,ItemJnlLine);
      ReservEntry.ClearTrackingFilter;
      //EXIT(NOT ReservEntry.ISEMPTY); //**4PS.o DP00121
      //**4PS.sn DP00121
      IF NOT ReservEntry.ISEMPTY THEN
        EXIT(TRUE);

      NSReservEntry.SETCURRENTKEY(
        "Source ID","Source Ref. No.","Source Type","Source Subtype",
        "Source Batch Name","Source Prod. Order Line","Reservation Status",
        "Shipment Date","Expected Receipt Date");

      NSReservEntry.SETRANGE("Source Type",DATABASE::"Item Journal Line");
      NSReservEntry.SETRANGE("Source Subtype",ItemJnlLine."Entry Type");
      NSReservEntry.SETRANGE("Source ID",ItemJnlLine."Journal Template Name");
      NSReservEntry.SETRANGE("Source Batch Name",ItemJnlLine."Journal Batch Name");
      NSReservEntry.SETRANGE("Source Prod. Order Line",0);
      NSReservEntry.SETRANGE("Source Ref. No.",ItemJnlLine."Line No.");

      EXIT(NOT NSReservEntry.ISEMPTY);
      //**4PS.en
    END;

    [External]
    PROCEDURE VerifyChange@62(VAR NewItemJnlLine@1000 : Record 83;VAR OldItemJnlLine@1001 : Record 83);
    VAR
      ItemJnlLine@1005 : Record 83;
      TempReservEntry@1002 : Record 337;
      ItemTrackManagement@1006 : Codeunit 6500;
      ShowError@1003 : Boolean;
      HasError@1004 : Boolean;
      SNRequired@1007 : Boolean;
      LNRequired@1008 : Boolean;
      PointerChanged@1009 : Boolean;
    BEGIN
      IF Blocked THEN
        EXIT;
      IF NewItemJnlLine."Line No." = 0 THEN
        IF NOT ItemJnlLine.GET(
             NewItemJnlLine."Journal Template Name",
             NewItemJnlLine."Journal Batch Name",
             NewItemJnlLine."Line No.")
        THEN
          EXIT;

      NewItemJnlLine.CALCFIELDS("Reserved Qty. (Base)");
      ShowError := NewItemJnlLine."Reserved Qty. (Base)" <> 0;
      //**4PS.sn DP00121
      IF NOT ShowError THEN BEGIN
        NewItemJnlLine.CALCFIELDS("Reserved NS Qty. (Base)");
        ShowError := NewItemJnlLine."Reserved NS Qty. (Base)" <> 0;
      END;
      //**4PS.en DP00121

      IF NewItemJnlLine."Posting Date" = 0D THEN
        IF ShowError THEN
          NewItemJnlLine.FIELDERROR("Posting Date",Text002)
        ELSE
          HasError := TRUE;

      IF NewItemJnlLine."Drop Shipment" THEN
        IF ShowError THEN
          NewItemJnlLine.FIELDERROR("Drop Shipment",Text003)
        ELSE
          HasError := TRUE;

      IF NewItemJnlLine."Item No." <> OldItemJnlLine."Item No." THEN
        IF ShowError THEN
          NewItemJnlLine.FIELDERROR("Item No.",Text004)
        ELSE
          HasError := TRUE;

      IF NewItemJnlLine."Entry Type" <> OldItemJnlLine."Entry Type" THEN
        IF ShowError THEN
          NewItemJnlLine.FIELDERROR("Entry Type",Text004)
        ELSE
          HasError := TRUE;

      IF (NewItemJnlLine."Entry Type" = NewItemJnlLine."Entry Type"::Transfer) AND
         (NewItemJnlLine."Quantity (Base)" < 0)
      THEN BEGIN
        IF NewItemJnlLine."New Location Code" <> OldItemJnlLine."Location Code" THEN
          IF ShowError THEN
            NewItemJnlLine.FIELDERROR("New Location Code",Text004)
          ELSE
            HasError := TRUE;
        IF NewItemJnlLine."New Bin Code" <> OldItemJnlLine."Bin Code" THEN BEGIN
          ItemTrackManagement.CheckWhseItemTrkgSetup(NewItemJnlLine."Item No.",SNRequired,LNRequired,FALSE);
          IF SNRequired OR LNRequired THEN
            IF ShowError THEN
              NewItemJnlLine.FIELDERROR("New Bin Code",Text004)
            ELSE
              HasError := TRUE;
        END
      END ELSE BEGIN
        IF NewItemJnlLine."Location Code" <> OldItemJnlLine."Location Code" THEN
          IF ShowError THEN
            NewItemJnlLine.FIELDERROR("Location Code",Text004)
          ELSE
            HasError := TRUE;
        IF (NewItemJnlLine."Bin Code" <> OldItemJnlLine."Bin Code") AND
           (NOT ReservMgt.CalcIsAvailTrackedQtyInBin(
              NewItemJnlLine."Item No.",NewItemJnlLine."Bin Code",
              NewItemJnlLine."Location Code",NewItemJnlLine."Variant Code",
              DATABASE::"Item Journal Line",NewItemJnlLine."Entry Type",
              NewItemJnlLine."Journal Template Name",NewItemJnlLine."Journal Batch Name",
              0,NewItemJnlLine."Line No."))
        THEN BEGIN
          IF ShowError THEN
            NewItemJnlLine.FIELDERROR("Bin Code",Text004);
          HasError := TRUE;
        END;
      END;
      IF NewItemJnlLine."Variant Code" <> OldItemJnlLine."Variant Code" THEN
        IF ShowError THEN
          NewItemJnlLine.FIELDERROR("Variant Code",Text004)
        ELSE
          HasError := TRUE;
      IF NewItemJnlLine."Line No." <> OldItemJnlLine."Line No." THEN
        HasError := TRUE;
      //**4PS.sn DP00121
      IF NewItemJnlLine."Job No." <> OldItemJnlLine."Job No." THEN
        IF ShowError THEN
          NewItemJnlLine.FIELDERROR("Job No.",Text004)
        ELSE
          HasError := TRUE;
      IF NewItemJnlLine."Service Order No." <> OldItemJnlLine."Service Order No." THEN
        IF ShowError THEN
          NewItemJnlLine.FIELDERROR("Service Order No.",Text004)
        ELSE
          HasError := TRUE;
      //**4PS.en

      OnVerifyChangeOnBeforeHasError(NewItemJnlLine,OldItemJnlLine,HasError,ShowError);

      IF HasError THEN BEGIN
        FindReservEntry(NewItemJnlLine,TempReservEntry);
        TempReservEntry.ClearTrackingFilter;

        PointerChanged := (NewItemJnlLine."Item No." <> OldItemJnlLine."Item No.") OR
          (NewItemJnlLine."Entry Type" <> OldItemJnlLine."Entry Type");

        IF PointerChanged OR
           (NOT TempReservEntry.ISEMPTY)
        THEN BEGIN
          IF PointerChanged THEN BEGIN
            ReservMgt.SetItemJnlLine(OldItemJnlLine);
            ReservMgt.DeleteReservEntries(TRUE,0);
            ReservMgt.SetItemJnlLine(NewItemJnlLine);
          END ELSE BEGIN
            ReservMgt.SetItemJnlLine(NewItemJnlLine);
            ReservMgt.DeleteReservEntries(TRUE,0);
          END;
          ReservMgt.AutoTrack(NewItemJnlLine."Quantity (Base)");
        END;
      END;
    END;

    [External]
    PROCEDURE VerifyQuantity@3(VAR NewItemJnlLine@1001 : Record 83;VAR OldItemJnlLine@1000 : Record 83);
    VAR
      ItemJnlLine@1002 : Record 83;
    BEGIN
      OnBeforeVerifyQuantity(NewItemJnlLine,OldItemJnlLine);

      IF Blocked THEN
        EXIT;

      WITH NewItemJnlLine DO BEGIN
        IF "Line No." = OldItemJnlLine."Line No." THEN
          IF "Quantity (Base)" = OldItemJnlLine."Quantity (Base)" THEN
            EXIT;
        IF "Line No." = 0 THEN
          IF NOT ItemJnlLine.GET("Journal Template Name","Journal Batch Name","Line No.") THEN
            EXIT;
        ReservMgt.SetItemJnlLine(NewItemJnlLine);
        IF "Qty. per Unit of Measure" <> OldItemJnlLine."Qty. per Unit of Measure" THEN
          ReservMgt.ModifyUnitOfMeasure;
        IF "Quantity (Base)" * OldItemJnlLine."Quantity (Base)" < 0 THEN
          ReservMgt.DeleteReservEntries(TRUE,0)
        ELSE
          ReservMgt.DeleteReservEntries(FALSE,"Quantity (Base)");
      END;
    END;

    [External]
    PROCEDURE TransferItemJnlToItemLedgEntry@34(VAR ItemJnlLine@1000 : Record 83;VAR ItemLedgEntry@1001 : Record 32;TransferQty@1002 : Decimal;SkipInventory@1003 : Boolean) : Boolean;
    VAR
      OldReservEntry@1004 : Record 337;
      OldReservEntry2@1005 : Record 337;
      Status@1006 : 'Reservation,Tracking,Surplus,Prospect';
      SkipThisRecord@1007 : Boolean;
    BEGIN
      IF NOT FindReservEntry(ItemJnlLine,OldReservEntry) THEN
        EXIT(FALSE);

      OldReservEntry.Lock;

      ItemLedgEntry.TESTFIELD("Item No.",ItemJnlLine."Item No.");
      ItemLedgEntry.TESTFIELD("Variant Code",ItemJnlLine."Variant Code");
      IF ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Transfer THEN BEGIN
        ItemLedgEntry.TESTFIELD("Location Code",ItemJnlLine."New Location Code");
      END ELSE
        ItemLedgEntry.TESTFIELD("Location Code",ItemJnlLine."Location Code");

      FOR Status := Status::Reservation TO Status::Prospect DO BEGIN
        IF TransferQty = 0 THEN
          EXIT(TRUE);
        OldReservEntry.SETRANGE("Reservation Status",Status);

        IF OldReservEntry.FINDSET THEN
          REPEAT
            OldReservEntry.TESTFIELD("Item No.",ItemJnlLine."Item No.");
            OldReservEntry.TESTFIELD("Variant Code",ItemJnlLine."Variant Code");

            IF SkipInventory THEN
              IF Status < Status::Surplus THEN BEGIN
                OldReservEntry2.GET(OldReservEntry."Entry No.",NOT OldReservEntry.Positive);
                SkipThisRecord := OldReservEntry2."Source Type" = DATABASE::"Item Ledger Entry";
              END ELSE
                SkipThisRecord := FALSE;

            IF NOT SkipThisRecord THEN BEGIN
              IF ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Transfer THEN BEGIN
                IF ItemLedgEntry.Quantity < 0 THEN
                  OldReservEntry.TESTFIELD("Location Code",ItemJnlLine."Location Code");
                CreateReservEntry.SetInbound(TRUE);
              END ELSE
                OldReservEntry.TESTFIELD("Location Code",ItemJnlLine."Location Code");

              TransferQty :=
                CreateReservEntry.TransferReservEntry(
                  DATABASE::"Item Ledger Entry",0,'','',0,
                  ItemLedgEntry."Entry No.",ItemLedgEntry."Qty. per Unit of Measure",
                  OldReservEntry,TransferQty);
            END ELSE
              IF Status = Status::Tracking THEN BEGIN
                OldReservEntry2.DELETE;
                OldReservEntry.DELETE;
                ReservMgt.ModifyActionMessage(OldReservEntry."Entry No.",0,TRUE);
              END;
          UNTIL (OldReservEntry.NEXT = 0) OR (TransferQty = 0);
      END; // DO

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE RenameLine@10(VAR NewItemJnlLine@1000 : Record 83;VAR OldItemJnlLine@1001 : Record 83);
    BEGIN
      ReservEngineMgt.RenamePointer(DATABASE::"Item Journal Line",
        OldItemJnlLine."Entry Type",
        OldItemJnlLine."Journal Template Name",
        OldItemJnlLine."Journal Batch Name",
        0,
        OldItemJnlLine."Line No.",
        NewItemJnlLine."Entry Type",
        NewItemJnlLine."Journal Template Name",
        NewItemJnlLine."Journal Batch Name",
        0,
        NewItemJnlLine."Line No.");
    END;

    [External]
    PROCEDURE DeleteLineConfirm@20(VAR ItemJnlLine@1000 : Record 83) : Boolean;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        IF NOT ReservEntryExist(ItemJnlLine) THEN
          EXIT(TRUE);

        ReservMgt.SetItemJnlLine(ItemJnlLine);
        IF ReservMgt.DeleteItemTrackingConfirm THEN
          DeleteItemTracking := TRUE;
      END;

      EXIT(DeleteItemTracking);
    END;

    [External]
    PROCEDURE DeleteLine@2(VAR ItemJnlLine@1000 : Record 83);
    BEGIN
      IF Blocked THEN
        EXIT;

      WITH ItemJnlLine DO BEGIN
        ReservMgt.SetItemJnlLine(ItemJnlLine);
        IF DeleteItemTracking THEN
          ReservMgt.SetItemTrackingHandling(1); // Allow Deletion
        ReservMgt.DeleteReservEntries(TRUE,0);
        ReservMgt.DeleteNSReservEntries(TRUE,0); //**4PS.n DP00121
        CALCFIELDS("Reserved Qty. (Base)");
      END;
    END;

    [External]
    PROCEDURE AssignForPlanning@5(VAR ItemJnlLine@1000 : Record 83);
    VAR
      PlanningAssignment@1001 : Record 99000850;
    BEGIN
      IF ItemJnlLine."Item No." <> '' THEN
        WITH ItemJnlLine DO BEGIN
          PlanningAssignment.ChkAssignOne("Item No.","Variant Code","Location Code","Posting Date");
          IF "Entry Type" = "Entry Type"::Transfer THEN
            PlanningAssignment.ChkAssignOne("Item No.","Variant Code","New Location Code","Posting Date");
        END;
    END;

    [External]
    PROCEDURE Block@6(SetBlocked@1000 : Boolean);
    BEGIN
      Blocked := SetBlocked;
    END;

    [External]
    PROCEDURE CallItemTracking@14(VAR ItemJnlLine@1000 : Record 83;IsReclass@1003 : Boolean);
    VAR
      TrackingSpecification@1001 : Record 336;
      ReservEntry@1004 : Record 337;
      ItemTrackingLines@1002 : Page 6510;
    BEGIN
      ItemJnlLine.TESTFIELD("Item No.");
      IF NOT ItemJnlLine.ItemPosting THEN BEGIN
        ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
        FilterReservFor(ReservEntry,ItemJnlLine);
        ReservEntry.ClearTrackingFilter;
        IF ReservEntry.ISEMPTY THEN
          ERROR(Text006,ItemJnlLine.FIELDCAPTION("Operation No."),ItemJnlLine."Operation No.");
      END;
      TrackingSpecification.InitFromItemJnlLine(ItemJnlLine);
      IF IsReclass THEN
        ItemTrackingLines.SetFormRunMode(1);

      //**4PS.sn DP00121
      IF (ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Purchase) AND
         ((ItemJnlLine."Job No." + ItemJnlLine."Service Order No.") <> '')
      THEN
        ItemTrackingLines.SetExchangeStockNonStock(
          TRUE,ItemJnlLine."Job No.",ItemJnlLine."Service Order No.");
      IF (ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Sale) AND (ItemJnlLine."Quantity (Base)" < 0) AND
         ((ItemJnlLine."Job No." + ItemJnlLine."Service Order No.") <> '')
      THEN BEGIN
        ItemTrackingLines.SetExchangeStockNonStock(TRUE, ItemJnlLine."Job No.", ItemJnlLine."Service Order No.");
        ItemTrackingLines.SetNonStockNegSaleItmJnl(TRUE);
      END;
      //**4PS.en

      ItemTrackingLines.SetSourceSpec(TrackingSpecification,ItemJnlLine."Posting Date");
      ItemTrackingLines.SetInbound(ItemJnlLine.IsInbound);
      ItemTrackingLines.RUNMODAL;
    END;

    [External]
    PROCEDURE RegisterBinContentItemTracking@1203(VAR ItemJournalLine@1003 : Record 83;VAR TempTrackingSpecification@1200 : TEMPORARY Record 336);
    VAR
      SourceTrackingSpecification@1202 : Record 336;
      ItemTrackingLines@1004 : Page 6510;
      FormRunMode@1201 : ',Reclass,Combined Ship/Rcpt,Drop Shipment,Transfer';
    BEGIN
      IF NOT TempTrackingSpecification.FINDSET THEN
        EXIT;
      SourceTrackingSpecification.InitFromItemJnlLine(ItemJournalLine);

      CLEAR(ItemTrackingLines);
      ItemTrackingLines.SetFormRunMode(FormRunMode::Reclass);
      ItemTrackingLines.RegisterItemTrackingLines(
        SourceTrackingSpecification,ItemJournalLine."Posting Date",TempTrackingSpecification);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeVerifyQuantity@8(VAR NewItemJournalLine@1000 : Record 83;OldItemJournalLine@1001 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnVerifyChangeOnBeforeHasError@21(NewItemJnlLine@1000 : Record 83;OldItemJnlLine@1001 : Record 83;VAR HasError@1002 : Boolean;VAR ShowError@1003 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

