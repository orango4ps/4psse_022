OBJECT Report 11012123 Create Purch. Reminders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Erstellen Einkaufsmahnung;
               ENU=Create Purch. Reminders;
               NLD=Aanmaken inkoop herinnering;
               NOR=Opprette innkj›psp†minnelser;
               SVE=Skapa ink”p P†minnelser];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               OnPreDataItem=BEGIN
                               IF ReminderHeaderReq."Reminder Date" = 0D THEN
                                 ERROR(Text000,ReminderHeaderReq.FIELDCAPTION("Reminder Date"));

                               "Purchase Header".FILTERGROUP := 2;
                               SETFILTER("Reminder Terms Code",'<>%1','');
                               SETFILTER("Document Type",'%1|%2', "Document Type"::Quote, "Document Type"::Order);
                               "Purchase Header".FILTERGROUP := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  CASE "Document Type" OF
                                    "Document Type"::Quote:
                                      IF "Quote Receipt Date"  > 0D THEN
                                        CurrReport.SKIP;
                                    "Document Type"::Order:
                                      IF "Return Date Signed Contract" > 0D THEN
                                        CurrReport.SKIP;
                                  END;

                                  ReminderTerms.GET("Reminder Terms Code");

                                  ReminderHeader.SETRANGE("Document Type", "Document Type");
                                  ReminderHeader.SETRANGE("Document No.", "No.");
                                  IF ReminderHeader.FIND('+') THEN BEGIN
                                    ReminderDate := ReminderHeader."Reminder Date";
                                    ReminderLevel := ReminderHeader."Reminder Level" + 1;
                                  END ELSE BEGIN
                                    ReminderDate := "Purchase Header"."Document Date";
                                    ReminderLevel := 1;
                                  END;

                                  IF (ReminderLevel > ReminderTerms."Max. No. of Reminders") AND (ReminderTerms."Max. No. of Reminders" <> 0) THEN
                                    CurrReport.SKIP;

                                  ReminderLevelRec.SETRANGE("Reminder Terms Code","Reminder Terms Code");
                                  ReminderLevelRec.SETRANGE("No.",1,ReminderLevel);
                                  IF NOT ReminderLevelRec.FIND('+') THEN
                                    ReminderLevelRec.INIT;

                                  IF CALCDATE(ReminderLevelRec."Grace Period",ReminderDate) >= ReminderHeaderReq."Reminder Date" THEN
                                    CurrReport.SKIP;

                                  ReminderLine.LOCKTABLE;
                                  ReminderHeader.LOCKTABLE;

                                  ReminderHeader.INIT;
                                  ReminderHeader."Document Type" := "Document Type";
                                  ReminderHeader."Document No." := "No.";
                                  ReminderHeader."Reminder Level" := ReminderLevel;
                                  ReminderHeader."Reminder Date" := ReminderHeaderReq."Reminder Date";
                                  ReminderHeader.INSERT;

                                  ReminderTextLine.SETRANGE("Reminder Terms Code", "Reminder Terms Code");
                                  ReminderTextLine.SETRANGE("Reminder Level", ReminderLevel);
                                  LineNo := 0;
                                  IF ReminderTextLine.FIND('-') THEN
                                    REPEAT
                                      ReminderLine.INIT;
                                      LineNo := LineNo + 10000;
                                      ReminderLine."Document Type" := "Document Type";
                                      ReminderLine."Document No." := "No.";
                                      ReminderLine."Reminder Level" := ReminderLevel;
                                      ReminderLine."Line No." := LineNo;
                                      ReminderLine.Description := ReminderTextLine.Text;
                                      ReminderLine.INSERT;
                                    UNTIL ReminderTextLine.NEXT = 0;;
                                END;

               ReqFilterFields=Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF ReminderHeaderReq."Reminder Date" = 0D THEN
                     ReminderHeaderReq."Reminder Date" := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Mahndatum;
                             ENU=Reminder Date;
                             NLD=Herinneringsdatum;
                             NOR=P†minnelsedato;
                             SVE=P†minnelsedatum];
                  SourceExpr=ReminderDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ReminderHeaderReq@1210190000 : Record 11020212;
      Text000@1210190001 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified.;NLD=%1 moet opgegeven worden.;NOR=%1 m† spesifiseres.;SVE=%1 m†ste specificeras.';
      ReminderHeader@1210190003 : Record 11020212;
      ReminderLine@1210190002 : Record 11020213;
      ReminderTerms@1210190004 : Record 11020209;
      ReminderTextLine@1210190008 : Record 11020211;
      ReminderLevelRec@1210190007 : Record 11020210;
      ReminderDate@1210190005 : Date;
      ReminderLevel@1210190006 : Integer;
      LineNo@1210190009 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

