OBJECT Table 222 Ship-to Address
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=UPGTK 011 to 022;
  }
  PROPERTIES
  {
    DataCaptionFields=Customer No.,Name,Code;
    OnInsert=VAR
               lvShipToAddressRec@1100485000 : Record 222;
             BEGIN
               //**4PS.sn
               IF Code = '' THEN BEGIN
                 lvShipToAddressRec.SETRANGE("Customer No.","Customer No.");
                 IF lvShipToAddressRec.FINDLAST THEN
                   Code := INCSTR(lvShipToAddressRec.Code);
               //C027523.so
               //  ELSE
               //    Code := '001';
               //C027523.eo
               //C027523.sn
                 IF Code = '' THEN
                   Code := '001';
               //C027523.en

                 WHILE lvShipToAddressRec.GET("Customer No.", Code) DO
                   Code := INCSTR(Code);
               END;
               //**4PS.en

               Cust.GET("Customer No.");
               Name := Cust.Name;
               //**4PS.sn
               "Name 2" := Cust."Name 2" ;
               "Derived from Customer Card" := FALSE;
               "Base Calendar" := Cust."Base Calendar Code";
               CentrDataMngmt.EditShipToAddr(Rec,1,xRec);
               //**4PS.en
               "Last Date Modified" := TODAY;
               "Modified by" := USERID;
               "Last Time Modified" := TIME;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               "Modified by" := USERID;
               "Last Time Modified" := TIME;

               //**4PS.sn
               CheckRelatedAddress;  //db, 18-12-07
               CentrDataMngmt.EditShipToAddr(Rec,2,xRec);
               //**4PS.en
             END;

    OnDelete=BEGIN
               //**4PS.sn
               CentrDataMngmt.EditShipToAddr(Rec,4,xRec);
               "Job Address Code" := '';
               UpdateJobLocationRelation();
               //**4PS.en
             END;

    OnRename=VAR
               RentalPackage@1100529500 : Record 11012941;
             BEGIN
               "Last Date Modified" := TODAY;
               //**4PS.sn
               IF (Code <> xRec.Code) AND (xRec.Code <> '') THEN BEGIN
                 RentalPackage.SETRANGE("Customer No.", "Customer No.");
                 RentalPackage.SETRANGE("Alternative Bill-to Address", xRec.Code);
                 IF RentalPackage.FINDFIRST THEN BEGIN
                   IF ("Customer No." <> xRec."Customer No.") THEN
                     RentalPackage.MODIFYALL("Alternative Bill-to Address", '')
                   ELSE
                     RentalPackage.MODIFYALL("Alternative Bill-to Address", Code);
                 END;
               END;
               //**4PS.en
             END;

    CaptionML=[ENU=Address;
               NOR=Lever til-adresse;
               SVE=Leveransadress];
    LookupPageID=Page301;
  }
  FIELDS
  {
    { 1   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr];
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code10        ;OnValidate=BEGIN
                                                                //**4PS.sn
                                                                IF (Code <> xRec.Code) AND (xRec.Code <> '') THEN
                                                                  CentrDataMngmt.EditShipToAddr(Rec,3,xRec);
                                                                //**4PS.en
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=No }
    { 3   ;   ;Name                ;Text100       ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 4   ;   ;Name 2              ;Text50        ;CaptionML=[ENU=Name 2;
                                                              NOR=Navn 2;
                                                              SVE=Namn 2] }
    { 5   ;   ;Address             ;Text100       ;CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress] }
    { 6   ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2;
                                                              SVE=Adress 2] }
    { 7   ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookupPostCode(City,"Post Code",County,"Country/Region Code");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              NOR=Sted;
                                                              SVE=Ort] }
    { 8   ;   ;Contact             ;Text100       ;CaptionML=[ENU=Contact;
                                                              NOR=Kontakt;
                                                              SVE=Kontaktperson] }
    { 9   ;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              NOR=Telefonnr.;
                                                              SVE=Telefonnr] }
    { 10  ;   ;Telex No.           ;Text30        ;CaptionML=[ENU=Telex No.;
                                                              NOR=Teleks;
                                                              SVE=Telexnr] }
    { 30  ;   ;Shipment Method Code;Code10        ;TableRelation="Shipment Method";
                                                   CaptionML=[ENU=Shipment Method/Incoterm Code;
                                                              NOR=Leveringsm†tekode;
                                                              SVE=Utleveransvillkorskod] }
    { 31  ;   ;Shipping Agent Code ;Code10        ;TableRelation="Shipping Agent";
                                                   OnValidate=BEGIN
                                                                IF "Shipping Agent Code" <> xRec."Shipping Agent Code" THEN
                                                                  VALIDATE("Shipping Agent Service Code",'');
                                                              END;

                                                   AccessByPermission=TableData 5790=R;
                                                   CaptionML=[ENU=Shipping Agent Code;
                                                              NOR=Transport›rkode;
                                                              SVE=Spedit”rkod] }
    { 32  ;   ;Place of Export     ;Code20        ;CaptionML=[ENU=Place of Export;
                                                              NOR=Eksportsted;
                                                              SVE=Exportplats] }
    { 35  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   OnValidate=BEGIN
                                                                PostCode.CheckClearPostCodeCityCounty(City,"Post Code",County,"Country/Region Code",xRec."Country/Region Code");
                                                              END;

                                                   CaptionML=[ENU=Country/Region Code;
                                                              NOR=Lands-/omr†dekode;
                                                              SVE=Lands-/regionkod] }
    { 54  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 83  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod] }
    { 84  ;   ;Fax No.             ;Text30        ;CaptionML=[ENU=Fax No.;
                                                              NOR=Faksnr.;
                                                              SVE=Telefaxnr] }
    { 85  ;   ;Telex Answer Back   ;Text20        ;CaptionML=[ENU=Telex Answer Back;
                                                              NOR=Teleks (tilbakesvar);
                                                              SVE=Telexsvarskod] }
    { 90  ;   ;GLN                 ;Code13        ;OnValidate=VAR
                                                                GLNCalculator@1000 : Codeunit 1607;
                                                              BEGIN
                                                                IF GLN <> '' THEN
                                                                  GLNCalculator.AssertValidCheckDigit13(GLN);
                                                              END;

                                                   CaptionML=[ENU=GLN;
                                                              NOR=GLN;
                                                              SVE=GLN] }
    { 91  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookupPostCode(City,"Post Code",County,"Country/Region Code");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr.;
                                                              SVE=Postnr] }
    { 92  ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              NOR=Fylke/delstat;
                                                              SVE=Delstat];
                                                   CaptionClass='5,1,' + "Country/Region Code" }
    { 102 ;   ;E-Mail              ;Text80        ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit 9520;
                                                              BEGIN
                                                                MailManagement.ValidateEmailAddressField("E-Mail");
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Email;
                                                              NOR=E-post;
                                                              SVE=E-post] }
    { 103 ;   ;Home Page           ;Text80        ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Home Page;
                                                              NOR=Hjemmeside;
                                                              SVE=Hemsida] }
    { 108 ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=[ENU=Tax Area Code;
                                                              NOR=Mva-omr†dekode;
                                                              SVE=Skatteomr†deskod] }
    { 109 ;   ;Tax Liable          ;Boolean       ;CaptionML=[ENU=Tax Liable;
                                                              NOR=Skyldig mva.;
                                                              SVE=Skattepliktig] }
    { 5792;   ;Shipping Agent Service Code;Code10 ;TableRelation="Shipping Agent Services".Code WHERE (Shipping Agent Code=FIELD(Shipping Agent Code));
                                                   CaptionML=[ENU=Shipping Agent Service Code;
                                                              NOR=Transport›rservicekode;
                                                              SVE=Spedit”r servicekod] }
    { 5900;   ;Service Zone Code   ;Code10        ;TableRelation="Service Zone";
                                                   CaptionML=[ENU=Service Zone Code;
                                                              NOR=Servicesonekode;
                                                              SVE=Servicezonskod] }
    { 11012000;;Shipping Address   ;Boolean       ;CaptionML=[ENU=Shipping Address;
                                                              NOR=Leveringsadresse;
                                                              SVE=Leveransadress] }
    { 11012010;;Project Address    ;Boolean       ;OnValidate=BEGIN
                                                                //**4PS
                                                                IF (NOT "Project Address") THEN
                                                                  TESTFIELD("Job Address Code", '');
                                                              END;

                                                   CaptionML=[ENU=Project Address;
                                                              NOR=Prosjektadresse;
                                                              SVE=Projektadress] }
    { 11012020;;Billing Address    ;Boolean       ;CaptionML=[ENU=Billing Address;
                                                              NOR=Fakturaadresse;
                                                              SVE=Fakturaadress] }
    { 11012100;;Derived from Customer Card;Boolean;CaptionML=[ENU=Derived from Customer Card;
                                                              NOR=Stammer fra kundekort;
                                                              SVE=H„rledd fr†n kundkort] }
    { 11012110;;Derived from Customer No.;Code20  ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                CheckDerivedFrom;  //**4PS
                                                              END;

                                                   CaptionML=[ENU=Derived from Customer No.;
                                                              NOR=Stammer fra kundenr;
                                                              SVE=H„rledd fr†n kundnr] }
    { 11012120;;Derived from Address Code;Code10  ;TableRelation="Ship-to Address".Code WHERE (Customer No.=FIELD(Derived from Customer No.));
                                                   OnValidate=BEGIN
                                                                CheckDerivedFrom;  //**4PS
                                                              END;

                                                   CaptionML=[ENU=Derived from Address Code;
                                                              NOR=Stammer fra adressekode;
                                                              SVE=H„rledd fr†n adresskod] }
    { 11012200;;Travel Distance    ;Decimal       ;CaptionML=[ENU=Travel Distance;
                                                              NOR=Reiseavstand;
                                                              SVE=Resstr„cka];
                                                   MinValue=0 }
    { 11012210;;Travel Time        ;Decimal       ;CaptionML=[ENU=Travel Time;
                                                              NOR=Reisetid;
                                                              SVE=Restid];
                                                   MinValue=0 }
    { 11012220;;Travel Cost        ;Decimal       ;CaptionML=[ENU=Travel Cost;
                                                              NOR=Reisekost;
                                                              SVE=Resekostnad];
                                                   MinValue=0 }
    { 11012230;;Travel Cost Code   ;Code10        ;TableRelation="Travel Cost Code";
                                                   CaptionML=[ENU=Travel Cost Code;
                                                              NOR=Reisekostkode;
                                                              SVE=Resekostnadskod] }
    { 11012240;;Phone No. at Work  ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No. at Work;
                                                              NOR=Telefon arbeid;
                                                              SVE=Telefonnr, arbete] }
    { 11012250;;Mobile Phone No.   ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Mobile Phone No.;
                                                              NOR=Mobiltelefonnr;
                                                              SVE=Mobiltelefonnr] }
    { 11012260;;Default Main Project;Code20       ;TableRelation=Job.No. WHERE (Single/Main/Sub Project=CONST(Main Project));
                                                   CaptionML=[ENU=Default Main Project;
                                                              NOR=Standardhovedprosjekt;
                                                              SVE=Standardhuvudprojekt] }
    { 11012270;;Estate Owner       ;Code20        ;TableRelation=Contact.No. WHERE (Type=CONST(Company));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Estate Owner Name");  //**4PS
                                                              END;

                                                   CaptionML=[ENU=Estate Owner;
                                                              NOR=Eiendomseier;
                                                              SVE=Fastighets„gare] }
    { 11012280;;Estate Owner Name  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Estate Owner)));
                                                   CaptionML=[ENU=Estate Owner Name;
                                                              NOR=Navn p† eiendomseier;
                                                              SVE=Fastighets„garnamn];
                                                   Editable=No }
    { 11012290;;Estate No.         ;Code20        ;CaptionML=[ENU=Estate No.;
                                                              NOR=Eiendomsnr;
                                                              SVE=Fastighetsnr.] }
    { 11012300;;Building           ;Text30        ;CaptionML=[ENU=Building;
                                                              NOR=Byggning;
                                                              SVE=Byggnad] }
    { 11012310;;Floor              ;Text10        ;CaptionML=[ENU=Floor;
                                                              NOR=Gulv;
                                                              SVE=V†ning] }
    { 11012320;;Department         ;Text30        ;CaptionML=[ENU=Department;
                                                              NOR=Avdeling;
                                                              SVE=Avdelning] }
    { 11012330;;Room               ;Text10        ;CaptionML=[ENU=Room;
                                                              NOR=Rom;
                                                              SVE=Rum] }
    { 11012340;;Urgency Code       ;Code10        ;TableRelation="Urgency Codes by Customer".Urgency WHERE (Customer No.=FIELD(Customer No.));
                                                   CaptionML=[ENU=Urgency Code;
                                                              NOR=Prioritet;
                                                              SVE=Kod f”r angel„genhetsgrad] }
    { 11012350;;Area Team          ;Text30        ;CaptionML=[ENU=Area Team;
                                                              NOR=Omr†deteam;
                                                              SVE=Omr†desteam] }
    { 11012360;;Phone No. Area Team;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No. Area Team;
                                                              NOR=Telefon omr†deteam;
                                                              SVE=Telefonnr, omr†desteam] }
    { 11012370;;Base Calendar      ;Code10        ;TableRelation="Base Calendar";
                                                   CaptionML=[ENU=Base Calendar;
                                                              NOR=Basiskalender;
                                                              SVE=Baskalender] }
    { 11012380;;Modified by        ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=Žndrad av];
                                                   Editable=No }
    { 11012390;;Copy Contract Main Project;Boolean;CaptionML=[ENU=Copy Contract Main Project;
                                                              NOR=Kopier kontraktens hovedprosjekt;
                                                              SVE=Kopiera kontraktets huvudprojekt] }
    { 11012400;;Change address by Project;Boolean ;InitValue=Yes;
                                                   CaptionML=[ENU=Change address by Project;
                                                              NOR=Endre adresse etter prosjekt;
                                                              SVE=Žndra adress efter projekt] }
    { 11012750;;E-Mail (Invoices)  ;Text80        ;OnValidate=VAR
                                                                RecRef@1100528701 : RecordRef;
                                                                FldRef@1100528700 : FieldRef;
                                                              BEGIN
                                                                //**4PS.sn
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(FIELDNO("E-Mail (Invoices)"));
                                                                ContactDataValidation.ValidateEmail(FldRef, "E-Mail (Invoices)");
                                                                //**4PS.en
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Email (Invoices);
                                                              SVE=E-post (Fakturor)];
                                                   Description=Email address for sending electronic invoices to [jhoek] }
    { 11012760;;E-Mail (Reminders) ;Text80        ;OnValidate=VAR
                                                                RecRef@1100528701 : RecordRef;
                                                                FldRef@1100528700 : FieldRef;
                                                              BEGIN
                                                                //**4PS.sn
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(FIELDNO("E-Mail (Reminders)"));
                                                                ContactDataValidation.ValidateEmail(FldRef, "E-Mail (Reminders)");
                                                                //**4PS.en
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Email (Reminders);
                                                              SVE=E-post (P†minnelser)] }
    { 11012770;;E-Mail (Shipments) ;Text80        ;OnValidate=VAR
                                                                RecRef@1100528701 : RecordRef;
                                                                FldRef@1100528700 : FieldRef;
                                                              BEGIN
                                                                //**4PS.sn
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(FIELDNO("E-Mail (Shipments)"));
                                                                ContactDataValidation.ValidateEmail(FldRef, "E-Mail (Shipments)");
                                                                //**4PS.en
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Email (Shipments);
                                                              SVE=E-post (Leveranser)] }
    { 11012780;;E-Mail (Order Confirmations);Text80;
                                                   OnValidate=VAR
                                                                RecRef@1100528701 : RecordRef;
                                                                FldRef@1100528700 : FieldRef;
                                                              BEGIN
                                                                //**4PS.sn
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(FIELDNO("E-Mail (Order Confirmations)"));
                                                                ContactDataValidation.ValidateEmail(FldRef, "E-Mail (Order Confirmations)");
                                                                //**4PS.en
                                                              END;

                                                   CaptionML=[ENU=Email (Order Confirmations);
                                                              SVE=E-post (Orderbekr.)] }
    { 11012920;;Job Address Code   ;Code20        ;OnValidate=BEGIN
                                                                //**4PS
                                                                IF ("Job Address Code" <> '') THEN BEGIN
                                                                  TESTFIELD("Project Address", TRUE);
                                                                  CheckJobLocationUnique();
                                                                END;
                                                                UpdateJobLocationRelation();
                                                              END;

                                                   CaptionML=[ENU=Job Address Code;
                                                              NOR=Prosjektadressekode;
                                                              SVE=Jobbadresskod] }
    { 11012930;;Contact No.        ;Code20        ;TableRelation=Contact.No.;
                                                   OnLookup=VAR
                                                              ContactBusinessRelation@1100527300 : Record 5054;
                                                              ContactPerson@1100527301 : Record 5050;
                                                            BEGIN
                                                              //**4PS
                                                              ContactBusinessRelation.SETRANGE("Link to Table", ContactBusinessRelation."Link to Table"::Customer);
                                                              ContactBusinessRelation.SETRANGE("No.", "Customer No.");
                                                              IF ContactBusinessRelation.FINDFIRST THEN BEGIN
                                                                ContactPerson.SETRANGE("Company No.", ContactBusinessRelation."Contact No.");
                                                                ContactPerson.SETRANGE(Type, ContactPerson.Type::Person);
                                                                IF PAGE.RUNMODAL(PAGE::"Contact List", ContactPerson) = ACTION::LookupOK THEN BEGIN
                                                                  "Contact No." := ContactPerson."No.";
                                                                  Contact := ContactPerson.Name;
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Contact No.;
                                                              NOR=Kontaktnr.;
                                                              SVE=Kontaktnr] }
    { 11128000;;UPGGLN No.         ;Code30        ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=GLN No.;
                                                              NOR=GLN-nr.;
                                                              SVE=GLN-nr] }
    { 11128001;;Last Time Modified ;Time          ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Last Time Modified;
                                                              NOR=Sist endret;
                                                              SVE=Uppdateringstid] }
    { 11250717;;EDI Code           ;Code10        ;CaptionML=[ENU=EDI Code;
                                                              SVE=EDI-kod];
                                                   Description=4PSFI_FINVOICE }
    { 11250720;;VAT Registration No.;Text30       ;CaptionML=SVE=Momsregistreringsnr;
                                                   Description=4PSFI_FINVOICE }
  }
  KEYS
  {
    {    ;Customer No.,Code                       ;Clustered=Yes }
    {    ;Address                                  }
    {    ;City,Address                             }
    {    ;Job Address Code                         }
    {    ;Derived from Customer No.,Derived from Address Code }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Name,Address,Address 2,City,Post Code,Shipping Address }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=untitled;NOR=uten navn;SVE=namnl”s';
      Cust@1001 : Record 18;
      PostCode@1002 : Record 225;
      Text001@1003 : TextConst 'ENU=Before you can use Online Map, you must fill in the Online Map Setup window.\See Setting Up Online Map in Help.;NOR=Du m† fylle ut vinduet Oppsett av Online Map f›r du kan bruke Online Map.\Se Konfigurere Online Map i Hjelp.;SVE=Innan du kan anv„nda Online Map m†ste du fylla i f”nstret Online Map - inst„llning.\Se Konfiguration av Online Map i hj„lpen';
      CentrDataMngmt@1210190002 : Codeunit 11012010;
      Text11012001@1100485000 : TextConst 'ENU=%1 ''%2'' is already present on Customer No. ''%3'', Address Code ''%4''.;NOR=%1 ''%2'' finnes allerede p† kundenr ''%3'', adressekode ''%4''.;SVE=%1 ''%2'' finns redan p† kundnr ''%3'', adresskod ''%4''.';
      Text11012002@1100525000 : TextConst 'ENU=c/o;NOR=c/o;SVE=c/o';
      ContactDataValidation@1100528700 : Codeunit 11012275;

    [External]
    PROCEDURE Caption@1() : Text;
    BEGIN
      IF "Customer No." = '' THEN
        EXIT(Text000);
      Cust.GET("Customer No.");
      EXIT(STRSUBSTNO('%1 %2 %3 %4',Cust."No.",Cust.Name,Code,Name));
    END;

    [External]
    PROCEDURE DisplayMap@8();
    VAR
      MapPoint@1001 : Record 800;
      MapMgt@1000 : Codeunit 802;
    BEGIN
      IF MapPoint.FINDFIRST THEN
        MapMgt.MakeSelection(DATABASE::"Ship-to Address",GETPOSITION)
      ELSE
        MESSAGE(Text001);
    END;

    [External]
    PROCEDURE GetFilterCustNo@2() : Code[20];
    BEGIN
      IF GETFILTER("Customer No.") <> '' THEN
        IF GETRANGEMIN("Customer No.") = GETRANGEMAX("Customer No.") THEN
          EXIT(GETRANGEMAX("Customer No."));
    END;

    PROCEDURE UpdateLocations@1100485000(lvCust@1100485002 : Code[20];lvCode@1100485005 : Code[20]);
    VAR
      ServLocRec@1100485000 : Record 11012801;
      ProjRec@1100485001 : Record 11072003;
      lvText000@1100485003 : TextConst 'ENU="Do you want to update related Project en Service Locations with changed Address Data ? ";NOR=Vil du oppdatere relatert prosjekt og servicelokasjoner med endrede adressedata?;SVE="Vill du uppdatera relaterade projekt och serviceplatser med „ndrade adressdata? "';
      AdrsRec@1100485004 : Record 222;
      FPSLicenseManagement@1100528500 : Codeunit 11229289;
    BEGIN
      //**4PS
      IF NOT CONFIRM(lvText000, FALSE) THEN EXIT;

      AdrsRec.RESET;
      AdrsRec.SETRANGE("Customer No.", lvCust);
      IF lvCode <> '' THEN
        AdrsRec.SETRANGE(Code, lvCode);
      AdrsRec.SETRANGE("Project Address", TRUE);
      IF AdrsRec.FINDSET THEN BEGIN
        REPEAT
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(ServLocRec) THEN BEGIN
            ServLocRec.SETCURRENTKEY("Customer No.");
            ServLocRec.SETRANGE("Customer No.", AdrsRec."Customer No.");
            ServLocRec.SETRANGE("Address Code", AdrsRec.Code);
            IF ServLocRec.FINDSET(TRUE,FALSE) THEN BEGIN
              REPEAT
                ServLocRec.ValidateAddressData(2);
                ServLocRec.MODIFY;
              UNTIL ServLocRec.NEXT = 0;
            END;
          END;

          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(ProjRec) THEN BEGIN
            ProjRec.SETCURRENTKEY("Bill-to Customer No.");
            ProjRec.SETRANGE("Bill-to Customer No.", AdrsRec."Customer No.");
            ProjRec.SETRANGE("Project Address Code", AdrsRec.Code);
            IF ProjRec.FINDSET(TRUE, FALSE) THEN BEGIN
              REPEAT
                ProjRec.Address := '';  //skip question
                ProjRec.UpdateProjectAddress;
                ProjRec.MODIFY;
              UNTIL ProjRec.NEXT = 0;
            END;
          END;
        UNTIL AdrsRec.NEXT = 0;
      END;
    END;

    PROCEDURE CheckJobLocationUnique@1100485001();
    VAR
      lvAddressRec@1100485000 : Record 222;
    BEGIN
      //**4PS
      IF ("Job Address Code" = '') THEN
        EXIT;

      lvAddressRec.SETCURRENTKEY("Job Address Code");
      lvAddressRec.SETRANGE("Job Address Code", "Job Address Code");
      IF lvAddressRec.FINDSET THEN BEGIN
        REPEAT
          IF (lvAddressRec."Customer No." <> "Customer No.") OR (lvAddressRec.Code <> Code) THEN
            ERROR(Text11012001,
              FIELDCAPTION("Job Address Code"),"Job Address Code",lvAddressRec."Customer No.",lvAddressRec.Code);
        UNTIL lvAddressRec.NEXT = 0;
      END;
    END;

    PROCEDURE UpdateJobLocationRelation@1100485002();
    VAR
      lvJobLocRec@1100485000 : Record 11020249;
    BEGIN
      //**4PS
      lvJobLocRec.SETCURRENTKEY("Customer No.", "Address Code");
      lvJobLocRec.SETRANGE("Customer No.", "Customer No.");
      lvJobLocRec.SETRANGE("Address Code", Code);
      IF lvJobLocRec.FINDFIRST THEN BEGIN
        IF ("Job Address Code" = '') OR (NOT "Project Address") THEN
          lvJobLocRec.DELETE
        ELSE BEGIN
          IF (lvJobLocRec."Job Location Code" <> "Job Address Code") THEN
            lvJobLocRec.RENAME("Job Address Code");
        END;
      END ELSE BEGIN
        IF ("Job Address Code" <> '') AND ("Project Address") THEN BEGIN
          lvJobLocRec.INIT;
          lvJobLocRec."Job Location Code" := "Job Address Code";
          lvJobLocRec."Customer No." := "Customer No.";
          lvJobLocRec."Address Code" := Code;
          lvJobLocRec.INSERT;
        END;
      END;
    END;

    PROCEDURE CheckAvailableAddress@1100485018(lvCust@1100485003 : Code[20];lvType@1100485002 : Integer) : Text[30];
    VAR
      AdrsRec@1100485001 : Record 222;
      NumAdrs@1100485000 : Integer;
    BEGIN
      //**4PS
      AdrsRec.RESET;
      AdrsRec.SETRANGE("Customer No.", lvCust);
      IF lvType = 1 THEN
        AdrsRec.SETRANGE("Shipping Address", TRUE);
      IF lvType = 2 THEN
        AdrsRec.SETRANGE("Project Address", TRUE);
      IF lvType = 3 THEN
        AdrsRec.SETRANGE("Billing Address", TRUE);
      NumAdrs := AdrsRec.COUNT;  //db, 20-09-07
      IF NumAdrs = 0 THEN
        EXIT('')
      ELSE
        EXIT(STRSUBSTNO('[%1]', NumAdrs));
    END;

    PROCEDURE GetRelatedAddress@1100485003(VAR gvAdrsRec@1100485002 : Record 222;lvRead@1100485004 : Boolean;lvCustRec@1100485001 : Record 18;lvAdrsRec@1100485000 : Record 222);
    VAR
      lvText001@1100485003 : TextConst 'ENU=Recursion;NOR=Rekursjon;SVE=Rekursion';
      lvCustRec2@1100485005 : Record 18;
    BEGIN
      //**4PS
      //db, 18-12-07: purpose is to save a project address of a customer (read: owner Service Location)
      //that should act as invoice address of the customer (read: Contract Owner) to define Name/Address/City on invoice

      WITH gvAdrsRec DO BEGIN
        IF ("Derived from Customer No." = '') AND ("Derived from Address Code" = '') THEN EXIT;

        IF ("Derived from Address Code" <> '') THEN BEGIN
          IF lvRead = TRUE THEN BEGIN
            IF ("Derived from Customer No." = "Customer No.") AND ("Derived from Address Code" = Code) THEN
              ERROR(lvText001);
            IF NOT lvAdrsRec.GET("Derived from Customer No.", "Derived from Address Code") THEN
              EXIT;
          END;
          Name := lvAdrsRec.Name;
          "Name 2" := lvAdrsRec."Name 2";
          Address := lvAdrsRec.Address;
          "Address 2" := lvAdrsRec."Address 2";
           City := lvAdrsRec.City;
          Contact := lvAdrsRec.Contact;
          County := lvAdrsRec.County;
          "Country/Region Code":= lvAdrsRec."Country/Region Code";
          "Post Code" := lvAdrsRec."Post Code";
          "Phone No." := lvAdrsRec."Phone No.";
          "Mobile Phone No." := lvAdrsRec."Mobile Phone No.";
          "Telex Answer Back" := lvAdrsRec."Telex Answer Back";
         "Telex No." := lvAdrsRec."Telex No.";
          "Fax No." := lvAdrsRec."Fax No.";
          "E-Mail" := lvAdrsRec."E-Mail";
          "Home Page" := lvAdrsRec."Home Page";
          IF ("Derived from Customer No." <> "Customer No.") THEN BEGIN  //db, 19-12-07
            IF lvCustRec2.GET("Customer No.") THEN BEGIN
              Name := lvCustRec2.Name;
              "Name 2" := Text11012002 + ' ' + COPYSTR(lvAdrsRec.Name, 1, 45);
            END;
          END;
        END ELSE BEGIN
          IF lvRead = TRUE THEN BEGIN
            IF NOT lvCustRec.GET("Derived from Customer No.") THEN
              EXIT;
          END;
          Name := lvCustRec.Name;
          "Name 2" := lvCustRec."Name 2";
          Address := lvCustRec.Address;
          "Address 2" := lvCustRec."Address 2";
          City := lvCustRec.City;
          Contact := lvCustRec.Contact;
          County := lvCustRec.County;
          "Country/Region Code":= lvCustRec."Country/Region Code";
          "Post Code" := lvCustRec."Post Code";
          "Phone No." := lvCustRec."Phone No.";
          "Mobile Phone No." := lvCustRec."Mobile Phone No.";
          "Telex Answer Back" := lvCustRec."Telex Answer Back";
          "Telex No." := lvCustRec."Telex No.";
          "Fax No." := lvCustRec."Fax No.";
          "E-Mail" := lvCustRec."E-Mail";
          "Home Page" := lvCustRec."Home Page";
          IF ("Derived from Customer No." <> "Customer No.") THEN BEGIN  //db, 19-12-07
            IF lvCustRec2.GET("Customer No.") THEN BEGIN
              Name := lvCustRec2.Name;
              "Name 2" := 'p/a ' + COPYSTR(lvCustRec.Name, 1, 45);
            END;
          END;
        END;
      END;
    END;

    PROCEDURE UpdateRelatedAddress@1100485004(gvCustRec@1100485005 : Record 18;gvAdrsRec@1100485004 : Record 222);
    VAR
      lvAdrsRec@1100485000 : Record 222;
    BEGIN
      //**4PS
      lvAdrsRec.RESET;
      lvAdrsRec.SETCURRENTKEY("Derived from Customer No.", "Derived from Address Code");
      IF gvAdrsRec.Code <> '' THEN BEGIN
        lvAdrsRec.SETRANGE("Derived from Customer No.", gvAdrsRec."Customer No.");
        lvAdrsRec.SETRANGE("Derived from Address Code", gvAdrsRec.Code);
      END ELSE BEGIN
        lvAdrsRec.SETRANGE("Derived from Customer No.", gvCustRec."No.");
        lvAdrsRec.SETFILTER("Derived from Address Code", '%1', '')
      END;
      IF lvAdrsRec.FINDSET(TRUE, FALSE) THEN BEGIN
        REPEAT
          GetRelatedAddress(lvAdrsRec,FALSE,gvCustRec,gvAdrsRec);
          lvAdrsRec.MODIFY;
        UNTIL lvAdrsRec.NEXT = 0;
      END;

      IF gvAdrsRec.Code = '' THEN BEGIN
        lvAdrsRec.RESET;
        lvAdrsRec.SETCURRENTKEY("Customer No.", Code);
        lvAdrsRec.SETRANGE("Customer No.", gvCustRec."No.");
        lvAdrsRec.SETFILTER("Derived from Customer No.", '<>%1', '');
        IF lvAdrsRec.FINDSET(TRUE, FALSE) THEN BEGIN
          REPEAT
            lvAdrsRec.Name := gvCustRec.Name;  //db, 31-03-08: M11439
            lvAdrsRec.MODIFY;
          UNTIL lvAdrsRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE CheckDerivedFrom@1100485005();
    VAR
      lvCustRec@1100485001 : Record 18;
      lvAdrsRec@1100485000 : Record 222;
    BEGIN
      //**4PS
      GetRelatedAddress(Rec,TRUE,lvCustRec,lvAdrsRec);
    END;

    PROCEDURE CheckRelatedAddress@1100485006();
    VAR
      lvCustRec@1100485001 : Record 18;
    BEGIN
      //**4PS
      UpdateRelatedAddress(lvCustRec,Rec);
    END;

    PROCEDURE TravelTimeUnit@1100525000() : Code[10];
    VAR
      ProjSetup@1100525000 : Record 315;
      DimValRec@1100525002 : Record 349;
      DimMgt@1100525001 : Codeunit 408;
    BEGIN
      //**4PS
      ProjSetup.GET;
      DimMgt.GetDimValueRec(2,ProjSetup."Cost Object Travel Time",DimValRec,FALSE,'');
      EXIT(DimValRec."Unit of Measure");
    END;

    PROCEDURE TravelDistanceUnit@1100525001() : Code[10];
    VAR
      ProjSetup@1100525000 : Record 315;
      DimValRec@1100525002 : Record 349;
      DimMgt@1100525001 : Codeunit 408;
    BEGIN
      //**4PS
      ProjSetup.GET;
      DimMgt.GetDimValueRec(2,ProjSetup."Cost Object Travel Distance",DimValRec,FALSE,'');
      EXIT(DimValRec."Unit of Measure");
    END;

    PROCEDURE GetAlternativeBilltoAddress@1100528600(BilltoCustomerNo@1100528600 : Code[20];VAR AlternativeBilltoAddressCode@1100528602 : Code[10]) : Boolean;
    VAR
      ShiptoAddress@1100528601 : Record 222;
      SalesReceivablesSetup@1100528603 : Record 311;
    BEGIN
      IF BilltoCustomerNo = '' THEN
        EXIT(FALSE);
      SalesReceivablesSetup.GET;
      IF NOT SalesReceivablesSetup."Auto Det. Alt. Bill-to Address" THEN
        EXIT(FALSE);
      ShiptoAddress.SETRANGE("Customer No.", BilltoCustomerNo);
      ShiptoAddress.SETRANGE("Billing Address", TRUE);
      IF NOT ShiptoAddress.FINDSET THEN
        EXIT(FALSE);
      IF ShiptoAddress.NEXT <> 0 THEN
        EXIT(FALSE);
      AlternativeBilltoAddressCode := ShiptoAddress.Code;
      EXIT(TRUE);
    END;

    PROCEDURE GetProjectAddress@1100525002(CustomerNo@1100525001 : Code[20];VAR AddressCode@1100525000 : Code[10]) : Boolean;
    VAR
      ShiptoAddress@1100525002 : Record 222;
    BEGIN
      IF CustomerNo = '' THEN
        EXIT(FALSE);
      ShiptoAddress.SETRANGE("Customer No.", CustomerNo);
      ShiptoAddress.SETRANGE("Project Address", TRUE);
      IF NOT ShiptoAddress.FINDSET THEN
        EXIT(FALSE);
      IF ShiptoAddress.NEXT <> 0 THEN
        EXIT(FALSE);
      AddressCode := ShiptoAddress.Code;
      EXIT(TRUE);
    END;

    BEGIN
    {
      14-04-2010 - Peter S: NotBlank set to No. to enable autonumbering in the OnInsert Trigger
    }
    END.
  }
}

