OBJECT Codeunit 341 VAT CaptionClass Mgmt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ExclVATTxt@1001 : TextConst 'ENU=Excl. VAT;NOR=Ekskl. mva.';
      InclVATTxt@1000 : TextConst 'ENU=Incl. VAT;NOR=Inkl. mva.;SVE=Inkl. moms';

    [EventSubscriber(Codeunit,42,OnResolveCaptionClass,"",Skip,Skip)]
    LOCAL PROCEDURE ResolveCaptionClass@1(CaptionArea@1001 : Text;CaptionExpr@1002 : Text;Language@1000 : Integer;VAR Caption@1003 : Text;VAR Resolved@1004 : Boolean);
    BEGIN
      IF CaptionArea = '2' THEN BEGIN
        Caption := VATCaptionClassTranslate(CaptionArea,CaptionExpr,Language);
        Resolved := TRUE;
      END;
    END;

    LOCAL PROCEDURE VATCaptionClassTranslate@2(CaptionArea@1008 : Text;CaptionExpr@1007 : Text;Language@1006 : Integer) : Text;
    VAR
      VATCaptionType@1003 : Text;
      VATCaptionRef@1002 : Text;
      CommaPosition@1001 : Integer;
      Caption@1004 : Text;
      IsHandled@1000 : Boolean;
    BEGIN
      OnBeforeVATCaptionClassTranslate(CaptionArea,CaptionExpr,Language,Caption,IsHandled);
      IF IsHandled THEN
        EXIT(Caption);

      // VATCAPTIONTYPE
      // <DataType>   := [SubString]
      // <Length>     =  1
      // <DataValue>  :=
      // '0' -> <field caption + 'Excl. VAT'>
      // '1' -> <field caption + 'Incl. VAT'>

      CommaPosition := STRPOS(CaptionExpr,',');
      IF CommaPosition > 0 THEN BEGIN
        VATCaptionType := COPYSTR(CaptionExpr,1,CommaPosition - 1);
        VATCaptionRef := COPYSTR(CaptionExpr,CommaPosition + 1);
        CASE VATCaptionType OF
          '0':
            EXIT(STRSUBSTNO('%1 %2',VATCaptionRef,ExclVATTxt));
          '1':
            EXIT(STRSUBSTNO('%1 %2',VATCaptionRef,InclVATTxt));
        END;
      END;
      EXIT('');
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeVATCaptionClassTranslate@3(CaptionArea@1002 : Text;CaptionExpr@1001 : Text;Language@1000 : Integer;VAR Caption@1003 : Text;VAR IsHandled@1004 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

