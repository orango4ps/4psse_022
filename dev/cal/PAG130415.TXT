OBJECT Page 130415 Semi-Manual Test Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Semi-Manual Test Wizard;
               NLD=Semi-Manual Test Wizard];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table130415;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    OnAfterGetRecord=BEGIN
                       IF "Step number" > "Total steps" THEN
                         StepHeading := 'TEST COMPLETE'
                       ELSE
                         StepHeading := STRSUBSTNO('Step %1 of %2. %3',"Step number","Total steps","Step heading");
                     END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 6       ;1   ;Action    ;
                      Name=GetExecutionLog;
                      CaptionML=[ENU=Download log;
                                 NLD=Download log];
                      ToolTipML=[ENU="Displays a list of actions executed so far ";
                                 NLD="Displays a list of actions executed so far "];
                      ApplicationArea=#All;
                      Enabled=TestExecuting;
                      InFooterBar=Yes;
                      OnAction=VAR
                                 SemiManualExecutionLog@1002 : Record 130416;
                                 FileManagement@1000 : Codeunit 419;
                                 File@1003 : File;
                                 OutStream@1004 : OutStream;
                                 ServerFileName@1001 : Text;
                               BEGIN
                                 ServerFileName := FileManagement.ServerTempFileName('txt');
                                 File.CREATE(ServerFileName);
                                 File.CREATEOUTSTREAM(OutStream);
                                 IF SemiManualExecutionLog.FINDSET THEN
                                   REPEAT
                                     OutStream.WRITE('[' + FORMAT(SemiManualExecutionLog."Time stamp") + '] ');
                                     OutStream.WRITETEXT(SemiManualExecutionLog.GetMessage);
                                     OutStream.WRITETEXT;
                                   UNTIL SemiManualExecutionLog.NEXT = 0;
                                 File.CLOSE;
                                 FileManagement.DownloadTempFile(ServerFileName);
                               END;
                                }
      { 5       ;1   ;Action    ;
                      Name=ClearExecutionLog;
                      CaptionML=[ENU=Clear log;
                                 NLD=Clear log];
                      ToolTipML=[ENU=Delete all entries in the execution log.;
                                 NLD=Delete all entries in the execution log.];
                      ApplicationArea=#All;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 SemiManualExecutionLog.DELETEALL;
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Name=LoadTest;
                      CaptionML=[ENU=Load;
                                 NLD=Load];
                      ToolTipML=[ENU=Load the selected codeunit.;
                                 NLD=Load the selected codeunit.];
                      ApplicationArea=#All;
                      Enabled=NOT TestExecuting;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 LoadTest;
                               END;
                                }
      { 15      ;1   ;Action    ;
                      Name=SkipStep;
                      CaptionML=[ENU=Skip step;
                                 NLD=Skip step];
                      ToolTipML=[ENU=Specifies that the automated actions are complete, and displays the manual actions for the next step.;
                                 NLD=Specifies that the automated actions are complete, and displays the manual actions for the next step.];
                      ApplicationArea=#All;
                      Enabled=TestExecuting;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 "Skip current step" := TRUE;
                                 OnNextStep;
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=NextStep;
                      CaptionML=[ENU=Next step;
                                 NLD=Next step];
                      ToolTipML=[ENU=Specifies that the manual actions for this step are complete, and displays the actions for the next step.;
                                 NLD=Specifies that the manual actions for this step are complete, and displays the actions for the next step.];
                      ApplicationArea=#All;
                      Enabled=TestExecuting;
                      InFooterBar=Yes;
                      OnAction=BEGIN
                                 "Skip current step" := FALSE;
                                 OnNextStep;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Manual test wizard;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                Name=StepCodeunit;
                CaptionML=ENU="";
                Visible=NOT TestExecuting;
                GroupType=Group }

    { 13  ;2   ;Group     ;
                Name=Para1;
                CaptionML=[ENU=Codeunit;
                           NLD=Codeunit];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose the codeunit, and then load it. The wizard will do the actions that could be automated, and list actions for each step that you need to do manually.;
                                     NLD=Choose the codeunit, and then load it. The wizard will do the actions that could be automated, and list actions for each step that you need to do manually.] }

    { 11  ;3   ;Field     ;
                ApplicationArea=#All;
                BlankZero=Yes;
                SourceExpr=CodeunitId;
                ColumnSpan=2;
                OnValidate=BEGIN
                             LoadTest;
                           END;

                OnDrillDown=VAR
                              AllObjWithCaption@1001 : Record 2000000058;
                              GetSemiManualTestCodeunits@1000 : Page 130416;
                            BEGIN
                              GetSemiManualTestCodeunits.LOOKUPMODE := TRUE;
                              IF GetSemiManualTestCodeunits.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                GetSemiManualTestCodeunits.SETSELECTIONFILTER(AllObjWithCaption);
                                IF AllObjWithCaption.FINDFIRST THEN
                                  CodeunitId := AllObjWithCaption."Object ID";
                                LoadTest;
                              END;
                            END;
                             }

    { 9   ;1   ;Group     ;
                Name=StepManualSteps;
                CaptionML=ENU="";
                Visible=TestExecuting;
                GroupType=Group }

    { 12  ;2   ;Group     ;
                Name=Para3;
                CaptionML=ENU="";
                GroupType=Group }

    { 18  ;3   ;Field     ;
                CaptionML=ENU="";
                ApplicationArea=#All;
                SourceExpr=CodeunitIdentifier;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=TRUE }

    { 14  ;2   ;Group     ;
                Name=Para2;
                CaptionML=[ENU=Manual steps;
                           NLD=Manual steps];
                GroupType=Group;
                InstructionalTextML=[ENU=These are the actions that cannot be automated. Manually perform each of the actions listed for each step. If an error message displays, you've found a bug! Copy information about the error after clicking on the Download log button, and provide that when you report the bug.;
                                     NLD=These are the actions that cannot be automated. Manually perform each of the actions listed for each step. If an error message displays, you've found a bug! Copy information about the error after clicking on the Download log button, and provide that when you report the bug.] }

    { 7   ;3   ;Field     ;
                ToolTipML=[ENU=Specifies title of this set of manual actions;
                           NLD=Specifies title of this set of manual actions];
                ApplicationArea=#All;
                SourceExpr=StepHeading;
                Editable=FALSE;
                Style=StrongAccent;
                StyleExpr=TRUE }

    { 8   ;3   ;Field     ;
                ToolTipML=[ENU=Specifies the manual actions for this step in the test.;
                           NLD=Specifies the manual actions for this step in the test.];
                ApplicationArea=#All;
                SourceExpr=ManualSteps;
                Editable=FALSE;
                MultiLine=Yes }

  }
  CODE
  {
    VAR
      SemiManualExecutionLog@1004 : Record 130416;
      StepHeading@1000 : Text;
      ManualSteps@1001 : Text;
      CodeunitId@1002 : Integer;
      CodeunitIdentifier@1007 : Text;
      TestExecuting@1005 : Boolean;
      ErrorOccuredErr@1003 : TextConst '@@@={Locked};ENU=The following error occured: %1;NLD=The following error occured: %1';
      TestSuccessfulMsg@1006 : TextConst 'ENU=Test successfully completed.;NLD=Test successfully completed.';

    LOCAL PROCEDURE LoadTest@12();
    VAR
      AllObjWithCaption@1001 : Record 2000000058;
    BEGIN
      TestExecuting := FALSE;
      IF CodeunitId <= 0 THEN
        EXIT;

      SemiManualExecutionLog.Log(STRSUBSTNO('Attempting to load codeunit %1.',CodeunitId));
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Codeunit);
      AllObjWithCaption.SETRANGE("Object ID",CodeunitId);
      IF NOT AllObjWithCaption.FINDFIRST THEN
        EXIT;

      CodeunitIdentifier := STRSUBSTNO('%1: %2',CodeunitId,AllObjWithCaption."Object Name");
      Initialize(AllObjWithCaption."Object ID",AllObjWithCaption."Object Name");
      ManualSteps := GetManualSteps;
      TestExecuting := TRUE;
      SemiManualExecutionLog.Log(STRSUBSTNO('Loaded codeunit %1. Total steps = %2.',
          CodeunitId,"Total steps"));
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE OnNextStep@1();
    BEGIN
      CLEARLASTERROR;
      SemiManualExecutionLog.Log(STRSUBSTNO('Manual step %1- %2 executed. Attempting to execute the automation post process.',
          "Step number","Step heading"));
      IF CODEUNIT.RUN("Codeunit number",Rec) THEN BEGIN
        ManualSteps := GetManualSteps;
        IF "Step number" > "Total steps" THEN BEGIN
          TestExecuting := FALSE;
          MESSAGE(TestSuccessfulMsg);
        END;
        CurrPage.UPDATE;
        IF "Skip current step" THEN
          SemiManualExecutionLog.Log('The automation post process step skipped.')
        ELSE
          SemiManualExecutionLog.Log('The automation post process executed without errors.');
      END ELSE BEGIN
        SemiManualExecutionLog.Log(STRSUBSTNO(ErrorOccuredErr,GETLASTERRORTEXT));
        ERROR(ErrorOccuredErr,GETLASTERRORTEXT);
      END;
    END;

    BEGIN
    END.
  }
}

