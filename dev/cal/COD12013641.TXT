OBJECT Codeunit 12013641 Ex Send Mail Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF500000;
  }
  PROPERTIES
  {
    TableNo=12013608;
    OnRun=VAR
            SmtpMail@1100285025 : Codeunit 400;
            _f@1100285024 : File;
            _f2@1100285023 : File;
            _appendText@1100285022 : Text[1024];
            _i@1100285021 : Integer;
            _int@1100285020 : Integer;
            _oStream@1100285019 : OutStream;
            _iStream@1100285018 : InStream;
            _writeHTML@1100285017 : Boolean;
            _color@1100285016 : Text[30];
            _bcolor@1100285015 : Text[30];
            _tempFilename@1100285014 : Text[250];
            _ch@1100285013 : Char;
            _text@1100285012 : Text[1024];
            AttachmentFile@1100285011 : Text[250];
            ExAppSetup@1100285010 : Record 12013601;
            LF@1100285009 : Char;
            CR@1100285008 : Char;
            dotNetArray@1100285007 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";
            dotNetFile@1100285006 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File";
            dotNetEncoding@1100285005 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
            dotNetFileStream@1100285004 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.FileStream";
            int@1100285003 : Integer;
            Company@1100285002 : Record 2000000006;
            EXFileMgt@1100285001 : Codeunit 12013602;
            ToMailAddress@1100285000 : Text[1024];
            SMTPSetup@1100285026 : Record 409;
            ExFlowEmailMgt@1100285027 : Codeunit 12013627;
          BEGIN
            CR := 13;
            LF := 10;

            IF NOT SMTPSetup.GET THEN
              CLEAR(SMTPSetup);

            ExAppSetup.GET;

            _emailHeader.TESTFIELD("ExFlow Web URL");
            _emailHeader.CALCFIELDS("HTML Template File");

            IF _emailHeader."HTML Template File".HASVALUE() THEN BEGIN
              // create local template.htm from blob
              _f.CREATETEMPFILE();
              _tempFilename := _f.NAME +'.txt';
              _f.CLOSE();
              _f.CREATE(_tempFilename);
              _f.CREATEOUTSTREAM(_oStream);
              _emailHeader."HTML Template File".CREATEINSTREAM(_iStream);
              COPYSTREAM(_oStream,_iStream);
              _f.CLOSE;

              IF _emailHeader."Test Email" <> '' THEN
                ToMailAddress := _emailHeader."Test Email"
              ELSE
                ToMailAddress := _exUserComp."E-mail ExFlow";

              dotNetArray := dotNetFile.ReadAllLines(_tempFilename,dotNetEncoding.GetEncoding(1252));
              WITH Rec DO BEGIN
                SmtpMail.CreateMessage(_emailHeader."Sender Name",
                  _emailHeader."Sender Address",
                  ToMailAddress,
                  _emailHeader."Email Subject",_emailHeader."Email Subject",TRUE);

                _writeHTML := FALSE;
                FOR int := 0 TO (dotNetArray.Length - 1) DO BEGIN
                  _text := dotNetArray.GetValue(int); // line by line
                  IF STRPOS(_text,'<html') > 0 THEN
                    _writeHTML := TRUE;

                  IF _writeHTML THEN BEGIN
                    _appendText := _text;

                    _i := STRPOS(UPPERCASE(_text),'USER_NAME');
                    IF _i > 0 THEN BEGIN
                      _appendText := COPYSTR(_text,1,_i -1) +_exUserComp."User Name";
                      IF STRLEN(_text) > _i +9 THEN
                        _appendText += COPYSTR(_text,_i +9);
                    END;

                    _i := STRPOS(UPPERCASE(_text),'EXFLOW_URL');
                    IF _i > 0 THEN BEGIN
                      _appendText := COPYSTR(_text,1,_i -1) +'<a href="' +_emailHeader."ExFlow Web URL" +'">ExFlow Web</a>';
                      IF STRLEN(_text) > _i +10 THEN
                        _appendText += COPYSTR(_text,_i +10);
                    END;

                    _i := STRPOS(UPPERCASE(_text),'RESULT_TABLES');
                    IF _i > 0 THEN BEGIN
                      _appendText := '<table cellpadding ="5px" cellspacing="0px" border="0px"  style="border-left: 1px solid #eaeaea; border-bottom: 1px solid #eaeaea;">';
                      SmtpMail.AppendBody(_appendText);

                      _appendText := COPYSTR(_text,1,_i -1);
                      SmtpMail.AppendBody(_appendText);

                      REPEAT
                        _color := '#000000';
                        _bcolor := '#f5f5f5';
                        IF "Due Date" < WORKDATE THEN BEGIN
                          _bcolor := '#bb0000';
                          _color := '#fff';
                        END ELSE
                          IF "Due Date" < CALCDATE('<+7D>',WORKDATE) THEN
                            _color := '#bb0000';
                        _appendText := STRSUBSTNO('%1 %2 %3 %4 %5 %6 %7'
                          ,'<tr style="background-color: ' +_bcolor +'; color: #444339; font-weight:bold; font-size: 12px; font-family: Lucida Sans, Tahoma, Verdana;">'
                          ,'<td style="width: 70px; border-right: 1px solid #eaeaea; color: ' +_color +'">' +Rec."Document No." +'</td>'
                          ,'<td style="width:100px; border-right: 1px solid #eaeaea; color: ' +_color +'">' +FORMAT(Rec."Document Date") +'</td>'
                          ,'<td style="width:100px; border-right: 1px solid #eaeaea; color: ' +_color +'">' +FORMAT(Rec."Due Date") +'</td>'
                          ,'<td style="width:150px; border-right: 1px solid #eaeaea; color: ' +_color +'">' +Rec."Vendor Name" +'</td>'
                          ,'<td style="width:150px; border-right: 1px solid #eaeaea; color: ' +_color +'">' +Rec."Temp Company Name" +'</td>'
                          ,'</tr>'
                          );

                        AttachmentFile := '';
                        IF _emailHeader."Attach Image" THEN BEGIN
                          AttachmentFile := ExAppSetup."Path to Used Invoices" + "Image Name";
                          IF NOT EXFileMgt.FileExist(ExAppSetup."Path to Used Invoices", "Image Name") THEN
                            AttachmentFile := '';
                        END;

                        SmtpMail.AppendBody(_appendText);
                        IF AttachmentFile <> '' THEN
                          SmtpMail.AddAttachment(AttachmentFile,"Image Name");
                      UNTIL NEXT = 0;

                      SmtpMail.AppendBody('</table>');
                    END
                    ELSE
                      SmtpMail.AppendBody(_appendText)
                  END;
                END;
                IF _emailHeader.GET(_emailHeader.Type) THEN BEGIN
                   _emailHeader."Last Sent" := CREATEDATETIME(TODAY,TIME);
                   _emailHeader.MODIFY;
                END;

                COMMIT;

                IF _emailHeader."CC Email" <> '' THEN
                  SmtpMail.AddCC(_emailHeader."CC Email");
                SmtpMail.Send();
              END;

              IF EXISTS(_tempFilename) THEN
                ERASE(_tempFilename);

            END ELSE BEGIN
              IF _emailHeader."Test Email" <> '' THEN
                ToMailAddress := _emailHeader."Test Email"
              ELSE
                ToMailAddress := _exUserComp."E-mail ExFlow";

              SmtpMail.CreateMessage(_emailHeader."Sender Name",
                _emailHeader."Sender Address",
                ToMailAddress,
                _emailHeader."Email Subject",_emailHeader."Email Subject",TRUE);

              WITH Rec DO BEGIN
                REPEAT
                  AttachmentFile := '';
                  IF _emailHeader."Attach Image" THEN BEGIN
                    AttachmentFile := ExAppSetup."Path to Used Invoices" + "Image Name";
                    IF NOT EXFileMgt.FileExist(ExAppSetup."Path to Used Invoices", "Image Name") THEN
                      AttachmentFile := '';
                  END;

                  _appendText := STRSUBSTNO(EMAILTXT,"Document No.") ;

                  SmtpMail.AppendBody(_appendText);
                  SmtpMail.AppendBody(FORMAT(CR)+FORMAT(LF));
                  IF AttachmentFile <> '' THEN
                    SmtpMail.AddAttachment(AttachmentFile,"Image Name");
                UNTIL NEXT = 0;
              END;

              _emailHeader."Last Sent" := CREATEDATETIME(TODAY,TIME);
              _emailHeader.MODIFY;

              COMMIT;

              IF _emailHeader."CC Email" <> '' THEN
                SmtpMail.AddCC(_emailHeader."CC Email");
              SmtpMail.Send();
            END;
          END;

  }
  CODE
  {
    VAR
      HideMessage@1100285001 : Boolean;
      EXF001@1100285013 : TextConst 'ENU=%1 %2;SVE=%1 %2';
      EXF002@1100285012 : TextConst 'ENU=Do you want to send emails based on the ExFlow email setup?;SVE=Vill du skicka ut epost baserat p† ExFlows epost inst„llningar?';
      EXF003@1100285011 : TextConst 'ENU=No emails sent.;SVE=Ingen epost skickad.';
      EXF004@1100285010 : TextConst 'ENU=Purchaser Code: %1;SVE=Ink”parkod: %1';
      EXF005@1100285009 : TextConst 'ENU=No email address for purchaser %1.;SVE=Ingen epost adress f”r ink”pare %1.';
      EXF006@1100285008 : TextConst 'ENU=No. of emails: %1, No. of Documents: %2;SVE=Antal email: %1, Antal dokument: %2';
      EXF007@1100285007 : TextConst 'ENU=Order No.: %1;SVE=Ordernr: %1';
      EXF008@1100285006 : TextConst 'ENU=Return Order No.: %1;SVE=Returordernr: %1';
      EXF009@1100285005 : TextConst 'ENU=Do you want to send emails for job %1?;SVE=Vill du skicka ut epost f”r jobb %1?';
      EXF010@1100285004 : TextConst 'ENU=ExFlow sign in ticket;SVE=ExFlow inloggningsbiljett';
      EXF011@1100285003 : TextConst 'ENU=You have requested a new sign in code to ExFlow. Your code to sign in to ExFlow is:;SVE=H„r kommer din inloggningskod till ExFlow. Koden „r giltig i 5 minuter och kan bara anv„das en g†ng. Din kod:';
      EMAILTXT@1100285002 : TextConst 'ENU=Document No. %1;SVE=Dokumentnr. %1';
      _emailHeader@1100285000 : Record 12013619;
      TableNo@1100285014 : Integer;
      _exUserComp@1100285015 : Record 12013641;

    PROCEDURE SetExFEmailHeader@1100285000(VAR ExFEmailHeader@1100285000 : Record 12013619);
    BEGIN
      _emailHeader := ExFEmailHeader;
    END;

    PROCEDURE "GetExFEmail Header"@1100285001(VAR ExFEmailHeader@1100285000 : Record 12013619);
    BEGIN
      ExFEmailHeader := _emailHeader;
    END;

    PROCEDURE SetTableNo@1100285002(TNo@1100285000 : Integer);
    BEGIN
      TableNo := TNo;
    END;

    PROCEDURE GetTableNo@1100285003(TNo@1100285000 : Integer);
    BEGIN
      TNo := TableNo;
    END;

    PROCEDURE SetExUserComp@1100285004(EXUserCompany@1100285000 : Record 12013641);
    BEGIN
      _exUserComp := EXUserCompany;
    END;

    BEGIN
    END.
  }
}

