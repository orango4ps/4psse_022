OBJECT Page 11128516 Payment Order - Settl. Status
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO10.00 (15000012),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Payment Order - Settlement Status;
               NOR=Oppdrag - Avregningsstatus];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table2000000026;
    PageType=ListPlus;
    OnOpenPage=BEGIN
                 UpdateInfo;
               END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 8   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           NOR=Generell;
                           SVE=Allm„nt] }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Payment order ID;
                           NOR=Oppdrags ID;
                           SVE=Betalningsorder ID];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=RemPaymOrder.ID;
                TableRelation="Remittance Payment Order";
                OnValidate=BEGIN
                             RemPaymOrder.GET(RemPaymOrder.ID);
                             CheckPaymOrderStatus(RemPaymOrder);
                             UpdateInfo;
                             CurrPage.UPDATE;
                           END;
                            }

    { 11  ;2   ;Field     ;
                CaptionML=[ENU=Description;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=RemPaymOrder.Comment;
                Editable=FALSE }

    { 1080000;1;Group      }

    { 13  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                CaptionClass=Text19066747;
                MultiLine=Yes }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 15  ;2   ;Field     ;
                CaptionML=[ENU=Journal, template name;
                           NOR=Kladd, malnavn];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ReturnTemplateName[Number] }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Journal name;
                           NOR=Kladd navn];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ReturnJournalName[Number] }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Number of payments;
                           NOR=Antall betalinger];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ReturnNumber[Number] }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      RemPaymOrder@1080000 : Record 11128504;
      WaitingJournalLine@1080001 : Record 11128507;
      ReturnTemplateName@1080002 : ARRAY [20] OF Code[10];
      ReturnJournalName@1080003 : ARRAY [20] OF Code[10];
      ReturnNumber@1080004 : ARRAY [20] OF Integer;
      NumberOfLines@1080005 : Integer;
      Text19066747@19055990 : TextConst 'ENU=Settled payments in this payment order are imported and transferred to following journals:;NOR=For dette oppdraget er avregnede betalinger lest inn og overf›rt til f›lgende kladder:';
      MaxNumberOfLinesMsg@1080006 : TextConst 'ENU=Return data is imported into more than 20 journals.\Return information is displayed only for the first 20 journals.;NOR=Retur er lest tilbake i mer enn 20 kladder.\Returinformasjon vises bare for de f›rste 20 kladdene.';

    PROCEDURE SetPaymOrder@1(SetRemPaymOrder@1080000 : Record 11128504);
    BEGIN
      CheckPaymOrderStatus(SetRemPaymOrder);
      RemPaymOrder := SetRemPaymOrder;
    END;

    LOCAL PROCEDURE UpdateInfo@2();
    VAR
      i@1000000000 : Integer;
      JournalFound@1080001 : Boolean;
    BEGIN
      // Update info shown on form
      CLEAR(ReturnTemplateName);
      CLEAR(ReturnJournalName);
      CLEAR(ReturnNumber);
      NumberOfLines := 0;

      WaitingJournalLine.SETCURRENTKEY("Payment Order ID - Settled");
      WaitingJournalLine.SETRANGE("Payment Order ID - Settled",RemPaymOrder.ID);

      // Count the number of payments placed in each journal
      IF WaitingJournalLine.FIND('-') THEN BEGIN
        REPEAT
          JournalFound := FALSE;
          FOR i := 1 TO NumberOfLines DO
            IF (WaitingJournalLine."Journal, Settlement Template" = ReturnTemplateName[i]) AND
               (WaitingJournalLine."Journal - Settlement" = ReturnJournalName[i])
            THEN BEGIN
              ReturnNumber[i] := ReturnNumber[i] + 1;
              JournalFound := TRUE;
            END;
          IF NOT JournalFound THEN BEGIN
            NumberOfLines := NumberOfLines + 1;
            IF NumberOfLines > ARRAYLEN(ReturnTemplateName) THEN
              MESSAGE(MaxNumberOfLinesMsg)
            ELSE BEGIN
              ReturnTemplateName[NumberOfLines] := WaitingJournalLine."Journal, Settlement Template";
              ReturnJournalName[NumberOfLines] := WaitingJournalLine."Journal - Settlement";
              ReturnNumber[NumberOfLines] := 1;
            END;
          END;
        UNTIL WaitingJournalLine.NEXT = 0;
      END;

      // Select lines to be shown
      SETRANGE(Number,1,NumberOfLines);
    END;

    LOCAL PROCEDURE CheckPaymOrderStatus@3(CheckRemPaymOrder@1080000 : Record 11128504);
    BEGIN
      CheckRemPaymOrder.TESTFIELD(Type,CheckRemPaymOrder.Type::Return);
    END;

    BEGIN
    END.
  }
}

