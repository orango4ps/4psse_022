OBJECT Table 6086350 Bank Mapping Rule
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Oldrules@1160040000 : Record 6086350;
             BEGIN
               IF "Rule No." = 0 THEN
                 IF Oldrules.FINDLAST THEN
                   "Rule No." := Oldrules."Rule No." + 10;
             END;

    CaptionML=[DEU=Bankabgleichregel;
               ENU=Bank Mapping Rule;
               NLD=Bankmappingregel];
    LookupPageID=Page6086350;
    DrillDownPageID=Page6086350;
  }
  FIELDS
  {
    { 1   ;   ;Rule No.            ;Integer       ;CaptionML=[DEU=Regelnummer;
                                                              ENU=Rule No.;
                                                              NLD=Regelnr.;
                                                              NOR=Regelnummer;
                                                              SVE=Regelnr] }
    { 2   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   CaptionML=[DEU=Continia-Benutzer-ID;
                                                              ENU=Continia User ID;
                                                              NLD=Continia gebruikers-id;
                                                              SVE=Anst„llningsnr] }
    { 10  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=CONST(6086330));
                                                   CaptionML=[DEU=Feldnr.;
                                                              ENU=Field No.;
                                                              NLD=Veldnr.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr] }
    { 11  ;   ;Value               ;Text50        ;CaptionML=[DEU=Wert;
                                                              ENU=Value;
                                                              NLD=Waarde;
                                                              NOR=Verdi;
                                                              SVE=V„rde] }
    { 12  ;   ;Field Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=CONST(6086330),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[DEU=Feldname;
                                                              ENU=Field Name;
                                                              NLD=Veldnaam;
                                                              NOR=Feltnavn;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
    { 20  ;   ;Expense Type Code   ;Code20        ;TableRelation="Expense Type";
                                                   CaptionML=[DEU=Ausgabenartcode;
                                                              ENU=Expense Type Code;
                                                              NLD=Uitgavetypecode] }
  }
  KEYS
  {
    {    ;Rule No.                                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE UseBankMappingRules@1160040000(VAR Transaction@1160040000 : Record 6086330);
    BEGIN
      Transaction.TESTFIELD("Matched to Expense",FALSE);

      IF Transaction."Expense Type" = '' THEN BEGIN
        SETRANGE("Continia User ID",Transaction."Continia User ID");
        IF Rec.FINDSET THEN
          REPEAT
            UseBankMappingRule(Transaction);
          UNTIL (Rec.NEXT = 0) OR (Transaction."Expense Type" <> '');

        IF Transaction."Expense Type" = '' THEN BEGIN
          SETRANGE("Continia User ID",'');
          IF Rec.FINDSET THEN
            REPEAT
              UseBankMappingRule(Transaction);
            UNTIL (Rec.NEXT = 0) OR (Transaction."Expense Type" <> '');
        END;
      END;
    END;

    LOCAL PROCEDURE UseBankMappingRule@1160040001(VAR Transaction@1160040000 : Record 6086330);
    VAR
      ExpenseType@1160040003 : Record 6086307;
      RecRef@1160040002 : RecordRef;
      FieldRef@1160040001 : FieldRef;
    BEGIN
      RecRef.GETTABLE(Transaction);
      IF "Field No." <> 0 THEN BEGIN
        FieldRef := RecRef.FIELD("Field No.");
        IF STRPOS(UPPERCASE(FORMAT(FieldRef.VALUE)),UPPERCASE(Value)) <> 0 THEN BEGIN
          Transaction."Expense Type" := "Expense Type Code";
          IF ExpenseType.GET("Expense Type Code") THEN
            IF ExpenseType."Exclude Transactions" THEN
              Transaction."Exclude Entry" := ExpenseType."Exclude Transactions";
          Transaction.MODIFY;
        END;
      END;
    END;

    PROCEDURE LookupFieldNo@1160040002(VAR Text@1160040001 : Text[1024]) : Boolean;
    VAR
      FieldRec@1160040000 : Record 2000000041;
    BEGIN
      FieldRec.SETRANGE(TableNo,DATABASE::"Bank Transaction");
      IF FieldRec.GET(DATABASE::"Bank Transaction","Field No.") THEN;

      IF PAGE.RUNMODAL(PAGE::"Field List",FieldRec) = ACTION::LookupOK THEN BEGIN
        Text := FORMAT(FieldRec."No.");
        EXIT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

