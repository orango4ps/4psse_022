OBJECT XMLport 11012135 Imp. Service Location
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Imp. Service Location;
               SVE=Imp. serviceplats];
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    XmlPortID := 11012135;
                    MainTableID := 11012801;
                    MainTableLookupPageID := 11012802;
                    XMLPortRequestPageID := 11230045;
                  END;

    OnPreXMLport=VAR
                   ConversionXMLPort4PS@1100527700 : Record 11020640;
                 BEGIN
                   IF NOT currXMLport.IMPORTFILE THEN BEGIN
                     AddConversionFileFormatLine;
                     ConversionXMLPort4PS.UpdateFromXMLPort(XmlPortID,MainTableID,MainTableLookupPageID,XMLPortRequestPageID,FALSE,0);
                     COMMIT;
                     ERROR('');
                   END;

                   ConvMgt.CheckComCde(CreateNewNosBln,MainTableID,CommentCde);
                   ConvMgt.CheckComCde(UseOldCustNoBln,18,CustCommentCde);

                   ConvMgt.DeleteConComment(XmlPortID);
                   ConvMgt.CreateConComment(XmlPortID,STRSUBSTNO('Import File: %1.',currXMLport.FILENAME));
                   ConvMgt.CreateConComment(XmlPortID,'Start');
                 END;

    OnPostXMLport=BEGIN
                    ConvMgt.CreateConComment(XmlPortID,'Stop');
                    ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateNoOfLinesCommentTxt(MainTableID,ImpLineCounter));
                  END;

    Format=Variable Text;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{755F5B7F-F062-4834-8364-DC4CD51C5A62}];  ;Root                ;Element ;Text     }

    { [{A7DBD164-8D3E-4F8D-B3A0-89F91BC12205}];1 ;ServiceLocation     ;Element ;Table   ;
                                                  VariableName=Service Location;
                                                  SourceTable=Table11012801;
                                                  SourceTableView=SORTING(Field1);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              ClearFields;
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 ResourceNo@1100529000 : Code[20];
                                                                               BEGIN
                                                                                 StripData;
                                                                                 ImpLineCounter := ImpLineCounter + 1;
                                                                                 CLEAR("Service Location");

                                                                                 WITH "Service Location" DO BEGIN
                                                                                   CustCde := ConvMgt.DetermineNewCode(UseOldCustNoBln,CommentRec."Table Name"::Customer,CustCommentCde,B);

                                                                                   IF CreateNewNosBln THEN
                                                                                     "No." := ''
                                                                                   ELSE
                                                                                     "No." := A;

                                                                                   INSERT(TRUE);
                                                                                   IF CreateNewNosBln THEN
                                                                                     ConvMgt.FillComment(CommentRec."Table Name"::"Service Location","No.",CommentCde,A);

                                                                                   IF (CustCde <> '') AND ConvMgt.IsCustomerPresent(CustCde) THEN
                                                                                     VALIDATE("Customer No.",CustCde)
                                                                                   ELSE
                                                                                     ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateFieldNotFilledCommentTxt(
                                                                                       18,
                                                                                       B,
                                                                                       FIELDCAPTION("Customer No."),
                                                                                       MainTableID,
                                                                                       "No."));

                                                                                   VALIDATE(Description,C);
                                                                                   VALIDATE("Description 2",D);
                                                                                   VALIDATE(Address,E);
                                                                                   VALIDATE("Address 2",F);
                                                                                   "Post Code" := G;
                                                                                   City := H;

                                                                                   IF PostCode.GET("Post Code",City) THEN
                                                                                     "Post Code Group" := PostCode.Group;

                                                                                   NormalizePostCodeHouseNo();

                                                                                   City := ConvMgt.PostCodePresent(G);
                                                                                   IF City = '' THEN
                                                                                     City := H;

                                                                                   VALIDATE("Country/Region Code",I);
                                                                                   VALIDATE("Territory Code",J);

                                                                                   "Phone No." := K;
                                                                                   "Fax No." := L;
                                                                                   "Mobile Phone No." := U;
                                                                                   "E-Mail" := V;
                                                                                   IF W <> '' THEN
                                                                                     "Contact Name" := W;

                                                                                   VALIDATE("Global Dimension 1 Code",M);
                                                                                   VALIDATE(Type,N);
                                                                                   VALIDATE("Travel Distance",RDec);
                                                                                   VALIDATE("Travel Time",SDec);

                                                                                   IF O <> '' THEN
                                                                                     VALIDATE("Search Name",O);

                                                                                   VALIDATE("Customer Feature",P);
                                                                                   VALIDATE("Complex Code",Q);
                                                                                   VALIDATE("Travel Cost Code",T);

                                                                                   MODIFY(TRUE);

                                                                                 END;

                                                                                 ClearFields;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{5C900A9C-2240-4551-8D3B-A67A2D629F53}];2 ;A                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{5E5EE9AF-296C-4B48-B3A4-71FD1D3BDBB8}];2 ;B                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A4D141C2-7800-4941-8428-EBDFE695445E}];2 ;C                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A94DDF8B-AF48-4B38-93FE-471C5E7FDE8B}];2 ;D                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{7009774A-5D5B-4893-B574-E0BA7086A95E}];2 ;E                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{61A54020-5A22-4777-8499-A876D2E08C7F}];2 ;F                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{C3892DEF-6174-4845-A9BD-FA81350F4D61}];2 ;G                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{3B150350-762E-4D06-90AD-0B0A47DC3FEC}];2 ;H                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{079B7881-C2A9-4D33-BDD1-9AC71DDCA4F1}];2 ;I                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{07C9B57E-65F8-4CAD-BA26-FC1EF7C6A2F3}];2 ;J                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{8161E34D-3A46-41B0-8FDD-5E185F9C463E}];2 ;K                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{DB52902E-AEA2-49AF-B81D-A7BFE2B4BCC0}];2 ;L                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{67E04C3C-8FA3-4B7D-90B0-B4F78E627497}];2 ;M                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{02596068-D144-4C72-A2E7-1BFC838E7413}];2 ;N                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{C572BE13-0ECC-4054-926C-D162EC84BA43}];2 ;O                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{4DE894A5-3D25-4067-B986-25835834C5D6}];2 ;P                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{64CC39BC-E41F-4BBE-992C-0F61A1F625B0}];2 ;Q                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A345EA35-9079-4219-A4A9-19ECE6D4D472}];2 ;R                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF R <> '' THEN
                                                                                    EVALUATE(RDec,R);
                                                                                END;
                                                                                 }

    { [{80227F89-07D2-4832-82F1-66D5E2AE22F9}];2 ;S                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF S <> '' THEN
                                                                                    EVALUATE(SDec,S);
                                                                                END;
                                                                                 }

    { [{A4F20BFC-579F-4D4B-9EF4-0BCEA57D7BB9}];2 ;T                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{CBF4615E-8F0E-43D6-B477-490FB7ECB247}];2 ;U                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{2557FF88-D523-45F8-BFF1-AC27FBFC903D}];2 ;V                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{E2312CA7-AEB6-4429-87DE-983E3F76BB7E}];2 ;W                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      CaptionML=ENU=Imp. Service Location OBJ;
      OnOpenPage=BEGIN
                   CreateNewNosBln := TRUE;
                   UseOldCustNoBln := TRUE;
                   RunIsImportRunBln := TRUE;
                 END;

    }
    CONTROLS
    {
      { 1100409010;;Container;
                  Name=Imp. Service Contract OBJ;
                  CaptionML=ENU=Imp. Service Contract OBJ;
                  ContainerType=ContentArea }

      { 1100409009;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525001;2;Field  ;
                  CaptionML=[ENU=Create new Service Location Nos;
                             SVE=Skapa nya serviceplatsnr];
                  SourceExpr=CreateNewNosBln;
                  OnValidate=BEGIN
                               IF NOT CreateNewNosBln THEN
                                 CommentCde := '';
                             END;
                              }

      { 1100525002;2;Field  ;
                  Name=ComCde;
                  CaptionML=[ENU=Comment Code for storing old Service Location No.;
                             SVE=Kommentarskod f”r lagring av gammalt serviceplatsnr];
                  SourceExpr=CommentCde;
                  TableRelation="Comment Code" }

      { 1100525003;2;Field  ;
                  CaptionML=[ENU=Lookup Customer No. based on old Customer No.;
                             SVE=Val av kundnr baserat p† gammalt kundnr];
                  SourceExpr=UseOldCustNoBln;
                  OnValidate=BEGIN
                               IF NOT UseOldCustNoBln THEN
                                 CustCommentCde := '';
                             END;
                              }

      { 1100525004;2;Field  ;
                  Name=CustComCde;
                  CaptionML=[ENU=Comment Code for searching Customer No.;
                             SVE=Kommentarskod f”r s”kning efter kundnr];
                  SourceExpr=CustCommentCde;
                  TableRelation="Comment Code" }

    }
  }
  CODE
  {
    VAR
      CommentRec@1100525045 : Record 97;
      ConvMgt@1100525044 : Codeunit 11020212;
      XmlPortID@1100525041 : Integer;
      MainTableID@1100525040 : Integer;
      MainTableLookupPageID@1100527701 : Integer;
      XMLPortRequestPageID@1100527700 : Integer;
      CreateNewNosBln@1100525039 : Boolean;
      ImpLineCounter@1100525038 : Integer;
      CommentCde@1100525037 : Code[10];
      ConversionFileFormat@1100525036 : Record 11020691;
      RunIsImportRunBln@1100525035 : Boolean;
      UseOldCustNoBln@1100525031 : Boolean;
      CustCommentCde@1100525029 : Code[20];
      CustCde@1100525028 : Code[20];
      RDec@1100525008 : Decimal;
      SDec@1100525007 : Decimal;
      PostCode@1100525003 : Record 225;

    PROCEDURE StripData@1100485001();
    BEGIN
      A := DELCHR(A,'<>',' ');
      B := DELCHR(B,'<>',' ');
      G := DELCHR(G,'<>',' ');
      H := DELCHR(H,'<>',' ');
      I := DELCHR(I,'<>',' ');
      J := DELCHR(J,'<>',' ');
      O := DELCHR(O,'<>',' ');
      P := DELCHR(P,'<>',' ');
      Q := DELCHR(Q,'<>',' ');
      W := DELCHR(W,'<>',' ');
    END;

    PROCEDURE AddConversionFileFormatLine@1100529901();
    BEGIN
      // This function stores the file format into table [Conversion File Format].
      // There is no other standard documentation; so please maintain this function in case of customization.
      //
      // Parameters: 1: [Dataport No.],
      //             2: [csv-Column],
      //             3: [NAV Table ID],
      //             4: [NAV Field Name],
      //             5: [Data Type of dataport field],
      //             6: [extra Comment].
      //
      WITH ConversionFileFormat DO BEGIN
        ConversionFileFormat.DeleteColumnLines(XmlPortID);
        InsertColumnLine(XmlPortID,'A',MainTableID,"Service Location".FIELDNAME("No."),'Code20','OldOrNewTxt');
        InsertColumnLine(XmlPortID,'B',MainTableID,"Service Location".FIELDNAME("Customer No."),'Code20','OldTxt');
        InsertColumnLine(XmlPortID,'C',MainTableID,"Service Location".FIELDNAME(Description),'Text50','');
        InsertColumnLine(XmlPortID,'D',MainTableID,"Service Location".FIELDNAME("Description 2"),'Text50','');
        InsertColumnLine(XmlPortID,'E',MainTableID,"Service Location".FIELDNAME(Address),'Text100','');
        InsertColumnLine(XmlPortID,'F',MainTableID,"Service Location".FIELDNAME("Address 2"),'Text50','');
        InsertColumnLine(XmlPortID,'G',MainTableID,"Service Location".FIELDNAME("Post Code"),'Code20','');
        InsertColumnLine(XmlPortID,'H',MainTableID,"Service Location".FIELDNAME(City),'Text30','');
        InsertColumnLine(XmlPortID,'I',MainTableID,"Service Location".FIELDNAME("Country/Region Code"),'Code10','');
        InsertColumnLine(XmlPortID,'J',MainTableID,"Service Location".FIELDNAME("Territory Code"),'Code10','');
        InsertColumnLine(XmlPortID,'K',MainTableID,"Service Location".FIELDNAME("Phone No."),'Text30','');
        InsertColumnLine(XmlPortID,'L',MainTableID,"Service Location".FIELDNAME("Fax No."),'Text30','');
        InsertColumnLine(XmlPortID,'M',MainTableID,"Service Location".FIELDNAME("Global Dimension 1 Code"),'Code20','');
        InsertColumnLine(XmlPortID,'N',MainTableID,"Service Location".FIELDNAME(Type),'Code10','');
        InsertColumnLine(XmlPortID,'O',MainTableID,"Service Location".FIELDNAME("Search Name"),'Code50','');
        InsertColumnLine(XmlPortID,'P',MainTableID,"Service Location".FIELDNAME("Customer Feature"),'Text30','');
        InsertColumnLine(XmlPortID,'Q',MainTableID,"Service Location".FIELDNAME("Complex Code"),'Code10','');
        InsertColumnLine(XmlPortID,'R',MainTableID,"Service Location".FIELDNAME("Travel Distance"),'Decimal','');
        InsertColumnLine(XmlPortID,'S',MainTableID,"Service Location".FIELDNAME("Travel Time"),'Decimal','');
        InsertColumnLine(XmlPortID,'T',MainTableID,"Service Location".FIELDNAME("Travel Cost Code"),'Code10','');
        InsertColumnLine(XmlPortID,'U',MainTableID,"Service Location".FIELDNAME("Mobile Phone No."),'Text30','');
        InsertColumnLine(XmlPortID,'V',MainTableID,"Service Location".FIELDNAME("E-Mail"),'Text80','');
        InsertColumnLine(XmlPortID,'W',MainTableID,"Service Location".FIELDNAME("Contact Name"),'Text100','');
      END;
    END;

    PROCEDURE ClearFields@1100529900();
    BEGIN
      CLEAR("Service Location");
      CLEAR(PostCode);

      A := '';
      B := '';
      C := '';
      D := '';
      E := '';
      F := '';
      G := '';
      H := '';
      I := '';
      J := '';
      K := '';
      L := '';
      M := '';
      N := '';
      O := '';
      P := '';
      Q := '';
      R := '';
      S := '';
      T := '';
      U := '';
      V := '';
      W := '';
      RDec := 0;
      SDec := 0;
    END;

    PROCEDURE InitXMLport@1100529500(iCreateNewNosBln@1100529503 : Boolean;iCommentCde@1100529502 : Code[10];iUseOldCustNoBln@1100529501 : Boolean;iCustCommentCde@1100529500 : Code[20]);
    BEGIN
      CreateNewNosBln := iCreateNewNosBln;
      CommentCde := iCommentCde;
      UseOldCustNoBln := iUseOldCustNoBln;
      CustCommentCde := iCustCommentCde;
    END;

    BEGIN
    {
      // Old No.'s are stored in Comment Table (97), marked with [CommentCde].
      // Object No is stored in [XmlPortID]. In case of customisation (copy of dataport) change in OnInitDataport;
      //
      // Please use [AddConversionFileFormatLine] in case Import file format is changed.
    }
    END.
  }
}

