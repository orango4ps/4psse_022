OBJECT Codeunit 11128504 E-Invoice Check Fin. Chrg.Memo
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO8.00 (10613+10617);
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CompanyInfo@1080000 : Record 79;
      GLSetup@1080001 : Record 98;
      EInvoiceDocumentEncode@1080002 : Codeunit 11128501;
      CompanyInfoRead@1080003 : Boolean;
      GLSetupRead@1080004 : Boolean;
      EmptyDescErr@1080006 : TextConst 'ENU=The Finance Charge Memo %1 contains lines in which the Type and the No. are specified, but the Description is empty. This is not allowed for an E-Invoice document which might be created from the posted document.;NOR=Finansnota %1 inneholder linjer der typen og nummeret er angitt, men der beskrivelsen er tom. Dette er ikke tillatt for et e-fakturadokument som kan opprettes fra det bokf›rte dokumentet.';
      EmptyFieldsQst@1080007 : TextConst 'ENU=The Finance Charge Memo %1 contains lines in which either the Type or the No. is empty. Lines that contain these empty fields are not taken into account when you create an E-Invoice document. Do you want to continue?;NOR=Finansnota %1 inneholder linjer der typen og nummeret er tom. Linjer som inneholder disse tomme feltene, tas ikke i betraktning n†r du oppretter et e-fakturadokument. Vil du fortsette?';
      InterruptedIssuanceErr@1080009 : TextConst 'ENU=The issuance of the document has been interrupted.;NOR=Utstedelsen av dokumentet er avbrutt.';
      InvalidPathErr@1080100 : TextConst 'ENU=does not contain a valid path;NOR=inneholder ikke en gyldig bane;SVE=inneh†ller inte en giltig s”kv„g';

    PROCEDURE CheckFinChrgMemo@1000000001(lRec@1000000000 : Record 302);
    BEGIN
      WITH lRec DO BEGIN
        IF "E-Invoice" THEN BEGIN
          ReadCompanyInfo;
          ReadGLSetup;

          "Currency Code" := EInvoiceDocumentEncode.GetEInvoiceCurrencyCode("Currency Code");

          CheckCompanyInfo;

          TESTFIELD(Name);
          TESTFIELD(Address);
          TESTFIELD(City);
          TESTFIELD("Post Code");
          TESTFIELD("Country/Region Code");
          EInvoiceDocumentEncode.GetEInvoiceCountryRegionCode("Country/Region Code");
          TESTFIELD("VAT Registration No.");
          TESTFIELD("Your Reference");

          CheckFinChargeMemoLines(lRec);
        END;
      END;
    END;

    LOCAL PROCEDURE ReadCompanyInfo@1080010();
    BEGIN
      IF NOT CompanyInfoRead THEN BEGIN
        CompanyInfo.GET;
        CompanyInfoRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE ReadGLSetup@1080011();
    BEGIN
      IF NOT GLSetupRead THEN BEGIN
        GLSetup.GET;
        GLSetupRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE CheckFinChargeMemoLines@1080012(FinChargeMemoHeader@1160000 : Record 302);
    VAR
      FinChargeMemoLine@1160001 : Record 303;
      EmptyLineFound@1160002 : Boolean;
    BEGIN
      EmptyLineFound := FALSE;
      WITH FinChargeMemoLine DO BEGIN
        RESET;
        SETRANGE("Finance Charge Memo No.",FinChargeMemoHeader."No.");
        IF FINDSET THEN
          REPEAT
            IF Description = '' THEN
              IF (Type <> Type::" ") AND ("No." <> '') THEN
                ERROR(EmptyDescErr,"Finance Charge Memo No.");
            IF (Type = Type::" ") OR ("No." = '') THEN
              EmptyLineFound := TRUE;
          UNTIL (NEXT = 0);

        IF EmptyLineFound THEN
          IF NOT CONFIRM(EmptyFieldsQst,TRUE,"Finance Charge Memo No.") THEN
            ERROR(InterruptedIssuanceErr);
      END;
    END;

    PROCEDURE CheckIssFinChrg@1000000000(lRec@1000000000 : Record 304);
    BEGIN
      CheckCompanyInfo;
      CheckSalesSetup;
      CheckFinChargeMemoHeader(lRec);
    END;

    LOCAL PROCEDURE CheckCompanyInfo@1080000();
    VAR
      CompanyInfo@1080000 : Record 79;
    BEGIN
      CompanyInfo.GET;

      CompanyInfo.TESTFIELD(Name);
      CompanyInfo.TESTFIELD(Address);
      CompanyInfo.TESTFIELD(City);
      CompanyInfo.TESTFIELD("Post Code");
      CompanyInfo.TESTFIELD("Country/Region Code");
      EInvoiceDocumentEncode.GetEInvoiceCountryRegionCode(CompanyInfo."Country/Region Code");
      CompanyInfo.TESTFIELD("VAT Registration No.");
    END;

    LOCAL PROCEDURE CheckSalesSetup@1080001();
    VAR
      SalesSetup@1080001 : Record 311;
      FileMgt@1080000 : Codeunit 419;
    BEGIN
      SalesSetup.GET;
      SalesSetup.TESTFIELD("E-Invoice Fin. Charge Path");
      IF NOT FileMgt.DirectoryExistsOnDotNetClient(SalesSetup."E-Invoice Fin. Charge Path") THEN
        SalesSetup.FIELDERROR("E-Invoice Fin. Charge Path",InvalidPathErr);
    END;

    LOCAL PROCEDURE CheckFinChargeMemoHeader@1080002(IssuedFinChargeMemoHeader@1080000 : Record 304);
    BEGIN
      WITH IssuedFinChargeMemoHeader DO BEGIN
        TESTFIELD(Name);
        TESTFIELD(Address);
        TESTFIELD(City);
        TESTFIELD("Post Code");
        TESTFIELD("Country/Region Code");
        EInvoiceDocumentEncode.GetEInvoiceCountryRegionCode("Country/Region Code");
      END;
    END;

    BEGIN
    END.
  }
}

