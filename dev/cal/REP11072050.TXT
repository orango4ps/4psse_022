OBJECT Report 11072050 Delete Interaction Log Entries
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00 (5190);
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aktiv.-Protokollposten l”schen;
               ENU=Delete Interaction Log Entries;
               NLD=Interactielogposten verw.;
               NOR=Slett interaksjonsloggtransaksjoner;
               SVE=Ta bort interaktionsloggtrans.];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   MESSAGE(Text000,NoOfInteractions,"Interaction Log Entry".TABLECAPTION);
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 3056;    ;DataItem;                    ;
               DataItemTable=Table11072027;
               DataItemTableView=SORTING(Canceled,Campaign No.,Campaign Entry No.,Date)
                                 WHERE(Canceled=CONST(Yes));
               OnAfterGetRecord=VAR
                                  SalesHeader@1000 : Record 36;
                                  PurchHeader@1001 : Record 38;
                                BEGIN
                                  NoOfInteractions := NoOfInteractions + 1;

                                  IF "Version No." <> 0 THEN
                                    CASE "Document Type" OF
                                      "Document Type"::"Sales Qte.":
                                        SetSalesDocInteraction("Interaction Log Entry",SalesHeader."Document Type"::Quote);
                                      "Document Type"::"Sales Ord. Cnfrmn.":
                                        SetSalesDocInteraction("Interaction Log Entry",SalesHeader."Document Type"::Order);
                                      "Document Type"::"Sales Blnkt. Ord":
                                        SetSalesDocInteraction("Interaction Log Entry",SalesHeader."Document Type"::"Blanket Order");
                                      "Document Type"::"Purch.Qte.":
                                        SetPurchDocInteraction("Interaction Log Entry",PurchHeader."Document Type"::Quote);
                                      "Document Type"::"Purch. Ord.":
                                        SetPurchDocInteraction("Interaction Log Entry",PurchHeader."Document Type"::Order);
                                      "Document Type"::"Purch. Blnkt. Ord.":
                                        SetPurchDocInteraction("Interaction Log Entry",PurchHeader."Document Type"::"Blanket Order");
                                    END;
                                  DELETE(TRUE);
                                END;

               ReqFilterFields=Entry No.,Contact No.,Date,Campaign No.,Interaction Group Code,Interaction Template Code,Salesperson Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 %2 wurde gel”scht.;ENU=%1 %2 has been deleted.;NLD=%1 %2 is verwijderd.;NOR=%1 %2 har blitt fjernet.;SVE=%1 %2 har tagits bort.';
      NoOfInteractions@1003 : Integer;

    LOCAL PROCEDURE SetPurchDocInteraction@1(InteractionLogEntry@1001 : Record 11072027;DocumentType@1000 : Option);
    VAR
      PurchHeader@1002 : Record 38;
      PurchHeaderArchive@1003 : Record 11072057;
    BEGIN
      WITH InteractionLogEntry DO BEGIN
        PurchHeaderArchive.GET(DocumentType,"Document No.","Doc. No. Occurrence","Version No.");
        PurchHeader.SETRANGE("Document Type",DocumentType);
        PurchHeader.SETRANGE("No.","Document No.");
        PurchHeader.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
        IF PurchHeader.FINDFIRST THEN BEGIN
          PurchHeaderArchive."Interaction Exist" := FALSE;
          PurchHeaderArchive.MODIFY;
        END ELSE
          PurchHeaderArchive.DELETE(TRUE);
      END;
    END;

    LOCAL PROCEDURE SetSalesDocInteraction@5(InteractionLogEntry@1001 : Record 11072027;DocumentType@1000 : Option);
    VAR
      SalesHeader@1002 : Record 36;
      SalesHeaderArchive@1003 : Record 11072055;
    BEGIN
      WITH InteractionLogEntry DO BEGIN
        SalesHeaderArchive.GET(DocumentType,"Document No.","Doc. No. Occurrence","Version No.");
        SalesHeader.SETRANGE("Document Type",DocumentType);
        SalesHeader.SETRANGE("No.","Document No.");
        SalesHeader.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
        IF SalesHeader.FINDFIRST THEN BEGIN
          SalesHeaderArchive."Interaction Exist" := FALSE;
          SalesHeaderArchive.MODIFY;
        END ELSE
          SalesHeaderArchive.DELETE(TRUE);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

