OBJECT Codeunit 5816 Undo Return Receipt Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    TableNo=6661;
    Permissions=TableData 37=imd,
                TableData 6507=ri,
                TableData 6509=rimd,
                TableData 6661=imd;
    OnRun=VAR
            IsHandled@1000 : Boolean;
            SkipTypeCheck@1001 : Boolean;
          BEGIN
            IsHandled := FALSE;
            SkipTypeCheck := FALSE;
            OnBeforeOnRun(Rec,IsHandled,SkipTypeCheck);
            IF IsHandled THEN
              EXIT;

            IF NOT SkipTypeCheck THEN BEGIN
              SETRANGE(Type,Type::Item);
              IF NOT FIND('-') THEN
                ERROR(Text005);
            END;

            IF NOT HideDialog THEN
              IF NOT CONFIRM(Text000) THEN
                EXIT;

            ReturnRcptLine.COPY(Rec);
            Code;
            Rec := ReturnRcptLine;
          END;

  }
  CODE
  {
    VAR
      ReturnRcptLine@1001 : Record 6661;
      TempWhseJnlLine@1016 : TEMPORARY Record 7311;
      TempGlobalItemLedgEntry@1029 : TEMPORARY Record 32;
      TempGlobalItemEntryRelation@1022 : TEMPORARY Record 6507;
      InvtSetup@1004 : Record 313;
      UndoPostingMgt@1005 : Codeunit 5817;
      ItemJnlPostLine@1000 : Codeunit 22;
      Text000@1007 : TextConst 'ENU=Do you really want to undo the selected Return Receipt lines?;NOR=Er du sikker p† at du vil angre de merkede returseddellinjene?;SVE=Vill du verkligen †terst„lla de valda returinleveransraderna?';
      Text001@1006 : TextConst 'ENU=Undo quantity posting...;NOR=Angre antallsbokf›ring...;SVE=terst„ll antalsbokf”ring...';
      Text002@1002 : TextConst 'ENU=There is not enough space to insert correction lines.;NOR=Det er ikke nok plass til † sette inn korrigeringslinjer.;SVE=Det finns inte tillr„ckligt med utrymme att infoga r„ttningsrader.';
      WhseUndoQty@1014 : Codeunit 7320;
      InvtAdjmt@1009 : Codeunit 5895;
      HideDialog@1008 : Boolean;
      Text003@1011 : TextConst 'ENU=Checking lines...;NOR=Kontrollerer linjer...;SVE=Kontrollerar rader...';
      NextLineNo@1015 : Integer;
      Text004@1012 : TextConst 'ENU=This receipt has already been invoiced. Undo Return Receipt can be applied only to posted, but not invoiced receipts.;NOR=Dette mottaket er allerede fakturert. Angre returseddel kan bare brukes p† bokf›rte, men ikke fakturerte mottak.;SVE=Den h„r inleveransen har redan fakturerats. terst„ll returinleverans kan bara anv„ndas f”r inleveranser som har bokf”rts men inte fakturerats.';
      Text005@1010 : TextConst 'ENU=Undo Return Receipt can be performed only for lines of type Item. Please select a line of the Item type and repeat the procedure.;NOR=Angre returseddel kan bare utf›res for linjer av typen Vare. Velg en linje av typen Vare og gjenta prosedyren.;SVE=terst„ll returinleverans kan bara anv„ndas f”r rader av typen Artikel. V„lj en rad av typen Artikel och f”rs”k igen.';
      AlreadyReversedErr@1013 : TextConst 'ENU=This return receipt has already been reversed.;NOR=Denne returseddelen er allerede tilbakef›rt.';

    [External]
    PROCEDURE SetHideDialog@7(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    LOCAL PROCEDURE Code@2();
    VAR
      PostedWhseRcptLine@1003 : Record 7319;
      SalesLine@1005 : Record 37;
      Window@1001 : Dialog;
      ItemShptEntryNo@1002 : Integer;
      DocLineNo@1000 : Integer;
      PostedWhseRcptLineFound@1004 : Boolean;
    BEGIN
      WITH ReturnRcptLine DO BEGIN
        CLEAR(ItemJnlPostLine);
        SETRANGE(Correction,FALSE);

        REPEAT
          IF NOT HideDialog THEN
            Window.OPEN(Text003);
          CheckReturnRcptLine(ReturnRcptLine);
        UNTIL NEXT = 0;

        FIND('-');
        REPEAT
          TempGlobalItemLedgEntry.RESET;
          IF NOT TempGlobalItemLedgEntry.ISEMPTY THEN
            TempGlobalItemLedgEntry.DELETEALL;
          TempGlobalItemEntryRelation.RESET;
          IF NOT TempGlobalItemEntryRelation.ISEMPTY THEN
            TempGlobalItemEntryRelation.DELETEALL;

          IF NOT HideDialog THEN
            Window.OPEN(Text001);

          PostedWhseRcptLineFound :=
            WhseUndoQty.FindPostedWhseRcptLine(
              PostedWhseRcptLine,
              DATABASE::"Return Receipt Line",
              "Document No.",
              DATABASE::"Sales Line",
              SalesLine."Document Type"::"Return Order",
              "Return Order No.",
              "Return Order Line No.");

          ItemShptEntryNo := PostItemJnlLine(ReturnRcptLine,DocLineNo);

          InsertNewReceiptLine(ReturnRcptLine,ItemShptEntryNo,DocLineNo);
          OnAfterInsertNewReceiptLine(ReturnRcptLine,PostedWhseRcptLine,PostedWhseRcptLineFound,DocLineNo);

          SalesLine.GET(SalesLine."Document Type"::"Return Order","Return Order No.",
            "Return Order Line No.");
          IF "Item Rcpt. Entry No." > 0 THEN
            IF SalesLine."Appl.-from Item Entry" <> 0 THEN BEGIN
              SalesLine."Appl.-from Item Entry" := ItemShptEntryNo;
              SalesLine.MODIFY;
            END;

          IF PostedWhseRcptLineFound THEN
            WhseUndoQty.UndoPostedWhseRcptLine(PostedWhseRcptLine);

          UpdateOrderLine(ReturnRcptLine);
          UpdateItemTrkgApplFromEntry(SalesLine);
          IF PostedWhseRcptLineFound THEN
            WhseUndoQty.UpdateRcptSourceDocLines(PostedWhseRcptLine);

          "Quantity Invoiced" := Quantity;
          "Qty. Invoiced (Base)" := "Quantity (Base)";
          "Return Qty. Rcd. Not Invd." := 0;
          Correction := TRUE;

          OnBeforeReturnRcptLineModify(ReturnRcptLine);
          MODIFY;
          OnAfterReturnRcptLineModify(ReturnRcptLine);
        UNTIL NEXT = 0;

        InvtSetup.GET;
        IF InvtSetup."Automatic Cost Adjustment" <>
           InvtSetup."Automatic Cost Adjustment"::Never
        THEN BEGIN
          InvtAdjmt.SetProperties(TRUE,InvtSetup."Automatic Cost Posting");
          InvtAdjmt.SetJobUpdateProperties(TRUE);
          InvtAdjmt.MakeMultiLevelAdjmt;
        END;

        WhseUndoQty.PostTempWhseJnlLine(TempWhseJnlLine);
      END;

      OnAfterCode(ReturnRcptLine);
    END;

    LOCAL PROCEDURE CheckReturnRcptLine@3(ReturnRcptLine@1001 : Record 6661);
    VAR
      TempItemLedgEntry@1006 : TEMPORARY Record 32;
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCheckReturnRcptLine(ReturnRcptLine,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH ReturnRcptLine DO BEGIN
        TESTFIELD(Type,Type::Item);
        IF Correction THEN
          ERROR(AlreadyReversedErr);
        IF "Return Qty. Rcd. Not Invd." <> Quantity THEN
          ERROR(Text004);

        UndoPostingMgt.TestReturnRcptLine(ReturnRcptLine);
        UndoPostingMgt.CollectItemLedgEntries(TempItemLedgEntry,DATABASE::"Return Receipt Line",
          "Document No.","Line No.","Quantity (Base)","Item Rcpt. Entry No.");
        UndoPostingMgt.CheckItemLedgEntries(TempItemLedgEntry,"Line No.");
      END;
    END;

    LOCAL PROCEDURE PostItemJnlLine@5(ReturnRcptLine@1000 : Record 6661;VAR DocLineNo@1003 : Integer) : Integer;
    VAR
      ItemJnlLine@1001 : Record 83;
      SalesLine@1004 : Record 37;
      SourceCodeSetup@1002 : Record 242;
      ReturnRcptHeader@1010 : Record 6660;
      ReturnRcptLine2@1007 : Record 6661;
      TempApplyToEntryList@1005 : TEMPORARY Record 32;
      LineSpacing@1006 : Integer;
      ItemLedgEntryNo@1009 : Integer;
      IsHandled@1008 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforePostItemJnlLine(ReturnRcptLine,DocLineNo,ItemLedgEntryNo,IsHandled);
      IF IsHandled THEN
        EXIT(ItemLedgEntryNo);

      WITH ReturnRcptLine DO BEGIN
        ReturnRcptLine2.SETRANGE("Document No.","Document No.");
        ReturnRcptLine2."Document No." := "Document No.";
        ReturnRcptLine2."Line No." := "Line No.";
        ReturnRcptLine2.FIND('=');

        IF ReturnRcptLine2.FIND('>') THEN BEGIN
          LineSpacing := (ReturnRcptLine2."Line No." - "Line No.") DIV 2;
          IF LineSpacing = 0 THEN
            ERROR(Text002);
        END ELSE
          LineSpacing := 10000;
        DocLineNo := "Line No." + LineSpacing;

        SourceCodeSetup.GET;
        ReturnRcptHeader.GET("Document No.");
        ItemJnlLine.INIT;
        ItemJnlLine."Entry Type" := ItemJnlLine."Entry Type"::Sale;
        ItemJnlLine."Item No." := "No.";
        ItemJnlLine."Posting Date" := ReturnRcptHeader."Posting Date";
        ItemJnlLine."Document No." := "Document No.";
        ItemJnlLine."Document Line No." := DocLineNo;
        ItemJnlLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        ItemJnlLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        ItemJnlLine."Location Code" := "Location Code";
        ItemJnlLine."Source Code" := SourceCodeSetup.Sales;
        ItemJnlLine."Applies-to Entry" := "Item Rcpt. Entry No.";
        ItemJnlLine.Correction := TRUE;
        ItemJnlLine."Variant Code" := "Variant Code";
        ItemJnlLine."Bin Code" := "Bin Code";
        ItemJnlLine.Quantity := Quantity;
        ItemJnlLine."Quantity (Base)" := "Quantity (Base)";
        ItemJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        ItemJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        ItemJnlLine."Document Date" := ReturnRcptHeader."Document Date";

        OnAfterCopyItemJnlLineFromReturnRcpt(ItemJnlLine,ReturnRcptHeader,ReturnRcptLine);

        WhseUndoQty.InsertTempWhseJnlLine(ItemJnlLine,
          DATABASE::"Sales Line",
          SalesLine."Document Type"::"Return Order",
          "Return Order No.",
          "Return Order Line No.",
          TempWhseJnlLine."Reference Document"::"Posted Rtrn. Rcpt.",
          TempWhseJnlLine,
          NextLineNo);

        IF "Item Rcpt. Entry No." <> 0 THEN BEGIN
          ItemJnlPostLine.RUN(ItemJnlLine);
          EXIT(ItemJnlLine."Item Shpt. Entry No.");
        END;
        UndoPostingMgt.CollectItemLedgEntries(TempApplyToEntryList,DATABASE::"Return Receipt Line",
          "Document No.","Line No.","Quantity (Base)","Item Rcpt. Entry No.");

        UndoPostingMgt.PostItemJnlLineAppliedToList(ItemJnlLine,TempApplyToEntryList,
          Quantity,"Quantity (Base)",TempGlobalItemLedgEntry,TempGlobalItemEntryRelation);

        EXIT(0); // "Item Shpt. Entry No."
      END;
    END;

    LOCAL PROCEDURE InsertNewReceiptLine@1(OldReturnRcptLine@1000 : Record 6661;ItemShptEntryNo@1001 : Integer;DocLineNo@1004 : Integer);
    VAR
      NewReturnRcptLine@1002 : Record 6661;
    BEGIN
      WITH OldReturnRcptLine DO BEGIN
        NewReturnRcptLine.INIT;
        NewReturnRcptLine.COPY(OldReturnRcptLine);
        NewReturnRcptLine."Line No." := DocLineNo;
        NewReturnRcptLine."Appl.-from Item Entry" := "Item Rcpt. Entry No.";
        NewReturnRcptLine."Item Rcpt. Entry No." := ItemShptEntryNo;
        NewReturnRcptLine.Quantity := -Quantity;
        NewReturnRcptLine."Return Qty. Rcd. Not Invd." := 0;
        NewReturnRcptLine."Quantity (Base)" := -"Quantity (Base)";
        NewReturnRcptLine."Quantity Invoiced" := NewReturnRcptLine.Quantity;
        NewReturnRcptLine."Qty. Invoiced (Base)" := NewReturnRcptLine."Quantity (Base)";
        NewReturnRcptLine.Correction := TRUE;
        NewReturnRcptLine."Dimension Set ID" := "Dimension Set ID";
        OnBeforeNewReturnRcptLineInsert(NewReturnRcptLine,OldReturnRcptLine);
        NewReturnRcptLine.INSERT;
        OnAfterNewReturnRcptLineInsert(NewReturnRcptLine,OldReturnRcptLine);

        InsertItemEntryRelation(TempGlobalItemEntryRelation,NewReturnRcptLine);
      END;
    END;

    [External]
    PROCEDURE UpdateOrderLine@4(ReturnRcptLine@1000 : Record 6661);
    VAR
      SalesLine@1001 : Record 37;
    BEGIN
      WITH ReturnRcptLine DO BEGIN
        SalesLine.GET(SalesLine."Document Type"::"Return Order","Return Order No.","Return Order Line No.");
        UndoPostingMgt.UpdateSalesLine(SalesLine,Quantity,"Quantity (Base)",TempGlobalItemLedgEntry);
        OnAfterUpdateSalesLine(ReturnRcptLine,SalesLine);
      END;
    END;

    LOCAL PROCEDURE InsertItemEntryRelation@13(VAR TempItemEntryRelation@1003 : TEMPORARY Record 6507;NewReturnRcptLine@1000 : Record 6661);
    VAR
      ItemEntryRelation@1001 : Record 6507;
    BEGIN
      IF TempItemEntryRelation.FIND('-') THEN
        REPEAT
          ItemEntryRelation := TempItemEntryRelation;
          ItemEntryRelation.TransferFieldsReturnRcptLine(NewReturnRcptLine);
          ItemEntryRelation.INSERT;
        UNTIL TempItemEntryRelation.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateItemTrkgApplFromEntry@14(SalesLine@1000 : Record 37);
    VAR
      ReservationEntry@1001 : Record 337;
      ItemApplicationEntry@1002 : Record 339;
      SalesLineReserve@1003 : Codeunit 99000832;
    BEGIN
      SalesLineReserve.FilterReservFor(ReservationEntry,SalesLine);
      IF ReservationEntry.FINDSET THEN
        REPEAT
          IF ReservationEntry."Appl.-from Item Entry" <> 0 THEN
            IF ItemApplicationEntry.AppliedOutbndEntryExists(ReservationEntry."Item Ledger Entry No.",FALSE,FALSE) THEN BEGIN
              ReservationEntry."Appl.-from Item Entry" := ItemApplicationEntry."Outbound Item Entry No.";
              ReservationEntry.MODIFY;
            END;
        UNTIL ReservationEntry.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCode@10(VAR ReturnReceiptLine@1000 : Record 6661);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyItemJnlLineFromReturnRcpt@11(VAR ItemJournalLine@1000 : Record 83;ReturnReceiptHeader@1001 : Record 6660;ReturnReceiptLine@1002 : Record 6661);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertNewReceiptLine@9(VAR ReturnReceiptLine@1000 : Record 6661;VAR PostedWhseReceiptLine@1001 : Record 7319;PostedWhseRcptLineFound@1002 : Boolean;DocLineNo@1003 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterNewReturnRcptLineInsert@16(VAR NewReturnReceiptLine@1000 : Record 6661;OldReturnReceiptLine@1001 : Record 6661);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReturnRcptLineModify@6(VAR ReturnRcptLine@1000 : Record 6661);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateSalesLine@17(VAR ReturnRcptLine@1000 : Record 6661;VAR SalesLine@1001 : Record 37);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckReturnRcptLine@8(VAR ReturnReceiptLine@1000 : Record 6661;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeOnRun@12(VAR ReturnReceiptLine@1000 : Record 6661;VAR IsHandled@1001 : Boolean;VAR SkipTypeCheck@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeNewReturnRcptLineInsert@15(VAR NewReturnReceiptLine@1000 : Record 6661;OldReturnReceiptLine@1001 : Record 6661);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostItemJnlLine@18(VAR ReturnReceiptLine@1000 : Record 6661;DocLineNo@1001 : Integer;VAR ItemLedgEntryNo@1002 : Integer;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReturnRcptLineModify@19(VAR ReturnReceiptLine@1000 : Record 6661);
    BEGIN
    END;

    BEGIN
    END.
  }
}

