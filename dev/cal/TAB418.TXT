OBJECT Table 418 IC Inbox Transaction
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICInboxJnlLine@1000 : Record 419;
               ICInboxPurchHdr@1001 : Record 436;
               ICInboxSalesHdr@1002 : Record 434;
             BEGIN
               CASE "Source Type" OF
                 "Source Type"::Journal:
                   BEGIN
                     ICInboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICInboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxJnlLine.FINDFIRST THEN
                       ICInboxJnlLine.DELETEALL(TRUE);
                   END;
                 "Source Type"::"Sales Document":
                   BEGIN
                     ICInboxSalesHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxSalesHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxSalesHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxSalesHdr.FINDFIRST THEN
                       ICInboxSalesHdr.DELETE(TRUE);
                   END;
                 "Source Type"::"Purchase Document":
                   BEGIN
                     ICInboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxPurchHdr.FINDFIRST THEN
                       ICInboxPurchHdr.DELETE(TRUE);
                   END;
               END;
             END;

    CaptionML=[ENU=IC Inbox Transaction;
               NOR=Inng. KI-transaksjon;
               SVE=Konc.int. inkorgstrans.];
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=[ENU=Transaction No.;
                                                              NOR=Transaksjonsnr.;
                                                              SVE=Verifikationslîpnr];
                                                   Editable=No }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=[ENU=IC Partner Code;
                                                              NOR=KI-partnerkode;
                                                              SVE=Konc.int. partnerkod];
                                                   Editable=No }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=Journal,Sales Document,Purchase Document;
                                                                    NOR=Kladd,Salgsdokument,Kjõpsdokument;
                                                                    SVE=Journal,FîrsÑljningsdokument,Inkîpsdokument];
                                                   OptionString=Journal,Sales Document,Purchase Document;
                                                   Editable=No }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              NOR=Bilagstype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order";
                                                                    NOR=" ,Betaling,Faktura,Kreditnota,Refusjon,Ordre,Ordreretur";
                                                                    SVE=" ,Betalning,Faktura,Kreditnota,èterbetalning,Order,Returorder"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Refund,Order,Return Order];
                                                   Editable=No }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr];
                                                   Editable=No }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              NOR=Bokfõringsdato;
                                                              SVE=Bokfîringsdatum];
                                                   Editable=No }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=[ENU=Transaction Source;
                                                              NOR=Transaksjonskilde;
                                                              SVE=TransaktionskÑlla];
                                                   OptionCaptionML=[ENU=Returned by Partner,Created by Partner;
                                                                    NOR=Returnert av partner,Opprettet av partner;
                                                                    SVE=Returnerad av partner,Skapad av partner];
                                                   OptionString=Returned by Partner,Created by Partner;
                                                   Editable=No }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=[ENU=Document Date;
                                                              NOR=Bilagsdato;
                                                              SVE=Dokumentdatum];
                                                   Editable=No }
    { 10  ;   ;Line Action         ;Option        ;OnValidate=BEGIN
                                                                IF (("Line Action" = "Line Action"::"Return to IC Partner") OR ("Line Action" = "Line Action"::Accept)) AND
                                                                   ("Transaction Source" = "Transaction Source"::"Returned by Partner")
                                                                THEN
                                                                  ERROR(Text000,"Transaction No.","IC Partner Code");

                                                                IF "Line Action" = "Line Action"::Accept THEN
                                                                  InboxCheckAccept;
                                                              END;

                                                   CaptionML=[ENU=Line Action;
                                                              NOR=Linjehandling;
                                                              SVE=RadÜtgÑrd];
                                                   OptionCaptionML=[ENU=No Action,Accept,Return to IC Partner,Cancel;
                                                                    NOR=Ingen handling,Godta,GÜ tilbake til KI-partner,Avbryt;
                                                                    SVE=Ingen ÜtgÑrd,Acceptera,Returnera till Konc.int. partner,Avbryt];
                                                   OptionString=No Action,Accept,Return to IC Partner,Cancel }
    { 11  ;   ;Original Document No.;Code20       ;CaptionML=[ENU=Original Document No.;
                                                              NOR=Opprinnelig bilagsnr.;
                                                              SVE=Ursprungligt dokumentnr] }
    { 12  ;   ;IC Partner G/L Acc. No.;Code20     ;CaptionML=[ENU=IC Partner G/L Acc. No.;
                                                              NOR=Finanskontonr. for KI-partner;
                                                              SVE=Redov.ktonr konc.int. partner] }
    { 13  ;   ;Source Line No.     ;Integer       ;CaptionML=[ENU=Source Line No.;
                                                              NOR=Kildelinjenr.;
                                                              SVE=Ursprungsradnr] }
  }
  KEYS
  {
    {    ;Transaction No.,IC Partner Code,Transaction Source,Document Type;
                                                   Clustered=Yes }
    {    ;IC Partner Code                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Transaction No. %1 has been returned by IC Partner %2.\You can only cancel returned transactions.;NOR=Transaksjonsnr. %1 er returnert av KI-partneren %2.\Du kan bare avbryte returnerte transaksjoner.;SVE=Transaktionsnr %1 har returnerats av konc.int. partner %2.\Du kan endast avbryta returnerade transaktioner.';

    [External]
    PROCEDURE ShowDetails@1();
    VAR
      ICInBoxJnlLine@1005 : Record 419;
      ICInboxSalesHeader@1004 : Record 434;
      ICInboxPurchHeader@1003 : Record 436;
      ICInboxJnlLines@1002 : Page 616;
      ICInboxSalesDoc@1001 : Page 644;
      ICInboxPurchDoc@1000 : Page 646;
    BEGIN
      CASE "Source Type" OF
        "Source Type"::Journal:
          BEGIN
            ICInBoxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            ICInBoxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            ICInBoxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICInboxJnlLines);
            ICInboxJnlLines.SETTABLEVIEW(ICInBoxJnlLine);
            ICInboxJnlLines.RUNMODAL;
          END;
        "Source Type"::"Sales Document":
          BEGIN
            ICInboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICInboxSalesDoc);
            ICInboxSalesDoc.SETTABLEVIEW(ICInboxSalesHeader);
            ICInboxSalesDoc.RUNMODAL;
          END;
        "Source Type"::"Purchase Document":
          BEGIN
            ICInboxPurchHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxPurchHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxPurchHeader.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICInboxPurchDoc);
            ICInboxPurchDoc.SETTABLEVIEW(ICInboxPurchHeader);
            ICInboxPurchDoc.RUNMODAL;
          END;
      END;
    END;

    LOCAL PROCEDURE InboxCheckAccept@40();
    VAR
      ICInboxTransaction2@1001 : Record 418;
      HandledICInboxTrans@1002 : Record 420;
      Text001@1003 : TextConst 'ENU=Transaction No. %2 is a copy of Transaction No. %1, which has already been set to Accept.\Do you also want to accept Transaction No. %2?;NOR=Transaksjonsnummer %2 er en kopi av transaksjonsnummer %1, som allerede er definert som Godta.\Vil du ogsÜ godta transaksjonsnummer %2?;SVE=Transaktionsnr %2 Ñr en kopia av transaktionsnr %1 som redan har vÑrdet Acceptera.\Vill du Ñven acceptera transaktionsnr %2?';
      Text002@1000 : TextConst 'ENU=A copy of Transaction No. %1 has already been accepted and is now in the Handled IC Inbox Transactions window.\Do you also want to accept Transaction No. %1?;NOR=En kopi av transaksjonsnummer %1 er allerede akseptert, og er nÜ i vinduet HÜndterte inngÜende KI-transaksjoner.\Vil du ogsÜ sende transaksjonsnummer %1?;SVE=En kopia av transaktionsnr %1 har redan skickats till konc.int. partner och finns nu i fînstret Hanterade konc.int. inkorgstrans.\Vill du Ñven acceptera transaktionsnr %1?';
      ICInboxPurchHeader@1004 : Record 436;
      PurchHeader@1005 : Record 38;
      PurchInvHeader@1006 : Record 122;
      Text003@1007 : TextConst 'ENU=A purchase order already exists for transaction %1. If you accept and post this document, you should delete the original purchase order %2 to avoid duplicate postings.;NOR=Det finnes allerede en bestilling for transaksjonen %1. Hvis du godtar og bokfõrer dette bilaget, bõr du slette den opprinnelige bestillingen %2 for Ü unngÜ duplikatbokfõringer.;SVE=Det finns redan en inkîpsorder fîr %1. Om du accepterar och bokfîr det hÑr dokumentet ska du ta bort den ursprungliga inkîpsordern %2 fîr att undvika dubbla bokfîringar.';
      Text004@1008 : TextConst 'ENU=Purchase invoice %1 has already been posted for transaction %2. If you accept and post this document, you will have duplicate postings.\Are you sure you want to accept the transaction?;NOR=Kjõpsfakturaen %1 er allerede bokfõrt for transaksjonen %2. Hvis du godtar og bokfõrer dette bilaget, fÜr du duplikatbokfõringer.\Er du sikker pÜ at du vil godta transaksjonen?;SVE=Inkîpsfakturan %1 har redan bokfîrts fîr transaktionen %2. Om du accepterar och bokfîr det hÑr dokumentet, fÜr du dubbla bokfîringar.\ér du sÑker pÜ att du vill acceptera transaktionen?';
      ConfirmManagement@1009 : Codeunit 27;
      IsHandled@1010 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeInboxCheckAccept(Rec,IsHandled);
      IF IsHandled THEN
        EXIT;

      HandledICInboxTrans.SETRANGE("IC Partner Code","IC Partner Code");
      HandledICInboxTrans.SETRANGE("Document Type","Document Type");
      HandledICInboxTrans.SETRANGE("Source Type","Source Type");
      HandledICInboxTrans.SETRANGE("Document No.","Document No.");
      IF HandledICInboxTrans.FINDFIRST THEN
        IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text002,"Transaction No."),TRUE) THEN
          ERROR('');

      ICInboxTransaction2.SETRANGE("IC Partner Code","IC Partner Code");
      ICInboxTransaction2.SETRANGE("Document Type","Document Type");
      ICInboxTransaction2.SETRANGE("Source Type","Source Type");
      ICInboxTransaction2.SETRANGE("Document No.","Document No.");
      ICInboxTransaction2.SETFILTER("Transaction No.",'<>%1',"Transaction No.");
      ICInboxTransaction2.SETRANGE("IC Partner G/L Acc. No.","IC Partner G/L Acc. No.");
      ICInboxTransaction2.SETRANGE("Source Line No.","Source Line No.");
      ICInboxTransaction2.SETRANGE("Line Action","Line Action"::Accept);
      IF ICInboxTransaction2.FINDFIRST THEN
        IF NOT ConfirmManagement.ConfirmProcess(
             STRSUBSTNO(Text001,ICInboxTransaction2."Transaction No.","Transaction No."),TRUE)
        THEN
          ERROR('');
      IF ("Source Type" = "Source Type"::"Purchase Document") AND
         ("Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"])
      THEN BEGIN
        ICInboxPurchHeader.GET("Transaction No.","IC Partner Code","Transaction Source");
        IF ICInboxPurchHeader."Your Reference" <> '' THEN BEGIN
          PurchHeader.SETRANGE("Your Reference",ICInboxPurchHeader."Your Reference");
          IF PurchHeader.FINDFIRST THEN
            MESSAGE(Text003,ICInboxPurchHeader."IC Transaction No.",ICInboxPurchHeader."Your Reference")
          ELSE BEGIN
            PurchInvHeader.SETRANGE("Your Reference",ICInboxPurchHeader."Your Reference");
            IF PurchInvHeader.FINDFIRST THEN
              IF NOT ConfirmManagement.ConfirmProcess(
                   STRSUBSTNO(
                     Text004,ICInboxPurchHeader."Your Reference",
                     ICInboxPurchHeader."IC Transaction No."),TRUE)
              THEN
                "Line Action" := xRec."Line Action";
          END;
        END;
      END;

      OnAfterInboxCheckAccept(Rec);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInboxCheckAccept@3(VAR ICInboxTransaction@1000 : Record 418);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInboxCheckAccept@2(VAR ICInboxTransaction@1000 : Record 418;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

