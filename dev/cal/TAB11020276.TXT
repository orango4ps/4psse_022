OBJECT Table 11020276 Vendor WKA Check
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Modified by" := USERID;
               "Modified on" := CURRENTDATETIME;
               CentrDataMgt.UpdateVendorWKACheck(Rec,1,xRec);
             END;

    OnModify=BEGIN
               "Modified by" := USERID;
               "Modified on" := CURRENTDATETIME;
               CentrDataMgt.UpdateVendorWKACheck(Rec,2,xRec);
             END;

    OnDelete=BEGIN
               CentrDataMgt.UpdateVendorWKACheck(Rec,4,xRec);
             END;

    CaptionML=[ENU=Vendor WKA Check;
               NOR=WKA-kontroll, leverand›r;
               SVE=WKA-kontroll, leverant”r];
    LookupPageID=Page11020331;
    DrillDownPageID=Page11020331;
  }
  FIELDS
  {
    { 10  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Vendor Name");
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Leverant”rsnr] }
    { 15  ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              NOR=Leverand›rsnavn;
                                                              SVE=Leverant”rsnamn];
                                                   Editable=No }
    { 20  ;   ;Code                ;Code20        ;TableRelation="WKA Check";
                                                   OnValidate=BEGIN
                                                                IF Code <> xRec.Code THEN BEGIN
                                                                  VendorWKACheck := xRec;
                                                                  IF VendorWKACheck.FIND THEN
                                                                    CentrDataMgt.UpdateVendorWKACheck(Rec,3,xRec);
                                                                END;
                                                                CALCFIELDS(Description, "Check Type", Mandatory, Request);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 30  ;   ;Value               ;Text50        ;OnValidate=BEGIN
                                                                IF Value <> '' THEN BEGIN
                                                                  CALCFIELDS("Check Type");
                                                                  TESTFIELD("Check Type", "Check Type"::Value);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Value;
                                                              NOR=Verdi;
                                                              SVE=V„rde] }
    { 40  ;   ;Due Date            ;Date          ;OnValidate=BEGIN
                                                                IF "Due Date" <> 0D THEN BEGIN
                                                                  CALCFIELDS("Check Type");
                                                                  TESTFIELD("Check Type", "Check Type"::"Due Date");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Due Date;
                                                              NOR=Forfallsdato;
                                                              SVE=F”rfallodatum] }
    { 50  ;   ;Approved            ;Boolean       ;OnValidate=BEGIN
                                                                IF Approved THEN BEGIN
                                                                  CALCFIELDS("Check Type");
                                                                  TESTFIELD("Check Type", "Check Type"::Approved);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Approved;
                                                              NOR=Godkjent;
                                                              SVE=Godk„nd] }
    { 60  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("WKA Check".Description WHERE (Code=FIELD(Code)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 70  ;   ;Check Type          ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("WKA Check"."Check Type" WHERE (Code=FIELD(Code)));
                                                   CaptionML=[ENU=Check Type;
                                                              NOR=Sjekktype;
                                                              SVE=Checktyp];
                                                   OptionCaptionML=[ENU=" ,Value,Due Date,Approved";
                                                                    NOR=" ,Verdi,Forfallsdato,Godkjent";
                                                                    SVE=" ,V„rde,F”rfallodatum,Godk„nt"];
                                                   OptionString=[ ,Value,Due Date,Approved];
                                                   Editable=No }
    { 80  ;   ;Mandatory           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("WKA Check".Mandatory WHERE (Code=FIELD(Code)));
                                                   CaptionML=[ENU=Mandatory;
                                                              NOR=Obligatorisk;
                                                              SVE=Obligatoriskt];
                                                   Editable=No }
    { 90  ;   ;Extra Obligation Vendor;Boolean    ;OnValidate=BEGIN
                                                                IF "Extra Obligation Vendor" THEN BEGIN
                                                                  CALCFIELDS(Mandatory);
                                                                  TESTFIELD(Mandatory, FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Extra Obligation for this Vendor;
                                                              NOR=Ekstra Forbliktelse for denne leverand›r;
                                                              SVE=Extra †tagande f”r denna leverant”r] }
    { 100 ;   ;Request             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("WKA Check".Request WHERE (Code=FIELD(Code)));
                                                   CaptionML=[ENU=Request;
                                                              NOR=Foresp›rsel;
                                                              SVE=F”rfr†gan];
                                                   Editable=No }
    { 110 ;   ;Extra Request Vendor;Boolean       ;OnValidate=BEGIN
                                                                IF "Extra Request Vendor" THEN BEGIN
                                                                  CALCFIELDS(Request);
                                                                  TESTFIELD(Request, FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Extra Request for this Vendor;
                                                              NOR=Ekstra foresp›rsel for denne leverand›r;
                                                              SVE=Extra f”rfr†gan f”r denna leverant”r] }
    { 120 ;   ;Modified on         ;DateTime      ;CaptionML=[ENU=Modified on;
                                                              NOR=Endret den;
                                                              SVE=Žndrad den];
                                                   Editable=No }
    { 130 ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserCode@1210190001 : Code[50];
                                                              UserMgt@1210190000 : Codeunit 418;
                                                            BEGIN
                                                              UserCode := "Modified by";
                                                              UserMgt.LookupUserID(UserCode);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=Žndrad av];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Vendor No.,Code                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      VendorWKACheck@1100485000 : Record 11020276;
      CentrDataMgt@1100485001 : Codeunit 11012010;
      IgnoreDueDateOlderThan@1100485002 : Date;

    PROCEDURE DataIsValid@1100485000(DueDate@1100485000 : Date) : Boolean;
    BEGIN
      // IgnoreDueDateOlderThan is not added as an argument but set as an global var, to ensure custom reports keep working fine
      CALCFIELDS("Check Type");
      CASE "Check Type" OF
        "Check Type"::Value: EXIT(Value <> '');
        "Check Type"::"Due Date":
          BEGIN
            IF DueDate = 0D THEN
              DueDate := TODAY;
            EXIT(("Due Date" >= DueDate) OR ("Due Date" < IgnoreDueDateOlderThan));
          END;
        "Check Type"::Approved: EXIT(Approved);
      END;
    END;

    PROCEDURE SetIgnoreDueDateOlderThan@1100485001(lIgnoreDueDateOlderThan@1100485000 : Date);
    BEGIN
      IgnoreDueDateOlderThan := lIgnoreDueDateOlderThan;
    END;

    BEGIN
    END.
  }
}

