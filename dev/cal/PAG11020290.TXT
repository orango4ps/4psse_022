OBJECT Page 11020290 Means Sales Prices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Mittelverkauspreise;
               ENU=Means Sales Prices;
               NLD=Middelverkoopprijzen;
               NOR=Middel salgspriser;
               SVE=Medelf”rs„ljningspriser];
    SaveValues=Yes;
    SourceTable=Table11020246;
    DataCaptionExpr=GetCaption;
    DelayedInsert=Yes;
    PageType=ListPlus;
    OnInit=BEGIN
             SalesCodeFilterCtrlEnable := TRUE;
             "Sales CodeEditable" := TRUE;
           END;

    OnOpenPage=BEGIN
                 GetRecFilters;
                 SetRecFilters;
               END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                CaptionML=[DEU=Filter;
                           ENU=Filter;
                           NLD=Algemeen;
                           NOR=Filter;
                           SVE=Urval];
                GroupType=Group }

    { 28  ;2   ;Field     ;
                CaptionML=[DEU=Verkaufsartenfilter;
                           ENU=Sales Type Filter;
                           NLD=Verkoopsoortfilter;
                           NOR=Salgstypefilter;
                           SVE=F”rs.typfilter];
                OptionCaptionML=[DEU=Debitor,Debitorenpreisgruppe,Alle Debitoren,Kampagne,Keine;
                                 ENU=Customer,Customer Price Group,All Customers,Campaign,None;
                                 NLD=Klant,Klantenprijsgroep,Alle klanten,Campagne,Geen;
                                 NOR=Kunde,Kundeprisgruppe,Alle kunder,Kampanje,Ingen;
                                 SVE=Kund,Kundprisgrupp,Alla kunder,Kampanj,Ingen];
                SourceExpr=SalesTypeFilter;
                OnValidate=BEGIN
                             SalesTypeFilterOnAfterValidate;
                           END;
                            }

    { 26  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl;
                CaptionML=[DEU=Verkaufscodefilter;
                           ENU=Sales Code Filter;
                           NLD=Verkoopcodefilter;
                           NOR=Salgskodefilter;
                           SVE=F”rs.kodfilter];
                SourceExpr=SalesCodeFilter;
                Enabled=SalesCodeFilterCtrlEnable;
                OnValidate=BEGIN
                             SalesCodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           CustList@1005 : Page 22;
                           CustPriceGrList@1004 : Page 7;
                           CampaignList@1002 : Page 11072059;
                         BEGIN
                           IF SalesTypeFilter = SalesTypeFilter::"All Customers" THEN EXIT;

                           CASE SalesTypeFilter OF
                             SalesTypeFilter::Customer:
                               BEGIN
                                 CustList.LOOKUPMODE := TRUE;
                                 IF CustList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CustList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             SalesTypeFilter::"Customer Price Group":
                               BEGIN
                                 CustPriceGrList.LOOKUPMODE := TRUE;
                                 IF CustPriceGrList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CustPriceGrList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             SalesTypeFilter::Campaign:
                               BEGIN
                                 CampaignList.LOOKUPMODE := TRUE;
                                 IF CampaignList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CampaignList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                           END;

                           EXIT(TRUE);
                         END;
                          }

    { 10  ;2   ;Field     ;
                Name=ItemNoFilterCtrl;
                CaptionML=[DEU=Mittelnr.-Filter;
                           ENU=Means No. Filter;
                           NLD=Middelnr.-filter;
                           NOR=Filter for middelnr.;
                           SVE=Filter f”r medelnr];
                SourceExpr=MeansNoFilter;
                OnValidate=BEGIN
                             MeansNoFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           MeansList@1002 : Page 11020289;
                         BEGIN
                           MeansList.LOOKUPMODE := TRUE;
                           IF MeansList.RUNMODAL = ACTION::LookupOK THEN
                             Text := MeansList.GetSelectionFilter
                           ELSE
                             EXIT(FALSE);

                           EXIT(TRUE);
                         END;
                          }

    { 34  ;2   ;Field     ;
                CaptionML=[DEU=Startdatumsfilter;
                           ENU=Starting Date Filter;
                           NLD=Begindatumfilter;
                           NOR=Startdatofilter;
                           SVE=Startdatumfilter];
                SourceExpr=StartingDateFilter;
                OnValidate=BEGIN
                             StartingDateFilterOnAfterValid;
                           END;
                            }

    { 38  ;2   ;Field     ;
                CaptionML=[DEU=W„hrungscodefilter;
                           ENU=Currency Code Filter;
                           NLD=Valutacodefilter;
                           NOR=Valutakodefilter;
                           SVE=Valutakodfilter];
                SourceExpr=CurrencyCodeFilter;
                OnValidate=BEGIN
                             CurrencyCodeFilterOnAfterValid;
                           END;

                OnLookup=VAR
                           CurrencyList@1000 : Page 5;
                         BEGIN
                           CurrencyList.LOOKUPMODE := TRUE;
                           IF CurrencyList.RUNMODAL = ACTION::LookupOK THEN
                             Text := CurrencyList.GetSelectionFilter
                           ELSE
                             EXIT(FALSE);

                           EXIT(TRUE);
                         END;
                          }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 32  ;2   ;Field     ;
                SourceExpr="Sales Type" }

    { 18  ;2   ;Field     ;
                SourceExpr="Sales Code";
                Editable="Sales CodeEditable" }

    { 14  ;2   ;Field     ;
                SourceExpr="Currency Code";
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr="Minimum Quantity" }

    { 6   ;2   ;Field     ;
                SourceExpr="Unit Price" }

    { 36  ;2   ;Field     ;
                SourceExpr="Starting Date" }

    { 2   ;2   ;Field     ;
                SourceExpr="Ending Date" }

    { 25  ;2   ;Field     ;
                SourceExpr="Allow Line Disc.";
                Visible=FALSE }

    { 42  ;2   ;Field     ;
                SourceExpr="Allow Invoice Disc.";
                Visible=FALSE }

    { 46  ;2   ;Field     ;
                SourceExpr="VAT Bus. Posting Gr. (Price)";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      Cust@1001 : Record 18;
      CustPriceGr@1005 : Record 6;
      Campaign@1008 : Record 11072034;
      Means@1004 : Record 11020245;
      SalesTypeFilter@1000 : 'Customer,Customer Price Group,All Customers,Campaign,None';
      SalesCodeFilter@1003 : Text[250];
      MeansNoFilter@1011 : Text[250];
      StartingDateFilter@1006 : Text[30];
      CurrencyCodeFilter@1002 : Text[250];
      Text000@1007 : TextConst 'DEU=Alle Debitoren;ENU=All Customers;NLD=Alle klanten;NOR=Alle kunder;SVE=Alla kunder';
      "Sales CodeEditable"@19031640 : Boolean INDATASET;
      SalesCodeFilterCtrlEnable@19013164 : Boolean INDATASET;

    PROCEDURE GetRecFilters@2();
    BEGIN
      IF GETFILTERS <> '' THEN BEGIN
        IF GETFILTER("Sales Type") <> '' THEN
          SalesTypeFilter := "Sales Type"
        ELSE
          SalesTypeFilter := SalesTypeFilter::None;

        SalesCodeFilter := GETFILTER("Sales Code");
        MeansNoFilter := GETFILTER("Means No.");
        CurrencyCodeFilter := GETFILTER("Currency Code");
      END;

      EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
    END;

    PROCEDURE SetRecFilters@1();
    BEGIN
      SalesCodeFilterCtrlEnable := TRUE;

      IF SalesTypeFilter <> SalesTypeFilter::None THEN
        SETRANGE("Sales Type",SalesTypeFilter)
      ELSE
        SETRANGE("Sales Type");

      IF SalesTypeFilter IN [SalesTypeFilter::"All Customers",SalesTypeFilter::None] THEN BEGIN
        SalesCodeFilterCtrlEnable := FALSE;
        SalesCodeFilter := '';
      END;

      IF SalesCodeFilter <> '' THEN
        SETFILTER("Sales Code",SalesCodeFilter)
      ELSE
        SETRANGE("Sales Code");

      IF StartingDateFilter <> '' THEN
        SETFILTER("Starting Date",StartingDateFilter)
      ELSE
        SETRANGE("Starting Date");

      IF MeansNoFilter <> '' THEN BEGIN
        SETFILTER("Means No.",MeansNoFilter);
      END ELSE
        SETRANGE("Means No.");

      IF CurrencyCodeFilter <> '' THEN BEGIN
        SETFILTER("Currency Code",CurrencyCodeFilter);
      END ELSE
        SETRANGE("Currency Code");

      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE GetCaption@3() : Text[250];
    VAR
      ObjTransl@1000 : Record 377;
      SourceTableName@1002 : Text[100];
      SalesSrcTableName@1003 : Text[100];
      Description@1001 : Text[250];
    BEGIN
      GetRecFilters;
      "Sales CodeEditable" := "Sales Type" <> "Sales Type"::"All Customers";

      SourceTableName := '';
      IF MeansNoFilter <> '' THEN
        SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,27);

      SalesSrcTableName := '';
      CASE SalesTypeFilter OF
        SalesTypeFilter::Customer:
          BEGIN
            SalesSrcTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,18);
            Cust."No." := SalesCodeFilter;
            IF Cust.FIND THEN
              Description := Cust.Name;
          END;
        SalesTypeFilter::"Customer Price Group":
          BEGIN
            SalesSrcTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,6);
            CustPriceGr.Code := SalesCodeFilter;
            IF CustPriceGr.FIND THEN
              Description := CustPriceGr.Description;
          END;
        SalesTypeFilter::Campaign:
          BEGIN
            SalesSrcTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,11072034);
            Campaign."No." := SalesCodeFilter;
            IF Campaign.FIND THEN
              Description := Campaign.Description;
          END;
        SalesTypeFilter::"All Customers":
          BEGIN
            SalesSrcTableName := Text000;
            Description := '';
          END;
      END;

      IF SalesSrcTableName = Text000 THEN
        EXIT(STRSUBSTNO('%1 %2 %3',SalesSrcTableName,SourceTableName,MeansNoFilter));
      EXIT(STRSUBSTNO('%1 %2 %3 %4 %5',SalesSrcTableName,SalesCodeFilter,Description,SourceTableName,MeansNoFilter));
    END;

    LOCAL PROCEDURE SalesCodeFilterOnAfterValidate@19067727();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE SalesTypeFilterOnAfterValidate@19024044();
    BEGIN
      CurrPage.SAVERECORD;
      SalesCodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE StartingDateFilterOnAfterValid@19055286();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE MeansNoFilterOnAfterValidate@19079544();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE CurrencyCodeFilterOnAfterValid@19015290();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      "Sales CodeEditable" := "Sales Type" <> "Sales Type"::"All Customers"
    END;

    BEGIN
    END.
  }
}

