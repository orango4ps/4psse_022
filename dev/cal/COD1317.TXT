OBJECT Codeunit 1317 Aged Inventory Chart Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      YCaptionTxt@4048 : TextConst 'ENU=Inventory Value;NOR=Lagerverdi';
      XCaptionTxt@4047 : TextConst 'ENU=Time on Inventory;NOR=Tid p† lager';
      PeriodStartDate@4049 : ARRAY [6] OF Date;
      XFromToYearsTxt@1001 : TextConst '@@@="%1=number of years,%2=number of years";ENU=%1..%2 years;NOR=%1..%2 †r;SVE=%1..%2 †r';
      XFromToDaysTxt@1000 : TextConst '@@@="%1=number of days,%2=number of days";ENU=%1..%2 days;NOR=%1..%2 dager;SVE=%1..%2 dagar';
      XOverYearsTxt@1002 : TextConst 'ENU=Over %1 years;NOR=Over %1 †r;SVE=Mer „n %1 †r';
      XOverDaysTxt@1003 : TextConst 'ENU=Over %1 days;NOR=Over %1 dager;SVE=Mer „n %1 dagar';

    [External]
    PROCEDURE UpdateChart@3(VAR BusChartBuf@1000 : Record 485);
    VAR
      ColumnIndex@1007 : Integer;
      PeriodStartDate@4049 : ARRAY [6] OF Date;
      InvtValue@4047 : ARRAY [5] OF Decimal;
    BEGIN
      WITH BusChartBuf DO BEGIN
        Initialize;
        AddMeasure(YCaptionTxt,1,"Data Type"::Decimal,"Chart Type"::StackedColumn);
        SetXAxis(XCaptionTxt,"Data Type"::String);
        CalcPeriodStartDates(PeriodStartDate,GetPeriodLengthInDays(BusChartBuf));
        AddChartColumns(BusChartBuf);
        CalcInventoryValuePerAge(InvtValue,PeriodStartDate);
        FOR ColumnIndex := 1 TO 5 DO
          SetValueByIndex(0,ColumnIndex - 1,InvtValue[6 - ColumnIndex]);
      END;
    END;

    [External]
    PROCEDURE DrillDown@5(VAR BusChartBuf@1000 : Record 485);
    VAR
      DrillDownXIndex@1001 : Integer;
    BEGIN
      CalcPeriodStartDates(PeriodStartDate,GetPeriodLengthInDays(BusChartBuf));
      DrillDownXIndex := BusChartBuf."Drill-Down X Index";
      CASE BusChartBuf."Drill-Down Measure Index" + 1 OF
        1: // Item Ledger Entries
          DrillDownItemLedgerEntries(PeriodStartDate[5 - DrillDownXIndex],PeriodStartDate[6 - DrillDownXIndex]);
      END;
    END;

    [External]
    PROCEDURE CalcInventoryValuePerAge@4050(VAR InvtValue@4048 : ARRAY [5] OF Decimal;PeriodStartDate@4047 : ARRAY [6] OF Date);
    VAR
      Item@4049 : Record 27;
      ItemLedgerEntry@4050 : Record 32;
      InvtQty@4051 : ARRAY [5] OF Decimal;
      UnitCost@4052 : Decimal;
      PeriodNo@4053 : Integer;
    BEGIN
      Item.SETRANGE(Type,Item.Type::Inventory,Item.Type::Inventory);
      IF Item.FIND('-') THEN
        REPEAT
          WITH ItemLedgerEntry DO BEGIN
            SETRANGE(Open,TRUE,TRUE);
            SETRANGE("Item No.",Item."No.",Item."No.");
            IF FIND('-') THEN
              REPEAT
                CalcRemainingQty(ItemLedgerEntry,PeriodStartDate,InvtQty,PeriodNo);
                UnitCost := CalcUnitCost(ItemLedgerEntry);
                InvtValue[PeriodNo] += UnitCost * ABS(InvtQty[PeriodNo]);
              UNTIL NEXT = 0;
          END;
        UNTIL Item.NEXT = 0;
    END;

    LOCAL PROCEDURE AddChartColumns@4051(VAR BusChartBuf@4047 : Record 485);
    VAR
      I@4048 : Integer;
      PeriodLengthOnXAxis@4049 : Integer;
      XAxisValueTxt@1000 : Text[30];
      LastXAxisValueTxt@1001 : Text[30];
    BEGIN
      WITH BusChartBuf DO BEGIN
        PeriodLengthOnXAxis := GetPeriodLengthInDays(BusChartBuf);
        IF PeriodLengthOnXAxis = 365 THEN BEGIN
          PeriodLengthOnXAxis := 1;
          XAxisValueTxt := XFromToYearsTxt;
          LastXAxisValueTxt := XOverYearsTxt;
        END ELSE BEGIN
          XAxisValueTxt := XFromToDaysTxt;
          LastXAxisValueTxt := XOverDaysTxt;
        END;
        FOR I := 0 TO 3 DO
          AddColumn(STRSUBSTNO(XAxisValueTxt,I * PeriodLengthOnXAxis,(I + 1) * PeriodLengthOnXAxis));  // X-Axis value
        AddColumn(STRSUBSTNO(LastXAxisValueTxt,FORMAT(4 * PeriodLengthOnXAxis)));  // X-Axis value
      END;
    END;

    LOCAL PROCEDURE CalcPeriodStartDates@4047(VAR PeriodStartDate@4047 : ARRAY [6] OF Date;PeriodLength@4049 : Integer);
    VAR
      I@4048 : Integer;
    BEGIN
      PeriodStartDate[6] := WORKDATE;
      PeriodStartDate[1] := 0D;
      FOR I := 2 TO 5 DO
        PeriodStartDate[I] := CALCDATE('<-' + FORMAT((6 - I) * PeriodLength) + 'D>',PeriodStartDate[6]);
    END;

    [External]
    PROCEDURE CalcRemainingQty@4052(ItemLedgerEntry@4047 : Record 32;PeriodStartDate@4049 : ARRAY [6] OF Date;VAR InvtQty@4048 : ARRAY [5] OF Decimal;VAR PeriodNo@4051 : Integer);
    BEGIN
      WITH ItemLedgerEntry DO BEGIN
        FOR PeriodNo := 1 TO 5 DO
          InvtQty[PeriodNo] := 0;

        FOR PeriodNo := 1 TO 5 DO
          IF ("Posting Date" > PeriodStartDate[PeriodNo]) AND
             ("Posting Date" <= PeriodStartDate[PeriodNo + 1])
          THEN
            IF "Remaining Quantity" <> 0 THEN BEGIN
              InvtQty[PeriodNo] := "Remaining Quantity";
              EXIT;
            END;
      END;
    END;

    [External]
    PROCEDURE CalcUnitCost@2(ItemLedgerEntry@4047 : Record 32) : Decimal;
    VAR
      ValueEntry@1055 : Record 5802;
      UnitCost@4048 : Decimal;
    BEGIN
      WITH ValueEntry DO BEGIN
        SETRANGE("Item Ledger Entry No.",ItemLedgerEntry."Entry No.");
        UnitCost := 0;

        IF FIND('-') THEN
          REPEAT
            IF "Partial Revaluation" THEN
              SumUnitCost(UnitCost,"Cost Amount (Actual)" + "Cost Amount (Expected)","Valued Quantity")
            ELSE
              SumUnitCost(UnitCost,"Cost Amount (Actual)" + "Cost Amount (Expected)",ItemLedgerEntry.Quantity);
          UNTIL NEXT = 0;
      END;
      EXIT(UnitCost);
    END;

    [External]
    PROCEDURE GetPeriodLengthInDays@4048(BusChartBuf@4047 : Record 485) : Integer;
    BEGIN
      WITH BusChartBuf DO
        CASE "Period Length" OF
          "Period Length"::Day:
            EXIT(1);
          "Period Length"::Week:
            EXIT(7);
          "Period Length"::Month:
            EXIT(30);
          "Period Length"::Quarter:
            EXIT(90);
          "Period Length"::Year:
            EXIT(365);
        END;
    END;

    LOCAL PROCEDURE SumUnitCost@4053(VAR UnitCost@1002 : Decimal;CostAmount@1000 : Decimal;Quantity@1001 : Decimal);
    BEGIN
      UnitCost := UnitCost + CostAmount / ABS(Quantity);
    END;

    LOCAL PROCEDURE DrillDownItemLedgerEntries@5050(StartDate@5048 : Date;EndDate@4047 : Date);
    VAR
      ItemLedgerEntry@5049 : Record 32;
    BEGIN
      ItemLedgerEntry.SETRANGE(Open,TRUE);
      // we don't want to include start date in the filter, unless it is 0D
      IF StartDate = 0D THEN
        ItemLedgerEntry.SETRANGE("Posting Date",StartDate,EndDate)
      ELSE
        ItemLedgerEntry.SETRANGE("Posting Date",CALCDATE('<1D>',StartDate),EndDate);
      ItemLedgerEntry.SETFILTER("Remaining Quantity",'<>0');
      PAGE.RUN(PAGE::"Item Ledger Entries",ItemLedgerEntry);
    END;

    [External]
    PROCEDURE FromToYearsTxt@1() : Text[30];
    BEGIN
      EXIT(XFromToYearsTxt);
    END;

    [External]
    PROCEDURE FromToDaysTxt@4() : Text[30];
    BEGIN
      EXIT(XFromToDaysTxt);
    END;

    [External]
    PROCEDURE OverYearsTxt@6() : Text[30];
    BEGIN
      EXIT(XOverYearsTxt);
    END;

    [External]
    PROCEDURE OverDaysTxt@7() : Text[30];
    BEGIN
      EXIT(XOverDaysTxt);
    END;

    BEGIN
    END.
  }
}

