OBJECT Table 11229468 Signhost Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               ResetSigners;
             END;

    CaptionML=ENU=Signhost Document;
    LookupPageID=Page11129740;
    DrillDownPageID=Page11129740;
  }
  FIELDS
  {
    { 10  ;   ;Record ID           ;RecordID      ;CaptionML=[ENU=Record ID;
                                                              SVE=Post-ID];
                                                   Editable=No }
    { 20  ;   ;Transaction Id      ;Text50        ;CaptionML=[ENU=Transaction Id;
                                                              SVE=Transaktions-ID];
                                                   Editable=No }
    { 30  ;   ;Sent Date-Time      ;DateTime      ;CaptionML=[ENU=Sent Date-Time;
                                                              SVE=Skickat datum/tid];
                                                   Editable=No }
    { 40  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=ENU=,,,,,Waiting for Document,,,,,Waiting for Signer,,,,,In Progress,,,,,Signed,,,,,Rejected,,,,,Expired,,,,,Cancelled,,,,,Failed;
                                                   OptionString=,,,,,Waiting for Document,,,,,Waiting for Signer,,,,,In Progress,,,,,Signed,,,,,Rejected,,,,,Expired,,,,,Cancelled,,,,,Failed;
                                                   Editable=No }
    { 45  ;   ;Sent Word Document No.;Code20      ;TableRelation="Document Properties" WHERE (External Document=CONST(No));
                                                   CaptionML=ENU=Sent Word Document No.;
                                                   Editable=No }
    { 50  ;   ;Signed Document No. ;Code20        ;TableRelation="Document Properties" WHERE (External Document=CONST(Yes));
                                                   CaptionML=ENU=Signed Document No.;
                                                   Editable=No }
    { 60  ;   ;Receipt Document No.;Code20        ;TableRelation="Document Properties" WHERE (External Document=CONST(Yes));
                                                   CaptionML=ENU=Receipt Document No.;
                                                   Editable=No }
    { 70  ;   ;No. of Signers      ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Signhost Signer" WHERE (Record ID=FIELD(Record ID),
                                                                                              Word Document No.=FIELD(Sent Word Document No.),
                                                                                              Sign Url=FILTER(<>'')));
                                                   CaptionML=ENU=No. of Signers;
                                                   Editable=No }
    { 80  ;   ;No. of Sent Sign Requests;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Signhost Signer" WHERE (Record ID=FIELD(Record ID),
                                                                                              Word Document No.=FIELD(Sent Word Document No.),
                                                                                              Sign Url=FILTER(<>''),
                                                                                              Sign Request Sent Date-Time=FILTER(<>'')));
                                                   CaptionML=ENU=No. of Sent Sign Requests;
                                                   Editable=No }
    { 90  ;   ;No. of Received Signatures;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Signhost Signer" WHERE (Record ID=FIELD(Record ID),
                                                                                              Word Document No.=FIELD(Sent Word Document No.),
                                                                                              Sign Url=FILTER(<>''),
                                                                                              Signed Date-Time=FILTER(<>'')));
                                                   CaptionML=ENU=No. of Received Signatures;
                                                   Editable=No }
    { 100 ;   ;Signing Process Finished;Boolean   ;CaptionML=ENU=Signing Process Finished;
                                                   Editable=No }
    { 110 ;   ;Ignore              ;Boolean       ;CaptionML=[ENU=Ignore;
                                                              SVE=Ignorera] }
    { 120 ;   ;Error Message       ;Text250       ;CaptionML=[ENU=Error Message;
                                                              SVE=Felmeddelande];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Record ID,Sent Word Document No.        ;Clustered=Yes }
    {    ;Transaction Id                           }
    {    ;Sent Word Document No.                   }
    {    ;Signing Process Finished                 }
    {    ;Record ID,Sent Date-Time                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ShowDocumentCard@1100528400();
    VAR
      PurchaseHeader@1100528401 : Record 38;
      RecRef@1100528400 : RecordRef;
      RecordID@1100528402 : RecordID;
    BEGIN
      RecordID := "Record ID";
      RecRef := RecordID.GETRECORD;
      CASE RecRef.NUMBER OF
        DATABASE::"Purchase Header":
          BEGIN
            RecRef.SETTABLE(PurchaseHeader);
            CASE PurchaseHeader."Document Type" OF
              PurchaseHeader."Document Type"::Order:
                BEGIN
                  PurchaseHeader.SETRANGE("Document Type", PurchaseHeader."Document Type");
                  PurchaseHeader.SETRANGE("No.", PurchaseHeader."No.");
                  PAGE.RUN(PAGE::"Purchase Order", PurchaseHeader);
                END;
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE ResetSigners@1100528401();
    VAR
      SignhostSigner@1100528400 : Record 11229467;
      SignhostSignerRejectionText@1100528401 : Record 11072462;
    BEGIN
      SignhostSigner.SETRANGE("Record ID", "Record ID");
      SignhostSigner.SETRANGE("Word Document No.", "Sent Word Document No.");
      IF SignhostSigner.FINDSET(TRUE) THEN
        REPEAT
          SignhostSigner."Last Activity" := 0;
          SignhostSigner."Sign Url" := '';
          SignhostSigner."Sign Request Sent Date-Time" := 0DT;
          SignhostSigner."Signed Date-Time" := 0DT;
          SignhostSigner."Rejected Date-Time" := 0DT;
          SignhostSigner."Signed Document Sent Date-Time" := 0DT;
          SignhostSigner.MODIFY;
        UNTIL SignhostSigner.NEXT = 0;

      SignhostSignerRejectionText.SETRANGE("Record ID", "Record ID");
      SignhostSignerRejectionText.SETRANGE("Word Document No.", "Sent Word Document No.");
      SignhostSignerRejectionText.DELETEALL;
    END;

    PROCEDURE DrillDownOnSentWordDocumentNo@1100528402();
    VAR
      DocumentProperties@1100528400 : Record 11012746;
    BEGIN
      DocumentProperties.SETRANGE("No.", "Sent Word Document No.");
      PAGE.RUN(PAGE::"Word Document", DocumentProperties);
    END;

    PROCEDURE DrillDownOnSignedDocumentNo@1100528403();
    VAR
      DocumentProperties@1100528400 : Record 11012746;
    BEGIN
      DocumentProperties.SETRANGE("No.", "Signed Document No.");
      PAGE.RUN(PAGE::"External Document", DocumentProperties);
    END;

    PROCEDURE DrillDownOnReceiptDocumentNo@1100528404();
    VAR
      DocumentProperties@1100528400 : Record 11012746;
    BEGIN
      DocumentProperties.SETRANGE("No.", "Receipt Document No.");
      PAGE.RUN(PAGE::"External Document", DocumentProperties);
    END;

    PROCEDURE DrillDownOnNoOfSigners@1100528405();
    VAR
      SignhostSigner@1100528400 : Record 11229467;
    BEGIN
      SignhostSigner.SETRANGE("Record ID", "Record ID");
      SignhostSigner.SETRANGE("Word Document No.", "Sent Word Document No.");
      SignhostSigner.SETFILTER("Sign Url", '<>%1', '');
      PAGE.RUN(0, SignhostSigner);
    END;

    PROCEDURE DrillDownOnNoOfSentSignRequests@1100528406();
    VAR
      SignhostSigner@1100528400 : Record 11229467;
    BEGIN
      SignhostSigner.SETRANGE("Record ID", "Record ID");
      SignhostSigner.SETRANGE("Word Document No.", "Sent Word Document No.");
      SignhostSigner.SETFILTER("Sign Url", '<>%1', '');
      SignhostSigner.SETFILTER("Sign Request Sent Date-Time", '<>%1', 0DT);
      PAGE.RUN(0, SignhostSigner);
    END;

    PROCEDURE DrillDownOnNoOfReceivedSignatures@1100528407();
    VAR
      SignhostSigner@1100528400 : Record 11229467;
    BEGIN
      SignhostSigner.SETRANGE("Record ID", "Record ID");
      SignhostSigner.SETRANGE("Word Document No.", "Sent Word Document No.");
      SignhostSigner.SETFILTER("Sign Url", '<>%1', '');
      SignhostSigner.SETFILTER("Signed Date-Time", '<>%1', 0DT);
      PAGE.RUN(0, SignhostSigner);
    END;

    PROCEDURE GetLastSignhostDocumentOfRecordID@1100528408(RecID@1100528400 : RecordID);
    VAR
      SignhostManagement@1100528401 : Codeunit 11012637;
    BEGIN
      CLEAR(Rec);
      SignhostManagement.GetLastSignhostDocumentOfRecordID(RecID, Rec);
      CALCFIELDS("No. of Sent Sign Requests", "No. of Signers", "No. of Received Signatures");
    END;

    PROCEDURE OtherPendingSignhostDocumentExistsForSourceRecord@1100528409() : Boolean;
    VAR
      SignhostDocument@1100528400 : Record 11229468;
    BEGIN
      SignhostDocument.SETRANGE("Record ID", "Record ID");
      SignhostDocument.SETFILTER("Sent Word Document No.", '<>%1', "Sent Word Document No.");
      SignhostDocument.SETFILTER(
        Status, '%1|%2|%3|%4',
        0, Status::"Waiting for Document", Status::"Waiting for Signer", Status::"In Progress");
      EXIT(NOT SignhostDocument.ISEMPTY);
    END;

    PROCEDURE GetSignDateOfLastSigner@1100528410() LastSignDate : Date;
    VAR
      SignhostSigner@1100528400 : Record 11229467;
      AllSignersHaveSigned@1100528401 : Boolean;
    BEGIN
      AllSignersHaveSigned := TRUE;
      SignhostSigner.SETRANGE("Record ID", "Record ID");
      SignhostSigner.SETRANGE("Word Document No.", "Sent Word Document No.");
      IF SignhostSigner.FINDSET THEN
        REPEAT
          IF DT2DATE(SignhostSigner."Signed Date-Time") > LastSignDate THEN
            LastSignDate := DT2DATE(SignhostSigner."Signed Date-Time");
          IF SignhostSigner."Signed Date-Time" = 0DT THEN
            AllSignersHaveSigned := FALSE;
        UNTIL SignhostSigner.NEXT = 0;
      IF NOT AllSignersHaveSigned THEN
        LastSignDate := 0D;
    END;

    BEGIN
    END.
  }
}

