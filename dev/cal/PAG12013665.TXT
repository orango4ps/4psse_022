OBJECT Page 12013665 ExFlow AD Users
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF512000;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=[ENU=ExFlow AD Users;
               SVE=ExFlow AD anv„ndare];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table12013598;
    PageType=List;
    SourceTableTemporary=Yes;
    RefreshOnActivate=Yes;
    OnOpenPage=BEGIN
                 ExFileMgt.CreateDotNetADTable(Rec);
               END;

    ActionList=ACTIONS
    {
      { 1100285007;  ;ActionContainer;
                      Name=Add;
                      CaptionML=[ENU=Functions;
                                 SVE=Funktioner];
                      ActionContainerType=ActionItems }
      { 1100285008;1 ;Action    ;
                      CaptionML=[ENU=Add Marked Users;
                                 SVE=L„gg till markerade];
                      Promoted=Yes;
                      Image=Import;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ReturnAdName;
                                 RESET;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1100285009;1 ;Action    ;
                      CaptionML=[ENU=Remove All Marks;
                                 SVE=Ta bort alla markeringar];
                      Promoted=Yes;
                      Image=Import;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SETRANGE(Add,TRUE);
                                 MODIFYALL(Add,FALSE);
                                 RESET;
                                 CurrPage.UPDATE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100285000;0;Container;
                ContainerType=ContentArea }

    { 1100285001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100285006;2;Field  ;
                SourceExpr=Add }

    { 1100285002;2;Field  ;
                SourceExpr="User ID" }

    { 1100285003;2;Field  ;
                SourceExpr=Name }

    { 1100285004;2;Field  ;
                SourceExpr=Email }

    { 1100285005;2;Field  ;
                SourceExpr=Department }

    { 1100285010;2;Field  ;
                SourceExpr="Domain Name" }

    { 1100285011;2;Field  ;
                SourceExpr=Error }

  }
  CODE
  {
    VAR
      EXF01@1100285000 : TextConst 'ENU=%1 Users out of %2 Marked has been succesfully added;SVE=%1 anv„ndare utav %2 markerade har lagts till';
      ExFileMgt@1100285002 : Codeunit 12013602;

    PROCEDURE ReturnAdName@1100285000();
    VAR
      ExUserComp@1100285000 : Record 12013641;
      CountAdd@1100285002 : Integer;
      ExSetup@1100285001 : Record 12013601;
      ValidatedUserID@1100285003 : Text[100];
      CountTot@1100285004 : Integer;
    BEGIN
      ExSetup.GET;

      RESET;
      SETRANGE(Add,TRUE);
      CountTot := COUNT;
      IF FINDSET(TRUE,TRUE) THEN
        REPEAT
          ExUserComp.INIT;
          ValidatedUserID := GetUserID("User ID","Domain Name",ExSetup);
          IF ValidatedUserID = '' THEN
            Error := TRUE
          ELSE BEGIN
            ExUserComp.VALIDATE("User ID",ValidatedUserID);
            ExUserComp."User Name" := Name;
            ExUserComp."E-mail ExFlow" := Email;
            IF ExUserComp.INSERT(TRUE) THEN BEGIN
              IF ExUserComp."E-mail ExFlow" <> '' THEN
                ExUserComp.CreateApprovalGroups(ValidatedUserID);
              CountAdd := CountAdd +1;
              Add := FALSE;
              Error := FALSE;
            END ELSE
              Error := TRUE;
          END;
          MODIFY;
        UNTIL NEXT = 0;

      MESSAGE(EXF01,CountAdd,CountTot);
    END;

    LOCAL PROCEDURE GetUserID@1100285001(InUserID@1100285002 : Text[100];DomainName@1100285003 : Text[100];ExSetup@1100285001 : Record 12013601) OutUserID : Text[100];
    BEGIN
      IF ExSetup."ExFlow User Domain Validation" = ExSetup."ExFlow User Domain Validation"::"With Domain" THEN BEGIN
        IF STRPOS(InUserID,'\') <> 0 THEN
          EXIT(InUserID);

        IF DomainName <> '' THEN
          EXIT(DomainName + '\' + InUserID);

        EXIT('');
      END;

      IF ExSetup."ExFlow User Domain Validation" = ExSetup."ExFlow User Domain Validation"::"Without Domain" THEN BEGIN
        IF STRPOS(InUserID,'\') <> 0 THEN
          EXIT(COPYSTR(InUserID,STRPOS(InUserID,'\')+1))
        ELSE
          EXIT(InUserID);
      END;

      EXIT(InUserID);
    END;

    BEGIN
    END.
  }
}

