OBJECT Page 11020521 Plant Request Subform
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Zeilen;
               ENU=Lines;
               NLD=Regels;
               NOR=Rader;
               SVE=Rader];
    SourceTable=Table11020521;
    DelayedInsert=Yes;
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnInit=BEGIN
             "Comment CP OrderEnable" := TRUE;
             "Rate CodeVisible" := TRUE;
             FromRateCodeVisible := TRUE;
             "Comment CP OrderVisible" := TRUE;
             ProjectStockEditable := TRUE;
             DeliverEditable := TRUE;
             ShipToLocationEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 PlantSetupRec.GET;
                 "Rate CodeVisible" := (PlantSetupRec."Rate Codes");
                 IF NOT PlantSetupRec."Rate Codes" THEN
                   FromRateCodeVisible := FALSE;

                 IF (NOT PlantSetupRec."Comment CP Order") THEN BEGIN
                   "Comment CP OrderEnable" := FALSE;
                   "Comment CP OrderVisible" := FALSE;
                 END;
               END;

    OnAfterGetRecord=BEGIN
                       GetItemInventoryLocationHideVa := FALSE;
                       "GetStockQuantity()HideValue" := FALSE;
                       IF (NOT "Comment CP OrderEnable") THEN
                         "Comment CP OrderVisible" := FALSE;
                       OnAfterGetCurrRec;
                       GetStockQuantityOnFormat(FORMAT(GetStockQuantity()));
                       GetItemInventoryLocationOnForm(FORMAT(GetItemInventoryLocation()));
                       UpdateEditable;
                     END;

    OnNewRecord=BEGIN
                  UpdateAfterAddLinkedPlant := FALSE;  //*01-07-2010
                  InitRecord;
                  OnAfterGetCurrRec;
                  UpdateEditable;
                END;

    OnInsertRecord=BEGIN
                     //AddStandardItemOnInsertRecord(BelowxRec);  //*01-07-2010.o
                     AddLinkedPlantTypeStndItemMgt.InsertOnPlantRequest(Rec, 0);  //*01-07-2010  0=Insert  (As in subform PO, F11012562)
                   END;

    OnModifyRecord=BEGIN
                     IF AddLinkedPlantTypeStndItemMgt.InsertOnPlantRequest(Rec, 1) THEN  //*01-07-2010  1=Modify
                       UpdateAfterAddLinkedPlant := TRUE;  //* Sometimes the form is not updated after Modify, therefore this is done
                                                           //* in 'OnAfterGetCurrRec'. Because 'CurrPage.UPDATE' not allowed in OnModify!.
                   END;

    ActionList=ACTIONS
    {
      { 1210190001;  ;ActionContainer;
                      CaptionML=[NOR=Funksjoner;
                                 SVE=Funktioner];
                      ActionContainerType=ActionItems }
      { 1100529606;1 ;ActionGroup;
                      CaptionML=[DEU=Funktionen;
                                 ENU=Functions;
                                 NLD=Functies] }
      { 1210190003;2 ;Action    ;
                      ShortCutKey=Shift+Ctrl+S;
                      CaptionML=[DEU=Satz explodieren;
                                 ENU=Explode &Set;
                                 NLD=Set exploderen;
                                 NOR=Utfold &sett;
                                 SVE=Expandera &set];
                      Image=ExplodeBOM;
                      OnAction=BEGIN
                                 ExplodeSetLines();
                               END;
                                }
      { 1100529605;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Teilweiser šbertrag in neuen Werkzeuganfrage;
                                 ENU=Partly Transfer to New Plant Request;
                                 NLD=Gedeeltelijk overzetten naar nieuwe materieelaanvraag];
                      Enabled=PartlyTransferRequestEnabled;
                      Image=ChangeBatch;
                      OnAction=VAR
                                 PlantRequestLine@1100529000 : Record 11020521;
                                 PlantRequestMgt@1100529001 : Codeunit 11012587;
                                 NewPlantRequestNo@1100529002 : Code[20];
                               BEGIN
                                 PlantSetupRec.TESTFIELD("Extended Picking Procedure", TRUE);
                                 CurrPage.SETSELECTIONFILTER(PlantRequestLine);

                                 PlantReqRec.GET("Plant Request No.");
                                 NewPlantRequestNo := PlantRequestMgt.RunPartlyMoveToNewPlantRequest(PlantReqRec, PlantRequestLine);
                                 IF NewPlantRequestNo <> '' THEN BEGIN
                                   COMMIT;
                                   CurrPage.UPDATE(FALSE);
                                   IF PlantReqRec.GET(NewPlantRequestNo) THEN
                                     PAGE.RUN(PAGE::"Plant Request", PlantReqRec);
                                 END;
                               END;
                                }
      { 1100529604;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Teilweiser šbertrag in vorhandenen Werkzeuganfrage;
                                 ENU=Partly Transfer to Existing Plant Request;
                                 NLD=Gedeeltelijk overzetten naar bestaande materieelaanvraag];
                      Enabled=PartlyTransferRequestEnabled;
                      Image=ChangeBatch;
                      OnAction=VAR
                                 FromPlantRequest@1100528001 : Record 11020520;
                                 ToPlantRequest@1100528002 : Record 11020520;
                                 PlantRequestLine@1100529602 : Record 11020521;
                                 PlantRequestMgt@1100529601 : Codeunit 11012587;
                                 ToPlantRequestNo@1100529002 : Code[20];
                               BEGIN
                                 PlantSetupRec.TESTFIELD("Extended Picking Procedure", TRUE);
                                 CurrPage.SETSELECTIONFILTER(PlantRequestLine);

                                 IF CONFIRM(Text001) THEN BEGIN
                                   FromPlantRequest.GET("Plant Request No.");
                                   ToPlantRequestNo := PlantRequestMgt.SelectPlantRequestForTranferringLines(FromPlantRequest);
                                   IF ToPlantRequestNo <> '' THEN BEGIN
                                     PlantRequestMgt.PartlyTransferToPlantRequest(PlantRequestLine, ToPlantRequestNo);
                                     COMMIT;
                                     CurrPage.UPDATE(FALSE);
                                     IF ToPlantRequest.GET(ToPlantRequestNo) THEN
                                       PAGE.RUN(PAGE::"Plant Request", ToPlantRequest);
                                   END;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 1100485000;2;Field  ;
                SourceExpr=Type;
                OnValidate=BEGIN
                             UpdateEditable;
                           END;
                            }

    { 1100485022;2;Field  ;
                SourceExpr="Set Code";
                Visible=FALSE;
                Editable="Set CodeEditable";
                OnValidate=BEGIN
                             UpdateEditable;
                           END;

                OnLookup=VAR
                           PlantSet@1100525000 : Record 11012587;
                         BEGIN
                           PlantReqRec.GET("Plant Request No.");
                           IF PlantReqRec.Type = PlantReqRec.Type::Arrival THEN BEGIN
                             PlantSet.FILTERGROUP(9);
                             PlantSet.SETRANGE("Order via Plant Request", TRUE);
                             PlantSet.FILTERGROUP(0);
                           END;
                           PlantSet.Code := "Set Code";
                           IF PAGE.RUNMODAL(0, PlantSet) = ACTION::LookupOK THEN
                             VALIDATE("Set Code", PlantSet.Code);
                         END;
                          }

    { 1100485002;2;Field  ;
                SourceExpr="Plant Type";
                Editable="Plant TypeEditable";
                OnValidate=BEGIN
                             UpdateEditable;
                           END;

                OnLookup=BEGIN
                           //LookupPlantType();  //*C-028869.o
                           LookupPlantType(Text);  //*C-028869.n
                           UpdateEditable;
                         END;

                OnAssistEdit=BEGIN
                               IF Type = Type::Plant THEN
                                 LookupPlantInventory(1);
                             END;
                              }

    { 1100529701;2;Field  ;
                SourceExpr="Plant Group";
                Visible=FALSE }

    { 1100529700;2;Field  ;
                SourceExpr="From Plant Group";
                Visible=FALSE }

    { 1100529702;2;Field  ;
                SourceExpr="To Plant Group";
                Visible=FALSE }

    { 1100485004;2;Field  ;
                SourceExpr="Plant No.";
                Editable="Plant NoEditable";
                OnAssistEdit=BEGIN
                               IF Type = Type::Plant THEN
                                 LookupPlantInventory(2);
                             END;
                              }

    { 1100485006;2;Field  ;
                SourceExpr="Item No.";
                OnLookup=VAR
                           Item@1100525000 : Record 27;
                           ItemList@1100527001 : Page 31;
                         BEGIN
                           IF Type <> Type::Item THEN
                             EXIT;
                           PlantReqRec.GET("Plant Request No.");
                           IF PlantReqRec.Type = PlantReqRec.Type::Arrival THEN BEGIN
                             Item.FILTERGROUP(9);
                             Item.SETRANGE("Order via Plant Request", TRUE);
                             Item.FILTERGROUP(0);
                           END;
                           ItemList.SETTABLEVIEW(Item);
                           Item."No." := "Item No.";
                           ItemList.SETRECORD(Item);
                           IF (PlantReqRec.Status <= PlantReqRec.Status::Requested) AND (Type = Type::Item) THEN
                             ItemList.DetermineInterface(DATABASE::"Plant Request Line","Plant Request No.",Element,'','',0,'','',"Line No.",'','');
                           ItemList.LOOKUPMODE(PlantReqRec.Status <= PlantReqRec.Status::Requested);
                           IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ItemList.GETRECORD(Item);
                             VALIDATE("Item No.", Item."No.");
                           END ELSE
                             IF ItemList.CheckMultiCopy THEN
                               CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1100485008;2;Field  ;
                SourceExpr=Description }

    { 1100529000;2;Field  ;
                SourceExpr="Description 2";
                Visible=FALSE;
                Editable=Description2Editable }

    { 1100525004;2;Field  ;
                SourceExpr="Unit Price" }

    { 1100525009;2;Field  ;
                SourceExpr="Inspection No.";
                Visible=FALSE }

    { 1100485010;2;Field  ;
                SourceExpr=Bulk }

    { 1100525005;2;Field  ;
                SourceExpr="Plant Category";
                Visible=FALSE }

    { 1100525006;2;Field  ;
                CaptionML=[DEU=Werkzeugkategorie Beschreibung;
                           ENU=Plant Category Description;
                           NLD=Omschrijving materieelcategorie];
                SourceExpr=GetPlantCategoryDescription();
                Visible=FALSE }

    { 1100485016;2;Field  ;
                CaptionML=[DEU=Extern;
                           ENU=External;
                           NLD=Extern;
                           NOR=Eksternt;
                           SVE=Externt];
                SourceExpr=GetExternal() }

    { 1100525002;2;Field  ;
                SourceExpr="From Rate Code (Other Transf.)";
                Visible=FromRateCodeVisible }

    { 1100485014;2;Field  ;
                SourceExpr="Rate Code";
                Visible="Rate CodeVisible" }

    { 1210190004;2;Field  ;
                SourceExpr="Location Code";
                Visible=FALSE }

    { 1100525000;2;Field  ;
                SourceExpr=Element;
                Visible=FALSE;
                HideValue=ElementHideValue }

    { 1100485020;2;Field  ;
                SourceExpr="Expected return on" }

    { 1100485012;2;Field  ;
                SourceExpr=Quantity;
                Editable=QuantityEditable }

    { 1100530000;2;Field  ;
                CaptionML=[DEU=Einheit;
                           ENU=Unit of Measure;
                           NLD=Eenheid;
                           NOR=M†leenhet;
                           SVE=M†ttenhet];
                SourceExpr=GetUnitOfMeasureCode() }

    { 1210190000;2;Field  ;
                CaptionML=[DEU=Lagerbestand;
                           ENU=Inventory;
                           NLD=Voorraad;
                           NOR=Beholdning;
                           SVE=Lager];
                SourceExpr=GetStockQuantity();
                Visible=FALSE;
                HideValue="GetStockQuantity()HideValue" }

    { 1100525001;2;Field  ;
                CaptionML=[DEU=Verfgbarer Werkzeugtyp in Depot;
                           ENU=Available Plant Type on Depot;
                           NLD=Beschikbaar mat.srt. op depot];
                BlankZero=Yes;
                SourceExpr=CalcPlantTypeAvailable();
                Visible=FALSE }

    { 1210190006;2;Field  ;
                CaptionML=[DEU=Artikellagerbestand Lagerort;
                           ENU=Item Inventory Location;
                           NLD=Artikelvoorraad vestiging;
                           NOR="Plassering for artikkellager ";
                           SVE=Artikellager per lagerst„lle];
                SourceExpr=GetItemInventoryLocation();
                Visible=FALSE;
                HideValue=GetItemInventoryLocationHideVa }

    { 1210190002;2;Field  ;
                CaptionML=[DEU=Zu buchender Auftrag;
                           ENU=Open Order;
                           NLD=Te boeken order;
                           NOR=pen ordre;
                           SVE=™ppen order];
                SourceExpr=GetOpenPlantOrderLineQuantity();
                Visible=FALSE;
                OnDrillDown=BEGIN
                              DrillDownOpenPlantOrderLineQty();
                            END;
                             }

    { 1100525003;2;Field  ;
                CaptionML=[DEU=Miettarif;
                           ENU=Rental Rate;
                           NLD=Huurtarief;
                           NOR=Leiesats;
                           SVE=Uthyrningsavgift];
                BlankZero=Yes;
                SourceExpr=GetRentalRate();
                AutoFormatType=2;
                Visible=FALSE }

    { 1100529001;2;Field  ;
                CaptionML=[DEU=šbertragen in Calculation;
                           ENU=Transf. from Estimate;
                           NLD=Overgezet van Calculatie;
                           NOR=Overf›r fra Kalkyle;
                           SVE=™verf. fr†n kalkyl];
                SourceExpr=PlantNeed.GetTransfFromEstimateNo("Location Copied Need","Line No. Copied Need");
                Visible=FALSE }

    { 1100485018;2;Field  ;
                SourceExpr="Comment CP Order";
                Visible="Comment CP OrderVisible";
                Enabled="Comment CP OrderEnable" }

    { 1100525007;2;Field  ;
                CaptionML=[DEU=Menge in Rcksendedepot von Lager;
                           ENU=Qty on Return Depot From Loc.;
                           NLD=Aantal op retourdepot van loc.];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=GetQtyPlantTypeOnReturnDepot();
                Visible=FALSE;
                Editable=FALSE }

    { 1100525008;2;Field  ;
                CaptionML=[DEU=Menge erwartete Rcksendung;
                           ENU=Qty Expected Return;
                           NLD=Aantal retour verwacht];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=GetQtyPlantTypeExpectedReturn();
                Visible=FALSE;
                Editable=FALSE }

    { 1100529600;2;Field  ;
                SourceExpr="Project No. (Inventory)";
                Visible=FALSE }

    { 1100529601;2;Field  ;
                SourceExpr="Project Stock";
                Editable=ProjectStockEditable;
                OnValidate=BEGIN
                             UpdateEditable;
                           END;
                            }

    { 1100529602;2;Field  ;
                SourceExpr=Deliver;
                Visible=FALSE;
                Editable=DeliverEditable;
                OnValidate=BEGIN
                             UpdateEditable;
                           END;
                            }

    { 1100529603;2;Field  ;
                SourceExpr="Ship To Location";
                Visible=FALSE;
                Editable=ShipToLocationEditable }

    { 1100529004;2;Field  ;
                SourceExpr="To Employee Company";
                Visible=FALSE;
                Editable=EmployeeEditable }

    { 1100529003;2;Field  ;
                SourceExpr="To Employee No.";
                Visible=FALSE;
                Editable=EmployeeEditable;
                OnLookup=BEGIN
                           LookupToEmployee(EmployeeEditable);
                         END;
                          }

    { 1100529002;2;Field  ;
                CaptionML=[DEU=Name Mitarbeiter;
                           ENU=Employee Name;
                           NLD=Naam werknemer;
                           NOR=Navn p† ansatt;
                           SVE=Namn p† anst„lld];
                SourceExpr=GetEmployeeName;
                Visible=FALSE }

    { 1100527350;2;Field  ;
                SourceExpr=Phase;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      PlantSetupRec@1100485000 : Record 11012550;
      PlantReqRec@1100485001 : Record 11020520;
      PlantNeed@1100529001 : Record 11012579;
      AddLinkedPlantTypeStndItemMgt@1100525001 : Codeunit 11012583;
      UpdateAfterAddLinkedPlant@1100525000 : Boolean;
      PlantListLastPlantType@1210190000 : Code[20];
      "Comment CP OrderVisible"@19005773 : Boolean INDATASET;
      "Rate CodeVisible"@19062503 : Boolean INDATASET;
      FromRateCodeVisible@1100525003 : Boolean INDATASET;
      "Comment CP OrderEnable"@19076467 : Boolean INDATASET;
      "GetStockQuantity()HideValue"@19031331 : Boolean INDATASET;
      GetItemInventoryLocationHideVa@19045862 : Boolean INDATASET;
      "Set CodeEditable"@1100409000 : Boolean INDATASET;
      "Plant TypeEditable"@1100409001 : Boolean INDATASET;
      "Plant NoEditable"@1100409002 : Boolean INDATASET;
      QuantityEditable@1100409003 : Boolean INDATASET;
      Description2Editable@1100529000 : Boolean INDATASET;
      ElementHideValue@1100525004 : Boolean INDATASET;
      ProjectStockEditable@1100529600 : Boolean INDATASET;
      DeliverEditable@1100529602 : Boolean INDATASET;
      ShipToLocationEditable@1100529601 : Boolean INDATASET;
      EmployeeEditable@1100529002 : Boolean INDATASET;
      PartlyTransferRequestEnabled@1100529603 : Boolean INDATASET;
      Text001@1100529604 : TextConst 'DEU=M”chten Sie die ausgew„hlten Zeilen in eine vorhandene Anfrage bertragen?;ENU=Do you want to transfer selected lines to an existing request?;NLD=Wilt u de geselecteerde regels overzetten naar een bestaande aanvraag?';

    PROCEDURE GetExternal@1100485001() : Boolean;
    VAR
      lvPlantTypeRec@1100485000 : Record 11012551;
      lvPlantNoRec@1100485001 : Record 11012552;
    BEGIN
      IF (Type <> Type::Plant) OR ("Plant Type" = '') THEN
        EXIT(FALSE);

      IF ("Plant No." = '') THEN BEGIN
        IF lvPlantTypeRec.GET("Plant Type") THEN
          EXIT(lvPlantTypeRec.External);
      END ELSE BEGIN
        IF lvPlantNoRec.GET("Plant Type", "Plant No.") THEN
          EXIT(lvPlantNoRec.External);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE LookupPlantType@1100525001(PlantType@1210190000 : Code[20]);
    VAR
      PlantTypeRec@1100525000 : Record 11012551;
      TmpPlantTypeRec@1100525002 : TEMPORARY Record 11012551;
      MultiCopyMgt@1100525003 : Codeunit 11012068;
      PlantTypeList@1100525001 : Page 11012552;
      FormLookupOK@1210190001 : Boolean;
    BEGIN
      IF NOT PlantReqRec.GET("Plant Request No.") THEN
        EXIT;
      IF (Type <> Type::Plant) THEN
        EXIT;

      IF PlantReqRec.Type = PlantReqRec.Type::Arrival THEN BEGIN
        PlantTypeRec.FILTERGROUP(9);
        PlantTypeRec.SETRANGE("Order via Plant Request",TRUE);
        PlantTypeRec.FILTERGROUP(0);
      END;
      PlantTypeList.SETTABLEVIEW(PlantTypeRec);

      //PlantTypeRec.Code := "Plant Type";  //*C-028869.o
      //*C-028869.sn
      IF PlantType = '' THEN
        PlantTypeRec.Code := PlantListLastPlantType
      ELSE
        PlantTypeRec.Code := PlantType;
      //*C-028869.en
      PlantTypeList.SETRECORD(PlantTypeRec);
      IF (PlantReqRec.Status <= PlantReqRec.Status::Requested) AND (Type = Type::Plant) THEN BEGIN
        PlantTypeList.LOOKUPMODE(TRUE);
        PlantTypeList.SetMultiCopyAllowed(TRUE);
      END;
      //IF PlantTypeList.RUNMODAL = ACTION::LookupOK THEN BEGIN  //*C-028869.o
      //  PlantTypeList.GETRECORD(PlantTypeRec);                 //*C-028869.o
      //*C-028869.sn
      FormLookupOK := (PlantTypeList.RUNMODAL = ACTION::LookupOK);
      PlantTypeList.GETRECORD(PlantTypeRec);
      PlantListLastPlantType := PlantTypeRec.Code;
      IF FormLookupOK THEN BEGIN
      //*C-028869.en
        VALIDATE("Plant Type", PlantTypeRec.Code);
      END ELSE BEGIN
        IF PlantTypeList.GetMultiCopyRec(TmpPlantTypeRec) THEN BEGIN
          MultiCopyMgt.CopyPlantTypeToPlantRequesLine(TmpPlantTypeRec, PlantReqRec, "Line No.");
          CurrPage.UPDATE(TRUE);
        END;
      END;
    END;

    PROCEDURE LookupPlantInventory@1100485000(ILevel@1100485003 : Integer);
    VAR
      lvInventoryRec@1100485001 : Record 11012555;
      lvInventoryPage@1100485000 : Page 11012559;
      lvDetails@1100485002 : 'InventEntry,PlantType,PlantNo,RateCode';
    BEGIN
      //* Level: 1-Plant Type, 2-Plant No.
      IF NOT PlantReqRec.GET("Plant Request No.") THEN
        EXIT;
      IF (Type <> Type::Plant) THEN
        EXIT;

      IF PlantReqRec.Type = PlantReqRec.Type::Arrival THEN BEGIN
        lvInventoryRec.FILTERGROUP(9);
        lvInventoryRec.SETRANGE("Order via Plant Request",TRUE);
        lvInventoryRec.FILTERGROUP(0);
      END;
      lvInventoryRec.SETRANGE(Removed,FALSE);
      lvInventoryRec.SETRANGE(Location, PlantReqRec."From Location");
      IF (ILevel = 2) THEN
        lvInventoryRec.SETRANGE("Plant Type", "Plant Type");
      lvInventoryRec.Removed := FALSE;
      lvInventoryRec.Location :=  PlantReqRec."From Location";
      lvInventoryRec."Plant Type" := "Plant Type";
      lvInventoryRec."Plant No." := "Plant No.";
      IF ILevel = 1 THEN
        lvInventoryPage.SetDetailSelection(lvDetails::PlantType)
      ELSE
        lvInventoryPage.SetDetailSelection(lvDetails::PlantNo);
      lvInventoryPage.SETTABLEVIEW(lvInventoryRec);
      lvInventoryPage.SETRECORD(lvInventoryRec);
      lvInventoryPage.LOOKUPMODE := TRUE;
      IF lvInventoryPage.RUNMODAL = ACTION::LookupOK THEN BEGIN
        lvInventoryPage.GETRECORD(lvInventoryRec);
        lvInventoryPage.GetDetailSelection(lvDetails);
        IF (ILevel = 1) THEN
          VALIDATE("Plant Type",lvInventoryRec."Plant Type");
        IF ((ILevel = 1) OR (ILevel = 2)) AND (lvDetails <> lvDetails::PlantType) THEN BEGIN
          IF lvInventoryRec."Plant No." = '0' THEN
            lvInventoryRec."Plant No." := '';  //* On Plant Request is '0' not allowed
          VALIDATE("Plant No.",lvInventoryRec."Plant No.");
        END;
      END;
    END;

    PROCEDURE ExplodeSetLines@1100485002();
    BEGIN
      ExplodeSet(Rec);
    END;

    LOCAL PROCEDURE OnAfterGetCurrRec@19077479();
    BEGIN
      xRec := Rec;
      IF UpdateAfterAddLinkedPlant THEN BEGIN  //*01-07-2010  (As in subform PO, F11012562)
       UpdateAfterAddLinkedPlant := FALSE;  //* First reset then update form!
       CurrPage.UPDATE(FALSE);
      END;
      IF NOT PlantReqRec.GET("Plant Request No.") THEN
        PlantReqRec.INIT;
      FromRateCodeVisible := (PlantSetupRec."Rate Codes" AND (PlantReqRec.Type = PlantReqRec.Type::"Other Transfers"));
      PartlyTransferRequestEnabled := (PlantReqRec.Type IN [PlantReqRec.Type::Arrival, PlantReqRec.Type::Removal]);
    END;

    LOCAL PROCEDURE RateCodeOnAfterInput@19044879(VAR Text@19044880 : Text[1024]);
    VAR
      PlantRateCodeRec@1100525000 : Record 11020502;
    BEGIN
      IF PlantReqRec.GET("Plant Request No.") THEN BEGIN
        IF PlantReqRec.Type = PlantReqRec.Type::Removal THEN
          PlantRateCodeRec.ExpandRateCodeOnInput(Text, PlantReqRec."From Location", '')
        ELSE
          PlantRateCodeRec.ExpandRateCodeOnInput(Text, PlantReqRec."To Location", '');
      END;
    END;

    LOCAL PROCEDURE GetStockQuantityOnFormat@19036905(Text@19003996 : Text[1024]);
    BEGIN
      IF NOT PlantReqRec.GET("Plant Request No.") THEN
        Text := ''
      ELSE
        IF PlantReqRec.Type <> PlantReqRec.Type::Arrival THEN
          "GetStockQuantity()HideValue" := TRUE;
    END;

    LOCAL PROCEDURE GetItemInventoryLocationOnForm@19055529(Text@19009914 : Text[1024]);
    BEGIN
      IF (Type <> Type::Item) OR ("Location Code" = '') THEN BEGIN
        GetItemInventoryLocationHideVa := TRUE;
        EXIT;
      END;
      IF NOT PlantReqRec.GET("Plant Request No.") THEN
        Text := ''
      ELSE
        IF PlantReqRec.Type <> PlantReqRec.Type::Arrival THEN
          GetItemInventoryLocationHideVa := TRUE;
    END;

    PROCEDURE UpdateEditable@1100409000();
    BEGIN
      "Set CodeEditable" := ((Type = Type::Plant) AND ("Plant Type" = ''));
      "Plant TypeEditable" := ((Type = Type::Plant) AND ("Set Code" = ''));
      "Plant NoEditable" := (((Type = Type::Plant) AND ("Set Code" = '')) OR (Type = Type::Text)); //DP00241.c (28876)
      QuantityEditable := (Type IN [Type::Plant, Type::Item]);
      Description2Editable := ((Type IN [Type::Plant, Type::Item]) AND ("Set Code" = ''));
      ElementHideValue := (Type = Type::Text);
      ProjectStockEditable := (Type = Type::Item) AND ("Project No. (Inventory)" <> '');
      DeliverEditable := ProjectStockEditable AND "Project Stock";
      ShipToLocationEditable := DeliverEditable AND (Deliver <> Deliver::" ");
      EmployeeEditable := (Type <> Type::Text);
    END;

    BEGIN
    END.
  }
}

