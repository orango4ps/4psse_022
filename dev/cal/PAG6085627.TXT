OBJECT Page 6085627 Activation Wizard - Obsolete
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW110.00.00.5.00.02;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Archivierungsassistent;
               ENU=Activation Wizard;
               NLD=Activeringswizard];
    InsertAllowed=No;
    DeleteAllowed=No;
    PageType=NavigatePage;
    OnInit=BEGIN
             LoadTopBanners;

             IF ProductCode = '' THEN BEGIN
               ProductCode := AboutDC.ProductCode;
               FullProductName := AboutDC.FullProductName;
               ShortProductName := AboutDC.ShortProductName;
               ObjectVersion := AboutDC.ObjectVersion;
             END;
           END;

    OnOpenPage=BEGIN
                 ResetWizardControls;
                 ShowIntroStep;

                 IF ProductCode = AboutDC.ProductCode THEN BEGIN
                   DCSetup.GET;
                   UseCloudOCR := DCSetup."Use Cloud OCR";
                 END ELSE
                   UseCloudOCR := FALSE;
               END;

    ActionList=ACTIONS
    {
      { 1160040012;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1160040011;1 ;Action    ;
                      Name=ActionBack;
                      CaptionML=[DEU=&ZurÅck;
                                 ENU=&Back;
                                 NLD=&Terug];
                      Promoted=Yes;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 1160040010;1 ;Action    ;
                      Name=ActionNext;
                      CaptionML=[DEU=&Weiter;
                                 ENU=&Next;
                                 NLD=&Volgende];
                      Promoted=Yes;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 1160040009;1 ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[DEU=B&eenden;
                                 ENU=&Finish;
                                 NLD=&Gereed];
                      Promoted=Yes;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 6085600; ;Container ;
                ContainerType=ContentArea }

    { 6085583;1;Group     ;
                Name=MediaRepositoryStandardGroup;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 6085582;2;Field     ;
                Name=MediaRepositoryStandard;
                SourceExpr=MediaRepositoryStandard.Image;
                Editable=FALSE;
                ShowCaption=No }

    { 6085581;1;Group     ;
                Name=MediaRepositoryDoneGroup;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 6085580;2;Field     ;
                Name=MediaRepositoryDone;
                SourceExpr=MediaRepositoryDone.Image;
                Editable=FALSE;
                ShowCaption=No }

    { 6085604;1;Group     ;
                Name=Intro;
                Visible=IntroVisible;
                GroupType=Group }

    { 6085602;2;Group     ;
                Name=WelcomeToActivateCompany;
                CaptionML=[DEU=Willkommen bei der Unternehmensaktivierung;
                           ENU=Welcome to Activate Company;
                           NLD=Welkom bij de bedrijfsactivering];
                Visible=IntroVisible;
                GroupType=Group }

    { 6085573;3;Field     ;
                Name=WelcomText1;
                CaptionClass=STRSUBSTNO(WelcomText1,ShortProductName);
                Visible=IntroVisible }

    { 6085574;3;Group     ;
                Name=WelcomText2;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=IntroVisible;
                GroupType=Group;
                InstructionalTextML=[DEU=Sie mÅssen zwei Client-IDs empfangen haben. Eine Client-ID fÅr den Gebrauch in Ihrer Produktionsumgebung und eine andere Client-ID fÅr Ihre Testumgebungen. Die Test-Client-ID ist normalerweise Ihre Produktions-Client-IC, vorangesellt miet "DEMO-".;
                                     ENU=You should have received two Client IDs. One Client ID to use in your production environment and another Client ID for your test environments. The test Client ID is usual your production Client ID prefixed with "DEMO-".;
                                     NLD=U dient twee client-ID's ontvangen te hebben. EÇn client-ID voor gebruik in uw productieomgeving en een andere client-ID voor uw testomgevingen. De test-client-ID is gewoonlijk uw productieclient-ID voorafgegaan door "DEMO-".] }

    { 6085578;3;Group     ;
                Name=WelcomText3;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=IntroVisible;
                GroupType=Group;
                InstructionalTextML=[DEU=Los geht's!;
                                     ENU=Let's go!;
                                     NLD=Laten we beginnen!] }

    { 6085579;3;Field     ;
                Name=WelcomText4;
                CaptionClass=STRSUBSTNO(WelcomText4,ShortProductName);
                Visible=IntroVisible }

    { 6085606;1;Group     ;
                Name=SelectCompanyType;
                Visible=SelectCompanyTypeVisible;
                GroupType=Group }

    { 6085607;2;Group     ;
                Name=SelectCompanyTypeText1;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=SelectCompanyTypeVisible;
                GroupType=Group;
                InstructionalTextML=[DEU=Mîchten Sie ein Test- oder Produktionsunternehmen aktivieren?;
                                     ENU=Do you want to activate a test company or do you want to activate a production company?;
                                     NLD=Wilt u een test- of een productiebedrijf activeren?] }

    { 6085603;3;Field     ;
                Name=ProductionCompanyCheckbox;
                CaptionML=[DEU=Produktionsunternehmen;
                           ENU=Production Company;
                           NLD=Productiebedrijf];
                SourceExpr=ProductionCompany;
                OnValidate=BEGIN
                             IF ProductionCompany THEN
                               TestCompany := FALSE;
                           END;
                            }

    { 6085601;3;Field     ;
                Name=TestCompanyCheckbox;
                CaptionML=[DEU=Testunternehmen;
                           ENU=Test Company;
                           NLD=Testbedrijf];
                SourceExpr=TestCompany;
                OnValidate=BEGIN
                             IF TestCompany THEN
                               ProductionCompany := FALSE;
                           END;
                            }

    { 6085575;1;Group     ;
                Name=ClientCredentialsAndCompanyCode;
                Visible=ClientCredentialsAndCompanyCodeVisible;
                GroupType=Group }

    { 6085591;2;Field     ;
                Name=ClientCredentialsText1;
                CaptionClass=STRSUBSTNO(ClientCredentialsText1,ShortProductName);
                Visible=ClientCredentialsAndCompanyCodeVisible }

    { 6085590;2;Field     ;
                Name=ClientID;
                CaptionML=[DEU=Client-ID;
                           ENU=Client ID;
                           NLD=Client-id];
                SourceExpr=ClientID;
                OnLookup=VAR
                           ContiniaClientCredentials@6085573 : Record 6192771;
                         BEGIN
                           IF PAGE.RUNMODAL(PAGE::"Continia Client Credentials",ContiniaClientCredentials) = ACTION::LookupOK THEN BEGIN
                             ClientID := ContiniaClientCredentials."Client ID";
                             ClientPassword := ContiniaClientCredentials."Client Password";
                           END;
                         END;
                          }

    { 6085589;2;Field     ;
                Name=ClientPassword;
                ExtendedDatatype=Masked;
                CaptionML=[DEU=Client-Kennwort;
                           ENU=Client Password;
                           NLD=Client wachtwoord];
                SourceExpr=ClientPassword }

    { 6085592;2;Field     ;
                Name=CompanyCode;
                CaptionML=[DEU=Unternehmenscode;
                           ENU=Company Code;
                           NLD=Bedrijfscode];
                CharAllowed=0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ+-;
                SourceExpr=CompanyCode }

    { 6085587;2;Group     ;
                Name=ClientCredentialsText2;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=ClientCredentialsAndCompanyCodeVisible;
                GroupType=Group;
                InstructionalTextML=[DEU=WÑhle Sie NÑchste um obenstehende Information zu speichern und mit dem Archivierungsassistenten fortzufahren.;
                                     ENU=Choose Next to save above information and to continue the activation wizard.;
                                     NLD=Kies Volgende om bovenstaande informatie op te slaan en door te gaan met de activeringswizard.] }

    { 6085577;1;Group     ;
                Name=ActivateCompany;
                Visible=ActivateCompanyVisible;
                GroupType=Group }

    { 6085608;2;Group     ;
                Name=ActivateCompanyTxt1;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=BeenActivatedBefore;
                GroupType=Group;
                InstructionalTextML=[DEU=Dieses Unternehmen ist bereits in Continia Online aktiviert mit den Informationen, die auf der markierten Zeile angezeigt werden.;
                                     ENU=This company has already been activated in Continia Online with the information shown on the highlighted line.;
                                     NLD=Dit bedrijf is al in Continia Online geactiveerd met de informatie die op de gemarkeerde regel wordt weergegeven.] }

    { 6085605;2;Field     ;
                Name=ActivateCompanyTxt2;
                CaptionClass=ActivateCompanyTxt2 }

    { 6085593;2;Field     ;
                Name=ActivateCompanyTxt3;
                CaptionClass=ActivateCompanyTxt3 }

    { 6085594;2;Field     ;
                Name=ActivateCompanyTxt4;
                CaptionClass=ActivateCompanyTxt4 }

    { 6085595;2;Field     ;
                Name=ActivateCompanyTxt5;
                CaptionClass=ActivateCompanyTxt5 }

    { 6085609;2;Field     ;
                Name=ActivateCompanyTxt6;
                CaptionClass=ActivateCompanyTxt6;
                Visible=TestCompany }

    { 6085626;2;Group     ;
                Name=ActivateCompanyTxt7;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=TestCompany;
                GroupType=Group;
                InstructionalTextML=[DEU=Wenn Sie ein Testunternehmen erneut aktivieren, wÑhlen Sie dann das dazugehîrige Unternehmen unten. Wenn Sie ein neues Testunternehmen aktivieren, wÑhlen Sie dann << Neue Aktivierung >> unten.;
                                     ENU=If you are reactivating a test company, then select the corresponding company activation below. If you are activating a new test company, then select << New activation >> below.;
                                     NLD=Als u een test bedrijf heractiveert, selecteer dan het bijbehorende bedrijf hieronder. Als u een nieuw test bedrijf activeert, selecteer dan << Nieuwe activatie >> hieronder.] }

    { 6085627;2;Group     ;
                Name=ActivateCompanyTxt8;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=ProductionCompany;
                GroupType=Group;
                InstructionalTextML=[DEU=Wenn Sie ein Produktionsunternehmen erneut aktivieren, wÑhlen Sie dann das dazugehîrige Unternehmen unten. Wenn Sie ein neues Produktionsunternehmen aktivieren, wÑhlen Sie dann << Neue Aktivierung >> unten.;
                                     ENU=If you are reactivating a production company, then select the corresponding company activation below. If you are activating a new production company, then select << New activation >> below.;
                                     NLD=Als u een productie bedrijf heractiveert, selecteer dan het bijbehorende bedrijf hieronder. Als u een nieuw productie bedrijf activeert, selecteer dan << Nieuwe activatie >> hieronder.] }

    { 6085599;2;Field     ;
                Name=ActivateCompanyBlankLine;
                CaptionML=[ENU="";
                           NLD=""] }

    { 6085598;2;Part      ;
                Name=Activations;
                CaptionML=[DEU=Aktivierungen in Continia Online;
                           ENU=Activations in Continia Online;
                           NLD=Activeringen in Continia Online];
                PagePartID=Page6085628;
                Visible=ActivateCompanyVisible;
                PartType=Page }

    { 6085576;1;Group     ;
                Name=AcceptLicense;
                Visible=AcceptLicenseVisible;
                GroupType=Group }

    { 6085586;2;Group     ;
                Name=LicenseTermsGroup;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=AcceptLicenseVisible;
                GroupType=Group }

    { 6085625;3;Field     ;
                Name=ContiniaLicenseTerms;
                CaptionML=[DEU=Continia-Lizenzbedingungen;
                           ENU=Continia License Terms;
                           NLD=Continia Licentievoorwaarden];
                Style=StrongAccent;
                StyleExpr=TRUE }

    { 6085624;3;Field     ;
                Name=LicenseInfo1;
                CaptionClass=LicenseInfo[1];
                Style=StandardAccent;
                StyleExpr=TRUE }

    { 6085623;3;Field     ;
                Name=LicenseInfo2;
                CaptionClass=LicenseInfo[2] }

    { 6085622;3;Field     ;
                Name=LicenseInfo3;
                CaptionClass=LicenseInfo[3] }

    { 6085621;3;Field     ;
                Name=LicenseInfo4;
                CaptionClass=LicenseInfo[4] }

    { 6085620;3;Field     ;
                Name=LicenseInfo5;
                CaptionClass=LicenseInfo[5] }

    { 6085619;3;Field     ;
                Name=LicenseInfo6;
                CaptionClass=LicenseInfo[6] }

    { 6085618;3;Field     ;
                Name=LicenseInfo7;
                CaptionClass=LicenseInfo[7] }

    { 6085617;3;Field     ;
                Name=LicenseInfo8;
                CaptionClass=LicenseInfo[8] }

    { 6085616;3;Field     ;
                Name=LicenseInfo9;
                CaptionClass=LicenseInfo[9] }

    { 6085615;3;Field     ;
                Name=LicenseInfo10;
                CaptionClass=LicenseInfo[10] }

    { 6085614;3;Field     ;
                Name=LicenseInfo11;
                CaptionClass=LicenseInfo[11] }

    { 6085613;3;Field     ;
                Name=LicenseInfo12;
                CaptionClass=LicenseInfo[12] }

    { 6085612;3;Field     ;
                Name=LicenseInfo13;
                CaptionClass=LicenseInfo[13] }

    { 6085611;3;Field     ;
                Name=LicenseInfo14;
                CaptionClass=LicenseInfo[14] }

    { 6085610;3;Field     ;
                Name=LicenseInfo15;
                CaptionClass=LicenseInfo[15] }

    { 6085597;3;Field     ;
                Name=LicenseInfo16;
                CaptionClass=LicenseInfo[16] }

    { 6085585;3;Field     ;
                Name=LicenseTermsAccepted;
                CaptionML=[DEU=Ich akzeptiere die Continia-Softwarelizenzbedingungen;
                           ENU=I accept the Continia Software License Terms;
                           NLD=Ik accepteer de Continia softwarelicentievoorwaarden];
                SourceExpr=LicenseTermsAccepted }

    { 6085584;3;Field     ;
                Name=LinkToLicenseTerms;
                CaptionML=[ENU="";
                           NLD=""];
                SourceExpr=GetLinkTitle;
                Editable=False;
                Style=StandardAccent;
                StyleExpr=TRUE;
                OnLookup=BEGIN
                           HYPERLINK(ContiniaOnlineLicenseMgt.GetContiniaSoftwareLicenseTerm);
                         END;
                          }

    { 6085596;1;Group     ;
                Name=WizardCompleted;
                Visible=WizardCompletedVisible;
                GroupType=Group }

    { 6085628;2;Field     ;
                Name=WizardCompleteTxt1;
                CaptionClass=STRSUBSTNO(WizardCompleteTxt1,FullProductName,COMPANYNAME) }

    { 6085629;2;Group     ;
                Name=WizardCompleteTxt2;
                CaptionML=[ENU="";
                           NLD=""];
                Visible=UseCloudOCR;
                GroupType=Group;
                InstructionalTextML=[DEU=OCR-Konfigurationsdateien sind exportiert worden.;
                                     ENU=OCR configuration files have been exported.;
                                     NLD=OCR-configuratiebestanden zijn geâxporteerd.] }

    { 6085630;2;Field     ;
                Name=WizardCompleteTxt3;
                CaptionClass=STRSUBSTNO(WizardCompleteTxt3,ShortProductName) }

  }
  CODE
  {
    VAR
      ContiniaCompanySetup@6085579 : Record 6192773;
      ContiniaClientCredentials@6085578 : Record 6192771;
      DCSetup@6085629 : Record 6085573;
      EMSetup@6085632 : Record 6086300;
      MediaRepositoryStandard@6085593 : Record 9400;
      MediaRepositoryDone@6085591 : Record 9400;
      AboutDC@6085607 : Codeunit 6085589;
      ContiniaOnlineLicenseMgt@6085623 : Codeunit 6192773;
      ContiniaOnlineMgt@6085602 : Codeunit 6192772;
      ContiniaOnlineDC@6085589 : Codeunit 6085617;
      CompanyGUIDSelected@6085616 : GUID;
      ClientPassword@6085581 : Text[50];
      FullProductName@6085575 : Text[1024];
      ShortProductName@6085631 : Text[30];
      CompanyNameSelected@6085614 : Text[50];
      LicenseInfo@6085580 : ARRAY [100] OF Text[1024];
      ObjectVersion@6085630 : Text[30];
      ClientID@6085642 : Code[20];
      CompanyCode@6085636 : Code[10];
      CompanyCodeSelected@6085577 : Code[10];
      ProductCode@6085615 : Code[10];
      PreviousStep@6085606 : 'Intro,SelectCompanyType,ClientCredentialsAndCompanyCode,ActivateCompany,AcceptLicense,WizardCompleted';
      CurrentStep@6085582 : 'Intro,SelectCompanyType,ClientCredentialsAndCompanyCode,ActivateCompany,AcceptLicense,WizardCompleted';
      ActivateCompanyVisible@6085590 : Boolean;
      AcceptLicenseVisible@6085587 : Boolean;
      BeenActivatedBefore@6085583 : Boolean;
      BackEnabled@6085594 : Boolean INDATASET;
      ClientCredentialsAndCompanyCodeVisible@6085586 : Boolean;
      DoneVisible@6085595 : Boolean;
      FinishEnabled@6085643 : Boolean INDATASET;
      GotClientCredentials@6085608 : Boolean;
      IntroVisible@6085576 : Boolean;
      LicenseTermsAccepted@6085627 : Boolean;
      NextEnabled@6085622 : Boolean INDATASET;
      NewRegistrationSelected@6085588 : Boolean;
      ProductionCompany@6085573 : Boolean;
      ActivateCompanyTxt2@6085605 : TextConst 'DEU=Sie mÅssen unten eine vorhandene Aktivierung auswÑhlen, wenn Sie das aktuelle Unternehmen neu aktivieren und mit einer Aktivierung in Continia Online verknÅpfen mîchten.;ENU=You must select an existing activation below if you want to reactivate the current company and link it to an activation in Continia Online. You need to reactivate when you have performed one of the following actions.;NLD=U moet een bestaande activering hieronder selecteren als u het huidige bedrijf opnieuw wilt activeren en koppelen aan een activering in Continia Online. U moet opnieuw activeren wanneer u een van de volgende acties hebt uitgevoerd.';
      ActivateCompanyTxt3@6085604 : TextConst 'DEU=" - Unternehmen umbenannt";ENU=" - Renamed the company";NLD=" - Bedrijf hernoemd"';
      ActivateCompanyTxt4@6085603 : TextConst 'DEU=" - Unternehmenscode geÑndert";ENU=" - Changed Company Code";NLD=" - Bedrijfscode gewijzigd"';
      ActivateCompanyTxt5@6085600 : TextConst 'DEU=" -Die Datenbank wurde auf einen anderen Server verschoben";ENU=" - Moved the database to another server";NLD=" -De database naar een andere server verplaatst"';
      ActivateCompanyTxt6@6085599 : TextConst 'DEU=- Ein bestehenedes Testunternehmen aus einem Produktionsunternehmen wiederherstellen;ENU=" - Restored an existing test company from a production company";NLD=" - Een bestaand testbedrijf van een productiebedrijf teruggezet"';
      ClientIDMissingErr@6085598 : TextConst 'DEU=Sie mÅssen eine Client-ID angeben.;ENU=You must specify a Client ID.;NLD=U moet een client-ID opgeven.';
      ClientPasswordMissingErr@6085592 : TextConst 'DEU=Sie mÅssen ein Kennwort angeben.;ENU=You must specify a Client Password.;NLD=U moet een wachtwoord opgeven.';
      CompanyCodeMissingErr@6085601 : TextConst 'DEU=Sie mÅssen einen Unternehmenscode angeben.;ENU=You must specify a Company Code.;NLD=U dient een bedrijfscode op te geven.';
      LicenseInfoTxt1@6085617 : TextConst 'DEU=Die folgenden Informationen werden an Continia Online gesendet:;ENU=Thank you for choosing %1.;NLD=Dank voor het kiezen van %1.';
      LicenseInfoTxt2@6085613 : TextConst 'DEU=Um Ihre %1-Installation zu aktivieren, mÅssen Sie mit den Softwarelizenzbedingungen von Continia zustimmen.;ENU=To activate your %1 installation, you must accept the Continia Software License Terms.;NLD=Om uw %1-installatie te activeren, moet u akkoord gaan met de Continia-software licentievoorwaarden.';
      LicenseInfoTxt3@6085611 : TextConst 'DEU=Sie mÅssen die Bedingungen der Endbenutzervereinbarung akzeptieren.;ENU=To accept the agreement click the check box below.;NLD=Om de voorwaarden te accepteren, vinkt u onderstaand veld aan.';
      LicenseInfoTxt4@6085610 : TextConst 'DEU=Klicken Sie unten in das KontrollkÑstchen, um die Vereinbarung zu akzeptieren.;ENU=The following information will be sent to Continia Online:;NLD=De volgende informatie wordt verstuurd naar Continia Online:';
      LicenseTermsMustBeAcceptedErr@6085618 : TextConst 'DEU=Sie mÅssen die Softwarelizenzbedingungen von Continia akzeptieren.;ENU=You must accept the Continia Software License Terms to continue.;NLD=U dient de Continia Software licentievoorwaarden te accepteren.';
      NoRegistrationSelectedErr@6085619 : TextConst 'DEU=Sie mÅssen eine vorhandene Aktivierung von Continia Online auswÑhlen oder wÑhlen Sie << Neue Akvitierung >> um fortzufahren.;ENU=You must select an existing activation from Continia Online or select << New activation >> to continue.;NLD=U moet een bestaande activatie van Continia Online selecteren of kies voor << Nieuwe activatie >> om verder te gaan.';
      ProdClientCredInTestCompErr@6085609 : TextConst 'DEU=Anmeldedaten Produktion (%1) kînnen nicht in einem Testunternehmen verwendet werden.;ENU=Production client credentials (%1) cannot be used in a test company.;NLD=Inloggegevens productie (%1) kunnen niet worden gebruikt in een testbedrijf.';
      SelectCompanyMissingErr@6085596 : TextConst 'DEU=Sie mÅssen auswÑhlen, ob Sie ein Produktionsunternehmen oder ein Testunternehmen aktivieren mîchten.;ENU=You must select if you wish to activate a production company or a test company.;NLD=U moet selecteren of u een productiebedrijf of een testbedrijf wilt activeren.';
      TestClientCredInProdCompErr@6085612 : TextConst 'DEU=Anmeldedaten Test (%1) kînnen nicht in einem Produktionsunternehmen verwendet werden.;ENU=Test client credentials (%1) cannot be used in a production company.;NLD=Inloggegevens test (%1) kunnen niet worden gebruikt in een productiebedrijf.';
      TextLinkTitleTxt@6085628 : TextConst 'DEU=Zeige Softwarelizenzbedingungen von Continia an.;ENU=Show Continia Software License Terms;NLD=Toon Continia Software licentievoorwaarden.';
      UpdateWarningQst@6085620 : TextConst 'DEU=Sie haben eine vorhandene Aktivierung ausgewÑhlt und diese wird die vorhandene Aktivierung in Continia Online aktualisieren.\\Es ist sehr wichtig, dass Sie dafÅr Sorge tragen, dass kein einziges anderes Unternehmen in einer NAV-Datenbank mit Ihren Kundenreferenzen diese Aktivierung verwendet.\\Mîchten Sie fortfahren?;ENU=You have selected an existing activation and this will update the existing activation in Continia Online.\\It is very important that you make sure no other company in any NAV database with your client credentials are using this activation.\\Do you want to continue?;NLD=U hebt een bestaande activering geselecteerd en dit zal de bestaande activering in Continia Online bijwerken. \\Het is zeer belangrijk dat u ervoor zorgt dat geen enkel ander bedrijf in een NAV-database met uw klantreferenties deze activering gebruikt.\\Wilt u doorgaan?';
      WizardCompleteTxt1@6085637 : TextConst 'DEU=%1 wurde aktiviert fÅr %2.;ENU=%1 has been activated for %2.;NLD=%1 is geactiveerd voor %2.';
      WizardCompleteTxt3@6085639 : TextConst 'DEU=Dieser Assistent ist jetzt fertig. Wir hoffen, dass Sie mit VergnÅgen mit Continia %1 arbeiten werden.;ENU=This wizard has now completed. We hope you will enjoy working with Continia %1.;NLD=Deze wizard is nu gereed. Wij hopen dat u met genoegen van Continia %1 gebruik zult maken.';
      SelectCompanyTypeVisible@6085597 : Boolean;
      TestCompany@6085584 : Boolean;
      TopBannerVisible@6085656 : Boolean;
      UseCloudOCR@6085621 : Boolean;
      WizardCompletedVisible@6085585 : Boolean INDATASET;
      TestClientIDConfiguredQst@6085574 : TextConst 'DEU=Die Anmeldedaten, die in der Continia Unternehmenseinrichtung in diesem Unternehmen konfiguriert wurden, sind fÅr die Testsysteme gemeint und kînnen nicht fÅr Produktionssysteme verwendet werden.\\Mîchten Sie, dass diese automatisch in den Anmeldedaten fÅr Produktionssysteme geÑndert werden?;ENU=The client credentials configured in this company in Continia Company Setup are for test systems and cannot be used for production systems.\\Do you want them changed automatically to your client credentials for production systems?;NLD=De inloggegevens die in de Continia bedrijfsinstellingen in dit bedrijf zijn geconfigureerd, zijn bedoeld voor testsystemen en kunnen niet worden gebruikt voor productiesystemen.\\Wilt u dat deze automatisch worden gewijzigd in de inloggegevens voor productiesystemen?';
      ProdClientIDConfiguredQst@6085655 : TextConst 'DEU=Die Anmeldedaten, die in der Continia Unternehmenseinrichtung in diesem Unternehmen konfiguriert wurden, sind fÅr die Produktionssysteme gemeint und kînnen nicht fÅr Testsysteme verwendet werden.\\Mîchten Sie, dass diese automatisch in den Anmeldedaten fÅr Testsysteme geÑndert werden?;ENU=The client credentials configured in this company in Continia Company Setup are for production systems and cannot be used for test systems.\\Do you want them changed automatically to your client credentials for test systems?;NLD=De inloggegevens die in de Continia bedrijfsinstellingen in dit bedrijf zijn geconfigureerd, zijn bedoeld voor productiesystemen en kunnen niet worden gebruikt voor testsystemen.\\Wilt u dat deze automatisch worden gewijzigd in de inloggegevens voor testsystemen?';
      WelcomText1@6085626 : TextConst 'DEU=Bevor Sie %1 verwenden kînnen, mÅssen Sie %1 mit den angegebenen Client-IDs aktivieren. Wenn Sie Ihre Client-IDs nicht empfangen haben, kînnen Sie %1 nicht aktivieren.;ENU=Before you can use %1 you must activate %1 using the provided Client IDs. If you haven''t received your Client IDs, you cannot activate %1.;NLD=Voordat u %1 kunt gebruiken, moet u %1 activeren met behulp van de opgegeven Client-ID''s. Als u uw Client-ID''s niet hebt ontvangen, kunt u %1 niet activeren.';
      WelcomText4@6085625 : TextConst 'DEU=WÑhlen Sie NÑchste um den Archivierungsassistenten %1 zu starten.;ENU=Choose Next to start the %1 activation wizard.;NLD=Kies Volgende om de activeringswizard %1 te starten.';
      ClientCredentialsText1@6085624 : TextConst 'DEU=Um %1 zu aktivieren und zu verwenden, mÅssen Sie Ihre Cntinia-Debitordaten und den Unternehmenscode angeben. Die Zugangsdaten des Continia-Clients erhalten Sie von Ihrem Reseller.;ENU=In order to activate and use %1 you must specify your Continia client credentials and a Company Code. The Continia client credentials are given by your reseller.;NLD=Om %1 te activeren en te gebruiken, moet u uw Continia-klantgegevens en een bedrijfscode opgeven. De legitimatiegegevens van de Continia-client worden door uw reseller verstrekt.';

    LOCAL PROCEDURE NextStep@6085573(Backwards@6085573 : Boolean);
    BEGIN
      PreviousStep := CurrentStep;

      IF Backwards THEN BEGIN
        CASE PreviousStep OF
          PreviousStep::AcceptLicense:
            BEGIN
              IF ActivationsExist(BeenActivatedBefore) THEN
                CurrentStep := CurrentStep - 1
              ELSE
                CurrentStep := CurrentStep - 2;
            END;
          PreviousStep::WizardCompleted:
            BEGIN
              CurrentStep := CurrentStep - 3;
            END;
          ELSE
            CurrentStep := CurrentStep - 1;
        END;
      END ELSE BEGIN
        CASE PreviousStep OF
          PreviousStep::SelectCompanyType:
            BEGIN
              MoveNextFromSelectCompanyType;
              CurrentStep := CurrentStep + 1;
            END;
          PreviousStep::ClientCredentialsAndCompanyCode:
            BEGIN
              MoveNextFromClientCredentialsAndCompanyCode;
              IF ActivationsExist(BeenActivatedBefore) THEN
                CurrentStep := CurrentStep + 1
              ELSE
                CurrentStep := CurrentStep + 2;
            END;
          PreviousStep::ActivateCompany:
            BEGIN
              MoveNextFromActivateCompany;
              CurrentStep := CurrentStep + 1;
            END;

          PreviousStep::AcceptLicense:
            BEGIN
              MoveNextFromAcceptLicense;
              CurrentStep := CurrentStep + 1;
            END;

          ELSE
            CurrentStep := CurrentStep + 1;
        END;
      END;

      ResetWizardControls;

      CASE (CurrentStep) OF
        CurrentStep::Intro:
          BEGIN
            ShowIntroStep;
          END;

        CurrentStep::SelectCompanyType:
          BEGIN
            ShowSelectCompanyType;
          END;

        CurrentStep::ClientCredentialsAndCompanyCode:
          BEGIN
            ShowClientCredentialsAndCompanyCode;
          END;

        CurrentStep::ActivateCompany:
          BEGIN
            ShowActivateCompany;
          END;

        CurrentStep::AcceptLicense:
          BEGIN
            ShowAcceptLicense;
          END;

        CurrentStep::WizardCompleted:
          BEGIN
            ShowWizardCompleted;
          END;
      END;
    END;

    LOCAL PROCEDURE ShowIntroStep@6085590();
    BEGIN
      IntroVisible := TRUE;
      BackEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowSelectCompanyType@6085578();
    BEGIN
      SelectCompanyTypeVisible := TRUE;
      GetClientCredentialsAndCompanyCode;
    END;

    LOCAL PROCEDURE ShowClientCredentialsAndCompanyCode@6085579();
    BEGIN
      ClientCredentialsAndCompanyCodeVisible := TRUE;
      GetClientCredentialsAndCompanyCode;
    END;

    LOCAL PROCEDURE ShowActivateCompany@6085580();
    BEGIN
      ActivateCompanyVisible := TRUE;
      CurrPage.Activations.PAGE.InitCard(ProductCode,CompanyCode);
      CurrPage.Activations.PAGE.UpdateTables;
    END;

    LOCAL PROCEDURE ShowAcceptLicense@6085581();
    BEGIN
      AcceptLicenseVisible := TRUE;
      InitLicenseInfo;
    END;

    LOCAL PROCEDURE ShowWizardCompleted@6085577();
    BEGIN
      WizardCompletedVisible := TRUE;
      BackEnabled := FALSE;
      NextEnabled := FALSE;
      FinishEnabled := TRUE;
    END;

    LOCAL PROCEDURE MoveNextFromSelectCompanyType@6085585();
    VAR
      IsTestClientCredentials@6085573 : Boolean;
    BEGIN
      IF NOT ProductionCompany AND NOT TestCompany THEN
        ERROR(SelectCompanyMissingErr);

      GetClientCredentialsAndCompanyCode;
      IsTestClientCredentials := ContiniaOnlineMgt.CheckIsDemo(ClientID) OR ContiniaOnlineMgt.CheckIsDev(ClientID);

      IF ProductionCompany AND IsTestClientCredentials THEN
        IF CONFIRM(TestClientIDConfiguredQst,TRUE,ContiniaCompanySetup.TABLECAPTION) THEN BEGIN
          IF COPYSTR(ClientID,1,4) = 'DEV-' THEN
            ClientID := COPYSTR(ClientID,5);

          IF COPYSTR(ClientID,1,5) = 'DEMO-' THEN
            ClientID := COPYSTR(ClientID,6);
        END;

      IF TestCompany AND NOT IsTestClientCredentials THEN
        IF CONFIRM(ProdClientIDConfiguredQst,TRUE,ContiniaCompanySetup.TABLECAPTION) THEN
          ClientID := 'DEMO-' + ClientID;
    END;

    LOCAL PROCEDURE MoveNextFromClientCredentialsAndCompanyCode@6085583();
    VAR
      IsTestClientCredentials@6085573 : Boolean;
    BEGIN
      IF ClientID = '' THEN
        ERROR(ClientIDMissingErr);
      IF ClientPassword = '' THEN
        ERROR(ClientPasswordMissingErr);
      IF CompanyCode = '' THEN
        ERROR(CompanyCodeMissingErr);

      IsTestClientCredentials := ContiniaOnlineMgt.CheckIsDemo(ClientID) OR ContiniaOnlineMgt.CheckIsDev(ClientID);
      IF IsTestClientCredentials AND ProductionCompany THEN
        ERROR(TestClientCredInProdCompErr,ClientID);

      IF NOT IsTestClientCredentials AND TestCompany THEN
        ERROR(ProdClientCredInTestCompErr,ClientID);

      IF ContiniaClientCredentials.GET(ClientID) THEN BEGIN
        ContiniaClientCredentials."Client Password" := ClientPassword;
        ContiniaClientCredentials.MODIFY(TRUE);
      END ELSE BEGIN
        ContiniaClientCredentials.INIT;
        ContiniaClientCredentials.VALIDATE("Client ID",ClientID);
        ContiniaClientCredentials."Client Password" := ClientPassword;
        ContiniaClientCredentials.INSERT(TRUE);
      END;

      IF NOT ContiniaCompanySetup.GET THEN BEGIN
        ContiniaCompanySetup.INIT;
        ContiniaCompanySetup.INSERT(TRUE);
      END;

      IF ContiniaCompanySetup."Client ID" <> ClientID THEN
        ContiniaCompanySetup.VALIDATE("Client ID",ClientID);

      IF ContiniaCompanySetup."Company Code" <> CompanyCode THEN
        ContiniaCompanySetup.VALIDATE("Company Code",CompanyCode);

      ContiniaCompanySetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE MoveNextFromActivateCompany@6085587();
    BEGIN
      CurrPage.Activations.PAGE.GetSelectedRecord(NewRegistrationSelected,CompanyNameSelected,CompanyCodeSelected,CompanyGUIDSelected);

      IF NOT NewRegistrationSelected THEN BEGIN
        IF CompanyNameSelected = '' THEN
          ERROR(NoRegistrationSelectedErr);

        ContiniaCompanySetup.GET;
        IF CompanyGUIDSelected <> ContiniaCompanySetup."Company GUID" THEN
          IF NOT CONFIRM(UpdateWarningQst,TRUE) THEN
            ERROR('');
      END;
    END;

    LOCAL PROCEDURE MoveNextFromAcceptLicense@6085589();
    VAR
      NullGUID@6085573 : GUID;
    BEGIN
      IF NOT LicenseTermsAccepted THEN
        ERROR(LicenseTermsMustBeAcceptedErr);

      CASE ProductCode OF
        AboutDC.ProductCode:
          BEGIN
            DCSetup.GET;
            DCSetup.VerifyFilePathsCrossComp;
          END;
        'CEM':
          BEGIN
            EMSetup.GET;
            //EMSetup.VerifyFilePathsCrossComp;
          END;
      END;

      IF CurrPage.Activations.PAGE.IsNewCompany THEN
        ContiniaOnlineLicenseMgt.ActivateProdAndAcceptAgreement(ProductCode,ObjectVersion,FullProductName,LicenseInfo,FALSE,NullGUID)
      ELSE
        ContiniaOnlineLicenseMgt.ActivateProdAndAcceptAgreement(ProductCode,ObjectVersion,FullProductName,LicenseInfo,TRUE,CurrPage.Activations.PAGE.GetGuid);

      COMMIT;
      ContiniaOnlineDC.UpdateCategories(NOT CurrPage.Activations.PAGE.IsNewCompany);
    END;

    LOCAL PROCEDURE ResetWizardControls@6085576();
    BEGIN
      // Buttons
      BackEnabled := TRUE;
      NextEnabled := TRUE;
      FinishEnabled := FALSE;

      // Pages
      IntroVisible := FALSE;
      SelectCompanyTypeVisible := FALSE;
      ClientCredentialsAndCompanyCodeVisible := FALSE;
      ActivateCompanyVisible := FALSE;
      AcceptLicenseVisible := FALSE;
      WizardCompletedVisible := FALSE;
    END;

    LOCAL PROCEDURE LoadTopBanners@6085574();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE)) AND
        MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE))
      THEN
        TopBannerVisible := MediaRepositoryDone.Image.HASVALUE;
    END;

    LOCAL PROCEDURE GetClientCredentialsAndCompanyCode@6085582();
    BEGIN
      IF (ClientID = '') OR (ClientPassword = '') OR (CompanyCode = '') THEN BEGIN
        GotClientCredentials := FALSE;
        IF ContiniaCompanySetup.GET THEN BEGIN
          IF CompanyCode = '' THEN
            CompanyCode := ContiniaCompanySetup."Company Code";
          GotClientCredentials := ContiniaClientCredentials.GET(ContiniaCompanySetup."Client ID");
        END;

        // Only use if Prod crendentials in Prod or Demo in demo
        IF NOT GotClientCredentials THEN
          GotClientCredentials := ContiniaClientCredentials.FINDFIRST;

        IF GotClientCredentials THEN BEGIN
          IF ClientID = '' THEN
            ClientID := ContiniaClientCredentials."Client ID";
          IF ClientPassword = '' THEN
            ClientPassword := ContiniaClientCredentials."Client Password";
        END;
      END;
    END;

    PROCEDURE InitLicenseInfo@6085575();
    VAR
      i@1160040002 : Integer;
      Count@1160040003 : Integer;
      NewLicenseInfo@6085573 : ARRAY [100] OF Text[250];
    BEGIN
      CLEAR(LicenseInfo);

      ContiniaOnlineLicenseMgt.GetStandardLicenseInfo(NewLicenseInfo);

      InserLicenseInfoLine(Count,FullProductName);
      InserLicenseInfoLine(Count,'');
      InserLicenseInfoLine(Count,STRSUBSTNO(LicenseInfoTxt1,ShortProductName));
      InserLicenseInfoLine(Count,STRSUBSTNO(LicenseInfoTxt2,ShortProductName));
      InserLicenseInfoLine(Count,LicenseInfoTxt3);
      InserLicenseInfoLine(Count,'');
      InserLicenseInfoLine(Count,LicenseInfoTxt4);

      i += 1;
      WHILE NewLicenseInfo[i] <> '' DO BEGIN
        InserLicenseInfoLine(Count,'- ' + NewLicenseInfo[i]);
        i += 1;
      END;

      InserLicenseInfoLine(Count,'');
    END;

    PROCEDURE InserLicenseInfoLine@6085586(VAR i@6085573 : Integer;LineText@6085574 : Text[1024]);
    BEGIN
      i += 1;
      LicenseInfo[i] := LineText;
    END;

    LOCAL PROCEDURE GetLinkTitle@6085588() : Text[1024];
    BEGIN
      EXIT(TextLinkTitleTxt)
    END;

    LOCAL PROCEDURE ActivationsExist@6085584(VAR BeenActivatedBefore1@6085575 : Boolean) : Boolean;
    VAR
      ClientActivationBufferTemp@6085573 : TEMPORARY Record 6192775;
      ContiniaOnlineLicenseMgt@6085574 : Codeunit 6192773;
    BEGIN
      BeenActivatedBefore1 := FALSE;

      ContiniaOnlineLicenseMgt.GetCompanyActivations(ClientActivationBufferTemp,FALSE);
      IF ClientActivationBufferTemp.ISEMPTY THEN
        EXIT(FALSE)
      ELSE BEGIN
        IF ContiniaCompanySetup.GET AND NOT ISNULLGUID(ContiniaCompanySetup."Company GUID") THEN BEGIN
          ClientActivationBufferTemp.SETRANGE("Company GUID",ContiniaCompanySetup."Company GUID");
          IF NOT ClientActivationBufferTemp.ISEMPTY THEN
            BeenActivatedBefore1 := TRUE;
        END;

        EXIT(TRUE);
      END;
    END;

    PROCEDURE SetupParameters@6085591(FullProductName1@1002 : Text[1024];ShortProductName1@1003 : Text[30];ObjectVersion1@1001 : Text[30];ProductCode1@1000 : Code[10]);
    BEGIN
      FullProductName := FullProductName1;
      ShortProductName := ShortProductName1;
      ObjectVersion := ObjectVersion1;
      ProductCode := ProductCode1;
    END;

    BEGIN
    END.
  }
}

