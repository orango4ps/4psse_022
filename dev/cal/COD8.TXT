OBJECT Codeunit 8 AccSchedManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=85;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=DEFAULT;NOR=STANDARD;SVE=STANDARD';
      Text001@1001 : TextConst 'ENU=Default Schedule;NOR=Standardskjema;SVE=Standarduppst„llning';
      Text002@1002 : TextConst 'ENU=Default Columns;NOR=Standardkolonne;SVE=Standardkolumner';
      Text012@1006 : TextConst 'ENU=You have entered an illegal value or a nonexistent row number.;NOR=Du har angitt en illegal verdi eller et ikke-eksisterende radnummer.;SVE=Du har skrivit ett otill†tet v„rde eller ett radnr som inte finns.';
      Text013@1007 : TextConst 'ENU=You have entered an illegal value or a nonexistent column number.;NOR=Du har angitt en ugyldig verdi eller et ikke-eksisterende kolonnenummer.;SVE=Du har skrivit ett otill†tet v„rde eller ett kolumnnr som inte finns.';
      Text016@1003 : TextConst '@@@={Locked};ENU=%1\\ %2 %3 %4.;NOR=%1\\ %2 %3 %4.;SVE=%1\\ %2 %3 %4';
      Text017@1011 : TextConst 'ENU=The error occurred when the program tried to calculate:\;NOR=Feilen oppstod da programmet pr›vde † beregne:\;SVE=Felet uppstod n„r programmet f”rs”kte ber„kna:\';
      Text018@1012 : TextConst '@@@="%1 = Row No., %2= Line No., %3 = Totaling";ENU="Acc. Sched. Line: Row No. = %1, Line No. = %2, Totaling = %3\";NOR="Kto.skjemalinje: Radnr. = %1, Linjenr. = %2, Total = %3\";SVE="Kontouppst„llning Rad: Radkod = %1, Rad-nr = %2, Summering = %3\"';
      Text019@1013 : TextConst '@@@="%1 = Column No., %2= Line No., %3 = Formula";ENU="Acc. Sched. Column: Column No. = %1, Line No. = %2, Formula  = %3";NOR="Kto.skjemakolonne: Kolonnenr. = %1, Linjenr. = %2, Formel  = %3";SVE="Kontouppst„llning Kolumn: Kolumnnr = %1, Radnr = %2, Formel = %3"';
      Text020@1014 : TextConst 'ENU=Because of circular references, the program cannot calculate a formula.;NOR=P† grunn av sirkelreferanser kan ikke programmet beregne en formel.;SVE=P† grund av en cirkul„r referens kan formeln inte ber„knas.';
      AccSchedName@1015 : Record 84;
      AccountScheduleLine@1016 : Record 85;
      ColumnLayoutName@1039 : Record 333;
      AccSchedCellValue@1017 : TEMPORARY Record 342;
      CurrExchRate@1018 : Record 330;
      GLSetup@1019 : Record 98;
      AddRepCurrency@1035 : Record 4;
      AnalysisView@1036 : Record 363;
      MatrixMgt@1009 : Codeunit 9200;
      AccountingPeriodMgt@1032 : Codeunit 360;
      AnalysisViewRead@1037 : Boolean;
      StartDate@1020 : Date;
      EndDate@1021 : Date;
      FiscalStartDate@1022 : Date;
      DivisionError@1023 : Boolean;
      PeriodError@1034 : Boolean;
      CallLevel@1024 : Integer;
      CallingAccSchedLineID@1025 : Integer;
      CallingColumnLayoutID@1026 : Integer;
      OldAccSchedLineFilters@1027 : Text;
      OldColumnLayoutFilters@1028 : Text;
      OldAccSchedLineName@1029 : Code[10];
      OldColumnLayoutName@1030 : Code[10];
      OldCalcAddCurr@1031 : Boolean;
      GLSetupRead@1033 : Boolean;
      Text021@1040 : TextConst 'ENU=Conversion of dimension totaling filter %1 results in a filter that becomes too long.;NOR=Konverteringen av dimensjonssammentellingsfilter %1 gj›r at filteret blir for langt.;SVE=Konvertering av dimensionssummeringsfilter %1 resulterar i ett filter som blir f”r l†ng.';
      BasePercentLine@1038 : ARRAY [50] OF Integer;
      Text022@1041 : TextConst 'ENU=You cannot have more than %1 lines with %2 of %3.;NOR=Du kan ikke ha flere enn %1 linjer med %2 i %3.;SVE=Det f†r inte finnas mer „n %1 rader med %2 av %3.';
      Text023@1042 : TextConst 'ENU=Formulas ending with a percent sign require %2 %1 on a line before it.;NOR=Formler som slutter med et prosenttegn, m† ha %2 %1 p† en linje foran seg.;SVE=Formler som avslutas med ett procenttecken m†ste f”reg†s av en rad med %2 %1.';
      Text024@1043 : TextConst 'ENU=The %1 %3 on the %2 must equal the %4 %6 on the %5 when any Dimension Totaling is used in any Column.;NOR=%1 %3 p† %2 m† v‘re lik %4 %6 p† %5 n†r det brukes dimensjonssammentelling i en kolonne.;SVE=%1 %3 p† %2 m†ste vara lika med %4 %6 p† the %5 n„r n†gon form av dimensionssummering anv„nds i n†gon kolumn.';
      ColumnFormulaMsg@1005 : TextConst 'ENU=Column formula: %1.;NOR=Kolonneformel: %1;SVE=Kolumnformel: %1.';
      RowFormulaMsg@1004 : TextConst 'ENU=Row formula: %1.;NOR=Radformel: %1.;SVE=Radformel: %1.';
      ColumnFormulaErrorMsg@1008 : TextConst 'ENU=Column formula: %1. \Error: %2.;NOR=Column formula: %1. \Error: %2.;SVE=Column formula: %1. \Error: %2.';
      Recalculate@1049 : Boolean;
      SystemGeneratedAccSchedQst@1010 : TextConst 'ENU=This account schedule may be automatically updated by the system, so any changes you make may be lost. Do you want to make a copy?;NOR=Dette kontoskjemaet blir kanskje oppdatert automatisk av systemet. Eventuelle endringer du gj›r, vil dermed g† tapt. Vil du ta en kopi?;SVE=Varning! Den h„r kontouppst„llningen kanske uppdateras automatiskt, s† alla „ndringar du g”r kan f”rsvinna.';

    [External]
    PROCEDURE OpenSchedule@1(VAR CurrentSchedName@1000 : Code[10];VAR AccSchedLine@1001 : Record 85);
    BEGIN
      CheckTemplateAndSetFilter(CurrentSchedName,AccSchedLine);
    END;

    [External]
    PROCEDURE OpenAndCheckSchedule@57(VAR CurrentSchedName@1000 : Code[10];VAR AccSchedLine@1001 : Record 85);
    VAR
      AccScheduleName@1003 : Record 84;
      GeneralLedgerSetup@1002 : Record 98;
      CopyAccountSchedule@1004 : Report 26;
      ConfirmManagement@1005 : Codeunit 27;
    BEGIN
      CheckTemplateAndSetFilter(CurrentSchedName,AccSchedLine);
      IF AccSchedLine.ISEMPTY THEN
        EXIT;
      GeneralLedgerSetup.GET;
      IF CurrentSchedName IN
         [GeneralLedgerSetup."Acc. Sched. for Balance Sheet",GeneralLedgerSetup."Acc. Sched. for Cash Flow Stmt",
          GeneralLedgerSetup."Acc. Sched. for Income Stmt.",GeneralLedgerSetup."Acc. Sched. for Retained Earn."]
      THEN
        IF ConfirmManagement.ConfirmProcess(SystemGeneratedAccSchedQst,TRUE) THEN BEGIN
          AccScheduleName.SETRANGE(Name,CurrentSchedName);
          CopyAccountSchedule.SETTABLEVIEW(AccScheduleName);
          CopyAccountSchedule.RUNMODAL;
          CurrentSchedName := CopyAccountSchedule.GetNewAccountScheduleName;
        END;
    END;

    LOCAL PROCEDURE CheckTemplateAndSetFilter@46(VAR CurrentSchedName@1000 : Code[10];VAR AccSchedLine@1001 : Record 85);
    BEGIN
      CheckTemplateName(CurrentSchedName);
      AccSchedLine.FILTERGROUP(2);
      AccSchedLine.SETRANGE("Schedule Name",CurrentSchedName);
      AccSchedLine.FILTERGROUP(0);
    END;

    LOCAL PROCEDURE CheckTemplateName@2(VAR CurrentSchedName@1000 : Code[10]);
    VAR
      AccSchedName@1001 : Record 84;
    BEGIN
      IF NOT AccSchedName.GET(CurrentSchedName) THEN BEGIN
        IF NOT AccSchedName.FINDFIRST THEN BEGIN
          AccSchedName.INIT;
          AccSchedName.Name := Text000;
          AccSchedName.Description := Text001;
          AccSchedName.INSERT;
          COMMIT;
        END;
        CurrentSchedName := AccSchedName.Name;
      END;
    END;

    [External]
    PROCEDURE CheckName@3(CurrentSchedName@1000 : Code[10]);
    VAR
      AccSchedName@1001 : Record 84;
    BEGIN
      AccSchedName.GET(CurrentSchedName);
    END;

    [External]
    PROCEDURE SetName@4(CurrentSchedName@1000 : Code[10];VAR AccSchedLine@1001 : Record 85);
    BEGIN
      AccSchedLine.FILTERGROUP(2);
      AccSchedLine.SETRANGE("Schedule Name",CurrentSchedName);
      AccSchedLine.FILTERGROUP(0);
      IF AccSchedLine.FIND('-') THEN;
    END;

    [External]
    PROCEDURE LookupName@5(CurrentSchedName@1000 : Code[10];VAR EntrdSchedName@1001 : Text[10]) : Boolean;
    VAR
      AccSchedName@1002 : Record 84;
    BEGIN
      AccSchedName.Name := CurrentSchedName;
      IF PAGE.RUNMODAL(0,AccSchedName) <> ACTION::LookupOK THEN
        EXIT(FALSE);

      EntrdSchedName := AccSchedName.Name;
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE OpenColumns@16(VAR CurrentColumnName@1000 : Code[10];VAR ColumnLayout@1001 : Record 334);
    BEGIN
      CheckColumnTemplateName(CurrentColumnName);
      ColumnLayout.FILTERGROUP(2);
      ColumnLayout.SETRANGE("Column Layout Name",CurrentColumnName);
      ColumnLayout.FILTERGROUP(0);
    END;

    LOCAL PROCEDURE CheckColumnTemplateName@15(VAR CurrentColumnName@1000 : Code[10]);
    VAR
      ColumnLayoutName@1001 : Record 333;
    BEGIN
      IF NOT ColumnLayoutName.GET(CurrentColumnName) THEN BEGIN
        IF NOT ColumnLayoutName.FINDFIRST THEN BEGIN
          ColumnLayoutName.INIT;
          ColumnLayoutName.Name := Text000;
          ColumnLayoutName.Description := Text002;
          ColumnLayoutName.INSERT;
          COMMIT;
        END;
        CurrentColumnName := ColumnLayoutName.Name;
      END;
    END;

    [External]
    PROCEDURE CheckColumnName@14(CurrentColumnName@1000 : Code[10]);
    VAR
      ColumnLayoutName@1001 : Record 333;
    BEGIN
      ColumnLayoutName.GET(CurrentColumnName);
    END;

    [External]
    PROCEDURE SetColumnName@13(CurrentColumnName@1000 : Code[10];VAR ColumnLayout@1001 : Record 334);
    BEGIN
      ColumnLayout.RESET;
      ColumnLayout.FILTERGROUP(2);
      ColumnLayout.SETRANGE("Column Layout Name",CurrentColumnName);
      ColumnLayout.FILTERGROUP(0);
    END;

    [External]
    PROCEDURE CopyColumnsToTemp@21(NewColumnName@1000 : Code[10];VAR TempColumnLayout@1001 : Record 334);
    VAR
      ColumnLayout@1002 : Record 334;
    BEGIN
      TempColumnLayout.DELETEALL;
      ColumnLayout.SETRANGE("Column Layout Name",NewColumnName);
      IF ColumnLayout.FIND('-') THEN
        REPEAT
          TempColumnLayout := ColumnLayout;
          TempColumnLayout.INSERT;
        UNTIL ColumnLayout.NEXT = 0;
      IF TempColumnLayout.FIND('-') THEN;
    END;

    [External]
    PROCEDURE LookupColumnName@11(CurrentColumnName@1000 : Code[10];VAR EntrdColumnName@1001 : Text[10]) : Boolean;
    VAR
      ColumnLayoutName@1002 : Record 333;
    BEGIN
      ColumnLayoutName.Name := CurrentColumnName;
      IF PAGE.RUNMODAL(0,ColumnLayoutName) <> ACTION::LookupOK THEN
        EXIT(FALSE);

      EntrdColumnName := ColumnLayoutName.Name;
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE CheckAnalysisView@36(CurrentSchedName@1000 : Code[10];CurrentColumnName@1001 : Code[10];TestColumnName@1002 : Boolean);
    VAR
      ColumnLayout2@1003 : Record 334;
      AnyColumnDimensions@1004 : Boolean;
    BEGIN
      IF NOT AnalysisViewRead THEN BEGIN
        AnalysisViewRead := TRUE;
        IF CurrentSchedName <> AccSchedName.Name THEN BEGIN
          CheckTemplateName(CurrentSchedName);
          AccSchedName.GET(CurrentSchedName);
        END;
        IF TestColumnName THEN
          IF CurrentColumnName <> ColumnLayoutName.Name THEN BEGIN
            CheckColumnTemplateName(CurrentColumnName);
            ColumnLayoutName.GET(CurrentColumnName);
          END;
        IF AccSchedName."Analysis View Name" = '' THEN BEGIN
          GetGLSetup;
          AnalysisView.INIT;
          AnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
          AnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
        END ELSE
          AnalysisView.GET(AccSchedName."Analysis View Name");
        IF AccSchedName."Analysis View Name" <> ColumnLayoutName."Analysis View Name" THEN BEGIN
          AnyColumnDimensions := FALSE;
          ColumnLayout2.SETRANGE("Column Layout Name",ColumnLayoutName.Name);
          IF ColumnLayout2.FIND('-') THEN
            REPEAT
              AnyColumnDimensions :=
                (ColumnLayout2."Dimension 1 Totaling" <> '') OR
                (ColumnLayout2."Dimension 2 Totaling" <> '') OR
                (ColumnLayout2."Dimension 3 Totaling" <> '') OR
                (ColumnLayout2."Dimension 4 Totaling" <> '');
            UNTIL AnyColumnDimensions OR (ColumnLayout2.NEXT = 0);
          IF AnyColumnDimensions THEN
            ERROR(
              Text024,
              AccSchedName.FIELDCAPTION("Analysis View Name"),
              AccSchedName.TABLECAPTION,
              AccSchedName."Analysis View Name",
              ColumnLayoutName.FIELDCAPTION("Analysis View Name"),
              ColumnLayoutName.TABLECAPTION,
              ColumnLayoutName."Analysis View Name");
        END;
      END;
    END;

    LOCAL PROCEDURE AccPeriodStartEnd@26(ColumnLayout@1002 : Record 334;Date@1000 : Date;VAR StartDate@1013 : Date;VAR EndDate@1014 : Date);
    VAR
      Steps@1005 : Integer;
      Type@1004 : ' ,Period,Fiscal Year';
      RangeFromType@1012 : 'Int,CP,LP';
      RangeToType@1011 : 'Int,CP,LP';
      RangeFromInt@1010 : Integer;
      RangeToInt@1009 : Integer;
    BEGIN
      IF ColumnLayout."Comparison Period Formula" = '' THEN
        EXIT;

      ColumnLayout.ParsePeriodFormula(
        ColumnLayout."Comparison Period Formula",Steps,Type,RangeFromType,RangeToType,RangeFromInt,RangeToInt);

      AccountingPeriodMgt.AccPeriodStartEnd(
        Date,StartDate,EndDate,PeriodError,Steps,Type,RangeFromType,RangeToType,RangeFromInt,RangeToInt);
    END;

    LOCAL PROCEDURE InitBasePercents@33(AccSchedLine@1000 : Record 85;ColumnLayout@1001 : Record 334);
    VAR
      BaseIdx@1002 : Integer;
    BEGIN
      CLEAR(BasePercentLine);
      BaseIdx := 0;

      WITH AccSchedLine DO BEGIN
        SETRANGE("Schedule Name","Schedule Name");
        IF FIND('-') THEN
          REPEAT
            IF "Totaling Type" = "Totaling Type"::"Set Base For Percent" THEN BEGIN
              BaseIdx := BaseIdx + 1;
              IF BaseIdx > ARRAYLEN(BasePercentLine) THEN
                ShowError(
                  STRSUBSTNO(Text022,ARRAYLEN(BasePercentLine),FIELDCAPTION("Totaling Type"),"Totaling Type"),
                  AccSchedLine,ColumnLayout);
              BasePercentLine[BaseIdx] := "Line No.";
            END;
          UNTIL NEXT = 0;
      END;

      IF BaseIdx = 0 THEN BEGIN
        AccSchedLine."Totaling Type" := AccSchedLine."Totaling Type"::"Set Base For Percent";
        ShowError(
          STRSUBSTNO(Text023,AccSchedLine.FIELDCAPTION("Totaling Type"),AccSchedLine."Totaling Type"),
          AccSchedLine,ColumnLayout);
      END;
    END;

    LOCAL PROCEDURE GetBasePercentLine@34(AccSchedLine@1000 : Record 85;ColumnLayout@1001 : Record 334) : Integer;
    VAR
      BaseIdx@1002 : Integer;
    BEGIN
      IF BasePercentLine[1] = 0 THEN
        InitBasePercents(AccSchedLine,ColumnLayout);

      BaseIdx := ARRAYLEN(BasePercentLine);
      REPEAT
        IF BasePercentLine[BaseIdx] <> 0 THEN
          IF BasePercentLine[BaseIdx] < AccSchedLine."Line No." THEN
            EXIT(BasePercentLine[BaseIdx]);
        BaseIdx := BaseIdx - 1;
      UNTIL BaseIdx = 0;

      AccSchedLine."Totaling Type" := AccSchedLine."Totaling Type"::"Set Base For Percent";
      ShowError(
        STRSUBSTNO(Text023,AccSchedLine.FIELDCAPTION("Totaling Type"),AccSchedLine."Totaling Type"),
        AccSchedLine,ColumnLayout);
    END;

    LOCAL PROCEDURE GetGLSetup@81() Result : Boolean;
    BEGIN
      IF NOT GLSetupRead THEN
        Result := GLSetup.GET;
      GLSetupRead := TRUE;
    END;

    [External]
    PROCEDURE CalcCell@8(VAR AccSchedLine@1000 : Record 85;VAR ColumnLayout@1001 : Record 334;CalcAddCurr@1002 : Boolean) : Decimal;
    VAR
      Result@1003 : Decimal;
    BEGIN
      AccountScheduleLine.COPYFILTERS(AccSchedLine);
      StartDate := AccountScheduleLine.GETRANGEMIN("Date Filter");
      IF EndDate <> AccountScheduleLine.GETRANGEMAX("Date Filter") THEN BEGIN
        EndDate := AccountScheduleLine.GETRANGEMAX("Date Filter");
        FiscalStartDate := AccountingPeriodMgt.FindFiscalYear(EndDate);
      END;

      //**4PS.sn
      IF (AccountScheduleLine.GETFILTER("Business Unit Filter") = '') AND
         (ColumnLayout."Business Unit Filter" <> '') THEN
        AccountScheduleLine.SETFILTER("Business Unit Filter", ColumnLayout."Business Unit Filter");
      //**4PS.en

      DivisionError := FALSE;
      PeriodError := FALSE;
      CallLevel := 0;
      CallingAccSchedLineID := AccSchedLine."Line No.";
      CallingColumnLayoutID := ColumnLayout."Line No.";

      IF (OldAccSchedLineFilters <> AccSchedLine.GETFILTERS) OR
         (OldColumnLayoutFilters <> ColumnLayout.GETFILTERS) OR
         (OldAccSchedLineName <> AccSchedLine."Schedule Name") OR
         (OldColumnLayoutName <> ColumnLayout."Column Layout Name") OR
         (OldCalcAddCurr <> CalcAddCurr) OR
         Recalculate
      THEN BEGIN
        AccSchedCellValue.RESET;
        AccSchedCellValue.DELETEALL;
        CLEAR(BasePercentLine);
        OldAccSchedLineFilters := AccSchedLine.GETFILTERS;
        OldColumnLayoutFilters := ColumnLayout.GETFILTERS;
        OldAccSchedLineName := AccSchedLine."Schedule Name";
        OldColumnLayoutName := ColumnLayout."Column Layout Name";
        OldCalcAddCurr := CalcAddCurr;
      END;

      Result := CalcCellValue(AccSchedLine,ColumnLayout,CalcAddCurr);
      WITH ColumnLayout DO BEGIN
        CASE Show OF
          Show::"When Positive":
            IF Result < 0 THEN
              Result := 0;
          Show::"When Negative":
            IF Result > 0 THEN
              Result := 0;
        END;
        IF "Show Opposite Sign" THEN
          Result := -Result;
        //**4PS.sn
        IF "Always Reproduce Positive" THEN
          IF Result < 0 THEN
            Result := Result * -1;
        //**4PS.en
      END;
      IF AccSchedLine."Show Opposite Sign" THEN
        Result := -Result;

      OnBeforeCalcCellExit(AccSchedLine,ColumnLayout,CalcAddCurr,Result);
      EXIT(Result);
    END;

    [External]
    PROCEDURE CalcCellValue@6(AccSchedLine@1000 : Record 85;ColumnLayout@1001 : Record 334;CalcAddCurr@1002 : Boolean) : Decimal;
    VAR
      GLAcc@1004 : Record 15;
      CostType@1005 : Record 1103;
      CFAccount@1003 : Record 841;
      Result@1015 : Decimal;
    BEGIN
      Result := 0;
      IF AccSchedLine.Totaling = '' THEN
        EXIT(Result);

      IF AccSchedCellValue.GET(AccSchedLine."Line No.",ColumnLayout."Line No.") THEN BEGIN
        Result := AccSchedCellValue.Value;
        DivisionError := DivisionError OR AccSchedCellValue."Has Error";
        PeriodError := PeriodError OR AccSchedCellValue."Period Error";
      END ELSE BEGIN
        IF ColumnLayout."Column Type" = ColumnLayout."Column Type"::Formula THEN
          Result :=
            EvaluateExpression(
              FALSE,ColumnLayout.Formula,AccSchedLine,ColumnLayout,CalcAddCurr)
        ELSE
          IF AccSchedLine."Totaling Type" IN
             [AccSchedLine."Totaling Type"::Formula,AccSchedLine."Totaling Type"::"Set Base For Percent"]
          THEN
            Result :=
              EvaluateExpression(
                TRUE,AccSchedLine.Totaling,AccSchedLine,ColumnLayout,CalcAddCurr)
          ELSE
            IF (StartDate = 0D) OR (EndDate = 0D) OR (EndDate = DMY2DATE(31,12,9999)) THEN BEGIN
              Result := 0;
              PeriodError := TRUE;
            END ELSE
              CASE AccSchedLine."Totaling Type" OF
                AccSchedLine."Totaling Type"::"Posting Accounts",
                AccSchedLine."Totaling Type"::"Total Accounts":
                  BEGIN
                    AccSchedLine.COPYFILTERS(AccountScheduleLine);
                    SetGLAccRowFilters(GLAcc,AccSchedLine);
                    SetGLAccColumnFilters(GLAcc,AccSchedLine,ColumnLayout);
                    IF (AccSchedLine."Totaling Type" = AccSchedLine."Totaling Type"::"Posting Accounts") AND
                       (AccSchedLine."Account Type" = AccSchedLine."Account Type"::Both) AND  //**4PS.n
                       (STRLEN(AccSchedLine.Totaling) <= MAXSTRLEN(GLAcc.Totaling)) AND (STRPOS(AccSchedLine.Totaling,'*') = 0)
                    THEN BEGIN
                      GLAcc."Account Type" := GLAcc."Account Type"::Total;
                      GLAcc.Totaling := AccSchedLine.Totaling;
                      Result := Result + CalcGLAcc(GLAcc,AccSchedLine,ColumnLayout,CalcAddCurr);
                    END ELSE
                      IF GLAcc.FIND('-') THEN
                        REPEAT
                          Result := Result + CalcGLAcc(GLAcc,AccSchedLine,ColumnLayout,CalcAddCurr);
                        UNTIL GLAcc.NEXT = 0;
                  END;
                AccSchedLine."Totaling Type"::"Cost Type",
                AccSchedLine."Totaling Type"::"Cost Type Total":
                  BEGIN
                    AccSchedLine.COPYFILTERS(AccountScheduleLine);
                    SetCostTypeRowFilters(CostType,AccSchedLine,ColumnLayout);
                    SetCostTypeColumnFilters(CostType,AccSchedLine,ColumnLayout);
                    IF (AccSchedLine."Totaling Type" = AccSchedLine."Totaling Type"::"Cost Type") AND
                       (STRLEN(AccSchedLine.Totaling) <= MAXSTRLEN(GLAcc.Totaling)) AND (STRPOS(AccSchedLine.Totaling,'*') = 0)
                    THEN BEGIN
                      CostType.Type := CostType.Type::Total;
                      CostType.Totaling := AccSchedLine.Totaling;
                      Result := Result + CalcCostType(CostType,AccSchedLine,ColumnLayout,CalcAddCurr);
                    END ELSE
                      IF CostType.FIND('-') THEN
                        REPEAT
                          Result := Result + CalcCostType(CostType,AccSchedLine,ColumnLayout,CalcAddCurr);
                        UNTIL CostType.NEXT = 0;
                  END;
                AccSchedLine."Totaling Type"::"Cash Flow Entry Accounts",
                AccSchedLine."Totaling Type"::"Cash Flow Total Accounts":
                  BEGIN
                    AccSchedLine.COPYFILTERS(AccountScheduleLine);
                    SetCFAccRowFilter(CFAccount,AccSchedLine);
                    SetCFAccColumnFilter(CFAccount,AccSchedLine,ColumnLayout);
                    IF (AccSchedLine."Totaling Type" = AccSchedLine."Totaling Type"::"Cash Flow Entry Accounts") AND
                       (STRLEN(AccSchedLine.Totaling) <= 30)
                    THEN BEGIN
                      CFAccount."Account Type" := CFAccount."Account Type"::Total;
                      CFAccount.Totaling := AccSchedLine.Totaling;
                      Result := Result + CalcCFAccount(CFAccount,AccSchedLine,ColumnLayout);
                    END ELSE
                      IF CFAccount.FIND('-') THEN
                        REPEAT
                          Result := Result + CalcCFAccount(CFAccount,AccSchedLine,ColumnLayout);
                        UNTIL CFAccount.NEXT = 0;
                  END;
              END;

        OnAfterCalcCellValue(AccSchedLine,ColumnLayout,Result);

        AccSchedCellValue."Row No." := AccSchedLine."Line No.";
        AccSchedCellValue."Column No." := ColumnLayout."Line No.";
        AccSchedCellValue.Value := Result;
        AccSchedCellValue."Has Error" := DivisionError;
        AccSchedCellValue."Period Error" := PeriodError;
        AccSchedCellValue.INSERT;
      END;

      OnCalcCellValueOnBeforeExit(AccSchedLine,ColumnLayout,CalcAddCurr,StartDate,EndDate,Result);
      EXIT(Result);
    END;

    LOCAL PROCEDURE CalcGLAcc@7(VAR GLAcc@1001 : Record 15;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1003 : Record 334;CalcAddCurr@1004 : Boolean) ColValue@1000 : Decimal;
    VAR
      GLEntry@1005 : Record 17;
      GLBudgEntry@1006 : Record 96;
      AnalysisViewEntry@1007 : Record 365;
      AnalysisViewBudgetEntry@1008 : Record 366;
      AmountType@1009 : 'Net Amount,Debit Amount,Credit Amount';
      TestBalance@1010 : Boolean;
      Balance@1011 : Decimal;
      UseBusUnitFilter@1111 : Boolean;
      UseDimFilter@1012 : Boolean;
      IsHandled@1013 : Boolean;
    BEGIN
      ColValue := 0;
      IsHandled := FALSE;
      OnBeforeCalcGLAcc(GLAcc,AccSchedLine,ColumnLayout,CalcAddCurr,ColValue,IsHandled);
      IF IsHandled THEN
        EXIT(ColValue);

      UseDimFilter := FALSE;
      IF AccSchedName.Name <> AccSchedLine."Schedule Name" THEN
        AccSchedName.GET(AccSchedLine."Schedule Name");

      IF ConflictAmountType(AccSchedLine,ColumnLayout."Amount Type",AmountType) THEN
        EXIT(0);

      TestBalance :=
        AccSchedLine.Show IN [AccSchedLine.Show::"When Positive Balance",AccSchedLine.Show::"When Negative Balance"];
      IF ColumnLayout."Column Type" <> ColumnLayout."Column Type"::Formula THEN BEGIN
        UseBusUnitFilter := (AccSchedLine.GETFILTER("Business Unit Filter") <> '') OR (ColumnLayout."Business Unit Totaling" <> '');
        UseDimFilter := HasDimFilter(AccSchedLine,ColumnLayout);
        CASE ColumnLayout."Ledger Entry Type" OF
          ColumnLayout."Ledger Entry Type"::Entries:
            IF AccSchedName."Analysis View Name" = '' THEN
              WITH GLEntry DO BEGIN
                SetGLAccGLEntryFilters(GLAcc,GLEntry,AccSchedLine,ColumnLayout,UseBusUnitFilter,UseDimFilter);
                CASE AmountType OF
                  AmountType::"Net Amount":
                    BEGIN
                      IF CalcAddCurr THEN BEGIN
                        CALCSUMS("Additional-Currency Amount");
                        ColValue := "Additional-Currency Amount";
                      END ELSE BEGIN
                        CALCSUMS(Amount);
                        ColValue := Amount;
                      END;
                      Balance := ColValue;
                    END;
                  AmountType::"Debit Amount":
                    IF CalcAddCurr THEN BEGIN
                      IF TestBalance THEN BEGIN
                        CALCSUMS("Add.-Currency Debit Amount","Additional-Currency Amount");
                        Balance := "Additional-Currency Amount";
                      END ELSE
                        CALCSUMS("Add.-Currency Debit Amount");
                      ColValue := "Add.-Currency Debit Amount";
                    END ELSE BEGIN
                      IF TestBalance THEN BEGIN
                        CALCSUMS("Debit Amount",Amount);
                        Balance := Amount;
                      END ELSE
                        CALCSUMS("Debit Amount");
                      ColValue := "Debit Amount";
                    END;
                  AmountType::"Credit Amount":
                    IF CalcAddCurr THEN BEGIN
                      IF TestBalance THEN BEGIN
                        CALCSUMS("Add.-Currency Credit Amount","Additional-Currency Amount");
                        Balance := "Additional-Currency Amount";
                      END ELSE
                        CALCSUMS("Add.-Currency Credit Amount");
                      ColValue := "Add.-Currency Credit Amount";
                    END ELSE BEGIN
                      IF TestBalance THEN BEGIN
                        CALCSUMS("Credit Amount",Amount);
                        Balance := Amount;
                      END ELSE
                        CALCSUMS("Credit Amount");
                      ColValue := "Credit Amount";
                    END;
                END;
              END
            ELSE
              WITH AnalysisViewEntry DO BEGIN
                SetGLAccAnalysisViewEntryFilters(GLAcc,AnalysisViewEntry,AccSchedLine,ColumnLayout);
                CASE AmountType OF
                  AmountType::"Net Amount":
                    BEGIN
                      IF CalcAddCurr THEN BEGIN
                        CALCSUMS("Add.-Curr. Amount");
                        ColValue := "Add.-Curr. Amount";
                      END ELSE BEGIN
                        CALCSUMS(Amount);
                        ColValue := Amount;
                      END;
                      Balance := ColValue;
                    END;
                  AmountType::"Debit Amount":
                    IF CalcAddCurr THEN BEGIN
                      IF TestBalance THEN BEGIN
                        CALCSUMS("Add.-Curr. Debit Amount","Add.-Curr. Amount");
                        Balance := "Add.-Curr. Amount";
                      END ELSE
                        CALCSUMS("Add.-Curr. Debit Amount");
                      ColValue := "Add.-Curr. Debit Amount";
                    END ELSE BEGIN
                      IF TestBalance THEN BEGIN
                        CALCSUMS("Debit Amount",Amount);
                        Balance := Amount;
                      END ELSE
                        CALCSUMS("Debit Amount");
                      ColValue := "Debit Amount";
                    END;
                  AmountType::"Credit Amount":
                    IF CalcAddCurr THEN BEGIN
                      IF TestBalance THEN BEGIN
                        CALCSUMS("Add.-Curr. Credit Amount","Add.-Curr. Amount");
                        Balance := "Add.-Curr. Amount";
                      END ELSE
                        CALCSUMS("Add.-Curr. Credit Amount");
                      ColValue := "Add.-Curr. Credit Amount";
                    END ELSE BEGIN
                      IF TestBalance THEN BEGIN
                        CALCSUMS("Credit Amount",Amount);
                        Balance := Amount;
                      END ELSE
                        CALCSUMS("Credit Amount");
                      ColValue := "Credit Amount";
                    END;
                END;
              END;
          ColumnLayout."Ledger Entry Type"::"Budget Entries":
            BEGIN
              IF AccSchedName."Analysis View Name" = '' THEN
                WITH GLBudgEntry DO BEGIN
                  SetGLAccGLBudgetEntryFilters(GLAcc,GLBudgEntry,AccSchedLine,ColumnLayout,UseBusUnitFilter,UseDimFilter);
                  CASE AmountType OF
                    AmountType::"Net Amount":
                      BEGIN
                        CALCSUMS(Amount);
                        ColValue := Amount;
                      END;
                    AmountType::"Debit Amount":
                      BEGIN
                        CALCSUMS(Amount);
                        ColValue := Amount;
                        IF ColValue < 0 THEN
                          ColValue := 0;
                      END;
                    AmountType::"Credit Amount":
                      BEGIN
                        CALCSUMS(Amount);
                        ColValue := -Amount;
                        IF ColValue < 0 THEN
                          ColValue := 0;
                      END;
                  END;
                  Balance := Amount;
                END
              ELSE
                WITH AnalysisViewBudgetEntry DO BEGIN
                  SetGLAccAnalysisViewBudgetEntries(GLAcc,AnalysisViewBudgetEntry,AccSchedLine,ColumnLayout);
                  CASE AmountType OF
                    AmountType::"Net Amount":
                      BEGIN
                        CALCSUMS(Amount);
                        ColValue := Amount;
                      END;
                    AmountType::"Debit Amount":
                      BEGIN
                        CALCSUMS(Amount);
                        ColValue := Amount;
                        IF ColValue < 0 THEN
                          ColValue := 0;
                      END;
                    AmountType::"Credit Amount":
                      BEGIN
                        CALCSUMS(Amount);
                        ColValue := -Amount;
                        IF ColValue < 0 THEN
                          ColValue := 0;
                      END;
                  END;
                  Balance := Amount;
                END;
              IF CalcAddCurr THEN
                ColValue := CalcLCYToACY(ColValue);
            END;
        END;

        OnBeforeTestBalance(
          GLAcc,AccSchedName,AccSchedLine,ColumnLayout,AmountType,ColValue,CalcAddCurr,TestBalance,GLEntry,GLBudgEntry);

        IF TestBalance THEN BEGIN
          IF AccSchedLine.Show = AccSchedLine.Show::"When Positive Balance" THEN
            IF Balance < 0 THEN
              EXIT(0);
          IF AccSchedLine.Show = AccSchedLine.Show::"When Negative Balance" THEN
            IF Balance > 0 THEN
              EXIT(0);
        END;
      END;
      EXIT(ColValue);
    END;

    LOCAL PROCEDURE CalcCFAccount@39(VAR CFAccount@1000 : Record 841;VAR AccSchedLine@1001 : Record 85;VAR ColumnLayout@1002 : Record 334) ColValue@1003 : Decimal;
    VAR
      CFForecastEntry@1004 : Record 847;
      AnalysisViewEntry@1007 : Record 365;
      AmountType@1009 : 'Net Amount,Debit Amount,Credit Amount';
      IsHandled@1005 : Boolean;
    BEGIN
      ColValue := 0;
      IsHandled := FALSE;
      OnBeforeCalcCFAcc(CFAccount,AccSchedLine,ColumnLayout,ColValue,IsHandled);
      IF IsHandled THEN
        EXIT(ColValue);

      IF AccSchedName.Name <> AccSchedLine."Schedule Name" THEN
        AccSchedName.GET(AccSchedLine."Schedule Name");

      IF ConflictAmountType(AccSchedLine,ColumnLayout."Amount Type",AmountType) THEN
        EXIT(0);

      IF ColumnLayout."Column Type" <> ColumnLayout."Column Type"::Formula THEN
        CASE ColumnLayout."Ledger Entry Type" OF
          ColumnLayout."Ledger Entry Type"::Entries:
            IF AccSchedName."Analysis View Name" = '' THEN
              WITH CFForecastEntry DO BEGIN
                SetCFEntryFilters(CFAccount,CFForecastEntry,AccSchedLine,ColumnLayout);
                CASE ColumnLayout."Amount Type" OF
                  ColumnLayout."Amount Type"::"Net Amount":
                    BEGIN
                      CALCSUMS("Amount (LCY)");
                      ColValue := "Amount (LCY)";
                    END;
                END;
              END
            ELSE
              WITH AnalysisViewEntry DO BEGIN
                SetCFAnalysisViewEntryFilters(CFAccount,AnalysisViewEntry,AccSchedLine,ColumnLayout);
                CASE ColumnLayout."Amount Type" OF
                  ColumnLayout."Amount Type"::"Net Amount":
                    BEGIN
                      CALCSUMS(Amount);
                      ColValue := Amount;
                    END;
                END;
              END;
        END;

      EXIT(ColValue);
    END;

    LOCAL PROCEDURE CalcCellValueInAccSchedLines@92(SourceAccSchedLine@1000 : Record 85;ColumnLayout@1004 : Record 334;Expression@1002 : Text;CalcAddCurr@1005 : Boolean;IsFilter@1007 : Boolean) Result : Decimal;
    VAR
      AccSchedLine@1001 : Record 85;
      IsHandled@1003 : Boolean;
      CellValue@1006 : Decimal;
    BEGIN
      AccSchedLine.SETRANGE("Schedule Name",SourceAccSchedLine."Schedule Name");
      AccSchedLine.SETFILTER("Row No.",Expression);
      IF AccSchedLine.FIND('-') THEN
        REPEAT
          IF AccSchedLine."Line No." <> SourceAccSchedLine."Line No." THEN BEGIN
            IsHandled := FALSE;
            OnEvaluateExpressionOnBeforeCalcAccSchedLineCellValue(
              SourceAccSchedLine,AccSchedLine,ColumnLayout,CalcAddCurr,IsHandled,CellValue);
            IF IsHandled THEN
              Result += CellValue
            ELSE
              Result := Result + CalcCellValue(AccSchedLine,ColumnLayout,CalcAddCurr);
          END;
        UNTIL AccSchedLine.NEXT = 0
      ELSE
        IF IsFilter OR (NOT EVALUATE(Result,Expression)) THEN
          ShowError(Text012,SourceAccSchedLine,ColumnLayout);
    END;

    LOCAL PROCEDURE CalcCellValueInColumnLayouts@93(SourceColumnLayout@1001 : Record 334;AccSchedLine@1003 : Record 85;Expression@1002 : Text;CalcAddCurr@1005 : Boolean;IsFilter@1004 : Boolean) Result : Decimal;
    VAR
      ColumnLayout@1000 : Record 334;
      IsHandled@1006 : Boolean;
      CellValue@1007 : Decimal;
    BEGIN
      ColumnLayout.SETRANGE("Column Layout Name",SourceColumnLayout."Column Layout Name");
      ColumnLayout.SETFILTER("Column No.",Expression);
      IF ColumnLayout.FIND('-') THEN
        REPEAT
          IF ColumnLayout."Line No." <> SourceColumnLayout."Line No." THEN BEGIN
            IsHandled := FALSE;
            OnEvaluateExpressionOnBeforeCalcColumnLayoutCellValue(
              SourceColumnLayout,AccSchedLine,ColumnLayout,CalcAddCurr,IsHandled,CellValue);
            IF IsHandled THEN
              Result += CellValue
            ELSE
              Result := Result + CalcCellValue(AccSchedLine,ColumnLayout,CalcAddCurr);
          END;
        UNTIL ColumnLayout.NEXT = 0
      ELSE
        IF IsFilter OR (NOT EVALUATE(Result,Expression)) THEN
          ShowError(Text013,AccSchedLine,SourceColumnLayout);
    END;

    [External]
    PROCEDURE SetGLAccRowFilters@17(VAR GLAcc@1000 : Record 15;VAR AccSchedLine2@1001 : Record 85);
    BEGIN
      //**4PS.sn
      WITH AccSchedLine2 DO
        CASE "Account Type" OF
          "Account Type"::"Income Statement":
            GLAcc.SETRANGE("Income/Balance",GLAcc."Income/Balance"::"Income Statement");
          "Account Type"::Balance:
            GLAcc.SETRANGE("Income/Balance",GLAcc."Income/Balance"::"Balance Sheet");
        ELSE
          GLAcc.SETRANGE("Income/Balance");
        END;
      //**4PS.en

      WITH AccSchedLine2 DO
        CASE "Totaling Type" OF
          "Totaling Type"::"Posting Accounts":
            BEGIN
              GLAcc.SETFILTER("No.",Totaling);
              GLAcc.SETRANGE("Account Type",GLAcc."Account Type"::Posting);
            END;
          "Totaling Type"::"Total Accounts":
            BEGIN
              GLAcc.SETFILTER("No.",Totaling);
              GLAcc.SETFILTER("Account Type",'<>%1',GLAcc."Account Type"::Posting);
            END;
        END;

      OnAfterSetGLAccRowFilters(GLAcc,AccSchedLine2);
    END;

    LOCAL PROCEDURE SetGLAccGLEntryFilters@35(VAR GLAcc@1000 : Record 15;VAR GLEntry@1001 : Record 17;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1003 : Record 334;UseBusUnitFilter@1004 : Boolean;UseDimFilter@1005 : Boolean);
    BEGIN
      WITH GLEntry DO BEGIN
        IF UseBusUnitFilter THEN
          IF UseDimFilter THEN
            SETCURRENTKEY(
              "G/L Account No.","Business Unit Code","Global Dimension 1 Code","Global Dimension 2 Code")
          ELSE
            SETCURRENTKEY(
              "G/L Account No.","Business Unit Code","Posting Date")
        ELSE
          IF UseDimFilter THEN
            SETCURRENTKEY("G/L Account No.","Global Dimension 1 Code","Global Dimension 2 Code")
          ELSE
            SETCURRENTKEY("G/L Account No.","Posting Date");
        IF GLAcc.Totaling = '' THEN
          SETRANGE("G/L Account No.",GLAcc."No.")
        ELSE
          SETFILTER("G/L Account No.",GLAcc.Totaling);
        GLAcc.COPYFILTER("Date Filter","Posting Date");
        AccSchedLine.COPYFILTER("Business Unit Filter","Business Unit Code");
        AccSchedLine.COPYFILTER("Dimension 1 Filter","Global Dimension 1 Code");
        AccSchedLine.COPYFILTER("Dimension 2 Filter","Global Dimension 2 Code");
        FILTERGROUP(2);
        SETFILTER("Global Dimension 1 Code",GetDimTotalingFilter(1,AccSchedLine."Dimension 1 Totaling"));
        SETFILTER("Global Dimension 2 Code",GetDimTotalingFilter(2,AccSchedLine."Dimension 2 Totaling"));
        FILTERGROUP(8);
        SETFILTER("Global Dimension 1 Code",GetDimTotalingFilter(1,ColumnLayout."Dimension 1 Totaling"));
        SETFILTER("Global Dimension 2 Code",GetDimTotalingFilter(2,ColumnLayout."Dimension 2 Totaling"));
        SETFILTER("Business Unit Code",ColumnLayout."Business Unit Totaling");
        FILTERGROUP(0);
      END;

      OnAfterSetGLAccGLEntryFilters(GLAcc,GLEntry,AccSchedLine,ColumnLayout,UseBusUnitFilter,UseDimFilter);
    END;

    LOCAL PROCEDURE SetGLAccGLBudgetEntryFilters@69(VAR GLAcc@1000 : Record 15;VAR GLBudgetEntry@1001 : Record 96;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1003 : Record 334;UseBusUnitFilter@1004 : Boolean;UseDimFilter@1005 : Boolean);
    BEGIN
      WITH GLBudgetEntry DO BEGIN
        IF UseBusUnitFilter OR UseDimFilter THEN
          SETCURRENTKEY(
            "Budget Name","G/L Account No.","Business Unit Code",
            "Global Dimension 1 Code","Global Dimension 2 Code",
            "Budget Dimension 1 Code","Budget Dimension 2 Code",
            "Budget Dimension 3 Code","Budget Dimension 4 Code",Date)
        ELSE
          SETCURRENTKEY("Budget Name","G/L Account No.",Date);
        IF GLAcc.Totaling = '' THEN
          SETRANGE("G/L Account No.",GLAcc."No.")
        ELSE
          SETFILTER("G/L Account No.",GLAcc.Totaling);
        GLAcc.COPYFILTER("Date Filter",Date);
        AccSchedLine.COPYFILTER("G/L Budget Filter","Budget Name");
        AccSchedLine.COPYFILTER("Business Unit Filter","Business Unit Code");
        AccSchedLine.COPYFILTER("Dimension 1 Filter","Global Dimension 1 Code");
        AccSchedLine.COPYFILTER("Dimension 2 Filter","Global Dimension 2 Code");
        FILTERGROUP(2);
        SETFILTER("Global Dimension 1 Code",GetDimTotalingFilter(1,AccSchedLine."Dimension 1 Totaling"));
        SETFILTER("Global Dimension 2 Code",GetDimTotalingFilter(2,AccSchedLine."Dimension 2 Totaling"));
        FILTERGROUP(8);
        SETFILTER("Global Dimension 1 Code",GetDimTotalingFilter(1,ColumnLayout."Dimension 1 Totaling"));
        SETFILTER("Global Dimension 2 Code",GetDimTotalingFilter(2,ColumnLayout."Dimension 2 Totaling"));
        SETFILTER("Business Unit Code",ColumnLayout."Business Unit Totaling");
        FILTERGROUP(0);
      END;

      OnAfterSetGLAccGLBudgetEntryFilters(GLAcc,GLBudgetEntry,AccSchedLine,ColumnLayout,UseBusUnitFilter,UseDimFilter);
    END;

    LOCAL PROCEDURE SetGLAccAnalysisViewBudgetEntries@72(VAR GLAcc@1000 : Record 15;VAR AnalysisViewBudgetEntry@1001 : Record 366;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1003 : Record 334);
    BEGIN
      WITH AnalysisViewBudgetEntry DO BEGIN
        IF GLAcc.Totaling = '' THEN
          SETRANGE("G/L Account No.",GLAcc."No.")
        ELSE
          SETFILTER("G/L Account No.",GLAcc.Totaling);
        SETRANGE("Analysis View Code",AccSchedName."Analysis View Name");
        GLAcc.COPYFILTER("Date Filter","Posting Date");
        AccSchedLine.COPYFILTER("G/L Budget Filter","Budget Name");
        AccSchedLine.COPYFILTER("Business Unit Filter","Business Unit Code");
        CopyDimFilters(AccSchedLine);
        FILTERGROUP(2);
        SetDimFilters(
          GetDimTotalingFilter(1,AccSchedLine."Dimension 1 Totaling"),
          GetDimTotalingFilter(2,AccSchedLine."Dimension 2 Totaling"),
          GetDimTotalingFilter(3,AccSchedLine."Dimension 3 Totaling"),
          GetDimTotalingFilter(4,AccSchedLine."Dimension 4 Totaling"));
        FILTERGROUP(8);
        SetDimFilters(
          GetDimTotalingFilter(1,ColumnLayout."Dimension 1 Totaling"),
          GetDimTotalingFilter(2,ColumnLayout."Dimension 2 Totaling"),
          GetDimTotalingFilter(3,ColumnLayout."Dimension 3 Totaling"),
          GetDimTotalingFilter(4,ColumnLayout."Dimension 4 Totaling"));
        SETFILTER("Business Unit Code",ColumnLayout."Business Unit Totaling");
        FILTERGROUP(0);
      END;

      OnAfterSetGLAccAnalysisViewBudgetEntries(GLAcc,AnalysisViewBudgetEntry,AccSchedLine,ColumnLayout);
    END;

    LOCAL PROCEDURE SetGLAccAnalysisViewEntryFilters@64(VAR GLAcc@1000 : Record 15;VAR AnalysisViewEntry@1001 : Record 365;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1003 : Record 334);
    BEGIN
      WITH AnalysisViewEntry DO BEGIN
        SETRANGE("Analysis View Code",AccSchedName."Analysis View Name");
        SETRANGE("Account Source","Account Source"::"G/L Account");
        IF GLAcc.Totaling = '' THEN
          SETRANGE("Account No.",GLAcc."No.")
        ELSE
          SETFILTER("Account No.",GLAcc.Totaling);
        GLAcc.COPYFILTER("Date Filter","Posting Date");
        AccSchedLine.COPYFILTER("Business Unit Filter","Business Unit Code");
        CopyDimFilters(AccSchedLine);
        FILTERGROUP(2);
        SetDimFilters(
          GetDimTotalingFilter(1,AccSchedLine."Dimension 1 Totaling"),
          GetDimTotalingFilter(2,AccSchedLine."Dimension 2 Totaling"),
          GetDimTotalingFilter(3,AccSchedLine."Dimension 3 Totaling"),
          GetDimTotalingFilter(4,AccSchedLine."Dimension 4 Totaling"));
        FILTERGROUP(8);
        SetDimFilters(
          GetDimTotalingFilter(1,ColumnLayout."Dimension 1 Totaling"),
          GetDimTotalingFilter(2,ColumnLayout."Dimension 2 Totaling"),
          GetDimTotalingFilter(3,ColumnLayout."Dimension 3 Totaling"),
          GetDimTotalingFilter(4,ColumnLayout."Dimension 4 Totaling"));
        SETFILTER("Business Unit Code",ColumnLayout."Business Unit Totaling");
        FILTERGROUP(0);
      END;

      OnAfterSetGLAccAnalysisViewEntryFilters(GLAcc,AnalysisViewEntry,AccSchedLine,ColumnLayout);
    END;

    [External]
    PROCEDURE SetCFAccRowFilter@817(VAR CFAccount@1000 : Record 841;VAR AccSchedLine2@1001 : Record 85);
    BEGIN
      WITH AccSchedLine2 DO BEGIN
        COPYFILTER("Cash Flow Forecast Filter",CFAccount."Cash Flow Forecast Filter");

        CASE "Totaling Type" OF
          "Totaling Type"::"Cash Flow Entry Accounts":
            BEGIN
              CFAccount.SETFILTER("No.",Totaling);
              CFAccount.SETRANGE("Account Type",CFAccount."Account Type"::Entry);
            END;
          "Totaling Type"::"Cash Flow Total Accounts":
            BEGIN
              CFAccount.SETFILTER("No.",Totaling);
              CFAccount.SETFILTER("Account Type",'<>%1',CFAccount."Account Type"::Entry);
            END;
        END;
      END;

      OnAfterSetCFAccRowFilter(CFAccount,AccSchedLine2);
    END;

    [External]
    PROCEDURE SetGLAccColumnFilters@20(VAR GLAcc@1000 : Record 15;AccSchedLine2@1005 : Record 85;VAR ColumnLayout@1001 : Record 334);
    VAR
      FromDate@1002 : Date;
      ToDate@1003 : Date;
      FiscalStartDate2@1004 : Date;
    BEGIN
      WITH ColumnLayout DO BEGIN
        CalcColumnDates(ColumnLayout,FromDate,ToDate,FiscalStartDate2);
        CASE "Column Type" OF
          "Column Type"::"Net Change":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                GLAcc.SETRANGE("Date Filter",FromDate,ToDate);
              AccSchedLine2."Row Type"::"Beginning Balance":
                GLAcc.SETFILTER("Date Filter",'..%1',CLOSINGDATE(FromDate - 1)); // always includes closing date
              AccSchedLine2."Row Type"::"Balance at Date":
                GLAcc.SETRANGE("Date Filter",0D,ToDate);
            END;
          "Column Type"::"Balance at Date":
            IF AccSchedLine2."Row Type" = AccSchedLine2."Row Type"::"Beginning Balance" THEN
              GLAcc.SETRANGE("Date Filter",0D) // Force a zero return
            ELSE
              GLAcc.SETRANGE("Date Filter",0D,ToDate);
          "Column Type"::"Beginning Balance":
            IF AccSchedLine2."Row Type" = AccSchedLine2."Row Type"::"Balance at Date" THEN
              GLAcc.SETRANGE("Date Filter",0D) // Force a zero return
            ELSE
              GLAcc.SETRANGE(
                "Date Filter",0D,CLOSINGDATE(FromDate - 1));
          "Column Type"::"Year to Date":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                GLAcc.SETRANGE("Date Filter",FiscalStartDate2,ToDate);
              AccSchedLine2."Row Type"::"Beginning Balance":
                GLAcc.SETFILTER("Date Filter",'..%1',CLOSINGDATE(FiscalStartDate2 - 1)); // always includes closing date
              AccSchedLine2."Row Type"::"Balance at Date":
                GLAcc.SETRANGE("Date Filter",0D,ToDate);
            END;
          "Column Type"::"Rest of Fiscal Year":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                GLAcc.SETRANGE(
                  "Date Filter",CALCDATE('<+1D>',ToDate),AccountingPeriodMgt.FindEndOfFiscalYear(FiscalStartDate2));
              AccSchedLine2."Row Type"::"Beginning Balance":
                GLAcc.SETRANGE("Date Filter",0D,ToDate);
              AccSchedLine2."Row Type"::"Balance at Date":
                GLAcc.SETRANGE("Date Filter",0D,AccountingPeriodMgt.FindEndOfFiscalYear(ToDate));
            END;
          "Column Type"::"Entire Fiscal Year":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                GLAcc.SETRANGE(
                  "Date Filter",
                  FiscalStartDate2,
                  AccountingPeriodMgt.FindEndOfFiscalYear(FiscalStartDate2));
              AccSchedLine2."Row Type"::"Beginning Balance":
                GLAcc.SETFILTER("Date Filter",'..%1',CLOSINGDATE(FiscalStartDate2 - 1)); // always includes closing date
              AccSchedLine2."Row Type"::"Balance at Date":
                GLAcc.SETRANGE("Date Filter",0D,AccountingPeriodMgt.FindEndOfFiscalYear(ToDate));
            END;
        END;
      END;

      OnAfterSetGLAccColumnFilters(GLAcc,AccSchedLine2,ColumnLayout)
    END;

    [External]
    PROCEDURE SetCFAccColumnFilter@38(VAR CFAccount@1000 : Record 841;AccSchedLine2@1005 : Record 85;VAR ColumnLayout2@1001 : Record 334);
    VAR
      FromDate@1002 : Date;
      ToDate@1003 : Date;
      FiscalStartDate2@1004 : Date;
    BEGIN
      WITH ColumnLayout2 DO BEGIN
        CalcColumnDates(ColumnLayout2,FromDate,ToDate,FiscalStartDate2);
        CASE "Column Type" OF
          "Column Type"::"Net Change":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                CFAccount.SETRANGE("Date Filter",FromDate,ToDate);
              AccSchedLine2."Row Type"::"Beginning Balance":
                CFAccount.SETFILTER("Date Filter",'..%1',CLOSINGDATE(FromDate - 1));
              AccSchedLine2."Row Type"::"Balance at Date":
                CFAccount.SETRANGE("Date Filter",0D,ToDate);
            END;
          "Column Type"::"Balance at Date":
            IF AccSchedLine2."Row Type" = AccSchedLine2."Row Type"::"Beginning Balance" THEN
              CFAccount.SETRANGE("Date Filter",0D) // Force a zero return
            ELSE
              CFAccount.SETRANGE("Date Filter",0D,ToDate);
          "Column Type"::"Beginning Balance":
            IF AccSchedLine2."Row Type" = AccSchedLine2."Row Type"::"Balance at Date" THEN
              CFAccount.SETRANGE("Date Filter",0D) // Force a zero return
            ELSE
              CFAccount.SETRANGE(
                "Date Filter",0D,CALCDATE('<-1D>',FromDate));
          "Column Type"::"Year to Date":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                CFAccount.SETRANGE("Date Filter",FiscalStartDate2,ToDate);
              AccSchedLine2."Row Type"::"Beginning Balance":
                CFAccount.SETFILTER("Date Filter",'..%1',FiscalStartDate2 - 1);
              AccSchedLine2."Row Type"::"Balance at Date":
                CFAccount.SETRANGE("Date Filter",0D,ToDate);
            END;
          "Column Type"::"Rest of Fiscal Year":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                CFAccount.SETRANGE(
                  "Date Filter",
                  CALCDATE('<+1D>',ToDate),AccountingPeriodMgt.FindEndOfFiscalYear(FiscalStartDate2));
              AccSchedLine2."Row Type"::"Beginning Balance":
                CFAccount.SETRANGE("Date Filter",0D,ToDate);
              AccSchedLine2."Row Type"::"Balance at Date":
                CFAccount.SETRANGE("Date Filter",0D,AccountingPeriodMgt.FindEndOfFiscalYear(ToDate));
            END;
          "Column Type"::"Entire Fiscal Year":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                CFAccount.SETRANGE(
                  "Date Filter",
                  FiscalStartDate2,AccountingPeriodMgt.FindEndOfFiscalYear(FiscalStartDate2));
              AccSchedLine2."Row Type"::"Beginning Balance":
                CFAccount.SETFILTER("Date Filter",'..%1',CLOSINGDATE(FiscalStartDate2 - 1));
              AccSchedLine2."Row Type"::"Balance at Date":
                CFAccount.SETRANGE("Date Filter",0D,AccountingPeriodMgt.FindEndOfFiscalYear(ToDate));
            END;
        END;
      END;

      OnAfterSetCFAccColumnFilter(CFAccount,AccSchedLine2,ColumnLayout2);
    END;

    LOCAL PROCEDURE SetCFEntryFilters@62(VAR CFAccount@1001 : Record 841;VAR CFForecastEntry@1000 : Record 847;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1003 : Record 334);
    BEGIN
      WITH CFForecastEntry DO BEGIN
        SETCURRENTKEY(
          "Cash Flow Account No.","Cash Flow Forecast No.","Global Dimension 1 Code",
          "Global Dimension 2 Code","Cash Flow Date");
        IF CFAccount.Totaling = '' THEN
          SETRANGE("Cash Flow Account No.",CFAccount."No.")
        ELSE
          SETFILTER("Cash Flow Account No.",CFAccount.Totaling);
        CFAccount.COPYFILTER("Date Filter","Cash Flow Date");
        AccSchedLine.COPYFILTER("Cash Flow Forecast Filter","Cash Flow Forecast No.");
        AccSchedLine.COPYFILTER("Dimension 1 Filter","Global Dimension 1 Code");
        AccSchedLine.COPYFILTER("Dimension 2 Filter","Global Dimension 2 Code");
        FILTERGROUP(2);
        SETFILTER("Global Dimension 1 Code",AccSchedLine."Dimension 1 Totaling");
        SETFILTER("Global Dimension 2 Code",AccSchedLine."Dimension 2 Totaling");
        FILTERGROUP(8);
        SETFILTER("Global Dimension 1 Code",GetDimTotalingFilter(1,ColumnLayout."Dimension 1 Totaling"));
        SETFILTER("Global Dimension 2 Code",GetDimTotalingFilter(2,ColumnLayout."Dimension 2 Totaling"));
        FILTERGROUP(0);
      END;
    END;

    LOCAL PROCEDURE SetCFAnalysisViewEntryFilters@66(VAR CFAccount@1000 : Record 841;VAR AnalysisViewEntry@1001 : Record 365;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1003 : Record 334);
    BEGIN
      WITH AnalysisViewEntry DO BEGIN
        SETRANGE("Analysis View Code",AccSchedName."Analysis View Name");
        SETRANGE("Account Source","Account Source"::"Cash Flow Account");
        IF CFAccount.Totaling = '' THEN
          SETRANGE("Account No.",CFAccount."No.")
        ELSE
          SETFILTER("Account No.",CFAccount.Totaling);
        CFAccount.COPYFILTER("Date Filter","Posting Date");
        AccSchedLine.COPYFILTER("Cash Flow Forecast Filter","Cash Flow Forecast No.");
        CopyDimFilters(AccSchedLine);
        FILTERGROUP(2);
        SetDimFilters(
          GetDimTotalingFilter(1,AccSchedLine."Dimension 1 Totaling"),
          GetDimTotalingFilter(2,AccSchedLine."Dimension 2 Totaling"),
          GetDimTotalingFilter(3,AccSchedLine."Dimension 3 Totaling"),
          GetDimTotalingFilter(4,AccSchedLine."Dimension 4 Totaling"));
        FILTERGROUP(8);
        SetDimFilters(
          GetDimTotalingFilter(1,ColumnLayout."Dimension 1 Totaling"),
          GetDimTotalingFilter(2,ColumnLayout."Dimension 2 Totaling"),
          GetDimTotalingFilter(3,ColumnLayout."Dimension 3 Totaling"),
          GetDimTotalingFilter(4,ColumnLayout."Dimension 4 Totaling"));
        FILTERGROUP(0);
      END;
    END;

    LOCAL PROCEDURE ApplyOperator@86(LeftResult@1000 : Decimal;RightResult@1001 : Decimal;Operator@1002 : Char;VAR DivisionError@1003 : Boolean) Result : Decimal;
    BEGIN
      CASE Operator OF
        '^':
          Result := POWER(LeftResult,RightResult);
        '%':
          IF RightResult = 0 THEN BEGIN
            Result := 0;
            DivisionError := TRUE;
          END ELSE
            Result := 100 * LeftResult / RightResult;
        '*':
          Result := LeftResult * RightResult;
        '/':
          IF RightResult = 0 THEN BEGIN
            Result := 0;
            DivisionError := TRUE;
          END ELSE
            Result := LeftResult / RightResult;
        '+':
          Result := LeftResult + RightResult;
        '-':
          Result := LeftResult - RightResult;
      END;
    END;

    LOCAL PROCEDURE ParseExpression@88(Expression@1000 : Text;VAR i@1004 : Integer) IsExpression : Boolean;
    VAR
      Parantheses@1003 : Integer;
      Operators@1002 : Text[8];
      OperatorNo@1001 : Integer;
    BEGIN
      Parantheses := 0;
      IsExpression := FALSE;
      Operators := '+-*/^%';
      OperatorNo := 1;
      REPEAT
        i := STRLEN(Expression);
        REPEAT
          IF Expression[i] = '(' THEN
            Parantheses := Parantheses + 1
          ELSE
            IF Expression[i] = ')' THEN
              Parantheses := Parantheses - 1;
          IF (Parantheses = 0) AND (Expression[i] = Operators[OperatorNo]) THEN
            IsExpression := TRUE
          ELSE
            i := i - 1;
        UNTIL IsExpression OR (i <= 0);
        IF NOT IsExpression THEN
          OperatorNo := OperatorNo + 1;
      UNTIL (OperatorNo > STRLEN(Operators)) OR IsExpression;
    END;

    LOCAL PROCEDURE EvaluateExpression@9(IsAccSchedLineExpression@1000 : Boolean;Expression@1001 : Text;AccSchedLine@1002 : Record 85;ColumnLayout@1003 : Record 334;CalcAddCurr@1004 : Boolean) : Decimal;
    VAR
      AccSchedLine2@1018 : Record 85;
      Result@1005 : Decimal;
      Operator@1007 : Char;
      LeftOperand@1008 : Text;
      RightOperand@1009 : Text;
      LeftResult@1010 : Decimal;
      RightResult@1011 : Decimal;
      i@1012 : Integer;
      IsExpression@1013 : Boolean;
      IsFilter@1014 : Boolean;
      MaxLevel@1019 : Integer;
    BEGIN
      Result := 0;
      MaxLevel := 25;

      OnBeforeEvaluateExpression(AccSchedLine,ColumnLayout,MaxLevel);

      CallLevel := CallLevel + 1;
      IF CallLevel > MaxLevel THEN
        ShowError(Text020,AccSchedLine,ColumnLayout);

      Expression := DELCHR(Expression,'<>',' ');
      IF STRLEN(Expression) > 0 THEN BEGIN
        IsExpression := ParseExpression(Expression,i);
        IF IsExpression THEN BEGIN
          IF i > 1 THEN
            LeftOperand := COPYSTR(Expression,1,i - 1)
          ELSE
            LeftOperand := '';
          IF i < STRLEN(Expression) THEN
            RightOperand := COPYSTR(Expression,i + 1)
          ELSE
            RightOperand := '';
          Operator := Expression[i];
          LeftResult :=
            EvaluateExpression(
              IsAccSchedLineExpression,LeftOperand,AccSchedLine,ColumnLayout,CalcAddCurr);
          IF (RightOperand = '') AND (Operator = '%') AND NOT IsAccSchedLineExpression AND
             (AccSchedLine."Totaling Type" <> AccSchedLine."Totaling Type"::"Set Base For Percent")
          THEN BEGIN
            AccSchedLine2.COPY(AccSchedLine);
            AccSchedLine2."Line No." := GetBasePercentLine(AccSchedLine,ColumnLayout);
            AccSchedLine2.FIND;
            RightResult :=
              EvaluateExpression(
                IsAccSchedLineExpression,LeftOperand,AccSchedLine2,ColumnLayout,CalcAddCurr);
          END ELSE
            RightResult :=
              EvaluateExpression(
                IsAccSchedLineExpression,RightOperand,AccSchedLine,ColumnLayout,CalcAddCurr);
          Result := ApplyOperator(LeftResult,RightResult,Operator,DivisionError);
        END ELSE
          IF (Expression[1] = '(') AND (Expression[STRLEN(Expression)] = ')') THEN
            Result :=
              EvaluateExpression(
                IsAccSchedLineExpression,COPYSTR(Expression,2,STRLEN(Expression) - 2),
                AccSchedLine,ColumnLayout,CalcAddCurr)
          ELSE BEGIN
            IsFilter := IsExpressionFilter(Expression);
            IF (STRLEN(Expression) > 10) AND (NOT IsFilter) THEN
              EVALUATE(Result,Expression)
            ELSE
              IF IsAccSchedLineExpression THEN
                Result := CalcCellValueInAccSchedLines(AccSchedLine,ColumnLayout,Expression,CalcAddCurr,IsFilter)
              ELSE
                Result := CalcCellValueInColumnLayouts(ColumnLayout,AccSchedLine,Expression,CalcAddCurr,IsFilter);
          END;
      END;
      CallLevel := CallLevel - 1;
      EXIT(Result);
    END;

    [External]
    PROCEDURE FormatCellAsText@24(VAR ColumnLayout2@1000 : Record 334;Value@1001 : Decimal;CalcAddCurr@1002 : Boolean) : Text[30];
    VAR
      ValueAsText@1003 : Text[30];
    BEGIN
      ValueAsText := MatrixMgt.FormatValue(Value,ColumnLayout2."Rounding Factor",CalcAddCurr);

      IF (ValueAsText <> '') AND
         (ColumnLayout2."Column Type" = ColumnLayout2."Column Type"::Formula) AND
         (STRPOS(ColumnLayout2.Formula,'%') > 1)
      THEN
        ValueAsText := ValueAsText + '%';

      EXIT(ValueAsText);
    END;

    [External]
    PROCEDURE GetDivisionError@18() : Boolean;
    BEGIN
      EXIT(DivisionError);
    END;

    [External]
    PROCEDURE GetPeriodError@31() : Boolean;
    BEGIN
      EXIT(PeriodError);
    END;

    LOCAL PROCEDURE ShowError@19(MessageLine@1000 : Text[100];VAR AccSchedLine@1001 : Record 85;VAR ColumnLayout@1002 : Record 334);
    BEGIN
      AccSchedLine.SETRANGE("Schedule Name",AccSchedLine."Schedule Name");
      AccSchedLine.SETRANGE("Line No.",CallingAccSchedLineID);
      IF AccSchedLine.FINDFIRST THEN;
      ColumnLayout.SETRANGE("Column Layout Name",ColumnLayout."Column Layout Name");
      ColumnLayout.SETRANGE("Line No.",CallingColumnLayoutID);
      IF ColumnLayout.FINDFIRST THEN;
      ERROR(Text016,
        MessageLine,
        Text017,
        STRSUBSTNO(Text018,AccSchedLine."Row No.",AccSchedLine."Line No.",AccSchedLine.Totaling),
        STRSUBSTNO(Text019,ColumnLayout."Column No.",ColumnLayout."Line No.",ColumnLayout.Formula));
    END;

    [External]
    PROCEDURE InsertGLAccounts@12(VAR AccSchedLine@1000 : Record 85);
    VAR
      GLAcc@1001 : Record 15;
      GLAccList@1002 : Page 18;
      AccCounter@1003 : Integer;
      AccSchedLineNo@1004 : Integer;
    BEGIN
      GLAccList.LOOKUPMODE(TRUE);
      IF GLAccList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        GLAccList.SetSelection(GLAcc);
        AccCounter := GLAcc.COUNT;
        IF AccCounter > 0 THEN BEGIN
          AccSchedLineNo := AccSchedLine."Line No.";
          MoveAccSchedLines(AccSchedLine,AccCounter);
          IF GLAcc.FINDSET THEN
            REPEAT
              AccSchedLine.INIT;
              AccSchedLineNo := AccSchedLineNo + 10000;
              AccSchedLine."Line No." := AccSchedLineNo;
              AccSchedLine.Description := GLAcc.Name;
              AccSchedLine.VALIDATE(Indentation,GLAcc.Indentation);
              AccSchedLine.Bold := GLAcc."Account Type" <> GLAcc."Account Type"::Posting;
              IF GLAcc."Account Type" IN
                 [GLAcc."Account Type"::Posting,GLAcc."Account Type"::Total,GLAcc."Account Type"::"End-Total"]
              THEN BEGIN
                AccSchedLine.Totaling := GLAcc."No.";
                AccSchedLine."Row No." := COPYSTR(GLAcc."No.",1,MAXSTRLEN(AccSchedLine."Row No."));
              END;
              IF GLAcc."Account Type" IN
                 [GLAcc."Account Type"::Total,GLAcc."Account Type"::"End-Total"]
              THEN
                AccSchedLine."Totaling Type" := AccSchedLine."Totaling Type"::"Total Accounts"
              ELSE
                AccSchedLine."Totaling Type" := AccSchedLine."Totaling Type"::"Posting Accounts";
              AccSchedLine.INSERT;
            UNTIL GLAcc.NEXT = 0;
        END;
      END;
    END;

    [External]
    PROCEDURE InsertCFAccounts@47(VAR AccSchedLine@1000 : Record 85);
    VAR
      CashFlowAcc@1001 : Record 841;
      CashFlowAccList@1002 : Page 855;
      AccCounter@1003 : Integer;
      AccSchedLineNo@1004 : Integer;
    BEGIN
      CashFlowAccList.LOOKUPMODE(TRUE);
      IF CashFlowAccList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        CashFlowAccList.SetSelection(CashFlowAcc);
        AccCounter := CashFlowAcc.COUNT;
        IF AccCounter > 0 THEN BEGIN
          AccSchedLineNo := AccSchedLine."Line No.";
          MoveAccSchedLines(AccSchedLine,AccCounter);
          IF CashFlowAcc.FINDSET THEN
            REPEAT
              AccSchedLine.INIT;
              AccSchedLineNo := AccSchedLineNo + 10000;
              AccSchedLine."Line No." := AccSchedLineNo;
              AccSchedLine.Description := CashFlowAcc.Name;
              IF CashFlowAcc."Account Type" IN
                 [CashFlowAcc."Account Type"::Entry,CashFlowAcc."Account Type"::Total,CashFlowAcc."Account Type"::"End-Total"]
              THEN BEGIN
                AccSchedLine.Totaling := CashFlowAcc."No.";
                AccSchedLine."Row No." := COPYSTR(CashFlowAcc."No.",1,MAXSTRLEN(AccSchedLine."Row No."));
              END;
              IF CashFlowAcc."Account Type" IN
                 [CashFlowAcc."Account Type"::Total,CashFlowAcc."Account Type"::"End-Total"]
              THEN
                AccSchedLine."Totaling Type" := AccSchedLine."Totaling Type"::"Cash Flow Total Accounts"
              ELSE
                AccSchedLine."Totaling Type" := AccSchedLine."Totaling Type"::"Cash Flow Entry Accounts";
              AccSchedLine.INSERT;
            UNTIL CashFlowAcc.NEXT = 0;
        END;
      END;
    END;

    [External]
    PROCEDURE InsertCostTypes@43(VAR AccSchedLine@1000 : Record 85);
    VAR
      CostType@1001 : Record 1103;
      CostTypeList@1002 : Page 1124;
      AccCounter@1003 : Integer;
      AccSchedLineNo@1004 : Integer;
    BEGIN
      CostTypeList.LOOKUPMODE(TRUE);
      IF CostTypeList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        CostTypeList.SetSelection(CostType);
        AccCounter := CostType.COUNT;
        IF AccCounter > 0 THEN BEGIN
          AccSchedLineNo := AccSchedLine."Line No.";
          MoveAccSchedLines(AccSchedLine,AccCounter);
          IF CostType.FINDSET THEN
            REPEAT
              AccSchedLine.INIT;
              AccSchedLineNo := AccSchedLineNo + 10000;
              AccSchedLine."Line No." := AccSchedLineNo;
              AccSchedLine.Description := CostType.Name;
              IF CostType.Type IN
                 [CostType.Type::"Cost Type",CostType.Type::Total,CostType.Type::"End-Total"]
              THEN BEGIN
                AccSchedLine.Totaling := CostType."No.";
                AccSchedLine."Row No." := COPYSTR(CostType."No.",1,MAXSTRLEN(AccSchedLine."Row No."));
              END;
              IF CostType.Type IN
                 [CostType.Type::Total,CostType.Type::"End-Total"]
              THEN
                AccSchedLine."Totaling Type" := AccSchedLine."Totaling Type"::"Cost Type Total"
              ELSE
                AccSchedLine."Totaling Type" := AccSchedLine."Totaling Type"::"Cost Type";
              AccSchedLine.INSERT;
            UNTIL CostType.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE IsExpressionFilter@84(Expression@1000 : Text) : Boolean;
    BEGIN
      EXIT(
        STRPOS(Expression,'..') +
        STRPOS(Expression,'|') +
        STRPOS(Expression,'<') +
        STRPOS(Expression,'>') +
        STRPOS(Expression,'&') +
        STRPOS(Expression,'=') > 0);
    END;

    LOCAL PROCEDURE ExchangeAmtAddCurrToLCY@23(AmountLCY@1000 : Decimal) : Decimal;
    BEGIN
      GetGLSetup;
      EXIT(
        CurrExchRate.ExchangeAmtLCYToFCY(
          0, '', //**4PS.n
          WORKDATE,GLSetup."Additional Reporting Currency",AmountLCY,
      //  CurrExchRate.ExchangeRate(WORKDATE,GLSetup."Additional Reporting Currency"))); //**4PS.o
          CurrExchRate.ExchangeRate(0, '',WORKDATE,GLSetup."Additional Reporting Currency",TRUE),TRUE)); //**4PS.n
    END;

    [External]
    PROCEDURE SetAccSchedName@22(VAR NewAccSchedName@1000 : Record 84);
    BEGIN
      AccSchedName := NewAccSchedName;
    END;

    [External]
    PROCEDURE GetDimTotalingFilter@32(DimNo@1001 : Integer;DimTotaling@1000 : Text[250]) : Text[1024];
    VAR
      DimTotaling2@1002 : Text[250];
      DimTotalPart@1005 : Text[250];
      ResultFilter@1003 : Text[1024];
      ResultFilter2@1006 : Text[1024];
      i@1004 : Integer;
    BEGIN
      IF DimTotaling = '' THEN
        EXIT(DimTotaling);
      DimTotaling2 := DimTotaling;
      REPEAT
        i := STRPOS(DimTotaling2,'|');
        IF i > 0 THEN BEGIN
          DimTotalPart := COPYSTR(DimTotaling2,1,i - 1);
          IF i < STRLEN(DimTotaling2) THEN
            DimTotaling2 := COPYSTR(DimTotaling2,i + 1)
          ELSE
            DimTotaling2 := '';
        END ELSE
          DimTotalPart := DimTotaling2;
        ResultFilter2 := ConvDimTotalingFilter(DimNo,DimTotalPart);
        IF ResultFilter2 <> '' THEN
          IF STRLEN(ResultFilter) + STRLEN(ResultFilter2) + 1 > MAXSTRLEN(ResultFilter) THEN
            ERROR(Text021,DimTotaling);

        IF ResultFilter <> '' THEN
          ResultFilter := ResultFilter + '|';
        ResultFilter := COPYSTR(ResultFilter + ResultFilter2,1,MAXSTRLEN(ResultFilter));
      UNTIL i <= 0;
      EXIT(ResultFilter);
    END;

    LOCAL PROCEDURE ConvDimTotalingFilter@28(DimNo@1000 : Integer;DimTotaling@1001 : Text[250]) : Text[1024];
    VAR
      DimVal@1003 : Record 349;
      CostAccSetup@1006 : Record 1108;
      DimCode@1002 : Code[20];
      ResultFilter@1004 : Text[1024];
      DimValTotaling@1005 : Boolean;
    BEGIN
      IF CostAccSetup.GET THEN;
      IF DimTotaling = '' THEN
        EXIT(DimTotaling);

      CheckAnalysisView(AccSchedName.Name,'',FALSE);

      CASE DimNo OF
        1:
          DimCode := AnalysisView."Dimension 1 Code";
        2:
          DimCode := AnalysisView."Dimension 2 Code";
        3:
          DimCode := AnalysisView."Dimension 3 Code";
        4:
          DimCode := AnalysisView."Dimension 4 Code";
        5:
          DimCode := CostAccSetup."Cost Center Dimension";
        6:
          DimCode := CostAccSetup."Cost Object Dimension";
      END;
      IF DimCode = '' THEN
        EXIT(DimTotaling);

      DimVal.SETRANGE("Dimension Code",DimCode);
      DimVal.SETFILTER(Code,DimTotaling);
      IF DimVal.FIND('-') THEN
        REPEAT
          DimValTotaling :=
            DimVal."Dimension Value Type" IN
            [DimVal."Dimension Value Type"::Total,DimVal."Dimension Value Type"::"End-Total"];
          IF DimValTotaling AND (DimVal.Totaling <> '') THEN BEGIN
            IF STRLEN(ResultFilter) + STRLEN(DimVal.Totaling) + 1 > MAXSTRLEN(ResultFilter) THEN
              ERROR(Text021,DimTotaling);
            IF ResultFilter <> '' THEN
              ResultFilter := ResultFilter + '|';
            ResultFilter := ResultFilter + DimVal.Totaling;
          END;
        UNTIL (DimVal.NEXT = 0) OR NOT DimValTotaling;

      IF DimValTotaling THEN
        EXIT(ResultFilter);

      EXIT(DimTotaling);
    END;

    LOCAL PROCEDURE CalcCostType@25(VAR CostType@1000 : Record 1103;VAR AccSchedLine@1001 : Record 85;VAR ColumnLayout@1002 : Record 334;CalcAddCurr@1003 : Boolean) ColValue@1004 : Decimal;
    VAR
      CostEntry@1005 : Record 1104;
      CostBudgEntry@1006 : Record 1109;
      AmountType@1008 : 'Net Amount,Debit Amount,Credit Amount';
      UseDimFilter@1007 : Boolean;
      TestBalance@1009 : Boolean;
      Balance@1010 : Decimal;
      IsHandled@1011 : Boolean;
    BEGIN
      ColValue := 0;
      IsHandled := FALSE;
      OnBeforeCalcCostType(CostType,AccSchedLine,ColumnLayout,CalcAddCurr,ColValue,IsHandled);
      IF IsHandled THEN
        EXIT(ColValue);

      IF AccSchedName.Name <> AccSchedLine."Schedule Name" THEN
        AccSchedName.GET(AccSchedLine."Schedule Name");

      IF ConflictAmountType(AccSchedLine,ColumnLayout."Amount Type",AmountType) THEN
        EXIT(0);

      TestBalance :=
        AccSchedLine.Show IN [AccSchedLine.Show::"When Positive Balance",AccSchedLine.Show::"When Negative Balance"];

      IF ColumnLayout."Column Type" <> ColumnLayout."Column Type"::Formula THEN BEGIN
        UseDimFilter := HasDimFilter(AccSchedLine,ColumnLayout) OR HasCostDimFilter(AccSchedLine);
        CASE ColumnLayout."Ledger Entry Type" OF
          ColumnLayout."Ledger Entry Type"::Entries:
            BEGIN
              WITH CostEntry DO BEGIN
                IF UseDimFilter THEN
                  SETCURRENTKEY("Cost Type No.","Cost Center Code","Cost Object Code")
                ELSE
                  SETCURRENTKEY("Cost Type No.","Posting Date");
                IF CostType.Totaling = '' THEN
                  SETRANGE("Cost Type No.",CostType."No.")
                ELSE
                  SETFILTER("Cost Type No.",CostType.Totaling);
                CostType.COPYFILTER("Date Filter","Posting Date");
                AccSchedLine.COPYFILTER("Cost Center Filter","Cost Center Code");
                AccSchedLine.COPYFILTER("Cost Object Filter","Cost Object Code");
                FILTERGROUP(2);
                SETFILTER("Cost Center Code",GetDimTotalingFilter(5,AccSchedLine."Cost Center Totaling"));
                SETFILTER("Cost Object Code",GetDimTotalingFilter(6,AccSchedLine."Cost Object Totaling"));
                FILTERGROUP(8);
                SETFILTER("Cost Center Code",GetDimTotalingFilter(5,ColumnLayout."Cost Center Totaling"));
                SETFILTER("Cost Object Code",GetDimTotalingFilter(6,ColumnLayout."Cost Object Totaling"));
                FILTERGROUP(0);
              END;
              CASE AmountType OF
                AmountType::"Net Amount":
                  BEGIN
                    IF CalcAddCurr THEN BEGIN
                      CostEntry.CALCSUMS("Additional-Currency Amount");
                      ColValue := CostEntry."Additional-Currency Amount";
                    END ELSE BEGIN
                      CostEntry.CALCSUMS(Amount);
                      ColValue := CostEntry.Amount;
                    END;
                    Balance := ColValue;
                  END;
                AmountType::"Debit Amount":
                  IF CalcAddCurr THEN BEGIN
                    CostEntry.CALCSUMS("Add.-Currency Debit Amount","Additional-Currency Amount");
                    IF TestBalance THEN
                      Balance := CostEntry."Additional-Currency Amount";
                    ColValue := CostEntry."Add.-Currency Debit Amount";
                  END ELSE BEGIN
                    IF TestBalance THEN BEGIN
                      CostEntry.CALCSUMS("Debit Amount",Amount);
                      Balance := CostEntry.Amount;
                    END ELSE
                      CostEntry.CALCSUMS("Debit Amount");
                    ColValue := CostEntry."Debit Amount";
                  END;
                AmountType::"Credit Amount":
                  IF CalcAddCurr THEN BEGIN
                    CostEntry.CALCSUMS("Add.-Currency Credit Amount","Additional-Currency Amount");
                    IF TestBalance THEN
                      Balance := CostEntry."Additional-Currency Amount";
                    ColValue := CostEntry."Add.-Currency Credit Amount";
                  END ELSE BEGIN
                    IF TestBalance THEN BEGIN
                      CostEntry.CALCSUMS("Credit Amount",Amount);
                      Balance := CostEntry.Amount;
                    END ELSE
                      CostEntry.CALCSUMS("Credit Amount");
                    ColValue := CostEntry."Credit Amount";
                  END;
              END;
            END;
          ColumnLayout."Ledger Entry Type"::"Budget Entries":
            BEGIN
              WITH CostBudgEntry DO BEGIN
                SETCURRENTKEY("Budget Name","Cost Type No.","Cost Center Code","Cost Object Code",Date);
                IF CostType.Totaling = '' THEN
                  SETRANGE("Cost Type No.",CostType."No.")
                ELSE
                  SETFILTER("Cost Type No.",CostType.Totaling);
                CostType.COPYFILTER("Date Filter",Date);
                AccSchedLine.COPYFILTER("Cost Budget Filter","Budget Name");
                AccSchedLine.COPYFILTER("Cost Center Filter","Cost Center Code");
                AccSchedLine.COPYFILTER("Cost Object Filter","Cost Object Code");
                FILTERGROUP(2);
                SETFILTER("Cost Center Code",GetDimTotalingFilter(5,AccSchedLine."Cost Center Totaling"));
                SETFILTER("Cost Object Code",GetDimTotalingFilter(6,AccSchedLine."Cost Object Totaling"));
                FILTERGROUP(8);
                SETFILTER("Cost Center Code",GetDimTotalingFilter(5,ColumnLayout."Cost Center Totaling"));
                SETFILTER("Cost Object Code",GetDimTotalingFilter(6,ColumnLayout."Cost Object Totaling"));
                FILTERGROUP(0);
              END;

              CostBudgEntry.CALCSUMS(Amount);

              CASE AmountType OF
                AmountType::"Net Amount":
                  ColValue := CostBudgEntry.Amount;
                AmountType::"Debit Amount":
                  IF CostBudgEntry.Amount > 0 THEN
                    ColValue := CostBudgEntry.Amount;
                AmountType::"Credit Amount":
                  IF CostBudgEntry.Amount < 0 THEN
                    ColValue := CostBudgEntry.Amount;
              END;
              Balance := CostBudgEntry.Amount;
              IF CalcAddCurr THEN
                ColValue := CalcLCYToACY(ColValue);
            END;
        END;

        IF TestBalance THEN BEGIN
          IF AccSchedLine.Show = AccSchedLine.Show::"When Positive Balance" THEN
            IF Balance < 0 THEN
              EXIT(0);
          IF AccSchedLine.Show = AccSchedLine.Show::"When Negative Balance" THEN
            IF Balance > 0 THEN
              EXIT(0);
        END;
      END;
      EXIT(ColValue);
    END;

    [External]
    PROCEDURE SetCostTypeRowFilters@37(VAR CostType@1000 : Record 1103;VAR AccSchedLine2@1001 : Record 85;VAR ColumnLayout@1002 : Record 334);
    BEGIN
      WITH AccSchedLine2 DO BEGIN
        CASE "Totaling Type" OF
          "Totaling Type"::"Cost Type":
            BEGIN
              CostType.SETFILTER("No.",Totaling);
              CostType.SETRANGE(Type,CostType.Type::"Cost Type");
            END;
          "Totaling Type"::"Cost Type Total":
            BEGIN
              CostType.SETFILTER("No.",Totaling);
              CostType.SETFILTER(Type,'<>%1',CostType.Type::"Cost Type");
            END;
        END;

        CostType.SETFILTER("Cost Center Filter",GETFILTER("Cost Center Filter"));
        CostType.SETFILTER("Cost Object Filter",GETFILTER("Cost Object Filter"));
        IF ColumnLayout."Ledger Entry Type" = ColumnLayout."Ledger Entry Type"::"Budget Entries" THEN
          CostType.SETFILTER("Budget Filter",GETFILTER("Cost Budget Filter"));
      END;
    END;

    [External]
    PROCEDURE SetCostTypeColumnFilters@838(VAR CostType@1000 : Record 1103;AccSchedLine2@1001 : Record 85;VAR ColumnLayout@1002 : Record 334);
    VAR
      FromDate@1003 : Date;
      ToDate@1004 : Date;
      FiscalStartDate2@1005 : Date;
    BEGIN
      WITH ColumnLayout DO BEGIN
        CalcColumnDates(ColumnLayout,FromDate,ToDate,FiscalStartDate2);
        CASE "Column Type" OF
          "Column Type"::"Net Change":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                CostType.SETRANGE("Date Filter",FromDate,ToDate);
              AccSchedLine2."Row Type"::"Beginning Balance":
                CostType.SETFILTER("Date Filter",'<%1',FromDate);
              AccSchedLine2."Row Type"::"Balance at Date":
                CostType.SETRANGE("Date Filter",0D,ToDate);
            END;
          "Column Type"::"Balance at Date":
            IF AccSchedLine2."Row Type" = AccSchedLine2."Row Type"::"Beginning Balance" THEN
              CostType.SETRANGE("Date Filter",0D) // Force a zero return
            ELSE
              CostType.SETRANGE("Date Filter",0D,ToDate);
          "Column Type"::"Beginning Balance":
            IF AccSchedLine2."Row Type" = AccSchedLine2."Row Type"::"Balance at Date" THEN
              CostType.SETRANGE("Date Filter",0D) // Force a zero return
            ELSE
              CostType.SETRANGE(
                "Date Filter",0D,CALCDATE('<-1D>',FromDate));
          "Column Type"::"Year to Date":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                CostType.SETRANGE("Date Filter",FiscalStartDate2,ToDate);
              AccSchedLine2."Row Type"::"Beginning Balance":
                CostType.SETFILTER("Date Filter",'<%1',FiscalStartDate2);
              AccSchedLine2."Row Type"::"Balance at Date":
                CostType.SETRANGE("Date Filter",0D,ToDate);
            END;
          "Column Type"::"Rest of Fiscal Year":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                CostType.SETRANGE(
                  "Date Filter",CALCDATE('<+1D>',ToDate),AccountingPeriodMgt.FindEndOfFiscalYear(FiscalStartDate2));
              AccSchedLine2."Row Type"::"Beginning Balance":
                CostType.SETRANGE("Date Filter",0D,ToDate);
              AccSchedLine2."Row Type"::"Balance at Date":
                CostType.SETRANGE("Date Filter",0D,AccountingPeriodMgt.FindEndOfFiscalYear(ToDate));
            END;
          "Column Type"::"Entire Fiscal Year":
            CASE AccSchedLine2."Row Type" OF
              AccSchedLine2."Row Type"::"Net Change":
                CostType.SETRANGE(
                  "Date Filter",FiscalStartDate2,AccountingPeriodMgt.FindEndOfFiscalYear(FiscalStartDate2));
              AccSchedLine2."Row Type"::"Beginning Balance":
                CostType.SETFILTER("Date Filter",'<%1',FiscalStartDate2);
              AccSchedLine2."Row Type"::"Balance at Date":
                CostType.SETRANGE("Date Filter",0D,AccountingPeriodMgt.FindEndOfFiscalYear(ToDate));
            END;
        END;
      END;
    END;

    [External]
    PROCEDURE HasDimFilter@42(VAR AccSchedLine@1001 : Record 85;VAR ColumnLayout@1000 : Record 334) : Boolean;
    BEGIN
      EXIT((AccSchedLine."Dimension 1 Totaling" <> '') OR
        (AccSchedLine."Dimension 2 Totaling" <> '') OR
        (AccSchedLine."Dimension 3 Totaling" <> '') OR
        (AccSchedLine."Dimension 4 Totaling" <> '') OR
        (AccSchedLine.GETFILTER("Dimension 1 Filter") <> '') OR
        (AccSchedLine.GETFILTER("Dimension 2 Filter") <> '') OR
        (AccSchedLine.GETFILTER("Dimension 3 Filter") <> '') OR
        (AccSchedLine.GETFILTER("Dimension 4 Filter") <> '') OR
        (ColumnLayout."Dimension 1 Totaling" <> '') OR
        (ColumnLayout."Dimension 2 Totaling" <> '') OR
        (ColumnLayout."Dimension 3 Totaling" <> '') OR
        (ColumnLayout."Dimension 4 Totaling" <> '') OR
        (ColumnLayout."Cost Center Totaling" <> '') OR
        (ColumnLayout."Cost Object Totaling" <> ''));
    END;

    LOCAL PROCEDURE HasCostDimFilter@40(VAR AccSchedLine@1001 : Record 85) : Boolean;
    BEGIN
      EXIT((AccSchedLine."Cost Center Totaling" <> '') OR
        (AccSchedLine."Cost Object Totaling" <> '') OR
        (AccSchedLine.GETFILTER("Cost Center Filter") <> '') OR
        (AccSchedLine.GETFILTER("Cost Object Filter") <> ''));
    END;

    [External]
    PROCEDURE CalcColumnDates@41(ColumnLayout@1003 : Record 334;VAR FromDate@1001 : Date;VAR ToDate@1002 : Date;VAR FiscalStartDate2@1005 : Date);
    VAR
      ComparisonDateFormula@1004 : DateFormula;
      ComparisonPeriodFormula@1000 : Code[20];
    BEGIN
      ComparisonDateFormula := ColumnLayout."Comparison Date Formula";
      ComparisonPeriodFormula := ColumnLayout."Comparison Period Formula";

      IF (FORMAT(ComparisonDateFormula) <> '0') AND (FORMAT(ComparisonDateFormula) <> '') THEN BEGIN
        FromDate := CALCDATE(ComparisonDateFormula,StartDate);
        ToDate := CALCDATE(ComparisonDateFormula,EndDate);
        IF (StartDate = CALCDATE('<-CM>',StartDate)) AND
           (FromDate = CALCDATE('<-CM>',FromDate)) AND
           (EndDate = CALCDATE('<CM>',EndDate))
        THEN
          ToDate := CALCDATE('<CM>',ToDate);
        FiscalStartDate2 := AccountingPeriodMgt.FindFiscalYear(ToDate);
      END ELSE
        IF ComparisonPeriodFormula <> '' THEN BEGIN
          AccPeriodStartEnd(ColumnLayout,StartDate,FromDate,ToDate);
          FiscalStartDate2 := AccountingPeriodMgt.FindFiscalYear(ToDate);
        END ELSE BEGIN
          FromDate := StartDate;
          ToDate := EndDate;
          FiscalStartDate2 := FiscalStartDate;
        END;
    END;

    LOCAL PROCEDURE MoveAccSchedLines@44(VAR AccSchedLine@1000 : Record 85;Place@1001 : Integer);
    VAR
      AccSchedLineNo@1002 : Integer;
      I@1003 : Integer;
    BEGIN
      AccSchedLineNo := AccSchedLine."Line No.";
      AccSchedLine.SETRANGE("Schedule Name",AccSchedLine."Schedule Name");
      IF AccSchedLine.FIND('+') THEN
        REPEAT
          I := AccSchedLine."Line No.";
          IF I > AccSchedLineNo THEN BEGIN
            AccSchedLine.DELETE;
            AccSchedLine."Line No." := I + 10000 * Place;
            AccSchedLine.INSERT;
            OnAfterAccSchedLineInsert(AccSchedLine);
          END;
        UNTIL (I <= AccSchedLineNo) OR (AccSchedLine.NEXT(-1) = 0);
    END;

    [External]
    PROCEDURE SetStartDateEndDate@50(NewStartDate@1000 : Date;NewEndDate@1001 : Date);
    BEGIN
      StartDate := NewStartDate;
      EndDate := NewEndDate;
    END;

    LOCAL PROCEDURE ConflictAmountType@45(AccSchedLine@1000 : Record 85;ColumnLayoutAmtType@1001 : 'Net Amount,Debit Amount,Credit Amount';VAR AmountType@1002 : Option) : Boolean;
    BEGIN
      IF (ColumnLayoutAmtType = AccSchedLine."Amount Type") OR
         (AccSchedLine."Amount Type" = AccSchedLine."Amount Type"::"Net Amount")
      THEN
        AmountType := ColumnLayoutAmtType
      ELSE
        IF ColumnLayoutAmtType = ColumnLayoutAmtType::"Net Amount" THEN
          AmountType := AccSchedLine."Amount Type"
        ELSE
          EXIT(TRUE);
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE DrillDown@52(TempColumnLayout@1001 : TEMPORARY Record 334;VAR AccScheduleLine@1000 : Record 85;PeriodLength@1002 : Option);
    VAR
      AccScheduleOverview@1003 : Page 490;
      ErrorType@1004 : 'None,Division by Zero,Period Error,Both';
    BEGIN
      WITH AccScheduleLine DO BEGIN
        IF TempColumnLayout."Column Type" = TempColumnLayout."Column Type"::Formula THEN BEGIN
          CalcFieldError(ErrorType,"Line No.",TempColumnLayout."Line No.");
          IF ErrorType <> ErrorType::None THEN
            MESSAGE(STRSUBSTNO(ColumnFormulaErrorMsg,TempColumnLayout.Formula,FORMAT(ErrorType)))
          ELSE
            MESSAGE(ColumnFormulaMsg,TempColumnLayout.Formula);
          EXIT;
        END;

        IF "Totaling Type" IN ["Totaling Type"::Formula,"Totaling Type"::"Set Base For Percent"] THEN BEGIN
          AccScheduleOverview.SetAccSchedName("Schedule Name");
          AccScheduleOverview.SETTABLEVIEW(AccScheduleLine);
          AccScheduleOverview.SETRECORD(AccScheduleLine);
          AccScheduleOverview.SetPeriodType(PeriodLength);
          AccScheduleOverview.RUN;
          EXIT;
        END;

        OnBeforeDrillDownOnAccounts(AccScheduleLine,TempColumnLayout,PeriodLength,StartDate,EndDate);

        IF Totaling = '' THEN
          EXIT;

        IF "Totaling Type" IN ["Totaling Type"::"Cash Flow Entry Accounts","Totaling Type"::"Cash Flow Total Accounts"] THEN
          DrillDownOnCFAccount(TempColumnLayout,AccScheduleLine)
        ELSE
          DrillDownOnGLAccount(TempColumnLayout,AccScheduleLine);
      END;
    END;

    [External]
    PROCEDURE DrillDownFromOverviewPage@54(TempColumnLayout@1001 : TEMPORARY Record 334;VAR AccScheduleLine@1000 : Record 85;PeriodLength@1002 : Option);
    VAR
      IsHandled@1003 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeDrillDownFromOverviewPage(TempColumnLayout,AccScheduleLine,PeriodLength,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH AccScheduleLine DO BEGIN
        IF ("Totaling Type" = "Totaling Type"::Formula) AND
           (TempColumnLayout."Column Type" = TempColumnLayout."Column Type"::Formula)
        THEN
          MESSAGE(RowFormulaMsg,TempColumnLayout.Formula)
        ELSE
          IF "Totaling Type" IN ["Totaling Type"::Formula,"Totaling Type"::"Set Base For Percent"] THEN
            MESSAGE(RowFormulaMsg,Totaling)
          ELSE
            DrillDown(TempColumnLayout,AccScheduleLine,PeriodLength);
      END;
    END;

    LOCAL PROCEDURE DrillDownOnGLAccount@51(TempColumnLayout@1007 : TEMPORARY Record 334;VAR AccScheduleLine@1003 : Record 85);
    VAR
      GLAcc@1006 : Record 15;
      GLAccAnalysisView@1005 : Record 376;
      CostType@1004 : Record 1103;
      ChartOfAccsAnalysisView@1002 : Page 569;
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeDrillDownOnGLAccount(TempColumnLayout,AccScheduleLine,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH AccScheduleLine DO
        IF "Totaling Type" IN ["Totaling Type"::"Cost Type","Totaling Type"::"Cost Type Total"] THEN BEGIN
          SetCostTypeRowFilters(CostType,AccScheduleLine,TempColumnLayout);
          SetCostTypeColumnFilters(CostType,AccScheduleLine,TempColumnLayout);
          COPYFILTER("Cost Center Filter",CostType."Cost Center Filter");
          COPYFILTER("Cost Object Filter",CostType."Cost Object Filter");
          COPYFILTER("Cost Budget Filter",CostType."Budget Filter");
          CostType.FILTERGROUP(2);
          CostType.SETFILTER("Cost Center Filter",GetDimTotalingFilter(1,"Cost Center Totaling"));
          CostType.SETFILTER("Cost Object Filter",GetDimTotalingFilter(1,"Cost Object Totaling"));
          CostType.FILTERGROUP(8);
          CostType.SETFILTER("Cost Center Filter",GetDimTotalingFilter(1,TempColumnLayout."Cost Center Totaling"));
          CostType.SETFILTER("Cost Object Filter",GetDimTotalingFilter(1,TempColumnLayout."Cost Object Totaling"));
          CostType.FILTERGROUP(0);
          PAGE.RUN(PAGE::"Chart of Cost Types",CostType);
        END ELSE BEGIN
          COPYFILTER("Business Unit Filter",GLAcc."Business Unit Filter");
          COPYFILTER("G/L Budget Filter",GLAcc."Budget Filter");
          SetGLAccRowFilters(GLAcc,AccScheduleLine);
          SetGLAccColumnFilters(GLAcc,AccScheduleLine,TempColumnLayout);
          AccSchedName.GET("Schedule Name");
          IF AccSchedName."Analysis View Name" = '' THEN BEGIN
            COPYFILTER("Dimension 1 Filter",GLAcc."Global Dimension 1 Filter");
            COPYFILTER("Dimension 2 Filter",GLAcc."Global Dimension 2 Filter");
            COPYFILTER("Business Unit Filter",GLAcc."Business Unit Filter");
            GLAcc.FILTERGROUP(2);
            GLAcc.SETFILTER("Global Dimension 1 Filter",GetDimTotalingFilter(1,"Dimension 1 Totaling"));
            GLAcc.SETFILTER("Global Dimension 2 Filter",GetDimTotalingFilter(2,"Dimension 2 Totaling"));
            GLAcc.FILTERGROUP(8);
            GLAcc.SETFILTER("Business Unit Filter",TempColumnLayout."Business Unit Totaling");
            GLAcc.SETFILTER("Global Dimension 1 Filter",GetDimTotalingFilter(1,TempColumnLayout."Dimension 1 Totaling"));
            GLAcc.SETFILTER("Global Dimension 2 Filter",GetDimTotalingFilter(2,TempColumnLayout."Dimension 2 Totaling"));
            GLAcc.FILTERGROUP(0);
            PAGE.RUN(PAGE::"Chart of Accounts (G/L)",GLAcc)
          END ELSE BEGIN
            GLAcc.COPYFILTER("Date Filter",GLAccAnalysisView."Date Filter");
            GLAcc.COPYFILTER("Budget Filter",GLAccAnalysisView."Budget Filter");
            GLAcc.COPYFILTER("Business Unit Filter",GLAccAnalysisView."Business Unit Filter");
            GLAccAnalysisView.SETRANGE("Analysis View Filter",AccSchedName."Analysis View Name");
            GLAccAnalysisView.CopyDimFilters(AccScheduleLine);
            GLAccAnalysisView.FILTERGROUP(2);
            GLAccAnalysisView.SetDimFilters(
              GetDimTotalingFilter(1,"Dimension 1 Totaling"),GetDimTotalingFilter(2,"Dimension 2 Totaling"),
              GetDimTotalingFilter(3,"Dimension 3 Totaling"),GetDimTotalingFilter(4,"Dimension 4 Totaling"));
            GLAccAnalysisView.FILTERGROUP(8);
            GLAccAnalysisView.SetDimFilters(
              GetDimTotalingFilter(1,TempColumnLayout."Dimension 1 Totaling"),
              GetDimTotalingFilter(2,TempColumnLayout."Dimension 2 Totaling"),
              GetDimTotalingFilter(3,TempColumnLayout."Dimension 3 Totaling"),
              GetDimTotalingFilter(4,TempColumnLayout."Dimension 4 Totaling"));
            GLAccAnalysisView.SETFILTER("Business Unit Filter",TempColumnLayout."Business Unit Totaling");
            GLAccAnalysisView.FILTERGROUP(0);
            CLEAR(ChartOfAccsAnalysisView);
            ChartOfAccsAnalysisView.InsertTempGLAccAnalysisViews(GLAcc);
            ChartOfAccsAnalysisView.SETTABLEVIEW(GLAccAnalysisView);
            ChartOfAccsAnalysisView.RUN;
          END;
        END;
    END;

    LOCAL PROCEDURE DrillDownOnCFAccount@49(TempColumnLayout@1001 : TEMPORARY Record 334;VAR AccScheduleLine@1000 : Record 85);
    VAR
      CFAccount@1004 : Record 841;
      GLAccAnalysisView@1003 : Record 376;
      ChartOfAccsAnalysisView@1002 : Page 569;
    BEGIN
      WITH AccScheduleLine DO BEGIN
        COPYFILTER("Cash Flow Forecast Filter",CFAccount."Cash Flow Forecast Filter");

        SetCFAccRowFilter(CFAccount,AccScheduleLine);
        SetCFAccColumnFilter(CFAccount,AccScheduleLine,TempColumnLayout);
        AccSchedName.GET("Schedule Name");
        IF AccSchedName."Analysis View Name" = '' THEN BEGIN
          COPYFILTER("Dimension 1 Filter",CFAccount."Global Dimension 1 Filter");
          COPYFILTER("Dimension 2 Filter",CFAccount."Global Dimension 2 Filter");
          CFAccount.FILTERGROUP(2);
          CFAccount.SETFILTER("Global Dimension 1 Filter",GetDimTotalingFilter(1,"Dimension 1 Totaling"));
          CFAccount.SETFILTER("Global Dimension 2 Filter",GetDimTotalingFilter(2,"Dimension 2 Totaling"));
          CFAccount.FILTERGROUP(8);
          CFAccount.SETFILTER("Global Dimension 1 Filter",GetDimTotalingFilter(1,TempColumnLayout."Dimension 1 Totaling"));
          CFAccount.SETFILTER("Global Dimension 2 Filter",GetDimTotalingFilter(2,TempColumnLayout."Dimension 2 Totaling"));
          CFAccount.FILTERGROUP(0);
          PAGE.RUN(PAGE::"Chart of Cash Flow Accounts",CFAccount)
        END ELSE BEGIN
          CFAccount.COPYFILTER("Date Filter",GLAccAnalysisView."Date Filter");
          CFAccount.COPYFILTER("Cash Flow Forecast Filter",GLAccAnalysisView."Cash Flow Forecast Filter");
          GLAccAnalysisView.SETRANGE("Analysis View Filter",AccSchedName."Analysis View Name");
          GLAccAnalysisView.CopyDimFilters(AccScheduleLine);
          GLAccAnalysisView.FILTERGROUP(2);
          GLAccAnalysisView.SetDimFilters(
            GetDimTotalingFilter(1,"Dimension 1 Totaling"),
            GetDimTotalingFilter(2,"Dimension 2 Totaling"),
            GetDimTotalingFilter(3,"Dimension 3 Totaling"),
            GetDimTotalingFilter(4,"Dimension 4 Totaling"));
          GLAccAnalysisView.FILTERGROUP(8);
          GLAccAnalysisView.SetDimFilters(
            GetDimTotalingFilter(1,TempColumnLayout."Dimension 1 Totaling"),
            GetDimTotalingFilter(2,TempColumnLayout."Dimension 2 Totaling"),
            GetDimTotalingFilter(3,TempColumnLayout."Dimension 3 Totaling"),
            GetDimTotalingFilter(4,TempColumnLayout."Dimension 4 Totaling"));
          GLAccAnalysisView.FILTERGROUP(0);
          CLEAR(ChartOfAccsAnalysisView);
          ChartOfAccsAnalysisView.InsertTempCFAccountAnalysisVie(CFAccount);
          ChartOfAccsAnalysisView.SETTABLEVIEW(GLAccAnalysisView);
          ChartOfAccsAnalysisView.RUN;
        END;
      END;
    END;

    [External]
    PROCEDURE FindPeriod@48(VAR AccScheduleLine@1004 : Record 85;SearchText@1003 : Text[3];PeriodType@1000 : 'Day,Week,Month,Quarter,Year,Accounting Period');
    VAR
      Calendar@1002 : Record 2000000007;
      PeriodFormMgt@1001 : Codeunit 359;
    BEGIN
      WITH AccScheduleLine DO BEGIN
        IF GETFILTER("Date Filter") <> '' THEN BEGIN
          Calendar.SETFILTER("Period Start",GETFILTER("Date Filter"));
          IF NOT PeriodFormMgt.FindDate('+',Calendar,PeriodType) THEN
            PeriodFormMgt.FindDate('+',Calendar,PeriodType::Day);
          Calendar.SETRANGE("Period Start");
        END;
        PeriodFormMgt.FindDate(SearchText,Calendar,PeriodType);
        SETRANGE("Date Filter",Calendar."Period Start",Calendar."Period End");
        IF GETRANGEMIN("Date Filter") = GETRANGEMAX("Date Filter") THEN
          SETRANGE("Date Filter",GETRANGEMIN("Date Filter"));
      END;
    END;

    [External]
    PROCEDURE CalcFieldError@53(VAR ErrorType@1002 : 'None,Division by Zero,Period Error,Both';RowNo@1000 : Integer;ColumnNo@1001 : Integer);
    BEGIN
      AccSchedCellValue.SETRANGE("Row No.",RowNo);
      AccSchedCellValue.SETRANGE("Column No.",ColumnNo);
      ErrorType := ErrorType::None;
      IF AccSchedCellValue.FINDFIRST THEN
        CASE TRUE OF
          AccSchedCellValue."Has Error":
            ErrorType := ErrorType::"Division by Zero";
          AccSchedCellValue."Period Error":
            ErrorType := ErrorType::"Period Error";
          AccSchedCellValue."Has Error" AND AccSchedCellValue."Period Error":
            ErrorType := ErrorType::Both;
        END;

      AccSchedCellValue.SETRANGE("Row No.");
      AccSchedCellValue.SETRANGE("Column No.");
    END;

    [External]
    PROCEDURE ForceRecalculate@55(NewRecalculate@1000 : Boolean);
    BEGIN
      Recalculate := NewRecalculate;
    END;

    LOCAL PROCEDURE CalcLCYToACY@56(ColValue@1000 : Decimal) : Decimal;
    BEGIN
      IF GetGLSetup THEN
        IF GLSetup."Additional Reporting Currency" <> '' THEN
          AddRepCurrency.GET(GLSetup."Additional Reporting Currency");
      IF GLSetup."Additional Reporting Currency" <> '' THEN
        EXIT(ROUND(ExchangeAmtAddCurrToLCY(ColValue),AddRepCurrency."Amount Rounding Precision"));
      EXIT(0);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterAccSchedLineInsert@63(VAR AccSchedLine@1000 : Record 85);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCalcCellValue@60(VAR AccSchedLine@1000 : Record 85;VAR ColumnLayout@1001 : Record 334;VAR Result@1002 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetGLAccRowFilters@10(VAR GLAccount@1000 : Record 15;VAR AccScheduleLine@1001 : Record 85);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetCFAccRowFilter@27(VAR CashFlowAccount@1000 : Record 841;VAR AccScheduleLine@1001 : Record 85);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetGLAccColumnFilters@29(VAR GLAccount@1001 : Record 15;VAR AccScheduleLine@1000 : Record 85;VAR ColumnLayout@1002 : Record 334);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetCFAccColumnFilter@30(VAR CashFlowAccount@1001 : Record 841;VAR AccScheduleLine@1000 : Record 85;VAR ColumnLayout@1002 : Record 334);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetGLAccGLEntryFilters@73(VAR GLAccount@1000 : Record 15;VAR GLEntry@1001 : Record 17;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1003 : Record 334;UseBusUnitFilter@1004 : Boolean;UseDimFilter@1005 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetGLAccGLBudgetEntryFilters@70(VAR GLAcc@1005 : Record 15;VAR GLBudgetEntry@1004 : Record 96;VAR AccSchedLine@1003 : Record 85;VAR ColumnLayout@1002 : Record 334;UseBusUnitFilter@1001 : Boolean;UseDimFilter@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetGLAccAnalysisViewEntryFilters@75(VAR GLAcc@1003 : Record 15;VAR AnalysisViewEntry@1002 : Record 365;VAR AccSchedLine@1001 : Record 85;VAR ColumnLayout@1000 : Record 334);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetGLAccAnalysisViewBudgetEntries@74(VAR GLAcc@1005 : Record 15;VAR AnalysisViewBudgetEntry@1004 : Record 366;VAR AccSchedLine@1003 : Record 85;VAR ColumnLayout@1002 : Record 334);
    BEGIN
    END;

    [Integration(TRUE)]
    LOCAL PROCEDURE OnBeforeCalcCellExit@58(VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1001 : Record 334;CalcAddCurr@1000 : Boolean;VAR Result@1003 : Decimal);
    BEGIN
    END;

    [Integration(TRUE)]
    LOCAL PROCEDURE OnBeforeCalcGLAcc@65(VAR GLAcc@1003 : Record 15;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1001 : Record 334;CalcAddCurr@1000 : Boolean;VAR ColValue@1004 : Decimal;VAR IsHandled@1005 : Boolean);
    BEGIN
    END;

    [Integration(TRUE)]
    LOCAL PROCEDURE OnBeforeCalcCFAcc@67(VAR CFAccount@1002 : Record 841;VAR AccSchedLine@1001 : Record 85;VAR ColumnLayout@1000 : Record 334;VAR ColValue@1003 : Decimal;VAR IsHandled@1004 : Boolean);
    BEGIN
    END;

    [Integration(TRUE)]
    LOCAL PROCEDURE OnBeforeCalcCostType@68(VAR CostType@1003 : Record 1103;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1001 : Record 334;CalcAddCurr@1000 : Boolean;VAR ColValue@1004 : Decimal;VAR IsHandled@1005 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeEvaluateExpression@71(VAR AccScheduleLine@1000 : Record 85;VAR ColumnLayout@1001 : Record 334;VAR MaxLevel@1002 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeTestBalance@59(VAR GLAccount@1000 : Record 15;VAR AccScheduleName@1003 : Record 84;VAR AccScheduleLine@1001 : Record 85;VAR ColumnLayout@1002 : Record 334;AmountType@1004 : Integer;VAR ColValue@1005 : Decimal;CalcAddCurr@1006 : Boolean;VAR TestBalance@1007 : Boolean;VAR GLEntry@1008 : Record 17;VAR GLBudgetEntry@1009 : Record 96);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDrillDownFromOverviewPage@61(VAR TempColumnLayout@1002 : TEMPORARY Record 334;VAR AccScheduleLine@1001 : Record 85;PeriodLength@1000 : Option;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDrillDownOnAccounts@78(VAR AccScheduleLine@1000 : Record 85;VAR TempColumnLayout@1001 : TEMPORARY Record 334;PeriodLength@1002 : Option;StartDate@1003 : Date;EndDate@1004 : Date);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDrillDownOnGLAccount@77(VAR TempColumnLayout@1000 : TEMPORARY Record 334;VAR AccScheduleLine@1001 : Record 85;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCalcCellValueOnBeforeExit@76(VAR AccScheduleLine@1000 : Record 85;VAR ColumnLayout@1001 : Record 334;CalcAddCurr@1002 : Boolean;StartDate@1003 : Date;EndDate@1004 : Date;VAR Result@1005 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnEvaluateExpressionOnBeforeCalcAccSchedLineCellValue@79(SourceAccSchedLine@1003 : Record 85;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1001 : Record 334;CalcAddCurr@1000 : Boolean;VAR IsHandled@1004 : Boolean;VAR CellValue@1005 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnEvaluateExpressionOnBeforeCalcColumnLayoutCellValue@80(SourceColumnLayout@1006 : Record 334;VAR AccSchedLine@1002 : Record 85;VAR ColumnLayout@1001 : Record 334;CalcAddCurr@1000 : Boolean;VAR IsHandled@1004 : Boolean;VAR CellValue@1005 : Decimal);
    BEGIN
    END;

    BEGIN
    END.
  }
}

