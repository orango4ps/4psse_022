OBJECT Codeunit 11130030 Autorization Event Handling
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Table,11020569,OnAfterInsertEvent,"",Skip)]
    LOCAL PROCEDURE OnAfterInsertFunctionProfile@1100525001(VAR Rec@1100525000 : Record 11020569;RunTrigger@1100525001 : Boolean);
    BEGIN
      IF Rec.ISTEMPORARY OR NOT RunTrigger THEN
        EXIT;
      SyncFunctionProfileToUserProfile(Rec);
    END;

    [EventSubscriber(Table,11020569,OnAfterModifyEvent,"",Skip)]
    LOCAL PROCEDURE OnAfterModifyFunctionProfile@1100525012(VAR Rec@1100525000 : Record 11020569;VAR xRec@1100525001 : Record 11020569;RunTrigger@1100525002 : Boolean);
    BEGIN
      IF Rec.ISTEMPORARY OR NOT RunTrigger THEN
        EXIT;
      SyncFunctionProfileToUserProfile(Rec);
    END;

    [EventSubscriber(Table,11020569,OnAfterDeleteEvent,"",Skip)]
    LOCAL PROCEDURE OnAfterDeleteFunctionProfle@1100525028(VAR Rec@1100525000 : Record 11020569;RunTrigger@1100525001 : Boolean);
    BEGIN
      IF Rec.ISTEMPORARY OR NOT RunTrigger THEN
        EXIT;
      DeleteUserProfileData(Rec);
    END;

    PROCEDURE SyncFunctionProfileToUserProfile@1100525002(FunctionProfile@1100525000 : Record 11020569);
    BEGIN
      Sync2CUserProfile(FunctionProfile);
      Sync2CUserRoleperUserProfile(FunctionProfile);
      Sync2CUserRole(FunctionProfile);
    END;

    PROCEDURE DeleteUserProfileData@1100525009(FunctionProfile@1100525000 : Record 11020569);
    BEGIN
      Delete2CUserRoleperUserProfile(FunctionProfile);
      Delete2CUserProfile(FunctionProfile);
      Delete2CUserRole(FunctionProfile);
    END;

    LOCAL PROCEDURE Sync2CUserProfile@1100529212(FunctionProfile@1100529200 : Record 11020569);
    VAR
      "2CUserProfile"@1100529201 : Record 11111979;
    BEGIN
      IF FunctionProfile."No." <> '' THEN BEGIN
        IF NOT "2CUserProfile".GET(FunctionProfile."No.") THEN BEGIN
          "2CUserProfile"."User Profile" := FunctionProfile."No.";
          "2CUserProfile".INSERT(TRUE);
        END;
        "2CUserProfile".Description := FunctionProfile.Name;
        "2CUserProfile".Status := "2CUserProfile".Status::Released;
        "2CUserProfile".MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE Sync2CUserRoleperUserProfile@1100525032(FunctionProfile@1100529200 : Record 11020569);
    VAR
      "2CUserRoleperUserProfile"@1100525000 : Record 11111978;
    BEGIN
      "2CUserRoleperUserProfile".SETRANGE("User Profile",FunctionProfile."No.");
      "2CUserRoleperUserProfile".SETRANGE("User Role ID",FunctionProfile."No.");
      IF "2CUserRoleperUserProfile".FINDSET THEN BEGIN
        REPEAT
          "2CUserRoleperUserProfile"."Sync Status" := "2CUserRoleperUserProfile"."Sync Status"::Synchronized;
          "2CUserRoleperUserProfile".MODIFY;
        UNTIL "2CUserRoleperUserProfile".NEXT = 0;
      END ELSE BEGIN
        "2CUserRoleperUserProfile"."User Profile" := FunctionProfile."No.";
        "2CUserRoleperUserProfile"."User Role ID" := FunctionProfile."No.";
        "2CUserRoleperUserProfile"."Sync Status" := "2CUserRoleperUserProfile"."Sync Status"::Synchronized;
        "2CUserRoleperUserProfile".INSERT;
      END;
    END;

    LOCAL PROCEDURE Sync2CUserRole@1100525007(FunctionProfile@1100529200 : Record 11020569);
    VAR
      "2CUserRole"@1100525000 : Record 11112040;
    BEGIN
      "2CUserRole".SETRANGE("Role ID",FunctionProfile."No.");
      IF "2CUserRole".FINDSET THEN BEGIN
        REPEAT
          "2CUserRole".Status := "2CUserRole".Status::Released;
          "2CUserRole".MODIFY(TRUE);
        UNTIL "2CUserRole".NEXT = 0;
      END ELSE BEGIN
        "2CUserRole".INIT;
        "2CUserRole"."Role ID" := FunctionProfile."No.";
        "2CUserRole".INSERT;
        "2CUserRole".Status := "2CUserRole".Status::Released;
        "2CUserRole".MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE Delete2CUserRoleperUserProfile@1100525000(FunctionProfile@1100525001 : Record 11020569);
    VAR
      "2CUserRoleperUserProfile"@1100525000 : Record 11111978;
    BEGIN
      "2CUserRoleperUserProfile".SETRANGE("User Profile",FunctionProfile."No.");
      "2CUserRoleperUserProfile".DELETEALL(FALSE);
    END;

    LOCAL PROCEDURE Delete2CUserProfile@1100525004(FunctionProfile@1100529200 : Record 11020569);
    VAR
      "2CUserProfile"@1100529201 : Record 11111979;
    BEGIN
      IF "2CUserProfile".GET(FunctionProfile."No.") THEN
        "2CUserProfile".DELETE(FALSE);
    END;

    LOCAL PROCEDURE Delete2CUserRole@1100525003(FunctionProfile@1100525001 : Record 11020569);
    VAR
      "2CUserRole"@1100525000 : Record 11112040;
    BEGIN
      "2CUserRole".SETRANGE("Role ID", FunctionProfile."No.");
      "2CUserRole".DELETEALL(FALSE);
    END;

    BEGIN
    END.
  }
}

