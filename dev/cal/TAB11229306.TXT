OBJECT Table 11229306 Table Field Translation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    CaptionML=ENU=Table Field Translation;
  }
  FIELDS
  {
    { 10  ;   ;Table No.           ;Integer       ;CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr];
                                                   Editable=No }
    { 11  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(TableData),
                                                                                         Company Name=FILTER(''),
                                                                                         ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              NOR=Tabellnavn;
                                                              SVE=Tabellnamn];
                                                   Editable=No }
    { 20  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  No.=FIELD(Field No.),
                                                                                  Type=CONST(Option));
                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr];
                                                   Editable=No }
    { 21  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              NOR=Feltnavn;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
    { 30  ;   ;Language Id         ;Integer       ;CaptionML=[ENU=Language Id;
                                                              SVE=Spr†k-ID];
                                                   Editable=No }
    { 31  ;   ;Abbreviated Name    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Windows Language"."Abbreviated Name" WHERE (Language ID=FIELD(Language Id)));
                                                   CaptionML=[ENU=Abbreviated Name;
                                                              NOR=Forkortet navn;
                                                              SVE=F”rkortat namn];
                                                   Editable=No }
    { 40  ;   ;Option SeqNo.       ;Integer       ;CaptionML=ENU=Option SeqNo.;
                                                   Editable=No }
    { 41  ;   ;Option Value        ;Text100       ;CaptionML=[ENU=Option Value;
                                                              NOR=Alternativverdi;
                                                              SVE=Alternativv„rde];
                                                   Editable=No }
    { 42  ;   ;Option Length       ;Integer       ;DataClassification=ToBeClassified }
    { 50  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
  }
  KEYS
  {
    {    ;Table No.,Field No.,Language Id,Option SeqNo.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TableFieldTranslationMgt@1100528300 : Codeunit 11020235;

    PROCEDURE GetOptionCaptionsFromObject@1100525000(VAR Field@1100525000 : Record 2000000041;GetAllOptions@1100525001 : Boolean);
    VAR
      TableFieldTranslation@1100525002 : Record 11229306;
    BEGIN
      Field.SETRANGE(Type, Field.Type::Option);
      IF (NOT Field.FINDSET) THEN
        EXIT;
      REPEAT
        TableFieldTranslation."Table No." := Field.TableNo;
        TableFieldTranslation."Field No."   := Field."No.";
        TableFieldTranslation.GetOptionCaptions(GetAllOptions);
      UNTIL (Field.NEXT = 0);
    END;

    PROCEDURE GetOptionCaptions@1100525001(CurrLangOnly@1100525006 : Boolean);
    VAR
      Language@1100525000 : Record 8;
      SaveLanguageId@1100525001 : Integer;
      RecRef@1100525005 : RecordRef;
      FieldRef@1100525004 : FieldRef;
      StringFunctions@1100525003 : Codeunit 11012273;
      Options@1100528001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
      Captions@1100528000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
    BEGIN
      IF (CurrLangOnly) THEN
        Language.SETFILTER("Windows Language ID", '%1', GLOBALLANGUAGE)
      ELSE
        Language.SETFILTER("Windows Language ID", '>%1', 0);
      IF (NOT Language.FINDSET) THEN
        EXIT;
      IF (NOT OpenTable(RecRef, "Table No.")) THEN
        EXIT;

      SaveLanguageId := GLOBALLANGUAGE;
      FieldRef := RecRef.FIELD("Field No.");
      StringFunctions.SplitString2(FieldRef.OPTIONSTRING, ',', Options);

      REPEAT
        GLOBALLANGUAGE(Language."Windows Language ID");
        StringFunctions.SplitString2(FieldRef.OPTIONCAPTION, ',', Captions);
        InsertOptionCaptions(Language, Options, Captions)
      UNTIL (Language.NEXT = 0);
      GLOBALLANGUAGE(SaveLanguageId);
    END;

    PROCEDURE GetCaptionsFromObject@1100525012(VAR Field@1100525001 : Record 2000000041;GetAllOptions@1100525000 : Boolean);
    VAR
      TableFieldTranslation@1100525002 : Record 11229306;
    BEGIN
      IF (NOT Field.FINDSET) THEN
        EXIT;
      REPEAT
        TableFieldTranslation."Table No." := Field.TableNo;
        TableFieldTranslation."Field No."   := Field."No.";
        TableFieldTranslation.GetCaptions(GetAllOptions);
      UNTIL (Field.NEXT = 0);
    END;

    PROCEDURE GetCaptions@1100525009(CurrLangOnly@1100525000 : Boolean);
    VAR
      Language@1100525009 : Record 8;
      SaveLanguageId@1100525008 : Integer;
      RecRef@1100525007 : RecordRef;
      FieldRef@1100525006 : FieldRef;
      Options@1100525002 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
      Captions@1100525001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
    BEGIN
      IF (CurrLangOnly) THEN
        Language.SETFILTER("Windows Language ID", '%1', GLOBALLANGUAGE)
      ELSE
        Language.SETFILTER("Windows Language ID", '>%1', 0);
      IF (NOT Language.FINDSET) THEN
        EXIT;
      IF (NOT OpenTable(RecRef, "Table No.")) THEN
        EXIT;

      FieldRef := RecRef.FIELD("Field No.");
      SaveLanguageId := GLOBALLANGUAGE;
      CLEAR(Options);
      Options := Options.ArrayList;
      Options.Add('');

      REPEAT
        CLEAR(Captions);
        Captions := Captions.ArrayList;
        GLOBALLANGUAGE(Language."Windows Language ID");
        Captions.Add(FieldRef.CAPTION);
        InsertCaption(Language, Options, Captions, -1,-1);
      UNTIL (Language.NEXT = 0);
      GLOBALLANGUAGE(SaveLanguageId);
    END;

    PROCEDURE SetCaptionsAndOptions@1100528300(TableNo@1100528300 : Integer;FieldNo@1100528301 : Integer;CurrLangOnly@1100528302 : Boolean);
    BEGIN
      "Table No." := TableNo;
      "Field No." := FieldNo;
      GetCaptions(CurrLangOnly);

      "Table No." := TableNo;
      "Field No."   := FieldNo;
      GetOptionCaptions(CurrLangOnly);
    END;

    LOCAL PROCEDURE "---Locals---"@1100525008();
    BEGIN
    END;

    LOCAL PROCEDURE InsertOptionCaptions@1100528008(Language@1100525007 : Record 8;Options@1100528001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";Captions@1100528000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList");
    VAR
      Counter@1100525002 : Integer;
    BEGIN
      FOR Counter := 0 TO Options.Count - 1 DO BEGIN
        IF (NOT Options.Equals(Options.Item(Counter), '')) THEN
          InsertCaption(Language, Options, Captions, Counter,Options.Count);
      END;
    END;

    LOCAL PROCEDURE InsertCaption@1100528009(Language@1100525007 : Record 8;Options@1100528002 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";Captions@1100528001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";Counter@1100528000 : Integer;ArrLen@1100285100 : Integer);
    VAR
      TableFieldTranslation@1100525005 : Record 11229306;
    BEGIN
      IF (NOT TableFieldTranslation.GET("Table No.", "Field No.",Language."Windows Language ID", Counter)) THEN BEGIN
        TableFieldTranslation.INIT;
        TableFieldTranslation."Table No." := "Table No.";
        TableFieldTranslation."Field No." := "Field No.";
        TableFieldTranslation."Language Id" := Language."Windows Language ID";
        TableFieldTranslation."Option SeqNo." := Counter;
        TableFieldTranslation.INSERT;
      END;
      IF (Counter >= 0) THEN BEGIN
        IF (Options.Equals(Options.Item(Counter), ' ')) THEN
          TableFieldTranslation."Option Value" := STRSUBSTNO('%1', TableFieldTranslationMgt.GetBlankOptionTxt)
        ELSE
          TableFieldTranslation."Option Value" := Options.Item(Counter);
        TableFieldTranslation.Description := Captions.Item(Counter);
      END ELSE BEGIN
        TableFieldTranslation."Option Value" := '';
        TableFieldTranslation.Description := Captions.Item(0)
      END;
      TableFieldTranslation."Option Length" := ArrLen;//4PSSE
      TableFieldTranslation.MODIFY;
    END;

    [TryFunction]
    LOCAL PROCEDURE OpenTable@1100528500(VAR RecRef@1100528500 : RecordRef;TableNo@1100528501 : Integer);
    BEGIN
      RecRef.OPEN(TableNo);
    END;

    BEGIN
    END.
  }
}

