OBJECT Report 11128538 Import Hogia Windows Payroll
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO10.00 (15000067),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Hogia Windows Payroll;
               NOR=Importer l›nnskostnad for Hogia Windows];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PayrollSetup.GET;
                 END;

    OnPreReport=BEGIN
                  ServerTempFile := FileMgt.UploadFileSilent(Filename);

                  TxtFile.TEXTMODE := TRUE;
                  TxtFile.OPEN(ServerTempFile);
                  IF PayrollDocumentDate = 0D THEN
                    ERROR(Text15000000);
                  IF Filename = '' THEN
                    ERROR(Text15000001);

                  COMMIT;
                  PayrollTools.CheckJournal(NextLine,DefGenJnlLine);
                  NextLine := 10000;

                  WHILE TxtFile.LEN <> TxtFile.POS DO BEGIN
                    TxtFile.READ(FileData);
                    Posttype := COPYSTR(FileData,1,3);
                    CASE Posttype OF
                      '001':
                        ManagePosttype1;
                      '002':
                        BEGIN
                          ManagePosttype2;
                          InsertJournalLine;
                        END;
                    END;
                  END;
                END;

    OnPostReport=BEGIN
                   TxtFile.CLOSE;
                   IF PayrollSetup."Save Payroll File" THEN
                     NewFilename;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   PayrollDocumentDate := WORKDATE;
                   IF DocumentText = '' THEN
                     DocumentText := Text15000007;
                   Filename := PayrollSetup."File Name";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1080000;2;Field     ;
                  Name=DocumentDate;
                  CaptionML=[ENU=Document Date;
                             NOR=Dokumentdato;
                             SVE=Dokumentdatum];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PayrollDocumentDate }

      { 1080002;2;Field     ;
                  Name=DocumentTextType;
                  CaptionML=[ENU=Document text;
                             NOR=Bilagstekst];
                  OptionCaptionML=[ENU=Automatic,Manual:;
                                   NOR=Automatisk,Manuelt:];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentTextType }

      { 1080005;2;Field     ;
                  Name=DocumentText;
                  CaptionML=[ENU=Document text;
                             NOR=Bilagstekst];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentText }

      { 1080006;2;Field     ;
                  Name=FileName;
                  AssistEdit=Yes;
                  CaptionML=[ENU=File Name;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Filename;
                  OnAssistEdit=BEGIN
                                 Filename := FileMgt.OpenFileDialog(Text15000002,Filename,Text15000003);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PayrollSetup@1080001 : Record 11128513;
      GenJnlLine@1080002 : Record 81;
      DefGenJnlLine@1080003 : Record 81;
      PrevGenJnlLine@1080004 : Record 81;
      PayrollTools@1080005 : Codeunit 11128517;
      FileMgt@1080006 : Codeunit 419;
      TxtFile@1080034 : File;
      DocumentText@1080009 : Text[50];
      Filename@1080035 : Text[250];
      Posttype@1080037 : Text[3];
      FileData@1080038 : Text[250];
      ImportDocumentText@1080039 : Text[50];
      AmountText@1080040 : Text[30];
      ServerTempFile@1080041 : Text[1024];
      file2@1080042 : Text[250];
      file1@1080043 : Text[250];
      NextLine@1080013 : Integer;
      ImportAccountNo@1080016 : Code[20];
      ImportGlDimCode1@1080019 : Code[10];
      ImportGlDimCode2@1080020 : Code[10];
      ImportAmount@1080021 : Decimal;
      Text15000000@1080025 : TextConst 'ENU=Please specify Posting Date.;NOR=Angi bokf›ringsdato.';
      Text15000001@1080026 : TextConst 'ENU=Please specify File Name.;NOR=Angi filnavn.';
      Text15000002@1080027 : TextConst 'ENU=Import from Payroll File.;NOR=Les inn fra l›nnsfil.';
      Text15000003@1080028 : TextConst 'ENU=Text Files (*.txt)|*.txt|All Files (*.*)|*.*;NOR=Tekstfiler (*.txt)|*.txt|Alle filer (*.*)|*.*';
      Text15000007@1080032 : TextConst 'ENU=Payroll;NOR=L›nnskostnad';
      PayrollDocumentDate@1080044 : Date;
      ImportDate@1080045 : Date;
      DocumentTextType@1080046 : 'Automatic,Manual';

    PROCEDURE Initialize@1080000(SetGenJnlLine@1080000 : Record 81);
    BEGIN
      DefGenJnlLine := SetGenJnlLine;
    END;

    LOCAL PROCEDURE ManagePosttype1@1080001();
    BEGIN
      EVALUATE(ImportDate,COPYSTR(FileData,4,8));
      ImportDocumentText := COPYSTR(FileData,32,12);
    END;

    LOCAL PROCEDURE ManagePosttype2@1080002();
    BEGIN
      ImportGlDimCode1 := DELCHR(COPYSTR(FileData,33,9),'<','0');
      ImportGlDimCode2 := DELCHR(COPYSTR(FileData,61,9),'<','0');
      ImportAccountNo := DELCHR(COPYSTR(FileData,42,9),'<','0');
      AmountText := COPYSTR(FileData,100,20);
      AmountText := CONVERTSTR(AmountText,'.',',');
      EVALUATE(ImportAmount,AmountText);
    END;

    LOCAL PROCEDURE InsertJournalLine@1080003();
    BEGIN
      PrevGenJnlLine := GenJnlLine;
      CLEAR(GenJnlLine);
      GenJnlLine.VALIDATE("Journal Template Name",DefGenJnlLine."Journal Template Name");
      GenJnlLine.VALIDATE("Journal Batch Name",DefGenJnlLine."Journal Batch Name");
      GenJnlLine.SetUpNewLine(PrevGenJnlLine,0,FALSE);
      GenJnlLine.VALIDATE("Line No.",NextLine);
      NextLine := NextLine + 10000;
      CASE PayrollSetup."Post to" OF
        PayrollSetup."Post to"::"G/L Account":
          GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"G/L Account");
        PayrollSetup."Post to"::"Bank Account":
          GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
      END;
      GenJnlLine.VALIDATE("Posting Date",ImportDate);
      GenJnlLine.VALIDATE("Document Date",PayrollDocumentDate);
      GenJnlLine.VALIDATE("Account No.",ImportAccountNo);
      GenJnlLine.VALIDATE(Amount,ImportAmount);
      IF DocumentTextType = DocumentTextType::Manual THEN
        GenJnlLine.VALIDATE(Description,DocumentText)
      ELSE
        GenJnlLine.VALIDATE(Description,ImportDocumentText);

      IF PayrollSetup."Import Department and Project" THEN BEGIN
        GenJnlLine.VALIDATE("Shortcut Dimension 1 Code",ImportGlDimCode1);
        GenJnlLine.VALIDATE("Shortcut Dimension 2 Code",ImportGlDimCode2);
      END;
      GenJnlLine.INSERT;
    END;

    PROCEDURE NewFilename@1080004();
    BEGIN
      IF FileMgt.ClientFileExists(Filename) THEN BEGIN
        IF (Filename[STRLEN(Filename)] = '~') OR
           (Filename[STRLEN(Filename) - 1] = '~')
        THEN
          EXIT;

        file1 := COPYSTR(Filename,1,STRLEN(Filename) - 1) + '~';
        file2 := COPYSTR(Filename,1,STRLEN(Filename) - 2) + '~~';
        IF FileMgt.ClientFileExists(file2) THEN
          FileMgt.DeleteClientFile(file2);
        IF FileMgt.ClientFileExists(file1) THEN
          FileMgt.MoveFile(file1,file2);
        FileMgt.MoveFile(Filename,file1);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

