OBJECT Report 77753 Switch 4PS Standaard Loc. Auth
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=LocalRelTool;
  }
  PROPERTIES
  {
    CaptionML=ENU=Switch 4PS Standard for Local Authorization;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Set4PSStdTo := TRUE;
                   SwitchPermSets := TRUE;
                   SwitchPermSetGroups := TRUE;
                 END;

    OnPreReport=BEGIN
                  IF FilterOn = '' THEN
                    ERROR(Text001);
                END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table2000000004;
               DataItemTableView=SORTING(Role ID);
               OnPreDataItem=BEGIN
                               "Permission Set".SETFILTER("Role ID",FilterOn);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF SwitchPermSets THEN BEGIN
                                    SwitchPermissionSet4PS();
                                    SwitchRolesLinkedToPermissionSet("Role ID");
                                  END;
                                END;

               OnPostDataItem=VAR
                                AuthToolIntMgt@1000 : Codeunit 77750;
                              BEGIN
                              END;
                               }

    { 2   ;    ;DataItem;                    ;
               DataItemTable=Table11020570;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               "Permission Set Group".SETFILTER("No.",FilterOn);
                               "Permission Set Group".SETFILTER("Standard 4PS",'<>%1',Set4PSStdTo);
                             END;

               OnAfterGetRecord=VAR
                                  RoleGroupRole@1000 : Record 11020571;
                                  PermissionSet@1001 : Record 2000000004;
                                BEGIN
                                  IF SwitchPermSetGroups THEN BEGIN
                                    "Permission Set Group"."Standard 4PS" := Set4PSStdTo;
                                    "Permission Set Group".MODIFY;
                                    SwitchRolesLinkedToPermissionSetGroups("No.");
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  ContainerType=ContentArea }

      { 1100409000;1;Field  ;
                  CaptionML=ENU=Filter on Permission Set and Permission Set Groups;
                  SourceExpr=FilterOn }

      { 2   ;1   ;Field     ;
                  CaptionML=ENU=Set 4PS Standard to;
                  SourceExpr=Set4PSStdTo }

      { 4   ;1   ;Field     ;
                  CaptionML=ENU=Switch Permission Sets;
                  SourceExpr=SwitchPermSets }

      { 5   ;1   ;Field     ;
                  CaptionML=ENU=Switch Permission Set Groups;
                  SourceExpr=SwitchPermSetGroups }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text001@1002 : TextConst 'ENU=Filter should be filled in.';
      SwitchPermSets@1004 : Boolean;
      SwitchPermSetGroups@1005 : Boolean;
      FilterOn@1100409000 : Text[20];
      Set4PSStdTo@1000 : Boolean;

    LOCAL PROCEDURE SwitchPermissionSet4PS@1100528500();
    VAR
      PermissionSet4PS@1100528500 : Record 11229416;
    BEGIN
      //Permission Set is standard if a record if present in Permission Set 4PS
      IF Set4PSStdTo THEN BEGIN
         IF NOT PermissionSet4PS.GET("Permission Set"."Role ID") THEN BEGIN
           PermissionSet4PS.INIT;
           PermissionSet4PS."Permission Set ID" := "Permission Set"."Role ID";
           PermissionSet4PS.INSERT;
         END;
      END ELSE BEGIN
         IF PermissionSet4PS.GET("Permission Set"."Role ID") THEN
           PermissionSet4PS.DELETE;
      END;
    END;

    LOCAL PROCEDURE SwitchRolesLinkedToPermissionSet@1(RoleID@1002 : Code[20]);
    VAR
      PermSetGroupPermSet@1001 : Record 11020571;
      PermSetGroup@1000 : Record 11020570;
    BEGIN
      //Table 11020571 'Perm. Set Group Permission Set' is a link between Permission Set Group and Permission Set.
      //This table also contains a field "Standard 4PS" that need to be set.
      //Only allowed to set this field to True when both Permission set and Permission Set Group are 'Standard 4PS'.

      PermSetGroupPermSet.SETRANGE("Permission Set No.", RoleID);
      IF PermSetGroupPermSet.FINDFIRST THEN
        REPEAT
          IF Set4PSStdTo THEN BEGIN
            PermSetGroup.GET(PermSetGroupPermSet."Permission Set Group No.");
            IF PermSetGroup."Standard 4PS" THEN BEGIN
              PermSetGroupPermSet."Standard 4PS" := Set4PSStdTo;
              PermSetGroupPermSet."Updated on" := TODAY;
              PermSetGroupPermSet.MODIFY;
            END
          END ELSE BEGIN
            PermSetGroupPermSet."Standard 4PS" := Set4PSStdTo;
            PermSetGroupPermSet.MODIFY;
            END
        UNTIL PermSetGroupPermSet.NEXT = 0;
    END;

    LOCAL PROCEDURE SwitchRolesLinkedToPermissionSetGroups@2(Code@1002 : Code[20]);
    VAR
      PermSetGroupPermSet@1001 : Record 11020571;
      PermissionSet4PS@1100528500 : Record 11229416;
    BEGIN
      //Table 11020571 'Perm. Set Group Permission Set' is a link between Permission Set Group and Permission Set.
      //This table also contains a field "Standard 4PS" that need to be set.
      //Only allowed to set this field to True when both Permission set and Permission Set Group are 'Standard 4PS'.

      PermSetGroupPermSet.SETRANGE("Permission Set Group No.", Code);
      IF PermSetGroupPermSet.FINDFIRST THEN
        REPEAT
          IF Set4PSStdTo THEN BEGIN
            IF PermissionSet4PS.GET(PermSetGroupPermSet."Permission Set No.") THEN BEGIN
              PermSetGroupPermSet."Standard 4PS" := Set4PSStdTo;
              PermSetGroupPermSet."Updated on" := TODAY;
              PermSetGroupPermSet.MODIFY;
            END
          END ELSE BEGIN
              PermSetGroupPermSet."Standard 4PS" := Set4PSStdTo;
              PermSetGroupPermSet.MODIFY;
          END;
        UNTIL PermSetGroupPermSet.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

