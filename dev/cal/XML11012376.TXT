OBJECT XMLport 11012376 Export Hours Payroll (old)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Export Hours Payroll;
               SVE=Exportera timmar Payroll];
    Direction=Export;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Exchange Salary Application");
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::Payroll);
                    HumanResourcesSetup.TESTFIELD("Receiving Company (Payroll)");
                    HumanResourcesSetup.TESTFIELD("Structure Part (Payroll)");
                  END;

    OnPreXMLport=BEGIN
                   IF DocumentNumberCode = '' THEN
                     ERROR(Text002);

                   PathText := HumanResourcesSetup."Pathname Files to Salary";
                   currXMLport.FILENAME(PathText + DocumentNumberCode + Text003);

                   IF FileMgt.ClientFileExists(currXMLport.FILENAME) THEN
                     IF NOT FileMgt.ExistingFileMustBeReplaced(currXMLport.FILENAME, TRUE) THEN
                       ERROR('');
                 END;

    Format=Fixed Text;
  }
  ELEMENTS
  {
    { [{51854F32-0E82-49C4-BEE2-D2232E63834E}];  ;Root                ;Element ;Text     }

    { [{27EFEFC3-7210-48B1-9F4B-F83055A3496E}];1 ;PostedHourLine      ;Element ;Table   ;
                                                  SourceTable=Table11012085;
                                                  SourceTableView=SORTING(Field41,Field30)
                                                                  WHERE(Field424=CONST(No),
                                                                        Field25=FILTER(''));
                                                  Export::OnPreXMLItem=VAR
                                                                         RecRef@1100528400 : RecordRef;
                                                                       BEGIN
                                                                         RecRef.GETTABLE("Posted Hour Line");
                                                                         XMLportStatusDialogMgt.ShowExportStatus(RecRef);

                                                                         WITH "Posted Hour Line" DO BEGIN
                                                                           SETFILTER("Document No.",DocumentNumberCode);
                                                                           SETFILTER("Cost Type", '%1|%2', "Cost Type"::Labor, "Cost Type"::Sundry);
                                                                           IF EmployerCde <> '' THEN
                                                                             SETRANGE(Employer,EmployerCde);
                                                                           IF NOT FINDFIRST THEN
                                                                             MESSAGE(Text004);

                                                                           IF HoursExportRec.FIND('+') THEN
                                                                             LineNoInt := HoursExportRec."Line No." + 1
                                                                           ELSE
                                                                             LineNoInt := 1;

                                                                           HoursExportRec.INIT;
                                                                           HoursExportRec."Line No." := LineNoInt;
                                                                           HoursExportRec."Document No." := DocumentNumberCode;
                                                                           HoursExportRec."User ID" := USERID;
                                                                           HoursExportRec.Date := TODAY;
                                                                           HoursExportRec.Time := TIME;
                                                                           HoursExportRec.INSERT;

                                                                           ExportDateTime := CURRENTDATETIME;
                                                                         END;
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             WITH "Posted Hour Line" DO BEGIN
                                                                               IF EmplRec.GET("Employee No.") THEN BEGIN
                                                                                 Employer := EmplRec.Employer;
                                                                                 DepartmentCode := EmplRec."Global Dimension 1 Code";
                                                                               END;
                                                                               IF (Type = Type::Indirect) AND HumanResourcesSetup."Export Cost Code as Department" THEN
                                                                                 DepartmentCode := "Cost Code";
                                                                               CompanyNumber := DetermineCompanyNumber("Receiving Company");
                                                                               IF "Calculate Wage Costs" = "Calculate Wage Costs"::"Fixed Rate" THEN BEGIN
                                                                                 "Project No." := '';
                                                                                 StructurePart := '';
                                                                                 "Cost Object" := '';
                                                                               END ELSE BEGIN
                                                                                 IF Type = Type::Service THEN BEGIN
                                                                                   "Project No." := 'S~' + COPYSTR("Service Order No.", 1, 4);
                                                                                   StructurePart := COPYSTR("Service Order No.", 5, MAXSTRLEN(StructurePart));
                                                                                 END ELSE BEGIN
                                                                                   StructurePart := HumanResourcesSetup."Structure Part (Payroll)";
                                                                                   sep := STRPOS("Project No.", '-');
                                                                                   IF NOT ProjRec.GET("Project No.") THEN ProjRec.INIT;
                                                                                   IF (ProjRec."Single/Main/Sub Project" = ProjRec."Single/Main/Sub Project"::"Sub Project") AND (sep <> 0) THEN BEGIN
                                                                                     IF Element = '' THEN
                                                                                       PartElement := HumanResourcesSetup."Structure Part (Payroll)"
                                                                                     ELSE
                                                                                       PartElement := COPYSTR("Project No.", (sep+1), (STRLEN("Project No.")-sep)) +
                                                                                                      COPYSTR("Project No.", sep, 1) + Element;
                                                                                     StructurePart := COPYSTR(PartElement, 1, MAXSTRLEN(StructurePart));
                                                                                     "Project No." := COPYSTR("Project No.", 1, (sep-1));
                                                                                   END;
                                                                                 END;
                                                                               END;
                                                                               CASE "Component Type" OF
                                                                                 "Component Type"::Hours: QuantAmountDec := "Total Line";
                                                                                 "Component Type"::Expenses:
                                                                                   BEGIN
                                                                                     WageCompRec.GET("Wage Component");
                                                                                     IF WageCompRec."Export Quantity i.s.o. Amount" THEN
                                                                                       QuantAmountDec := Quantity
                                                                                     ELSE
                                                                                       QuantAmountDec := "Amount (LCY)" - "Precalc. Surcharge Expenses";
                                                                                   END;
                                                                                 "Component Type"::Days: QuantAmountDec:= Quantity;
                                                                               END;
                                                                               LineNumber := LineNumber + 1;
                                                                               UpdateStatus(Year, Week, "Employee No.", "Line No.");
                                                                             END;
                                                                           END;
                                                                            }

    { [{B130658A-03C0-4B7D-B7E7-E7CF40B4FE44}];2 ;EmployerTxt         ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 EmployerTxt := fill("Posted Hour Line".Employer,3,' ','<');
                                                                               END;

                                                  Width=3 }

    { [{33CFBFD8-A0E6-4A52-A24F-D4C368A80B75}];2 ;EmployeeNo          ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Posted Hour Line::Employee No.;
                                                  Width=4 }

    { [{BEC7384C-7B70-44FB-9A56-CBC4DBA9BF09}];2 ;WeekTxt             ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 WeekTxt := fill(STRSUBSTNO('%1',"Posted Hour Line".Week),2,' ','<');
                                                                               END;

                                                  Width=2 }

    { [{0EEF46CA-C77A-4A24-B95F-D22F7CA2451F}];2 ;LineNumberTxt       ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 LineNumberTxt := fill(STRSUBSTNO('%1',LineNumber),3,'0','<');
                                                                               END;

                                                  Width=3 }

    { [{1D4E33AF-833E-4D43-BD7A-7546A4C95DDC}];2 ;WageComponentTxt    ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 WageComponentTxt := fill(STRSUBSTNO('%1',"Posted Hour Line"."Wage Component"),3,' ','<');
                                                                               END;

                                                  Width=3 }

    { [{9799CF4D-FAC9-42B7-A60B-48C86C757E1D}];2 ;ProjectNoTxt        ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 ProjectNoTxt := fill(STRSUBSTNO('%1',"Posted Hour Line"."Project No."),6,' ','<');
                                                                               END;

                                                  Width=6 }

    { [{6BA44C2B-2F86-43FB-9968-951408893429}];2 ;StructurePartTxt    ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 StructurePartTxt := fill(STRSUBSTNO('%1',StructurePart),8,' ','<');
                                                                               END;

                                                  Width=8 }

    { [{68789C47-1F96-4971-8C85-060D775D8708}];2 ;CostObjectTxt       ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 CostObjectTxt := fill(STRSUBSTNO('%1',"Posted Hour Line"."Cost Object"),8,' ','<');
                                                                               END;

                                                  Width=8 }

    { [{F2826E3A-4CDE-463D-B92E-3FACF79AF623}];2 ;CompanyNumber       ;Element ;Text    ;
                                                  Width=3 }

    { [{A007DFB4-0707-4A4E-B51B-96BE408FAEFB}];2 ;DepartmentCodeTxt   ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 DepartmentCodeTxt := fill(STRSUBSTNO('%1',DepartmentCode),6,' ','<');
                                                                               END;

                                                  Width=6 }

    { [{3164DCE1-36AD-4796-BF68-41E6A5B92CE4}];2 ;QuantAmountDecTxt   ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 QuantAmountDecTxt := FORMAT(QuantAmountDec,0,'<Precision,2:2><Standard format,0>');
                                                                                 QuantAmountDecTxt := DELCHR(QuantAmountDecTxt,'=',',');
                                                                                 QuantAmountDecTxt := fill(QuantAmountDecTxt,9,' ','<');
                                                                               END;

                                                  Width=9 }

    { [{4F37008A-862C-451F-8EA7-DA88A61EF9D9}];2 ;PostingDateTxt      ;Element ;Text    ;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 PostingDateTxt := FORMAT("Posted Hour Line"."Posting Date", 0, '<Year4><Month,2><Day,2>');
                                                                               END;

                                                  Width=8 }

    { [{DB356CD5-31EA-4C71-A9BF-77C171579C67}];2 ;EmployeeNo          ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Posted Hour Line::Employee No.;
                                                  Width=6 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      EmplRec@1100528416 : Record 5200;
      HoursExportRec@1100528415 : Record 11012041;
      HumanResourcesSetup@1100528414 : Record 5218;
      ProjRec@1100528413 : Record 11072003;
      WageCompRec@1100528412 : Record 11012014;
      XMLportStatusDialogMgt@1100528424 : Codeunit 11130006;
      FileMgt@1100528425 : Codeunit 419;
      DocumentNumberCode@1100528411 : Code[20];
      PathText@1100528410 : Text[50];
      LineNoInt@1100528409 : Integer;
      LineNumber@1100528408 : Integer;
      DepartmentCode@1100528406 : Code[6];
      StructurePart@1100528405 : Code[8];
      sep@1100528404 : Integer;
      PartElement@1100528403 : Code[20];
      EmployerCde@1100528402 : Code[20];
      QuantAmountDec@1100528401 : Decimal;
      ExportDateTime@1100528400 : DateTime;
      Text002@1100528421 : TextConst 'ENU=Fill out Document Number.;SVE=Fyll i dokumentnummer.';
      Text003@1100528420 : TextConst 'ENU=.txt;SVE=.txt';
      Text004@1100528419 : TextConst 'ENU=No hours present for this Document Number;SVE=Det finns inga timmar f”r det h„r dokumentnumret';
      Text007@1100528407 : TextConst 'ENU=Only < and > are allowed as parameter in function outline;SVE=Endast < och > till†ts som parametrar i funktionsutkastet';

    PROCEDURE SetSelection@10(DocumentNo@11012000 : Code[20]);
    BEGIN
      DocumentNumberCode := DocumentNo;
    END;

    PROCEDURE SetGlobalVars@1100528400(DocumentNumberCode2@1100528400 : Code[20];EmployerCde2@1100528401 : Code[20]);
    BEGIN
      DocumentNumberCode := DocumentNumberCode2;
      EmployerCde := EmployerCde2;
    END;

    PROCEDURE fill@4(String@11012001 : Text[80];TotLength@11012002 : Integer;WithCharacter@11012003 : Text[1];TrueBool@11012004 : Char) Text@11012000 : Text[80];
    VAR
      Length@11012005 : Integer;
    BEGIN
      Length:=STRLEN(String);
      IF TotLength>Length THEN BEGIN
        CASE TrueBool OF
          '<': String:=PADSTR('',TotLength-Length,WithCharacter)+String;
          '>': String:=String+PADSTR('',TotLength-Length,WithCharacter);
          ELSE ERROR(Text007);
        END;
      END;

      EXIT(String);
    END;

    PROCEDURE UpdateStatus@1(lvYear@11012001 : Integer;lvWeek@11012002 : Integer;lvEmpl@11012003 : Code[20];lvLine@11012004 : Integer);
    VAR
      lvHourRec@11012005 : Record 11012085;
    BEGIN
      lvHourRec.GET(lvYear, lvWeek, lvEmpl, lvLine);
      lvHourRec.Status := lvHourRec.Status::Export;
      lvHourRec.Exported := TRUE;
      lvHourRec."Exported on" := ExportDateTime;
      lvHourRec.MODIFY;
    END;

    LOCAL PROCEDURE DetermineCompanyNumber@1100528401(ReceivingCompany@1100528400 : Text[30]) : Code[3];
    VAR
      HumanResourcesSetup@1100528401 : Record 5218;
    BEGIN
      IF ReceivingCompany <> '' THEN
        HumanResourcesSetup.CHANGECOMPANY(ReceivingCompany);

      HumanResourcesSetup.GET;
      EXIT(HumanResourcesSetup."Receiving Company (Payroll)");
    END;

    BEGIN
    END.
  }
}

