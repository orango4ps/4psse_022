OBJECT Page 6086059 Our Cont. to Purch./Salespers
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.5.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Unser Kontakt mit EinkÑufer/VerkÑufer;
               ENU=Our Contact to Purchaser/Salesperson;
               NLD=Ons contact met Inkoper/verkoper];
    LinksAllowed=No;
    SourceTable=Table6085778;
    DelayedInsert=Yes;
    PageType=List;
    UsageCategory=Lists;
    OnOpenPage=BEGIN
                 TypeVisible := SpecificSourceNo = '';
                 EnabledForVendVisible := (SpecificSourceNo <> '') AND (SpecificSourceTableID = DATABASE::Vendor);
                 EnabledForCustVisible := (SpecificSourceNo <> '') AND (SpecificSourceTableID = DATABASE::Customer);
                 SourceIDEditable := SpecificSourceNo = '';
               END;

    OnAfterGetRecord=BEGIN
                       IF "Vendor/Customer No." = '' THEN
                         EnabledFor := EnabledFor::All
                       ELSE
                         EnabledFor := EnabledFor::Specific;
                     END;

    OnNewRecord=BEGIN
                  EnabledFor := EnabledFor::All;
                  IF (SpecificSourceNo <> '') AND (SpecificSourceTableID = DATABASE::Customer) THEN
                    Type := Type::Customer;
                END;

  }
  CONTROLS
  {
    { 1160040000;;Container;
                Name=Control1160040000;
                ContainerType=ContentArea }

    { 1160040001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1160040008;2;Field  ;
                Name=Type;
                SourceExpr=Type;
                Visible=TypeVisible }

    { 1160040002;2;Field  ;
                Name=Our Contact;
                SourceExpr="Our Contact" }

    { 1160040003;2;Field  ;
                Name=Salespers./Purch. Code;
                SourceExpr="Salespers./Purch. Code" }

    { 1160040004;2;Field  ;
                Name=EnabledFor1;
                CaptionML=[DEU=Aktiv fÅr;
                           ENU=Enabled for;
                           NLD=Actief voor];
                OptionCaptionML=[DEU=Alle Kreditoren,Nur dieser Kreditor;
                                 ENU=All vendors,This vendor only;
                                 NLD=Alle leverancies,Alleen deze leverancier];
                SourceExpr=EnabledFor;
                Visible=EnabledForVendVisible;
                OnValidate=VAR
                             OurContToPurchSalespers@1000 : Record 6085778;
                           BEGIN
                             IF EnabledFor = EnabledFor::All THEN
                               VALIDATE("Vendor/Customer No.",'')
                             ELSE
                               VALIDATE("Vendor/Customer No.",SpecificSourceNo);
                           END;
                            }

    { 1160040007;2;Field  ;
                Name=EnabledFor2;
                CaptionML=[DEU=Aktiv fÅr;
                           ENU=Enabled for;
                           NLD=Actief voor];
                OptionCaptionML=[DEU=Alle Kunden,Nur dieser Kunde;
                                 ENU=All customers,This customer only;
                                 NLD=Alle klanten,Alleen deze klant];
                SourceExpr=EnabledFor;
                Visible=EnabledForCustVisible;
                OnValidate=BEGIN
                             IF EnabledFor = EnabledFor::All THEN
                               VALIDATE("Vendor/Customer No.",'')
                             ELSE
                               VALIDATE("Vendor/Customer No.",SpecificSourceNo);
                           END;
                            }

    { 1160040005;2;Field  ;
                Name=Vendor/Customer No.;
                SourceExpr="Vendor/Customer No.";
                CaptionClass='3,' + GetSourceNoCaption;
                Editable=SourceIDEditable }

    { 1160040006;2;Field  ;
                Name=Control1160040006;
                SourceExpr=GetName;
                CaptionClass='3,' + GetSourceNameCaption }

  }
  CODE
  {
    VAR
      SpecificSourceTableID@1160040006 : Integer;
      SpecificSourceNo@1160040002 : Code[20];
      EnabledFor@1160040000 : 'All,Specific';
      TypeVisible@1160040013 : Boolean INDATASET;
      EnabledForVendVisible@1160040003 : Boolean INDATASET;
      EnabledForCustVisible@1160040005 : Boolean INDATASET;
      SourceIDEditable@1160040004 : Boolean INDATASET;
      Text001@1160040012 : TextConst 'DEU=Kreditorennr.;ENU=Vendor No.;NLD=Leveranciersnr.';
      Text002@1160040011 : TextConst 'DEU=Debitorennr.;ENU=Customer No.;NLD=Klantnr.';
      Text003@1160040010 : TextConst 'DEU=Kreditor-/Kundennr.;ENU=Vendor/Customer No.;NLD=Leverancier/klantnr.';
      Text004@1160040009 : TextConst 'DEU=Kreditorenname;ENU=Vendor Name;NLD=Leveranciersnaam';
      Text005@1160040008 : TextConst 'DEU=Debitor Name;ENU=Customer Name;NLD=Klantnaam';
      Text006@1160040007 : TextConst 'DEU=Kreditor-/Kundenname;ENU=Vendor/Customer Name;NLD=Leveranciers/klantnaam';

    PROCEDURE SetVars@1160040000(NewSpecificSourceTableID@1160040001 : Integer;NewSpecificSourceNo@1160040000 : Code[20]);
    BEGIN
      SpecificSourceTableID := NewSpecificSourceTableID;
      SpecificSourceNo := NewSpecificSourceNo;

      FILTERGROUP(4);
      IF SpecificSourceTableID = DATABASE::Vendor THEN
        SETRANGE(Type,Type::Vendor)
      ELSE
        SETRANGE(Type,Type::Customer);

      IF SpecificSourceNo <> '' THEN
        SETFILTER("Vendor/Customer No.",'%1|%2','',SpecificSourceNo);
      FILTERGROUP(0);
    END;

    PROCEDURE GetSourceNoCaption@1160040001() : Text[250];
    BEGIN
      CASE TRUE OF
        (SpecificSourceNo <> '') AND (SpecificSourceTableID = DATABASE::Vendor):
          EXIT(Text001);
        (SpecificSourceNo <> '') AND (SpecificSourceTableID = DATABASE::Customer):
          EXIT(Text002);
        ELSE
          EXIT(Text003);
      END;
    END;

    PROCEDURE GetSourceNameCaption@1160040002() : Text[250];
    BEGIN
      CASE TRUE OF
        (SpecificSourceNo <> '') AND (SpecificSourceTableID = DATABASE::Vendor):
          EXIT(Text004);
        (SpecificSourceNo <> '') AND (SpecificSourceTableID = DATABASE::Customer):
          EXIT(Text005);
        ELSE
          EXIT(Text006);
      END;
    END;

    BEGIN
    END.
  }
}

