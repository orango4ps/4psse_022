OBJECT Codeunit 11012101 Plant Order Picking Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PlantSetup@1100529000 : Record 11012550;
      Text001@1100529001 : TextConst 'DEU=%1 ist schon ''%2'' in %3 ''%4''. Sind Sie sicher?;ENU=%1 is already ''%2'' in %3 ''%4''. Are you sure?;NLD=%1 is al ''%2'' in %3 ''%4''. Weet u het zeker?;SVE=%1 „r redan ''%2'' i %3 ''%4''. Žr du s„ker?';
      Text002@1100529002 : TextConst 'DEU=Kein %1 gefunden in %2;ENU=No %1 found in %2;NLD=Geen %1 gevonden in %2;SVE=Ingen %1 funnen i %2';
      UserSetup@1100529003 : Record 91;
      Text003@1100529004 : TextConst 'DEU=%1 ist schon ''%2'' in %3 ''%4''. Sind Sie sicher?;ENU=%1 is already ''%2'' in %3 ''%4''. Are you sure?;NLD=%1 is al ''%2'' in %3 ''%4''. Weet u het zeker?;SVE=%1 „r redan ''%2'' i %3 ''%4''. Žr du s„ker?';
      Text004@1100529005 : TextConst 'DEU=Nicht zul„ssig, %1 ist %2 (%3);ENU=Not allowed, %1 is %2 (%3);NLD=Niet toegestaan, %1 is %2 (%3);SVE=Ej till†tet, %1 „r %2 (%3)';
      Text005@1100529007 : TextConst 'DEU=Kommissionierung beendet?;ENU=Orderpicken ready?;NLD=Orderpicken gereed?';
      Text006@1100529006 : TextConst 'DEU=%1 Zeilen mit Menge entsprechen nicht der angefragten Menge und/oder Werkzeugnr. nicht ausgefllt. \Kommissionierung beendet, sind Sie sicher?;ENU=%1 lines with quantity unequal to asked and/or plant no. not filled. \Orderpicking ready, are you sure?;NLD=%1 regels met aantal ongelijk aan gevraagd en/of materieelnr. niet gevuld. \Orderpicking gereed, weet u het zeker?';
      gNewOrderPicker@1100525000 : Code[20];

    PROCEDURE CheckOrderPickingAllowed@1100529001();
    BEGIN
      PlantSetup.GET;
      PlantSetup.TESTFIELD("Order Picking Procedure", TRUE);
      PlantSetup.TESTFIELD("Extended Picking Procedure", TRUE);
    END;

    PROCEDURE CheckUserAccessOrderPicking@1100529002();
    BEGIN
      UserSetup.GET(USERID);
      UserSetup.TESTFIELD("OP Filter Arrival Plant Orders");
      UserSetup.TESTFIELD("Employee No.");
    END;

    PROCEDURE UserIsOrderPicker@1100529006(ErrorMode@1100529000 : Boolean) : Boolean;
    BEGIN
      UserSetup.GET(USERID);
      IF ErrorMode THEN
        UserSetup.TESTFIELD("Plant Order Picker", TRUE);
      EXIT(UserSetup."Plant Order Picker");
    END;

    PROCEDURE SetOrderPickerFilterUserOPList@1100529003(VAR PlantOrder@1100529000 : Record 11012556);
    BEGIN
      UserSetup.GET(USERID);
      PlantOrder.FILTERGROUP(2);
      PlantOrder.SETFILTER("Order Picker", UserSetup."OP Filter Arrival Plant Orders");
      PlantOrder.FILTERGROUP(0);
    END;

    PROCEDURE ArrivalPlantOrderToOrderPicking@1100525012(PlantOrder@1100525002 : Record 11012556;OP_Mode@1100529000 : Integer);
    VAR
      DummyPlantOrderLine@1100529003 : Record 11012557;
      TmpEmpl@1100529001 : TEMPORARY Record 5200;
      NewOrderPicker@1100529002 : Code[20];
    BEGIN
      // OP-Mode: 1=To Plan for order picking, 2=Plan the order (assign order picker)
      CheckOrderPickingAllowed();
      PlantOrder.TESTFIELD("No.");
      PlantOrder.TESTFIELD(Type, PlantOrder.Type::Arrival);
      PlantOrder.TESTFIELD(Posted,FALSE);
      IF PlantOrder.Status >= PlantOrder.Status::Released THEN
        PlantOrder.FIELDERROR(Status);

      IF (PlantOrder."Order Picking Status" = PlantOrder."Order Picking Status"::"To Plan") OR
         (PlantOrder."Order Picking Status" = PlantOrder."Order Picking Status"::Planned) OR
         (PlantOrder."Order Picking Status" = PlantOrder."Order Picking Status"::"In Progress")
      THEN
        PlantOrder.FIELDERROR("Order Picking Status");
      IF PlantOrder."Order Picking Status" = PlantOrder."Order Picking Status"::Ready THEN BEGIN
        IF NOT CONFIRM(Text001, FALSE,
          PlantOrder.FIELDCAPTION("Order Picking Status"), PlantOrder."Order Picking Status",
          PlantOrder.TABLECAPTION, PlantOrder."No.")
        THEN
          EXIT;
      END;
      PlantOrder.TESTFIELD("Rental Free Order Type", 0);  // Should not exist/shown in not-posted PO's
      IF (OP_Mode = 2) AND (gNewOrderPicker <> '') THEN BEGIN
        NewOrderPicker := gNewOrderPicker;
      END ELSE BEGIN
        BuildOrderPickerEmployees(TmpEmpl);
        IF TmpEmpl.ISEMPTY THEN
          ERROR(Text002, UserSetup.FIELDCAPTION("Plant Order Picker"), UserSetup.TABLECAPTION);

        NewOrderPicker := '';
        IF OP_Mode = 2 THEN BEGIN
          IF TmpEmpl.COUNT = 1 THEN BEGIN
            TmpEmpl.FINDFIRST;
            NewOrderPicker := TmpEmpl."No.";
          END ELSE BEGIN
            IF NOT TmpEmpl.GET(PlantOrder."Order Picker") THEN
              TmpEmpl.FINDFIRST;
            IF PAGE.RUNMODAL(0, TmpEmpl) = ACTION::LookupOK THEN
              NewOrderPicker := TmpEmpl."No.";
          END;
          PlantOrder."Order Picker" := NewOrderPicker;
          PlantOrder.TESTFIELD("Order Picker");
          gNewOrderPicker := NewOrderPicker;
        END;
      END;

      PlantOrder.ExplodeSet(FALSE, FALSE, DummyPlantOrderLine);  // Sets and unique must be exploded because
      PlantOrder.Explode(FALSE, FALSE, DummyPlantOrderLine);     // orderpicker can only modify PlantNo and Qty.

      PlantOrder."Order Picker" := NewOrderPicker;
      IF PlantOrder."Order Picker" = '' THEN
        PlantOrder."Order Picking Status" := PlantOrder."Order Picking Status"::"To Plan"
      ELSE
        PlantOrder."Order Picking Status" := PlantOrder."Order Picking Status"::Planned;
      PlantOrder.MODIFY(FALSE);
    END;

    PROCEDURE ReOpenOrderPicking@1100525001(PlantOrder@1100525000 : Record 11012556);
    BEGIN
      CheckOrderPickingAllowed();
      PlantOrder.TESTFIELD("No.");
      PlantOrder.TESTFIELD(Type, PlantOrder.Type::Arrival);
      PlantOrder.TESTFIELD(Posted,FALSE);
      IF PlantOrder.Status >= PlantOrder.Status::Released THEN
        PlantOrder.FIELDERROR(Status);
      IF PlantOrder."Order Picking Status" = PlantOrder."Order Picking Status"::"In Progress" THEN
        PlantOrder.FIELDERROR("Order Picking Status");
      IF PlantOrder."Order Picking Status" = PlantOrder."Order Picking Status"::Ready THEN BEGIN
        IF NOT CONFIRM(Text003, FALSE,
          PlantOrder.FIELDCAPTION("Order Picking Status"), PlantOrder."Order Picking Status",
          PlantOrder.TABLECAPTION, PlantOrder."No.")
        THEN
          EXIT;
      END;

      PlantOrder."Order Picking Status" := PlantOrder."Order Picking Status"::Open;
      PlantOrder.MODIFY(FALSE);
    END;

    PROCEDURE TakeOrderPickingInProcess@1100529004(PlantOrder@1100529000 : Record 11012556);
    BEGIN
      CheckOrderPickingAllowed();
      UserIsOrderPicker(TRUE);
      CheckUserAccessOrderPicking();
      PlantOrder.TESTFIELD("No.");
      PlantOrder.TESTFIELD(Type, PlantOrder.Type::Arrival);
      PlantOrder.TESTFIELD(Posted,FALSE);
      IF PlantOrder.Status >= PlantOrder.Status::Released THEN
        PlantOrder.FIELDERROR(Status);

      IF PlantOrder."Order Picking Status" <> PlantOrder."Order Picking Status"::"In Progress" THEN BEGIN
        UserSetup.GET(USERID);
        IF (PlantOrder."Order Picker" <> '') AND (PlantOrder."Order Picker" <> UserSetup."Employee No.") THEN BEGIN
          PlantOrder.CALCFIELDS("Order Picker Name");
          ERROR(Text004, PlantOrder.FIELDCAPTION("Order Picker"), PlantOrder."Order Picker Name", PlantOrder."Order Picker");
        END;
        IF PlantOrder."Order Picker" = '' THEN
          PlantOrder."Order Picker" := UserSetup."Employee No.";
        PlantOrder."Order Picking Status" := PlantOrder."Order Picking Status"::"In Progress";
        PlantOrder.MODIFY(FALSE);
      END;
    END;

    PROCEDURE SetOrderPickingReady@1100525000(PlantOrder@1100525000 : Record 11012556) : Boolean;
    VAR
      PlantOrderLine@1100525002 : Record 11012557;
      Counter@1100525004 : Integer;
    BEGIN
      CheckOrderPickingAllowed();
      CheckUserAccessOrderPicking();
      UserSetup.GET(USERID);
      PlantOrder.TESTFIELD("Order Picker", UserSetup."Employee No.");

      PlantOrder.TESTFIELD("No.");
      PlantOrder.TESTFIELD(Type, PlantOrder.Type::Arrival);
      PlantOrder.TESTFIELD(Posted,FALSE);
      IF PlantOrder.Status >= PlantOrder.Status::Released THEN
        PlantOrder.FIELDERROR(Status);
      PlantOrder.TESTFIELD("Order Picking Status", PlantOrder."Order Picking Status"::"In Progress");

      PlantOrderLine.RESET;
      PlantOrderLine.SETRANGE("Plant Order No.", PlantOrder."No.");
      PlantOrderLine.SETFILTER(Type, '<>%1', PlantOrderLine.Type::Text);
      IF PlantOrderLine.FINDSET THEN BEGIN
        REPEAT
          IF PlantOrderLine.Quantity <> PlantOrderLine."Asked Quantity" THEN
            Counter := Counter + 1
          ELSE BEGIN
            IF PlantOrderLine.Type = PlantOrderLine.Type::Plant THEN BEGIN
              PlantOrderLine.CALCFIELDS(Bulk);
              IF (NOT PlantOrderLine.Bulk) AND (PlantOrderLine."No." = '') THEN
                Counter := Counter + 1
            END;
          END;
        UNTIL PlantOrderLine.NEXT = 0;
      END;
      IF GUIALLOWED THEN BEGIN
        IF Counter = 0 THEN BEGIN
          IF NOT CONFIRM(Text005, TRUE) THEN
            EXIT(FALSE);
        END ELSE BEGIN
          IF NOT CONFIRM(Text006, FALSE, Counter) THEN
            EXIT(FALSE);
        END;
      END;

      PlantOrder."Order Picking Status" := PlantOrder."Order Picking Status"::Ready;
      PlantOrder.MODIFY(FALSE);
      EXIT(TRUE);
    END;

    PROCEDURE BuildOrderPickerEmployees@1100529000(VAR TmpEmpl@1100529000 : TEMPORARY Record 5200);
    VAR
      UsersSetup@1100529001 : Record 91;
      Empl@1100529003 : Record 5200;
    BEGIN
      //NOTE: TmpEmpl must be Temporary and Empty in the calling process/function!
      UsersSetup.SETRANGE("Plant Order Picker", TRUE);
      UsersSetup.SETFILTER("Employee No.", '<>%1', '');
      IF UsersSetup.FINDSET THEN BEGIN
        REPEAT
          IF Empl.GET(UsersSetup."Employee No.") AND (NOT TmpEmpl.GET(UsersSetup."Employee No.")) THEN BEGIN
            TmpEmpl := Empl;
            TmpEmpl.INSERT;
          END;
        UNTIL UsersSetup.NEXT =0;
      END;
    END;

    BEGIN
    END.
  }
}

