OBJECT Table 12013609 EX Document Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF513000,4PS;
  }
  PROPERTIES
  {
    Permissions=TableData 12013608=rimd,
                TableData 12013609=rimd,
                TableData 12013610=rimd,
                TableData 12013611=rimd;
    OnInsert=BEGIN
               DocumentStatus(Rec,'New');

               "Change Date" := CREATEDATETIME(TODAY,TIME);
               "Changed By" := USERID;
               "Created Date" := CREATEDATETIME(TODAY,TIME);
               "Created By" := USERID;
             END;

    OnModify=BEGIN
               DocumentStatus(Rec,'Update');

               "Change Date" := CREATEDATETIME(TODAY,TIME);
               "Changed By" := USERID;
             END;

    OnDelete=VAR
               ExDoc@1100285000 : Record 12013608;
               TXT001@1100285002 : TextConst 'ENU=Connected lines can not be deleted.;SVE=Kan ej ta bort Kopplade rader.';
               TXT002@1100285001 : TextConst 'ENU=You can only remove new lines.;SVE=Kan endast ta bort nya rader.';
             BEGIN
               ExDoc.GET("Document ID");
               DeleteLine(0,ExDoc);

               DocumentStatus(Rec,'Delete');
             END;

    CaptionML=[ENU=Document Line;
               SVE=Dokumentrad];
  }
  FIELDS
  {
    { 2   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,,,Requisition,Receipt;
                                                                    SVE=Offert,Order,Faktura,Kreditnota,Avropsorder,Returorder,,,Rekvisition,Inleverans];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,,,Requisition,Receipt }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              SVE=Dokumentnr] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              SVE=Radnr] }
    { 10  ;   ;Flowstatus          ;Option        ;CaptionML=[ENU=ExFlow Status;
                                                              SVE=ExFlow status];
                                                   OptionCaptionML=[ENU='',Active,Approved,Rejected;
                                                                    SVE='',Aktiv,Attesterad,Avvisad];
                                                   OptionString='',Active,Approved,Rejected }
    { 11  ;   ;Line Type           ;Option        ;OnValidate=BEGIN
                                                                Changeable;
                                                                ConnectedToReceipt(FALSE);

                                                                IF NOT ExFlow.LineTypeAllowed("Line Type", TRUE) THEN
                                                                  ERROR(STRSUBSTNO(EXF49,"Line Type"));

                                                                IF ("Line Type" = 0) OR (xRec."Line Type" <> "Line Type") THEN BEGIN
                                                                  "No." := '';
                                                                  Description := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Line Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=" ,G/L Account,Item,,Fixed Asset,Charge (Item)";
                                                                    SVE=" ,Redov.konto,Artikel,,Anl.tillgÜng,Omkostnad (artikel)"];
                                                   OptionString=[ ,G/L Account,Item,,Fixed Asset,Charge (Item)] }
    { 12  ;   ;No.                 ;Code20        ;TableRelation=IF (Line Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Line Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Line Type=CONST(Item)) Item
                                                                 ELSE IF (Line Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Line Type=CONST("Charge (Item)")) "Item Charge";
                                                   OnValidate=BEGIN
                                                                Changeable;
                                                                ConnectedToReceipt(FALSE);

                                                                CreateDim(
                                                                  DimMgt.TypeToTableID3("Line Type"),"No.",
                                                                  DATABASE::Job,"Job No.",
                                                                  DATABASE::"Responsibility Center",'',
                                                                  DATABASE::"Work Center",'');
                                                              END;

                                                   ValidateTableRelation=Yes;
                                                   TestTableRelation=Yes;
                                                   CaptionML=[ENU=No.;
                                                              SVE=Nr] }
    { 13  ;   ;Description         ;Text100       ;OnValidate=BEGIN
                                                                Changeable;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
    { 14  ;   ;Quantity            ;Decimal       ;OnValidate=VAR
                                                                ExDoc@1100285000 : Record 12013608;
                                                              BEGIN
                                                                Changeable;
                                                                ConnectedToReceipt(TRUE);

                                                                IF (CurrFieldNo = FIELDNO(Quantity)) AND ("Connected Line ID" <> 0) THEN BEGIN
                                                                  IF ABS(Quantity) > ABS(xRec.Quantity) THEN
                                                                    ERROR(EXF53);

                                                                  IF Quantity > 0 THEN
                                                                    Quantity := -Quantity;
                                                                END;

                                                                CalcAmount;

                                                                IF CurrFieldNo = FIELDNO(Quantity) THEN BEGIN
                                                                  ExDoc.GET("Document ID");
                                                                  UndoReceipt(FALSE,ExDoc);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5 }
    { 15  ;   ;Direct Unit Cost    ;Decimal       ;OnValidate=BEGIN
                                                                Changeable;
                                                                CalcAmount;
                                                              END;

                                                   CaptionML=[ENU=Direct Unit Cost;
                                                              SVE=Inkîpspris];
                                                   DecimalPlaces=2:5 }
    { 16  ;   ;Line Discount %     ;Decimal       ;InitValue=0;
                                                   OnValidate=BEGIN
                                                                Changeable;
                                                                CalcAmount;
                                                              END;

                                                   CaptionML=[ENU=Line Discount %;
                                                              SVE=Radrabatt %];
                                                   DecimalPlaces=2:5 }
    { 18  ;   ;Line Difference     ;Boolean       ;InitValue=No;
                                                   CaptionML=[ENU=Line Difference;
                                                              SVE=Raddifferens] }
    { 20  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   OnValidate=BEGIN
                                                                Changeable;

                                                                CreateDim(
                                                                  DATABASE::Job,"Job No.",
                                                                  DimMgt.TypeToTableID3("Line Type"),"No.",
                                                                  DATABASE::"Responsibility Center",'',
                                                                  DATABASE::"Work Center",'');
                                                              END;

                                                   CaptionML=[ENU=Job No.;
                                                              SVE=Projektnr] }
    { 21  ;   ;Amount              ;Decimal       ;CaptionML=[ENU=Amount;
                                                              SVE=Belopp];
                                                   DecimalPlaces=2:5 }
    { 25  ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   OnValidate=VAR
                                                                JobTaskDimension@1100285000 : Record 11020400;
                                                                ExDocLineDim@1100285001 : Record 12013611;
                                                                TempColumnID@1100285002 : Integer;
                                                              BEGIN
                                                                Changeable;

                                                                JobTaskDimension.RESET;
                                                                JobTaskDimension.SETRANGE("Job No.", "Job No.");
                                                                JobTaskDimension.SETRANGE("Job Task No.", "Job Task No.");
                                                                IF JobTaskDimension.FINDSET THEN BEGIN
                                                                  ExDocLineDim.SETCURRENTKEY("Document Line ID");
                                                                  ExDocLineDim.SETRANGE("Document Line ID", Rec.ID);
                                                                  REPEAT
                                                                    TempColumnID := ExFlow.GetColumnID(JobTaskDimension."Dimension Code");
                                                                    IF TempColumnID <> 0 THEN BEGIN
                                                                      ExDocLineDim.SETRANGE("Dimension Code", JobTaskDimension."Dimension Code");
                                                                      IF ExDocLineDim.FIND('-') THEN BEGIN
                                                                        ExDocLineDim."Dimension Value" := JobTaskDimension."Dimension Value Code";
                                                                        ExDocLineDim.MODIFY;
                                                                      END ELSE BEGIN
                                                                        ExDocLineDim.INIT;
                                                                        ExDocLineDim.ID := 0;
                                                                        ExDocLineDim."Document Type" := Rec."Document Type";
                                                                        ExDocLineDim."Document No." := Rec."Document No.";
                                                                        ExDocLineDim."Line No." := Rec."Line No.";
                                                                        ExDocLineDim."Dimension Code" := JobTaskDimension."Dimension Code";
                                                                        ExDocLineDim."Dimension Value" := JobTaskDimension."Dimension Value Code";
                                                                        ExDocLineDim."Column ID" := TempColumnID;
                                                                        ExDocLineDim."Document Line ID" := Rec.ID;
                                                                        ExDocLineDim."Document ID" := Rec."Document ID";
                                                                        ExDocLineDim."Changed By" := USERID;
                                                                        ExDocLineDim."Change Date" := CREATEDATETIME(TODAY,TIME);
                                                                        ExDocLineDim.INSERT;
                                                                      END;
                                                                    END;
                                                                  UNTIL JobTaskDimension.NEXT = 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Job Task No.;
                                                              SVE=Projektaktivitetsnr] }
    { 30  ;   ;New Line            ;Boolean       ;CaptionML=[ENU=New Line;
                                                              SVE=Ny rad] }
    { 31  ;   ;Finish Comment      ;Text120       ;CaptionML=[ENU=Comments;
                                                              SVE=Kommentar] }
    { 32  ;   ;Finish Code         ;Option        ;CaptionML=[ENU=Finish Code;
                                                              SVE=Avslutskod];
                                                   OptionCaptionML=[ENU=Complete Invoice line,Complete all Invoice lines;
                                                                    SVE=Slutattestera aktuell fakturarad,Slutattestera alla fakturarader];
                                                   OptionString=Complete Invoice line,Complete all Invoice lines }
    { 33  ;   ;Unit of Measure     ;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(No.));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              SVE=Enhet] }
    { 71  ;   ;Requisition Archive Doc ID;BigInteger;
                                                   CaptionML=[ENU=Requisition Archive ID;
                                                              SVE=Rekvistionsarkivsid];
                                                   Editable=No }
    { 72  ;   ;Requisition Archive Line ID;BigInteger;
                                                   CaptionML=[ENU=Requisition Archive Line ID;
                                                              SVE=Rekvistionsarkivsradid];
                                                   Editable=No }
    { 90  ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              SVE=Moms produktbokfîringsmall>] }
    { 100 ;   ;Periodic Template Code;Code10      ;TableRelation="Deferral Template";
                                                   OnValidate=BEGIN
                                                                Changeable;
                                                              END;

                                                   CaptionML=[ENU=Deferral Code;
                                                              SVE=Periodiseringskod] }
    { 101 ;   ;Periodic Starting Date;Date        ;OnValidate=BEGIN
                                                                Changeable;
                                                              END;

                                                   CaptionML=[ENU=Deferral Start Date;
                                                              SVE=Periodiseringsstartdatum] }
    { 110 ;   ;Base Starting Date  ;Date          ;CaptionML=[ENU=Base Starting Date;
                                                              SVE=Base startdatum] }
    { 111 ;   ;Base Temporary Account No.;Text20  ;InitValue=<Empty>;
                                                   TableRelation="G/L Account";
                                                   CaptionML=[ENU=Base Temporary Account No.;
                                                              SVE=Base Interimskonto] }
    { 112 ;   ;Base No. of Periods ;Integer       ;InitValue=0;
                                                   CaptionML=[ENU=Base No. of Periods;
                                                              SVE=Base Antal perioder] }
    { 113 ;   ;Document ID         ;BigInteger    ;CaptionML=[ENU=Document ID;
                                                              SVE=Dokument-ID] }
    { 114 ;   ;ID                  ;BigInteger    ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              SVE=ID] }
    { 115 ;   ;Connected Line ID   ;BigInteger    ;CaptionML=[ENU=Connected Line ID;
                                                              SVE=Kopplad till rad-ID] }
    { 116 ;   ;Order No.           ;Code20        ;OnLookup=VAR
                                                              PurchHeader@1100285000 : Record 38;
                                                            BEGIN
                                                              IF "Order No." <> '' THEN
                                                                IF PurchHeader.GET(PurchHeader."Document Type"::Order,"Order No.") THEN
                                                                  PAGE.RUN(0,PurchHeader)
                                                                ELSE IF PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Order No.") THEN
                                                                  PAGE.RUN(0,PurchHeader);
                                                            END;

                                                   CaptionML=[ENU=Order No.;
                                                              SVE=Ordernr];
                                                   Editable=No }
    { 117 ;   ;Order Unit Cost     ;Decimal       ;CaptionML=[ENU=Order Unit Cost;
                                                              SVE=Orderns inkîpspris];
                                                   DecimalPlaces=2:5 }
    { 118 ;   ;Order Line No.      ;Integer       ;OnLookup=VAR
                                                              PurchLine2@1100285000 : Record 39;
                                                            BEGIN
                                                              IF "Order No." <> '' THEN BEGIN
                                                                PurchLine2.RESET;
                                                                CASE "Document Type" OF
                                                                  "Document Type"::Invoice:
                                                                    PurchLine2.SETRANGE("Document Type", PurchLine2."Document Type"::Order);
                                                                  "Document Type"::"Credit Memo":
                                                                    PurchLine2.SETRANGE("Document Type", PurchLine2."Document Type"::"Return Order");
                                                                END;

                                                                PurchLine2.SETRANGE("Document No.", "Order No.");
                                                                IF "Order Line No." <> 0 THEN BEGIN
                                                                  PurchLine2.SETRANGE("Line No.","Order Line No.");
                                                                  IF PurchLine2.FINDFIRST THEN;
                                                                  PurchLine2.SETRANGE("Line No.");
                                                                END;

                                                                PAGE.RUNMODAL(0,PurchLine2);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Order Line No.;
                                                              SVE=Orderradnr];
                                                   Editable=No }
    { 120 ;   ;Split from Parent ID;BigInteger    ;CaptionML=[ENU=Split from Parent ID;
                                                              SVE=Delad frÜn rad ID];
                                                   Editable=No }
    { 500 ;   ;Requisition No.     ;Code20        ;CaptionML=[ENU=Requisition No.;
                                                              SVE=Rekvisitionsnr] }
    { 501 ;   ;Error Message       ;Text250       ;CaptionML=[ENU=Error Message;
                                                              SVE=Felmeddelande] }
    { 600 ;   ;Changed By          ;Code50        ;CaptionML=[ENU=Changed By;
                                                              SVE=éndrad av];
                                                   Editable=No }
    { 601 ;   ;Change Date         ;DateTime      ;CaptionML=[ENU=Change Date;
                                                              SVE=éndrad datum];
                                                   Editable=No }
    { 602 ;   ;Last Change Date Approval;DateTime ;FieldClass=FlowField;
                                                   CalcFormula=Max("EX Document Line Approver"."Change Date" WHERE (Document Line ID=FIELD(ID)));
                                                   CaptionML=[ENU=Last Changed Date Approval;
                                                              SVE=Senast Ñndrad Attestflîde];
                                                   Editable=No }
    { 603 ;   ;Last Change Date Dimension;DateTime;FieldClass=FlowField;
                                                   CalcFormula=Max("EX Document Line Dimension"."Change Date" WHERE (Document Line ID=FIELD(ID)));
                                                   CaptionML=[ENU=Last Changed Date Dimension;
                                                              SVE=Senast Ñndrad Dimensioner];
                                                   Editable=No }
    { 650 ;   ;Created By          ;Code50        ;CaptionML=[ENU=Created By;
                                                              SVE=Skapad av];
                                                   Editable=No }
    { 651 ;   ;Created Date        ;DateTime      ;CaptionML=[ENU=Created Date;
                                                              SVE=Skapad datum];
                                                   Editable=No }
    { 11012800;;Service Order No.  ;Code20        ;TableRelation="Service Order".No.;
                                                   CaptionML=[ENU=Service Order No.;
                                                              SVE=Serviceordernr];
                                                   Description=4PS;
                                                   Editable=No }
    { 12013603;;First Approver     ;Code50        ;OnValidate=VAR
                                                                TempApproverName@1100285000 : Text[50];
                                                              BEGIN
                                                                "First Approver" := ExFlow.FindUserGroup("First Approver",TempApproverName,FALSE);
                                                              END;

                                                   OnLookup=VAR
                                                              TempApprover@1100285001 : Code[50];
                                                              TempApproverName@1100285000 : Text[50];
                                                            BEGIN
                                                              TempApprover := ExFlow.LookUpUserGroup("First Approver",TempApproverName);
                                                              IF TempApprover <> "First Approver" THEN
                                                                VALIDATE("First Approver", TempApprover);
                                                            END;

                                                   CaptionML=[ENU=First Approver;
                                                              SVE=Fîrste attestant];
                                                   Description=ExFlow }
    { 12013611;;Invoice to Customer;Boolean       ;CaptionML=[ENU=Invoice to Customer;
                                                              SVE=Vidarefakturera till kund];
                                                   Description=ExFlow }
    { 12013612;;Invoicing Amount   ;Decimal       ;CaptionML=[ENU=Invoicing Amount;
                                                              SVE=Belopp att vidarefakturera];
                                                   Description=ExFlow }
    { 12013613;;Start Date for Invoicing;Date     ;CaptionML=[ENU=Start Date for Invoicing;
                                                              SVE=Startdatum fîr fakturering];
                                                   Description=ExFlow }
    { 12013614;;Invoicing Code     ;Code10        ;TableRelation="EX Invoicing Code";
                                                   CaptionML=[ENU=Invoicing Code;
                                                              SVE=Vidarefakturingskod];
                                                   Description=ExFlow }
    { 12013659;;Applied Template Rule;Code20      ;TableRelation="Template Rule".Code;
                                                   CaptionML=[ENU=Applied Template Rule;
                                                              SVE=TillÑmpad attestregel];
                                                   Description=ExFlow;
                                                   Editable=No }
    { 12013660;;Template Rule      ;Code20        ;TableRelation="Template Rule".Code;
                                                   CaptionML=[ENU=Template Rule;
                                                              SVE=Attestregel];
                                                   Description=ExFlow }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Document No.                             }
    {    ;Document No.,Line No.                    }
    {    ;Flowstatus,Document Type                 }
    {    ;Document Type,Document No.,Line No.      }
    {    ;Document ID                              }
    {    ;Document ID,Flowstatus                   }
    {    ;Connected Line ID                        }
    {    ;Order No.                               ;SumIndexFields=Order Unit Cost }
    {    ;No.                                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ExFlow@1100285002 : Codeunit 12013601;
      EXF55@1100285012 : TextConst 'ENU=Connected lines can not be deleted.;SVE=Kan ej ta bort Kopplade rader.';
      EXF56@1100285009 : TextConst 'ENU=You can only remove new lines.;SVE=Kan endast ta bort nya rader.';
      EXF03@1100285005 : TextConst 'ENU=Approvers copied to %1 line/ lines;SVE=Attestanter kopierade till %1 rad/rader';
      EXF05@1100285003 : TextConst 'ENU=,Restart from marked Approver,Restart line,Restart all lines,Restart from marked Approver for all lines;SVE=,èterstarta frÜn markerad attestant,èterstarta raden,èterstarta alla rader,èterstarta frÜn markerad attestant fîr alla rader';
      EXF06@1100285010 : TextConst 'ENU=The Document %1 is on hold!;SVE=Dokumentet %1 Ñr stoppat!';
      EXF08@1100285014 : TextConst 'ENU=The Line is approved!;SVE=Raden Ñr attesterad!';
      EXF09@1100285015 : TextConst 'ENU=The Line is on hold!;SVE=Raden Ñr pÜ bevakning!';
      EXF10@1100285016 : TextConst 'ENU=The line is connected to a Receipt line!;SVE=Raden Ñr kopplad till en inleveransrad!';
      EXF11@1100285017 : TextConst 'ENU="Max. Quantity =  %1!";SVE=Antal kan maximalt vara %1!';
      EXF13@1100285019 : TextConst 'ENU="Quote and Order can not be changed here!  ";SVE=Offert och order mÜste Ñndras via utgÜngen Kort!';
      EXF41@1100285000 : TextConst 'ENU=Warning! The line has been approved!\\Do you still want to update line?;SVE=Varning! Raden har attesterats! \\Vill du ÑndÜ uppdatera raden?';
      EXF42@1100285023 : TextConst 'ENU=The Line is Ready for Posting and needs to be restarted before it can be changed!;SVE=Raden Ñr klar fîr bokfîring och behîver Üterstartas fîr att kunna Ñndras!';
      DimMgt@1100285008 : Codeunit 408;
      GLSetupShortcutDimCode@1100285024 : ARRAY [8] OF Code[20];
      HasGotGLSetup@1100285022 : Boolean;
      DoByPassControl@1100285025 : Boolean;
      EXF45@1100285027 : TextConst 'ENU=ExFlow do not use Dimension %1!;SVE=Dimensionen %1 anvÑnds inte av ExFlow!';
      EXF47@1100285011 : TextConst 'ENU=The status of the document cannot be Invoiced.;SVE=Statusen pÜ dokumentet kan inte vara Fakturerad.';
      EXF48@1100285026 : TextConst 'ENU=Operation aborted.;SVE=Funktionen avbryten.';
      EXF49@1100285028 : TextConst 'ENU=Line Type %1 is not setup for ExFlow.;SVE=Radtyp %1 Ñr inte uppsatt fîr ExFlow.';
      EXF50@1100285004 : TextConst 'ENU=Restarting line:  #1###############;SVE=èterstartar raden: #1###############';
      Number@1100285001 : Integer;
      EXF51@1100285013 : TextConst 'ENU=Finishing line:  #1###############;SVE=Slutattesterar raden: #1###############';
      EXF52@1100285018 : TextConst 'ENU=Quantity received cannot be changed to a lesser value than what is already invoiced.;SVE=Inlevererat antal kan inte Ñndras till ett mindre antal Ñn vad som Ñr redan fakturerat.';
      EXF53@1100285006 : TextConst 'ENU=Quantity received cannot be increased, only decreased.;SVE=Inlevererat antal kan ej îkas, bara sÑnkas.';
      EXF54@1100285007 : TextConst 'ENU=Getting documents   #1#########;SVE=HÑmtar dokument   #1#########';

    PROCEDURE ShowNextApprover@1000000000(ShowCurrentApprover@1100285003 : Boolean);
    VAR
      DocHead@1100285009 : Record 12013608;
      DocLine@1100285001 : Record 12013609;
      DocLineApp@1100285000 : Record 12013610;
      DocLineApp2@1100285008 : Record 12013610;
      DocLineApp3@1100285006 : Record 12013610;
      TempDocLineApp@1100285002 : TEMPORARY Record 12013610;
      PurchHeader@1100285005 : Record 38;
      TempDate@1100285010 : Date;
      PerApprover@1100285004 : Page 12013620;
      Window@1100285007 : Dialog;
    BEGIN
      TempDocLineApp.RESET;
      TempDocLineApp.DELETEALL;

      IF GUIALLOWED THEN
        Window.OPEN(EXF54);

      DocLineApp.RESET;
      DocLineApp.SETCURRENTKEY(Flowstatus,"Document ID");
      DocLineApp.SETRANGE(Flowstatus, DocLineApp.Flowstatus::Current);
      IF DocLineApp.FINDSET THEN BEGIN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(1, DocLineApp."Document ID");

          DocLineApp3 := DocLineApp;

          DocHead.GET(DocLineApp3."Document ID");

          IF NOT PurchHeader.GET(DocLineApp."Document Type",DocLineApp."Document No.") THEN BEGIN
            PurchHeader.INIT;
            PurchHeader."Due Date" := DocHead."Due Date";
            PurchHeader."Document Date" := DocHead."Document Date";
          END;

          DocLine.GET(DocLineApp."Document Line ID");

          DocLineApp3."Due Date" := PurchHeader."Due Date";
          DocLineApp3."Document Date" := PurchHeader."Document Date";
          DocLineApp3."No." := DocLine."No.";
          IF DocLine.Description = '' THEN
            DocLineApp3.Description := DocLine.GetName
          ELSE
            DocLineApp3.Description := DocLine.Description;
          DocLineApp3."Line Amount" := DocLine.Amount;

          IF PurchHeader."Due Date" <> 0D THEN
            DocLineApp3."Days to Due Date" := PurchHeader."Due Date" - WORKDATE
          ELSE
            DocLineApp3."Days to Due Date" := 0;

          DocLineApp2.RESET;
          DocLineApp2.SETCURRENTKEY("Document Type","Document No.","Line No.","Approver Order");
          DocLineApp2.SETRANGE("Document Type",DocLineApp."Document Type");
          DocLineApp2.SETRANGE("Document No.",DocLineApp."Document No.");
          DocLineApp2.SETRANGE("Line No.",DocLineApp."Line No.");
          DocLineApp2.SETFILTER("Approved By",'<>%1','');
          IF DocLineApp2.FINDLAST THEN BEGIN
            TempDate := DT2DATE(DocLineApp2."Approval Date");
            DocLineApp3."Processing Days" := WORKDATE - TempDate;
          END ELSE IF DocHead."Create Date" <> 0D THEN
            DocLineApp3."Processing Days" := WORKDATE - DocHead."Create Date";

          IF ShowCurrentApprover THEN BEGIN
            TempDocLineApp.INIT;
            TempDocLineApp.TRANSFERFIELDS(DocLineApp3);
            TempDocLineApp.INSERT;
          END ELSE BEGIN // Show next approver
            DocLineApp2.RESET;
            DocLineApp2.SETCURRENTKEY("Document Type","Document No.","Line No.","Approver Order");
            DocLineApp2.SETRANGE("Document Type",DocLineApp."Document Type");
            DocLineApp2.SETRANGE("Document No.",DocLineApp."Document No.");
            DocLineApp2.SETRANGE("Line No.",DocLineApp."Line No.");
            DocLineApp2.SETFILTER(Approver, '<>%1', DocLineApp.Approver);
            DocLineApp2.SETRANGE(Flowstatus,DocLineApp.Flowstatus::"Not processed");
            IF DocLineApp2.FINDFIRST THEN BEGIN
              TempDocLineApp.INIT;
              TempDocLineApp.TRANSFERFIELDS(DocLineApp3);
              TempDocLineApp.Approver := DocLineApp2.Approver;
              TempDocLineApp.INSERT;
            END;
          END;
        UNTIL DocLineApp.NEXT = 0;

        IF GUIALLOWED THEN
          Window.CLOSE;

        PerApprover.InitTempTable(TempDocLineApp);
        PerApprover.RUN;
      END;
    END;

    PROCEDURE CopyApprover@1000000003(DocLineIn@1100285001 : Record 12013609);
    VAR
      DocLine@1000000000 : Record 12013609;
      PurchLine@1100285002 : Record 39;
      DocLineApp@1100285000 : Record 12013610;
    BEGIN
      DocLineApp.IsUserAllowedToChange(0);
      DocLineIn.TESTFIELD("Connected Line ID",0);
      Number := 0;

      DocLine.RESET;
      DocLine.SETRANGE("Document ID",DocLineIn."Document ID");
      DocLine.SETFILTER("Line No.",'<>%1',DocLineIn."Line No.");
      DocLine.SETRANGE("Connected Line ID",0);
      IF DocLine.FINDSET(TRUE) THEN
        REPEAT
          IF NOT DocLine."New Line" THEN BEGIN
            PurchLine.RESET;
            PurchLine.SETRANGE("Document Type",DocLine."Document Type");
            PurchLine.SETRANGE("Document No.",DocLine."Document No.");
            PurchLine.SETRANGE("Line No.",DocLine."Line No.");
            PurchLine.SETRANGE(Approve,TRUE);
            IF PurchLine.FINDFIRST THEN BEGIN
              PurchLine."Approval Flow Modified" := TRUE;
              PurchLine.MODIFY;
            END;
          END;
          UpdateApp(DocLine,DocLineIn);
        UNTIL DocLine.NEXT = 0;

      MESSAGE(EXF03,Number);
    END;

    PROCEDURE Approved@1100285002(DocLine@1100285000 : Record 12013609) : Boolean;
    VAR
      DocLineApp@1100285001 : Record 12013610;
    BEGIN
      DocLineApp.RESET;
      DocLineApp.SETCURRENTKEY("Document Line ID","Approver Order");
      DocLineApp.SETRANGE("Document Line ID",DocLine.ID);
      DocLineApp.SETFILTER("Approved By",'<>%1','');
      IF DocLineApp.FINDFIRST THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    LOCAL PROCEDURE UpdateApp@1000000006(ToDocLine@1000000000 : Record 12013609;FromDocLine@1100285001 : Record 12013609);
    VAR
      DocLineApp@1100285002 : Record 12013610;
      DocLineApp2@1100285003 : Record 12013610;
    BEGIN
      Number := Number + 1;

      // Remove line Approvers
      DocLineApp.RESET;
      DocLineApp.SETCURRENTKEY("Document Line ID");
      DocLineApp.SETRANGE("Document Line ID",ToDocLine.ID);
      DocLineApp.SETFILTER(Flowstatus, '<>%1&<>%2', DocLineApp.Flowstatus::Rejected,DocLineApp.Flowstatus::Approved);
      DocLineApp.DELETEALL;

      // Read org. line and make copies
      DocLineApp.RESET;
      DocLineApp.SETCURRENTKEY("Document Line ID");
      DocLineApp.SETRANGE("Document Line ID",FromDocLine.ID);
      DocLineApp.SETFILTER(Flowstatus, '<>%1&<>%2', DocLineApp.Flowstatus::Rejected,DocLineApp.Flowstatus::Approved);
      IF DocLineApp.FINDSET(TRUE) THEN BEGIN
        REPEAT
          DocLineApp2.INIT;
          DocLineApp2 := DocLineApp;

          DocLineApp2."Line No." := ToDocLine."Line No.";
          DocLineApp2.Comment := '';
          DocLineApp2."Approval Date" := 0DT;
          DocLineApp2."Approved By" := '';
          DocLineApp2."Reminder Sent Date" := 0DT;
          DocLineApp2."Reminder Status" := 0;
          DocLineApp2."No. of Reminders" := 0;
          DocLineApp2.ID := 0;
          DocLineApp2.Flowstatus := 0;
          DocLineApp2."Document Line ID" := ToDocLine.ID;
          DocLineApp2."Document ID" := ToDocLine."Document ID";
          DocLineApp2.INSERT;
        UNTIL DocLineApp.NEXT = 0;

        DocLineApp2.RESET;
        DocLineApp2.SETCURRENTKEY("Document Line ID");
        DocLineApp2.SETRANGE("Document Line ID",ToDocLine.ID);
        DocLineApp2.FINDFIRST;
        DocLineApp2.ApprovalStatus(DocLineApp2,'Update');
        DocLineApp2.MODIFY;
      END;
    END;

    PROCEDURE FinishApprover@1100285000(DocLineIn@1100285004 : Record 12013609);
    VAR
      FinishApprovalOption@1100285002 : Report 12013635;
    BEGIN
      FinishApprovalOption.SetDocLine(DocLineIn);
      FinishApprovalOption.RUNMODAL;
    END;

    PROCEDURE FinishApproverUpdate@1100285019(DocLineIn@1100285005 : Record 12013609;FinishComment@1100285004 : Text[250];FinishCode@1100285003 : Integer);
    VAR
      DocLine@1100285002 : Record 12013609;
      DocHead@1100285000 : Record 12013608;
      Window@1100285001 : Dialog;
    BEGIN
      CASE FinishCode OF
        0 : BEGIN
              ExFlow.FinishApprover(DocLineIn,FinishComment);
              SetDocumentStatus(DocLineIn);
            END;

        1 : BEGIN
              Window.OPEN(EXF51);

              DocLine.RESET;
              DocLine.SETCURRENTKEY("Document ID");
              DocLine.SETRANGE("Document ID",DocLineIn."Document ID");
              IF DocLine.FINDSET(TRUE) THEN BEGIN
                REPEAT
                  Window.UPDATE(1,DocLine."Line No.");

                  ExFlow.FinishApprover(DocLine,FinishComment);
                UNTIL DocLine.NEXT = 0;

                SetDocumentStatus(DocLine);
              END;

              DocHead.GET(DocLine."Document ID");
              DocHead.Status := DocHead.Status::Approved;
              DocHead.MODIFY(TRUE);
            END;
      END;
    END;

    PROCEDURE RestartApprover@1100285003(DocLineIn@1100285000 : Record 12013609;WorkingLine@1100285004 : Integer);
    VAR
      Restart@1100285002 : Integer;
      DocHead@1100285001 : Record 12013608;
      DocLine@1100285003 : Record 12013609;
      Window@1100285005 : Dialog;
    BEGIN
      Restart := STRMENU(EXF05);
      IF Restart = 0 THEN
        ERROR('');  //2476

      DocLine.GET(DocLineIn.ID);

      CASE Restart OF
        1 : ERROR(''); // Obsolete option
        2 : ExFlow.RestartApprover(DocLine,WorkingLine,FALSE);
        3 : ExFlow.RestartApprover(DocLine,0,FALSE);
        4 : BEGIN
              Window.OPEN(EXF50);

              DocLine.RESET;
              DocLine.SETCURRENTKEY("Document ID");
              DocLine.SETRANGE("Document ID", DocLineIn."Document ID");
              IF DocLine.FINDSET THEN
                REPEAT
                  Window.UPDATE(1,DocLine."Line No.");

                  ExFlow.RestartApprover(DocLine,0,FALSE);
                UNTIL DocLine.NEXT = 0;

              Window.CLOSE;
            END;
        5 : BEGIN
              Window.OPEN(EXF50);

              DocLine.RESET;
              DocLine.SETCURRENTKEY("Document ID");
              DocLine.SETRANGE("Document ID", DocLineIn."Document ID");
              IF DocLine.FINDSET THEN
                REPEAT
                  Window.UPDATE(1,DocLine."Line No.");

                  ExFlow.RestartApprover(DocLine,WorkingLine,FALSE);
                UNTIL DocLine.NEXT = 0;

              Window.CLOSE;
            END;
      END;

      IF DocLine.Flowstatus = DocLine.Flowstatus::Rejected THEN BEGIN
        DocLine.GET(DocLine.ID);
        DocLine.Flowstatus := DocLine.Flowstatus::Active;
        DocLine.MODIFY(FALSE);
      END;

      DocHead.GET(DocLine."Document ID");
      IF DocHead.Status = DocHead.Status::"Ready for posting" THEN BEGIN
        DocHead.Status := DocHead.Status::Active;
        DocHead."Error Message" := '';
        DocHead.MODIFY(TRUE);
      END ELSE BEGIN
        DocHead."Error Message" := '';
        DocHead.MODIFY(TRUE);
      END;
    END;

    PROCEDURE DocumentStatus@1100285005(VAR DocLine@1100285000 : Record 12013609;Action@1100285001 : Text[10]);
    VAR
      DocLine2@1100285002 : Record 12013609;
      DocHead@1100285004 : Record 12013608;
      PurchHeader@1100285012 : Record 38;
      NoOfLines@1100285011 : Integer;
      NoOfZero@1100285010 : Integer;
      NoOfApproved@1100285009 : Integer;
      NoOfActive@1100285008 : Integer;
    BEGIN
      IF NOT DocHead.GET(DocLine."Document ID") THEN
        EXIT;

      DocHead.CALCFIELDS("Hold by Approver");
      IF DocHead."Hold by Approver" THEN
        ERROR(STRSUBSTNO(EXF06,DocHead."Document No."));

      IF (DocHead.Status = DocHead.Status::Inactive) AND (DocHead."Document Type" = DocHead."Document Type"::Order) THEN
        EXIT;

      IF DocHead."Document Type" IN [DocHead."Document Type"::Quote,
                                     DocHead."Document Type"::Order] THEN BEGIN
        PurchHeader.GET(DocHead."Document Type",DocHead."Document No.");
        IF PurchHeader.Status = PurchHeader.Status::Open THEN
          EXIT;
      END;

      IF (DocHead.Status >= DocHead.Status::"On hold") THEN
        EXIT;

      DocLine2.RESET;
      DocLine2.SETCURRENTKEY("Document ID",Flowstatus);
      DocLine2.SETRANGE("Document ID", DocLine."Document ID");
      NoOfLines := DocLine2.COUNT;
      DocLine2.SETRANGE(Flowstatus, 0);
      NoOfZero := DocLine2.COUNT;
      DocLine2.SETRANGE(Flowstatus, DocLine2.Flowstatus::Approved);
      NoOfApproved := DocLine2.COUNT;
      DocLine2.SETRANGE(Flowstatus, DocLine2.Flowstatus::Active);
      NoOfActive := DocLine2.COUNT;

      IF Action = 'Delete' THEN BEGIN
        NoOfLines := NoOfLines - 1;
        IF Flowstatus = 0 THEN
          NoOfZero := NoOfZero - 1
        ELSE IF Flowstatus = Flowstatus::Approved THEN
          NoOfApproved := NoOfApproved - 1
        ELSE IF Flowstatus = Flowstatus::Active THEN
          NoOfActive := NoOfActive - 1;
      END ELSE IF Action = 'New' THEN BEGIN
        NoOfLines := NoOfLines + 1;
        IF Flowstatus = 0 THEN
          NoOfZero := NoOfZero + 1
        ELSE IF Flowstatus = Flowstatus::Approved THEN
          NoOfApproved := NoOfApproved + 1
        ELSE IF Flowstatus = Flowstatus::Active THEN
          NoOfActive := NoOfActive + 1;
      END;

      IF (NoOfLines = 0) OR (NoOfLines = NoOfZero) OR (NoOfZero <> 0) THEN BEGIN
        DocHead.Status := DocHead.Status::Inactive;
        DocHead."Change Jnl Processed PO" := FALSE;
        DocHead.MODIFY(TRUE);

        EXIT;
      END;

      IF NoOfLines = NoOfApproved THEN BEGIN
        IF DocHead.Status <> DocHead.Status::"Ready for posting" THEN BEGIN
          DocHead.Status := DocHead.Status::Approved;
          DocHead."Change Jnl Processed PO" := FALSE;
          DocHead.MODIFY(TRUE);
        END;

        EXIT;
      END;

      DocHead.Status := DocHead.Status::Active;
      DocHead."Change Jnl Processed PO" := FALSE;
      DocHead.MODIFY(TRUE);
    END;

    PROCEDURE SetDocumentStatus@1100285006(DocLineIn@1100285000 : Record 12013609);
    VAR
      DocHead@1100285001 : Record 12013608;
      DocLine@1100285003 : Record 12013609;
      PurchHeader@1100285009 : Record 38;
      NoOfLines@1100285004 : Integer;
      NoOfZero@1100285005 : Integer;
      NoOfApproved@1100285006 : Integer;
    BEGIN
      DocHead.GET(DocLineIn."Document ID");

      DocHead.CALCFIELDS("Hold by Approver");
      IF DocHead."Hold by Approver" THEN
        EXIT;

      IF DocHead."Document Type" IN [DocHead."Document Type"::Quote,
                                     DocHead."Document Type"::Order] THEN BEGIN
        PurchHeader.GET(DocHead."Document Type",DocHead."Document No.");
        IF PurchHeader.Status = PurchHeader.Status::Open THEN
          EXIT;
      END;

      DocLine.RESET;
      DocLine.SETCURRENTKEY("Document ID",Flowstatus);
      DocLine.SETRANGE("Document ID",DocLineIn."Document ID");
      NoOfLines := DocLine.COUNT;
      DocLine.SETRANGE(Flowstatus, 0);
      NoOfZero := DocLine.COUNT;
      DocLine.SETRANGE(Flowstatus, DocLine.Flowstatus::Approved);
      NoOfApproved := DocLine.COUNT;

      IF (NoOfLines = 0) OR (NoOfLines = NoOfZero) THEN BEGIN
        DocHead.Status := DocHead.Status::Inactive;
        DocHead."Change Jnl Processed PO" := FALSE;
        DocHead.MODIFY(TRUE);

        EXIT;
      END;

      IF NoOfLines = NoOfApproved THEN BEGIN
        IF (DocHead.Status <> DocHead.Status::"Ready for posting") AND (DocHead.Status <> DocHead.Status::Received) THEN BEGIN
          DocHead.Status := DocHead.Status::Approved;
          IF DocHead."Document Type" <> DocHead."Document Type"::Order THEN
            DocHead."Change Jnl Processed PO" := FALSE;
          DocHead.MODIFY(TRUE);
        END;

        EXIT;
      END;

      DocHead.Status := DocHead.Status::Active;
      DocHead."Change Jnl Processed PO" := FALSE;
      DocHead.MODIFY(TRUE);
    END;

    PROCEDURE FinishOneLine@1100285007(DocLineIn@1100285000 : Record 12013609;VAR DocLineApp@1100285002 : Record 12013610);
    VAR
      FinishApprovalOneLineOption@1100285001 : Report 12013636;
    BEGIN
      FinishApprovalOneLineOption.SetDocLine(DocLineIn,DocLineApp);
      FinishApprovalOneLineOption.SetFlowStatus(0);
      FinishApprovalOneLineOption.RUNMODAL;
    END;

    PROCEDURE RejectOneLine@1100285024(DocLineIn@1100285000 : Record 12013609;VAR DocLineApp@1100285002 : Record 12013610;VAR FinishComment@1100285003 : Text[180]);
    VAR
      FinishApprovalOneLineOption@1100285001 : Report 12013636;
    BEGIN
      FinishApprovalOneLineOption.SetDocLine(DocLineIn,DocLineApp);
      FinishApprovalOneLineOption.SetFlowStatus(1);
      FinishApprovalOneLineOption.SetFinishComment(FinishComment);
      FinishApprovalOneLineOption.RUNMODAL;
      FinishComment := FinishApprovalOneLineOption.GetFinishComment;
    END;

    PROCEDURE FinishOneLineUpdate@1100285020(DocLineIn@1100285000 : Record 12013609;VAR DocLineApp@1100285002 : Record 12013610;FinishComment@1100285003 : Text[250];FlowStatus@1100285001 : Integer);
    VAR
      ExUserComp@1100285004 : Record 12013641;
      ExFlow@1100285005 : Codeunit 12013601;
    BEGIN
      DocLineApp."Approval Date" := CREATEDATETIME(WORKDATE, TIME);
      DocLineApp."Approved By" := USERID;
      IF ExFlow.GetUserComp(ExUserComp,USERID) THEN
        DocLineApp."Approved By" := ExUserComp."User ID";
      IF FlowStatus = 0 THEN
        DocLineApp.Flowstatus := DocLineApp.Flowstatus::Approved
      ELSE IF FlowStatus = 1 THEN
        DocLineApp.Flowstatus := DocLineApp.Flowstatus::Rejected;

      DocLineApp.Comment := FinishComment;
      DocLineApp.MODIFY(FALSE);

      IF DocLineApp.Flowstatus <> DocLineApp.Flowstatus::Rejected THEN BEGIN
        DocLineApp.ApprovalStatus(DocLineApp,'Update');

        DocLineApp.RESET;
        DocLineApp.SETCURRENTKEY("Document Line ID");
        DocLineApp.SETRANGE("Document Line ID", DocLineIn.ID);
        DocLineApp.SETFILTER(Flowstatus,'<>%1',DocLineApp.Flowstatus);
        IF NOT DocLineApp.FINDFIRST THEN BEGIN
          DocLineIn.GET(DocLineIn.ID);
          DocLineIn.Flowstatus := DocLineApp.Flowstatus;
          DocLineIn.MODIFY(TRUE);

          DocLineIn.SetDocumentStatus(DocLineIn);
        END;
      END ELSE BEGIN
        DocLineIn.GET(DocLineIn.ID);
        DocLineIn.Flowstatus := DocLineApp.Flowstatus;
        DocLineIn.MODIFY(TRUE);

        DocLineIn.SetDocumentStatus(DocLineIn);
      END;
    END;

    PROCEDURE CalcAmount@1100285008();
    BEGIN
      Amount := Quantity * "Direct Unit Cost" * (1 - "Line Discount %"/100);
    END;

    PROCEDURE HoldLine@1100285001(DocLineApp@1100285002 : Record 12013610);
    VAR
      HoldLineOption@1100285001 : Report 12013634;
      ReleaseLineOption@1100285004 : Report 12013637;
    BEGIN
      IF DocLineApp.Flowstatus = DocLineApp.Flowstatus::"On hold" THEN BEGIN
        ReleaseLineOption.SetDocLine(DocLineApp);
        ReleaseLineOption.RUNMODAL;
      END ELSE BEGIN
        HoldLineOption.SetDocLine(DocLineApp);
        HoldLineOption.RUNMODAL;
      END;
    END;

    PROCEDURE HoldLineUpdate@1100285021(DocLineApp@1100285002 : Record 12013610;FinishComment@1100285001 : Text[250];FinishCode@1100285005 : Option);
    VAR
      DocLineApp2@1100285000 : Record 12013610;
      DocLineApp3@1100285004 : Record 12013610;
    BEGIN
      CASE FinishCode OF
        0: // Hold line
          BEGIN
            DocLineApp.Comment := FinishComment;
            DocLineApp.Flowstatus := DocLineApp.Flowstatus::"On hold";
            DocLineApp."Approval Date" := CREATEDATETIME(WORKDATE, TIME);
            DocLineApp."Approved By" := USERID;
            DocLineApp.MODIFY(FALSE);
          END;
        1: // Hold all lines
          BEGIN
            DocLineApp2.RESET;
            DocLineApp2.SETCURRENTKEY("Document No.");
            DocLineApp2.SETRANGE("Document Type",DocLineApp."Document Type");
            DocLineApp2.SETRANGE("Document No.",DocLineApp."Document No.");
            DocLineApp2.SETRANGE(Flowstatus,DocLineApp2.Flowstatus::Current);
            IF DocLineApp2.FINDSET(TRUE,TRUE) THEN
              REPEAT
                DocLineApp3 := DocLineApp2;
                DocLineApp3.Comment := FinishComment;
                DocLineApp3.Flowstatus := DocLineApp.Flowstatus::"On hold";
                DocLineApp3."Approval Date" := CREATEDATETIME(WORKDATE, TIME);
                DocLineApp3."Approved By" := USERID;
                DocLineApp3.MODIFY(FALSE);
              UNTIL DocLineApp2.NEXT = 0;
          END;
      END;
    END;

    PROCEDURE ReleaseLineUpdate@1100285022(DocLineApp@1100285002 : Record 12013610;FinishCode@1100285000 : Option);
    VAR
      DocLineApp2@1100285001 : Record 12013610;
      DocLineApp3@1100285003 : Record 12013610;
      DocLineApp4@1100285004 : Record 12013610;
    BEGIN
      CASE FinishCode OF
        0: // Release line
          BEGIN
            DocLineApp.Flowstatus := DocLineApp.Flowstatus::Current;
            DocLineApp."Approval Date" := CREATEDATETIME(0D, 0T);
            DocLineApp."Approved By" := '';
            DocLineApp.MODIFY(FALSE);

            DocLineApp2.RESET;
            DocLineApp2.SETCURRENTKEY("Document Line ID","Approver Order");
            DocLineApp2.SETRANGE("Document Line ID",DocLineApp2."Document Line ID");
            DocLineApp2.SETFILTER("Approver Order",'>%1',DocLineApp."Approver Order");
            IF DocLineApp2.FINDSET(TRUE) THEN
              REPEAT
                DocLineApp2.Flowstatus := DocLineApp2.Flowstatus::"Not processed";
                DocLineApp2.MODIFY(FALSE);
              UNTIL DocLineApp2.NEXT = 0;
          END;

        1: // Release all lines
          BEGIN
            DocLineApp2.RESET;
            DocLineApp2.SETCURRENTKEY("Document No.");
            DocLineApp2.SETRANGE("Document Type",DocLineApp."Document Type");
            DocLineApp2.SETRANGE("Document No.",DocLineApp."Document No.");
            DocLineApp2.SETRANGE(Flowstatus,DocLineApp2.Flowstatus::"On hold");
            IF DocLineApp2.FINDSET(TRUE,TRUE) THEN
              REPEAT
                DocLineApp4 := DocLineApp2;
                DocLineApp4.Flowstatus := DocLineApp4.Flowstatus::Current;
                DocLineApp4."Approval Date" := CREATEDATETIME(0D, 0T);
                DocLineApp4."Approved By" := '';
                DocLineApp4.MODIFY(FALSE);

                DocLineApp3.RESET;
                DocLineApp3.SETCURRENTKEY("Document Line ID","Approver Order");
                DocLineApp3.SETRANGE("Document Line ID",DocLineApp2."Document Line ID");
                DocLineApp3.SETFILTER("Approver Order",'>%1',DocLineApp2."Approver Order");
                IF DocLineApp3.FINDSET(TRUE) THEN BEGIN
                  DocLineApp3.Flowstatus := DocLineApp3.Flowstatus::"Not processed";
                  DocLineApp3.MODIFY(FALSE);
                END;
              UNTIL DocLineApp2.NEXT = 0;
          END;
      END;
    END;

    PROCEDURE UpdateExtraFromDocLine@1100285009(LineField@1100285000 : Code[20];DocLineXrec@1100285001 : Record 12013609;DocLine@1100285002 : Record 12013609);
    VAR
      DocLineDim@1100285006 : Record 12013611;
      Column@1100285005 : Record 12013661;
      ExFWorkFlowMgt@1100285007 : Codeunit 12013593;
      Empty@1100285003 : Boolean;
      ColumnID@1100285004 : Integer;
    BEGIN
      Empty := FALSE;

      DocLineDim.RESET;
      DocLineDim.SETCURRENTKEY("Document Line ID");
      DocLineDim.SETRANGE("Document Line ID", DocLine.ID);

      CASE LineField OF
        'G_LACCOUNT','LINETYPE':
          BEGIN
            CASE DocLineXrec."Line Type" OF
              1 : DocLineDim.SETRANGE("Dimension Code",'G_LACCOUNT');
              2 : DocLineDim.SETRANGE("Dimension Code",'ITEM');
              4 : DocLineDim.SETRANGE("Dimension Code",'FIXEDASSET');
              5 : DocLineDim.SETRANGE("Dimension Code",'CHARGE');
            END;

           IF (DocLine."Line Type" = 0) OR (DocLine."No." = '') THEN
             Empty := TRUE;
          END;

        'JOB':
           BEGIN
             IF DocLine."Job No." = '' THEN
               Empty := TRUE;

             DocLineDim.SETRANGE("Dimension Code",'JOB');
           END;

        'JOBTASK':
           BEGIN
             IF DocLine."Job Task No." = '' THEN
               Empty := TRUE;

             DocLineDim.SETRANGE("Dimension Code",'JOBTASK');
           END;

        'PERCODE':
           BEGIN
             IF DocLine."Periodic Template Code" = '' THEN
               Empty := TRUE;

             DocLineDim.SETRANGE("Dimension Code",'PERCODE');
           END;

        'PERDATE':
           BEGIN
             IF DocLine."Periodic Starting Date" = 0D THEN
               Empty := TRUE;

             DocLineDim.SETRANGE("Dimension Code",'PERDATE');
           END;
      END;

      IF DocLineDim.FIND('-') THEN BEGIN
        IF Empty THEN
          DocLineDim.DELETE
        ELSE BEGIN
          CASE LineField OF
            'LINETYPE':
              BEGIN
                CASE DocLine."Line Type" OF
                  1 : DocLineDim."Dimension Code" := 'G_LACCOUNT';
                  2 : DocLineDim."Dimension Code" := 'ITEM';
                  4 : DocLineDim."Dimension Code" := 'FIXEDASSET';
                  5 : DocLineDim."Dimension Code" := 'CHARGE';
                END;

                DocLineDim."Dimension Value" := DocLine."No.";
              END;

            'JOB'    : DocLineDim."Dimension Value" := DocLine."Job No.";
            'JOBTASK': DocLineDim."Dimension Value" := DocLine."Job Task No.";
            'PERCODE': DocLineDim."Dimension Value" := DocLine."Periodic Template Code";
            'PERDATE': IF DocLine."Periodic Starting Date" <> 0D THEN
              DocLineDim."Dimension Value" := ExFWorkFlowMgt.TransformDateToText(DocLine."Periodic Starting Date");
          END;

          DocLineDim.MODIFY(FALSE);
        END;
      END ELSE BEGIN
        IF (NOT Empty) THEN BEGIN
          DocLineDim.INIT;
          DocLineDim.ID := 0;
          DocLineDim."Document Type" := DocLine."Document Type";
          DocLineDim."Document No." := DocLine."Document No.";
          DocLineDim."Line No." := DocLine."Line No.";

          IF LineField = 'LINETYPE' THEN BEGIN
            CASE DocLine."Line Type" OF
              1 : DocLineDim."Dimension Code" := 'G_LACCOUNT';
              2 : DocLineDim."Dimension Code" := 'ITEM';
              4 : DocLineDim."Dimension Code" := 'FIXEDASSET';
              5 : DocLineDim."Dimension Code" := 'CHARGE';
            END;
          END ELSE
            DocLineDim."Dimension Code" := LineField;

          ColumnID := ExFlow.GetColumnID(DocLineDim."Dimension Code");
          IF ColumnID = 0 THEN
            ERROR(STRSUBSTNO(EXF45,DocLineDim."Dimension Code"));

          DocLineDim."Document Line ID" := DocLine.ID;
          DocLineDim."Document ID" := DocLine."Document ID";

          CASE LineField OF
            'LINETYPE': DocLineDim."Dimension Value" := DocLine."No.";
            'JOB'     : DocLineDim."Dimension Value" := DocLine."Job No.";
            'JOBTASK' : DocLineDim."Dimension Value" := DocLine."Job Task No.";
            'PERCODE' : DocLineDim."Dimension Value" := DocLine."Periodic Template Code";
            'PERDATE' : IF DocLine."Periodic Starting Date" <> 0D THEN
              DocLineDim."Dimension Value" := ExFWorkFlowMgt.TransformDateToText(DocLine."Periodic Starting Date");
          END;

          IF ColumnID <> 0 THEN
            IF Column.GET(ColumnID) THEN BEGIN
              DocLineDim."Column ID" := Column.ID;
              DocLineDim.INSERT;
            END;
        END;
      END;
    END;

    PROCEDURE Changeable@1100285010();
    VAR
      AppSetup@1100285001 : Record 12013601;
      DocHead@1100285000 : Record 12013608;
    BEGIN
      IF NOT DoByPassControl THEN
        IF CurrFieldNo = 0 THEN
          EXIT;

      IF "Connected Line ID" <> 0 THEN
        EXIT;

      AppSetup.GET;

      DocHead.GET("Document ID");

      DocHead.CALCFIELDS("Hold by Approver");

      IF DocHead."Hold by Approver" THEN
        ERROR(EXF09);

      IF Flowstatus > Flowstatus::Active THEN BEGIN
        IF DocHead.Status = DocHead.Status::"Ready for posting" THEN
          ERROR(EXF42);

        IF NOT ExFlow.IsUserSuperior(USERID) THEN
          ERROR(EXF08);

        IF Flowstatus <> Flowstatus::Rejected THEN
          IF NOT AppSetup."Suppress Approved Message" THEN
            IF NOT CONFIRM(EXF41) THEN
              ERROR(EXF48);
      END;

      //check here
      IF (("Document Type" = "Document Type"::Quote) OR
         (("Document Type" = "Document Type"::Order) AND ("Connected Line ID" = 0))) THEN
        ERROR(EXF13);
    END;

    PROCEDURE ConnectedToReceipt@1100285011(FieldQuantity@1100285000 : Boolean);
    VAR
      PurchLine@1100285001 : Record 39;
      ReceiptLine@1100285002 : Record 121;
    BEGIN
      IF NOT PurchLine.GET("Document Type", "Document No.", "Line No.") THEN
        EXIT;

      IF PurchLine."Receipt No." = '' THEN
        EXIT;

      IF FieldQuantity THEN BEGIN
        IF ReceiptLine.GET(PurchLine."Receipt No.", PurchLine."Receipt Line No.") THEN
          IF Quantity > ReceiptLine."Qty. Rcd. Not Invoiced" THEN
            ERROR(STRSUBSTNO(EXF11, ReceiptLine."Qty. Rcd. Not Invoiced"));
      END ELSE
        ERROR(EXF10);
    END;

    PROCEDURE ShowShortcutDimCode@1100285012(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    VAR
      ExDocLineDim@1100285000 : Record 12013611;
      i@1100285001 : Integer;
    BEGIN
      GetGLSetup;

      ExDocLineDim.RESET;
      ExDocLineDim.SETCURRENTKEY("Document Line ID");
      ExDocLineDim.SETRANGE("Document Line ID", ID);
      FOR i := 1 TO 8 DO BEGIN
        ShortcutDimCode[i] := '';
        IF GLSetupShortcutDimCode[i] <> '' THEN BEGIN
          ExDocLineDim.SETRANGE("Dimension Code", GLSetupShortcutDimCode[i]);
          IF ExDocLineDim.FINDFIRST THEN
            ShortcutDimCode[i] := ExDocLineDim."Dimension Value";
        END;
      END;
    END;

    PROCEDURE LookupShortcutDimCode@30(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimMgt@1100285001 : Codeunit 408;
      TempShortcutDimCode@1100285000 : Code[20];
    BEGIN
      TempShortcutDimCode := ShortcutDimCode;
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);

      IF TempShortcutDimCode <> ShortcutDimCode THEN
        ValidateShortcutDimCode(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      ExDocLineDim@1100285000 : Record 12013611;
      DimMgt@1100285003 : Codeunit 408;
    BEGIN
      DoByPassControl := TRUE;
      Changeable;

      GetGLSetup;

      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);

      ExDocLineDim.RESET;
      ExDocLineDim.SETCURRENTKEY("Document Line ID");
      ExDocLineDim.SETRANGE("Document Line ID", ID);
      ExDocLineDim.SETRANGE("Dimension Code", GLSetupShortcutDimCode[FieldNumber]);
      IF ExDocLineDim.FIND('-') THEN BEGIN
        IF ShortcutDimCode = '' THEN BEGIN
          ExDocLineDim.DELETE;

          "Changed By" := USERID;
          "Change Date" := CREATEDATETIME(TODAY,TIME);
          MODIFY;
        END ELSE BEGIN
          ExDocLineDim."Dimension Value" := ShortcutDimCode;
          ExDocLineDim."Change Date" := CREATEDATETIME(TODAY,TIME);
          ExDocLineDim."Changed By" := USERID;
          ExDocLineDim.MODIFY;
        END;
      END ELSE BEGIN
        ExDocLineDim.INIT;
        ExDocLineDim."Document Type" := "Document Type";
        ExDocLineDim."Document No." := "Document No.";
        ExDocLineDim."Line No." := "Line No.";
        ExDocLineDim."Dimension Code" := GLSetupShortcutDimCode[FieldNumber];
        ExDocLineDim."Dimension Value" := ShortcutDimCode;
        ExDocLineDim."Column ID" := ExFlow.GetColumnID(GLSetupShortcutDimCode[FieldNumber]);
        IF ExDocLineDim."Column ID" = 0 THEN
          ERROR(STRSUBSTNO(EXF45,ExDocLineDim."Dimension Code"));

        ExDocLineDim."Document Line ID" := ID;
        ExDocLineDim."Document ID" := "Document ID";
        ExDocLineDim.ID := 0;
        ExDocLineDim."Change Date" := CREATEDATETIME(TODAY,TIME);
        ExDocLineDim."Changed By" := USERID;
        ExDocLineDim.INSERT;
      END;
    END;

    LOCAL PROCEDURE GetGLSetup@52();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      IF NOT HasGotGLSetup THEN BEGIN
        GLSetup.GET;
        GLSetupShortcutDimCode[1] := GLSetup."Shortcut Dimension 1 Code";
        GLSetupShortcutDimCode[2] := GLSetup."Shortcut Dimension 2 Code";
        GLSetupShortcutDimCode[3] := GLSetup."Shortcut Dimension 3 Code";
        GLSetupShortcutDimCode[4] := GLSetup."Shortcut Dimension 4 Code";
        GLSetupShortcutDimCode[5] := GLSetup."Shortcut Dimension 5 Code";
        GLSetupShortcutDimCode[6] := GLSetup."Shortcut Dimension 6 Code";
        GLSetupShortcutDimCode[7] := GLSetup."Shortcut Dimension 7 Code";
        GLSetupShortcutDimCode[8] := GLSetup."Shortcut Dimension 8 Code";
        HasGotGLSetup := TRUE;
      END;
    END;

    PROCEDURE ShowCard@1100285014();
    BEGIN
      Card;
    END;

    LOCAL PROCEDURE Card@1100285004();
    VAR
      PurchHeader@1100285000 : Record 38;
      PurchInvHeader@1100285001 : Record 122;
      PurchCrMemoHeader@1100285002 : Record 124;
    BEGIN
      PurchHeader.RESET;
      PurchHeader.SETRANGE("No.","Document No.");

      CASE "Document Type" OF
        "Document Type"::Invoice :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            PAGE.RUNMODAL(51,PurchHeader);
          END;
        "Document Type"::"Credit Memo" :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
            PAGE.RUNMODAL(52,PurchHeader);
          END;
        "Document Type"::Order :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Order);
            PAGE.RUNMODAL(50,PurchHeader);
          END;
        "Document Type"::Quote :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Quote);
            PAGE.RUNMODAL(49,PurchHeader);
          END;
        "Document Type"::"6" :
          BEGIN
            PurchInvHeader.SETRANGE("No.", "Document No.");
            PAGE.RUNMODAL(138,PurchInvHeader);
          END;
        "Document Type"::"7" :
          BEGIN
            PurchCrMemoHeader.SETRANGE("No.", "Document No.");
            PAGE.RUNMODAL(140,PurchCrMemoHeader);
          END;
      END;
    END;

    PROCEDURE RetrieveRecord@1100285013(VAR ExDocLine@1100285004 : Record 12013609;DocType@1100285001 : Integer;DocNo@1100285002 : Code[20];LineNo@1100285003 : Integer) : Boolean;
    BEGIN
      WITH ExDocLine DO BEGIN
        RESET;
        CLEAR(ExDocLine);
        SETCURRENTKEY("Document No.");
        SETRANGE("Document Type", DocType);
        SETRANGE("Document No.", DocNo);
        SETRANGE("Line No.", LineNo);
        EXIT(FINDFIRST);
      END;
    END;

    PROCEDURE GetName@1100285015() : Text[50];
    VAR
      GLAccount@1100285000 : Record 15;
      Item@1100285001 : Record 27;
      FixedAsset@1100285002 : Record 5600;
      ItemCharge@1100285003 : Record 5800;
      PurchLine@1000000000 : Record 39;
      ExFlow4PS@1000000001 : Codeunit 12057160;
      DimCode@1000000002 : Code[20];
      DimVal@1000000003 : Code[20];
      UpdatePurchLine@1000000004 : Codeunit 12013609;
      DimValRec@1000000005 : Record 349;
    BEGIN
      IF "No." = '' THEN
        EXIT('');

      //4PS20200221
      IF ExFlow4PS.GetCostObjDimCode(DimCode) THEN BEGIN
        DimVal := UpdatePurchLine.GetAddFromDocLineDim(Rec,DimCode,COMPANYNAME);
        IF DimVal <> '' THEN BEGIN
          DimValRec.SETRANGE("Dimension Code",DimCode);
          DimValRec.SETRANGE(Code,DimVal);
          IF DimValRec.FINDFIRST THEN
              EXIT(DimValRec.Name);
        END;
        //To avoid setting ExDocLine.Desc if ExFlow Dim is not yet written
        IF NOT "New Line" THEN
          IF PurchLine.GET("Document Type","Document No.","Line No.") THEN
            IF PurchLine."Shortcut Dimension 2 Code" <> '' THEN
              EXIT(PurchLine.Description);
      END;
      //4PS20200221

      CASE "Line Type" OF
        "Line Type"::"G/L Account":
          BEGIN
            IF GLAccount.GET("No.") THEN
              EXIT(GLAccount.Name);
          END;
        "Line Type"::Item:
          BEGIN
            IF Item.GET("No.") THEN
              EXIT(Item.Description);
          END;
        "Line Type"::"Fixed Asset":
          BEGIN
            IF FixedAsset.GET("No.") THEN
              EXIT(FixedAsset.Description);
          END;
        "Line Type"::"Charge (Item)":
          BEGIN
            IF ItemCharge.GET("No.") THEN
              EXIT(ItemCharge.Description);
          END;
      END;

      EXIT('');
    END;

    PROCEDURE DeleteLine@1100285016(NewDocID@1100285001 : Integer;VAR ExDoc@1100285006 : Record 12013608);
    VAR
      ExDeletedDoc@1100285007 : Record 12013622;
      ExDeletedDocLine@1100285004 : Record 12013623;
      ExDocLineApp@1100285003 : Record 12013610;
      ExDeletedDocLineApp@1100285005 : Record 12013624;
      ExDocLineDim@1100285002 : Record 12013611;
      ExDeletedDocLineDim@1100285000 : Record 12013625;
    BEGIN
      ExDeletedDocLine.INIT;
      ExDeletedDocLine.TRANSFERFIELDS(Rec);
      ExDeletedDocLine.ID := 0;
      IF NewDocID <> 0 THEN
        ExDeletedDocLine."Document ID" := NewDocID
      ELSE BEGIN
        ExDeletedDoc.RESET;
        ExDeletedDoc.SETCURRENTKEY("Document No.");
        ExDeletedDoc.SETRANGE("Document Type", "Document Type");
        ExDeletedDoc.SETRANGE("Document No.", "Document No.");
        IF ExDeletedDoc.FINDFIRST THEN
          ExDeletedDocLine."Document ID" := ExDeletedDoc.ID;
      END;
      ExDeletedDocLine."Deleted By" := USERID;
      ExDeletedDocLine."Deleted Date" := CREATEDATETIME(TODAY,TIME);
      ExDeletedDocLine."Old ID" := ID;
      ExDeletedDocLine."Old Document ID" := "Document ID";
      ExDeletedDocLine.INSERT;

      UndoReceipt(TRUE,ExDoc);

      ExDocLineApp.RESET;
      ExDocLineApp.SETCURRENTKEY("Document Line ID");
      ExDocLineApp.SETRANGE("Document Line ID", ID);
      IF ExDocLineApp.FINDSET(TRUE) THEN BEGIN
        REPEAT
          ExDeletedDocLineApp.INIT;
          ExDeletedDocLineApp.TRANSFERFIELDS(ExDocLineApp);
          ExDeletedDocLineApp."Document Line ID" := ExDeletedDocLine.ID;
          ExDeletedDocLineApp.ID := 0;
          ExDeletedDocLineApp."Deleted By" := USERID;
          ExDeletedDocLineApp."Deleted Date" := CREATEDATETIME(TODAY,TIME);
          ExDeletedDocLineApp."Old ID" := ExDocLineApp.ID;
          ExDeletedDocLineApp."Old Document Line ID" := ExDocLineApp."Document Line ID";
          ExDeletedDocLineApp.INSERT;
        UNTIL ExDocLineApp.NEXT = 0;

        ExDocLineApp.DELETEALL;
      END;

      ExDocLineDim.RESET;
      ExDocLineDim.SETCURRENTKEY("Document Line ID");
      ExDocLineDim.SETRANGE("Document Line ID", ID);
      IF ExDocLineDim.FINDSET(TRUE) THEN BEGIN
        REPEAT
          ExDeletedDocLineDim.INIT;
          ExDeletedDocLineDim.TRANSFERFIELDS(ExDocLineDim);
          ExDeletedDocLineDim."Document Line ID" := ExDeletedDocLine.ID;
          ExDeletedDocLineDim.ID := 0;
          ExDeletedDocLineDim."Deleted By" := USERID;
          ExDeletedDocLineDim."Deleted Date" := CREATEDATETIME(TODAY,TIME);
          ExDeletedDocLineDim."Old ID" := ExDocLineDim.ID;
          ExDeletedDocLineDim."Old Document Line ID" := ExDocLineDim."Document Line ID";
          ExDeletedDocLineDim.INSERT;
        UNTIL ExDocLineDim.NEXT = 0;

        ExDocLineDim.DELETEALL;
      END;

      ExDoc."Changed By" := USERID;
      ExDoc."Change Date" := CREATEDATETIME(TODAY,TIME);
      ExDoc.MODIFY;
    END;

    PROCEDURE InitRecord@1100285017(xDocLine@1100285000 : Record 12013609;BelowxRec@1100285002 : Boolean);
    VAR
      DocHead@1100285001 : Record 12013608;
      TempLineNo@1100285003 : Integer;
    BEGIN
      IF DocHead.GET("Document ID") THEN BEGIN
        "Document Type" := DocHead."Document Type";
        "Document No." := DocHead."Document No.";
        IF xDocLine.GET(xDocLine.ID) THEN BEGIN
          IF BelowxRec THEN
            "Line No." := xDocLine."Line No." + 10000
          ELSE BEGIN
            TempLineNo := GetPreviousLineNo(Rec,xDocLine."Line No.");
            "Line No." := TempLineNo + ((xDocLine."Line No." -  TempLineNo) / 2);
          END;
        END;

        IF "Line No." = 0 THEN
          "Line No." := 10000;
      END;
    END;

    PROCEDURE GetPreviousLineNo@1100285018(ExDocLine@1100285000 : Record 12013609;xLineNo@1100285002 : Integer) : Integer;
    VAR
      ExDocLine2@1100285001 : Record 12013609;
    BEGIN
      WITH ExDocLine2 DO BEGIN
        SETCURRENTKEY("Document Type","Document No.","Line No.");
        SETRANGE("Document Type", ExDocLine."Document Type");
        SETRANGE("Document No.", ExDocLine."Document No.");
        SETFILTER("Line No.", '<%1', xLineNo);
        IF FINDLAST THEN
          EXIT("Line No.")
        ELSE
          EXIT(0);
      END;
    END;

    PROCEDURE UndoReceipt@1100285023(Delete@1100285005 : Boolean;VAR DocHead@1100285006 : Record 12013608);
    VAR
      ConnExDoc@1100285001 : Record 12013609;
      ConnExDoc2@1100285003 : Record 12013609;
      ConnPurchLine@1100285000 : Record 39;
      TotalQtyReceived@1100285004 : Decimal;
      ExFlowSetup@1100285002 : Record 12013601;
    BEGIN
      IF "Connected Line ID" <> 0 THEN
        EXIT;

      ExFlowSetup.GET;
      IF ExFlowSetup."Web Receipts" <> ExFlowSetup."Web Receipts"::"Web confirmed" THEN
        EXIT;

      IF ConnExDoc.GET("Connected Line ID") THEN BEGIN
        ConnExDoc2.RESET;
        ConnExDoc2.SETCURRENTKEY("Connected Line ID");
        ConnExDoc2.SETRANGE("Connected Line ID", "Connected Line ID");
        IF ConnExDoc2.FINDSET THEN
          REPEAT
            IF ConnExDoc2.ID = ID THEN BEGIN
              IF NOT Delete THEN
                TotalQtyReceived := TotalQtyReceived + ABS(Quantity);
            END ELSE
              TotalQtyReceived := TotalQtyReceived + ABS(ConnExDoc2.Quantity);
          UNTIL ConnExDoc2.NEXT = 0;

        ConnPurchLine.GET(ConnExDoc."Document Type",ConnExDoc."Document No.",ConnExDoc."Line No.");
        IF ConnPurchLine."Quantity Invoiced" > TotalQtyReceived THEN
          ERROR(EXF52);

        IF DocHead.Status = DocHead.Status::Invoiced THEN
          ERROR(EXF47);

        IF ConnPurchLine."Document Type" = ConnPurchLine."Document Type"::"Return Order" THEN BEGIN
          IF ConnPurchLine."Return Qty. Shipped" >= Quantity THEN BEGIN
            ConnPurchLine."Return Qty. Shipped" := TotalQtyReceived;
            ConnPurchLine."Return Qty. Shipped (Base)" := TotalQtyReceived;
            ConnPurchLine.InitOutstanding;
            ConnPurchLine.InitQtyToShip;
            ConnPurchLine.MODIFY;

            IF DocHead.Status = DocHead.Status::Received THEN BEGIN
              DocHead.Status := DocHead.Status::Approved;
              DocHead.MODIFY;
            END;
          END;
        END ELSE IF ConnPurchLine."Document Type" = ConnPurchLine."Document Type"::Order THEN BEGIN
          IF ConnPurchLine."Quantity Received" >= Quantity THEN BEGIN
            ConnPurchLine."Quantity Received" := TotalQtyReceived;
            ConnPurchLine."Qty. Received (Base)" := TotalQtyReceived;
            ConnPurchLine.InitOutstanding;
            ConnPurchLine.InitQtyToReceive;
            ConnPurchLine.MODIFY;

            IF DocHead.Status = DocHead.Status::Received THEN BEGIN
              DocHead.Status := DocHead.Status::Approved;
              DocHead.MODIFY;
            END;
          END;
        END;
      END;
    END;

    PROCEDURE ShowDim@1100285025();
    VAR
      DocLineDim@1100285004 : Record 12013611;
    BEGIN
      DocLineDim.RESET;
      DocLineDim.SETCURRENTKEY("Document Line ID");
      DocLineDim.SETRANGE("Document Line ID",ID);
      PAGE.RUN(12013611,DocLineDim);
    END;

    PROCEDURE ShowNonDimCodes@1100285028();
    VAR
      ExDocLineDim@1100285000 : Record 12013611;
      UpdatePurchLine@1100285002 : Codeunit 12013609;
    BEGIN
      ExDocLineDim.RESET;
      ExDocLineDim.SETCURRENTKEY("Document Line ID");
      ExDocLineDim.SETRANGE("Document Line ID", ID);
      // fields not updated from the web
      ExDocLineDim.SETRANGE("Dimension Code", 'PERCODE');
      IF ExDocLineDim.FINDFIRST THEN
        "Periodic Template Code" := ExDocLineDim."Dimension Value";

      ExDocLineDim.SETRANGE("Dimension Code", 'PERDATE');
      IF ExDocLineDim.FINDFIRST THEN
        "Periodic Starting Date" := UpdatePurchLine.GetAddDateFromDocLineDim(Rec,ExDocLineDim."Dimension Code", COMPANYNAME);

      ExDocLineDim.SETRANGE("Dimension Code", 'JOB');
      IF ExDocLineDim.FINDFIRST THEN
        "Job No." := ExDocLineDim."Dimension Value";

      ExDocLineDim.SETRANGE("Dimension Code", 'JOBTASK');
      IF ExDocLineDim.FINDFIRST THEN
        "Job Task No." := ExDocLineDim."Dimension Value";
    END;

    PROCEDURE GetCustomerValue@1100285033() : Code[20];
    VAR
      UpdatePurchLine@1100285000 : Codeunit 12013609;
    BEGIN
      EXIT(UpdatePurchLine.GetAddFromDocLineDim(Rec,'CUSTOMER',COMPANYNAME));
    END;

    PROCEDURE CreateDim@26(Type1@1000 : Integer;No1@1001 : Code[20];Type2@1002 : Integer;No2@1003 : Code[20];Type3@1004 : Integer;No3@1005 : Code[20];Type4@1006 : Integer;No4@1007 : Code[20]);
    VAR
      SourceCodeSetup@1008 : Record 242;
      ExDocLineDim@1100285003 : Record 12013611;
      DimSetEntry@1100285004 : Record 480;
      TableID@1009 : ARRAY [10] OF Integer;
      TempDimSet@1100285000 : Integer;
      TempColumnID@1100285005 : Integer;
      No@1010 : ARRAY [10] OF Code[20];
      TempShortCut@1100285002 : Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := Type1;
      No[1] := No1;
      TableID[2] := Type2;
      No[2] := No2;
      TableID[3] := Type3;
      No[3] := No3;
      TableID[4] := Type4;
      No[4] := No4;

      TempDimSet := DimMgt.GetDefaultDimID(TableID,No,SourceCodeSetup.Purchases,TempShortCut,TempShortCut,0,DATABASE::Vendor);
      IF TempDimSet = 0 THEN
        EXIT;

      ExDocLineDim.SETCURRENTKEY("Document Line ID");
      ExDocLineDim.SETRANGE("Document Line ID", Rec.ID);
      DimSetEntry.SETRANGE("Dimension Set ID", TempDimSet);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          TempColumnID := ExFlow.GetColumnID(DimSetEntry."Dimension Code");
          IF TempColumnID <> 0 THEN BEGIN
            ExDocLineDim.SETRANGE("Dimension Code", DimSetEntry."Dimension Code");
            IF ExDocLineDim.FIND('-') THEN BEGIN
              ExDocLineDim."Dimension Value" := DimSetEntry."Dimension Value Code";
              ExDocLineDim.MODIFY;
            END ELSE BEGIN
              ExDocLineDim.INIT;
              ExDocLineDim.ID := 0;
              ExDocLineDim."Document Type" := Rec."Document Type";
              ExDocLineDim."Document No." := Rec."Document No.";
              ExDocLineDim."Line No." := Rec."Line No.";
              ExDocLineDim."Dimension Code" := DimSetEntry."Dimension Code";
              ExDocLineDim."Dimension Value" := DimSetEntry."Dimension Value Code";
              ExDocLineDim."Column ID" := TempColumnID;
              ExDocLineDim."Document Line ID" := Rec.ID;
              ExDocLineDim."Document ID" := Rec."Document ID";
              ExDocLineDim."Changed By" := USERID;
              ExDocLineDim."Change Date" := CREATEDATETIME(TODAY,TIME);
              ExDocLineDim.INSERT;
            END;
          END;
        UNTIL DimSetEntry.NEXT = 0;
    END;

    PROCEDURE CheckDeleteLine@1100285026();
    BEGIN
      IF "New Line" = FALSE THEN
        ERROR(EXF56);

      IF ("Connected Line ID" <> 0) AND ("New Line"=TRUE) THEN
        ERROR(EXF55);
    END;

    BEGIN
    {
      4PS20200221 Customization to load desc from cost object in GetName FUnction
    }
    END.
  }
}

