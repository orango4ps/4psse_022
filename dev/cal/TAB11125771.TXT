OBJECT Table 11125771 Time Slot (Glazing Service)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=VAR
               GlazingServiceCall@1100525000 : Record 11125777;
               Text000@1100525001 : TextConst 'ENU=You cannot delete %1 %2: one or more service calls still refer to it.;NOR=Du kan ikke slette %1 %2: en eller flere servicesamtaler refererer fortsatt til den.;SVE=Du kan inte ta bort %1 %2: en eller flera servicesamtal h„nvisar till den.';
               GlazingServiceCall2@1100525002 : Record 11125777;
               GlazingServiceCall3@1100525003 : Record 11125777;
             BEGIN
               GlazingServiceCall.SETRANGE("Appointment 1 Time Slot Code", Code);
               IF NOT GlazingServiceCall.ISEMPTY THEN
                 ERROR(Text000, TABLECAPTION, Code);

               GlazingServiceCall2.SETRANGE("Appointment 2 Time Slot Code", Code);
               IF NOT GlazingServiceCall2.ISEMPTY THEN
                 ERROR(Text000, TABLECAPTION, Code);

               GlazingServiceCall3.SETRANGE("Appointment 3 Time Slot Code", Code);
               IF NOT GlazingServiceCall3.ISEMPTY THEN
                 ERROR(Text000, TABLECAPTION, Code);
             END;

    CaptionML=[ENU=Time Slot (Glazing Service);
               NOR=Tidsluke (Innglassingsservice);
               SVE=Tidslucka (inglasningsservice)];
    LookupPageID=Page11125778;
    DrillDownPageID=Page11125778;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 10  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 20  ;   ;Starting Time       ;Time          ;OnValidate=BEGIN
                                                                CheckTimes;
                                                                UpdateDescription;
                                                              END;

                                                   CaptionML=[ENU=Starting Time;
                                                              NOR=Starttidspunkt;
                                                              SVE=Starttid] }
    { 30  ;   ;Ending Time         ;Time          ;OnValidate=BEGIN
                                                                CheckTimes;
                                                                UpdateDescription;
                                                              END;

                                                   CaptionML=[ENU=Ending Time;
                                                              NOR=Sluttidspunkt;
                                                              SVE=Sluttid] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=cannot be before %1;NOR=Kan ikke v‘re f›r %1;SVE=f†r inte vara f”re %1';
      Text001@1100525001 : TextConst 'ENU=Between %1 and %2;NOR=Mellom %1 og %2;SVE=Mellan %1 och %2';

    PROCEDURE CheckTimes@1100525001();
    BEGIN
      IF ("Starting Time" <> 0T) AND ("Ending Time" <> 0T) THEN
        IF "Ending Time" < "Starting Time" THEN
          FIELDERROR("Ending Time", STRSUBSTNO(Text000, FIELDCAPTION("Starting Time")));
    END;

    PROCEDURE UpdateDescription@1100525004();
    BEGIN
      IF (Description = '') OR (Description = BuildDescription(xRec."Starting Time", xRec."Ending Time")) THEN
        Description := BuildDescription("Starting Time", "Ending Time");
    END;

    PROCEDURE BuildDescription@1100525002(StartingTime@1100525000 : Time;EndingTime@1100525001 : Time) : Text[30];
    BEGIN
      IF (StartingTime <> 0T) AND (EndingTime <> 0T) THEN
        EXIT(STRSUBSTNO(Text001, StartingTime, EndingTime));
    END;

    BEGIN
    END.
  }
}

