OBJECT Page 1879 VAT Product Posting Grp Part
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=VAT Product Posting Grp Part;
               NOR=Mva-bokf›ringsgruppe - vare;
               SVE=Del av moms produktbokf”ringsmall];
    SourceTable=Table1877;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 VATNotification.ID := FORMAT(CREATEGUID);
                 PopulateVATProdGroups;
                 ShowVATRates;
                 SETRANGE(Default,FALSE);
               END;

    OnNewRecord=BEGIN
                  VALIDATE(Selected,TRUE);
                  VALIDATE(Default,FALSE);
                  VALIDATE("Application Type","Application Type"::Items);
                END;

    OnInsertRecord=BEGIN
                     IF VATAccountsGroup OR VATClausesGroup THEN
                       ERROR(VATAddIsNotallowedErr);
                   END;

    OnDeleteRecord=BEGIN
                     IF CheckExistingItemAndServiceWithVAT("VAT Prod. Posting Group","Application Type" = "Application Type"::Services ) THEN BEGIN
                       TrigerNotification(VATDeleteIsNotallowedErr);
                       EXIT(FALSE);
                     END;
                     IF VATAccountsGroup OR VATClausesGroup THEN BEGIN
                       SETRANGE(Selected,TRUE);
                       IF COUNT = 1 THEN BEGIN
                         TrigerNotification(VATEmptyErrorMsg);
                         EXIT(FALSE);
                       END;
                     END;
                   END;

    ActionList=ACTIONS
    {
      { 3       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 9   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies whether to include the VAT product posting group on the line.;
                           NOR=Angir om du vil inkludere mva-bokf›ringsgruppe - vare p† linjen.;
                           SVE=Anger om du vill inkludera r”relsebokf”ringsmallen med moms p† raden.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Selected;
                Visible=VATRatesGroup;
                OnValidate=BEGIN
                             IF Selected THEN
                               EXIT;

                             IF CheckExistingItemAndServiceWithVAT(xRec."VAT Prod. Posting Group",xRec."Application Type" = "Application Type"::Services) THEN BEGIN
                               TrigerNotification(VATDeleteIsNotallowedErr);
                               ERROR('');
                             END;
                           END;
                            }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the VAT specification of the involved item or resource to link transactions made for this record with the appropriate general ledger account according to the VAT posting setup.;
                           NOR=Angir mva-spesifikasjonen for den involverte varen eller ressursen for † koble transaksjoner som ble utf›rt for denne posten, med riktig finanskonto i henhold til det generelle mva-bokf›ringsoppsettet.;
                           SVE=Anger momsspecifikationen f”r den ber”rda artikeln eller resursen i syfte att koppla transaktioner som skapas f”r posten till det l„mpliga redovisningskontot enligt momsbokf”ringsinst„llningen.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Prod. Posting Group";
                OnValidate=BEGIN
                             IF CheckExistingItemAndServiceWithVAT(xRec."VAT Prod. Posting Group",xRec."Application Type" = "Application Type"::Services) THEN BEGIN
                               TrigerNotification(VATDeleteIsNotallowedErr);
                               ERROR('');
                             END;
                           END;
                            }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies how a cost recipient is linked to its cost source to provide cost forwarding according to the costing method.;
                           NOR=Angir hvordan en kostmottaker kobles til kostkilden, som angir hvordan kostnaden videresendes i henhold til kostberegningsmetoden.;
                           SVE=Anger hur en kostnadsmottagare „r kopplad till sin kostnadsk„lla, vilket anger hur kostnaden ska skickas vidare enligt v„rderingsprincipen.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Application Type";
                Visible=VATRatesGroup;
                OnValidate=BEGIN
                             IF CheckExistingItemAndServiceWithVAT(xRec."VAT Prod. Posting Group",xRec."Application Type" = "Application Type"::Services) THEN BEGIN
                               TrigerNotification(VATDeleteIsNotallowedErr);
                               ERROR('');
                             END;
                           END;
                            }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a description of the VAT product posting group.;
                           NOR=Angir en beskrivelse for mva-bokf›ringsgruppen - vare.;
                           SVE=Anger en beskrivning av produktbokf”ringsmallen med moms.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Prod. Posting Grp Desc.";
                Visible=VATRatesGroup }

    { 5   ;2   ;Field     ;
                Width=3;
                ToolTipML=[ENU=Specifies the VAT percentage used.;
                           NOR=Angir mva-prosenten som er brukt.;
                           SVE=Anger vilken momsprocentsats som anv„nds.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT %";
                Visible=VATRatesGroup }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the general ledger account number to which to post sales VAT, for the particular combination of VAT business posting group and VAT product posting group.;
                           NOR=Angir nummeret p† finanskontoen som du vil bokf›re salgsmva til for den bestemte kombinasjonen av mva-bokf›ringsgruppe - firma og mva-bokf›ringsgruppe - vare.;
                           SVE=Anger redovisningskontonumret till vilket momsen bokf”rs f”r just den kombinationen av r”relse- och produktbokf”ringsmall med moms.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales VAT Account";
                Visible=VATAccountsGroup }

    { 7   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the general ledger account number to which to post purchase VAT.;
                           NOR=Angir nummeret p† finanskontoen som inng†ende mva skal bokf›res til.;
                           SVE=Anger numret p† det redovisningskonto som du vill bokf”ra ing†ende moms p†.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Purchase VAT Account";
                Visible=VATAccountsGroup }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the general ledger account number to which you want to post reverse charge VAT (purchase VAT) for this combination of VAT business posting group and VAT product posting group, if you have selected the Reverse Charge VAT option in the VAT Calculation Type field.;
                           NOR=Angir nummeret for finanskontoen som du vil bokf›re snudd avregning (inng†ende mva) til for denne kombinasjonen av mva-bokf›ringsgruppe - firma og mva-bokf›ringsgruppe - vare, hvis du har valgt alternativet Snudd avregning i feltet Mva-beregningstype.;
                           SVE=Om du valde alternativet f”r omv„nd moms i f„ltet Momsber„kningstyp anger detta numret p† det redovisningskonto till vilket du vill bokf”ra omv„nd moms (ing†ende moms) f”r den aktuella kombinationen av r”relse- och produktbokf”ringsmall med moms.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Reverse Chrg. VAT Acc.";
                Visible=VATAccountsGroup }

    { 11  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a description of the VAT clause.;
                           NOR=Angir en beskrivelse for VAT-setning.;
                           SVE=Anger en beskrivning av momsklausulen.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Clause Desc";
                Visible=VATClausesGroup }

  }
  CODE
  {
    VAR
      VATNotification@1007 : Notification;
      VATRatesGroup@1000 : Boolean;
      VATAccountsGroup@1001 : Boolean;
      VATClausesGroup@1002 : Boolean;
      VATAddIsNotallowedErr@1003 : TextConst 'ENU=You can''t add accounts now because they won''t have settings like VAT rates. Go back to the VAT Rates for Items and Services page, add a line, and continue.;NOR=Du kan ikke legge til kontoer n† fordi de ikke vil ha innstillinger som mva-satser. G† tilbake til siden for mva-satsene for varer og tjenester, legg til en linje og fortsett.;SVE=Du kan inte l„gga till konton nu eftersom dessa inte skulle ha inst„llningar som t.ex. momssatser. G† tillbaka till sidan f”r momssatser f”r artiklar och tj„nster, l„gg till en rad och forts„tt.';
      VATDeleteIsNotallowedErr@1004 : TextConst 'ENU=You can''t delete or modify this VAT record because it is connected to existing item.;NOR=Du kan ikke slette eller endre denne mva-posten fordi den er tilknyttet en eksisterende vare.;SVE=Det g†r inte att ta bort eller „ndra den h„r momsposten eftersom den „r kopplad till en befintlig artikel.';
      VATEmptyErrorMsg@1009 : TextConst 'ENU=You can''t delete the record because the VAT setup would be empty.;NOR=Du kan ikke slette posten fordi mva-oppsettet vil v‘re tomt.;SVE=Du kan inte ta bort posten eftersom momskonfigurationen d† skulle vara tom.';

    [External]
    PROCEDURE ShowVATRates@1();
    BEGIN
      ResetView;
      VATRatesGroup := TRUE;
      RESET;
      SETRANGE(Default,FALSE);
      CurrPage.UPDATE;
    END;

    [External]
    PROCEDURE ShowVATAccounts@2();
    BEGIN
      ResetView;
      VATAccountsGroup := TRUE;
      ShowOnlySelectedSrvItem;
    END;

    [External]
    PROCEDURE ShowVATClauses@3();
    BEGIN
      ResetView;
      VATClausesGroup := TRUE;
      ShowOnlySelectedSrvItem;
    END;

    LOCAL PROCEDURE ResetView@4();
    BEGIN
      VATNotification.RECALL;
      VATRatesGroup := FALSE;
      VATAccountsGroup := FALSE;
      VATClausesGroup := FALSE;
    END;

    LOCAL PROCEDURE ShowOnlySelectedSrvItem@13();
    BEGIN
      SETRANGE(Selected,TRUE);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE TrigerNotification@15(NotificationMsg@1000 : Text);
    BEGIN
      VATNotification.RECALL;
      VATNotification.MESSAGE(NotificationMsg);
      VATNotification.SEND;
    END;

    [External]
    PROCEDURE HideNotification@5();
    VAR
      DummyGuid@1000 : GUID;
    BEGIN
      IF VATNotification.ID = DummyGuid THEN
        EXIT;
      VATNotification.MESSAGE := '';
      VATNotification.RECALL;
    END;

    BEGIN
    END.
  }
}

