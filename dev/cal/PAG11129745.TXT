OBJECT Page 11129745 Document Viewer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[DEU=Document Viewer;
               ENU=Document Viewer;
               NLD=Document Viewer];
    SourceTable=Table11072460;
    PageType=CardPart;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             ControlAddInReady := FALSE;
           END;

    OnAfterGetCurrRecord=BEGIN
                           HandleCurrentRecord;
                         END;

    ActionList=ACTIONS
    {
      { 1100525000;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525001;1 ;Action    ;
                      Name=SendAsMail;
                      CaptionML=[DEU=Sende Beleg;
                                 ENU=Send Document;
                                 NLD=Verstuur document];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SendMail;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentMailing@1100525001 : Codeunit 260;
                               BEGIN
                                 SaveContentToTempServerFile;
                                 DocumentMailing.EmailFile(FileName, STRSUBSTNO('%1.%2', "No.", FileExtension), '', '', '', EmailSubject, FALSE, 0);
                               END;
                                }
      { 1100528701;1 ;Action    ;
                      Name=SaveDocument;
                      CaptionML=[DEU=Beleg speichern;
                                 ENU=Save Document;
                                 NLD=Document opslaan];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Save;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 FileManagement@1100528700 : Codeunit 419;
                               BEGIN
                                 SaveContentToTempServerFile;
                                 FileManagement.DownloadHandler(FileName, 'Save Document', '', '', STRSUBSTNO('%1.%2', "No.", FileExtension));
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528500;;Container;
                ContainerType=ContentArea }

    { 11129745;1;Field    ;
                Name=ImageViewer;
                CaptionML=[DEU=Beleg;
                           ENU=Document;
                           NLD=Document];
                ControlAddIn=[Fps.Nav.Addin.DocumentViewer;PublicKeyToken=024bd013665211e9] }

  }
  CODE
  {
    VAR
      Text001@1100528510 : TextConst 'DEU=4PS Viewer kann nicht gestartet werden.\\(Kann Datei %1 nicht laden);ENU=4PS viewer can not be started.\\(File %1 can not be loaded);NLD=4PS viewer kan niet gestart worden.\\(Kan file %1 niet laden)';
      DocumentViewerSetup@1100525001 : Record 11072461;
      FileContent@1100525004 : BigText;
      FileExtension@1100525005 : Text;
      EmailSubject@1100525007 : Text;
      Text002@1100528100 : TextConst 'DEU=Der Document Viewer kann die Datei ''%1'' nicht anzeigen.;ENU=The Document Viewer cannot display file ''%1''.;NLD=De Document Viewer kan het bestand ''%1'' niet weergeven.';
      Text003@1100528101 : TextConst 'DEU=%1 wird nicht unterstÅtzt.;ENU=%1 is not supported.;NLD=%1 is niet ondersteund.';
      FileName@1100528102 : Text;
      ControlAddInReady@1100528700 : Boolean;

    LOCAL PROCEDURE LoadDocumentUsingBase64@1100528502(FileContent2@1100528500 : BigText;Extension@1100528501 : Text;FileName2@1100525000 : Text);
    BEGIN
      IF NOT(Extension IN ['pdf', 'tif']) THEN
        CurrPage.ImageViewer.DisplayMessage(STRSUBSTNO(Text002, "No."), TRUE)
      ELSE BEGIN
        IF (FileContent.LENGTH <> FileContent2.LENGTH) OR (FileName <> FileName2) THEN BEGIN
          FileContent := FileContent2;
          FileName := FileName2;
          FileExtension := Extension;

          IF PlaceDocument(FileContent, FileExtension) THEN;
        END;
      END;
    END;

    LOCAL PROCEDURE CalcYearAndMonth@1100528501(InputDate@1100528502 : Date) : Text[8];
    VAR
      Month@1100528500 : Integer;
      Year@1100528501 : Integer;
    BEGIN
      Month := DATE2DMY(InputDate,2);
      Year := DATE2DMY(InputDate,3);

      IF Month < 10 THEN
        EXIT(STRSUBSTNO('%1\0%2\',Year,Month))
      ELSE
        EXIT(STRSUBSTNO('%1\%2\',Year,Month))
    END;

    PROCEDURE SetEmailSubject@1100525001(Subject@1100525000 : Text);
    BEGIN
      EmailSubject := Subject;
    END;

    LOCAL PROCEDURE HandleCurrentRecord@1100528100();
    VAR
      DCDocument@1100528109 : Record 6085590;
      DCFile@1100528107 : Record 6085608;
      DotNetSystemConvert@1100528106 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      DotNetSystemIOFile@1100528105 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File";
      DocumentManagementWebservice@1100528104 : Codeunit 11012405;
      ErrorMessage@1100528103 : Text;
      FileManagement@1100528102 : Codeunit 419;
      WhiteVisionManagement@1100528100 : Codeunit 11012084;
      FileContent2@1100528101 : BigText;
      FileName2@1100528108 : Text;
    BEGIN
      IF "No." = '' THEN BEGIN
        CurrPage.ImageViewer.DisplayMessage('', TRUE);
        CLEAR(FileContent);
        CLEAR(FileName);
        EXIT;
      END;

      CASE "Source Type" OF
        "Source Type"::DocumentCapture:
          BEGIN
            DCDocument.GET("No.");
            IF DCDocument.GetPdfFile(DCFile) THEN BEGIN
              FileContent2.ADDTEXT(DotNetSystemConvert.ToBase64String(DotNetSystemIOFile.ReadAllBytes(DCFile.GetFilePath())));
              LoadDocumentUsingBase64(FileContent2, 'pdf', "No." + '.pdf');
            END ELSE IF DCDocument.GetTiffFile(DCFile) THEN BEGIN
              FileContent2.ADDTEXT(DotNetSystemConvert.ToBase64String(DotNetSystemIOFile.ReadAllBytes(DCFile.GetFilePath())));
              LoadDocumentUsingBase64(FileContent2, 'tif', "No." + '.tif');
            END ELSE BEGIN
              CurrPage.ImageViewer.DisplayMessage(STRSUBSTNO(Text002, "No."), TRUE);
              CLEAR(FileContent);
              CLEAR(FileName);
            END;
          END;
        "Source Type"::"4PSExternalDocuments":
          BEGIN
            IF NOT DocumentManagementWebservice.GetDocumentContent("No.", FileContent2, ErrorMessage) THEN BEGIN
              CurrPage.ImageViewer.DisplayMessage(ErrorMessage, TRUE);
              CLEAR(FileContent);
              CLEAR(FileName);
              EXIT;
            END;
            FileName2 := DocumentManagementWebservice.GetDocumentShortFileName("No.");
            FileExtension := LOWERCASE(FileManagement.GetExtension(FileName2));
            LoadDocumentUsingBase64(FileContent2, FileExtension, Rec."No." + '.' + FileExtension);
          END;
        "Source Type"::WhiteVision:
          BEGIN
            IF NOT WhiteVisionManagement.GetDocumentFilePath(DT2DATE("Date/Time"), "No.", FileName2) THEN BEGIN
              CurrPage.ImageViewer.DisplayMessage(STRSUBSTNO(Text001, FileName2), TRUE);
              CLEAR(FileContent);
              CLEAR(FileName);
              EXIT;
            END;
            FileContent2.ADDTEXT(DotNetSystemConvert.ToBase64String(DotNetSystemIOFile.ReadAllBytes(FileName2)));
            FileExtension := LOWERCASE(FileManagement.GetExtension(FileName2));
            LoadDocumentUsingBase64(FileContent2, FileExtension, "No." + '.' + FileExtension);
          END;
        ELSE BEGIN
          CurrPage.ImageViewer.DisplayMessage(STRSUBSTNO(Text003, "Source Type"), TRUE);
          CLEAR(FileContent);
          CLEAR(FileName);
        END;
      END;
    END;

    PROCEDURE ResetDocument@1100528102();
    BEGIN
      CurrPage.ImageViewer.DisplayMessage('', TRUE);
    END;

    PROCEDURE PlaceDocumentUsingRecord@1100528105(VAR DocumentViewerTempDoc@1100528100 : TEMPORARY Record 11072460);
    BEGIN
      COPY(DocumentViewerTempDoc);
      HandleCurrentRecord;
    END;

    [TryFunction]
    LOCAL PROCEDURE PlaceDocument@1100528101(FileContent@1100528100 : BigText;FileExtension@1100528101 : Text);
    BEGIN
      IF ControlAddInReady THEN
        CurrPage.ImageViewer.PlaceDocument(FileContent, FileExtension);
    END;

    LOCAL PROCEDURE SaveContentToTempServerFile@1100528700();
    VAR
      DotNetSystemConvert@1100528702 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      DotNetSystemIOFile@1100528701 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File";
      FileManagement@1100528700 : Codeunit 419;
    BEGIN
      IF "No." = '' THEN
        EXIT;

      FileName := FileManagement.ServerTempFileName(FileExtension);
      DotNetSystemIOFile.WriteAllBytes(FileName, DotNetSystemConvert.FromBase64String(FileContent));
    END;

    EVENT ImageViewer@-11129745::ControlAddInReady@6();
    VAR
      DocumentViewerConfig@1100528700 : DotNet "'Fps.Nav.Addin.DocumentViewer, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Addin.DocumentViewer.DocumentViewerConfig";
      DocumentViewerTranslations@1100525000 : DotNet "'Fps.Nav.Addin.DocumentViewer, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Addin.DocumentViewer.DocumentViewerTranslations";
    BEGIN
      ControlAddInReady := TRUE;
      DocumentViewerSetup.GET;
      WITH DocumentViewerSetup DO BEGIN
        GetDocumentViewerTranslations(DocumentViewerTranslations);
        DocumentViewerConfig := DocumentViewerConfig.DocumentViewerConfig;
        DocumentViewerConfig.displayErrors(DisplayErrors);
        DocumentViewerConfig.displayWarnings(DisplayWarnings);
        DocumentViewerConfig.displayLogs(DisplayLogs);
        DocumentViewerConfig.defaultZoomLevel("Default Zoom Level");
        DocumentViewerConfig.annotationFontSize("Annotation Font Size");
        DocumentViewerConfig.annotationsDefaultOpen("Annotations Always Open");
        DocumentViewerConfig.showThumbnails(FALSE);
        CurrPage.ImageViewer.DocumentViewerInitialize(DocumentViewerConfig, DocumentViewerTranslations);
      END;

      IF FileContent.LENGTH > 0 THEN
        CurrPage.ImageViewer.PlaceDocument(FileContent, FileExtension);
    END;

    BEGIN
    END.
  }
}

