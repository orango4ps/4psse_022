OBJECT Codeunit 5818 Undo Service Shipment Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    TableNo=5991;
    Permissions=TableData 339=rmd,
                TableData 5902=imd,
                TableData 5907=i,
                TableData 5908=im,
                TableData 5991=imd,
                TableData 6507=ri;
    OnRun=VAR
            ConfirmManagement@1002 : Codeunit 27;
            ConfMessage@1000 : Text[250];
            IsHandled@1001 : Boolean;
          BEGIN
            IsHandled := FALSE;
            OnBeforeOnRun(Rec,IsHandled);
            IF IsHandled THEN
              EXIT;

            IF NOT FIND('-') THEN
              EXIT;

            ConfMessage := Text000;

            IF CheckComponentsAdjusted(Rec) THEN
              ConfMessage :=
                STRSUBSTNO(Text004,FIELDCAPTION("Service Item No."),FORMAT("Service Item No.")) +
                Text000;

            IF NOT HideDialog THEN
              IF NOT ConfirmManagement.ConfirmProcess(ConfMessage,TRUE) THEN
                EXIT;

            LOCKTABLE;
            ServShptLine.COPY(Rec);
            Code;
            Rec := ServShptLine;
          END;

  }
  CODE
  {
    VAR
      ServShptHeader@1004 : Record 5990;
      ServShptLine@1001 : Record 5991;
      TempGlobalItemLedgEntry@1026 : TEMPORARY Record 32;
      TempGlobalItemEntryRelation@1025 : TEMPORARY Record 6507;
      InvtSetup@1007 : Record 313;
      TempWhseJnlLine@1011 : TEMPORARY Record 7311;
      WhseUndoQty@1010 : Codeunit 7320;
      UndoPostingMgt@1024 : Codeunit 5817;
      ItemJnlPostLine@1000 : Codeunit 22;
      Text000@1003 : TextConst 'ENU=Do you want to undo the selected shipment line(s)?;NOR=Vil du angre valgt(e) fõlgeseddellinje(r)?;SVE=Vill du Üngra markerade utleveransrader?';
      Text001@1009 : TextConst 'ENU=Undo quantity posting...;NOR=Angre antallsbokfõring...;SVE=èterstÑll antalsbokfîring...';
      Text002@1002 : TextConst 'ENU=There is not enough space to insert correction lines.;NOR=Det er ikke nok plass til Ü sette inn korrigeringslinjer.;SVE=Det finns inte tillrÑckligt med utrymme att infoga rÑttningsrader.';
      InvtAdjmt@1008 : Codeunit 5895;
      Text003@1006 : TextConst 'ENU=Checking lines...;NOR=Kontrollerer linjer...;SVE=Kontrollerar rader...';
      NextLineNo@1014 : Integer;
      HideDialog@1005 : Boolean;
      Text004@1012 : TextConst 'ENU=The component list for %1 %2 was changed. You may need to adjust the list manually.\;NOR=Komponentlisten for %1 %2 er endret. Du mÜ kanskje justere listen manuelt.\;SVE=Komponentlistan fîr %1 %2 har Ñndrats. Du kan behîva justera listan manuellt.\';
      Text005@1015 : TextConst 'ENU=Some shipment lines may have unused service items. Do you want to delete them?;NOR=Det kan finnes ubrukte servicevarer pÜ noen fõlgeseddellinjer. Vil du slette disse?;SVE=NÜgra utleveransrader kan ha oanvÑnda serviceartiklar. Vill du ta bort dem?';
      AlreadyReversedErr@1016 : TextConst 'ENU=This service shipment has already been reversed.;NOR=Denne servicefõlgeseddelen er allerede tilbakefõrt.';

    LOCAL PROCEDURE CheckComponentsAdjusted@16(VAR ServiceShptLine@1000 : Record 5991) : Boolean;
    VAR
      LocalServShptLine@1001 : Record 5991;
    BEGIN
      LocalServShptLine.COPY(ServiceShptLine);
      WITH LocalServShptLine DO BEGIN
        SETFILTER("Spare Part Action",'%1|%2',
          "Spare Part Action"::"Component Replaced","Spare Part Action"::"Component Installed");
        SETFILTER("Service Item No.",'<>%1','');
        EXIT(NOT ISEMPTY);
      END;
    END;

    [External]
    PROCEDURE SetHideDialog@8(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    LOCAL PROCEDURE Code@2();
    VAR
      PostedWhseShptLine@1005 : Record 7323;
      SalesLine@1006 : Record 37;
      ServItem@1003 : Record 5940;
      ServLedgEntriesPost@1007 : Codeunit 5912;
      ConfirmManagement@1010 : Codeunit 27;
      Window@1001 : Dialog;
      ItemShptEntryNo@1002 : Integer;
      ServLedgEntryNo@1008 : Integer;
      WarrantyLedgEntryNo@1009 : Integer;
      DeleteServItems@1000 : Boolean;
      PostedWhseShptLineFound@1004 : Boolean;
    BEGIN
      WITH ServShptLine DO BEGIN
        CLEAR(ItemJnlPostLine);
        SETRANGE(Correction,FALSE);
        FINDFIRST;
        REPEAT
          IF NOT HideDialog THEN
            Window.OPEN(Text003);
          CheckServShptLine(ServShptLine);
        UNTIL NEXT = 0;

        ServItem.SETCURRENTKEY("Sales/Serv. Shpt. Document No.");
        ServItem.SETRANGE("Sales/Serv. Shpt. Document No.","Document No.");
        ServItem.SETRANGE("Sales/Serv. Shpt. Line No.","Line No.");
        ServItem.SETRANGE("Shipment Type",ServItem."Shipment Type"::Service);

        IF ServItem.FINDFIRST THEN
          IF NOT HideDialog THEN
            DeleteServItems := ConfirmManagement.ConfirmProcess(Text005,TRUE)
          ELSE
            DeleteServItems := TRUE;

        ServLedgEntriesPost.InitServiceRegister(ServLedgEntryNo,WarrantyLedgEntryNo);
        FIND('-');
        REPEAT
          TempGlobalItemLedgEntry.RESET;
          IF NOT TempGlobalItemLedgEntry.ISEMPTY THEN
            TempGlobalItemLedgEntry.DELETEALL;
          TempGlobalItemEntryRelation.RESET;
          IF NOT TempGlobalItemEntryRelation.ISEMPTY THEN
            TempGlobalItemEntryRelation.DELETEALL;

          IF NOT HideDialog THEN
            Window.OPEN(Text001);

          PostedWhseShptLineFound :=
            WhseUndoQty.FindPostedWhseShptLine(
              PostedWhseShptLine,
              DATABASE::"Service Shipment Line",
              "Document No.",
              DATABASE::"Service Line",
              SalesLine."Document Type"::Order,
              "Order No.",
              "Order Line No.");

          IF Type = Type::Item THEN
            ItemShptEntryNo := PostItemJnlLine(ServShptLine)
          ELSE
            ItemShptEntryNo := 0;

          IF Type = Type::Resource THEN
            PostResJnlLine(ServShptLine);

          InsertNewShipmentLine(ServShptLine,ItemShptEntryNo);
          OnAfterInsertNewShipmentLine(ServShptLine,PostedWhseShptLine,PostedWhseShptLineFound);

          IF PostedWhseShptLineFound THEN
            WhseUndoQty.UndoPostedWhseShptLine(PostedWhseShptLine);

          ServLedgEntriesPost.ReverseServLedgEntry(ServShptLine);
          IF Type IN [Type::Item,Type::Resource] THEN
            ServLedgEntriesPost.ReverseWarrantyEntry(ServShptLine);

          UpdateOrderLine(ServShptLine);
          IF PostedWhseShptLineFound THEN
            WhseUndoQty.UpdateShptSourceDocLines(PostedWhseShptLine);

          IF DeleteServItems THEN
            DeleteServShptLineServItems(ServShptLine);

          "Quantity Invoiced" := Quantity;
          "Qty. Invoiced (Base)" := "Quantity (Base)";
          "Qty. Shipped Not Invoiced" := 0;
          "Qty. Shipped Not Invd. (Base)" := 0;
          Correction := TRUE;
          MODIFY;

        UNTIL NEXT = 0;
        ServLedgEntriesPost.FinishServiceRegister(ServLedgEntryNo,WarrantyLedgEntryNo);

        InvtSetup.GET;
        IF InvtSetup."Automatic Cost Adjustment" <>
           InvtSetup."Automatic Cost Adjustment"::Never
        THEN BEGIN
          ServShptHeader.GET("Document No.");
          InvtAdjmt.SetProperties(TRUE,InvtSetup."Automatic Cost Posting");
          InvtAdjmt.SetJobUpdateProperties(TRUE);
          InvtAdjmt.MakeMultiLevelAdjmt;
        END;
        WhseUndoQty.PostTempWhseJnlLine(TempWhseJnlLine);
      END;

      OnAfterCode(ServShptLine);
    END;

    LOCAL PROCEDURE GetCorrectiveShptLineNoStep@29(DocumentNo@1001 : Code[20];LineNo@1002 : Integer) LineSpacing : Integer;
    VAR
      TestServShptLine@1000 : Record 5991;
    BEGIN
      TestServShptLine.SETRANGE("Document No.",DocumentNo);
      TestServShptLine."Document No." := DocumentNo;
      TestServShptLine."Line No." := LineNo;
      TestServShptLine.FIND('=');

      IF TestServShptLine.FIND('>') THEN BEGIN
        LineSpacing := (TestServShptLine."Line No." - LineNo) DIV 2;
        IF LineSpacing = 0 THEN
          ERROR(Text002);
      END ELSE
        LineSpacing := 10000;
    END;

    LOCAL PROCEDURE CheckServShptLine@3(VAR ServShptLine@1001 : Record 5991);
    VAR
      TempItemLedgEntry@1015 : TEMPORARY Record 32;
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCheckServShptLine(ServShptLine,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH ServShptLine DO BEGIN
        TESTFIELD(Quantity);
        TESTFIELD("Qty. Shipped Not Invoiced",Quantity);
        IF Correction THEN
          ERROR(AlreadyReversedErr);
        UndoPostingMgt.TestServShptLine(ServShptLine);
        IF Type = Type::Item THEN BEGIN
          UndoPostingMgt.CollectItemLedgEntries(TempItemLedgEntry,DATABASE::"Service Shipment Line",
            "Document No.","Line No.","Quantity (Base)","Item Shpt. Entry No.");
          UndoPostingMgt.CheckItemLedgEntries(TempItemLedgEntry,"Line No.");
        END;
      END;
    END;

    LOCAL PROCEDURE PostItemJnlLine@5(ServShptLine@1000 : Record 5991) : Integer;
    VAR
      ItemJnlLine@1001 : Record 83;
      ServLine@1006 : Record 5902;
      ServShptHeader@1004 : Record 5990;
      SourceCodeSetup@1002 : Record 242;
      TempApplyToEntryList@1005 : TEMPORARY Record 32;
    BEGIN
      WITH ServShptLine DO BEGIN
        SourceCodeSetup.GET;
        ServShptHeader.GET("Document No.");
        ItemJnlLine.INIT;
        ItemJnlLine."Entry Type" := ItemJnlLine."Entry Type"::Sale;
        ItemJnlLine."Document Type" := ItemJnlLine."Document Type"::"Service Shipment";
        ItemJnlLine."Document No." := ServShptHeader."No.";
        ItemJnlLine."Document Line No." := "Line No." + GetCorrectiveShptLineNoStep("Document No.","Line No.");
        ItemJnlLine."Item No." := "No.";
        ItemJnlLine."Posting Date" := "Posting Date";
        ItemJnlLine."Document No." := "Document No.";
        ItemJnlLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        ItemJnlLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        ItemJnlLine."Source Posting Group" := ServShptHeader."Customer Posting Group";
        ItemJnlLine."Salespers./Purch. Code" := ServShptHeader."Salesperson Code";
        ItemJnlLine."Country/Region Code" := ServShptHeader."Country/Region Code";
        ItemJnlLine."Posting No. Series" := ServShptHeader."No. Series";
        ItemJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        ItemJnlLine."Location Code" := "Location Code";
        ItemJnlLine."Source Code" := SourceCodeSetup.Sales;
        ItemJnlLine."Applies-to Entry" := "Item Shpt. Entry No.";
        ItemJnlLine.Correction := TRUE;
        ItemJnlLine."Variant Code" := "Variant Code";
        ItemJnlLine."Bin Code" := "Bin Code";
        ItemJnlLine.Quantity := -"Quantity (Base)";
        ItemJnlLine."Quantity (Base)" := -"Quantity (Base)";
        ItemJnlLine."Document Date" := ServShptHeader."Document Date";

        OnAfterCopyItemJnlLineFromServShpt(ItemJnlLine,ServShptHeader,ServShptLine);

        WhseUndoQty.InsertTempWhseJnlLine(ItemJnlLine,
          DATABASE::"Service Line",
          ServLine."Document Type"::Order,
          "Order No.",
          "Order Line No.",
          TempWhseJnlLine."Reference Document"::"Posted Shipment",
          TempWhseJnlLine,
          NextLineNo);

        IF "Item Shpt. Entry No." <> 0 THEN BEGIN
          ItemJnlPostLine.RUN(ItemJnlLine);
          EXIT(ItemJnlLine."Item Shpt. Entry No.");
        END;
        UndoPostingMgt.CollectItemLedgEntries(TempApplyToEntryList,DATABASE::"Service Shipment Line",
          "Document No.","Line No.","Quantity (Base)","Item Shpt. Entry No.");

        UndoPostingMgt.PostItemJnlLineAppliedToList(ItemJnlLine,TempApplyToEntryList,
          Quantity,"Quantity (Base)",TempGlobalItemLedgEntry,TempGlobalItemEntryRelation);

        EXIT(0); // "Item Shpt. Entry No."
      END;
    END;

    LOCAL PROCEDURE InsertNewShipmentLine@1(OldServShptLine@1000 : Record 5991;ItemShptEntryNo@1001 : Integer);
    VAR
      NewServShptLine@1002 : Record 5991;
    BEGIN
      WITH OldServShptLine DO BEGIN
        NewServShptLine.RESET;
        NewServShptLine.INIT;
        NewServShptLine.COPY(OldServShptLine);
        NewServShptLine."Line No." := "Line No." + GetCorrectiveShptLineNoStep("Document No.","Line No.");
        NewServShptLine."Item Shpt. Entry No." := ItemShptEntryNo;
        NewServShptLine."Appl.-to Service Entry" := "Appl.-to Service Entry";
        NewServShptLine.Quantity := -Quantity;
        NewServShptLine."Qty. Shipped Not Invoiced" := 0;
        NewServShptLine."Qty. Shipped Not Invd. (Base)" := 0;
        NewServShptLine."Quantity (Base)" := -"Quantity (Base)";
        NewServShptLine."Quantity Invoiced" := NewServShptLine.Quantity;
        NewServShptLine."Qty. Invoiced (Base)" := NewServShptLine."Quantity (Base)";
        NewServShptLine.Correction := TRUE;
        NewServShptLine."Dimension Set ID" := "Dimension Set ID";
        OnBeforeNewServiceShptLineInsert(NewServShptLine,OldServShptLine);
        NewServShptLine.INSERT;

        InsertItemEntryRelation(TempGlobalItemEntryRelation,NewServShptLine);
      END;
    END;

    LOCAL PROCEDURE UpdateOrderLine@4(ServShptLine@1000 : Record 5991);
    VAR
      ServLine@1001 : Record 5902;
    BEGIN
      WITH ServShptLine DO BEGIN
        ServLine.GET(ServLine."Document Type"::Order,"Order No.","Order Line No.");
        UndoPostingMgt.UpdateServLine(ServLine,Quantity,"Quantity (Base)",TempGlobalItemLedgEntry);
        OnAfterUpdateOrderLine(ServLine,ServShptLine);
      END;
    END;

    LOCAL PROCEDURE InsertItemEntryRelation@12(VAR TempItemEntryRelation@1003 : TEMPORARY Record 6507;NewServShptLine@1000 : Record 5991);
    VAR
      ItemEntryRelation@1002 : Record 6507;
    BEGIN
      IF TempItemEntryRelation.FIND('-') THEN
        REPEAT
          ItemEntryRelation := TempItemEntryRelation;
          ItemEntryRelation.TransferFieldsServShptLine(NewServShptLine);
          ItemEntryRelation.INSERT;
        UNTIL TempItemEntryRelation.NEXT = 0;
    END;

    LOCAL PROCEDURE PostResJnlLine@11(VAR ServiceShptLine@1000 : Record 5991);
    VAR
      ResJnlLine@1002 : Record 207;
      SrcCodeSetup@1003 : Record 242;
      ServiceShptHeader@1001 : Record 5990;
      ResJnlPostLine@1004 : Codeunit 212;
      TimeSheetMgt@1005 : Codeunit 950;
    BEGIN
      ResJnlLine.INIT;
      SrcCodeSetup.GET;
      WITH ResJnlLine DO BEGIN
        ServiceShptHeader.GET(ServiceShptLine."Document No.");
        "Entry Type" := "Entry Type"::Usage;
        "Document No." := ServiceShptLine."Document No.";
        "Posting Date" := ServiceShptLine."Posting Date";
        "Document Date" := ServiceShptHeader."Document Date";
        "Resource No." := ServiceShptLine."No.";
        Description := ServiceShptLine.Description;
        "Work Type Code" := ServiceShptLine."Work Type Code";
        Quantity := -ServiceShptLine."Qty. Shipped Not Invoiced";
        "Unit Cost" := ServiceShptLine."Unit Cost (LCY)";
        "Total Cost" := ServiceShptLine."Unit Cost (LCY)" * Quantity;
        "Unit Price" := ServiceShptLine."Unit Price";
        "Total Price" := "Unit Price" * Quantity;
        "Shortcut Dimension 1 Code" := ServiceShptHeader."Shortcut Dimension 1 Code";
        "Shortcut Dimension 2 Code" := ServiceShptHeader."Shortcut Dimension 2 Code";
        "Dimension Set ID" := ServiceShptLine."Dimension Set ID";
        "Unit of Measure Code" := ServiceShptLine."Unit of Measure Code";
        "Qty. per Unit of Measure" := ServiceShptLine."Qty. per Unit of Measure";
        "Source Code" := SrcCodeSetup."Service Management";
        "Gen. Bus. Posting Group" := ServiceShptLine."Gen. Bus. Posting Group";
        "Gen. Prod. Posting Group" := ServiceShptLine."Gen. Prod. Posting Group";
        "Posting No. Series" := ServiceShptHeader."No. Series";
        "Reason Code" := ServiceShptHeader."Reason Code";
        "Source Type" := "Source Type"::Customer;
        "Source No." := ServiceShptLine."Bill-to Customer No.";
        "Qty. per Unit of Measure" := ServiceShptLine."Qty. per Unit of Measure";
        OnBeforePostResJnlLine(ResJnlLine,ServiceShptLine);
        ResJnlPostLine.RunWithCheck(ResJnlLine);
      END;

      TimeSheetMgt.CreateTSLineFromServiceShptLine(ServiceShptLine);
    END;

    LOCAL PROCEDURE DeleteServShptLineServItems@20(ServShptLine@1000 : Record 5991);
    VAR
      ServItem@1003 : Record 5940;
    BEGIN
      ServItem.SETCURRENTKEY("Sales/Serv. Shpt. Document No.","Sales/Serv. Shpt. Line No.");
      ServItem.SETRANGE("Sales/Serv. Shpt. Document No.",ServShptLine."Document No.");
      ServItem.SETRANGE("Sales/Serv. Shpt. Line No.",ServShptLine."Line No.");
      ServItem.SETRANGE("Shipment Type",ServItem."Shipment Type"::Service);
      IF ServItem.FIND('-') THEN
        REPEAT
          IF ServItem.CheckIfCanBeDeleted = '' THEN
            IF ServItem.DELETE(TRUE) THEN;
        UNTIL ServItem.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCode@13(VAR ServiceShipmentLine@1000 : Record 5991);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyItemJnlLineFromServShpt@21(VAR ItemJournalLine@1000 : Record 83;ServiceShipmentHeader@1001 : Record 5990;ServiceShipmentLine@1002 : Record 5991);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertNewShipmentLine@10(VAR ServiceShipmentLine@1000 : Record 5991;PostedWhseShptLine@1001 : Record 7323;VAR PostedWhseShptLineFound@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateOrderLine@6(VAR ServiceLine@1000 : Record 5902;VAR ServiceShptLine@1001 : Record 5991);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckServShptLine@7(VAR ServiceShptLine@1000 : Record 5991;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeNewServiceShptLineInsert@15(VAR NewServiceShipmentLine@1000 : Record 5991;OldServiceShipmentLine@1001 : Record 5991);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostResJnlLine@9(VAR ResJournalLine@1000 : Record 207;VAR ServiceShptLine@1001 : Record 5991);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeOnRun@14(VAR ServiceShipmentLine@1000 : Record 5991;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

