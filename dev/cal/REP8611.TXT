OBJECT Report 8611 Create Customer Journal Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Debitoren Buch.-Blattzeilen erstellen;
               ENU=Create Customer Journal Lines;
               NLD=Klantdagboekregels maken;
               NOR=Opprett kundekladdelinjer;
               SVE=Skapa kundjournalrader];
    ApplicationArea=#Basic,#Suite;
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   MESSAGE(Text004);
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table18;
               DataItemTableView=SORTING(No.)
                                 WHERE(Bill-to Customer No.=FILTER(''));
               OnPreDataItem=BEGIN
                               CheckJournalTemplate;
                               CheckBatchName;
                               CheckPostingDate;

                               GenJnlLine.SETRANGE("Journal Template Name",JournalTemplate);
                               GenJnlLine.SETRANGE("Journal Batch Name",BatchName);
                               IF GenJnlLine.FINDLAST THEN
                                 LineNo := GenJnlLine."Line No." + 10000
                               ELSE
                                 LineNo := 10000;

                               GenJnlBatch.GET(JournalTemplate,BatchName);
                               IF TemplateCode <> '' THEN
                                 StdGenJournal.GET(JournalTemplate,TemplateCode);
                             END;

               OnAfterGetRecord=VAR
                                  StdGenJournalLine@1000 : Record 751;
                                BEGIN
                                  GenJnlLine.INIT;
                                  IF GetStandardJournalLine THEN BEGIN
                                    Initialize(StdGenJournal,GenJnlBatch.Name);

                                    StdGenJournalLine.SETRANGE("Journal Template Name",StdGenJournal."Journal Template Name");
                                    StdGenJournalLine.SETRANGE("Standard Journal Code",StdGenJournal.Code);
                                    IF StdGenJournalLine.FINDSET THEN
                                      REPEAT
                                        CopyGenJnlFromStdJnl(StdGenJournalLine,GenJnlLine);

                                        IF PostingDate <> 0D THEN
                                          GenJnlLine.VALIDATE("Posting Date",PostingDate);

                                        GenJnlLine.VALIDATE("Document Type",DocumentTypes);
                                        GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::Customer);
                                        GenJnlLine.VALIDATE("Account No.","No.");
                                        IF (GenJnlBatch."Bal. Account Type" = GenJnlBatch."Bal. Account Type"::"G/L Account") AND
                                           (GenJnlBatch."Bal. Account No." <> '')
                                        THEN BEGIN
                                          GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
                                          GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
                                        END ELSE
                                          IF "Customer Posting Group" <> '' THEN
                                            IF CustPostGrp.GET("Customer Posting Group") THEN BEGIN
                                              GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
                                              GenJnlLine.VALIDATE("Bal. Account No.",CustPostGrp."Receivables Account");
                                            END;

                                        IF DocumentDate <> 0D THEN BEGIN
                                          GenJnlLine.VALIDATE("Posting Date",DocumentDate);
                                          GenJnlLine."Posting Date" := PostingDate;
                                        END;

                                        GenJnlLine.MODIFY(TRUE);
                                      UNTIL StdGenJournalLine.NEXT = 0;
                                  END ELSE BEGIN
                                    GenJnlLine.VALIDATE("Journal Template Name",GenJnlLine.GETFILTER("Journal Template Name"));
                                    GenJnlLine.VALIDATE("Journal Batch Name",BatchName);
                                    GenJnlLine."Line No." := LineNo;
                                    GenJnlLine.SetUpNewLine(LastGenJnlLine,0,TRUE);
                                    LineNo := LineNo + 10000;

                                    IF PostingDate <> 0D THEN
                                      GenJnlLine.VALIDATE("Posting Date",PostingDate);

                                    GenJnlLine.VALIDATE("Document Type",DocumentTypes);
                                    IF (GenJnlLine."Document No." = '') AND (DocumentNo <> '') THEN
                                      GenJnlLine.VALIDATE("Document No.",DocumentNo);
                                    GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::Customer);
                                    GenJnlLine.VALIDATE("Account No.","No.");
                                    IF (GenJnlBatch."Bal. Account Type" = GenJnlBatch."Bal. Account Type"::"G/L Account") AND
                                       (GenJnlBatch."Bal. Account No." <> '')
                                    THEN BEGIN
                                      GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
                                      GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
                                    END ELSE
                                      IF "Customer Posting Group" <> '' THEN
                                        IF CustPostGrp.GET("Customer Posting Group") THEN BEGIN
                                          GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
                                          GenJnlLine.VALIDATE("Bal. Account No.",CustPostGrp."Receivables Account");
                                        END;
                                    IF DocumentDate <> 0D THEN BEGIN
                                      GenJnlLine.VALIDATE("Posting Date",DocumentDate);
                                      GenJnlLine."Posting Date" := PostingDate;
                                    END;

                                    IF NOT GenJnlLine.INSERT(TRUE) THEN
                                      GenJnlLine.MODIFY(TRUE);
                                  END;
                                END;

               ReqFilterFields=No.,Currency Code,Country/Region Code,Salesperson Code,Customer Posting Group,Blocked }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PostingDate = 0D THEN
                     PostingDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Belegart;
                             ENU=Document Type;
                             NLD=Documenttype;
                             NOR=Bilagstype;
                             SVE=Dokumenttyp];
                  ToolTipML=[DEU=Gibt den Typ des Dokuments an, das vom Bericht oder der Stapelverarbeitung verarbeitet wird.;
                             ENU=Specifies the type of document that is processed by the report or batch job.;
                             NLD=Hiermee wordt het type opgegeven van het document dat wordt verwerkt door het rapport of de batchverwerking.;
                             NOR=Angir typen dokument som behandles av rapporten eller kjõrselen.;
                             SVE=Anger dokumenttypen som rapporten eller buntjobbet bearbetar.];
                  OptionCaptionML=[DEU=" ,Zahlung,Rechnung,Gutschrift,Zinsrechnung,Mahnung,Erstattung";
                                   ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                   NLD=" ,Betaling,Factuur,Creditnota,Rentefactuur,Aanmaning,Terugbetaling";
                                   NOR=" ,Betaling,Faktura,Kreditnota,Rentenota,Purring,Refusjon";
                                   SVE=" ,Betalning,Faktura,Kreditnota,RÑntefaktura,BetalningspÜminnelse,èterbetalning"];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentTypes }

      { 3   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Dokumentnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt die Standardbelegnummer der Buch.-Blattzeile an.;
                             ENU=Specifies the default document number of the journal line.;
                             NLD=Hiermee wordt het standaarddocumentnummer van de dagboekregel opgegeven.;
                             NOR=Angir standard dokumentnummer pÜ kladdelinjen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentNo }

      { 4   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokf¬îringsdatum];
                  ToolTipML=[DEU=Legt das Datum fÅr die Buchung dieser Stapelverarbeitung fest. StandardmÑ·ig wird das Arbeitsdatum eingegeben, Sie kînnen es allerdings Ñndern.;
                             ENU=Specifies the date for the posting of this batch job. By default, the working date is entered, but you can change it.;
                             NLD=Hiermee wordt de datum opgegeven voor de boeking van deze batchverwerking. Standaard wordt de werkdatum ingevuld in dit veld, maar u kunt deze wijzigen.;
                             NOR=Angir datoen for bokfõring av denne kjõrselen. Som standard oppgis arbeidsdatoen, men du kan endre den.;
                             SVE=Anger datumet fîr bokfîringen av detta batch-jobb. Arbetsdatumet anges standard, men du kan Ñndra det.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               CheckPostingDate;
                             END;
                              }

      { 6   ;2   ;Field     ;
                  CaptionML=[DEU=Belegdatum;
                             ENU=Document Date;
                             NLD=Documentdatum;
                             NOR=Bilagsdato;
                             SVE=Dokumentdatum];
                  ToolTipML=[DEU=Legt das Belegdatum fest, das in die erstellten DatensÑtze eingefÅgt wird.;
                             ENU=Specifies the document date that will be inserted on the created records.;
                             NLD=Hiermee wordt de documentdatum opgegeven die wordt ingevoegd in de gemaakte records.;
                             NOR=Angir dokumentdatoen som skal settes inn i de opprettede postene.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentDate }

      { 12  ;2   ;Field     ;
                  CaptionML=[DEU=Buch.-Blattvorlage;
                             ENU=Journal Template;
                             NLD=Dagboeksjabloon;
                             NOR=Kladdemal;
                             SVE=Journalmall];
                  ToolTipML=[DEU=Gibt die Buch.-Blattvorlage an, auf der das Debitoren Buch.-Blatt basiert.;
                             ENU=Specifies the journal template that the customer journal is based on.;
                             NLD=Hiermee wordt de dagboeksjabloon opgegeven waarop het klantdagboek is gebaseerd.;
                             NOR=Angir kladdemalen som kundekladden er basert pÜ.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=JournalTemplate;
                  TableRelation="Gen. Journal Template".Name;
                  OnValidate=BEGIN
                               CheckJournalTemplate;
                             END;

                  OnLookup=VAR
                             GenJnlTemplate@1001 : Record 80;
                             GenJnlTemplates@1000 : Page 101;
                           BEGIN
                             GenJnlTemplate.SETRANGE(Type,GenJnlTemplate.Type::General);
                             GenJnlTemplate.SETRANGE(Recurring,FALSE);
                             GenJnlTemplates.SETTABLEVIEW(GenJnlTemplate);

                             GenJnlTemplates.LOOKUPMODE := TRUE;
                             GenJnlTemplates.EDITABLE := FALSE;
                             IF GenJnlTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               GenJnlTemplates.GETRECORD(GenJnlTemplate);
                               JournalTemplate := GenJnlTemplate.Name;
                             END;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  CaptionML=[DEU=Buch.-Blattname;
                             ENU=Batch Name;
                             NLD=Batchnaam;
                             NOR=Bunkenavn;
                             SVE=Journalnamn];
                  ToolTipML=[DEU=Gibt den Namen des Buch.-Blattnamens an, eines personalisierten Buch.-Blattlayouts, auf dem die Erfassung basiert.;
                             ENU=Specifies the name of the journal batch, a personalized journal layout, that the journal is based on.;
                             NLD=Hiermee wordt de naam opgegeven van de dagboekbatch, een aangepaste dagboeklay-out, waarop het dagboek is gebaseerd.;
                             NOR=Angir navnet pÜ kladden, et tilpasset kladdoppsett, som kladden er basert pÜ.;
                             SVE=Anger namnet pÜ journalen, en anpassad journallayout, som journalen Ñr baserad pÜ.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=BatchName;
                  TableRelation="Gen. Journal Batch".Name;
                  OnValidate=BEGIN
                               CheckBatchName;
                             END;

                  OnLookup=VAR
                             GenJnlBatches@1001 : Page 251;
                           BEGIN
                             IF JournalTemplate <> '' THEN BEGIN
                               GenJnlBatch.SETRANGE("Journal Template Name",JournalTemplate);
                               GenJnlBatches.SETTABLEVIEW(GenJnlBatch);
                             END;

                             GenJnlBatches.LOOKUPMODE := TRUE;
                             GenJnlBatches.EDITABLE := FALSE;
                             IF GenJnlBatches.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               GenJnlBatches.GETRECORD(GenJnlBatch);
                               BatchName := GenJnlBatch.Name;
                             END;
                           END;
                            }

      { 10  ;2   ;Field     ;
                  CaptionML=[DEU=Standard Fibu Buch.-Blatt;
                             ENU=Standard General Journal;
                             NLD=Standaarddagboek;
                             NOR=Standard finanskladd;
                             SVE=Standardredovisningsjournal];
                  ToolTipML=[DEU=Gibt das Standard Fibu Buch.-Blatt an, das die Stapelverarbeitung verwendet.;
                             ENU=Specifies the standard general journal that the batch job uses.;
                             NLD=Hiermee wordt het standaarddagboek opgegeven dat door de batchverwerking wordt gebruikt.;
                             NOR=Angir standard finanskladd som kjõrselen bruker.];
                  ApplicationArea=#Suite;
                  SourceExpr=TemplateCode;
                  TableRelation="Standard General Journal".Code;
                  OnLookup=VAR
                             StdGenJournal1@1005 : Record 750;
                             StdGenJnls@1000 : Page 750;
                           BEGIN
                             IF JournalTemplate <> '' THEN BEGIN
                               StdGenJournal1.SETRANGE("Journal Template Name",JournalTemplate);
                               StdGenJnls.SETTABLEVIEW(StdGenJournal1);
                             END;

                             StdGenJnls.LOOKUPMODE := TRUE;
                             StdGenJnls.EDITABLE := FALSE;
                             IF StdGenJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               StdGenJnls.GETRECORD(StdGenJournal1);
                               TemplateCode := StdGenJournal1.Code;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      StdGenJournal@1007 : Record 750;
      GenJnlBatch@1006 : Record 232;
      GenJnlLine@1005 : Record 81;
      LastGenJnlLine@1012 : Record 81;
      CustPostGrp@1009 : Record 92;
      DocumentTypes@1000 : ' ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund';
      PostingDate@1001 : Date;
      DocumentDate@1002 : Date;
      BatchName@1003 : Code[10];
      TemplateCode@1004 : Code[20];
      LineNo@1008 : Integer;
      JournalTemplate@1015 : Text[10];
      Text001@1016 : TextConst 'DEU=Der Fibu Buch.-Blattvorlagenname ist leer.;ENU=Gen. Journal Template name is blank.;NLD=Naam standaardmemoriaalsjabloon is leeg.;NOR=Navn pÜ finanskladdemal er tomt.';
      Text002@1013 : TextConst 'DEU=Der Fibu Buch.-Blattname ist leer.;ENU=Gen. Journal Batch name is blank.;NLD=Naam memoriaalbatch is leeg.;NOR=Navn pÜ finanskladd er tomt.';
      Text004@1017 : TextConst 'DEU=Die Fibu Buch.-Blattzeilen wurden erfolgreich erstellt.;ENU=General journal lines are successfully created.;NLD=Dagboekregels zijn gemaakt.;NOR=Finanskladdelinjer er opprettet.';
      PostingDateIsEmptyErr@1010 : TextConst 'DEU=Das Buchungsdatum ist leer.;ENU=The posting date is empty.;NLD=Boekingsdatum is leeg.;NOR=Bokfõringsdatoen er tom.';
      DocumentNo@1011 : Code[20];

    LOCAL PROCEDURE GetStandardJournalLine@3() : Boolean;
    VAR
      StdGenJounalLine@1000 : Record 751;
    BEGIN
      IF TemplateCode = '' THEN
        EXIT(FALSE);
      StdGenJounalLine.SETRANGE("Journal Template Name",StdGenJournal."Journal Template Name");
      StdGenJounalLine.SETRANGE("Standard Journal Code",StdGenJournal.Code);
      EXIT(StdGenJounalLine.FINDFIRST)
    END;

    [External]
    PROCEDURE Initialize@15(VAR StdGenJnl@1001 : Record 750;JnlBatchName@1000 : Code[10]);
    BEGIN
      GenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
      GenJnlLine."Journal Batch Name" := JnlBatchName;
      GenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      LastGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      LastGenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      IF LastGenJnlLine.FINDLAST THEN;

      GenJnlBatch.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlBatch.SETRANGE(Name,JnlBatchName);

      IF GenJnlBatch.FINDFIRST THEN;
    END;

    LOCAL PROCEDURE CopyGenJnlFromStdJnl@1(StdGenJnlLine@1001 : Record 751;VAR GenJnlLine@1002 : Record 81);
    VAR
      GenJnlManagement@1000 : Codeunit 230;
      Balance@1007 : Decimal;
      TotalBalance@1006 : Decimal;
      ShowBalance@1005 : Boolean;
      ShowTotalBalance@1004 : Boolean;
    BEGIN
      GenJnlLine.INIT;
      GenJnlLine."Line No." := 0;
      GenJnlManagement.CalcBalance(GenJnlLine,LastGenJnlLine,Balance,TotalBalance,ShowBalance,ShowTotalBalance);
      GenJnlLine.SetUpNewLine(LastGenJnlLine,Balance,TRUE);
      IF LastGenJnlLine."Line No." <> 0 THEN
        GenJnlLine."Line No." := LastGenJnlLine."Line No." + 10000
      ELSE
        GenJnlLine."Line No." := 10000;

      GenJnlLine.TRANSFERFIELDS(StdGenJnlLine,FALSE);
      GenJnlLine.UpdateLineBalance;
      GenJnlLine.VALIDATE("Currency Code");

      IF GenJnlLine."VAT Prod. Posting Group" <> '' THEN
        GenJnlLine.VALIDATE("VAT Prod. Posting Group");
      IF (GenJnlLine."VAT %" <> 0) AND GenJnlBatch."Allow VAT Difference" THEN
        GenJnlLine.VALIDATE("VAT Amount",StdGenJnlLine."VAT Amount");
      GenJnlLine.VALIDATE("Bal. VAT Prod. Posting Group");

      IF GenJnlBatch."Allow VAT Difference" THEN
        GenJnlLine.VALIDATE("Bal. VAT Amount",StdGenJnlLine."Bal. VAT Amount");
      GenJnlLine.INSERT(TRUE);

      LastGenJnlLine := GenJnlLine;
    END;

    [External]
    PROCEDURE InitializeRequest@2(DocumentTypesFrom@1000 : Option;PostingDateFrom@1001 : Date;DocumentDateFrom@1002 : Date);
    BEGIN
      DocumentTypes := DocumentTypesFrom;
      PostingDate := PostingDateFrom;
      DocumentDate := DocumentDateFrom;
    END;

    [External]
    PROCEDURE InitializeRequestTemplate@4(JournalTemplateFrom@1000 : Text[10];BatchNameFrom@1001 : Code[10];TemplateCodeFrom@1002 : Code[20]);
    BEGIN
      JournalTemplate := JournalTemplateFrom;
      BatchName := BatchNameFrom;
      TemplateCode := TemplateCodeFrom;
    END;

    [External]
    PROCEDURE SetDefaultDocumentNo@5(NewDocumentNo@1000 : Code[20]);
    BEGIN
      DocumentNo := NewDocumentNo;
    END;

    LOCAL PROCEDURE CheckPostingDate@24();
    BEGIN
      IF PostingDate = 0D THEN
        ERROR(PostingDateIsEmptyErr);
    END;

    LOCAL PROCEDURE CheckBatchName@32();
    BEGIN
      IF BatchName = '' THEN
        ERROR(Text002);
    END;

    LOCAL PROCEDURE CheckJournalTemplate@33();
    BEGIN
      IF JournalTemplate = '' THEN
        ERROR(Text001);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

