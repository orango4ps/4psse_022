OBJECT Page 1309 O365 Getting Started
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Trial Experience;
               NOR=Pr›veabonnement;
               SVE=Utv„rdering];
    SourceTable=Table1309;
    PageType=NavigatePage;
    OnInit=VAR
             AllProfile@1001 : Record 2000000178;
             ConfPersonalizationMgt@1000 : Codeunit 9170;
           BEGIN
             SETRANGE("User ID",USERID);
             CurrProfileID := ConfPersonalizationMgt.GetCurrentProfileIDNoError;

             IF CurrProfileID <> '' THEN
               AllProfile.SETRANGE("Profile ID",CurrProfileID);

             IF NOT AllProfile.FINDFIRST THEN
               EXIT;

             CurrProfileID := AllProfile."Profile ID";
             ChangedProfileID := AllProfile."Profile ID";
           END;

    OnOpenPage=BEGIN
                 IF NOT AlreadyShown THEN
                   MarkAsShown;

                 CurrentPage := TRUE;
               END;

    OnClosePage=BEGIN
                  "Tour in Progress" := FALSE;
                  "Tour Completed" := TRUE;
                  MODIFY;
                END;

    OnQueryClosePage=VAR
                       ConfPersonalizationMgt@1000 : Codeunit 9170;
                     BEGIN
                       IF (ConfPersonalizationMgt.GetCurrentProfileIDNoError <> ChangedProfileID) AND RoleCenterOverview.GetAcceptAction THEN
                         IF NOT CONFIRM(RoleNotSavedQst) THEN
                           ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 6       ;    ;ActionContainer;
                      ActionContainerType=NewDocumentItems }
      { 7       ;1   ;Action    ;
                      Name=Get Started;
                      CaptionML=[ENU=Get Started;
                                 NOR=Kom i gang;
                                 SVE=Kom ig†ng];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      InFooterBar=Yes;
                      OnAction=VAR
                                 UserPersonalization@1002 : Record 2000000073;
                                 AllProfile@1003 : Record 2000000178;
                                 ConfPersonalizationMgt@1001 : Codeunit 9170;
                                 SessionSet@1000 : SessionSettings;
                               BEGIN
                                 IF ConfPersonalizationMgt.IsCurrentProfile(ChangedProfileScope,ChangedProfileAppID,ChangedProfileID) THEN
                                   CurrPage.CLOSE;

                                 IF RoleCenterOverview.GetAcceptAction THEN BEGIN
                                   IF NOT AllProfile.GET(ChangedProfileScope,ChangedProfileAppID,ChangedProfileID) THEN
                                     CurrPage.CLOSE;

                                   ConfPersonalizationMgt.SetCurrentProfile(AllProfile);
                                   UserPersonalization.GET(USERSECURITYID);

                                   WITH SessionSet DO BEGIN
                                     INIT;
                                     PROFILEID := ChangedProfileID;
                                     PROFILEAPPID := UserPersonalization."App ID";
                                     PROFILESYSTEMSCOPE := UserPersonalization.Scope = UserPersonalization.Scope::System;
                                     LANGUAGEID := UserPersonalization."Language ID";
                                     LOCALEID := UserPersonalization."Locale ID";
                                     TIMEZONE := UserPersonalization."Time Zone";
                                     REQUESTSESSIONUPDATE(TRUE);
                                   END;
                                 END;

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 3   ;1   ;Group     ;
                Visible=CurrentPage;
                GroupType=Group }

    { 4   ;2   ;Group     ;
                GroupType=Group }

    { 2   ;3   ;Field     ;
                Name=WelcomeWizard;
                ApplicationArea=#Basic,#Suite;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.WelcomeWizard;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      RoleCenterOverview@1004 : Page 1486;
      CurrProfileID@1003 : Code[30];
      ChangedProfileID@1001 : Code[30];
      RoleNotSavedQst@1005 : TextConst 'ENU=Your choice of role center is not saved. Are you sure you want to close?;NOR=Valg av rollesenter lagres ikke. Er du sikker p† at du vil avslutte?;SVE=Ditt val av rollcenter har inte sparats. Vill du st„nga?';
      TitleTxt@1006 : TextConst '@@@=%1 is the branding PRODUCTNAME.MARKETING string constant;ENU=Welcome to %1;NOR=Velkommen tilÿ%1;SVE=V„lkommen till %1';
      SubTitleTxt@1007 : TextConst 'ENU=Let''s get started;NOR=La oss komme i gang;SVE=Nu s„tter vi ig†ng';
      ExplanationTxt@1008 : TextConst '@@@=%1 - This is the COMPANYNAME. ex. Cronus US Inc.;ENU=Start with basic business processes, or jump right in to advanced operations. Use our %1 demo company and data, or create a new company and import your own data.;NOR=Start med grunnleggende forretningsprosesser eller g† direkte til avanserte operasjoner. Bruk v†rt %1-demonstrasjonsselskap og v†re data, eller opprett et nytt selskap og importer egne data.;SVE=B”rja med grundl„ggande processer eller direkt med avancerade †tg„rder. Anv„nd v†r %1-demonstrationsf”retag och -data, eller skapa ett nytt f”retag och importera dina egna data.';
      IntroTxt@1009 : TextConst 'ENU=Introduction;NOR=Introduksjon;SVE=Introduktion';
      IntroDescTxt@1010 : TextConst 'ENU=Get to know Business Central;NOR=Gj›r deg kjent med Business Central;SVE=L„r k„nna Business Central';
      GetStartedTxt@1011 : TextConst 'ENU=Get Started;NOR=Kom i gang;SVE=Kom ig†ng';
      GetStartedDescTxt@1012 : TextConst 'ENU=See the important first steps;NOR=Se de viktige f›rste trinnene;SVE=Visa de viktiga f”rsta stegen';
      FindHelpTxt@1013 : TextConst 'ENU=Get Assistance;NOR=F† hjelp;SVE=F† hj„lp';
      FindHelpDescTxt@1014 : TextConst 'ENU=Know where to go for information;NOR=Vit hvor du finner informasjon;SVE=Hit kan du g† f”r att f† information';
      RoleCentersTxt@1015 : TextConst 'ENU=Role Centers;NOR=Rollesentre;SVE=Rollcenter';
      RoleCentersDescTxt@1016 : TextConst 'ENU=Explore different business roles;NOR=Utforsk ulike forretningsroller;SVE=Utforska olika aff„rsroller';
      ChangedProfileAppID@1017 : GUID;
      ChangedProfileScope@1018 : Option;
      CurrentPage@1000 : Boolean;
      LegalDescriptionTxt@1002 : TextConst 'ENU=Demo data is provided for demonstration purposes only and should be used only for evaluation, training and test systems.;NOR=Demonstrasjonsdata er oppgitt bare til demonstrasjonsform†l, ogÿm† brukes bareÿtil evaluering, oppl‘ring og testing av systemer.;SVE=Demodata tillhandah†lls endast i demonstrationssyfte och ska bara anv„ndas f”r utv„rdering, utbildning och testsystem.';

    [External]
    PROCEDURE GetNextPageID@1014(Increment@1000 : Integer;CurrentPageID@1002 : Integer) NextPageID : Integer;
    BEGIN
      NextPageID := CurrentPageID + Increment;
    END;

    LOCAL PROCEDURE GetProfileDescription@1() : Text[250];
    VAR
      AllProfile@1000 : Record 2000000178;
    BEGIN
      AllProfile.SETRANGE(Scope,AllProfile.Scope::System);
      AllProfile.SETFILTER("Profile ID",ChangedProfileID);
      IF AllProfile.FINDFIRST THEN
        EXIT(AllProfile.Description);
    END;

    EVENT WelcomeWizard@-2::ControlAddInReady@3();
    VAR
      ExplanationStr@1000 : Text;
      WelcomeToTitle@1001 : Text;
    BEGIN
      ExplanationStr := STRSUBSTNO(ExplanationTxt,COMPANYNAME);
      WelcomeToTitle := STRSUBSTNO(TitleTxt,PRODUCTNAME.MARKETING);
      CurrPage.WelcomeWizard.Initialize(WelcomeToTitle,SubTitleTxt,ExplanationStr,IntroTxt,IntroDescTxt,
        GetStartedTxt,GetStartedDescTxt,FindHelpTxt,FindHelpDescTxt,RoleCentersTxt,RoleCentersDescTxt,GetProfileDescription,
        LegalDescriptionTxt);
    END;

    EVENT WelcomeWizard@-2::ErrorOccurred@4(error@1001 : Text;description@1000 : Text);
    BEGIN
    END;

    EVENT WelcomeWizard@-2::Refresh@5();
    BEGIN
    END;

    EVENT WelcomeWizard@-2::ThumbnailClicked@6(selection@1000 : Integer);
    VAR
      VideoLink@1001 : Page 1821;
    BEGIN
      CASE selection OF
        1:
          BEGIN
            VideoLink.SetURL('https://go.microsoft.com/fwlink/?linkid=867632');
            VideoLink.RUNMODAL;
          END;
        2:
          BEGIN
            VideoLink.SetURL('https://go.microsoft.com/fwlink/?linkid=867634');
            VideoLink.RUNMODAL;
          END;
        3:
          BEGIN
            VideoLink.SetURL('https://go.microsoft.com/fwlink/?linkid=867635');
            VideoLink.RUNMODAL;
          END;
        4:
          BEGIN
            CLEAR(RoleCenterOverview);
            RoleCenterOverview.DelaySessionUpdateRequest;
            IF RoleCenterOverview.RUNMODAL = ACTION::OK THEN BEGIN
              RoleCenterOverview.GetSelectedProfile(ChangedProfileScope,ChangedProfileAppID,ChangedProfileID);
              IF RoleCenterOverview.GetAcceptAction THEN
                CurrPage.WelcomeWizard.UpdateProfileId(GetProfileDescription);
            END;
          END;
      END;
    END;

    BEGIN
    END.
  }
}

