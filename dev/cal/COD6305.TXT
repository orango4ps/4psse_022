OBJECT Codeunit 6305 Set Power BI User Config
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PowerBIServiceMgt@1001 : Codeunit 6301;

    [External]
    PROCEDURE SetUserConfig@2(VAR PowerBIUserConfiguration@1001 : Record 6304;PageID@1002 : Text) : Boolean;
    VAR
      PowerBIVisible@1003 : Boolean;
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeSetUserConfig(PowerBIUserConfiguration,PageID,PowerBIVisible,IsHandled);
      IF IsHandled THEN
        EXIT(PowerBIVisible);

      // load existing UserConfig entry to get PowerBI FactBox visibility
      // entry by itself is created on the FactBox page
      PowerBIUserConfiguration.RESET;
      PowerBIUserConfiguration.SETFILTER("Page ID",PageID);
      PowerBIUserConfiguration.SETFILTER("User Security ID",USERSECURITYID);
      //PowerBIUserConfiguration.SETFILTER("Profile ID",PowerBIServiceMgt.GetEnglishContext); //**4PS.o
      PowerBIUserConfiguration.SETRANGE("Profile ID",PowerBIServiceMgt.GetEnglishContext); //**4PS.n
      IF NOT PowerBIUserConfiguration.ISEMPTY THEN BEGIN
        PowerBIUserConfiguration.FINDFIRST;
        EXIT(PowerBIUserConfiguration."Report Visibility");
      END;

      // if entry has not been created yet, set PowerBI FactBox visibility to defalut TRUE value
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE CreateOrReadUserConfigEntry@3(VAR PowerBIUserConfiguration@1001 : Record 6304;VAR LastOpenedReportID@1003 : GUID;Context@1000 : Text[50]);
    BEGIN
      // create a new Power BI User Configuration table entry or read one if it exist
      PowerBIUserConfiguration.RESET;
      PowerBIUserConfiguration.SETFILTER("Page ID",Context);
      PowerBIUserConfiguration.SETFILTER("User Security ID",USERSECURITYID);
      //PowerBIUserConfiguration.SETFILTER("Profile ID",PowerBIServiceMgt.GetEnglishContext); //**4PS.o
      PowerBIUserConfiguration.SETRANGE("Profile ID",PowerBIServiceMgt.GetEnglishContext); //**4PS.n
      IF PowerBIUserConfiguration.ISEMPTY THEN BEGIN
        PowerBIUserConfiguration."Page ID" := Context;
        PowerBIUserConfiguration."User Security ID" := USERSECURITYID;
        PowerBIUserConfiguration."Profile ID" := PowerBIServiceMgt.GetEnglishContext;
        PowerBIUserConfiguration."Report Visibility" := TRUE;
        // SelectedReportId field is set to an empty GUID by default
        CLEAR(LastOpenedReportID);
        PowerBIUserConfiguration.INSERT(TRUE);
        COMMIT;
      END ELSE BEGIN
        PowerBIUserConfiguration.FINDFIRST;
        LastOpenedReportID := PowerBIUserConfiguration."Selected Report ID";
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSetUserConfig@1(VAR PowerBIUserConfiguration@1001 : Record 6304;PageID@1000 : Text;VAR PowerBIVisible@1002 : Boolean;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

