OBJECT Table 11071774 SUP Production Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceOrder@1100528301 : Record 11012823;
               SUPProduction@1100528600 : Record 11071773;
             BEGIN
               ServiceOrder.GET("Service Order No.");
               ServiceOrder.CheckStatus(1);

               IF SUPProduction.GET("Service Order No.", "Production Seq. No.") THEN BEGIN
                 SUPProduction.TESTFIELD(Status, SUPProduction.Status::Proposal);
                 VALIDATE("Production Date", SUPProduction."Production Date");
                 VALIDATE("Production Time", SUPProduction."Production Time");
                 VALIDATE("Production Date/Time", SUPProduction."Production Date/Time");
                 IF (ServiceOrder.Origin = ServiceOrder.Origin::DSP) AND ("Object No." = '') THEN
                   "Object No." := SUPProduction."Object No.";
               END;
             END;

    OnModify=VAR
               ServiceOrder@1100525000 : Record 11012823;
               PBInstallmentOverview@1100528600 : Record 11072317;
             BEGIN
               IF ServiceOrder.GET("Service Order No.") THEN
                 ServiceOrder.CheckStatus(1);
               IF "Installment Overview No." <> '' THEN BEGIN
                 PBInstallmentOverview.GET("Installment Overview No.");
                 IF PBInstallmentOverview.Status <> PBInstallmentOverview.Status::Open THEN
                   PBInstallmentOverview.FIELDERROR(Status);
               END;
               CalculateFields;
               IF MODIFY THEN
                 RecalcNextRecordingFields;
             END;

    OnDelete=VAR
               SUPProdLineCommentLine@1100528600 : Record 11072299;
             BEGIN
               TESTFIELD("Cost Plus Entries Created", FALSE);
               TESTFIELD("Installment Overview No.", '');

               SUPProdLineCommentLine.SETRANGE("Service Order No.", "Service Order No.");
               SUPProdLineCommentLine.SETRANGE("Production Seq. No.", "Production Seq. No.");
               SUPProdLineCommentLine.SETRANGE("Production Line No.", "Line No.");
               SUPProdLineCommentLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               IF (NOT RenameIsAllowed) THEN
                 ERROR(Text000);
             END;

    CaptionML=[ENU=SUP Production Line;
               SVE=Enhetspris prod.registreringsrad];
    LookupPageID=Page11125301;
    DrillDownPageID=Page11125301;
  }
  FIELDS
  {
    { 10  ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   OnValidate=VAR
                                                                ServiceOrder@1100525000 : Record 11012823;
                                                                ServiceOrderExtension@1100525001 : Record 11071727;
                                                              BEGIN
                                                                IF NOT ServiceOrder.GET("Service Order No.") THEN
                                                                  EXIT;
                                                                ServiceOrderExtension.GetServOrderExtension("Service Order No.");
                                                                "Price Book Type" := ServiceOrder."Price Book Type";
                                                                "Price Book Code" := ServiceOrderExtension."Price Book Code";
                                                                "Index Date" := ServiceOrderExtension."Price Book Index Date";
                                                              END;

                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 15  ;   ;Production Seq. No. ;Integer       ;TableRelation="SUP Production"."Seq. No." WHERE (Service Order No.=FIELD(Service Order No.));
                                                   CaptionML=ENU=Production Seq. No.;
                                                   Editable=No }
    { 17  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer];
                                                   Editable=No }
    { 20  ;   ;Production Date     ;Date          ;TableRelation="SUP Production"."Production Date" WHERE (Service Order No.=FIELD(Service Order No.));
                                                   CaptionML=[ENU=Production Date;
                                                              NOR=Produksjonsdato;
                                                              SVE=Produktionsdatum];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 22  ;   ;Production Time     ;Time          ;CaptionML=ENU=Production Time }
    { 25  ;   ;Production Date/Time;DateTime      ;CaptionML=ENU=Production Date/Time;
                                                   Editable=No }
    { 30  ;   ;Price Book Type     ;Option        ;CaptionML=[ENU=Price Book Type;
                                                              SVE=Enhetspristyp];
                                                   OptionCaptionML=[ENU=" ,Internal,Customer";
                                                                    NOR=" ,Intern,Kunde";
                                                                    SVE=" ,Intern,Kund"];
                                                   OptionString=[ ,Internal,Customer];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Price Book Code     ;Code20        ;TableRelation="Service Price Book".Code WHERE (Type=FIELD(Price Book Type));
                                                   OnLookup=VAR
                                                              ServiceUnitPrice@1100528600 : Record 11071768;
                                                              ServiceUnitPriceList@1100528601 : Page 11125269;
                                                            BEGIN
                                                              ServiceUnitPrice.SETRANGE(Type, "Price Book Type");
                                                              ServiceUnitPriceList.LOOKUPMODE(TRUE);
                                                              ServiceUnitPriceList.SETTABLEVIEW(ServiceUnitPrice);
                                                              IF (ServiceUnitPriceList.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                                                                ServiceUnitPriceList.GETRECORD(ServiceUnitPrice);
                                                                "Price Book Code" := ServiceUnitPrice.Code;
                                                                "Index Date" := ServiceUnitPrice."Index Date";
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Price Book Code;
                                                              SVE=Enhetspriskod];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 50  ;   ;Index Date          ;Date          ;CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 55  ;   ;Room                ;Code20        ;TableRelation="SUP Room".Code;
                                                   OnValidate=BEGIN
                                                                AddOrUpdateSUPRecordingLine(CurrFieldNo);
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              CustomerNo@1100528601 : Code[20];
                                                              ServiceLocationType@1100528602 : Code[20];
                                                            BEGIN
                                                              CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo("Price Book Type", "Price Book Code");
                                                              ServiceLocationType := ServiceUnitPriceManagement.GetServiceLocationTypeFromServiceOrder("Service Order No.");
                                                              IF ServiceUnitPriceManagement.LookupRoom(CustomerNo, ServiceLocationType, Room) THEN
                                                                VALIDATE(Room);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Room;
                                                              NOR=Rom;
                                                              SVE=Rum] }
    { 60  ;   ;Unit Price Code     ;Code20        ;TableRelation="Service Unit Price".Code WHERE (Price Book Type=FIELD(Price Book Type),
                                                                                                  Price Book Code=FIELD(Price Book Code),
                                                                                                  Index Date=FIELD(Index Date),
                                                                                                  Code=FIELD(Unit Price Code));
                                                   OnValidate=VAR
                                                                ServiceUnitPriceLine@1100528600 : Record 11071769;
                                                              BEGIN
                                                                IF (xRec."Unit Price Code" <> '') AND ("Unit Price Code" <> xRec."Unit Price Code") AND ("Recording Line No." <> 0) THEN
                                                                  ERROR(Text001, FIELDCAPTION("Unit Price Code"));
                                                                CALCFIELDS("Unit Price Type");
                                                                IF "Unit Price Type" = "Unit Price Type"::"Summary Element" THEN
                                                                  FIELDERROR("Unit Price Type");
                                                                IF ServiceUnitPriceLine.GET("Price Book Code", "Index Date", "Unit Price Code") THEN BEGIN
                                                                  Description := ServiceUnitPriceLine.Description;
                                                                  "Description 2" := ServiceUnitPriceLine."Description 2";
                                                                  "Description 3" := ServiceUnitPriceLine."Description 3";
                                                                  "Unit of Measure" := ServiceUnitPriceLine."Unit of Measure";
                                                                  "Unit Price" := ServiceUnitPriceLine."Unit Price";
                                                                END ELSE BEGIN
                                                                  Description := '';
                                                                  "Description 2" := '';
                                                                  "Description 3" := '';
                                                                  "Unit of Measure" := '';
                                                                  "Unit Price" := 0;
                                                                END;
                                                                "Alternative Unit Price Code" := "Unit Price Code";
                                                                IF "Unit Price Code" <> '' THEN
                                                                  "VAT Prod. Posting Group" := '';
                                                                AddOrUpdateSUPRecordingLine(FIELDNO("Unit Price Code"));
                                                              END;

                                                   OnLookup=VAR
                                                              CopyUsed@1100529600 : Boolean;
                                                            BEGIN
                                                              LookupUnitPriceLineCode(FALSE, CopyUsed);
                                                            END;

                                                   CaptionML=[ENU=Unit Price Code;
                                                              NOR=Enhetspriskode;
                                                              SVE=Enhetspriskod] }
    { 65  ;   ;Alternative Unit Price Code;Code20 ;OnValidate=BEGIN
                                                                AddOrUpdateSUPRecordingLine(CurrFieldNo);
                                                              END;

                                                   CaptionML=ENU=Alternative Unit Price Code }
    { 70  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                IF Description <> xRec.Description THEN
                                                                  IF GUIALLOWED THEN BEGIN //Needed for VMA
                                                                    TESTFIELD("Unit Price Code", '');
                                                                    AddOrUpdateSUPRecordingLine(CurrFieldNo);
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 71  ;   ;Description 2       ;Text50        ;OnValidate=BEGIN
                                                                IF "Description 2" <> xRec."Description 2" THEN
                                                                  IF GUIALLOWED THEN BEGIN //Needed for VMA
                                                                    TESTFIELD("Unit Price Code", '');
                                                                    AddOrUpdateSUPRecordingLine(CurrFieldNo);
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 72  ;   ;Description 3       ;Text50        ;OnValidate=BEGIN
                                                                IF "Description 3" <> xRec."Description 3" THEN
                                                                  IF GUIALLOWED THEN BEGIN //Needed for VMA
                                                                    TESTFIELD("Unit Price Code", '');
                                                                    AddOrUpdateSUPRecordingLine(CurrFieldNo);
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Description 3;
                                                              NOR=Beskrivelse 3;
                                                              SVE=Beskrivning 3] }
    { 80  ;   ;Unit of Measure     ;Code10        ;OnValidate=BEGIN
                                                                IF "Unit of Measure" <> xRec."Unit of Measure" THEN
                                                                  IF GUIALLOWED THEN BEGIN //Needed for VMA
                                                                    TESTFIELD("Unit Price Code", '');
                                                                    AddOrUpdateSUPRecordingLine(CurrFieldNo);
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=Enhet] }
    { 90  ;   ;Unit Price          ;Decimal       ;OnValidate=VAR
                                                                ServiceSetup@1100528500 : Record 11012800;
                                                              BEGIN
                                                                ServiceSetup.GET;
                                                                IF ("Price Book Type" <> "Price Book Type"::Internal) OR NOT ServiceSetup."Allow Internal SUP Line Modif." THEN
                                                                  IF GUIALLOWED THEN
                                                                    TESTFIELD("Unit Price Code", '');
                                                                AddOrUpdateSUPRecordingLine(CurrFieldNo);
                                                              END;

                                                   CaptionML=[ENU=Unit Price;
                                                              NOR=Salgspris;
                                                              SVE=A-pris];
                                                   DecimalPlaces=2:5 }
    { 100 ;   ;Recording Quantity  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SUP Recording Line"."Recording Quantity" WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                                       Line No.=FIELD(Recording Line No.)));
                                                   OnValidate=BEGIN
                                                                IF "Recording Quantity" <> 0 THEN
                                                                  TestIfLineIsNonHeader;
                                                              END;

                                                   CaptionML=[ENU=Recording Quantity;
                                                              NOR=Registrering Kvantum;
                                                              SVE=Registreringsantal];
                                                   DecimalPlaces=0:2;
                                                   Editable=No }
    { 110 ;   ;Production Cumulative;Decimal      ;OnValidate=BEGIN
                                                                IF "Production Cumulative" <> 0 THEN
                                                                  TestIfLineIsNonHeader;
                                                                Production := "Production Cumulative" - GetPreviousProdCumulative;
                                                                CalculateFields;
                                                              END;

                                                   CaptionML=[ENU=Production Cumulative;
                                                              NOR=Akkumulert Produksjon;
                                                              SVE=ProductionCumulative];
                                                   DecimalPlaces=0:2 }
    { 120 ;   ;Production          ;Decimal       ;OnValidate=BEGIN
                                                                IF Production <> 0 THEN
                                                                  TestIfLineIsNonHeader;
                                                                "Production Cumulative" := GetPreviousProdCumulative + Production;
                                                                CalculateFields;
                                                              END;

                                                   CaptionML=[ENU=Production;
                                                              NOR=Produksjon;
                                                              SVE=Produktion];
                                                   DecimalPlaces=0:2 }
    { 130 ;   ;Production Value    ;Decimal       ;CaptionML=[ENU=Production Value;
                                                              NOR=Produksjonsverdi;
                                                              SVE=Produktionsv„rde];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 140 ;   ;Extension Value     ;Decimal       ;CaptionML=[ENU=Extension Value;
                                                              NOR=ETA-verdi;
                                                              SVE=ŽTA-v„rde];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 150 ;   ;Extension Production;Decimal       ;CaptionML=[ENU=Extension Production;
                                                              NOR=ETA-produksjon;
                                                              SVE=ŽTA-produktion];
                                                   DecimalPlaces=0:2;
                                                   Editable=No }
    { 155 ;   ;Previous Prod. Cumulative;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Production Line".Production WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                           Production Date/Time=FIELD(Prev. Prod. Date/Time Filter),
                                                                                                           Recording Line No.=FIELD(Recording Line No.),
                                                                                                           SUP Production Status=FILTER(<>Expired)));
                                                   CaptionML=[ENU=Previous Production Cumulative;
                                                              NOR=Forrige Akkumulerte Produksjon;
                                                              SVE=Ackumulerad f”reg†ende produktion];
                                                   DecimalPlaces=0:2;
                                                   Editable=No }
    { 160 ;   ;Previous Prod Value Cumulative;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Production Line"."Production Value" WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                                   Production Date/Time=FIELD(Prev. Prod. Date/Time Filter),
                                                                                                                   Recording Line No.=FIELD(Recording Line No.),
                                                                                                                   SUP Production Status=FILTER(<>Expired)));
                                                   CaptionML=[ENU=Previous Prod Value Cumulative;
                                                              NOR=Forrige prod.verdi akkumulert;
                                                              SVE=F”reg†ende prod.v„rde ackumulerat];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 170 ;   ;Previous Production Date;Date      ;FieldClass=FlowField;
                                                   CalcFormula=Max("SUP Production Line"."Production Date" WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                                  Production Date/Time=FIELD(FILTER(Prev. Prod. Date/Time Filter)),
                                                                                                                  Recording Line No.=FIELD(Recording Line No.),
                                                                                                                  SUP Production Status=FILTER(<>Expired)));
                                                   CaptionML=[ENU=Previous Production Date;
                                                              NOR=Forrige Produksjonsdato;
                                                              SVE=F”reg†ende produktionsdatum];
                                                   Editable=No }
    { 180 ;   ;Previous Prod Date Filter OLD;Text30;
                                                   CaptionML=[ENU=Previous Prod Date Filter OLD;
                                                              SVE=F”reg†ende prod.datumfilter];
                                                   Description=OBSOLETE;
                                                   Editable=No }
    { 185 ;   ;Prev. Prod. Date/Time Filter;DateTime;
                                                   FieldClass=FlowFilter;
                                                   CaptionML=ENU=Previous Production Date/Time Filter;
                                                   Editable=No }
    { 186 ;   ;Production Date/Time Filter;DateTime;
                                                   FieldClass=FlowFilter;
                                                   CaptionML=ENU=Production Date/Time Filter;
                                                   Editable=No }
    { 190 ;   ;Production Value Cumulative;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Production Line"."Production Value" WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                                   Recording Line No.=FIELD(Recording Line No.),
                                                                                                                   Production Date/Time=FIELD(Production Date/Time Filter),
                                                                                                                   SUP Production Status=FILTER(<>Expired)));
                                                   CaptionML=[ENU=Production Value;
                                                              NOR=Produksjonsverdi;
                                                              SVE=Produktionsv„rde];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 200 ;   ;Additional Info     ;Text50        ;CaptionML=[ENU=Additional Info;
                                                              NOR=Tilleggsinfo;
                                                              SVE=Ytterligare info] }
    { 210 ;   ;Bill-to Customer No.;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SUP Recording Line"."Bill-to Customer No." WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                                         Line No.=FIELD(Recording Line No.)));
                                                   CaptionML=[ENU=Bill-to Customer No.;
                                                              NOR=Faktureringskundenr;
                                                              SVE=Faktureringskundnr];
                                                   Editable=No }
    { 215 ;   ;Bill-to Name        ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Bill-to Customer No.)));
                                                   CaptionML=[ENU=Bill-to Name;
                                                              NOR=Faktura til-navn;
                                                              SVE=Faktureringsnamn];
                                                   Editable=No }
    { 220 ;   ;Cost Plus Entries Created;Boolean  ;CaptionML=[ENU=Cost Plus Entries Created;
                                                              NOR=Selvkosttransaksjoner opprettet;
                                                              SVE=Redovisade sj„lvkostnader har skapats];
                                                   Editable=No }
    { 230 ;   ;Creation Date Cost Plus Entry;Date ;CaptionML=[ENU=Creation Date Cost Plus Entry;
                                                              NOR=Opprettelsesdato for selvkosttransaksjon;
                                                              SVE=Skapandedatum f”r redovisad sj„lvkostnad];
                                                   Editable=No }
    { 240 ;   ;Cost Plus Line No.  ;Integer       ;CaptionML=[ENU=Cost Plus Line No.;
                                                              NOR=Radnr for selvkost;
                                                              SVE=Radnr f”r redovisag sj„lvkostnad];
                                                   Editable=No }
    { 250 ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 260 ;   ;Part                ;Code20        ;TableRelation="SUP Part".Code;
                                                   OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                                CustomerNo@1100528601 : Code[20];
                                                              BEGIN
                                                                CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo("Price Book Type", "Price Book Code");
                                                                ServiceUnitPriceManagement.ValidatePart(CustomerNo, Part);
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPriceManagement@1100528601 : Codeunit 11012846;
                                                              CustomerNo@1100528600 : Code[20];
                                                            BEGIN
                                                              CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo("Price Book Type", "Price Book Code");
                                                              IF ServiceUnitPriceManagement.LookupPart(CustomerNo, Room, Part) THEN
                                                                VALIDATE(Part);
                                                            END;

                                                   CaptionML=[ENU=Part;
                                                              NOR=Del;
                                                              SVE=Del] }
    { 270 ;   ;SUP Production Status;Option       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SUP Production".Status WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                     Seq. No.=FIELD(Production Seq. No.)));
                                                   CaptionML=ENU=SUP Production Status;
                                                   OptionCaptionML=[ENU=Proposal,Offered,Approved,Expired,Rejected;
                                                                    NOR=Forslag,Tilbudt,Godkjent,Forfalt;
                                                                    SVE=F”rslag,Erbjuden,Godk„nd,F”rfallen,Avb”jd];
                                                   OptionString=Proposal,Offered,Approved,Expired,Rejected;
                                                   Editable=No }
    { 280 ;   ;Recording Line No.  ;Integer       ;CaptionML=ENU=Recording Line No.;
                                                   Editable=No }
    { 290 ;   ;Installment Overview No.;Code20    ;TableRelation="PB Installment Overview";
                                                   OnValidate=BEGIN
                                                                IF "Installment Overview No." = '' THEN
                                                                  VALIDATE("Installment Overview Line No.", 0);
                                                              END;

                                                   CaptionML=ENU=Installment Overview No.;
                                                   Editable=No }
    { 300 ;   ;Installment Overview Line No.;Integer;
                                                   CaptionML=ENU=Installment Overview Line No.;
                                                   Editable=No }
    { 310 ;   ;Unit Price Type     ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Unit Price"."Unit Price Type" WHERE (Price Book Type=FIELD(Price Book Type),
                                                                                                                    Price Book Code=FIELD(Price Book Code),
                                                                                                                    Index Date=FIELD(Index Date),
                                                                                                                    Code=FIELD(Unit Price Code)));
                                                   CaptionML=[ENU=Unit Price Type;
                                                              NOR=Enhetspris, type;
                                                              SVE=Enhetspristyp];
                                                   OptionCaptionML=ENU=" ,Header,Administrative,Summary Element";
                                                   OptionString=[ ,Header,Administrative,Summary Element];
                                                   Editable=No }
    { 320 ;   ;Price Book Quantity ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Unit Price".Quantity WHERE (Price Book Code=FIELD(Price Book Code),
                                                                                                           Index Date=FIELD(Index Date),
                                                                                                           Code=FIELD(Unit Price Code)));
                                                   CaptionML=ENU=Price Book Quantity;
                                                   Editable=No }
    { 330 ;   ;Object No.          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SUP Recording Line"."Object No." WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                               Line No.=FIELD(Recording Line No.)));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Object Description");
                                                              END;

                                                   CaptionML=[ENU=Object No.;
                                                              NOR=Objektnr.;
                                                              SVE=Objektnr];
                                                   Editable=No }
    { 331 ;   ;Object Description  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=[ENU=Object Description;
                                                              SVE=Objektbeskrivning];
                                                   Editable=No }
    { 340 ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "VAT Prod. Posting Group" <> '' THEN
                                                                  TESTFIELD("Unit Price Code", '');
                                                              END;

                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              NOR=Moms produktbokf›ringsmal;
                                                              SVE=Moms produktbokf”ringsmall];
                                                   Editable=No }
    { 350 ;   ;Sub-part of         ;Code20        ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Sub-part of }
  }
  KEYS
  {
    {    ;Service Order No.,Production Seq. No.,Line No.;
                                                   SumIndexFields=Production Value,Production Cumulative;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Service Order No.,Price Book Type,Price Book Code,Index Date,Room,Unit Price Code;
                                                   SumIndexFields=Production }
    {    ;Service Order No.,Production Date/Time  ;SumIndexFields=Production Value }
    {    ;Price Book Code,Index Date               }
    {    ;Installment Overview No.                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=Rename is not allowed.;NOR=Gi nytt navn ikke tillatt;SVE=Att ge nytt namn „r inte till†tet.';
      RenameIsAllowed@1100528300 : Boolean;
      Text001@1100528601 : TextConst 'ENU=Field %1 cannot be changed, delete current line and create a new one.';
      Text002@1100528302 : TextConst 'ENU=Cost Plus Entries could be incorrect and needs to be changed for the following date(s):\%1;NOR=Selvkosttransaksjoner kan v‘re feil, og m† endres for f›lgende dato(er)::\%1';
      Text003@1100528602 : TextConst 'ENU=Cannot update %1 because multiple production lines exist.';

    PROCEDURE CalculateFields@1100528600() : Boolean;
    VAR
      PrevExtensionProduction@1100528300 : Decimal;
    BEGIN
      PrevExtensionProduction := "Extension Production";
      "Production Value" := Production * "Unit Price";

      SETFILTER("Prev. Prod. Date/Time Filter", '<%1', "Production Date/Time");
      SETFILTER("Production Date/Time Filter", '<=%1', "Production Date/Time");
      CALCFIELDS("Recording Quantity", "Previous Prod Value Cumulative",
        "Previous Production Date", "Production Value Cumulative");

      IF "Production Cumulative" > "Recording Quantity" THEN BEGIN
        "Extension Production" := "Production Cumulative" - "Recording Quantity";
        IF "Extension Production" > Production THEN
          "Extension Production" := Production;
      END ELSE
        "Extension Production" := 0;

      IF ("Extension Production" < 0 ) THEN
        "Extension Production" := 0;
      "Extension Value" := "Extension Production" * "Unit Price";
      IF ("Extension Value" < 0) THEN
        "Extension Value" := 0;

      IF ("Cost Plus Entries Created") AND ("Extension Production" <> PrevExtensionProduction) THEN
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    PROCEDURE RecalcNextRecordingFields@1100528303();
    VAR
      UnitPriceProdRecordingLine@1100528300 : Record 11071774;
      MessageStr@1100528302 : Text[250];
    BEGIN
      MessageStr := '';

      UnitPriceProdRecordingLine.SETRANGE("Service Order No.", "Service Order No.");
      UnitPriceProdRecordingLine.SETFILTER("Production Date/Time", '>%1', "Production Date/Time");
      UnitPriceProdRecordingLine.SETRANGE("Price Book Type", "Price Book Type");
      UnitPriceProdRecordingLine.SETRANGE("Price Book Code", "Price Book Code");
      UnitPriceProdRecordingLine.SETRANGE("Index Date", "Index Date");
      UnitPriceProdRecordingLine.SETRANGE(Room, Room);
      UnitPriceProdRecordingLine.SETRANGE("Unit Price Code", "Unit Price Code");
      IF NOT UnitPriceProdRecordingLine.FINDSET(TRUE) THEN
        EXIT;

      REPEAT
        UnitPriceProdRecordingLine.SETFILTER("Prev. Prod. Date/Time Filter", '<%1', UnitPriceProdRecordingLine."Production Date/Time");
        UnitPriceProdRecordingLine.CALCFIELDS("Previous Prod. Cumulative");
        UnitPriceProdRecordingLine.VALIDATE(Production);
        IF (NOT UnitPriceProdRecordingLine.CalculateFields) THEN BEGIN
          IF (UnitPriceProdRecordingLine."Cost Plus Entries Created") THEN BEGIN
            IF MessageStr <> '' THEN
              MessageStr += ', ';
            MessageStr := FORMAT(UnitPriceProdRecordingLine."Production Date/Time")
          END;
        END;
        UnitPriceProdRecordingLine.MODIFY;
      UNTIL (UnitPriceProdRecordingLine.NEXT = 0);

      IF (MessageStr <> '') AND (GUIALLOWED) THEN
        MESSAGE(Text002, MessageStr)
    END;

    PROCEDURE GetPreviousProdCumulative@1100528602() : Decimal;
    BEGIN
      SETFILTER("Prev. Prod. Date/Time Filter", '<%1', "Production Date/Time");
      CALCFIELDS("Previous Prod. Cumulative");
      EXIT("Previous Prod. Cumulative");
    END;

    PROCEDURE LookupUnitPriceLineCode@1100528700(CopyAllowed@1100528702 : Boolean;VAR CopyUsed@1100529600 : Boolean);
    VAR
      ServiceUnitPriceLine@1100528701 : Record 11071769;
      ServiceOrder@1100529604 : Record 11012823;
      ServiceOrderExtension@1100529603 : Record 11071727;
      ServiceUnitPriceLineList@1100528700 : Page 11125271;
    BEGIN
      ServiceOrder.GET("Service Order No.");
      ServiceOrderExtension.GetServOrderExtension("Service Order No.");

      ServiceUnitPriceLine.SETRANGE("Price Book Type", ServiceOrder."Price Book Type");
      ServiceUnitPriceLine.SETRANGE("Price Book Code", ServiceOrderExtension."Price Book Code");
      ServiceUnitPriceLine.SETRANGE("Index Date", ServiceOrderExtension."Price Book Index Date");
      ServiceUnitPriceLine.SETFILTER("Unit Price Type", '<>%1', ServiceUnitPriceLine."Unit Price Type"::"Summary Element");

      ServiceUnitPriceLineList.LOOKUPMODE(TRUE);
      ServiceUnitPriceLineList.SETTABLEVIEW(ServiceUnitPriceLine);
      ServiceUnitPriceLineList.LOOKUPMODE(TRUE);
      ServiceUnitPriceLineList.SETTABLEVIEW(ServiceUnitPriceLine);
      ServiceUnitPriceLine.SETRANGE(Code, "Unit Price Code");
      IF ServiceUnitPriceLine.FINDFIRST THEN
        ServiceUnitPriceLineList.SETRECORD(ServiceUnitPriceLine);
      ServiceUnitPriceLine.SETRANGE(Code);

      IF CopyAllowed THEN
        ServiceUnitPriceLineList.SetCopyFilter("Service Order No.", ServiceOrder."Price Book Type",
          ServiceOrderExtension."Price Book Code",  ServiceOrderExtension."Price Book Index Date", "Unit Price Code", Part, Room,
          "Production Seq. No.", DATABASE::"SUP Production Line");
      IF ServiceUnitPriceLineList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ServiceUnitPriceLineList.GETRECORD(ServiceUnitPriceLine);
        VALIDATE("Unit Price Code", ServiceUnitPriceLine.Code);
      END;
      CopyUsed := ServiceUnitPriceLineList.GetCopyUsed;
    END;

    PROCEDURE SetRenameIsAllowed@1100528300(NewRenameIsAllowed@1100528300 : Boolean);
    BEGIN
      RenameIsAllowed := NewRenameIsAllowed;
    END;

    PROCEDURE FillKeyFields@1100528301();
    VAR
      ServiceOrder@1100528300 : Record 11012823;
      ServiceOrderExtension@1100528301 : Record 11071727;
    BEGIN
      IF ("Service Order No." = '') THEN
        EXIT;

      ServiceOrder.GET("Service Order No.");
      ServiceOrderExtension.GetServOrderExtension("Service Order No.");

      IF ("Price Book Type" = 0) THEN
        "Price Book Type" := ServiceOrder."Price Book Type";
      IF ("Price Book Code" = '') THEN
        "Price Book Code" := ServiceOrderExtension."Price Book Code";
      IF ("Index Date" = 0D) THEN
        "Index Date" := ServiceOrderExtension."Price Book Index Date";
    END;

    LOCAL PROCEDURE AddOrUpdateSUPRecordingLine@1100528604(ICurrFieldNo@1100528602 : Integer);
    VAR
      ServiceSetup@1100528604 : Record 11012800;
      SUPRecordingLine@1100528600 : Record 11071772;
      SUPProductionLine@1100528603 : Record 11071774;
      LineNo@1100528601 : Integer;
    BEGIN
      IF ICurrFieldNo = 0 THEN
        EXIT;
      IF "Recording Line No." = 0 THEN BEGIN
        SUPRecordingLine.LOCKTABLE;
        SUPRecordingLine.SETRANGE("Service Order No.", "Service Order No.");
        IF SUPRecordingLine.FINDLAST THEN
          LineNo := SUPRecordingLine."Line No.";
        LineNo += 10000;
        SUPRecordingLine.INIT;
        SUPRecordingLine."Service Order No." := "Service Order No.";
        SUPRecordingLine.InitRecord;
        SUPRecordingLine."Line No." := LineNo;
        SUPRecordingLine.INSERT(TRUE);
        "Recording Line No." := SUPRecordingLine."Line No.";
      END ELSE BEGIN
        SUPRecordingLine.GET("Service Order No.", "Recording Line No.");
        SUPProductionLine.SETRANGE("Service Order No.", "Service Order No.");
        SUPProductionLine.SETRANGE("Recording Line No.", "Recording Line No.");
        SUPProductionLine.SETFILTER("Production Seq. No.", '<>%1', "Production Seq. No.");
        IF NOT SUPProductionLine.ISEMPTY THEN
          ERROR(Text003, SUPProductionLine.TABLECAPTION);
      END;
      IF SUPRecordingLine."Unit Price Code" <> "Unit Price Code" THEN
        SUPRecordingLine.VALIDATE("Unit Price Code", "Unit Price Code");
      IF SUPRecordingLine."Alternative Unit Price Code" <> "Alternative Unit Price Code" THEN
        SUPRecordingLine.VALIDATE("Alternative Unit Price Code", "Alternative Unit Price Code");
      IF SUPRecordingLine.Room <> Room THEN
        SUPRecordingLine.VALIDATE(Room, Room);
      IF "Unit Price Code" = '' THEN BEGIN
        IF SUPRecordingLine.Description <> Description THEN
          SUPRecordingLine.Description := Description;
        IF SUPRecordingLine."Description 2" <> "Description 2" THEN
          SUPRecordingLine."Description 2" := "Description 2";
        IF SUPRecordingLine."Description 3" <> "Description 3" THEN
          SUPRecordingLine."Description 3" := "Description 3";
        IF SUPRecordingLine."Unit of Measure" <> "Unit of Measure" THEN
          SUPRecordingLine."Unit of Measure" := "Unit of Measure";
      END;
      ServiceSetup.GET;
      IF ("Unit Price Code" = '') OR (ServiceSetup."Allow Internal SUP Line Modif." AND ("Price Book Type" = "Price Book Type"::Internal)) THEN
        IF SUPRecordingLine."Unit Price" <> "Unit Price" THEN BEGIN
          SUPRecordingLine."Unit Price" := "Unit Price";
          SUPRecordingLine.CalculateAmountFields();
        END;
      SUPRecordingLine.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE TestIfLineIsNonHeader@1100528609();
    BEGIN
      CALCFIELDS("Unit Price Type");
      IF "Unit Price Type" = "Unit Price Type"::Header THEN
        FIELDERROR("Unit Price Type");
    END;

    BEGIN
    END.
  }
}

