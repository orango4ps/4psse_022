OBJECT Report 11012323 Create ICK Files
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Erstellen ICK-Dateien;
               ENU=Create ICK Files;
               NLD=Aanmaken ICK files;
               NOR=Opprett ICK-filer;
               SVE=Skapa ICK-filer];
    ProcessingOnly=Yes;
    OnPostReport=VAR
                   ErrorRec@1210190000 : Record 11012051;
                   Command@1100525000 : Text[1024];
                 BEGIN
                   IF SendToVendor THEN  BEGIN
                     IF TempVendorRec.FIND('-') THEN BEGIN
                       REPEAT
                         IF TempVendorRec."ICK Batch" <> '' THEN BEGIN
                           Command := STRSUBSTNO('"%1"', TempVendorRec."ICK Batch");
                           OperatingSystemMgt.ClientShell(Command, '', FALSE, TRUE);
                         END;
                       UNTIL TempVendorRec.NEXT = 0;
                     END;
                   END;

                   COMMIT;

                   ErrorRec.SETRANGE("User ID", USERID);
                   ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"ICM Export");
                   IF ErrorRec.FIND('-') THEN
                     PAGE.RUNMODAL(PAGE::Errors, ErrorRec);
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order),
                                       ICM Login Code=FILTER(<>''));
               OnAfterGetRecord=VAR
                                  ExportICMOrders@1100525001 : XMLport 11012343;
                                  OrderOkay@1210190001 : Boolean;
                                  TempBlob@1100525002 : Record 99008535;
                                  PurchLineRec@1210190000 : Record 39;
                                  lVendorRec@1100485000 : Record 23;
                                  OStream@1100525004 : OutStream;
                                  PurchaseHeaderIsOK@1100525000 : Boolean;
                                  ClientTypeManagement@1100529500 : Codeunit 4030;
                                  InStream@1100529501 : InStream;
                                  ToFile@1100529502 : Text;
                                BEGIN
                                  IF "Purchase Header"."ICM Status" > "Purchase Header"."ICM Status"::Create THEN
                                    IF NOT CONFIRM(Text000,FALSE,"Purchase Header"."No.","Purchase Header"."Buy-from Vendor No.") THEN
                                      CurrReport.SKIP;

                                  PurchaseHeaderIsOK := ExportICMOrders.CheckPurchHeader("Purchase Header");

                                  IF PurchaseHeaderIsOK THEN BEGIN
                                    PurchLineRec.SETRANGE("Document Type",PurchLineRec."Document Type"::Order);
                                    PurchLineRec.SETRANGE("Document No.","Purchase Header"."No.");
                                    PurchLineRec.SETRANGE("Removal Contribution",FALSE);
                                    IF PurchLineRec.FINDSET THEN BEGIN
                                      REPEAT
                                        OrderOkay := ExportICMOrders.CheckPurchLine(PurchLineRec);
                                        IF NOT OrderOkay THEN
                                          EXIT;
                                      UNTIL PurchLineRec.NEXT = 0;
                                    END;

                                    IF OrderOkay THEN BEGIN
                                      ExportICMOrders.SetSelection(
                                        "Purchase Header"."No.","Purchase Header"."Buy-from Vendor No.",TRUE,TRUE);
                                      TempBlob.Blob.CREATEOUTSTREAM(OStream);
                                      ExportICMOrders.SETDESTINATION(OStream);
                                      ExportICMOrders.EXPORT;

                                      IF NOT ClientTypeManagement.IsCommonWebClientType THEN
                                        FileMgt.BLOBExport4PS(TempBlob, ExportICMOrders.FILENAME, FALSE, TRUE)
                                      ELSE BEGIN
                                        ToFile := 'icm_filename.txt';
                                        TempBlob.Blob.CREATEINSTREAM(InStream);
                                        DOWNLOADFROMSTREAM(InStream, Text001, '', '', ToFile);
                                      END;

                                      IF SendToVendor THEN BEGIN
                                        IF NOT TempVendorRec.GET("Purchase Header"."Buy-from Vendor No.") THEN BEGIN
                                          IF lVendorRec.GET("Purchase Header"."Buy-from Vendor No.") THEN BEGIN
                                            TempVendorRec.TRANSFERFIELDS(lVendorRec);
                                            IF TempVendorRec.INSERT THEN;
                                          END;
                                        END;
                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Buy-from Vendor No.,ICM Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100409002;0;Container;
                  ContainerType=ContentArea }

      { 1100409001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100409000;2;Field  ;
                  Name=SendToVendor;
                  CaptionML=[DEU=Direktlieferung an Kreditor;
                             ENU=Send to Vendor;
                             NLD=Doorzenden naar leverancier;
                             NOR=Send til leverandõr;
                             SVE=Skicka till leverantîr];
                  SourceExpr=SendToVendor }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012000 : TextConst 'DEU=ICM-Datei fÅr Auftrag %1 Kreditor %2 wurde bereits erstellt. Neu erstellen?;ENU=ICM file for Order %1 Vendor %2 already created, again ?;NLD=ICM file voor order %1 leverancier %2 reeds aangemaakt, opnieuw aanmaken ?;NOR=ICM-fil for ordre %1, leverandõr %2 er allerede opprettet. Opprette igen?;SVE=ICM-fil fîr order %1, leverantîr %2 har redan skapats. Skapa igen?';
      SendToVendor@1100485000 : Boolean;
      TempVendorRec@1100485001 : TEMPORARY Record 23;
      FileMgt@1100525500 : Codeunit 419;
      OperatingSystemMgt@1100409000 : Codeunit 11012364;
      Text001@1100529500 : TextConst 'DEU=Export Datei...;ENU=Export File...;NLD=Exporteer bestand...';

    BEGIN
    {
      db, 25-02-10: copy from report 11012308 (create ICM-files)
    }
    END.
  }
  RDLDATA
  {
  }
}

