OBJECT Table 11012457 Complaint Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lvComplRec@1100485000 : Record 11012450;
             BEGIN
               IF GUIALLOWED THEN BEGIN   // No check filled if webservices
                 TESTFIELD("Document Type");
                 TESTFIELD("Document No.");
               END;

               lvComplRec.GET("Complaint No.");
               "Complaint Status" := lvComplRec.Status;
             END;

    OnModify=BEGIN
               TESTFIELD("Document Type");
               TESTFIELD("Document No.");
             END;

    CaptionML=[ENU=Complaint Document;
               NOR=Klagedokument;
               SVE=Klagom†lsdokument];
  }
  FIELDS
  {
    { 10  ;   ;Complaint No.       ;Code20        ;TableRelation=Complaint.No.;
                                                   CaptionML=[ENU=Complaint No.;
                                                              NOR=Klagenr;
                                                              SVE=Klagom†lsnr] }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;Document Type       ;Option        ;OnValidate=BEGIN
                                                                IF ("Document Type" <> xRec."Document Type") THEN
                                                                  "Document No." := '';
                                                              END;

                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=ENU=" ,Sales Quote,Sales Order,Sales Invoice,Credit Memo,Purch. Quote,Purch. Order,,,Plant Order,Transport Order(Plant),Exit Order (Plant),Rental Correction (Plant)";
                                                   OptionString=[ ,Q-S,O-S,PI-S,PM-S,Q-P,O-P,,,PO-Pl,TO-Pl,EO-Pl,RC-Pl];
                                                   Description=When change options, also update table 'Complaint' (T11012450, field 420) }
    { 40  ;   ;Document No.        ;Code20        ;TableRelation=IF (Document Type=CONST(Q-S)) "Sales Header".No. WHERE (Document Type=CONST(Quote))
                                                                 ELSE IF (Document Type=CONST(O-S)) "Sales Header".No. WHERE (Document Type=CONST(Order))
                                                                 ELSE IF (Document Type=CONST(PI-S)) "Sales Invoice Header"
                                                                 ELSE IF (Document Type=CONST(PM-S)) "Sales Cr.Memo Header"
                                                                 ELSE IF (Document Type=CONST(Q-P)) "Purchase Header".No. WHERE (Document Type=CONST(Quote))
                                                                 ELSE IF (Document Type=CONST(O-P)) "Purchase Header".No. WHERE (Document Type=CONST(Order))
                                                                 ELSE IF (Document Type=CONST(PO-Pl)) "Plant Order" WHERE (Posted=CONST(Yes))
                                                                 ELSE IF (Document Type=CONST(TO-Pl)) "Posted Plant Transport Order"
                                                                 ELSE IF (Document Type=CONST(EO-Pl)) "Exit Order" WHERE (Posted=CONST(Yes))
                                                                 ELSE IF (Document Type=CONST(RC-Pl)) "Rental Correction" WHERE (Posted=CONST(Yes));
                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr] }
    { 60  ;   ;Complaint Status    ;Option        ;FieldClass=Normal;
                                                   CaptionML=[ENU=Complaint Status;
                                                              NOR=Klagestatus;
                                                              SVE=Klagom†lsstatus];
                                                   OptionCaptionML=[ENU=Received,In Process,Waiting,Finished;
                                                                    NOR=Mottatt,Under Behandling,Venter,Ferdig;
                                                                    SVE=Mottaget,Under behandling,V„ntar,F„rdig];
                                                   OptionString=Received,In Process,Waiting,Finished;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Complaint No.,Line No.                  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE LookupDocumentNo@1100485002(IEditableMode@1100485004 : Boolean);
    VAR
      lvComplRec@1100485007 : Record 11012450;
      lvSalesHeadRec@1100485000 : Record 36;
      lvSalesInvoiceHeadRec@1100485001 : Record 112;
      lvSalesCredMemHeadRec@1100485002 : Record 114;
      lvPurchHeadRec@1100485003 : Record 38;
      lvPlantOrderRec@1100485005 : Record 11012556;
      lvPostedPlTransOrderRec@1100485006 : Record 11020507;
      ExitOrder@1100525000 : Record 11012559;
      RentalCorrection@1100525001 : Record 11012565;
    BEGIN
      IF NOT lvComplRec.GET("Complaint No.") THEN BEGIN
        lvComplRec."Source Type" := 0;
        lvComplRec."Source No." := '';
      END;

      CASE "Document Type" OF
        "Document Type":: "Q-S",
        "Document Type":: "O-S":
          BEGIN
            lvSalesHeadRec.FILTERGROUP(9);
            IF ("Document Type" = "Document Type":: "Q-S") THEN
              lvSalesHeadRec."Document Type" := lvSalesHeadRec."Document Type"::Quote
            ELSE
              lvSalesHeadRec."Document Type" := lvSalesHeadRec."Document Type"::Order;
            lvSalesHeadRec.SETRANGE("Document Type", lvSalesHeadRec."Document Type");
            lvSalesHeadRec.FILTERGROUP(0);
            IF (lvComplRec."Source Type" = lvComplRec."Source Type"::Cust) AND (lvComplRec."Source No." <> '') THEN BEGIN
              lvSalesHeadRec.SETCURRENTKEY("Document Type", "Sell-to Customer No.", "No.");
              lvSalesHeadRec.SETRANGE("Sell-to Customer No.", lvComplRec."Source No.");
              lvSalesHeadRec."Sell-to Customer No." := lvComplRec."Source No.";
            END;
            lvSalesHeadRec."No." := "Document No.";
            IF PAGE.RUNMODAL(0, lvSalesHeadRec) = ACTION::LookupOK THEN BEGIN
              IF IEditableMode THEN
                "Document No." := lvSalesHeadRec."No.";
            END;
          END;
        "Document Type":: "PI-S":
          BEGIN
            IF (lvComplRec."Source Type" = lvComplRec."Source Type"::Cust) AND (lvComplRec."Source No." <> '') THEN BEGIN
              lvSalesInvoiceHeadRec.SETCURRENTKEY("Sell-to Customer No.", "No.");
              lvSalesInvoiceHeadRec.SETRANGE("Sell-to Customer No.", lvComplRec."Source No.");
              lvSalesInvoiceHeadRec."Sell-to Customer No." := lvComplRec."Source No.";
            END;
            lvSalesInvoiceHeadRec."No." := "Document No.";
            IF PAGE.RUNMODAL(0, lvSalesInvoiceHeadRec) = ACTION::LookupOK THEN BEGIN
              IF IEditableMode THEN
                "Document No." := lvSalesInvoiceHeadRec."No.";
            END;
          END;
        "Document Type":: "PM-S":
          BEGIN
            IF (lvComplRec."Source Type" = lvComplRec."Source Type"::Cust) AND (lvComplRec."Source No." <> '') THEN BEGIN
              lvSalesCredMemHeadRec.SETCURRENTKEY("Sell-to Customer No.", "No.");
              lvSalesCredMemHeadRec.SETRANGE("Sell-to Customer No.", lvComplRec."Source No.");
              lvSalesCredMemHeadRec."Sell-to Customer No." := lvComplRec."Source No.";
            END;
            lvSalesCredMemHeadRec."No." := "Document No.";
            IF PAGE.RUNMODAL(0, lvSalesCredMemHeadRec) = ACTION::LookupOK THEN BEGIN
              IF IEditableMode THEN
                "Document No." := lvSalesCredMemHeadRec."No.";
            END;
          END;
        "Document Type":: "Q-P",
        "Document Type":: "O-P":
          BEGIN
            lvPurchHeadRec.FILTERGROUP(9);
            IF ("Document Type" = "Document Type":: "Q-P") THEN
              lvPurchHeadRec."Document Type" := lvPurchHeadRec."Document Type"::Quote
            ELSE
              lvPurchHeadRec."Document Type" := lvPurchHeadRec."Document Type"::Order;
            lvPurchHeadRec.SETRANGE("Document Type", lvPurchHeadRec."Document Type");
            lvPurchHeadRec.FILTERGROUP(0);
            IF (lvComplRec."Source Type" = lvComplRec."Source Type"::Vend) AND (lvComplRec."Source No." <> '') THEN BEGIN
              lvPurchHeadRec.SETCURRENTKEY("Document Type", "Buy-from Vendor No.", "No.");
              lvPurchHeadRec.SETRANGE("Buy-from Vendor No.", lvComplRec."Source No.");
              lvPurchHeadRec."Buy-from Vendor No." := lvComplRec."Source No.";
            END;
            lvPurchHeadRec."No." := "Document No.";
            IF PAGE.RUNMODAL(0, lvPurchHeadRec) = ACTION::LookupOK THEN BEGIN
              IF IEditableMode THEN
                "Document No." := lvPurchHeadRec."No.";
            END;
          END;
        "Document Type":: "PO-Pl":
          BEGIN
            lvPlantOrderRec."No." := "Document No.";
            IF PAGE.RUNMODAL(PAGE::"Posted Plant Order List", lvPlantOrderRec) = ACTION::LookupOK THEN BEGIN
              IF IEditableMode THEN
                "Document No." := lvPlantOrderRec."No.";
            END;
          END;
        "Document Type":: "TO-Pl":
          BEGIN
            lvPostedPlTransOrderRec."No." := "Document No.";
            IF PAGE.RUNMODAL(0, lvPostedPlTransOrderRec) = ACTION::LookupOK THEN BEGIN
              IF IEditableMode THEN
                "Document No." := lvPostedPlTransOrderRec."No.";
            END;
          END;
        "Document Type":: "EO-Pl":
          BEGIN
            ExitOrder."No." := "Document No.";
            IF PAGE.RUNMODAL(PAGE::"Posted Exit Order List", ExitOrder) = ACTION::LookupOK THEN BEGIN
              IF IEditableMode THEN
                "Document No." := ExitOrder."No.";
            END;
          END;
        "Document Type":: "RC-Pl":
          BEGIN
            RentalCorrection."No." := "Document No.";
            IF PAGE.RUNMODAL(PAGE::"Posted Rental Correction List", RentalCorrection) = ACTION::LookupOK THEN BEGIN
              IF IEditableMode THEN
                "Document No." := RentalCorrection."No.";
            END;
          END;
      END;
    END;

    BEGIN
    END.
  }
}

