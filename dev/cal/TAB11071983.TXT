OBJECT Table 11071983 SUP Room Relation Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF NOT IsRoomRelationLineWithinRoomUnique THEN BEGIN
                 IF "Unit Price Code" <> '' THEN
                   FIELDERROR("Unit Price Code", Text000);
                 IF "Unit Price Set Code" <> '' THEN
                   FIELDERROR("Unit Price Set Code", Text000);
               END;
             END;

    CaptionML=ENU=SUP Room Relation Line;
    LookupPageID=Page11229813;
    DrillDownPageID=Page11229813;
  }
  FIELDS
  {
    { 10  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                IF "Customer No." <> '' THEN BEGIN
                                                                  "Price Book Type" := "Price Book Type"::Customer;
                                                                  "Price Book Code" := "Customer No.";
                                                                END ELSE BEGIN
                                                                  "Price Book Type" := "Price Book Type"::Internal;
                                                                  "Price Book Code" := '';
                                                                END;
                                                                "Unit Price Code" := '';
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr];
                                                   Editable=No }
    { 20  ;   ;Room Code           ;Code20        ;TableRelation="SUP Room".Code;
                                                   CaptionML=ENU=Room Code;
                                                   Editable=No }
    { 30  ;   ;Index Date          ;Date          ;OnValidate=BEGIN
                                                                IF ("Index Date" <> 0D) AND (xRec."Index Date" <> "Index Date") THEN
                                                                  "Unit Price Code" := '';
                                                              END;

                                                   CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum];
                                                   Editable=No }
    { 35  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer];
                                                   Editable=No }
    { 40  ;   ;Price Book Type     ;Option        ;CaptionML=[ENU=Price Book Type;
                                                              SVE=Enhetspristyp];
                                                   OptionCaptionML=[ENU=" ,Internal,Customer";
                                                                    NOR=" ,Intern,Kunde";
                                                                    SVE=" ,Intern,Kund"];
                                                   OptionString=[ ,Internal,Customer];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 50  ;   ;Price Book Code     ;Code20        ;TableRelation="Service Price Book".Code WHERE (Type=FIELD(Price Book Type));
                                                   OnLookup=VAR
                                                              ServiceUnitPrice@1100528600 : Record 11071768;
                                                              ServiceUnitPriceList@1100528601 : Page 11125269;
                                                            BEGIN
                                                              ServiceUnitPrice.SETRANGE(Type, "Price Book Type");
                                                              ServiceUnitPriceList.LOOKUPMODE(TRUE);
                                                              ServiceUnitPriceList.SETTABLEVIEW(ServiceUnitPrice);
                                                              IF (ServiceUnitPriceList.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                                                                ServiceUnitPriceList.GETRECORD(ServiceUnitPrice);
                                                                "Price Book Code" := ServiceUnitPrice.Code;
                                                                "Index Date" := ServiceUnitPrice."Index Date";
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Price Book Code;
                                                              SVE=Enhetspriskod];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 60  ;   ;Unit Price Code     ;Code20        ;TableRelation="Service Unit Price".Code WHERE (Price Book Type=FIELD(Price Book Type),
                                                                                                  Price Book Code=FIELD(Price Book Code),
                                                                                                  Index Date=FIELD(Index Date));
                                                   OnValidate=BEGIN
                                                                IF "Unit Price Code" <> '' THEN
                                                                  TESTFIELD("Unit Price Set Code", '');
                                                              END;

                                                   CaptionML=[ENU=Unit Price Code;
                                                              NOR=Enhetspriskode;
                                                              SVE=Enhetspriskod] }
    { 70  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Unit Price".Description WHERE (Price Book Type=FIELD(Price Book Type),
                                                                                                              Price Book Code=FIELD(Price Book Code),
                                                                                                              Index Date=FIELD(Index Date),
                                                                                                              Code=FIELD(Unit Price Code)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 80  ;   ;Unit Price Set Code ;Code20        ;TableRelation="Service Unit Price Set".Code WHERE (Price Book Type=FIELD(Price Book Type),
                                                                                                      Price Book Code=FIELD(Price Book Code),
                                                                                                      Price Book Index Date=FIELD(Index Date));
                                                   OnValidate=BEGIN
                                                                IF "Unit Price Set Code" <> '' THEN
                                                                  TESTFIELD("Unit Price Code", '');
                                                              END;

                                                   CaptionML=ENU=Unit Price Set Code }
  }
  KEYS
  {
    {    ;Customer No.,Room Code,Index Date,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=already exists';

    PROCEDURE LookupUnitPriceLineCode@1100528700(CopyAllowed@1100528702 : Boolean;VAR OCopyUsed@1100528601 : Boolean);
    VAR
      ServiceUnitPriceLine@1100528701 : Record 11071769;
      ServiceUnitPriceLineList@1100528700 : Page 11125271;
    BEGIN
      ServiceUnitPriceLine.SETRANGE("Price Book Type", "Price Book Type");
      ServiceUnitPriceLine.SETRANGE("Price Book Code", "Price Book Code");
      ServiceUnitPriceLine.SETRANGE("Index Date", "Index Date");
      ServiceUnitPriceLine.SETFILTER(Part, '%1', '');
      ServiceUnitPriceLineList.LOOKUPMODE(TRUE);
      ServiceUnitPriceLineList.SETTABLEVIEW(ServiceUnitPriceLine);
      ServiceUnitPriceLine.SETRANGE(Code, "Unit Price Code");
      IF ServiceUnitPriceLine.FINDFIRST THEN
        ServiceUnitPriceLineList.SETRECORD(ServiceUnitPriceLine);
      ServiceUnitPriceLine.SETRANGE(Code);
      IF CopyAllowed THEN
        ServiceUnitPriceLineList.SetCopyFilter('', "Price Book Type",
          "Price Book Code", "Index Date", "Unit Price Code", '', "Room Code", 0, DATABASE::"SUP Room Relation Line");
      IF ServiceUnitPriceLineList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ServiceUnitPriceLineList.GETRECORD(ServiceUnitPriceLine);
        VALIDATE("Unit Price Code", ServiceUnitPriceLine.Code);
      END;
      OCopyUsed := ServiceUnitPriceLineList.GetCopyUsed;
    END;

    PROCEDURE IsRoomRelationLineWithinRoomUnique@1100528600() : Boolean;
    VAR
      SUPRoomRelationLine@1100528600 : Record 11071983;
    BEGIN
      SUPRoomRelationLine.SETRANGE("Customer No.", "Customer No.");
      SUPRoomRelationLine.SETRANGE("Room Code", "Room Code");
      SUPRoomRelationLine.SETRANGE("Index Date", "Index Date");
      SUPRoomRelationLine.SETRANGE("Price Book Type", "Price Book Type");
      SUPRoomRelationLine.SETRANGE("Price Book Code", "Price Book Code");
      IF "Unit Price Code" <> '' THEN
        SUPRoomRelationLine.SETRANGE("Unit Price Code", "Unit Price Code");
      IF "Unit Price Set Code" <> '' THEN
        SUPRoomRelationLine.SETRANGE("Unit Price Set Code", "Unit Price Set Code");
      EXIT(SUPRoomRelationLine.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

