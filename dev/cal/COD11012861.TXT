OBJECT Codeunit 11012861 BORG User Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100528601 : TextConst 'DEU=UngÅltiges Passwort;ENU=Invalid Password;NLD=Ongeldig wachtwoord;NOR=Ugyldig passord;SVE=Ogiltigt lîsenord';
      Authorized@1100528603 : Boolean;
      EmployeeNo@1100528602 : Code[20];
      LastEntry@1100528604 : DateTime;

    PROCEDURE UpdateBorgEmplTimeOut@1100528605();
    VAR
      ServiceSetup@1100528602 : Record 11012800;
    BEGIN
      ServiceSetup.GET;
      IF Authorized THEN
        IF CURRENTDATETIME - LastEntry <= ServiceSetup."BORG Password Time Out" * 1000 * 60 THEN
          LastEntry := CURRENTDATETIME
        ELSE
          Authorized := FALSE;
    END;

    PROCEDURE IsBorgEmplTimedOut@1100528611() : Boolean;
    VAR
      BORGManagement@1100528600 : Codeunit 11012859;
      ServiceSetup@1100528602 : Record 11012800;
    BEGIN
      ServiceSetup.GET;
      IF (BORGManagement.GetEmployeeNo = EmployeeNo) AND Authorized THEN
        IF CURRENTDATETIME - LastEntry > ServiceSetup."BORG Password Time Out" * 1000 * 60 THEN
          Authorized := FALSE;
      EXIT(NOT Authorized);
    END;

    PROCEDURE AskBORGPassword@1100528606() : Boolean;
    VAR
      BORGManagement@1100528604 : Codeunit 11012859;
      BORGPasswordFRM@1100528603 : Page 11071799;
      Password@1100528600 : Text[80];
    BEGIN
      Authorized := FALSE;
      BORGManagement.TestBORGUser;
      BORGManagement.TestBORGPassword;
      BORGManagement.TestBORGPasswordTimeOut;
      BORGPasswordFRM.LOOKUPMODE := TRUE;
      IF BORGPasswordFRM.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Password := BORGPasswordFRM.GetPassword;
        IF BORGManagement.IsBORGPasswordOK(Password) THEN BEGIN
          Authorized := TRUE;
          EmployeeNo := BORGManagement.GetEmployeeNo;
          LastEntry := CURRENTDATETIME;
        END ELSE
          ERROR(Text000);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE RemoveBorgEmplTimeOut@1100528604();
    BEGIN
      Authorized := FALSE;
      CLEAR(LastEntry);
    END;

    BEGIN
    {
      Single Instance=Yes
    }
    END.
  }
}

