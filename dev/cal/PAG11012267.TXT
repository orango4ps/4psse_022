OBJECT Page 11012267 Extra Receipts
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Extra Receipts;
               NOR=Ekstra kvitteringer;
               SVE=Extra inleveranser];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table39;
    SourceTableView=WHERE(Document Type=CONST(Order));
    DataCaptionFields=Description;
    PageType=Card;
    OnInit=BEGIN
             AmountTextBoxEditable := TRUE;
             QuanTextBoxEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
               END;

    OnAfterGetRecord=BEGIN
                       PurchHeader.GET("Document Type", "Document No.");
                       QuanTextBoxEditable := (NOT PurchHeader."Amounts only");
                       AmountTextBoxEditable := (PurchHeader."Amounts only");
                     END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190013;1 ;Action    ;
                      CaptionML=[ENU=&Insert;
                                 NOR=&Sett inn;
                                 SVE=&Infoga];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 InsertExtraOrderLine;

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group      }

    { 1210190000;2;Field  ;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 1210190002;2;Field  ;
                SourceExpr="Buy-from Vendor No.";
                Editable=FALSE }

    { 1210190004;2;Field  ;
                SourceExpr="Shortcut Dimension 2 Code";
                Editable=FALSE }

    { 1210190006;2;Field  ;
                SourceExpr=Quantity;
                Editable=FALSE }

    { 1210190008;2;Field  ;
                SourceExpr="Unit of Measure Code";
                Editable=FALSE }

    { 1210190010;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE }

    { 1100525000;2;Field  ;
                CaptionML=[ENU=Element;
                           NOR=Element;
                           SVE=Element];
                SourceExpr=ElementCde;
                TableRelation="Project Element".Element WHERE (Project No.=FIELD(Job No.));
                OnValidate=BEGIN
                             IF ElementCde <> '' THEN
                               IF NOT ProjectElementRec.GET("Job No.",ElementCde) THEN
                                 ERROR(STRSUBSTNO(text001,ElementCde,"Job No."));
                           END;

                OnLookup=VAR
                           ProjectElementPage@1100525000 : Page 11012029;
                         BEGIN
                           IF "Job No." <> '' THEN BEGIN
                             CLEAR(ProjectElementPage);
                             ProjectElementRec.SETRANGE("Project No.","Job No.");
                             ProjectElementPage.SETTABLEVIEW(ProjectElementRec);
                             ProjectElementPage.LOOKUPMODE(TRUE);
                             IF ProjectElementPage.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               ProjectElementPage.GETRECORD(ProjectElementRec);
                               ElementCde := ProjectElementRec.Element;
                             END;
                           END;
                         END;
                          }

    { 1210190014;2;Field  ;
                CaptionML=[ENU=Extension Contract;
                           NOR=ETA;
                           SVE=ŽTA];
                SourceExpr=ExtensionCde;
                OnValidate=VAR
                             ExtensionContractRec@1210190000 : Record 11012004;
                           BEGIN
                             IF ExtensionCde <> '' THEN
                               IF NOT ExtensionContractRec.GET("Job No.",ExtensionCde) THEN
                                 ERROR(STRSUBSTNO(text000,ExtensionCde,"Job No."));
                           END;

                OnLookup=VAR
                           ExtensionContractFrm@1210190000 : Page 11012014;
                           ExtensionContractRec@1210190001 : Record 11012004;
                         BEGIN
                           IF "Job No." <> '' THEN BEGIN
                             CLEAR(ExtensionContractFrm);
                             ExtensionContractRec.SETRANGE("Project No.","Job No.");
                             ExtensionContractFrm.SETTABLEVIEW(ExtensionContractRec);
                             ExtensionContractFrm.LOOKUPMODE(TRUE);
                             IF ExtensionContractFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               ExtensionContractFrm.GETRECORD(ExtensionContractRec);
                               ExtensionCde := ExtensionContractRec."Contract No.";
                             END;
                           END;
                         END;
                          }

    { 1210190009;2;Field  ;
                CaptionML=[ENU=Extra Description;
                           NOR=Ekstra Beskrivelse;
                           SVE=Extra beskrivning];
                SourceExpr=ExtraDescTxt }

    { 1210190015;2;Field  ;
                Name=QuanTextBox;
                CaptionML=[ENU=Extra Quantity;
                           NOR=Ekstra antall;
                           SVE=Extra antal];
                DecimalPlaces=0:5;
                SourceExpr=ExtraQuanDec;
                Editable=QuanTextBoxEditable }

    { 1100485000;2;Field  ;
                Name=AmountTextBox;
                CaptionML=[ENU=Extra Amount;
                           NOR=Ekstra bel›p;
                           SVE=Extra belopp];
                SourceExpr=ExtraAmount;
                Editable=AmountTextBoxEditable }

  }
  CODE
  {
    VAR
      PurchHeader@1100485004 : Record 38;
      ExtensionCde@1100485003 : Code[10];
      ExtraDescTxt@1210190000 : Text[50];
      ExtraQuanDec@1210190001 : Decimal;
      ExtraAmount@1100485000 : Decimal;
      text000@1210190003 : TextConst 'ENU=Extension contract %1 does not exist for project %2;NOR=ETA %1 finnes ikke for prosjektet %2;SVE=ŽTA %1 finns inte f”r projektet %2';
      DPA_IsHardFilter@1100000999 : Boolean;
      QuanTextBoxEditable@19017492 : Boolean INDATASET;
      AmountTextBoxEditable@19030150 : Boolean INDATASET;
      ElementCde@1100525000 : Code[20];
      ProjectElementRec@1100525001 : Record 11012010;
      text001@1100525002 : TextConst 'ENU=Extension contract %1 does not exist for project %2;NOR=ETA %1 finnes ikke for prosjektet %2;SVE=ŽTA %1 finns inte f”r projektet %2';

    LOCAL PROCEDURE InsertExtraOrderLine@1100525001();
    VAR
      PurchLineRec@1100525001 : Record 39;
      PurchaseLineExtension@1100525000 : Record 11020644;
      PurchaseHeader@1100528903 : Record 38;
      Item@1100528900 : Record 27;
      TradeItem@1100528901 : Record 11012317;
      RemovalContribution@1100528902 : Decimal;
    BEGIN
      IF (ExtraQuanDec = 0) AND (ExtraAmount = 0) THEN
        EXIT;

      PurchLineRec.INIT;
      PurchLineRec.SuspendStatusCheck(TRUE);
      PurchLineRec."Document Type"  := "Document Type";
      PurchLineRec."Document No." := "Document No.";
      PurchLineRec."Line No." := NewLineNo();
      PurchLineRec.INSERT;  //C056705

      PurchLineRec.VALIDATE("Currency Code","Currency Code");
      //C055731.sn
      PurchLineRec.VALIDATE(Type,Type);
      PurchLineRec."System-Created Entry"  := TRUE;
      //C055731.en
      PurchLineRec."Job No." := "Job No.";
      PurchLineRec."Service Contract No." := "Service Contract No.";
      PurchLineRec.VALIDATE("Service Order No.", "Service Order No.");
      PurchLineRec.VALIDATE("No.", "No.");
      PurchLineRec."Unit Price (LCY)" := "Unit Price (LCY)";
      PurchLineRec."Subcontracting Activity" := "Subcontracting Activity";
      IF ElementCde <> '' THEN
        PurchLineRec.Element := ElementCde
      ELSE
        PurchLineRec.Element := Element;
      IF ExtensionCde <> '' THEN
        PurchLineRec."Extension Contract" := ExtensionCde
      ELSE
        PurchLineRec."Extension Contract" := "Extension Contract";
      PurchLineRec.VALIDATE("Purchase Action", "Purchase Action");
      PurchLineRec.VALIDATE("Tender Paragraph","Tender Paragraph");
      PurchLineRec."Purchase Phase" := '';
      PurchLineRec."Cost Type" := "Cost Type";
      //C057041.sn: change sequence validate item data
      IF "Trade Item"<> '' THEN BEGIN
        PurchLineRec."Vendor (Trade Item)" := "Vendor (Trade Item)";
        PurchLineRec.VALIDATE("Trade Item","Trade Item");
      END ELSE BEGIN
        IF "Basic Item" <> '' THEN BEGIN
          PurchLineRec.Manufacturer := Manufacturer;
          PurchLineRec.VALIDATE("Basic Item", "Basic Item");
        END ELSE BEGIN
          IF "Item No."<> '' THEN
            PurchLineRec.VALIDATE("Item No.", "Item No.");
        END;
      END;
      //C057041.en
      PurchLineRec.VALIDATE("Plant Type","Plant Type");  //*29594   Do not fill PlantNo (extra receipt of same PlantNo not allowed
      PurchLineRec.VALIDATE("Cost Component Plant","Cost Component Plant");  //*29594
      //
      PurchLineRec."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
      PurchLineRec."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
      PurchLineRec."Dimension Set ID" := "Dimension Set ID";
      //
      PurchLineRec."Extra Receipt Line" := TRUE;
      IF PurchHeader."Amounts only" THEN BEGIN
        PurchLineRec.VALIDATE("Line Amount", ExtraAmount);
        PurchLineRec.VALIDATE("Unit of Measure Code","Unit of Measure Code");
      END ELSE BEGIN
        PurchLineRec.VALIDATE("Location Code", "Location Code");
        IF "Bin Code" <> '' THEN
          PurchLineRec.VALIDATE("Bin Code", "Bin Code");  //C059122
        PurchLineRec.VALIDATE(Quantity, ExtraQuanDec);
        PurchLineRec.VALIDATE("Unit of Measure Code","Unit of Measure Code");
        IF "Direct Unit Cost" < 0 THEN
          PurchLineRec.VALIDATE("Direct Unit Cost",-"Direct Unit Cost")
        ELSE
          PurchLineRec.VALIDATE("Direct Unit Cost","Direct Unit Cost");
      END;
      PurchLineRec.VALIDATE("Line Discount %","Line Discount %");  //*C-025870.n
      PurchLineRec."Cost Component" := "Cost Component";
      PurchLineRec."Posting Date" := "Posting Date";
      PurchLineRec.VALIDATE("Expected Receipt Date","Expected Receipt Date");
      IF ExtraDescTxt <> '' THEN
        PurchLineRec.Description := ExtraDescTxt
      ELSE
        PurchLineRec.Description := Description;
      PurchLineRec.FillMainProject;
      PurchLineRec."Inward Processing Relief" := "Inward Processing Relief";
      PurchLineRec."Input by" := USERID;
      PurchLineRec."Input Date" := TODAY;
      PurchLineRec.MODIFY;  //C056705

      PurchLineRec.CreatePurchOrderControl(FALSE);
      PurchaseLineExtension.CopyPurchLineExtension(
        "Document Type", "Document No.", "Line No.",
        PurchLineRec."Document Type", PurchLineRec."Document No.", PurchLineRec."Line No.");

      //C057041.sn
      IF "Trade Item" <> '' THEN BEGIN
        IF NOT TradeItem.GET("Vendor (Trade Item)", "Trade Item") THEN EXIT;
        RemovalContribution := TradeItem."Removal Contribution";
      END ELSE BEGIN
        IF "Item No." <> '' THEN BEGIN
          IF NOT Item.GET("Item No.") THEN EXIT;
          RemovalContribution := Item."Removal Contribution";
        END;
      END;
      IF RemovalContribution <> 0 THEN BEGIN
        PurchaseHeader.GET(PurchLineRec."Document Type", PurchLineRec."Document No.");
        PurchaseHeader.DetermineRemovalContribution;
      END;
      //C057041.en
    END;

    LOCAL PROCEDURE NewLineNo@1210190000() LineNo : Integer;
    VAR
      lPurchLineRec@1210190000 : Record 39;
    BEGIN
      LineNo := "Line No.";
      WHILE lPurchLineRec.GET(lPurchLineRec."Document Type"::Order,"Document No.",LineNo) DO
        LineNo := LineNo + 10;
      EXIT(LineNo);
    END;

    BEGIN
    {
      4PS, 13-12-2013, T004036, Dimensions are copied from original purchase order line to the new line
    }
    END.
  }
}

