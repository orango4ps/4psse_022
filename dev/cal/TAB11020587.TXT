OBJECT Table 11020587 Loan Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               DimMgt.UpdateDefaultDim(
                 DATABASE::"Loan Header",Code,
                 "Global Dimension 1 Code","Global Dimension 2 Code");
             END;

    OnDelete=VAR
               LoanLine@1100525000 : Record 11020588;
               LoanGuarantee@1100528700 : Record 11229289;
               LoanLedgerEntry@1100528500 : Record 11229444;
             BEGIN
               LoanLedgerEntry.SETRANGE("Loan Code", Code);
               IF LoanLedgerEntry.FINDSET THEN
                 ERROR(Text012);

               IF "End Date" > TODAY THEN
                 IF NOT CONFIRM(Text001) THEN
                   ERROR(Text000);

               LoanLine.SETRANGE("Loan Header Code", Code);
               LoanLine.DELETEALL(TRUE);

               LoanGuarantee.SETRANGE("Loan Header Code", Code);
               LoanGuarantee.DELETEALL(TRUE);

               DimMgt.DeleteDefaultDim(DATABASE::"Loan Header",Code);
             END;

    CaptionML=[ENU=Loan Header;
               NOR=LÜnetittel;
               SVE=LÜnerubrik];
    LookupPageID=Page11124871;
    DrillDownPageID=Page11124871;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=Lent,Borrowed,Derivative;
                                                   OptionString=Lent,Borrowed,Derivative }
    { 40  ;   ;Relation Type       ;Option        ;OnValidate=BEGIN
                                                                VALIDATE(Relation, '');
                                                              END;

                                                   CaptionML=[ENU=Relation Type;
                                                              NOR=Relasjonstype;
                                                              SVE=Relationstyp];
                                                   OptionCaptionML=[ENU=Intern,Extern;
                                                                    NOR=Internt,Eksternt;
                                                                    SVE=Internt,Externt];
                                                   OptionString=Intern,Extern }
    { 50  ;   ;Relation            ;Text30        ;TableRelation=IF (Relation Type=CONST(Intern)) Company
                                                                 ELSE IF (Relation Type=CONST(Extern)) Contact;
                                                   OnValidate=BEGIN
                                                                IF Relation <> '' THEN
                                                                  VALIDATE("Project No.")
                                                                ELSE
                                                                  VALIDATE("Project No.", '');
                                                              END;

                                                   CaptionML=[ENU=Relation;
                                                              NOR=Relasjon;
                                                              SVE=Relation] }
    { 60  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                CheckPresentLines();
                                                                CheckAmount;
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              NOR=Belõp;
                                                              SVE=Belopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 70  ;   ;Start Date          ;Date          ;OnValidate=BEGIN
                                                                CalcEndDate;
                                                              END;

                                                   CaptionML=[ENU=Start Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 80  ;   ;Term                ;DateFormula   ;OnValidate=BEGIN
                                                                CalcEndDate;
                                                              END;

                                                   CaptionML=[ENU=Term;
                                                              NOR=Periode;
                                                              SVE=Villkor] }
    { 90  ;   ;Interest %          ;Decimal       ;OnValidate=BEGIN
                                                                CheckPresentLines();
                                                                SetInterestGroup("Interest %");
                                                              END;

                                                   CaptionML=[ENU=Interest %;
                                                              NOR=Rente %;
                                                              SVE=RÑnta %];
                                                   DecimalPlaces=0:5 }
    { 100 ;   ;Interest Calculation;Option        ;OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=[ENU=Interest Calculation;
                                                              NOR=Renteberegning;
                                                              SVE=RÑnteberÑkning];
                                                   OptionCaptionML=[ENU=Act/365,Act/360,365/365;
                                                                    NOR=Handling/365,Handling/360,365/365;
                                                                    SVE=ètg./365,ètg./360,365/365];
                                                   OptionString=Act/365,Act/360,365/365 }
    { 110 ;   ;Installment Term    ;DateFormula   ;OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=[ENU=Installment Term;
                                                              NOR=Betalingsplansperiode;
                                                              SVE=Betalplansvillkor] }
    { 115 ;   ;Installment Method  ;Option        ;OnValidate=BEGIN
                                                                CheckPresentLines();
                                                                IF FORMAT("Installment Term") <> '' THEN
                                                                  ERROR(Text004, FIELDCAPTION("Installment Term"));
                                                                IF "Installment Method" = "Installment Method"::"End Term" THEN
                                                                  FillEndTermFields;
                                                              END;

                                                   CaptionML=[ENU=Installment Method;
                                                              NOR=Betalingsplansmetode;
                                                              SVE=Betalplansmetod];
                                                   OptionCaptionML=[ENU=Lineair,End Term;
                                                                    NOR=Lineër,Avslutt periode;
                                                                    SVE=LinjÑr,Sluttid];
                                                   OptionString=Lineair,End Term }
    { 120 ;   ;Date First Installment;Date        ;OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=[ENU=Date First Installment;
                                                              NOR=Dato fõrste avdrag;
                                                              SVE=Datum fîrsta avbetalning] }
    { 130 ;   ;Interim Payments Possible;Boolean  ;CaptionML=[ENU=Interim Payments Possible;
                                                              NOR=Midlertige betalinger mulige;
                                                              SVE=Lîpande betalningar mîjliga] }
    { 135 ;   ;No of Installments Planned;Integer ;OnValidate=BEGIN
                                                                CalcInstallmentAmount;
                                                              END;

                                                   CaptionML=ENU=No of Installments Planned }
    { 137 ;   ;Installment Amount  ;Decimal       ;OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=[ENU=Installment Amount;
                                                              NOR=Betalingsplansbelõp;
                                                              SVE=Betalplansbelopp] }
    { 140 ;   ;Comments            ;Text100       ;CaptionML=[ENU=Comments;
                                                              NOR=Merknader;
                                                              SVE=Kommentarer] }
    { 150 ;   ;Interest % Group    ;Text10        ;CaptionML=[ENU=Interest % Group;
                                                              NOR=Rente %, Gruppe;
                                                              SVE=RÑnta %, grupp];
                                                   Editable=No }
    { 160 ;   ;End Date            ;Date          ;CaptionML=[ENU=End Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum];
                                                   Editable=No }
    { 170 ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 180 ;   ;Interest Term       ;DateFormula   ;OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=[ENU=Interest Term;
                                                              NOR=Renteperiode;
                                                              SVE=RÑntevillkor] }
    { 190 ;   ;No of Interest Payments;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Loan Line" WHERE (Loan Header Code=FIELD(Code),
                                                                                        Type=CONST(Interest)));
                                                   OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=ENU=No of Interest Payments;
                                                   Editable=No }
    { 200 ;   ;Amount Interest Payments;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Loan Line".Amount WHERE (Loan Header Code=FIELD(Code),
                                                                                             Type=CONST(Interest)));
                                                   OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=[ENU=Amount Interest Payments;
                                                              NOR=Rentebetalinger, belõp;
                                                              SVE=RÑntebetalningar, belopp];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 210 ;   ;No of Installments  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Loan Line" WHERE (Loan Header Code=FIELD(Code),
                                                                                        Type=FILTER(Installment|Last Installment)));
                                                   OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=ENU=No of Installments;
                                                   Editable=No }
    { 220 ;   ;Amount Installments ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Loan Line".Amount WHERE (Loan Header Code=FIELD(Code),
                                                                                             Type=FILTER(Installment|Last Installment)));
                                                   OnValidate=BEGIN
                                                                CheckPresentLines();
                                                              END;

                                                   CaptionML=[ENU=Amount Installments;
                                                              NOR=Belõp, avdrag;
                                                              SVE=Betalplaner, belopp];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 230 ;   ;Amount Repaid Prognosis;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Loan Line".Amount WHERE (Loan Header Code=FIELD(Code),
                                                                                             Type=FILTER(Installment|Last Installment),
                                                                                             Date=FIELD(Date Filter)));
                                                   CaptionML=ENU=Amount Repaid (Prognosis);
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 240 ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              NOR=Datofilter;
                                                              SVE=Datumfilter] }
    { 250 ;   ;Type of Loan        ;Option        ;CaptionML=ENU=Type of Loan;
                                                   OptionCaptionML=ENU=Subordinated,Private,Financial Lease,Sundry;
                                                   OptionString=Subordinated,Private,Financial Lease,Sundry }
    { 260 ;   ;Project No.         ;Code20        ;OnValidate=VAR
                                                                Job@1100528700 : Record 11072003;
                                                                Company@1100528701 : Record 2000000006;
                                                              BEGIN
                                                                IF ("Relation Type" = "Relation Type"::Intern) AND ("Project No." <> '') THEN BEGIN
                                                                  TESTFIELD(Relation);
                                                                  Company.GET(Relation);
                                                                  Job.CHANGECOMPANY(Relation);
                                                                  Job.GET("Project No.");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              Job@1100528701 : Record 11072003;
                                                              Company@1100528700 : Record 2000000006;
                                                            BEGIN
                                                              IF "Relation Type" = "Relation Type"::Intern THEN BEGIN
                                                                TESTFIELD(Relation);
                                                                Company.GET(Relation);
                                                                Job.CHANGECOMPANY(Relation);
                                                                IF "Project No." <> '' THEN
                                                                  IF Job.GET("Project No.") THEN;
                                                                IF PAGE.RUNMODAL(0, Job) = ACTION::LookupOK THEN
                                                                  VALIDATE("Project No.", Job."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 270 ;   ;Loan Agreement Present;Boolean     ;CaptionML=ENU=Loan Agreement Present }
    { 280 ;   ;Short Term          ;Boolean       ;CaptionML=ENU=Short Term }
    { 290 ;   ;Interest Type       ;Option        ;OnValidate=BEGIN
                                                                IF "Interest Type" = "Interest Type"::Fixed THEN
                                                                  "Basic Interest" := '';
                                                              END;

                                                   CaptionML=ENU=Interest Type;
                                                   OptionCaptionML=ENU=Fixed,Variable;
                                                   OptionString=Fixed,Variable }
    { 300 ;   ;Basic Interest      ;Code10        ;TableRelation="Basic Interest".Code;
                                                   OnValidate=BEGIN
                                                                IF "Interest Type" = "Interest Type"::Fixed THEN
                                                                  "Basic Interest" := '';
                                                              END;

                                                   CaptionML=ENU=Basic Interest }
    { 310 ;   ;Loan Posting Group  ;Code10        ;TableRelation="Loan Posting Group";
                                                   CaptionML=ENU=Loan Posting Group }
    { 320 ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              SVE=Department];
                                                   CaptionClass='1,1,1' }
    { 330 ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              SVE=Global dimension 2 kod];
                                                   CaptionClass='1,1,2' }
    { 340 ;   ;Principal Amount    ;Decimal       ;CaptionML=ENU=Principal Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=Process has been interrupted to respect the warning.;NOR=Prosessen ble avbrutt av hensyn til advarselen.;SVE=Processen har avbrutits med hÑnsyn till varningen.';
      Text001@1100525001 : TextConst 'ENU=Ending date after today. Do you want to delete the loan including all lines?;NOR=Sluttdato etter idag. Vil du fjerne lÜnet inkludert alle rader?;SVE=Slutdatum efter idag. Vill du ta bort lÜnet inklusive alla rader?';
      Text002@1100525002 : TextConst 'ENU=Already loan lines present. Do you want to delete this and create lines again?;NOR=Det finnes allerede lÜnerader. Vil du fjerne disse og opprette rader igen?;SVE=Det finns redan lÜnerader. Vill du ta bort detta och skapa rader igen?';
      LoanLines@1100525003 : Record 11020588;
      LineNo@1100525004 : Integer;
      Text003@1100525005 : TextConst 'ENU=Already loan lines present. If you modify this field, perhaps de loan lines must be modified or created again. Continue?;NOR=Det finnes allerede lÜnerader. Om du endrer feltet kanskje lÜneradene mÜ endres eller opprettes igen. Fortsette?;SVE=Det finns redan lÜnerader. Om du Ñndrar fÑltet kanske lÜneraderna mÜste Ñndras eller skapas igen. FortsÑtta?';
      Text004@1100525006 : TextConst 'ENU=%1 must be empty.;NOR=%1 mÜ vëre tomt.;SVE=%1 mÜste vara tomt.';
      Text005@1100525007 : TextConst 'ENU=Total amount (%1) may not be less than repayment amount (2).;NOR=Totalt belõp (%1) kan ikke vëre mindre enn tilbakebetalingsbelõpet (2).;SVE=Totalt belopp (%1) fÜr inte vara mindre Ñn Üterbetalningsbeloppet (2).';
      Text006@1100525008 : TextConst 'ENU=All not realized lines will be deleted. New lines will be created regarding the total repayment amount until now. Continue?;NOR=Alle rader som ikke er realisert vil fjernes. Nye rader vil opprettes angÜende det totale tilbakebetalingsbelõpet frem til nÜ. Fortsette?;SVE=Alla rader som inte har realiserats kommer att tas bort. Nya rader kommer att skapas angÜende det totala Üterbetalningsbeloppet tills nu. FortsÑtta?';
      AfterIncInstallment@1100525009 : Boolean;
      Text007@1100525010 : TextConst 'ENU=Only 2 repayments allowed between 2 interest payments (between %1 ans %2).;NOR=Kun 2 tilbakebetalinger er tillatt mellom 2 rentebetalinger (mellom %1 og %2).;SVE=Endast 2 Üterbetalningar Ñr tillÜtna mellan 2 rÑntebetalningar (mellan %1 Ür %2).';
      Text008@1100525011 : TextConst 'ENU="Repayment must be on a date where also is a interest payment. Adjust interestterm, installment term ans/or date firstinstallment. ";NOR=Tilbakebetaling mÜ skje pÜ en dato hvor det ogsÜ finnes en rentebetaling. Juster rentetid, avdragstid og/eller dato for fõrste avdrag.;SVE="èterbetalning mÜste intrÑffa pÜ ett datum dÜ det Ñven finns en rÑntebetalning. Justera rÑntetid, avbetalningstid och/eller datum fîr fîrsta avbetalning. "';
      Text009@1100525016 : TextConst 'ENU=Realised Line may not be changed.;NOR=Realisert rad kan ikke endres.;SVE=Realiserad rad kan inte Ñndras.';
      DimMgt@1100528500 : Codeunit 408;
      Text010@1100525019 : TextConst 'ENU=There are Repayments in the General Ledger present, initial generate is not possible.;NOR=Det finnes tilbakebetalinger i hovedbok, initier generering er ikke mulig.';
      Text011@1100525020 : TextConst 'ENU=Total amount from General Ledger (%1) may not differ from repayment amount total (2).;NOR=Totalt belõp fra finans (%1) kan ikke vëre ulikt totalt tilbakebetalingsbelõp (%2)';
      Text012@1100527450 : TextConst 'ENU=You cannot delete the Loan because there are one or more Loan Ledger Entries in place.';

    PROCEDURE SetInterestGroup@1100525000(InterestPerc@1100525000 : Decimal);
    VAR
      InterestRoundedDown@1100525001 : Integer;
      InterestRoundedUp@1100525002 : Integer;
    BEGIN
      InterestRoundedDown := ROUND(InterestPerc, 1, '<');
      InterestRoundedUp := ROUND(InterestPerc, 1, '>');
      IF InterestRoundedDown = InterestRoundedUp THEN
        InterestRoundedUp := InterestRoundedDown + 1;
      VALIDATE("Interest % Group", STRSUBSTNO('%1%2%3%4', InterestRoundedDown,
                                                          '-',
                                                          InterestRoundedUp,
                                                          '%'));
    END;

    PROCEDURE GenerateLines@1100525001();
    VAR
      InterestAmount@1100525002 : Decimal;
      CurrentDayDate@1100525005 : Date;
      NextInstallmentDate@1100525004 : Date;
      NextInterestPaymentDate@1100525007 : Date;
      PrevInterestPaymentDate@1100525008 : Date;
      Days@1100525010 : Integer;
      AmountForIntPay@1100525011 : Decimal;
      InstallmentAmount@1100525001 : Decimal;
    BEGIN
      LoanAmountLoanEntries(Amount);

      TESTFIELD(Amount);
      TESTFIELD("Start Date");
      TESTFIELD(Term);
      TESTFIELD("Date First Installment");
      TESTFIELD("Interest Term");
      TESTFIELD("Installment Amount");
      MODIFY;
      IF AfterIncInstallment THEN
        CheckPresentLinesGenAfterInst
      ELSE
        CheckPresentLinesGenerate;

      InitLoanLine;

      SetStartingValues(CurrentDayDate,
                        NextInstallmentDate,
                        InstallmentAmount,
                        NextInterestPaymentDate,
                        PrevInterestPaymentDate,
                        AmountForIntPay);

      REPEAT
        IF CurrentDayDate = NextInterestPaymentDate THEN BEGIN
          CalcInterestAmount(CurrentDayDate,
                             PrevInterestPaymentDate,
                             InterestAmount,
                             AmountForIntPay,
                             NextInterestPaymentDate);
        END;
        IF (CurrentDayDate = NextInstallmentDate) AND (AmountForIntPay > 0) THEN BEGIN
          IF AmountForIntPay < InstallmentAmount THEN
            InstallmentAmount := AmountForIntPay;
          InsertLoanLine(InstallmentAmount, CurrentDayDate, 0, 0, 0);
          NextInstallmentDate := CALCDATE("Installment Term", CurrentDayDate);
          AmountForIntPay := AmountForIntPay - InstallmentAmount;
        END;
        CurrentDayDate := CALCDATE('<1D>', CurrentDayDate);  //db, 16-03-11
      // UNTIL CurrentDayDate = "End Date";  M23718 o
      UNTIL (CurrentDayDate = "End Date") OR (AmountForIntPay <= 0);

      IF AmountForIntPay > 0 THEN BEGIN
        Days := CurrentDayDate - PrevInterestPaymentDate;
        InterestAmount := AmountForIntPay * "Interest %" / 100 * CalcFactorInterestPayment(Days, CurrentDayDate);
        InsertLoanLine(InterestAmount, CurrentDayDate, AmountForIntPay, 1, 0);
        InsertLoanLine(AmountForIntPay, CurrentDayDate, 0, 2, 0);
      END;
    END;

    PROCEDURE CheckPresentLinesGenerate@1100525005();
    BEGIN
      LoanLines.RESET;
      LoanLines.SETRANGE("Loan Header Code", Code);
      LoanLines.SETRANGE(Realised, TRUE);
      IF NOT LoanLines.ISEMPTY THEN
        ERROR(Text009);

      IF LoanRepaymentLoanEntries <> 0 THEN
        ERROR(Text010);

      LoanLines.RESET;
      LoanLines.SETRANGE("Loan Header Code", Code);
      IF NOT LoanLines.ISEMPTY THEN BEGIN
        IF NOT CONFIRM(Text002) THEN
          ERROR(Text000);
        LoanLines.DELETEALL(TRUE);
      END;
      CLEAR(LoanLines);
    END;

    PROCEDURE CheckPresentLinesGenAfterInst@1100525015();
    VAR
      TotalAmount@1100525000 : Decimal;
    BEGIN
      TotalAmount := 0;
      LoanLines.RESET;
      LoanLines.SETCURRENTKEY("Loan Header Code", Type, Realised);
      LoanLines.SETRANGE("Loan Header Code", Code);
      LoanLines.SETRANGE(Realised, TRUE);
      LoanLines.SETFILTER(Type, '<>%1', LoanLines.Type::Interest);
      IF NOT LoanLines.ISEMPTY THEN BEGIN
        LoanLines.CALCSUMS(Amount);
        TotalAmount := LoanLines.Amount;
      END;

      IF LoanRepaymentLoanEntries <> TotalAmount THEN
        ERROR(Text011, LoanRepaymentLoanEntries, TotalAmount);

      IF NOT CONFIRM(Text006) THEN
        ERROR(Text000);
      LoanLines.RESET;
      LoanLines.SETRANGE("Loan Header Code", Code);
      LoanLines.SETRANGE(Realised, FALSE);
      IF NOT LoanLines.ISEMPTY THEN
        LoanLines.DELETEALL(TRUE);

      CLEAR(LoanLines);
    END;

    PROCEDURE CalcFactorInterestPayment@1100525009(Days@1100525000 : Integer;CurrentDate@1100525001 : Date) : Decimal;
    VAR
      Factor@1100525002 : Decimal;
    BEGIN
      CASE "Interest Calculation" OF
        "Interest Calculation"::"Act/365":
          BEGIN
            IF IsLeapYear(CurrentDate) THEN
              Factor := Days / 365
            ELSE
              Factor := Days / 365;
          END;
        "Interest Calculation"::"Act/360":
          BEGIN
            IF IsLeapYear(CurrentDate) THEN
              Factor := Days / 360
            ELSE
              Factor := Days / 360;
          END;
        "Interest Calculation"::"365/365":
          BEGIN
            IF Days = 366 THEN Days := 365;
            Factor := Days / 365;
          END;
      END;
      EXIT(Factor);
    END;

    PROCEDURE IsLeapYear@1100525002(Date@1100525000 : Date) : Boolean;
    VAR
      DatePrevYear@1100525001 : Date;
    BEGIN
      DatePrevYear := CALCDATE('<-1Y>', Date);  //db, 16-03-11 //call 30615
      EXIT(Date - DatePrevYear = 366);
    END;

    PROCEDURE InitLoanLine@1100525006();
    VAR
      CurLoanLine@1100525000 : Record 11020588;
    BEGIN
      LoanLines.INIT;
      LoanLines.VALIDATE("Loan Header Code", Code);
      LineNo := 10000;

      IF AfterIncInstallment THEN BEGIN
        CurLoanLine.RESET;
        CurLoanLine.SETRANGE("Loan Header Code", Code);
        IF CurLoanLine.FINDLAST THEN
          LineNo := CurLoanLine."Line No." + 10000;
      END;
    END;

    PROCEDURE InsertLoanLine@1100525004(Amount@1100525000 : Decimal;CurrentDayDate@1100525001 : Date;AmountForIntPay@1100525002 : Decimal;Type@1100525004 : 'Installment,Interest,Last Installment';AmountForIntPayII@1100525005 : Decimal);
    VAR
      LoanLineInterest@1100525003 : Record 11020588;
    BEGIN
      LoanLines.VALIDATE("Line No.", LineNo);
      LineNo += 10000;
      LoanLines.VALIDATE(Type, Type);
      LoanLines.VALIDATE(Amount, Amount);
      LoanLines.VALIDATE(Date, CurrentDayDate);
      IF Type <> Type::Interest THEN BEGIN
        LoanLineInterest.SETCURRENTKEY("Loan Header Code", Type);
        LoanLineInterest.SETRANGE("Loan Header Code", LoanLines."Loan Header Code");
        LoanLineInterest.SETRANGE(Type, LoanLineInterest.Type::Interest);
        LoanLineInterest.SETRANGE(Date, LoanLines.Date);
        IF LoanLineInterest.ISEMPTY THEN
          ERROR(Text008);
      END;
      LoanLines.VALIDATE(Type, Type);
      LoanLines.VALIDATE("Used Amount For Calculation", AmountForIntPay);
      LoanLines.VALIDATE("Used Amount For Calculation II", AmountForIntPayII);
      LoanLines.INSERT(TRUE);
    END;

    PROCEDURE CalcInstallmentAmount@1100525007();
    BEGIN
      CheckPresentLines();
      IF "No of Installments Planned" = 0 THEN
        VALIDATE("Installment Amount", Amount)
      ELSE
        VALIDATE("Installment Amount", Amount / "No of Installments Planned");
    END;

    PROCEDURE CalcEndDate@1100525008();
    BEGIN
      CheckPresentLines();
      IF (FORMAT(Term) <> '') AND ("Start Date" <> 0D) THEN
        VALIDATE("End Date", CALCDATE(Term, "Start Date"))
      ELSE
        VALIDATE("End Date", 0D);
    END;

    PROCEDURE CheckPresentLines@1100525003();
    BEGIN
      LoanLines.RESET;
      LoanLines.SETRANGE("Loan Header Code", Code);
      IF NOT LoanLines.ISEMPTY THEN
        IF NOT CONFIRM(Text003) THEN
          ERROR(Text000);

      CLEAR(LoanLines);
    END;

    PROCEDURE FillEndTermFields@1100525010();
    BEGIN
      VALIDATE("Date First Installment", "End Date");
      VALIDATE("No of Installments Planned", 1);
      VALIDATE("Installment Amount", Amount);
    END;

    PROCEDURE CheckAmount@1100525011();
    BEGIN
      CALCFIELDS("Amount Installments");
      IF Amount < "Amount Installments" THEN
        ERROR(Text005, Amount, "Amount Installments");
    END;

    PROCEDURE GenerateLinesAfterInstallment@1100525012();
    BEGIN
      TESTFIELD("Interim Payments Possible");

      AfterIncInstallment := TRUE;

      GenerateLines;

      AfterIncInstallment := FALSE;
    END;

    PROCEDURE SetStartingValues@1100525048(VAR CurrentDayDate@1100525000 : Date;VAR NextInstallmentDate@1100525001 : Date;VAR InstallmentAmount@1100525002 : Decimal;VAR NextInterestPaymentDate@1100525003 : Date;VAR PrevInterestPaymentDate@1100525004 : Date;VAR AmountForIntPay@1100525005 : Decimal);
    BEGIN
      CurrentDayDate := "Start Date";

      IF AfterIncInstallment THEN CurrentDayDate := GetNewStartDateCalc;

      IF FORMAT("Installment Term") = '' THEN BEGIN
        NextInstallmentDate := "End Date";
      END ELSE BEGIN
        NextInstallmentDate := CALCDATE("Installment Term", CurrentDayDate);
        IF NextInstallmentDate < "Date First Installment" THEN
          //NextInstallmentDate := CALCDATE("Installment Term", "Date First Installment"); //**4PS.o
          NextInstallmentDate := "Date First Installment";                                 //**4PS.n
      END;

      IF AfterIncInstallment THEN
        CalcRemainingInstallAmounts(InstallmentAmount, AmountForIntPay)
      ELSE BEGIN
        InstallmentAmount := "Installment Amount";
        AmountForIntPay := Amount;
      END;

      NextInterestPaymentDate := CALCDATE("Interest Term", CurrentDayDate);
      PrevInterestPaymentDate := CurrentDayDate;
    END;

    PROCEDURE GetNewStartDateCalc@1100525014() : Date;
    VAR
      CurLoanLine@1100525000 : Record 11020588;
    BEGIN
      CurLoanLine.RESET;
      CurLoanLine.SETCURRENTKEY("Loan Header Code", Type);
      CurLoanLine.SETRANGE("Loan Header Code", Code);
      CurLoanLine.SETRANGE(Type, CurLoanLine.Type::Interest);
      IF CurLoanLine.FINDLAST THEN
        EXIT(CurLoanLine.Date)
      ELSE
        EXIT("Start Date");
    END;

    PROCEDURE CalcRemainingInstallAmounts@1100525029(VAR InstallmentAmount@1100525002 : Decimal;VAR AmountForIntPay@1100525003 : Decimal);
    VAR
      RemainingNoOfInstallments@1100525000 : Integer;
      RemainingAmountInstallments@1100525001 : Decimal;
    BEGIN
      CALCFIELDS("Amount Installments", "No of Installments");
      RemainingNoOfInstallments := "No of Installments Planned" - "No of Installments";
      RemainingAmountInstallments := Amount - "Amount Installments";
      AmountForIntPay := RemainingAmountInstallments;
      InstallmentAmount := "Installment Amount"; //M23718  n
      //M23718  so
      //Because field "No Of Installments Planned" apparently not be filled.
      //IF RemainingNoOfInstallments > 1 THEN BEGIN;
      //  InstallmentAmount := RemainingAmountInstallments / RemainingNoOfInstallments;
      //END ELSE BEGIN;
      //  InstallmentAmount := RemainingAmountInstallments;
      //END;
      //M23718  so
    END;

    PROCEDURE CalcInterestAmount@1100525013(VAR CurrentDayDate@1100525001 : Date;VAR PrevInterestPaymentDate@1100525002 : Date;VAR InterestAmount@1100525003 : Decimal;VAR AmountForIntPay@1100525004 : Decimal;VAR NextInterestPaymentDate@1100525005 : Date);
    VAR
      Days@1100525000 : Integer;
      LoanLineInstallment@1100525006 : Record 11020588;
      AmountForIntPayI@1100525007 : Decimal;
      AmountForIntPayII@1100525008 : Decimal;
    BEGIN
      LoanLineInstallment.RESET;
      LoanLineInstallment.SETCURRENTKEY("Loan Header Code", Type);
      LoanLineInstallment.SETRANGE("Loan Header Code", Code);
      LoanLineInstallment.SETRANGE(Type, LoanLineInstallment.Type::Installment);
      LoanLineInstallment.SETRANGE(Date, PrevInterestPaymentDate, CurrentDayDate);
      CASE LoanLineInstallment.COUNT OF
        0: BEGIN
             Days := CurrentDayDate - PrevInterestPaymentDate;
             InterestAmount := AmountForIntPay * "Interest %" / 100 * CalcFactorInterestPayment(Days, CurrentDayDate);
             InsertLoanLine(InterestAmount, CurrentDayDate, AmountForIntPay, 1, 0);
             NextInterestPaymentDate := CALCDATE("Interest Term", CurrentDayDate);
             PrevInterestPaymentDate := CurrentDayDate;
           END;
        1: BEGIN
             Days := CurrentDayDate - PrevInterestPaymentDate;
             InterestAmount := AmountForIntPay * "Interest %" / 100 * CalcFactorInterestPayment(Days, CurrentDayDate);
             InsertLoanLine(InterestAmount, CurrentDayDate, AmountForIntPay, 1, 0);
             NextInterestPaymentDate := CALCDATE("Interest Term", CurrentDayDate);
             PrevInterestPaymentDate := CurrentDayDate;
           END;
        2: BEGIN
             LoanLineInstallment.FINDLAST;
             Days := LoanLineInstallment.Date - PrevInterestPaymentDate;
             AmountForIntPayI := AmountForIntPay + LoanLineInstallment.Amount;
             InterestAmount := (AmountForIntPayI) *
                               "Interest %" / 100 *
                               CalcFactorInterestPayment(Days, LoanLineInstallment.Date);
             Days := CurrentDayDate - LoanLineInstallment.Date;
             InterestAmount += AmountForIntPay * "Interest %" / 100 * CalcFactorInterestPayment(Days, CurrentDayDate);
             AmountForIntPayII := AmountForIntPay;
             InsertLoanLine(InterestAmount, CurrentDayDate, AmountForIntPayI, 1, AmountForIntPayII);
             NextInterestPaymentDate := CALCDATE("Interest Term", CurrentDayDate);
             PrevInterestPaymentDate := CurrentDayDate;
           END;
        ELSE
          ERROR(Text007, PrevInterestPaymentDate, CurrentDayDate);
      END;
    END;

    PROCEDURE LoanAmountLoanEntries@1100525016(VAR LoanAmount@1100525001 : Decimal) : Boolean;
    VAR
      LoanLedgerEntry@1100528500 : Record 11229444;
    BEGIN
      LoanLedgerEntry.SETRANGE("Loan Code",Code);
      LoanLedgerEntry.SETFILTER("Loan Type",'%1|%2',LoanLedgerEntry."Loan Type"::Acquisition,LoanLedgerEntry."Loan Type"::Appreciation);
      LoanLedgerEntry.CALCSUMS(Amount);
      LoanAmount := LoanLedgerEntry.Amount;
      EXIT(LoanAmount <> 0);
    END;

    PROCEDURE LoanRepaymentLoanEntries@1100525019() : Decimal;
    VAR
      LoanLedgerEntry@1100528500 : Record 11229444;
    BEGIN
      LoanLedgerEntry.SETRANGE("Loan Code",Code);
      LoanLedgerEntry.SETFILTER("Loan Type",'%1|%2',LoanLedgerEntry."Loan Type"::Redemption,LoanLedgerEntry."Loan Type"::"Write-Down");
      LoanLedgerEntry.CALCSUMS(Amount);
      EXIT(LoanLedgerEntry.Amount);
    END;

    PROCEDURE InterestAmountLoanEntries@1100528503() : Decimal;
    VAR
      LoanLedgerEntry@1100528500 : Record 11229444;
    BEGIN
      LoanLedgerEntry.SETRANGE("Loan Code",Code);
      LoanLedgerEntry.SETRANGE("Loan Type",LoanLedgerEntry."Loan Type"::Interest);
      LoanLedgerEntry.CALCSUMS(Amount);
      EXIT(LoanLedgerEntry.Amount);
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::"Loan Header",Code,FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE GetLoanAccountCode@1100528500(LoanCode@1100528500 : Code[10];LoanType@1100528502 : Integer) LoanAccountCode : Code[10];
    VAR
      LoanPostingGroup@1100528501 : Record 11229443;
      GenJnlLine@1100528503 : Record 81;
    BEGIN
      GET(LoanCode);
      TESTFIELD("Loan Posting Group");
      LoanPostingGroup.GET("Loan Posting Group");

      CASE LoanType OF
        GenJnlLine."Loan Type"::"Granted/Received":
          BEGIN
            LoanPostingGroup.TESTFIELD("Granted/Received Account");
            LoanAccountCode := LoanPostingGroup."Granted/Received Account";
          END;
        GenJnlLine."Loan Type"::"Depreciation/Capital Appreciation":
          BEGIN
            LoanPostingGroup.TESTFIELD("Depreciation/Appreciation Acc.");
            LoanAccountCode := LoanPostingGroup."Depreciation/Appreciation Acc.";
          END;
        GenJnlLine."Loan Type"::Repayment:
          BEGIN
            LoanPostingGroup.TESTFIELD("Repayment Account");
            LoanAccountCode := LoanPostingGroup."Repayment Account";
          END;
        GenJnlLine."Loan Type"::"Loan Provision":
          BEGIN
            LoanPostingGroup.TESTFIELD("Loan Provision Account");
            LoanAccountCode := LoanPostingGroup."Loan Provision Account";
          END;
        GenJnlLine."Loan Type"::"Interest Payment":
          BEGIN
            LoanPostingGroup.TESTFIELD("Interest Payment Account");
            LoanAccountCode := LoanPostingGroup."Interest Payment Account";
          END;
        GenJnlLine."Loan Type"::"Interest Payment Provision":
          BEGIN
            LoanPostingGroup.TESTFIELD("Interest Payment Provision Ac.");
            LoanAccountCode := LoanPostingGroup."Interest Payment Provision Ac.";
          END;
        GenJnlLine."Loan Type"::Interest:
          BEGIN
            LoanPostingGroup.TESTFIELD("Interest Account");
            LoanAccountCode := LoanPostingGroup."Interest Account";
          END;
      END;
    END;

    PROCEDURE ShowInterestLoanEntries@1100528501();
    VAR
      LoanLedgerEntry@1100528500 : Record 11229444;
    BEGIN
      LoanLedgerEntry.SETRANGE("Loan Code",Code);
      LoanLedgerEntry.SETRANGE("Loan Type",LoanLedgerEntry."Loan Type"::Interest);
      PAGE.RUNMODAL(0,LoanLedgerEntry);
    END;

    PROCEDURE ShowRepaymentLoanEntries@1100528502();
    VAR
      LoanLedgerEntry@1100528500 : Record 11229444;
    BEGIN
      LoanLedgerEntry.SETRANGE("Loan Code",Code);
      LoanLedgerEntry.SETFILTER("Loan Type",'%1|%2',LoanLedgerEntry."Loan Type"::Redemption,LoanLedgerEntry."Loan Type"::"Write-Down");
      PAGE.RUNMODAL(0,LoanLedgerEntry);
    END;

    BEGIN
    END.
  }
}

