OBJECT Page 11012454 Complaint Commisions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=BeschwerdeauftrÑge;
               ENU=Complaint Commisions;
               NLD=Klachtopdrachten;
               NOR=Klagekommisjoner;
               SVE=KlagomÜlsprovisioner];
    SourceTable=Table11012456;
    DelayedInsert=Yes;
    PageType=List;
    RefreshOnActivate=Yes;
    OnAfterGetRecord=BEGIN
                       ActionByAuthorityContactNo := GetActionByAuthContactNo;
                     END;

    OnNewRecord=VAR
                  lComplaintRec@1100485001 : Record 11012450;
                BEGIN
                  "Commision No." := Rec.GetFreeCommissionNo("Complaint No.");

                  IF ("Complaint No." <> '') THEN BEGIN
                    //* Het vullen van deze velden gebeurde eerst automatisch via SubPageLink in het kopscherm, maar de velden
                    //* bleven soms leeg (terwijl ActivateOnUpdate aan staat). De regels waren dan niet meer zichtbaar.
                    //* Bij SubPageLink staat nu alleen nog het klachtnummer, hier worden de overige key velden nu gevuld.
                    //* Eigenlijk zouden deze velde helemaal niet in key-1 moeten zitten.
                    lComplaintRec.GET("Complaint No.");
                    "Project No." := lComplaintRec."Project No.";
                    "Plot No." := lComplaintRec."Plot No.";
                    Delivery := lComplaintRec.Delivery;
                    "Delivered Object" := lComplaintRec."Delivered Object";
                    "Expected Solving Date" := lComplaintRec."Expected Solving Date";
                  END;
                END;

    OnDeleteRecord=VAR
                     lvComplaintRec@1100525000 : Record 11012450;
                   BEGIN
                     IF lvComplaintRec.GET("Complaint No.") THEN BEGIN
                       IF lvComplaintRec.Status = lvComplaintRec.Status::Finished THEN
                         lvComplaintRec.FIELDERROR(Status);
                     END;
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1907515804;1 ;ActionGroup;
                      CaptionML=[DEU=&Beschwerdenregistrierung;
                                 ENU=&Complaints;
                                 NLD=&Klachtregistratie;
                                 NOR=&Klager;
                                 SVE=&KlagomÜl] }
      { 1907946504;2 ;Action    ;
                      CaptionML=[DEU=Text&block;
                                 ENU=Text&field;
                                 NLD=Tekst&blok;
                                 NOR=Tekst&felt;
                                 SVE=Text&fÑlt];
                      Image=Text;
                      OnAction=BEGIN
                                 EditText;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1906587504;1 ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions;
                                 NLD=F&uncties;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 1903473204;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Debitorenauftrag versenden;
                                 ENU=Forward Complaint Commission;
                                 NLD=Klachtopdracht &doorsturen;
                                 NOR=Videresend Klagekommisjon;
                                 SVE=Vidarebefordra klagomÜlsprovision];
                      Image=EditReminder;
                      OnAction=BEGIN
                                 ForwardCommission();
                               END;
                                }
      { 1210190001;2 ;Action    ;
                      CaptionML=[DEU=Bearbeiten Word-Dokumente;
                                 ENU=Edit Word Documents;
                                 NLD=Bewerk worddocumenten;
                                 NOR=Rediger Word-dokument;
                                 SVE=Redigera Word-dokument];
                      Image=Word;
                      OnAction=VAR
                                 ProjectAssignment@1100525001 : Record 11012456;
                                 Contact@1100525002 : Record 5050;
                                 Contact2@1100525003 : Record 5050;
                                 Employee@1100409002 : Record 5200;
                                 Vendor@1100409001 : Record 23;
                                 Complaint@1100409003 : Record 11012450;
                                 ComplaintsSetup@1100529000 : Record 11012108;
                               BEGIN
                                 TESTFIELD("Complaint No.");
                                 ProjectAssignment := Rec;
                                 CASE ProjectAssignment.Assignment OF
                                   ProjectAssignment.Assignment::Employee :
                                     BEGIN
                                       ProjectAssignment.TESTFIELD("Action by");
                                       //IF Contact.GetContactByEmployee(ProjectAssignment."Action by", Contact, TRUE) THEN BEGIN  //DP00846.o
                                                                                              //Not used, so do not determine/check exist
                                         Employee.GET(ProjectAssignment."Action by");
                                         //Contact2.GET("Contact No.");  //T001274.o
                                         Complaint.GET(ProjectAssignment."Complaint No.");  //T001274.n
                                         IF Complaint."Contact No." <> ''THEN  //DP00846.n
                                           Contact2.GET(Complaint."Contact No.")            //T001274.n
                                         ELSE BEGIN                                         //DP00846.sn
                                           ComplaintsSetup.GET;
                                           ComplaintsSetup.TESTFIELD("Contact No.");
                                           Contact2.GET(ComplaintsSetup."Contact No.");
                                         END;                                               //DP00846.en
                                         IF Contact2.IsContactPerson THEN
                                           ProjectAssignment.EditDocuments(Contact2."Company No.", Contact2."No.", TRUE, Employee."E-Mail")
                                         ELSE
                                           ProjectAssignment.EditDocuments(Contact2."No.", '', TRUE, Employee."E-Mail");
                                       //END;
                                     END;
                                   ProjectAssignment.Assignment::External :
                                     BEGIN
                                       ProjectAssignment.TESTFIELD("Action by");
                                       IF Contact.GetContactByVendor(ProjectAssignment."Action by", Contact, TRUE) THEN BEGIN
                                         Vendor.GET(ProjectAssignment."Action by");
                                         IF ProjectAssignment."Contact No." <> '' THEN BEGIN
                                           Contact2.GET(ProjectAssignment."Contact No.");
                                           IF Contact2.IsContactPerson THEN
                                             ProjectAssignment.EditDocuments(Contact."No.", Contact2."No.", TRUE, Contact."E-Mail")
                                           ELSE
                                             ProjectAssignment.EditDocuments(Contact."No.", '', TRUE, Vendor."E-Mail");
                                         END ELSE
                                           ProjectAssignment.EditDocuments(Contact."No.", '', TRUE, Vendor."E-Mail");
                                       END;
                                     END;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Commision No." }

    { 8   ;2   ;Field     ;
                SourceExpr="Commission Description" }

    { 1100485018;2;Field  ;
                SourceExpr="Text Block" }

    { 1100485020;2;Field  ;
                SourceExpr=Text;
                Visible=FALSE }

    { 1100485000;2;Field  ;
                SourceExpr=Assignment;
                OnValidate=BEGIN
                             ActionByAuthorityContactNo := GetActionByAuthContactNo;
                           END;
                            }

    { 1100485002;2;Field  ;
                SourceExpr="Action by";
                OnValidate=BEGIN
                             ActionByAuthorityContactNo := GetActionByAuthContactNo;
                           END;
                            }

    { 1100485004;2;Field  ;
                SourceExpr="Action by Name" }

    { 1100528400;2;Field  ;
                Name=ActionByAuthorityContactNo;
                CaptionML=[DEU=Aktion nach Projektinstanz Kontaktnr.;
                           ENU=Action by Authority Contact No.;
                           NLD=Actie door instantierelatienr.];
                SourceExpr=ActionByAuthorityContactNo;
                OnValidate=BEGIN
                             ValidateActionByAuthContactNo(ActionByAuthorityContactNo);
                           END;

                OnLookup=BEGIN
                           IF LookupActionByAuthContactNo(ActionByAuthorityContactNo) THEN
                             ValidateActionByAuthContactNo(ActionByAuthorityContactNo);
                         END;
                          }

    { 1100485016;2;Field  ;
                SourceExpr="Contact No." }

    { 1100485008;2;Field  ;
                SourceExpr="Contact Person Name";
                Visible=FALSE }

    { 1100485014;2;Field  ;
                SourceExpr="Extension Contact Person";
                Visible=FALSE }

    { 1100485012;2;Field  ;
                SourceExpr="Phone Contact Person";
                Visible=FALSE }

    { 1100485010;2;Field  ;
                SourceExpr="Fax Contact Person";
                Visible=FALSE }

    { 1210190000;2;Field  ;
                SourceExpr="Seasonal Activity";
                Visible=FALSE }

    { 1100485006;2;Field  ;
                SourceExpr="Responsible Person" }

    { 10  ;2   ;Field     ;
                SourceExpr=Date }

    { 1100528402;2;Field  ;
                SourceExpr="Expected Solving Date" }

    { 1100525000;2;Field  ;
                SourceExpr="Solving Date" }

    { 12  ;2   ;Field     ;
                SourceExpr="Commision Status" }

  }
  CODE
  {
    VAR
      Text001@1100485001 : TextConst 'DEU=Status der Beschwerde ''%1'' ist ''%2''. HinzufÅgen Textblock nicht gestattet.;ENU=Status of Complaint ''%1'' is ''%2'', insert Text Block not allowed.;NLD=Status van klacht ''%1'' is ''%2'', toevoegen tekstblok niet toegestaan.;NOR=Status for klage ''%1'' er ''%2''. Innsetting av tekstblock er ikke tillatt.;SVE=Status fîr klagomÜl ''%1'' Ñr ''%2''. Infogning av textblock Ñr inte tillÜtet.';
      Text009@1100485000 : TextConst 'DEU=Geben Sie zuerst eine Beschwerdenr. ein und danach den Textblock.;ENU=Please fill Complaintno. before textfield.;NLD=Voer eerst een klachtnr. in en daarna het tekstblok.;NOR=Fyll ut klagenummeret fõr tekstfeltet.;SVE=Fyll i klagomÜlsnumret fîre textfÑltet.';
      ActionByAuthorityContactNo@1100528400 : Code[20];

    PROCEDURE EditText@1210190000();
    VAR
      lvComplRec@1100485000 : Record 11012450;
      TextFrm@11012001 : Page 11012120;
    BEGIN
      IF "Complaint No." = '' THEN
        ERROR(Text009);

      lvComplRec.GET("Complaint No.");
      IF lvComplRec.Status = lvComplRec.Status::Finished THEN BEGIN
        IF "Text Block" THEN
          TextFrm.EDITABLE(FALSE)
        ELSE
          ERROR(Text001, "Complaint No.", lvComplRec.Status);
      END;

      TextFrm.SetSelection(DATABASE::"Project Assignment","Complaint No.",
        "Commision No.",'','','','',0,0);

      TextFrm.RUNMODAL;
      CLEAR(TextFrm);
    END;

    PROCEDURE ForwardCommission@1100485000();
    VAR
      lvComplRec@1100485002 : Record 11012450;
      lvCommRec@1100485000 : Record 11012456;
      lvForwardCommFrm@1100485001 : Page 11020411;
      lvText000@1100485003 : TextConst 'DEU=%1 ist ''%2'', sind Sie sich sicher, dass Sie den Auftrag versenden mîchten?;ENU=%1 is ''%2'', are you sure you want to forward the commission?;NLD=%1 is ''%2'', weet u zeker dat u de opdracht wilt doorsturen?;NOR=%1 er ''%2''. Er du sikker pÜ Ü du vil videresende kommisjonen?;SVE=%1 Ñr ''%2''. ér du sÑker pÜ att du vill vidarebefordra provisionen?';
    BEGIN
      TESTFIELD("Complaint No.");
      lvComplRec.GET("Complaint No.");
      IF lvComplRec.Status = lvComplRec.Status::Finished THEN
        lvComplRec.FIELDERROR(Status);

      IF ("Commision No." = '') THEN BEGIN
        lvCommRec.SETRANGE("Complaint No.", "Complaint No.");
        IF lvCommRec.FINDFIRST THEN
          TESTFIELD("Commision No.");
      END ELSE BEGIN
        IF "Commision Status" = "Commision Status"::Finished THEN BEGIN
          IF NOT CONFIRM(lvText000, FALSE, FIELDCAPTION("Commision Status"), "Commision Status") THEN
            EXIT;
        END;
      END;

      lvForwardCommFrm.InitSelection(Rec);
      lvForwardCommFrm.RUNMODAL;
    END;

    BEGIN
    {
      //20060705-JE : Added fields [Contact No.], [Text Block], [Text].
      //20060705-JE : Added function [EditText]
    }
    END.
  }
}

