OBJECT Table 11072334 DPT Authority Perm Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CALCFIELDS("Subsite Structure");
               IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    OnDelete=BEGIN
               CheckRecordChangeAllowed;
             END;

    OnRename=BEGIN
               CheckRecordChangeAllowed;
             END;

    CaptionML=ENU=DPT Authority Permission (Project);
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=SVE=Typ;
                                                   OptionCaptionML=ENU=,Access;
                                                   OptionString=,Access;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 30  ;   ;Authority Type      ;Code20        ;TableRelation="Project Authority"."Authority Type" WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Authority Type;
                                                              NOR=Myndighetstype;
                                                              SVE=Beh”righetstyp];
                                                   NotBlank=Yes }
    { 31  ;   ;Authority Type Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Authority Type".Description WHERE (Code=FIELD(Authority Type)));
                                                   CaptionML=[ENU=Authority Type Description;
                                                              NOR=Beskrivelse av myndighetstype;
                                                              SVE=Beskrivning av beh”righetstyp];
                                                   Editable=No }
    { 40  ;   ;Contact No.         ;Code20        ;TableRelation="Project Authority"."Contact No." WHERE (Project No.=FIELD(Project No.),
                                                                                                          Authority Type=FIELD(Authority Type));
                                                   CaptionML=[ENU=Contact No.;
                                                              NOR=Kontaktnr;
                                                              SVE=Kontaktnr];
                                                   NotBlank=Yes }
    { 41  ;   ;Contact Name        ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              NOR=Kontaktnavn;
                                                              SVE=Kontaktnamn];
                                                   Editable=No }
    { 50  ;   ;Contact Person No.  ;Code20        ;TableRelation="Project Authority"."Contact Person No." WHERE (Project No.=FIELD(Project No.),
                                                                                                                 Authority Type=FIELD(Authority Type),
                                                                                                                 Contact No.=FIELD(Contact No.));
                                                   CaptionML=[ENU=Contact Person No.;
                                                              NOR=Kontaktperson;
                                                              SVE=Kontaktperson] }
    { 51  ;   ;Contact Person Name ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Person No.)));
                                                   CaptionML=[ENU=Contact Person Name;
                                                              NOR=Kontaktpersonens navn;
                                                              SVE=Kontaktpersonens namn];
                                                   Editable=No }
    { 60  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Subsite Project".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                   Quote Request Subsite=CONST(No));
                                                   CaptionML=ENU=Subsite Code }
    { 61  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 62  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project"."Subsite Structure" WHERE (Project No.=FIELD(Project No.),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 63  ;   ;Quote Request Subsite;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project"."Quote Request Subsite" WHERE (Project No.=FIELD(Project No.),
                                                                                                                           Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Quote Request Subsite;
                                                   Editable=No }
    { 70  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     Subsite Structure=FILTER(>OneDocLibrary)) "DPT Subsite Doc Type Project"."Document Type" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                                                     Subsite Code=FIELD(Subsite Code));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 71  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 80  ;   ;Project Element     ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Project Element;
                                                              NOR=Prosjektelement;
                                                              SVE=Projektelement] }
    { 81  ;   ;Project Element Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Project Element)));
                                                   CaptionML=ENU=Project Element Description;
                                                   Editable=No }
    { 90  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                CheckPermissions;
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member;
                                                   OptionString=None,Visitor,Member }
    { 100 ;   ;Show in View OLD    ;Boolean       ;CaptionML=;
                                                   Editable=No }
    { 110 ;   ;Subsite Synced On   ;DateTime      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project"."Synchronized On" WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Synchronized On;
                                                   Editable=No }
    { 120 ;   ;Subsite Doc Type Synced On;DateTime;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Doc Type Project"."Synchronized On" WHERE (Project No.=FIELD(Project No.),
                                                                                                                              Subsite Code=FIELD(Subsite Code),
                                                                                                                              Document Type=FIELD(Document Type)));
                                                   CaptionML=ENU=Subsite Document Type Synchronized On;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Project No.,Authority Type,Contact No.,Contact Person No.,Subsite Code,Document Type,Project Element;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AddCounter@1100528301 : Integer;
      ModifyCounter@1100528300 : Integer;
      Text000@1100528303 : TextConst 'ENU=%1 Permission Records added and %2 Permission Records modified.';

    PROCEDURE CheckPermissions@1100409001();
    BEGIN
      CheckModifyPermission;
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100528302();
    VAR
      DPTAuthorityPermProject@1100528300 : Record 11072334;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      IF (("Subsite Code" = '') AND ("Document Type" = '') AND ("Permission Level" = "Permission Level"::None)) THEN BEGIN
        DPTAuthorityPermProject.SETRANGE(Type , DPTAuthorityPermProject.Type::Access);
        DPTAuthorityPermProject.SETRANGE("Project No.", "Project No.");
        DPTAuthorityPermProject.SETRANGE("Authority Type", "Authority Type");
        DPTAuthorityPermProject.SETRANGE("Contact No.", "Contact No.");
        DPTAuthorityPermProject.SETRANGE("Contact Person No.", "Contact Person No.");
        DPTAuthorityPermProject.SETFILTER("Subsite Code", '<>%1', "Subsite Code");
        DPTAuthorityPermProject.MODIFYALL("Permission Level", "Permission Level"::None);
        EXIT;
      END;
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) THEN
        EXIT;

      IF ("Document Type" = '') THEN BEGIN
        DPTAuthorityPermProject.SETRANGE(Type, Type);
        DPTAuthorityPermProject.SETRANGE("Project No.", "Project No.");
        DPTAuthorityPermProject.SETRANGE("Authority Type", "Authority Type");
        DPTAuthorityPermProject.SETRANGE("Contact No.", "Contact No.");
        DPTAuthorityPermProject.SETRANGE("Contact Person No.", "Contact Person No.");
        DPTAuthorityPermProject.SETRANGE("Subsite Code", "Subsite Code");
        DPTAuthorityPermProject.SETFILTER("Document Type", '<>%1', '');
        DPTAuthorityPermProject.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        IF (DPTAuthorityPermProject.GET(Type, "Authority Type", "Contact No.", "Contact Person No.", "Subsite Code", '')) THEN BEGIN
          IF (DPTAuthorityPermProject."Permission Level" < "Permission Level") THEN BEGIN
            DPTAuthorityPermProject."Permission Level" := "Permission Level";
            DPTAuthorityPermProject.MODIFY;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE CheckModifyPermission@1100409000();
    VAR
      DPTRespPersPermProject@1100409000 : Record 11072332;
    BEGIN
      DPTRespPersPermProject."Project No." := "Project No.";
      DPTRespPersPermProject.CheckModifyPermission;
    END;

    PROCEDURE AddPortalPermissions@1100528301(ProjectNo@1100528300 : Code[20];AuthorityTypeFilter@1100528307 : Code[20];ContactNoFilter@1100528308 : Code[20];ContactPersonNoFilter@1100528309 : Code[20];SubsiteCodeFilter@1100528306 : Code[20];ShowMessage@1100528310 : Boolean);
    VAR
      ProjectAuthority@1100528301 : Record 11020216;
      DPTSubsiteProject@1100528303 : Record 11072330;
      DPTSubsiteDocTypeProject@1100528302 : Record 11072331;
      Job@1100528304 : Record 11072003;
    BEGIN
      AddCounter := 0;
      ModifyCounter := 0;
      Job.GET(ProjectNo);

      ProjectAuthority.SETRANGE("Project No.", ProjectNo);
      IF ((AuthorityTypeFilter <> '') OR (ContactNoFilter <> '') OR (ContactPersonNoFilter <> '')) THEN BEGIN
        ProjectAuthority.SETRANGE("Authority Type", AuthorityTypeFilter);
        ProjectAuthority.SETRANGE("Contact No.", ContactNoFilter);
        ProjectAuthority.SETRANGE("Contact Person No.", ContactPersonNoFilter);
      END;
      IF (NOT ProjectAuthority.FINDSET) THEN
        EXIT;

      REPEAT
        InsertRecord(ProjectAuthority, 1, '', '');
        DPTSubsiteProject.SETRANGE("Project No.", ProjectNo);
        IF (SubsiteCodeFilter <> '') THEN
          DPTSubsiteProject.SETRANGE(Code, SubsiteCodeFilter);
        DPTSubsiteProject.SETRANGE("Subsite Structure", DPTSubsiteProject."Subsite Structure"::PermissionPerDocType);
        IF (DPTSubsiteProject.FINDSET) THEN BEGIN
          REPEAT
            DPTSubsiteDocTypeProject.SETRANGE("Project No.", ProjectNo);
            DPTSubsiteDocTypeProject.SETRANGE("Subsite Code", DPTSubsiteProject.Code);
            IF (DPTSubsiteDocTypeProject.FINDSET) THEN BEGIN
              InsertRecord(ProjectAuthority, 1, DPTSubsiteProject.Code, ''); //1=Access
              REPEAT
                InsertRecord(ProjectAuthority, 1, DPTSubsiteProject.Code, DPTSubsiteDocTypeProject."Document Type");
              UNTIL(DPTSubsiteDocTypeProject.NEXT = 0);
            END;
          UNTIL (DPTSubsiteProject.NEXT = 0);
        END;

        DPTSubsiteProject.SETFILTER("Subsite Structure", '<%1', DPTSubsiteProject."Subsite Structure"::PermissionPerDocType);
        IF (DPTSubsiteProject.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(ProjectAuthority, 1, DPTSubsiteProject.Code, ''); //1=Access
          UNTIL (DPTSubsiteProject.NEXT = 0);
        END;
      UNTIL (ProjectAuthority.NEXT = 0);
      IF (ShowMessage) THEN
        MESSAGE(Text000, AddCounter, ModifyCounter - AddCounter);
    END;

    LOCAL PROCEDURE InsertRecord@1100528300(ProjectAuthority@1100528306 : Record 11020216;PermissionType@1100528300 : Option;SubsiteCode@1100528304 : Code[20];DocumentType@1100528305 : Code[20]);
    VAR
      DPTAuthorityPermProject@1100528301 : Record 11072334;
      DPTDefAuthorityPermProject@1100528303 : Record 11072335;
      Job@1100528302 : Record 11072003;
    BEGIN
      Job.GET(ProjectAuthority."Project No.");
      IF (NOT DPTDefAuthorityPermProject.GET(PermissionType, ProjectAuthority."Authority Type", SubsiteCode, DocumentType))
      THEN BEGIN
        DPTDefAuthorityPermProject.INIT;
      END;

      IF (NOT DPTAuthorityPermProject.GET(PermissionType, ProjectAuthority."Project No.", ProjectAuthority."Authority Type",
          ProjectAuthority."Contact No.", ProjectAuthority."Contact Person No.", SubsiteCode, DocumentType))
      THEN BEGIN
        DPTAuthorityPermProject.INIT;
        DPTAuthorityPermProject.Type := PermissionType;
        DPTAuthorityPermProject."Project No." := ProjectAuthority."Project No.";
        DPTAuthorityPermProject."Authority Type" := ProjectAuthority."Authority Type";
        DPTAuthorityPermProject."Contact No." := ProjectAuthority."Contact No.";
        DPTAuthorityPermProject."Contact Person No." := ProjectAuthority."Contact Person No.";
        DPTAuthorityPermProject."Subsite Code" := SubsiteCode;
        DPTAuthorityPermProject."Document Type" := DocumentType;
        DPTAuthorityPermProject.INSERT;
        AddCounter += 1;
      END;
      DPTAuthorityPermProject."Permission Level" := DPTDefAuthorityPermProject."Permission Level";
      DPTAuthorityPermProject.MODIFY;
      ModifyCounter += 1;
    END;

    LOCAL PROCEDURE CheckRecordChangeAllowed@1100528307();
    VAR
      Job@1100528300 : Record 11072003;
    BEGIN
      IF (Type <> Type::Access) OR ("Project Element" <> '') OR (NOT Job.GET("Project No.")) THEN
        EXIT;
    END;

    BEGIN
    END.
  }
}

