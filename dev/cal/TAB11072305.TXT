OBJECT Table 11072305 Service Process Step Check
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Service Process Step Check;
    LookupPageID=Page11126791;
    DrillDownPageID=Page11126791;
  }
  FIELDS
  {
    { 5   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::"Service Call":
                                                                    VALIDATE("Table Type", "Table Type"::"Service Call");
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=Service Order,Service Call;
                                                   OptionString=Service Order,Service Call }
    { 10  ;   ;Step Code           ;Code10        ;TableRelation="Service Process Step".Code WHERE (Type=FIELD(Type));
                                                   CaptionML=ENU=Step Code;
                                                   NotBlank=Yes }
    { 20  ;   ;Step Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Process Step".Description WHERE (Type=FIELD(Type),
                                                                                                                Code=FIELD(Step Code)));
                                                   CaptionML=ENU=Step Description;
                                                   Editable=No }
    { 30  ;   ;Check Description   ;Text50        ;CaptionML=ENU=Check Description }
    { 40  ;   ;Table Type          ;Option        ;OnValidate=BEGIN
                                                                CASE "Table Type" OF
                                                                  "Table Type"::"Service Order":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Service Order";
                                                                    END;
                                                                  "Table Type"::"Work Order":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Work Order";
                                                                    END;
                                                                  "Table Type"::"Advice Order":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Advice Order Line";
                                                                    END;
                                                                  "Table Type"::"Purchase Line":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Purchase Line";
                                                                    END;
                                                                  "Table Type"::"Cost Plus Entry":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Service Order Cost Plus Entry";
                                                                    END;
                                                                  "Table Type"::"Budget Line":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Service Order Budget Line";
                                                                    END;
                                                                  "Table Type"::"Service Order Extension":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Service Order Extension";
                                                                    END;
                                                                  "Table Type"::"Item Journal Line":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Item Journal Line";
                                                                    END;
                                                                  "Table Type"::"Service Call":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Call");
                                                                      "Table No." := DATABASE::"Service Call";
                                                                    END;
                                                                  "Table Type"::"Service-Contract":
                                                                    BEGIN
                                                                      "Table No." := DATABASE::"Service-Contract";
                                                                    END;
                                                                  "Table Type"::Customer:
                                                                    BEGIN
                                                                      "Table No." := DATABASE::Customer;
                                                                    END;
                                                                  "Table Type"::"Service Order Object":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Service Order Object";
                                                                    END;
                                                                  "Table Type"::"Service Order Inspection Line":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Service Order Inspection Line";
                                                                    END;
                                                                  "Table Type"::"Service Order Obj. Spec. Line":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Service Order Obj. Spec. Line";
                                                                    END;
                                                                  "Table Type"::"Service-Object":
                                                                    BEGIN
                                                                      "Table No." := DATABASE::"Service-Object";
                                                                    END;
                                                                  "Table Type"::"Debrief Text":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Debrief Text";
                                                                    END;
                                                                  "Table Type"::"Problem Text", "Table Type"::"Internal Text", "Table Type"::"Resolve Text":
                                                                    BEGIN
                                                                      "Table No." := DATABASE::"Service Text";
                                                                    END;
                                                                  "Table Type"::"Cause Text", "Table Type"::"Invoice Text":
                                                                    BEGIN
                                                                      TESTFIELD(Type, Type::"Service Order");
                                                                      "Table No." := DATABASE::"Service Text";
                                                                    END;
                                                                  "Table Type"::"Manual Check":
                                                                    "Table No." := 0;
                                                                END;
                                                                VALIDATE("Field No.", 0);
                                                                "No. of Records Needed" := "No. of Records Needed"::" ";
                                                              END;

                                                   CaptionML=[ENU=Table Type;
                                                              NOR=Tabelltype;
                                                              SVE=Tabelltyp];
                                                   OptionCaptionML=ENU=Service Order,Work Order,Advice Line,Purchase Order,Cost Plus Entry,Budget Line,Service Order Extension,Item Journal Line,Service Call,Service Contract,Customer,Service Order Object,Service Order Inspection Line,Service Order Object Specification Line,Object,Debrief Text,Problem Text,Internal Text,Resolve Text,Cause Text,Invoice Text,,,,,,,Manual Check;
                                                   OptionString=Service Order,Work Order,Advice Order,Purchase Line,Cost Plus Entry,Budget Line,Service Order Extension,Item Journal Line,Service Call,Service-Contract,Customer,Service Order Object,Service Order Inspection Line,Service Order Obj. Spec. Line,Service-Object,Debrief Text,Problem Text,Internal Text,Resolve Text,Cause Text,Invoice Text,,,,,,,Manual Check }
    { 50  ;   ;Table No.           ;Integer       ;InitValue=11012823;
                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 60  ;   ;Seq. No.            ;Integer       ;CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 70  ;   ;No. of Records Needed;Option       ;OnValidate=BEGIN
                                                                IF "No. of Records Needed" <> "No. of Records Needed"::" " THEN
                                                                  IF ("Table Type" = "Table Type"::"Manual Check") OR MaximallyOneRecordExists THEN
                                                                    FIELDERROR("Table Type");
                                                              END;

                                                   CaptionML=ENU=No. of Records Needed;
                                                   OptionCaptionML=ENU=" ,None,One,One Minimal,More than One";
                                                   OptionString=[ ,None,One,One Minimal,More than One] }
    { 80  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=FILTER(Normal|FlowField),
                                                                                  Type=FILTER(<>BLOB));
                                                   OnValidate=BEGIN
                                                                IF "Field No." <> 0 THEN BEGIN
                                                                  TESTFIELD("No. of Records Needed", "No. of Records Needed"::" ");
                                                                  CALCFIELDS("Field Name", "Field Caption");
                                                                  "Records to Check" := "Records to Check"::All;
                                                                END ELSE
                                                                  "Records to Check" := "Records to Check"::" ";
                                                                ClearFieldCheckFilter;
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1100528600 : Record 2000000041;
                                                              FieldTableManagement@1100528601 : Codeunit 11012372;
                                                            BEGIN
                                                              Field.SETRANGE(TableNo, "Table No.");
                                                              Field.SETFILTER(Class, '%1|%2', Field.Class::Normal, Field.Class::FlowField);
                                                              Field.SETFILTER(Type, '<>%1', Field.Type::BLOB);
                                                              FieldTableManagement.SelectFieldFromList("Table No.", "Field No.", Field);
                                                              VALIDATE("Field No.");
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr];
                                                   BlankZero=Yes }
    { 90  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              NOR=Feltnavn;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
    { 100 ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              NOR=Felttekst;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
    { 110 ;   ;Field Type          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Table No.),
                                                                                               No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Felttype;
                                                              SVE=Field Type];
                                                   Editable=No }
    { 120 ;   ;Field Check Filter  ;Text250       ;OnValidate=BEGIN
                                                                IF "Field Check Filter" <> '' THEN
                                                                  IF "Related Table Type" <> "Related Table Type"::" " THEN
                                                                    FIELDERROR("Related Table Type");
                                                                EvaluateFieldCheckFilter;
                                                              END;

                                                   CaptionML=ENU=Field Check Filter }
    { 130 ;   ;Field Check Filter (ENU);Text250   ;CaptionML=ENU=Field Check Filter (ENU);
                                                   Editable=No }
    { 140 ;   ;Records to Check    ;Option        ;OnValidate=BEGIN
                                                                IF "Records to Check" <> "Records to Check"::" " THEN
                                                                  TESTFIELD("No. of Records Needed", "No. of Records Needed"::" ");
                                                                IF "Field No." <> 0 THEN BEGIN
                                                                  IF "Records to Check" = "Records to Check"::" " THEN
                                                                    FIELDERROR("Records to Check");
                                                                  IF MaximallyOneRecordExists THEN
                                                                    TESTFIELD("Records to Check", "Records to Check"::All);
                                                                END;
                                                                IF "Records to Check" <> "Records to Check"::" " THEN
                                                                  IF "Table Type" = "Table Type"::"Manual Check" THEN
                                                                    FIELDERROR("Table Type");
                                                              END;

                                                   CaptionML=ENU=Records to Check;
                                                   OptionCaptionML=ENU=" ,Some,All";
                                                   OptionString=[ ,Some,All] }
    { 150 ;   ;Filter is Date Formula;Boolean     ;OnValidate=VAR
                                                                Field@1100528600 : Record 2000000041;
                                                              BEGIN
                                                                IF "Filter is Date Formula" THEN BEGIN
                                                                  TESTFIELD("Related Table Type", "Related Table Type"::" ");
                                                                  Field.GET("Table No.", "Field No.");
                                                                  IF Field.Type <> Field.Type::Date THEN
                                                                    FIELDERROR("Filter is Date Formula", Text001);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Filter is Date Formula }
    { 200 ;   ;Related Table Type  ;Option        ;OnValidate=BEGIN
                                                                IF Type = Type::"Service Call" THEN
                                                                  IF "Related Table Type" <> "Related Table Type"::" " THEN
                                                                    FIELDERROR("Related Table Type");
                                                                CASE "Related Table Type" OF
                                                                  "Related Table Type"::"Service Order":
                                                                    "Related Table No." := DATABASE::"Service Order";
                                                                  "Related Table Type"::"Service Order Extension":
                                                                    "Related Table No." := DATABASE::"Service Order Extension";
                                                                ELSE
                                                                  "Related Table No." := 0;
                                                                  "Relation Type" := "Relation Type"::" ";
                                                                END;
                                                                IF "Related Table Type" <> "Related Table Type"::" " THEN
                                                                  VALIDATE("Field Check Filter", '');
                                                                VALIDATE("Related Field No.", 0);
                                                              END;

                                                   CaptionML=ENU=Related Table Type;
                                                   OptionCaptionML=ENU=" ,Service Order,Service Order Extension";
                                                   OptionString=[ ,Service Order,Service Order Extension] }
    { 210 ;   ;Related Table No.   ;Integer       ;CaptionML=[ENU=Related Table No.;
                                                              NOR=Relatert tabellnr.;
                                                              SVE=Relaterat tabellnr.] }
    { 220 ;   ;Related Field No.   ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Related Table No.),
                                                                                  Class=FILTER(Normal|FlowField),
                                                                                  Type=FILTER(<>BLOB));
                                                   OnValidate=BEGIN
                                                                IF "Related Field No." <> 0 THEN BEGIN
                                                                  TESTFIELD("Table No.");
                                                                  TESTFIELD("Field No.");
                                                                  CALCFIELDS("Related Field Name", "Related Field Caption", "Related Field Type");
                                                                  TESTFIELD("Field Type", "Related Field Type");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1100528600 : Record 2000000041;
                                                              FieldTableManagement@1100528601 : Codeunit 11012372;
                                                            BEGIN
                                                              Field.SETRANGE(TableNo, "Related Table No.");
                                                              Field.SETFILTER(Class, '%1|%2', Field.Class::Normal, Field.Class::FlowField);
                                                              CALCFIELDS("Field Type");
                                                              Field.SETRANGE("Type Name", "Field Type");
                                                              FieldTableManagement.SelectFieldFromList("Related Table No.", "Related Field No.", Field);
                                                              VALIDATE("Related Field No.");
                                                            END;

                                                   CaptionML=ENU=Related Field No. }
    { 230 ;   ;Related Field Name  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Related Table No.),
                                                                                             No.=FIELD(Related Field No.)));
                                                   CaptionML=ENU=Related Field Name;
                                                   Editable=No }
    { 240 ;   ;Related Field Caption;Text80       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Related Table No.),
                                                                                                   No.=FIELD(Related Field No.)));
                                                   CaptionML=[ENU=Related Field Caption;
                                                              NOR=Tekst for relatert felt;
                                                              SVE=Relaterad f„ltrubrik];
                                                   Editable=No }
    { 250 ;   ;Related Field Type  ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Related Table No.),
                                                                                               No.=FIELD(Related Field No.)));
                                                   CaptionML=ENU=Related Field Type;
                                                   Editable=No }
    { 260 ;   ;Relation Type       ;Option        ;OnValidate=BEGIN
                                                                IF "Relation Type" <> "Relation Type"::" " THEN BEGIN
                                                                  TESTFIELD("Related Table Type");
                                                                  TESTFIELD("Related Table No.");
                                                                  TESTFIELD("Related Field No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Relation Type;
                                                              NOR=Relasjonstype;
                                                              SVE=Relationstyp];
                                                   OptionCaptionML=ENU=" ,=,<,>,<=,>=,<>";
                                                   OptionString=[ ,=,<,>,<=,>=,<>] }
  }
  KEYS
  {
    {    ;Type,Step Code,Table No.,Seq. No.       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=Filter may cause problems in a multi language environment';
      Text001@1100528601 : TextConst 'ENU=can only be set with field type Date';

    LOCAL PROCEDURE EvaluateFieldCheckFilter@1100528606();
    VAR
      Field@1100528602 : Record 2000000041;
      ServiceProcessManagement@1100528604 : Codeunit 11012883;
      RecRef@1100528601 : RecordRef;
      FieldRef@1100528600 : FieldRef;
      CurrentLanguageID@1100528603 : Integer;
      DummyDateFormula@1100528605 : DateFormula;
    BEGIN
      IF "Field Check Filter" = '' THEN
        EXIT;
      IF "Table Type" = "Table Type"::"Manual Check" THEN
        FIELDERROR("Table Type");
      IF NOT Field.GET("Table No.", "Field No.") THEN
        EXIT;
      IF Field.Type IN [Field.Type::Date, Field.Type::DateTime] THEN
        MESSAGE(Text000);
      RecRef.OPEN("Table No.");
      FieldRef := RecRef.FIELD("Field No.");

      CurrentLanguageID := GLOBALLANGUAGE;
      IF (Field.Type = Field.Type::Date) AND "Filter is Date Formula" THEN BEGIN
        EVALUATE(DummyDateFormula, "Field Check Filter");
        "Field Check Filter" := FORMAT(DummyDateFormula);
        GLOBALLANGUAGE(1033);
        "Field Check Filter (ENU)" := STRSUBSTNO('<%1>', FORMAT(DummyDateFormula));
        GLOBALLANGUAGE(CurrentLanguageID);
      END ELSE
        FieldRef.SETFILTER("Field Check Filter");
      IF Field.Type IN [Field.Type::Date, Field.Type::DateTime, Field.Type::Option, Field.Type::Boolean] THEN
        IF NOT "Filter is Date Formula" THEN
          "Field Check Filter" := FieldRef.GETFILTER;
      CASE Field.Type OF
        Field.Type::Boolean,
        Field.Type::Option:
          BEGIN
            GLOBALLANGUAGE(1033);
            "Field Check Filter (ENU)" := FieldRef.GETFILTER;
            GLOBALLANGUAGE(CurrentLanguageID);
          END;
        Field.Type::Decimal:
          "Field Check Filter (ENU)" := ServiceProcessManagement.ConvertDecimalFilterToENU("Field Check Filter");
      END;
    END;

    LOCAL PROCEDURE ClearFieldCheckFilter@1100528602();
    BEGIN
      "Field Check Filter" := '';
      "Field Check Filter (ENU)" := '';
    END;

    PROCEDURE DetermineSeqNo@1100528600();
    VAR
      ServiceProcessStepCheck@1100528600 : Record 11072305;
    BEGIN
      ServiceProcessStepCheck := Rec;
      ServiceProcessStepCheck.SETRECFILTER;
      ServiceProcessStepCheck.SETRANGE("Seq. No.");
      IF NOT ServiceProcessStepCheck.FINDLAST THEN
        "Seq. No." := 1
      ELSE
        "Seq. No." := ServiceProcessStepCheck."Seq. No." +1;
    END;

    PROCEDURE ShowCard@1100528601(ISourceNo@1100528600 : Code[20]);
    VAR
      ServiceOrder@1100528601 : Record 11012823;
      WorkOrder@1100528604 : Record 11229279;
      AdviceOrderLine@1100528605 : Record 11012055;
      PurchaseLine@1100528606 : Record 39;
      ServiceOrderCostPlusEntry@1100528607 : Record 11012825;
      ServiceOrderBudgetLine@1100528608 : Record 11012875;
      ItemJournalLine@1100528609 : Record 83;
      ServiceOrderExtension@1100528610 : Record 11071727;
      ServiceCall@1100528612 : Record 11012822;
      ServiceContract@1100528400 : Record 11012812;
      Customer@1100528401 : Record 18;
      ServiceOrderObject@1100528402 : Record 11071724;
      ServiceOrderInspectionLine@1100528403 : Record 11071753;
      ServiceOrderObjSpecLine@1100528404 : Record 11071782;
      ServiceObject@1100528405 : Record 11071691;
      DebriefText@1100528406 : Record 11012886;
      ServiceText@1100528407 : Record 11012882;
      ServiceProcessManagement@1100528602 : Codeunit 11012883;
      ServiceOrderInfoMgt@1100528611 : Codeunit 11012841;
      RecRef@1100528603 : RecordRef;
    BEGIN
      IF NOT ServiceProcessManagement.GetProcessStepRecRef(ISourceNo, Rec, RecRef) THEN
        EXIT;
      CASE "Table Type" OF
        "Table Type"::"Service Order":
          BEGIN
            RecRef.SETTABLE(ServiceOrder);
            IF ServiceOrder.FINDFIRST THEN
              ServiceOrder.ShowServiceOrderCard;
          END;
        "Table Type"::"Work Order":
          BEGIN
            RecRef.SETTABLE(WorkOrder);
            ServiceOrderInfoMgt.ShowSOWorkOrders(ISourceNo);
          END;
        "Table Type"::"Advice Order":
          BEGIN
            RecRef.SETTABLE(AdviceOrderLine);
            PAGE.RUNMODAL(0, AdviceOrderLine);
          END;
        "Table Type"::"Purchase Line":
          BEGIN
            RecRef.SETTABLE(PurchaseLine);
            PAGE.RUNMODAL(0, PurchaseLine);
          END;
        "Table Type"::"Cost Plus Entry":
          BEGIN
            RecRef.SETTABLE(ServiceOrderCostPlusEntry);
            PAGE.RUNMODAL(PAGE::"Service Order Cost Plus SF3", ServiceOrderCostPlusEntry);
          END;
        "Table Type"::"Budget Line":
          BEGIN
            RecRef.SETTABLE(ServiceOrderBudgetLine);
            PAGE.RUNMODAL(PAGE::"Service Order Budget Lines 2", ServiceOrderBudgetLine);
          END;
        "Table Type"::"Item Journal Line":
          BEGIN
            RecRef.SETTABLE(ItemJournalLine);
            PAGE.RUNMODAL(0, ItemJournalLine);
          END;
        "Table Type"::"Service Order Extension":
          BEGIN
            RecRef.SETTABLE(ServiceOrderExtension);
            IF ServiceOrderExtension.FINDSET THEN BEGIN
              ServiceOrder.GET(ServiceOrderExtension."Service Order No.");
              ServiceOrder.ShowServiceOrderCard;
            END;
          END;
        "Table Type"::"Service Call":
          BEGIN
            RecRef.SETTABLE(ServiceCall);
            PAGE.RUNMODAL(0, ServiceCall);
          END;
        "Table Type"::"Service-Contract":
          BEGIN
            RecRef.SETTABLE(ServiceContract);
            IF ServiceContract.FINDFIRST THEN
              PAGE.RUNMODAL(PAGE::"Service Contract Card", ServiceContract);
          END;
        "Table Type"::Customer:
          BEGIN
            RecRef.SETTABLE(Customer);
            IF Customer.FINDFIRST THEN
              PAGE.RUNMODAL(PAGE::"Customer Card", Customer);
          END;
        "Table Type"::"Service Order Object":
          BEGIN
            RecRef.SETTABLE(ServiceOrderObject);
            PAGE.RUNMODAL(PAGE::"Service Order Objects", ServiceOrderObject);
          END;
        "Table Type"::"Service Order Inspection Line":
          BEGIN
            RecRef.SETTABLE(ServiceOrderInspectionLine);
            PAGE.RUNMODAL(PAGE::"Service Order Inspection Lines", ServiceOrderInspectionLine);
          END;
        "Table Type"::"Service Order Obj. Spec. Line":
          BEGIN
            RecRef.SETTABLE(ServiceOrderObjSpecLine);
            PAGE.RUNMODAL(PAGE::"Service Order Obj. Spec. Lines", ServiceOrderObjSpecLine);
          END;
        "Table Type"::"Service-Object":
          BEGIN
            RecRef.SETTABLE(ServiceObject);
            PAGE.RUNMODAL(PAGE::"Service Object List", ServiceObject);
          END;
        "Table Type"::"Debrief Text":
          BEGIN
            RecRef.SETTABLE(DebriefText);
            PAGE.RUNMODAL(PAGE::"Debrief Text", DebriefText);
          END;
        "Table Type"::"Problem Text", "Table Type"::"Internal Text", "Table Type"::"Resolve Text",
        "Table Type"::"Cause Text", "Table Type"::"Invoice Text":
          BEGIN
            RecRef.SETTABLE(ServiceText);
            PAGE.RUNMODAL(PAGE::"Service Text", ServiceText);
          END;
      END;
    END;

    PROCEDURE GetFieldCheckFilter@1100528603(IType@1100528612 : 'Service Order,Service Call';INo@1100528605 : Code[20]) CheckFilter : Text;
    VAR
      ServiceOrder@1100528608 : Record 11012823;
      ServiceOrderExtension@1100528609 : Record 11071727;
      Field@1100528600 : Record 2000000041;
      RelatedField@1100528611 : Record 2000000041;
      ServiceProcessManagement@1100528601 : Codeunit 11012883;
      CurrentLanguageID@1100528602 : Integer;
      DateFormula@1100528603 : DateFormula;
      SeekDate@1100528604 : Date;
      RelatedRecRef@1100528606 : RecordRef;
      RelatedFieldRef@1100528607 : FieldRef;
      RecRef@1100528613 : RecordRef;
      FieldRef@1100528614 : FieldRef;
    BEGIN
      IF NOT Field.GET("Table No.", "Field No.") THEN
        Field.INIT;
      IF "Related Table Type" = "Related Table Type"::" " THEN BEGIN
        CASE Field.Type OF
          Field.Type::Decimal:
            CheckFilter := ServiceProcessManagement.ConvertDecimalFilterToCurrLanguage("Field Check Filter (ENU)");
          Field.Type::Boolean,
          Field.Type::Option:
            BEGIN
              RecRef.OPEN("Table No.");
              FieldRef := RecRef.FIELD("Field No.");
              CurrentLanguageID := GLOBALLANGUAGE;
              GLOBALLANGUAGE(1033);
              FieldRef.SETFILTER("Field Check Filter (ENU)");
              GLOBALLANGUAGE(CurrentLanguageID);
              CheckFilter := FieldRef.GETFILTER;
            END;
          Field.Type::Date:
            BEGIN
              IF NOT "Filter is Date Formula" THEN
                CheckFilter := "Field Check Filter"
              ELSE BEGIN
                CurrentLanguageID := GLOBALLANGUAGE;
                GLOBALLANGUAGE(1033);
                EVALUATE(DateFormula, "Field Check Filter (ENU)");
                SeekDate := CALCDATE(DateFormula);
                GLOBALLANGUAGE(CurrentLanguageID);
                IF SeekDate > TODAY THEN
                  CheckFilter := STRSUBSTNO('..%1', SeekDate);
                IF SeekDate < TODAY THEN
                  CheckFilter := STRSUBSTNO('%1..', SeekDate);
                IF SeekDate = TODAY THEN
                  CheckFilter := STRSUBSTNO('%1', SeekDate);
              END;
            END;
        ELSE
          CheckFilter := "Field Check Filter";
        END;
      END ELSE BEGIN
        CASE "Related Table Type" OF
          "Related Table Type"::"Service Order":
            BEGIN
              ServiceOrder.GET(INo);
              RelatedRecRef.GETTABLE(ServiceOrder);
            END;
          "Related Table Type"::"Service Order Extension":
            BEGIN
              ServiceOrderExtension.GetServOrderExtension(INo);
              RelatedRecRef.GETTABLE(ServiceOrderExtension);
            END;
        END;
        RelatedFieldRef := RelatedRecRef.FIELD("Related Field No.");
        IF NOT RelatedField.GET("Related Table No.", "Related Field No.") THEN
          RelatedField.INIT;
        IF RelatedField.Class = RelatedField.Class::FlowField THEN
          RelatedFieldRef.CALCFIELD;
        CheckFilter := STRSUBSTNO('%1%2', "Relation Type", RelatedFieldRef.VALUE);
        IF CheckFilter = FORMAT("Relation Type") THEN
          CheckFilter += '''''';
      END;
    END;

    LOCAL PROCEDURE MaximallyOneRecordExists@1100528400() : Boolean;
    BEGIN
      IF "Table Type" IN [
        "Table Type"::"Service Order", "Table Type"::"Service Order Extension",
        "Table Type"::"Service Call", "Table Type"::"Service-Contract", "Table Type"::Customer]
      THEN
        EXIT(TRUE);

      IF (Type = Type::"Service Call") AND
         ("Table Type" = "Table Type"::"Service-Object")
      THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

