OBJECT Table 11012567 Rental Rate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckCostPriceAllowed(FIELDNO("Cost Price"), "Cost Price", FALSE);
               CheckCostPriceAllowed(FIELDNO("Cost Price (Week)"), "Cost Price (Week)", FALSE);
               CheckCostPriceAllowed(FIELDNO("Cost Price (Month)"), "Cost Price (Month)", FALSE);
             END;

    OnModify=BEGIN
               PlantSetupRec.GET;
               IF (PlantSetupRec."Rate Component Result" <> '') AND
                  (PlantSetupRec."Type Division of Rate Comp." = PlantSetupRec."Type Division of Rate Comp."::DiffOnResult)
               THEN BEGIN
                 RateComponentRec.RESET;
                 RateComponentRec.SETRANGE("Rental Type", "Rental Type");
                 RateComponentRec.SETRANGE("Plant Type", "Plant Type");
                 RateComponentRec.SETRANGE("Plant No.", "Plant No.");
                 RateComponentRec.SETRANGE("Starting Date", "Starting Date");
                 RateComponentRec.SETRANGE("Rental Rate Code", PlantSetupRec."Rate Component Result");
                 IF RateComponentRec.FINDFIRST THEN
                   ERROR(Text000,
                     PlantSetupRec.FIELDCAPTION("Rate Component Result"), PlantSetupRec."Rate Component Result",
                     PlantSetupRec.FIELDCAPTION("Type Division of Rate Comp."), PlantSetupRec."Type Division of Rate Comp.");
               END;
               CheckCostPriceAllowed(FIELDNO("Cost Price"), "Cost Price", FALSE);
               CheckCostPriceAllowed(FIELDNO("Cost Price (Week)"), "Cost Price (Week)", FALSE);
               CheckCostPriceAllowed(FIELDNO("Cost Price (Month)"), "Cost Price (Month)", FALSE);
             END;

    OnDelete=BEGIN
               RateComponentRec.RESET;
               RateComponentRec.SETRANGE("Rental Type", "Rental Type");
               RateComponentRec.SETRANGE("Plant Type", "Plant Type");
               RateComponentRec.SETRANGE("Plant No.", "Plant No.");
               RateComponentRec.SETRANGE("Starting Date", "Starting Date");
               RateComponentRec.DELETEALL;
             END;

    OnRename=BEGIN
               CheckCostPriceAllowed(FIELDNO("Cost Price"), "Cost Price", FALSE);
               CheckCostPriceAllowed(FIELDNO("Cost Price (Week)"), "Cost Price (Week)", FALSE);
               CheckCostPriceAllowed(FIELDNO("Cost Price (Month)"), "Cost Price (Month)", FALSE);
             END;

    CaptionML=[ENU=Rental Rate;
               NOR=Leiesats;
               SVE=Uthyrningsavgift];
  }
  FIELDS
  {
    { 5   ;   ;Rental Type         ;Option        ;OnValidate=BEGIN
                                                                IF PlantRec.GET("Plant Type") THEN BEGIN
                                                                  IF ("Rental Type" = "Rental Type"::Rental) AND (PlantRec."Rental Type" = PlantRec."Rental Type"::"Sales/Buy Back") THEN
                                                                    FIELDERROR("Rental Type");

                                                                  IF ("Rental Type" = "Rental Type"::"Sales / Buy Back") AND (PlantRec."Rental Type" = PlantRec."Rental Type"::Rental) THEN
                                                                    FIELDERROR("Rental Type");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Rental Type;
                                                              NOR=Leietype;
                                                              SVE=Uthyrningstyp];
                                                   OptionCaptionML=[ENU=Rental,Sales / Buy Back,Plant Hours;
                                                                    NOR=Leie,Salg / Tilbakekjõp,Maskintimer;
                                                                    SVE=Uthyrning,FîrsÑljning/Üterkîp,Maskintimmar];
                                                   OptionString=Rental,Sales / Buy Back,Plant Hours;
                                                   Editable=No }
    { 10  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Plant Description", "Plant No. Description", "Plant Unit of Measure", Bulk);
                                                                CalcfieldRateUnit();
                                                              END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 15  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Plant No. Description");
                                                              END;

                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr];
                                                   Editable=No }
    { 20  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                CheckStartEndDate;
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 25  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                CheckStartEndDate;
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 30  ;   ;Rental Rate (Days)  ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=[ENU=Internal Rental Rate (Day);
                                                              NOR=Intern leiesats (Dag);
                                                              SVE=Intern uthyrningsavgift (dag)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 32  ;   ;Rental Rate External (Days);Decimal;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=[ENU=External Rental Rate (Day);
                                                              NOR=Ekstern leiesats (Dag);
                                                              SVE=Extern uthyrningsavgift (dag)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 35  ;   ;Rental Rate (Hours) ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::"Plant Hours");
                                                              END;

                                                   CaptionML=[ENU=Rental Rate Internal (Hours);
                                                              NOR=Intern leiesats (Timer);
                                                              SVE=Intern uthyrningsavgift (timmar)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 36  ;   ;Rental Rate External (Hours);Decimal;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::"Plant Hours");
                                                              END;

                                                   CaptionML=[ENU=Rental Rate External (Hours);
                                                              NOR=Ekstern leiesats (Timer);
                                                              SVE=Extern uthyrningsavgift (timmar)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 40  ;   ;Plant Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Description WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Plant Type Description;
                                                              NOR=Maskintype Beskrivelse;
                                                              SVE=Maskintyp beskrivning];
                                                   Editable=No }
    { 41  ;   ;Plant Unit of Measure;Code10       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Plant Unit of Measure;
                                                              NOR=Maskin MÜleenhet;
                                                              SVE=Maskin mÜttenhet];
                                                   Editable=No }
    { 42  ;   ;Rate Unit of Measure;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Rate Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Rate Unit of Measure;
                                                              NOR=MÜleenhet for Avgift;
                                                              SVE=Avgiftsenhet];
                                                   Editable=No }
    { 43  ;   ;Bulk                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Bulk WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Bulk;
                                                              NOR=Bulk;
                                                              SVE=Bulk];
                                                   Editable=No }
    { 45  ;   ;Plant No. Description;Text50       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=[ENU=Plant No. Description;
                                                              NOR=Maskinnr., Beskrivelse;
                                                              SVE=Maskinnr, beskrivning];
                                                   Editable=No }
    { 50  ;   ;Sales Rate          ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::"Sales / Buy Back");
                                                                VALIDATE("% of Sales Rate");
                                                              END;

                                                   CaptionML=[ENU=Sales Rate;
                                                              NOR=Salgsrate;
                                                              SVE=FîrsÑljningsavgift];
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 60  ;   ;% of Sales Rate     ;Decimal       ;OnValidate=BEGIN
                                                                //dbeekman, 7-4-09
                                                                IF "% of Sales Rate" <> 0 THEN BEGIN
                                                                  TESTFIELD("Rental Type","Rental Type"::"Sales / Buy Back");
                                                                  "Buy Back Rate" := ROUND("Sales Rate" * "% of Sales Rate" / 100,PriceRoundingPrecision);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=% of Sales Rate;
                                                              NOR=% av salgsrate;
                                                              SVE=Procent av fîrsÑljningsavgift];
                                                   BlankZero=Yes }
    { 70  ;   ;Buy Back Rate       ;Decimal       ;OnValidate=BEGIN
                                                                //dbeekman, 7-4-09
                                                                IF "Sales Rate" <> 0 THEN BEGIN
                                                                  TESTFIELD("Rental Type","Rental Type"::"Sales / Buy Back");
                                                                  "% of Sales Rate" := ROUND("Buy Back Rate" / "Sales Rate" * 100, 0.00001);
                                                                END ELSE
                                                                  "% of Sales Rate" := 0;
                                                              END;

                                                   CaptionML=[ENU=Buy Back Rate;
                                                              NOR=Tilbakekjõpsrate;
                                                              SVE=èterkîpsavgift];
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 80  ;   ;Current Price       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Current Price" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Current Price;
                                                              NOR=Aktuell pris;
                                                              SVE=Aktuellt pris];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 90  ;   ;Internal Rate on Components;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Rate Component".Amount WHERE (Rental Type=FIELD(Rental Type),
                                                                                                  Plant Type=FIELD(Plant Type),
                                                                                                  Plant No.=FIELD(Plant No.),
                                                                                                  Starting Date=FIELD(Starting Date),
                                                                                                  Internal/External=CONST(Internal)));
                                                   CaptionML=[ENU=Internal Day Rate on Components;
                                                              NOR=Intern Dagsats pÜ Komponenter;
                                                              SVE=Intern dagsavgift pÜ komponenter];
                                                   Description=This is the hour rate for rental type 'Plant Hours' instead of day rate like at rental type 'Rental'.;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 100 ;   ;External Rate on Components;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Rate Component".Amount WHERE (Rental Type=FIELD(Rental Type),
                                                                                                  Plant Type=FIELD(Plant Type),
                                                                                                  Plant No.=FIELD(Plant No.),
                                                                                                  Starting Date=FIELD(Starting Date)));
                                                   CaptionML=[ENU=External Day Rate on Components;
                                                              NOR=Ekstern dagsats pÜ komponenter;
                                                              SVE=Extern dagsavgift pÜ komponenter];
                                                   Description=This is the hour rate for rental type 'Plant Hours' instead of day rate like at rental type 'Rental'.;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 110 ;   ;Expected Occupation Perc.;Decimal  ;CaptionML=[ENU=Expected Occupation Perc.;
                                                              NOR=Forventet beleggprosent;
                                                              SVE=FîrvÑntad belÑggningsprocent];
                                                   MinValue=0 }
    { 120 ;   ;Rental Rate (Month) ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=[ENU=Rental Rate (Month);
                                                              NOR=Leiesats (MÜned);
                                                              SVE=Uthyrningsavgift (mÜnad)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 121 ;   ;Rental Rate External (Month);Decimal;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=[ENU=External Rental Rate (Month);
                                                              NOR=Ekstern leiesats (MÜned);
                                                              SVE=Extern uthyrningsavgift (mÜnad)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 130 ;   ;Rental Rate (Week)  ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=[ENU=Rental Rate (Week);
                                                              NOR=Leiesats (Uke);
                                                              SVE=Uthyrningsavgift (vecka)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 140 ;   ;Rental Rate External (Week);Decimal;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=[ENU=External Rental Rate (Week);
                                                              NOR=Ekstern leiesats (Uke);
                                                              SVE=Extern uthyrningsavgift (vecka)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 200 ;   ;Cost Price          ;Decimal       ;OnValidate=BEGIN
                                                                CheckCostPriceAllowed(FIELDNO("Cost Price"), "Cost Price", TRUE);
                                                              END;

                                                   CaptionML=[ENU=Cost Price;
                                                              NOR=Selvkost;
                                                              SVE=SjÑlvkostnadspris];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   Description=Only for Cost Prices in Estimate granules;
                                                   AutoFormatType=2 }
    { 210 ;   ;Cost Price (Week)   ;Decimal       ;OnValidate=BEGIN
                                                                CheckCostPriceAllowed(FIELDNO("Cost Price (Week)"), "Cost Price (Week)", TRUE);
                                                              END;

                                                   CaptionML=ENU=Cost Price (Week);
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   Description=Only for Cost Prices in Estimate granules;
                                                   AutoFormatType=2 }
    { 220 ;   ;Cost Price (Month)  ;Decimal       ;OnValidate=BEGIN
                                                                CheckCostPriceAllowed(FIELDNO("Cost Price (Month)"), "Cost Price (Month)", TRUE);
                                                              END;

                                                   CaptionML=ENU=Cost Price (Month);
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   Description=Only for Cost Prices in Estimate granules;
                                                   AutoFormatType=2 }
  }
  KEYS
  {
    {    ;Rental Type,Plant Type,Plant No.,Starting Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetupRec@1100485000 : Record 11012550;
      GLSetup@11012000 : Record 98;
      PlantRec@11012001 : Record 11012551;
      RateComponentRec@1210190000 : Record 11012585;
      Text000@1100485001 : TextConst 'ENU="%1 ''%2'' is present on the Rate Components of the Rate, this is not allowed when ''%3'' is ''%4''. ";NOR=%1 ''%2'' finnes pÜ Satsens Satskomponenter, dette er ikke tillatt nÜr ''%3'' er ''%4''.;SVE="%1 ''%2'' finns pÜ avgiftens avgiftskomponenter, detta Ñr inte tillÜtet nÑr ''%3'' Ñr ''%4''. "';
      Text001@1100525000 : TextConst 'ENU=%1 may not lie before the %2;NOR=%1 kan ikke vëre fõr %2;SVE=%1 fÜr inte vara fîre %2';
      Text002@1100525001 : TextConst 'ENU=on plant number level not allowed for bulk plant';
      Text003@1100529000 : TextConst 'ENU=Rate Components only allowed for existing Rates.;NOR=Satskomponenter tillates kun for eksisterende gebyrer.;SVE=Avgiftskomponenter tillÜts endast fîr befintliga avgifter.';

    PROCEDURE PriceRoundingPrecision@1() : Decimal;
    BEGIN
      GLSetup.GET;
      GLSetup.TESTFIELD("Unit-Amount Rounding Precision");
      EXIT(GLSetup."Unit-Amount Rounding Precision");
    END;

    PROCEDURE CalcfieldRateUnit@1100485000() : Code[10];
    BEGIN
      CALCFIELDS("Plant Unit of Measure", "Rate Unit of Measure");
      CASE "Rental Type" OF
        "Rental Type"::Rental:
          IF ("Rate Unit of Measure" = '') THEN
            "Rate Unit of Measure" := "Plant Unit of Measure";
        "Rental Type"::"Sales / Buy Back":
          "Rate Unit of Measure" := "Plant Unit of Measure";
        "Rental Type"::"Plant Hours":
          BEGIN
            PlantSetupRec.GET;
            "Rate Unit of Measure" := PlantSetupRec."Plant Hours Unit of Measure";
          END;
        ELSE
          "Rate Unit of Measure" := '';
      END;
      EXIT("Rate Unit of Measure");
    END;

    PROCEDURE CheckStartEndDate@1100525000();
    BEGIN
      IF ("Ending Date" <> 0D) AND ("Ending Date" < "Starting Date") THEN
        ERROR(Text001, FIELDCAPTION("Ending Date"), FIELDCAPTION("Starting Date"));
    END;

    LOCAL PROCEDURE CheckCostPriceAllowed@1100525001(FieldNumber@1100525001 : Integer;VAR CostPrice@1100525002 : Decimal;ErrorMode@1100525000 : Boolean);
    BEGIN
      //DP01047
      IF CostPrice = 0 THEN
        EXIT;
      IF ("Rental Type" <> "Rental Type"::Rental) AND (FieldNumber IN [FIELDNO("Cost Price (Week)"), FIELDNO("Cost Price (Month)")]) THEN BEGIN
        IF ErrorMode THEN
          TESTFIELD("Rental Type", "Rental Type"::Rental);
        CostPrice := 0;
      END;
      IF "Plant No." <> '' THEN BEGIN
        CALCFIELDS(Bulk);
        IF Bulk THEN BEGIN
          IF ErrorMode THEN BEGIN
            CASE FieldNumber OF
              FIELDNO("Cost Price"): FIELDERROR("Cost Price", Text002);
              FIELDNO("Cost Price (Week)"): FIELDERROR("Cost Price (Week)", Text002);
              FIELDNO("Cost Price (Month)"): FIELDERROR("Cost Price (Month)", Text002);
            END;
          END;
          CostPrice := 0;
        END;
      END;
    END;

    PROCEDURE ShowAndEditRateComponents@1210190000();
    VAR
      RateComponent@1210190001 : Record 11012585;
      RentalRate@1100529000 : Record 11012567;
    BEGIN
      IF NOT RentalRate.GET("Rental Type", "Plant Type", "Plant No.", "Starting Date") THEN
        ERROR(Text003);
      RateComponent.FILTERGROUP := 4;
      RateComponent.SETRANGE("Rental Type", "Rental Type");
      RateComponent.SETRANGE("Plant Type", "Plant Type");
      RateComponent.SETRANGE("Plant No.", "Plant No.");
      RateComponent.SETRANGE("Starting Date", "Starting Date");
      RateComponent.FILTERGROUP := 0;
      PAGE.RUNMODAL(PAGE::"Rate components", RateComponent);
    END;

    PROCEDURE UpdateRatesFromRateComponents@1100529000();
    VAR
      RentalRate@1100529000 : Record 11012567;
      UpdateRentalRatesFromComp@1100529001 : Report 11012580;
    BEGIN
      IF NOT ("Rental Type" IN ["Rental Type"::Rental, "Rental Type"::"Plant Hours"]) THEN
        EXIT;
      RentalRate.FILTERGROUP := 9;
      RentalRate.SETRANGE("Rental Type", "Rental Type");
      RentalRate.FILTERGROUP := 0;
      RentalRate.SETRANGE("Plant Type", "Plant Type");
      RentalRate.SETRANGE("Plant No.", "Plant No.");
      RentalRate.SETRANGE("Starting Date", "Starting Date");
      UpdateRentalRatesFromComp.SETTABLEVIEW(RentalRate);
      UpdateRentalRatesFromComp.RUNMODAL;
    END;

    PROCEDURE DetermineCalculatedRateFromComponents@1210190001(VAR InternalRateCalc@1100529000 : Decimal;VAR ExternalRateCalc@1100529001 : Decimal);
    BEGIN
      GLSetup.GET;
      CALCFIELDS("Internal Rate on Components","External Rate on Components");
      InternalRateCalc := "Internal Rate on Components";
      ExternalRateCalc := "External Rate on Components";
      IF ("Expected Occupation Perc." <> 0) THEN BEGIN
        InternalRateCalc := InternalRateCalc * (100 / "Expected Occupation Perc.");
        ExternalRateCalc := ExternalRateCalc * (100 / "Expected Occupation Perc.");
      END;
      InternalRateCalc := ROUND(InternalRateCalc, GLSetup."Unit-Amount Rounding Precision");
      ExternalRateCalc := ROUND(ExternalRateCalc, GLSetup."Unit-Amount Rounding Precision");
    END;

    PROCEDURE DefaultExpectedOccupationPerc@1100529001() : Decimal;
    VAR
      PlantNumber@1100529001 : Record 11012552;
    BEGIN
      EXIT(PlantNumber.ExpectedOccupationPercentage("Plant Type", "Plant No."));
    END;

    BEGIN
    {
      04-10-2002 Cor Hofman Field "Rental Rate (Hours)" added for "Plant Hour Accounting"
    }
    END.
  }
}

