OBJECT Codeunit 11012552 InsertPlantPlantOrder
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11012556;
    OnRun=BEGIN
            OrderRec.COPY(Rec);
            Code('', '');
          END;

  }
  CODE
  {
    VAR
      Text000@11012012 : TextConst 'DEU=M”chten Sie das Werkzeug eingeben, das am %1 an Standort ''%2'' vorhanden ist?;ENU=Do you want to insert plant that is present per %1 on location ''%2'' ?;NLD=Wilt u het materieel opvoeren dat per %1 aanwezig is op locatie ''%2''?';
      Text001@11012013 : TextConst 'DEU=Zeilen werden erstellt @1@@@@@@@@@@@@@;ENU=Creating Lines  @1@@@@@@@@@@@@@;NLD=Regels worden aangemaakt @1@@@@@@@@@@@@@;NOR=Opprette Rad  @1@@@@@@@@@@@@@;SVE=Skapar rader @1@@@@@@@@@@@@@';
      Text002@11012014 : TextConst 'DEU=Ab %2 ist kein Werkzeug an Standort % vorhanden.;ENU=No plant present on location %1 per %2.;NLD=Er is geen materieel aanwezig op locatie %1 per %2.;NOR=Det finnes ingen maskin p† lokasjon %1 per %2.;SVE=Det finns ingen Maskin p† lagerst„llet %1 per %2.';
      Text002b@1100485006 : TextConst 'DEU=Ab %2 ist vom Mitarbeiter %3 kein Werkzeug an Standort %1 vorhanden.;ENU=Of employee %3 there is no plant present on location %1 per %2.;NLD=Van werknemer %3, is er geen materieel aanwezig op locatie %1 per %2.;NOR=Det finnes ingen maskin p† lokasjon %1 per %2 for den ansatte %3.;SVE=Det finns ingen Maskin p† lagerst„llet %1 per %2 f”r den anst„llda %3.';
      Text003@11012015 : TextConst 'DEU=Es wurden %1 %2(n) erstellt.;ENU=There are %1 %2s created.;NLD=Er zijn %1 %2s aangemaakt.;NOR=%1 %2 er opprettet.;SVE=%1 %2 har skapats.';
      Text004@11012016 : TextConst 'DEU=M”chten Sie die negative Lagermenge an %1 mit dem Werkzeug erg„nzen, das ab %3 an %2 vorhanden ist?;ENU=Do you want to replenish the negative stock on %1 with plant present on %2 per %3 ?;NLD=Wilt u de negatieve voorraad op %1 aanvullen met materieel dat op %2 aanwezig is per %3 ?;NOR=Vil du fylle p† det negative artikkelbeholdningen i %1 med maskinen som finnes p† %2 per %3?;SVE=Vill du fylla p† det negativa lagret i %1 med Maskinen som finns p† %2 per %3?';
      Text005a@11012017 : TextConst 'DEU=M”chten Sie dat Werkzeug von %1 ''%2'' hinzufgen?;ENU=Do you want to add plant of %1 ''%2''?;NLD=Wilt u het materieel opvoeren van %1 ''%2''?';
      Text005b@1100528500 : TextConst 'DEU=Werkzeug von %1 hinzufgen:;ENU=Add plant of %1:;NLD=Materieel opvoeren van %1:';
      Text006@11012018 : TextConst 'DEU=Es ist kein Werkzueg an %1 vorhanden.;ENU=No plant present on %1?;NLD=Er is geen materieel aanwezig op %1.;NOR=Finnes det ingen maskin p† %1?;SVE=Finns det ingen Maskin p† %1?';
      PlantSetupRec@1100525000 : Record 11012550;
      OrderRec@11012000 : Record 11012556;
      InventoryRec@11012001 : Record 11012555;
      InventoryRec2@11012002 : Record 11012555;
      NegInventoryRec@11012003 : Record 11012555;
      OrderLineRec@11012004 : Record 11012557;
      OrderCostLineRec@1210190001 : Record 11012558;
      PlantLocRec@11012005 : Record 11012554;
      PlantLocRec2@1100525001 : Record 11012554;
      PlantHireCorrRec@11012006 : Record 11012565;
      PlantHireCorrLineRec@11012007 : Record 11012566;
      LineNo@11012008 : Integer;
      Window@11012009 : Dialog;
      NoOfRecords@11012010 : Integer;
      LineCounter@11012011 : Integer;
      Text007@1210190000 : TextConst 'DEU=M”chten Sie Schadenzeilen erstellen?;ENU=Do you want to create Repair Cost Lines ?;NLD=Wilt u schaderegels aanmaken?;NOR=Vil du opprette reparasjonskostrader?;SVE=Vill du skapa reparationskostnadslinjer?';
      Text007b@1100525002 : TextConst 'DEU=Alle Standard-Schadenszeilen hinzufgen, Alle Standard-Schadenszeilen ausw„hlen (wenn mehr als 1);ENU=Add all default damage lines,Select default damage lines (if more than 1);NLD=Alle standaard schaderegels toevoegen,Selecteren standaard schaderegels (als meer dan 1)';
      Text008@1210190002 : TextConst 'DEU=Es ist kein Schadenwerkzeug vorhanden;ENU=No damaged plant present;NLD=Er is geen schade materieel aanwezig;NOR=Det finnes ingen skadede maskiner;SVE=Det finns inga skadade Maskiner';
      Text009@1210190003 : TextConst 'DEU=Reparaturkostenzeilen fr %1 Werkzeugauftragszeile(n) erstellt.;ENU=Repair Cost Lines created for %1 Plant order Line(s).;NLD=Schaderegels aangemaakt voor %1 materieelorderregel(s).;NOR=Reparer Kostrader opprettet for %1 maskin Ordrerad(r)';
      Text010@1100485000 : TextConst 'DEU=Es ist keine negative Lagermenge auf Standort %1 am %2 vorhanden.;ENU=No negative stock present on location %1 per %2.;NLD=Er is geen negatieve voorraad aanwezig op locatie %1 per %2.;NOR=Det finnes ingen negativ artikkelbeholdning p† lager %1 per %2.;SVE=Det finns inget negativt lager p† lagerst„llet %1 per %2.';
      Text011@1100485001 : TextConst 'DEU=Ab %2 ist kein Werkzeug an Standort %1 zum Korrigieren der negativen Lagermengen vorhanden.;ENU=No plant present on location %1 per %2 to correct the negative stock.;NLD=Er is geen materieel aanwezig op locatie %1 per %2 om de negatieve voorraad te corrigeren.;NOR=Det finnes ingen maskin p† lokasjon %1 per %2 for † rette opp negativt lager.;SVE=Det finns ingen Maskin p† lagerst„llet %1 per %2 f”r att kunna r„tta till negativt lager.';
      Text012@1100485002 : TextConst 'DEU=Es sind %1 %2s estellt, aber ab %4 gibt es nicht gengend Vorr„te am Standort %3 zum Korrigieren aller negativen Lagermengen.;ENU=There are %1 %2s created, but there is not enough stock on location %2 per %4 to correct all negative stock.;NLD=Er zijn %1 %2s aangemaakt, maar er is niet voldoende voorraad aanwezig op locatie %3 per %4 om alle negatieve voorraad te corrigeren.;NOR=%1 %2 er opprettet men det finnes ikke tilstrekkelig i lagerbeholdningen p† lager %2 per %4 for † korrigere all negativ artikkelbeholdning.;SVE=%1 %2 har skapats men det finns inte tillr„ckligt i lagret p† lagerst„llet %2 per %4 f”r att korrigera negativt lager.';
      Text013@1100485003 : TextConst 'DEU=M”chten Sie das verkaufte V/Z-Werkzeug am %2 erstellen, das an %1 vorhanden ist?;ENU=Do you want to transfer sold plant (S/BB) present on %1 to %2 ?;NLD=Wilt u het verkochte v/t-materieel dat op %1 aanwezig is per %2 opvoeren?;NOR=Vil du overf›re solgt maskin (S/BB) i %1 til %2?;SVE=Vill du ”verf”ra s†ld Maskin (S/BB) i %1 till %2?';
      Text014@1100485004 : TextConst 'DEU=Ab %2 ist kein verkauftes V/Z-Werkzeug an Standort %1 vorhanden.;ENU=No sold plant present (S/BB) on location %1 per %2.;NLD=Er is geen verkocht v/t-materieel aanwezig op locatie %1 per %2.;NOR=Det finnes ingen solgt maskin (S/BB) p† lokasjon %1 per %2.;SVE=Det finns ingen s†ld Maskin (S/BB) p† lagerst„llet %1 per %2.';
      Text015@1100485005 : TextConst 'DEU=An Standort ''%1'' ist kein Werkzeug vorhanden, das mit einem Mitarbeiter verbunden ist.;ENU=On location ''%1'' no plant present that is linked to an employee.;NLD=Op locatie ''%1'' geen materieel aanwezig dat aan een werknemer is gekoppeld.;NOR=Det finnes ingen maskin p† lokasjon ''%1'' som er koblet til en ansatt.;SVE=Det finns ingen Maskin p† lagerst„llet ''%1'' som „r l„nkat till en anst„lld.';
      Text016@1100485007 : TextConst 'DEU=Kein Mitarbeiter, verantwortlich fr Werkzeug, an Standort des Firmas ''%1'' vorhanden.;ENU=No plant reponsible employee present on location of company ''%1''.;NLD=Geen materieel verantwoordelijke werknemer aanwezig op locatie van bedrijf ''%1''.;NOR=Det finnes ingen maskinansvarlig p† plass i Bedrift ''%1''.;SVE=Det finns ingen Maskinsansvarig p† plats i f”retaget ''%1''.';
      Text017@1100528501 : TextConst 'DEU=%1 und %2 beide nicht gefllt;ENU=%1 and %2 both not filled;NLD=%1 en %2 beide niet gevuld';

    PROCEDURE Code@1(IEmplNo@1100485002 : Code[20];IEmplComp@1100485001 : Text[30]);
    VAR
      lvNewOrderLineCounter@1100485000 : Integer;
    BEGIN
      WITH OrderRec DO BEGIN
        TESTFIELD(Posted,FALSE);
        IF (Status >= Status::Released) THEN
          FIELDERROR(Status);
        TESTFIELD("From Location");
        TESTFIELD("Transfer Date");
        IF NOT (Type IN [Type::Arrival, Type::Removal, Type::"Other Transfers"]) THEN
          FIELDERROR(Type);

        PlantSetupRec.GET;
        PlantLocRec.GET("From Location");
        PlantLocRec.TESTFIELD(Blocked,FALSE);

        IF IEmplNo = '' THEN BEGIN  //* Als gevuld dan geen vraag nodig, dan is er al bewust een werknemer geselecteerd.
          IF NOT CONFIRM(Text000, FALSE, "Transfer Date", "From Location") THEN
            EXIT;
        END;

        Window.OPEN(Text001);
        LineCounter := 0;

        InventoryRec.LOCKTABLE;
        OrderLineRec.LOCKTABLE;

        IF IEmplNo = '' THEN BEGIN
          //* Als voor een employee dan niet niet eerst alles verwijderen (zie opm in 'InsertPlantOfEmplOnPlantOrder').
          OrderLineRec.RESET;
          OrderLineRec.SETRANGE("Plant Order No.","No.");
          OrderLineRec.SETRANGE(Type, OrderLineRec.Type::Plant);
          //* Niet terugkoop verwijderen want daar is een aparte functie voor (C-011288)
          OrderLineRec.SETFILTER("Rental Type", '<%1', OrderLineRec."Rental Type"::"Buy Back");
          OrderLineRec.DELETEALL(TRUE);
        END;
        LineNo := 0;
        OrderLineRec.RESET;
        OrderLineRec.SETRANGE("Plant Order No.","No.");
        IF OrderLineRec.FINDLAST THEN
          LineNo := OrderLineRec."Line No.";  //* Als niet alle regels verwijderd dan bepalen (C-011288)

        InventoryRec.RESET;
        InventoryRec.SETRANGE(Removed,FALSE);
        InventoryRec.SETRANGE(Location,"From Location");
        InventoryRec.SETFILTER("Present from",'<= %1',"Transfer Date");
        IF Type <> Type::Arrival THEN
          InventoryRec.SETRANGE(Sold,FALSE);
        InventoryRec.SETFILTER(Quantity,'>0');
        IF IEmplNo <> '' THEN BEGIN  //* Dan alleen van de betreffende (geselecteerde) werknemer
          InventoryRec.SETRANGE("Employee Company", IEmplComp);
          InventoryRec.SETRANGE("Employee No.", IEmplNo);
        END;
        NoOfRecords := InventoryRec.COUNT;
        IF InventoryRec.FINDSET THEN
          REPEAT
            LineCounter := LineCounter + 1;
            Window.UPDATE(1,ROUND(LineCounter / NoOfRecords * 10000,1));

            OrderLineRec.SETRANGE("Plant Order No.","No.");
            OrderLineRec.SETRANGE("Plant Type",InventoryRec."Plant Type");
            OrderLineRec.SETRANGE("No.",InventoryRec."Plant No.");
            OrderLineRec.SETRANGE("From Rate Code",InventoryRec."Rate Code");  //* Onafhankelijk van instelling!
            OrderLineRec.SETRANGE("From Plant Group",InventoryRec."Plant Group");
            OrderLineRec.SETFILTER("Rental Type", '<%1', OrderLineRec."Rental Type"::"Buy Back"); //* (C-011288)
            IF NOT OrderLineRec.FINDFIRST THEN BEGIN
              LineNo := LineNo + 10000;
              OrderLineRec.INIT;
              OrderLineRec."Plant Order No." := "No.";
              OrderLineRec.InitRecord();  //* Eerst ordernr. vullen dan pas aanroepen
              OrderLineRec."Line No." := LineNo;
              OrderLineRec."From Location" := "From Location";
              OrderLineRec."Transfer Date" := "Transfer Date";
              OrderLineRec.SETFILTER("Date Filter",'..%1',"Transfer Date");
              OrderLineRec."Plant Type" := InventoryRec."Plant Type";
              OrderLineRec.VALIDATE("Plant Type");
              IF (OrderLineRec."Rental Type" <> OrderLineRec."Rental Type"::Rental) AND (NOT PlantLocRec.Depot) THEN BEGIN
                //* Als iets naar locatie is verzonden als huur, maar is nu geen huurmaterieel meer.
                //* Dit kan eigenlijk alleen maar gebeuren als bij materieelsoort het huurtype is gewijzigd in 'v/t'.
                //* Dan toch maar op 'Huur' zetten, bij vrijgeven/boeken MO krijgt men dan wel een melding (men moet dan
                //* zelf maar aktie ondernemen). Niet deze overslaan want dan blijft er wat staan terwijl je dit niet merkt.
                OrderLineRec."Rental Type" := OrderLineRec."Rental Type"::Rental;  //* Niet met 'Validate' (geen error)
                OrderLineRec.SetBoughtFilter();
              END;
              OrderLineRec."No." := InventoryRec."Plant No.";
              OrderLineRec.VALIDATE("No.");
              OrderLineRec."From Rate Code" := InventoryRec."Rate Code";
              OrderLineRec."From Plant Group" := InventoryRec."Plant Group";
              OrderLineRec.SETRANGE("Plant Group Filter", OrderLineRec."From Plant Group");
              //* Gebruik 'Stock Rate Code' en niet "Stock Plant No."! (als alles op tarc='' dan toch gelijk)
              OrderLineRec.CALCFIELDS("Stock Rate Code on From Loc.");
              IF Type = Type::Arrival THEN BEGIN
                IF PlantSetupRec."Extended Picking Procedure" THEN
                  OrderLineRec."Asked Quantity" := OrderLineRec."Stock Rate Code on From Loc."
                ELSE
                  OrderLineRec.Quantity := OrderLineRec."Stock Rate Code on From Loc.";
              END ELSE BEGIN
                IF (NOT (PlantSetupRec."Removal with Qty. to Receive" AND (Type = Type::Removal))) OR
                   (PlantSetupRec."Removal with Qty. to Receive" AND (Type = Type::Removal) AND
                    PlantSetupRec."Fill Qty. with Qty. to Receive")
                THEN
                  OrderLineRec.Quantity := OrderLineRec."Stock Rate Code on From Loc.";
                //* Als voor een employee dan bij bulk toch volledig partij opvoeren (zie opm in 'InsertPlantOfEmplOnPlantOrder').
                IF (PlantSetupRec."Removal with Qty. to Receive") AND (Type = Type::Removal) THEN
                  OrderLineRec."Qty. to Receive" :=  OrderLineRec."Stock Rate Code on From Loc.";
                IF Type = Type::Removal THEN
                  OrderLineRec."Plant Group" := InventoryRec."Plant Group";
              END;
              OrderLineRec.INSERT(TRUE);
              lvNewOrderLineCounter := lvNewOrderLineCounter + 1;
            END;
          UNTIL InventoryRec.NEXT = 0;

        COMMIT;
        Window.CLOSE;
        IF lvNewOrderLineCounter = 0 THEN BEGIN
          IF IEmplNo = '' THEN
            MESSAGE(Text002, "From Location", "Transfer Date")
          ELSE
            MESSAGE(Text002b, "From Location", "Transfer Date", IEmplNo);
        END ELSE
          MESSAGE(Text003,lvNewOrderLineCounter,OrderLineRec.TABLECAPTION);

      END;
    END;

    PROCEDURE InsertPlantSelectionOnPO@1100525000(VAR Rec@1100525000 : Record 11012556);
    VAR
      TmpPlantInventRec@1100525002 : TEMPORARY Record 11012555;
      PlantOrderLine2@1100525006 : Record 11012557;
      AddLinkedPlantTypeStndItemMgt@1100525005 : Codeunit 11012583;
      PlantInventPage@1100525001 : Page 11012559;
      SoldAllowed@1100525003 : Boolean;
      DetailLevel@1100525004 : Integer;
    BEGIN
      OrderRec.COPY(Rec);
      WITH OrderRec DO BEGIN
        TESTFIELD(Posted,FALSE);
        IF (Status >= Status::Released) THEN
          FIELDERROR(Status);
        TESTFIELD("From Location");
        TESTFIELD("To Location");
        //IF NOT (Type IN [Type::Removal, Type::"Other Transfers"]) THEN  //33092.o
        IF NOT (Type IN [Type::Arrival, Type::Removal, Type::"Other Transfers"]) THEN  //33092.n
          FIELDERROR(Type);
        TESTFIELD("Transfer Date");

        PlantSetupRec.GET;
        PlantLocRec.GET("From Location");
        PlantLocRec2.GET("To Location");
        PlantLocRec2.TESTFIELD(Blocked,FALSE);
        SoldAllowed :=
          (Type = Type::Removal) OR
          ((Type = Type::"Other Transfers") AND
           (NOT PlantLocRec.Depot) AND (NOT PlantLocRec2.Depot) AND ("From Location" <> "To Location"));

        InventoryRec.RESET;
        InventoryRec.FILTERGROUP(9);
        InventoryRec.SETRANGE(Removed, FALSE);
        InventoryRec.SETRANGE(Location, "From Location");
        InventoryRec.SETFILTER("Present from", '<=%1', "Transfer Date");
        InventoryRec.SETFILTER("Date Filter", '<=%1', "Transfer Date");
        IF Type <> Type::Arrival THEN BEGIN  //33092.n
          IF NOT SoldAllowed THEN BEGIN
            InventoryRec.SETRANGE(Sold, FALSE);
            InventoryRec.SETRANGE("Sold Filter", FALSE);
          END;
        END;  //33092.n
        InventoryRec.SETFILTER(Quantity, '>%1', 0);
        InventoryRec.SETFILTER("Quantity Filter", '>%1', 0);
        InventoryRec.FILTERGROUP(0);
        IF PlantSetupRec."Rate Codes" THEN  //* Detail Level: 2=Plant No., 3=Rate Code
          DetailLevel := 3
        ELSE BEGIN
          InventoryRec2.COPY(InventoryRec);
          InventoryRec2.SETFILTER("Rate Code", '<>%1', '');
          IF InventoryRec2.ISEMPTY THEN
            DetailLevel := 2
          ELSE
            DetailLevel := 3;
        END;

        PlantInventPage.SetDetailSelection(DetailLevel);
        PlantInventPage.SetMultiSelection();  //* Then Detail Level not editable in form, so in returned in the level that is set
        PlantInventPage.SETTABLEVIEW(InventoryRec);
        PlantInventPage.LOOKUPMODE(TRUE);
        IF PlantInventPage.RUNMODAL <> ACTION::LookupOK THEN
          EXIT;
        IF NOT PlantInventPage.GetMarkedRecords(TmpPlantInventRec) THEN //* In tmprec compressed quantity is returned
          EXIT;

        TmpPlantInventRec.RESET;
        IF TmpPlantInventRec.FINDSET THEN BEGIN
          OrderLineRec.RESET;
          OrderLineRec.SETRANGE("Plant Order No.", "No.");
          IF OrderLineRec.FINDLAST THEN
            LineNo := OrderLineRec."Line No."
          ELSE
            LineNo := 0;
          AddLinkedPlantTypeStndItemMgt.SetGeneralConfirm();
          REPEAT
            TmpPlantInventRec.CALCFIELDS(Bulk);
            OrderLineRec.RESET;
            OrderLineRec.SETRANGE("Plant Order No.","No.");
            OrderLineRec.SETRANGE("Plant Type",TmpPlantInventRec."Plant Type");
            OrderLineRec.SETRANGE("No.",TmpPlantInventRec."Plant No.");
            OrderLineRec.SETRANGE("From Plant Group",TmpPlantInventRec."Plant Group");
            IF NOT TmpPlantInventRec.Bulk THEN BEGIN
              IF NOT OrderLineRec.ISEMPTY THEN
                TmpPlantInventRec.Quantity := 0;   //* Unique plant no. already on plant order
            END ELSE BEGIN
              OrderLineRec.SETRANGE("From Location", TmpPlantInventRec.Location);
              OrderLineRec.SETRANGE("From Rate Code",TmpPlantInventRec."Rate Code");
              IF OrderLineRec.FINDFIRST THEN BEGIN         //* Correction: minus qty already on PO
                IF NOT ((Type = Type::Arrival) AND PlantSetupRec."Extended Picking Procedure") THEN BEGIN  //33092.n
                  OrderLineRec.SetBoughtFilter();
                  OrderLineRec.SETRANGE("Plant Group Filter",TmpPlantInventRec."Plant Group");
                  OrderLineRec.CALCFIELDS("Quantity Rate Code");
                  TmpPlantInventRec.Quantity := TmpPlantInventRec.Quantity - OrderLineRec."Quantity Rate Code";
                //33092.sn
                END ELSE BEGIN
                  REPEAT
                    IF OrderLineRec.Quantity > OrderLineRec."Asked Quantity" THEN
                      TmpPlantInventRec.Quantity := TmpPlantInventRec.Quantity - OrderLineRec.Quantity
                    ELSE
                      TmpPlantInventRec.Quantity := TmpPlantInventRec.Quantity - OrderLineRec."Asked Quantity";
                  UNTIL OrderLineRec.NEXT = 0;
                END;
                //33092.en
              END;
            END;
            IF TmpPlantInventRec.Quantity > 0 THEN BEGIN
              LineNo := LineNo + 10000;
              OrderLineRec.INIT;
              OrderLineRec."Plant Order No." := "No.";
              OrderLineRec.InitRecord();
              OrderLineRec."Line No." := LineNo;
              OrderLineRec."From Location" := TmpPlantInventRec.Location;
              OrderLineRec."Transfer Date" := "Transfer Date";
              OrderLineRec."Plant Type" := TmpPlantInventRec."Plant Type";
              OrderLineRec.VALIDATE("Plant Type");
              IF Type <> Type::Arrival THEN BEGIN  //33092.n
                IF NOT (TmpPlantInventRec.Sold AND SoldAllowed) THEN
                  OrderLineRec."Rental Type" := OrderLineRec."Rental Type"::Rental
                ELSE BEGIN
                  IF Type = Type::"Other Transfers" THEN
                    OrderLineRec."Rental Type" := OrderLineRec."Rental Type"::"Buy Back and Sales"
                  ELSE
                    OrderLineRec."Rental Type" := OrderLineRec."Rental Type"::"Buy Back";
                END;
              END;  //33092.n
              OrderLineRec."No." := TmpPlantInventRec."Plant No.";
              OrderLineRec.VALIDATE("No.");
              OrderLineRec."From Rate Code" := TmpPlantInventRec."Rate Code";
              OrderLineRec."From Plant Group" := TmpPlantInventRec."Plant Group";
              //33092.sn
              IF Type = Type::Arrival THEN BEGIN
                IF PlantSetupRec."Extended Picking Procedure" THEN
                  OrderLineRec."Asked Quantity" := TmpPlantInventRec.Quantity
                ELSE
                  OrderLineRec.Quantity := TmpPlantInventRec.Quantity;
              END ELSE BEGIN
              //33092.en
                IF (NOT (PlantSetupRec."Removal with Qty. to Receive" AND (Type = Type::Removal))) OR
                   (PlantSetupRec."Removal with Qty. to Receive" AND (Type = Type::Removal) AND
                    PlantSetupRec."Fill Qty. with Qty. to Receive")
                THEN
                  OrderLineRec.Quantity := TmpPlantInventRec.Quantity;
                IF (PlantSetupRec."Removal with Qty. to Receive") AND (Type = Type::Removal) THEN
                  OrderLineRec."Qty. to Receive" :=  TmpPlantInventRec.Quantity;
                IF Type = Type::Removal THEN
                  OrderLineRec."Plant Group" := TmpPlantInventRec."Plant Group";
              END;
              OrderLineRec.INSERT(TRUE);
              IF AddLinkedPlantTypeStndItemMgt.InsertOnPlantOrder(OrderLineRec, 0) THEN BEGIN  // 0=Insert
                PlantOrderLine2.SETRANGE("Plant Order No.", "No.");
                PlantOrderLine2.FINDLAST;
                LineNo := PlantOrderLine2."Line No."
              END;
            END;  //33092.n
          UNTIL TmpPlantInventRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InsertPlantOfEmplOnPlantOrder@1100485001(VAR Rec@1100485000 : Record 11012556);
    VAR
      lvCompRec@1100485001 : Record 2000000006;
      lvEmplRec@1100485002 : Record 5200;
      lvTmpEmplRec@1100485005 : TEMPORARY Record 5200;
      lvCompName@1100485003 : Text[30];
    BEGIN
      //* Opmerkingen (PS/GJK):
      //* Bij het opvoeren van materieel op van een locatie worden eerst altijd alle reeds aanwezige orderregels verwijderd.
      //* Dit is niet gewenst als het materieel van een bepaalde werknemer opgevoerd moet worden. Het kan zijn dat hierna nog het
      //* materieel van een andere werknemer op wilt voeren, je zou dan de orderregels van de eerste werknemer weer kwijt zijn.
      //* Verder werkt de functie niet voor bulk materieel. Als er meerdere werknemers verantwoordelijk zijn voor een bulkpartij
      //* dan is dit niet mogelijk, bij de verwerking van de MO pakt het systeem dan toch een willekeurig deel. Daarom is besloten
      //* om dan maar altijd de volledige partij op te voeren.

      OrderRec.COPY(Rec);
      WITH OrderRec DO BEGIN
        TESTFIELD(Posted,FALSE);
        IF (Status >= Status::Released) THEN
          FIELDERROR(Status);
        TESTFIELD("From Location");
        TESTFIELD("Transfer Date");

        PlantLocRec.GET("From Location");
        IF (PlantLocRec."Employee No." <> '') THEN BEGIN
          Code('', '');  //* Dan is alles op locatie toch van 1 werkenemer, dus dan alles opvoeren
          EXIT;
        END;

        //* Zelfde filters als in functie 'Code', alleen extra/andere filters op employee/company
        InventoryRec.RESET;
        InventoryRec.SETRANGE(Removed,FALSE);
        InventoryRec.SETRANGE(Location,"From Location");
        InventoryRec.SETFILTER("Present from",'<= %1',"Transfer Date");
        InventoryRec.SETRANGE(Sold,FALSE);
        InventoryRec.SETFILTER(Quantity,'>0');
        InventoryRec.SETFILTER("Employee No.", '<>%1', '');
        IF NOT InventoryRec.FINDFIRST THEN
          ERROR(Text015, "From Location");

        InventoryRec.SETFILTER("Employee Company", '<>%1', '');
        IF InventoryRec.FINDFIRST THEN BEGIN
          lvCompName := InventoryRec."Employee Company";
          InventoryRec.SETFILTER("Employee Company", '<>%1', lvCompName);
          IF InventoryRec.FINDFIRST THEN BEGIN  //* Alleen bedrijf selecteren als werknemers van verschillende bedrijven
            lvCompName := '';
            IF PAGE.RUNMODAL(0, lvCompRec) = ACTION::LookupOK THEN BEGIN
              IF lvCompRec.Name <> COMPANYNAME THEN
                lvCompName := lvCompRec.Name;
            END;
          END;
        END;

        IF lvCompName <> '' THEN
          lvEmplRec.CHANGECOMPANY(lvCompName);
        InventoryRec.SETFILTER("Employee No.", '<>%1', '');
        InventoryRec.SETRANGE("Employee Company", lvCompName);
        IF NOT InventoryRec.FINDFIRST THEN BEGIN
          IF lvCompName = '' THEN
            ERROR(Text016, COMPANYNAME)
          ELSE
            ERROR(Text016, lvCompName);
        END ELSE BEGIN
          REPEAT
            IF NOT lvTmpEmplRec.GET(InventoryRec."Employee No.") THEN BEGIN
              IF lvEmplRec.GET(InventoryRec."Employee No.") THEN
                lvTmpEmplRec := lvEmplRec
              ELSE BEGIN
                lvTmpEmplRec.INIT;
                lvTmpEmplRec."No." := InventoryRec."Employee No.";
                lvTmpEmplRec.Status := lvTmpEmplRec.Status::Active;  //* Anders wordt werknemer mogelijk niet getoond
              END;
              lvTmpEmplRec.INSERT;
            END;
          UNTIL InventoryRec.NEXT = 0;
        END;
        IF PAGE.RUNMODAL(0,lvTmpEmplRec) = ACTION::LookupOK THEN BEGIN
          IF lvTmpEmplRec."No." <> '' THEN
            Code(lvTmpEmplRec."No.", lvCompName);
        END;
      END;
    END;

    PROCEDURE InsertSoldPlantOnPlantOrder@1100485000(VAR Rec@1100485002 : Record 11012556);
    VAR
      lvPlantTypeRec@1100485000 : Record 11012551;
      lvPlantToLocRec@1100485001 : Record 11012554;
      lvNewOrderLineCounter@1100485003 : Integer;
    BEGIN
      //* Gekopieerd van functie 'Code' (melding C-011288).
      //* Verkocht v/t-materieel terughalen.
      OrderRec.COPY(Rec);
      WITH OrderRec DO BEGIN
        TESTFIELD(Posted,FALSE);
        IF (Status >= Status::Released) THEN
          FIELDERROR(Status);
        TESTFIELD("From Location");
        IF NOT (Type IN [Type::Removal, Type::"Other Transfers"]) THEN
          FIELDERROR(Type);
        TESTFIELD("Transfer Date");

        PlantSetupRec.GET;
        PlantLocRec.GET("From Location");
        PlantLocRec.TESTFIELD(Blocked,FALSE);
        PlantLocRec.TESTFIELD(Depot,FALSE);

        lvPlantToLocRec.GET("To Location");
        lvPlantToLocRec.TESTFIELD(Blocked,FALSE);

        IF NOT CONFIRM(Text013, FALSE, "From Location", "Transfer Date") THEN
          EXIT;

        Window.OPEN(Text001);
        LineCounter := 0;

        InventoryRec.LOCKTABLE;
        OrderLineRec.LOCKTABLE;

        OrderLineRec.RESET;
        OrderLineRec.SETRANGE("Plant Order No.","No.");
        OrderLineRec.SETRANGE(Type, OrderLineRec.Type::Plant);
        OrderLineRec.SETRANGE("Rental Type",
          OrderLineRec."Rental Type"::"Buy Back", OrderLineRec."Rental Type"::"Buy Back and Sales");
        OrderLineRec.DELETEALL(TRUE);
        LineNo := 0;
        OrderLineRec.RESET;
        OrderLineRec.SETRANGE("Plant Order No.","No.");
        IF OrderLineRec.FINDLAST THEN
          LineNo := OrderLineRec."Line No.";  //* Als niet alle regels verwijderd dan bepalen

        InventoryRec.SETRANGE(Removed,FALSE);
        InventoryRec.SETRANGE(Location,"From Location");
        InventoryRec.SETFILTER("Present from",'<= %1',"Transfer Date");
        InventoryRec.SETRANGE(Sold,TRUE);
        InventoryRec.SETFILTER(Quantity,'>0');
        NoOfRecords := InventoryRec.COUNT;
        IF InventoryRec.FINDSET THEN
          REPEAT
            LineCounter := LineCounter + 1;
            Window.UPDATE(1,ROUND(LineCounter / NoOfRecords * 10000,1));

            lvPlantTypeRec.GET(InventoryRec."Plant Type");
            IF (lvPlantTypeRec."Rental Type" <> lvPlantTypeRec."Rental Type"::Rental) THEN BEGIN
              //* Als op 'Huur' gezet maat overslaan (zal men in praktijk niet doen)

              OrderLineRec.SETRANGE("Plant Order No.","No.");
              OrderLineRec.SETRANGE("Plant Type",InventoryRec."Plant Type");
              OrderLineRec.SETRANGE("No.",InventoryRec."Plant No.");
              OrderLineRec.SETRANGE("From Rate Code",InventoryRec."Rate Code");  //* Onafhankelijk van instelling!
              OrderLineRec.SETRANGE("From Plant Group",InventoryRec."Plant Group");
              OrderLineRec.SETRANGE("Rental Type",
                OrderLineRec."Rental Type"::"Buy Back", OrderLineRec."Rental Type"::"Buy Back and Sales");
              IF NOT OrderLineRec.FINDFIRST THEN BEGIN
                LineNo := LineNo + 10000;
                OrderLineRec.INIT;
                OrderLineRec."Plant Order No." := "No.";
                OrderLineRec.InitRecord();  //* Eerst ordernr. vullen dan pas aanroepen
                OrderLineRec."Line No." := LineNo;
                OrderLineRec."From Location" := "From Location";
                OrderLineRec."Transfer Date" := "Transfer Date";
                OrderLineRec.SETFILTER("Date Filter",'..%1',"Transfer Date");
                OrderLineRec."Plant Type" := InventoryRec."Plant Type";
                OrderLineRec.VALIDATE("Plant Type");
                IF lvPlantToLocRec.Depot THEN
                  OrderLineRec.VALIDATE("Rental Type", OrderLineRec."Rental Type"::"Buy Back")
                ELSE
                  OrderLineRec.VALIDATE("Rental Type", OrderLineRec."Rental Type"::"Buy Back and Sales");
                OrderLineRec.SetBoughtFilter();
                OrderLineRec."No." := InventoryRec."Plant No.";
                OrderLineRec.VALIDATE("No.");
                OrderLineRec."From Rate Code" := InventoryRec."Rate Code";
                OrderLineRec."From Plant Group" := InventoryRec."Plant Group";
                OrderLineRec.SETRANGE("Plant Group Filter", OrderLineRec."From Plant Group");
                //* Gebruik 'Stock Rate Code' en niet "Stock Plant No."! (als alles op tarc='' dan toch gelijk)
                OrderLineRec.CALCFIELDS("Stock Rate Code on From Loc.");
                IF (NOT (PlantSetupRec."Removal with Qty. to Receive" AND (Type = Type::Removal))) OR
                   (PlantSetupRec."Removal with Qty. to Receive" AND (Type = Type::Removal) AND
                    PlantSetupRec."Fill Qty. with Qty. to Receive")
                THEN
                  OrderLineRec.Quantity := OrderLineRec."Stock Rate Code on From Loc.";
                IF (PlantSetupRec."Removal with Qty. to Receive") AND (Type = Type::Removal) THEN
                  OrderLineRec."Qty. to Receive" :=  OrderLineRec."Stock Rate Code on From Loc.";
                OrderLineRec.INSERT(TRUE);
                lvNewOrderLineCounter := lvNewOrderLineCounter + 1;
              END;
            END;
          UNTIL InventoryRec.NEXT = 0;

        COMMIT;
        Window.CLOSE;
        IF lvNewOrderLineCounter = 0 THEN
          MESSAGE(Text014, "From Location", "Transfer Date")
        ELSE
          MESSAGE(Text003,lvNewOrderLineCounter,OrderLineRec.TABLECAPTION);
      END;
    END;

    PROCEDURE InsertNegativeInventory@2(VAR Rec@26370 : Record 11012556);
    VAR
      lvNegInventoryRec2@1100485004 : Record 11012555;
      lvNegPresent@1100485005 : Boolean;
      lvCompleteNegCorr@1100485007 : Boolean;
      lvHoldPlantType@1100485000 : Code[20];
      lvHoldPlantNo@1100485001 : Code[20];
      lvHoldRateCode@1100485002 : Code[10];
      lvHoldPlantGroup@1100529700 : Code[20];
      lvNegQuantity@1100485003 : Decimal;
      lvInsertedQuantity@1100485006 : Decimal;
    BEGIN
      OrderRec.COPY(Rec);

      WITH OrderRec DO BEGIN
        TESTFIELD(Posted,FALSE);
        TESTFIELD(Type,OrderRec.Type::"Negative Inventory Correction");
        IF (Status >= Status::Released) THEN
          FIELDERROR(Status);
        TESTFIELD("From Location");
        TESTFIELD("To Location");
        TESTFIELD("Transfer Date");

        IF NOT CONFIRM(Text004,FALSE, "To Location", "From Location", "Transfer Date") THEN
          EXIT;

        Window.OPEN(Text001);
        LineCounter := 0;

        InventoryRec.LOCKTABLE;
        OrderLineRec.LOCKTABLE;

        OrderLineRec.RESET;
        OrderLineRec.SETRANGE("Plant Order No.","No.");
        OrderLineRec.DELETEALL(TRUE);
        LineNo := 0;
        lvNegPresent := FALSE;
        lvCompleteNegCorr := TRUE;

        NegInventoryRec.SETRANGE(Removed,FALSE);
        NegInventoryRec.SETRANGE(Location,"To Location");
        NegInventoryRec.SETFILTER("Present from",'<= %1',"Transfer Date");
        NegInventoryRec.SETFILTER(Quantity,'<0');
        NoOfRecords := NegInventoryRec.COUNT;
        IF NegInventoryRec.FINDSET THEN BEGIN
          lvNegPresent := TRUE;
          lvNegInventoryRec2.COPYFILTERS(NegInventoryRec);  //* Algemene filters 'Neg-Rec2' hier al vullen (eenmalig)
          REPEAT
            LineCounter := LineCounter + 1;
            Window.UPDATE(1,ROUND(LineCounter / NoOfRecords * 10000,1));

            IF (NegInventoryRec."Plant Type" <> lvHoldPlantType) OR
               (NegInventoryRec."Plant No." <> lvHoldPlantNo) OR
               (NegInventoryRec."Rate Code" <> lvHoldRateCode) OR
               (NegInventoryRec."Plant Group" <> lvHoldPlantGroup)
            THEN BEGIN
              lvHoldPlantType := NegInventoryRec."Plant Type";
              lvHoldPlantNo := NegInventoryRec."Plant No.";
              lvHoldRateCode := NegInventoryRec."Rate Code";
              lvHoldPlantGroup := NegInventoryRec."Plant Group";
              lvNegQuantity := 0;
              lvNegInventoryRec2.SETRANGE("Plant Type",NegInventoryRec."Plant Type");
              lvNegInventoryRec2.SETRANGE("Plant No.",NegInventoryRec."Plant No.");
              lvNegInventoryRec2.SETRANGE("Rate Code",NegInventoryRec."Rate Code");  //* Onafhankelijk van instelling!
              lvNegInventoryRec2.SETRANGE("Plant Group",NegInventoryRec."Plant Group");
              IF lvNegInventoryRec2.FINDSET THEN BEGIN
                REPEAT
                  lvNegQuantity := lvNegQuantity + lvNegInventoryRec2.Quantity;
                UNTIL lvNegInventoryRec2.NEXT = 0;
              END;
              lvInsertedQuantity := 0;

              InventoryRec.SETRANGE(Removed,FALSE);
              InventoryRec.SETRANGE(Location,"From Location");
              InventoryRec.SETRANGE("Plant Type",NegInventoryRec."Plant Type");
              InventoryRec.SETRANGE("Plant No.",NegInventoryRec."Plant No.");
              InventoryRec.SETFILTER("Present from",'<= %1',"Transfer Date");
              InventoryRec.SETFILTER(Quantity,'>0');
              IF InventoryRec.FINDSET THEN BEGIN
                REPEAT
                  OrderLineRec.SETRANGE("Plant Order No.","No.");
                  OrderLineRec.SETRANGE("Plant Type",InventoryRec."Plant Type");
                  OrderLineRec.SETRANGE("No.",InventoryRec."Plant No.");
                  OrderLineRec.SETRANGE("From Rate Code",InventoryRec."Rate Code");  //* Onafhankelijk van instelling!
                  OrderLineRec.SETRANGE("From Plant Group",InventoryRec."Plant Group");
                  IF NOT OrderLineRec.FINDFIRST THEN BEGIN
                    LineNo := LineNo + 10000;
                    OrderLineRec.INIT;
                    OrderLineRec."Plant Order No." := "No.";
                    OrderLineRec.InitRecord();  //* Eerst ordernr. vullen dan pas aanroepen
                    OrderLineRec."Line No." := LineNo;
                    OrderLineRec."From Location" := "From Location";
                    OrderLineRec."Transfer Date" := "Transfer Date";
                    OrderLineRec.SETFILTER("Date Filter",'..%1',"Transfer Date");
                    OrderLineRec."Plant Type" := InventoryRec."Plant Type";
                    OrderLineRec.VALIDATE("Plant Type");
                    OrderLineRec."No." := InventoryRec."Plant No.";
                    OrderLineRec.VALIDATE("No.");
                    OrderLineRec."From Rate Code" := InventoryRec."Rate Code";
                    OrderLineRec."To Rate Code" := NegInventoryRec."Rate Code";
                    OrderLineRec."From Plant Group" := InventoryRec."Plant Group";
                    OrderLineRec."To Plant Group" := NegInventoryRec."Plant Group";
                    OrderLineRec.SETRANGE("Plant Group Filter",OrderLineRec."From Plant Group");
                    //* Gebruik 'Stock Rate Code' en niet "Stock Plant No."! (als alles op tarc='' dan toch gelijk)
                    OrderLineRec.CALCFIELDS("Stock Rate Code on From Loc.");
                    IF OrderLineRec."Stock Rate Code on From Loc." > ABS(lvNegQuantity) THEN
                      OrderLineRec.Quantity := ABS(lvNegQuantity)
                    ELSE
                      OrderLineRec.Quantity := OrderLineRec."Stock Rate Code on From Loc.";
                    OrderLineRec.INSERT;
                    lvInsertedQuantity := lvInsertedQuantity + OrderLineRec.Quantity;
                  END;
                UNTIL (InventoryRec.NEXT = 0) OR (lvInsertedQuantity >= ABS(lvNegQuantity));
              END;
              IF (lvInsertedQuantity < ABS(lvNegQuantity)) THEN
                lvCompleteNegCorr := FALSE;
            END;
          UNTIL NegInventoryRec.NEXT = 0;
        END;

        COMMIT;
        Window.CLOSE;
        IF LineNo = 0 THEN BEGIN
          IF lvNegPresent THEN
            MESSAGE(Text011, "From Location", "Transfer Date")
          ELSE
            MESSAGE(Text010, "To Location", "Transfer Date");
        END ELSE BEGIN
          IF lvCompleteNegCorr THEN
            MESSAGE(Text003,LineNo/10000,OrderLineRec.TABLECAPTION)
          ELSE
            MESSAGE(Text012,LineNo/10000,OrderLineRec.TABLECAPTION, "From Location", "Transfer Date");
        END;
      END;
    END;

    PROCEDURE RentalCorrection@3(VAR Rec@11012000 : Record 11012565);
    VAR
      PlantLocationNo@1100528500 : Code[20];
      NumberNewRows@11012001 : Integer;
    BEGIN
      PlantHireCorrRec.COPY(Rec);

      WITH PlantHireCorrRec DO BEGIN
        PlantLocationNo := RentalCorrChecksAndDetermineLocation;
        IF PlantLocationNo = '' THEN
          EXIT;
        Window.OPEN(Text001);
        LineCounter := 0;
        NumberNewRows := 0;

        InventoryRec.LOCKTABLE;
        PlantHireCorrLineRec.LOCKTABLE;
        PlantHireCorrLineRec.RESET;
        PlantHireCorrLineRec.SETRANGE("Rental  Correction No.","No.");
        IF PlantHireCorrLineRec.FIND('+') THEN
          LineNo := PlantHireCorrLineRec."Line No."
        ELSE
          LineNo := 0;

        InventoryRec.SETRANGE(Removed,FALSE);
        InventoryRec.SETRANGE(Location,PlantLocationNo);
        InventoryRec.SETRANGE(Sold,FALSE);
        InventoryRec.SETFILTER(Quantity,'>0');
        NoOfRecords := InventoryRec.COUNT;
        IF InventoryRec.FINDSET THEN
          REPEAT
            LineCounter := LineCounter + 1;
            Window.UPDATE(1,ROUND(LineCounter / NoOfRecords * 10000,1));

            InventoryRec2.COPY(InventoryRec);
            InventoryRec2.SETCURRENTKEY(Removed,Location,"Plant Type","Plant No.","Rate Code",Sold);
            InventoryRec2.SETRANGE("Plant Type",InventoryRec."Plant Type");
            InventoryRec2.SETRANGE("Plant No.",InventoryRec."Plant No.");
            InventoryRec2.SETRANGE("Plant Group",InventoryRec."Plant Group");
            InventoryRec2.CALCSUMS(Quantity);
            PlantHireCorrLineRec.SETRANGE("Rental  Correction No.","No.");
            PlantHireCorrLineRec.SETRANGE("Plant Type",InventoryRec."Plant Type");
            PlantHireCorrLineRec.SETRANGE("No.",InventoryRec."Plant No.");
            PlantHireCorrLineRec.SETRANGE("Plant Group",InventoryRec."Plant Group");
            IF NOT PlantHireCorrLineRec.FIND('-') THEN BEGIN
              NumberNewRows := NumberNewRows + 1;
              LineNo := LineNo + 10000;
              PlantHireCorrLineRec.INIT;
              PlantHireCorrLineRec."Rental  Correction No." := "No.";
              PlantHireCorrLineRec."Line No." := LineNo;
              PlantHireCorrLineRec.VALIDATE("Plant Type",InventoryRec."Plant Type");
              PlantHireCorrLineRec.VALIDATE("No.",InventoryRec."Plant No.");
              PlantHireCorrLineRec.VALIDATE("Plant Group",InventoryRec."Plant Group");
              PlantHireCorrLineRec.VALIDATE(Quantity,InventoryRec2.Quantity);
              PlantHireCorrLineRec.INSERT;
            END ELSE BEGIN
              PlantHireCorrLineRec.VALIDATE(Quantity,InventoryRec2.Quantity);
              PlantHireCorrLineRec.MODIFY;
            END;
          UNTIL InventoryRec.NEXT = 0;

        COMMIT;
        Window.CLOSE;
        IF LineCounter = 0 THEN
          MESSAGE(Text006,PlantLocationNo)
        ELSE
          MESSAGE(Text003,NumberNewRows,PlantHireCorrLineRec.TABLECAPTION);

      END;
    END;

    PROCEDURE InsertPlantSelectionOnRentalCorrection@1100528501(VAR Rec@1100525000 : Record 11012565);
    VAR
      TmpPlantInventory@1100525002 : TEMPORARY Record 11012555;
      PlantOrderLine2xxx@1100525006 : Record 11012557;
      PlantInventoryList@1100525001 : Page 11012559;
      DetailLevel@1100525004 : Integer;
      PlantLocationNo@1100528500 : Code[20];
    BEGIN
      PlantHireCorrRec.COPY(Rec);
      WITH PlantHireCorrRec DO BEGIN
        PlantSetupRec.GET;
        PlantLocationNo := RentalCorrChecksAndDetermineLocation;
        IF PlantLocationNo = '' THEN
          EXIT;
        InventoryRec.RESET;
        InventoryRec.FILTERGROUP(9);
        InventoryRec.SETRANGE(Removed, FALSE);
        InventoryRec.SETRANGE(Location, PlantLocationNo);
        InventoryRec.SETRANGE(Sold, FALSE);
        InventoryRec.SETFILTER(Quantity, '>%1', 0);
        InventoryRec.SETFILTER("Quantity Filter", '>%1', 0);
        InventoryRec.SETRANGE(Sold, FALSE);
        InventoryRec.SETRANGE("Sold Filter", FALSE);
        InventoryRec.FILTERGROUP(0);
        IF PlantSetupRec."Rate Codes" THEN  //* Detail Level: 2=Plant No., 3=(Plant No./)Rate Code
          DetailLevel := 3
        ELSE
          DetailLevel := 2;
        PlantInventoryList.SetDetailSelection(DetailLevel);
        PlantInventoryList.SetMultiSelection();  //* Then Detail Level not editable in form, so in returned in the level that is set
        PlantInventoryList.SetInventoryLocationsSameProjectDisabled;
        PlantInventoryList.SETTABLEVIEW(InventoryRec);
        PlantInventoryList.LOOKUPMODE(TRUE);
        IF PlantInventoryList.RUNMODAL <> ACTION::LookupOK THEN
          EXIT;
        IF NOT PlantInventoryList.GetMarkedRecords(TmpPlantInventory) THEN //* In tmprec compressed quantity is returned
          EXIT;

        TmpPlantInventory.RESET;
        IF TmpPlantInventory.FINDSET THEN BEGIN
          PlantHireCorrLineRec.RESET;
          PlantHireCorrLineRec.SETRANGE("Rental  Correction No.","No.");
          IF PlantHireCorrLineRec.FIND('+') THEN
            LineNo := PlantHireCorrLineRec."Line No."
          ELSE
            LineNo := 0;
          REPEAT
            TmpPlantInventory.CALCFIELDS(Bulk);
            IF TmpPlantInventory.Quantity > 0 THEN BEGIN
              LineNo := LineNo + 10000;
              PlantHireCorrLineRec.INIT;
              PlantHireCorrLineRec."Rental  Correction No." := "No.";
              PlantHireCorrLineRec."Line No." := LineNo;
              PlantHireCorrLineRec.VALIDATE("Plant Type", TmpPlantInventory."Plant Type");
              PlantHireCorrLineRec.VALIDATE("No.", TmpPlantInventory."Plant No.");
              PlantHireCorrLineRec.VALIDATE("Plant Group", TmpPlantInventory."Plant Group");
              PlantHireCorrLineRec.VALIDATE(Quantity, TmpPlantInventory.Quantity);
              PlantHireCorrLineRec.INSERT;
            END;
          UNTIL TmpPlantInventory.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE RentalCorrChecksAndDetermineLocation@1100528500() : Code[20];
    VAR
      PlantLocationNo@1100528500 : Code[20];
    BEGIN
      WITH PlantHireCorrRec DO BEGIN
        TESTFIELD("No.");
        TESTFIELD(Posted, FALSE);
        IF ("Credit Location" = '') AND ("Debit Location" = '') THEN
          ERROR(Text017, FIELDCAPTION("Credit Location"), FIELDCAPTION("Debit Location"));
        CheckBlock("Debit Location");
        CheckBlock("Credit Location");

        PlantLocationNo := '';
        IF ("Credit Location" <> '') AND (("Debit Location" = '') OR ("Debit Location" = "Credit Location")) THEN
          PlantLocationNo := "Credit Location"
        ELSE BEGIN
          IF ("Debit Location" <> '') AND (("Credit Location" = '') OR ("Credit Location" = "Debit Location")) THEN
            PlantLocationNo := "Debit Location";
        END;
        IF PlantLocationNo <> '' THEN BEGIN
          IF NOT CONFIRM(Text005a, FALSE, PlantLocRec.TABLECAPTION, PlantLocationNo) THEN
            EXIT('');
          EXIT(PlantLocationNo);
        END;
        CASE STRMENU(
          FIELDCAPTION("Debit Location") + ' ' + "Debit Location" +','+ FIELDCAPTION("Credit Location") + ' ' + "Credit Location",
          2, STRSUBSTNO(Text005b, PlantLocRec.TABLECAPTION))
        OF
          1: EXIT("Debit Location");
          2: EXIT("Credit Location");
        END;
      END;
      EXIT('');
    END;

    PROCEDURE InsertRepairCostLines@1210190000(VAR Rec@1210190000 : Record 11012556);
    VAR
      PlantCostLine@1100529000 : Record 11012591;
      OrderCostLine2@1100529001 : Record 11012558;
      PlantOrderCostLineMgt@1100529002 : Codeunit 11012581;
      WithSelection@1100525000 : Boolean;
      TotalDamage@1210190001 : Decimal;
      Counter2@1100529003 : Integer;
      OptionNo@1100525001 : Integer;
      NoOfDamageLines@1100525002 : Integer;
    BEGIN
      OrderRec.COPY(Rec);

      WITH OrderRec DO BEGIN
        TESTFIELD(Posted,FALSE);
        TESTFIELD("From Location");
        TESTFIELD(Type, Type::Removal);
        TESTFIELD("Advanced Removal");
        TESTFIELD("To Location Repair");

        PlantLocRec.GET("From Location");
        PlantLocRec.TESTFIELD(Blocked,FALSE);

        //IF NOT CONFIRM(Text007,FALSE) THEN  //C004697-2.o
        //  EXIT;
        //C004697-2.sn
        OptionNo := STRMENU(Text007b, 2, Text007);
        IF OptionNo = 0 THEN
          EXIT;
        IF OptionNo = 2 THEN
          WithSelection := TRUE;
        //C004697-2.en

        Window.OPEN(Text001);
        LineCounter := 0;

        //DP00241.so (C004697)
        //OrderCostLineRec.RESET;
        //OrderCostLineRec.SETRANGE("Plant Order No.","No.");
        //OrderCostLineRec.SETRANGE(Type, OrderCostLineRec.Type::Damage);
        //IF OrderCostLineRec.FINDFIRST THEN
        //  IF NOT CONFIRM(Text009, FALSE) THEN
        //    EXIT;

        //OrderCostLineRec.SETRANGE(Type);
        //OrderCostLineRec.LOCKTABLE;
        //IF OrderCostLineRec.FINDLAST THEN
        //  LineNo := OrderCostLineRec."Line No."
        //ELSE
        //  LineNo := 0;
        //DP00241.eo (C004697)

        OrderLineRec.RESET;
        OrderLineRec.SETRANGE("Plant Order No.","No.");
        OrderLineRec.SETFILTER("Quantity Damaged", '<>0');
        NoOfRecords := OrderLineRec.COUNT;

        IF OrderLineRec.FINDSET THEN
          REPEAT
            LineCounter := LineCounter + 1;
            Window.UPDATE(1,ROUND(LineCounter / NoOfRecords * 10000,1));

            //LineNo := LineNo + 10000;  //DP00241.o (C004697)

            //DP00241.sn (C004697)
            OrderCostLine2.RESET;
            OrderCostLine2.SETRANGE("Plant Order No.", OrderLineRec."Plant Order No.");
            OrderCostLine2.SETRANGE("Plant Order Line No.", OrderLineRec."Line No.");
            OrderCostLine2.SETRANGE("Plant Type", OrderLineRec."Plant Type");
            OrderCostLine2.SETRANGE(Type, OrderCostLine2.Type::Damage);
            IF OrderCostLine2.ISEMPTY THEN BEGIN
              //Counter2 := Counter2 + 1; //C004697-2.o
              PlantCostLine.SETRANGE("Cost Level", PlantCostLine."Cost Level"::PlantType);  //DP00671
              PlantCostLine.SETRANGE(Code, OrderLineRec."Plant Type");
              PlantCostLine.SETRANGE(Type, PlantCostLine.Type::Damage);
              IF PlantCostLine.FINDFIRST THEN BEGIN
                NoOfDamageLines := PlantCostLine.COUNT;  //C004697-2.n
                IF WithSelection AND (NoOfDamageLines > 1) THEN  //C004697-2.n
                  Window.CLOSE;
                IF PlantOrderCostLineMgt.InsertStdCostLinesDamagePOLine(OrderLineRec, WithSelection) THEN //C004697-2.c
                  Counter2 := Counter2 + 1;  //C004697-2.n
                IF WithSelection AND (NoOfDamageLines > 1) THEN BEGIN  //C004697-2.n
                  Window.OPEN(Text001);
                  Window.UPDATE(1,ROUND(LineCounter / NoOfRecords * 10000,1));
                END;
              END ELSE BEGIN
                Counter2 := Counter2 + 1;  //C004697-2.n
                OrderCostLine2.RESET;
                OrderCostLine2.SETRANGE("Plant Order No.", OrderLineRec."Plant Order No.");
                IF OrderCostLine2.FINDLAST THEN
                  LineNo := OrderCostLine2."Line No." + 10000
                ELSE
                  LineNo := 10000;
            //DP00241.en (C004697)
                OrderCostLineRec.INIT;
                OrderCostLineRec."Plant Order No." := "No.";
                OrderCostLineRec."Line No." := LineNo;
                OrderCostLineRec.InitRecord;
                OrderCostLineRec.VALIDATE(Type, OrderCostLineRec.Type::Damage);
                OrderCostLineRec.VALIDATE(Quantity, OrderLineRec."Quantity Damaged");
                OrderCostLineRec.VALIDATE("Plant Type", OrderLineRec."Plant Type");
                OrderCostLineRec.VALIDATE("Plant Group", OrderLineRec."From Plant Group");
                IF (OrderLineRec."No." <> '') AND (OrderLineRec."No." <> '0') THEN
                  OrderCostLineRec.VALIDATE("Plant No.", OrderLineRec."No.");
                OrderCostLineRec."Plant Order Line No." := OrderLineRec."Line No.";  //DP00241.n (C004697)
                OrderCostLineRec.Element := OrderLineRec.Element;
                OrderCostLineRec.INSERT;
                TotalDamage := TotalDamage + OrderCostLineRec.Amount;
              END;  //DP00241.n (C004697)
            END;    //DP00241.n (C004697)
          UNTIL OrderLineRec.NEXT = 0;

        IF PlantLocRec."Damage Settlement" AND (TotalDamage <> 0) THEN BEGIN
          LineCounter := LineCounter + 1;
          //LineNo := LineNo + 10000;  //DP00241.o (C004697)
          //DP00241.sn (C004697)
          OrderCostLine2.RESET;
          OrderCostLine2.SETRANGE("Plant Order No.", "No.");
          IF OrderCostLine2.FINDLAST THEN
            LineNo := OrderCostLine2."Line No." + 10000
          ELSE
            LineNo := 10000;
          //DP00241.en (C004697)

          OrderCostLineRec.INIT;
          OrderCostLineRec."Plant Order No." := "No.";
          OrderCostLineRec."Line No." := LineNo;
          OrderCostLineRec.InitRecord;
          OrderCostLineRec.VALIDATE(Type, OrderCostLineRec.Type::"Sundry Costs");
          OrderCostLineRec.VALIDATE(Quantity, 1);
          OrderCostLineRec.VALIDATE(Amount, -TotalDamage);
          OrderCostLineRec.INSERT;
        END;

        COMMIT;
        Window.CLOSE;
        IF LineCounter = 0 THEN
          MESSAGE(Text008)
        ELSE
        //  MESSAGE(Text003, LineCounter, OrderCostLineRec.TABLECAPTION);  //DP00241.o (C004697)
          MESSAGE(Text009, Counter2);  //DP00241.n (C004697)
      END;
    END;

    PROCEDURE InsertItemInventSelectionOnPO@1100525001(VAR Rec@1100525000 : Record 11012556);
    VAR
      PlantOrderLine@1100525001 : Record 11012557;
      TmpPlantLocItemInvent@1100525002 : TEMPORARY Record 11020555;
      PlantLocItemInventFrm@1100525003 : Page 11071772;
    BEGIN
      //* 23818
      OrderRec.COPY(Rec);
      WITH OrderRec DO BEGIN
        TESTFIELD("No.");
        TESTFIELD(Posted, FALSE);
        IF (Status >= Status::Released) THEN
          FIELDERROR(Status);
        TESTFIELD(Type, Type::Removal);
        TESTFIELD("From Location");
        TESTFIELD("Transfer Date");

        PlantSetupRec.GET;

        PlantLocItemInventFrm.SetSelections("From Location","Transfer Date");
        PlantLocItemInventFrm.SetMultiSelection();
        PlantLocItemInventFrm.LOOKUPMODE(TRUE);
        IF PlantLocItemInventFrm.RUNMODAL <> ACTION::LookupOK THEN
          EXIT;
        IF NOT PlantLocItemInventFrm.GetMarkedRecords(TmpPlantLocItemInvent) THEN
          EXIT;

        TmpPlantLocItemInvent.RESET;
        IF TmpPlantLocItemInvent.FINDSET THEN BEGIN
          PlantOrderLine.RESET;
          PlantOrderLine.SETRANGE("Plant Order No.", "No.");
          IF PlantOrderLine.FINDLAST THEN
            LineNo := PlantOrderLine."Line No."
          ELSE
            LineNo := 0;
          REPEAT
            LineNo := LineNo + 10000;
            PlantOrderLine.INIT;
            PlantOrderLine."Plant Order No." := "No.";
            PlantOrderLine."Line No." := LineNo;
            PlantOrderLine.Type := PlantOrderLine.Type::Item;
            PlantOrderLine.VALIDATE("Item No.", TmpPlantLocItemInvent."Item No.");
            IF TmpPlantLocItemInvent."Variant Code" <> '' THEN
              PlantOrderLine.VALIDATE("Variant Code", TmpPlantLocItemInvent."Variant Code");
            PlantOrderLine.VALIDATE("Unit of Measure", TmpPlantLocItemInvent."Unit of Measure");
            IF TmpPlantLocItemInvent."Plant Group" <> '' THEN
              PlantOrderLine.VALIDATE("From Plant Group", TmpPlantLocItemInvent."Plant Group");
            IF PlantSetupRec."Removal with Qty. to Receive" THEN
              PlantOrderLine."Qty. to Receive" := TmpPlantLocItemInvent.Quantity;
            IF (NOT PlantSetupRec."Removal with Qty. to Receive") OR
               (PlantSetupRec."Removal with Qty. to Receive" AND PlantSetupRec."Fill Qty. with Qty. to Receive")
            THEN
              PlantOrderLine.VALIDATE(Quantity, TmpPlantLocItemInvent.Quantity);
            PlantOrderLine.INSERT(TRUE);
          UNTIL TmpPlantLocItemInvent.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InsertPlantOrderOnChargePurch@1100409000(VAR PlantOrderNo@1100409004 : Code[20];PlantLoc@1100409003 : Code[20];PlantLedgEntry@1100409002 : Record 11012572;PlantCostComp@1100409007 : Record 11012575);
    VAR
      PlantOrder@1100409000 : Record 11012556;
      CostLine@1100409001 : Record 11012558;
      DimVal@1100409013 : Record 349;
      DimMgt@1100409012 : Codeunit 408;
      PostingDate@1100409014 : Date;
      NextLineNo@1100409005 : Integer;
    BEGIN
      //*C001519
      PlantCostComp.TESTFIELD("Charge Purchase", TRUE);
      PlantCostComp.TESTFIELD("Cost Object Charge Purchase");
      DimMgt.GetDimValueRec(2, PlantCostComp."Cost Object Charge Purchase", DimVal, TRUE, '');
      //DimVal.TESTFIELD(Plant, DimVal.Plant::Sundry);  //C018429.o
      DimVal.TESTFIELD(Plant);  //C018429.n

      PostingDate := PlantLedgEntry."Posting Date";
      NextLineNo := 10000;
      IF PlantOrderNo <> '' THEN BEGIN
        PlantOrder.GET(PlantOrderNo);
        PlantOrder.TESTFIELD("To Location", PlantLoc);
        IF PostingDate = 0D THEN
          PostingDate := PlantOrder."Transfer Date";
        CostLine.SETRANGE("Plant Order No.", PlantOrder."No.");
        IF CostLine.FINDLAST THEN
          NextLineNo := CostLine."Line No." + 10000;
      END ELSE BEGIN
        IF PostingDate = 0D THEN
          PostingDate := WORKDATE;
        PlantOrder.INIT;
        PlantOrder."No." := '';
        PlantOrder.INSERT(TRUE);
        PlantOrder.TESTFIELD("No.");
        PlantOrder.VALIDATE(Type, PlantOrder.Type::Arrival);
        PlantOrder.VALIDATE("Transfer Date", PostingDate);
        PlantOrder."Def. Transfer" := PlantOrder."Transfer Date";  //C005327.n
        PlantOrder.VALIDATE("To Location", PlantLoc);
        PlantOrder.MODIFY(FALSE); //False, Do not add location related std. cost lines
        PlantOrderNo := PlantOrder."No.";
      END;

      CostLine.INIT;
      CostLine."Plant Order No." := PlantOrder."No.";
      CostLine.InitRecord;
      CostLine."Line No." := NextLineNo;
      //CostLine.Type := CostLine.Type::"Sundry Costs";  //C018429.o
      //C018429.sn
      CASE DimVal.Plant OF
        DimVal.Plant::Hours:
          CostLine.Type := CostLine.Type::Hours;
        DimVal.Plant::Transport:
          CostLine.Type := CostLine.Type::Transport;
        DimVal.Plant::Sundry:
          CostLine.Type := CostLine.Type::"Sundry Costs";
        DimVal.Plant::Damage:
          CostLine.Type := CostLine.Type::Damage;
        ELSE
          DimVal.FIELDERROR(Plant);
      END;
      //C018429.sn
      CostLine."No." := PlantCostComp."Cost Object Charge Purchase";
      CostLine."Plant Type" := PlantLedgEntry."Plant Type";
      CostLine."Plant No." := PlantLedgEntry."Plant No.";
      CostLine.TESTFIELD("Plant Type");  //Will always be filled id Cost Component for charging is filled
      CostLine.Description := PlantLedgEntry.Description;
      CostLine."Description 2" := PlantLedgEntry."Description 2";
      CostLine.Date := PostingDate;
      CostLine."Unit of Measure" := PlantLedgEntry."Unit of Measure Code";
      CostLine.Quantity := PlantLedgEntry.Quantity;
      IF (CostLine.Quantity = 0) AND (PlantLedgEntry."Total Cost" <> 0) THEN
        CostLine.Quantity := 1;
      CostLine.VALIDATE(Amount, PlantLedgEntry."Total Cost");
      CostLine.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

