OBJECT Codeunit 99000778 OrderTrackingManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 37=r,
                TableData 39=r,
                TableData 99000799=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=DatensÑtze werden gezÑhlt...;ENU=Counting records...;NLD=Tellen records...;NOR=Teller poster...;SVE=RÑknar poster...';
      Text003@1001 : TextConst 'DEU=AKTUELLE ZEILE;ENU=CURRENT LINE;NLD=HUIDIGE REGEL;NOR=GJELDENDE LINJE;SVE=AKTUELL RAD';
      Text004@1002 : TextConst 'DEU=STORNIERUNG;ENU=CANCELLATION;NLD=ANNULERING;NOR=AVBRYTELSE;SVE=ANNULLERING';
      Text005@1003 : TextConst 'DEU="OHNE VERWENDUNGSNACHWEIS ";ENU="NON-PEGGED ";NLD="NIET-GETRACEERD ";NOR="IKKETILKN. ";SVE="EJ-BEHOVHé "';
      Text006@1004 : TextConst 'DEU=LIEFERUNG;ENU=SHIPMENT;NLD=VERZENDING;NOR=LEVERING;SVE=UTLEVERANS';
      Text007@1005 : TextConst 'DEU=LIEFERUNG;ENU=RECEIPT;NLD=ONTVANGST;NOR=MOTTAK;SVE=INLEVERANS';
      Text008@1006 : TextConst 'DEU=Es gibt keine Bedarfsverursacherposten fÅr diese Zeile.;ENU=There are no order tracking entries for this line.;NLD=Er zijn geen ordertraceringsposten voor deze regel.;NOR=Det finnes ingen ordresporingsposter for denne linjen.;SVE=Det finns inga orderspÜrningstransaktioner fîr raden.';
      Text009@1007 : TextConst 'DEU=Die Bedarfsverursacherposten fÅr diese Zeile weisen einen Datumskonflikt auf.;ENU=The order tracking entries for this line have a date conflict.;NLD=De ordertraceringsposten voor deze regel hebben een datumconflict.;NOR=Det finnes en datokonflikt i ordresporingspostene for denne linjen.;SVE=OrderspÜrningstrans. fîr raden har en datumkonflikt.';
      ItemLedgEntry@1008 : Record 32;
      ItemLedgEntry2@1009 : Record 32;
      ItemLedgEntry3@1010 : Record 32;
      SalesLine@1011 : Record 37;
      PurchLine@1012 : Record 39;
      ItemJnlLine@1013 : Record 83;
      ReqLine@1014 : Record 246;
      ProdOrderLine@1015 : Record 5406;
      ProdOrderComp@1016 : Record 5407;
      AsmHeader@1051 : Record 900;
      AsmLine@1052 : Record 901;
      PlanningComponent@1017 : Record 99000829;
      ServLine@1018 : Record 5902;
      JobPlanningLine@1049 : Record 11020401;
      ReservEntry@1019 : Record 337;
      TempReservEntryList@1020 : TEMPORARY Record 337;
      TempOrderTrackingEntry@1022 : TEMPORARY Record 99000799;
      CreateReservEntry@1023 : Codeunit 99000830;
      ReservEngineMgt@1024 : Codeunit 99000831;
      ReserveSalesLine@1025 : Codeunit 99000832;
      ReserveReqLine@1026 : Codeunit 99000833;
      ReservePurchLine@1027 : Codeunit 99000834;
      ReserveProdOrderLine@1029 : Codeunit 99000837;
      ReserveProdOrderComp@1030 : Codeunit 99000838;
      AsmHeaderReserve@1053 : Codeunit 925;
      AsmLineReserve@1054 : Codeunit 926;
      ReservePlanningComponent@1031 : Codeunit 99000840;
      ReserveItemLedgEntry@1032 : Codeunit 99000841;
      ServLineReserve@1033 : Codeunit 99000842;
      JobPlanningLineReserve@1050 : Codeunit 1032;
      CaptionText@1034 : Text[80];
      Type@1036 : ' ,Sales,Req. Line,Purchase,Item Jnl,BOM Jnl,Item Ledg. Entry,Prod. Order Line,Prod. Order Comp.,Planning Line,Planning Comp.,Transfer,Service Order';
      ID@1037 : Code[20];
      BatchName@1038 : Code[20];
      Subtype@1039 : Integer;
      ProdOrderLineNo@1040 : Integer;
      RefNo@1041 : Integer;
      EntryNo@1042 : Integer;
      MultipleSummedUpQty@1048 : Decimal;
      SearchUp@1043 : Boolean;
      IsPlanning@1045 : Boolean;
      DateWarning@1046 : Boolean;
      SearchUpIsSet@1047 : Boolean;
      MultipleItemLedgEntries@1028 : Boolean;

    [External]
    PROCEDURE IsSearchUp@11() : Boolean;
    BEGIN
      EXIT(SearchUp);
    END;

    [External]
    PROCEDURE GetCaption@7() : Text[80];
    BEGIN
      EXIT(CaptionText);
    END;

    [External]
    PROCEDURE SetSalesLine@24(VAR CurrentSalesLine@1000 : Record 37);
    VAR
      SaleShptLine@1001 : Record 111;
    BEGIN
      CurrentSalesLine.TESTFIELD(Type,CurrentSalesLine.Type::Item);
      SalesLine := CurrentSalesLine;
      ReservEntry."Source Type" := DATABASE::"Sales Line";

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReserveSalesLine.FilterReservFor(ReservEntry,SalesLine);

      CaptionText := ReserveSalesLine.Caption(SalesLine);

      IF CurrentSalesLine."Qty. Shipped (Base)" <> 0 THEN BEGIN
        SaleShptLine.SETCURRENTKEY("Order No.","Order Line No.");
        SaleShptLine.SETRANGE("Order No.",CurrentSalesLine."Document No.");
        SaleShptLine.SETRANGE("Order Line No.",CurrentSalesLine."Line No.");
        IF SaleShptLine.FIND('-') THEN
          REPEAT
            IF ItemLedgEntry2.GET(SaleShptLine."Item Shpt. Entry No.") THEN
              ItemLedgEntry2.MARK(TRUE);
          UNTIL SaleShptLine.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE SetReqLine@23(VAR CurrentReqLine@1000 : Record 246);
    BEGIN
      ReqLine := CurrentReqLine;
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReserveReqLine.FilterReservFor(ReservEntry,ReqLine);
      CaptionText := ReserveReqLine.Caption(ReqLine);

      IsPlanning := ReqLine."Planning Line Origin" <> ReqLine."Planning Line Origin"::" ";
    END;

    [External]
    PROCEDURE SetPurchLine@22(VAR CurrentPurchLine@1000 : Record 39);
    VAR
      PurchRcptLine@1001 : Record 121;
    BEGIN
      CurrentPurchLine.TESTFIELD(Type,CurrentPurchLine.Type::Item);
      PurchLine := CurrentPurchLine;

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReservePurchLine.FilterReservFor(ReservEntry,PurchLine);
      CaptionText := ReservePurchLine.Caption(PurchLine);

      IF CurrentPurchLine."Qty. Received (Base)" <> 0 THEN BEGIN
        PurchRcptLine.SETCURRENTKEY("Order No.","Order Line No.");
        PurchRcptLine.SETRANGE("Order No.",CurrentPurchLine."Document No.");
        PurchRcptLine.SETRANGE("Order Line No.",CurrentPurchLine."Line No.");
        IF PurchRcptLine.FIND('-') THEN
          REPEAT
            IF ItemLedgEntry2.GET(PurchRcptLine."Item Rcpt. Entry No.") THEN
              ItemLedgEntry2.MARK(TRUE);
          UNTIL PurchRcptLine.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE SetProdOrderLine@19(VAR CurrentProdOrderLine@1000 : Record 5406);
    BEGIN
      ProdOrderLine := CurrentProdOrderLine;

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReserveProdOrderLine.FilterReservFor(ReservEntry,ProdOrderLine);
      CaptionText := ReserveProdOrderLine.Caption(ProdOrderLine);

      IF CurrentProdOrderLine."Finished Quantity" <> 0 THEN BEGIN
        ItemLedgEntry2.SETCURRENTKEY("Order Type","Order No.","Order Line No.","Entry Type");
        ItemLedgEntry2.SETRANGE("Order Type",ItemLedgEntry2."Order Type"::Production);
        ItemLedgEntry2.SETRANGE("Order No.",CurrentProdOrderLine."Prod. Order No.");
        ItemLedgEntry2.SETRANGE("Order Line No.",CurrentProdOrderLine."Line No.");
        ItemLedgEntry2.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Output);
        ItemLedgEntry2.SETRANGE("Item No.",CurrentProdOrderLine."Item No.");

        IF ItemLedgEntry2.FIND('-') THEN
          REPEAT
            ItemLedgEntry2.MARK(TRUE);
          UNTIL ItemLedgEntry2.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE SetProdOrderComp@18(VAR CurrentProdOrderComp@1000 : Record 5407);
    BEGIN
      ProdOrderComp := CurrentProdOrderComp;

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReserveProdOrderComp.FilterReservFor(ReservEntry,ProdOrderComp);
      CaptionText := ReserveProdOrderComp.Caption(ProdOrderComp);

      IF (CurrentProdOrderComp."Remaining Quantity" <> CurrentProdOrderComp."Expected Quantity") AND
         (CurrentProdOrderComp.Status IN
          [CurrentProdOrderComp.Status::Released,CurrentProdOrderComp.Status::Finished])
      THEN BEGIN
        ProdOrderLine.GET(
          CurrentProdOrderComp.Status,
          CurrentProdOrderComp."Prod. Order No.",
          CurrentProdOrderComp."Prod. Order Line No.");

        ItemLedgEntry2.SETCURRENTKEY("Order Type","Order No.","Order Line No.","Entry Type","Prod. Order Comp. Line No.");
        ItemLedgEntry2.SETRANGE("Order Type",ItemLedgEntry2."Order Type"::Production);
        ItemLedgEntry2.SETRANGE("Order No.",CurrentProdOrderComp."Prod. Order No.");
        ItemLedgEntry2.SETRANGE("Order Line No.",CurrentProdOrderComp."Prod. Order Line No.");
        ItemLedgEntry2.SETRANGE("Prod. Order Comp. Line No.",CurrentProdOrderComp."Line No.");
        ItemLedgEntry2.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Consumption);
        ItemLedgEntry2.SETRANGE("Item No.",CurrentProdOrderComp."Item No.");
        IF ItemLedgEntry2.FIND('-') THEN
          REPEAT
            ItemLedgEntry2.MARK(TRUE);
          UNTIL ItemLedgEntry2.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE SetAsmHeader@21(VAR CurrentAsmHeader@1000 : Record 900);
    BEGIN
      AsmHeader := CurrentAsmHeader;

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      AsmHeaderReserve.FilterReservFor(ReservEntry,AsmHeader);
      CaptionText := AsmHeaderReserve.Caption(AsmHeader);

      IF CurrentAsmHeader."Assembled Quantity (Base)" <> 0 THEN BEGIN
        ItemLedgEntry2.SETCURRENTKEY("Order Type","Order No.");
        ItemLedgEntry2.SETRANGE("Order Type",ItemLedgEntry2."Order Type"::Assembly);
        ItemLedgEntry2.SETRANGE("Order No.",CurrentAsmHeader."No.");
        ItemLedgEntry2.SETRANGE("Order Line No.",0);
        IF ItemLedgEntry2.FIND('-') THEN
          REPEAT
            ItemLedgEntry2.MARK(TRUE);
          UNTIL ItemLedgEntry2.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE SetAsmLine@25(VAR CurrentAsmLine@1000 : Record 901);
    BEGIN
      AsmLine := CurrentAsmLine;

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      AsmLineReserve.FilterReservFor(ReservEntry,AsmLine);
      CaptionText := AsmLineReserve.Caption(AsmLine);

      IF CurrentAsmLine."Consumed Quantity (Base)" <> 0 THEN BEGIN
        ItemLedgEntry2.SETCURRENTKEY("Order Type","Order No.");
        ItemLedgEntry2.SETRANGE("Order Type",ItemLedgEntry."Order Type"::Assembly);
        ItemLedgEntry2.SETRANGE("Order No.",CurrentAsmLine."No.");
        ItemLedgEntry2.SETRANGE("Order Line No.",CurrentAsmLine."Line No.");
        IF ItemLedgEntry2.FIND('-') THEN
          REPEAT
            ItemLedgEntry2.MARK(TRUE);
          UNTIL ItemLedgEntry2.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE SetPlanningComponent@6(VAR CurrentPlanningComponent@1000 : Record 99000829);
    BEGIN
      PlanningComponent := CurrentPlanningComponent;

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReservePlanningComponent.FilterReservFor(ReservEntry,PlanningComponent);
      CaptionText := ReservePlanningComponent.Caption(PlanningComponent);
      IsPlanning := TRUE;
    END;

    [External]
    PROCEDURE SetItemLedgEntry@10(VAR CurrentItemLedgEntry@1000 : Record 32);
    BEGIN
      ItemLedgEntry := CurrentItemLedgEntry;

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReserveItemLedgEntry.FilterReservFor(ReservEntry,ItemLedgEntry);
      CaptionText := ReserveItemLedgEntry.Caption(ItemLedgEntry);
      ItemLedgEntry2 := ItemLedgEntry;
      ItemLedgEntry2.MARK(TRUE);
    END;

    [External]
    PROCEDURE SetMultipleItemLedgEntries@16(VAR TempItemLedgEntry@1007 : TEMPORARY Record 32;SourceType@1006 : Integer;SourceSubtype@1005 : Integer;SourceID@1004 : Code[20];SourceBatchName@1003 : Code[10];SourceProdOrderLine@1002 : Integer;SourceRefNo@1000 : Integer);
    VAR
      ItemTrackingMgt@1001 : Codeunit 6500;
    BEGIN
      // Used from posted shipment and receipt with item tracking.

      ItemTrackingMgt.CollectItemEntryRelation(TempItemLedgEntry,SourceType,SourceSubtype,SourceID,
        SourceBatchName,SourceProdOrderLine,SourceRefNo,0);

      TempItemLedgEntry.SETFILTER("Remaining Quantity",'<>%1',0);
      IF NOT TempItemLedgEntry.FIND('-') THEN
        EXIT;

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReserveItemLedgEntry.FilterReservFor(ReservEntry,TempItemLedgEntry);
      CaptionText := ReserveItemLedgEntry.Caption(TempItemLedgEntry);

      REPEAT
        ItemLedgEntry2 := TempItemLedgEntry;
        ItemLedgEntry2.MARK(TRUE);
        MultipleSummedUpQty += TempItemLedgEntry."Remaining Quantity";
      UNTIL TempItemLedgEntry.NEXT = 0;

      MultipleItemLedgEntries := (TempItemLedgEntry.COUNT > 1);
    END;

    [External]
    PROCEDURE SetServLine@15(VAR CurrentServLine@1000 : Record 5902);
    VAR
      ServShptLine@1001 : Record 5991;
    BEGIN
      CurrentServLine.TESTFIELD(Type,CurrentServLine.Type::Item);
      ServLine := CurrentServLine;
      ReservEntry."Source Type" := DATABASE::"Service Line";

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ServLineReserve.FilterReservFor(ReservEntry,ServLine);

      CaptionText := ServLineReserve.Caption(ServLine);

      IF CurrentServLine."Qty. Shipped (Base)" <> 0 THEN BEGIN
        ServShptLine.SETCURRENTKEY("Order No.","Order Line No.");
        ServShptLine.SETRANGE("Order No.",CurrentServLine."Document No.");
        ServShptLine.SETRANGE("Order Line No.",CurrentServLine."Line No.");
        IF ServShptLine.FIND('-') THEN
          REPEAT
            IF ItemLedgEntry2.GET(ServShptLine."Item Shpt. Entry No.") THEN
              ItemLedgEntry2.MARK(TRUE);
          UNTIL ServShptLine.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE SetJobPlanningLine@20(VAR CurrentJobPlanningLine@1000 : Record 11020401);
    VAR
      JobUsageLink@1002 : Record 1020;
      JobLedgEntry@1001 : Record 11072005;
    BEGIN
      CurrentJobPlanningLine.TESTFIELD(Type,CurrentJobPlanningLine.Type::Item);
      JobPlanningLine := CurrentJobPlanningLine;
      ReservEntry."Source Type" := DATABASE::"Job Planning Line";

      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      JobPlanningLineReserve.FilterReservFor(ReservEntry,JobPlanningLine);

      CaptionText := JobPlanningLineReserve.Caption(JobPlanningLine);

      IF CurrentJobPlanningLine."Qty. Posted" <> 0 THEN BEGIN
        JobUsageLink.SETRANGE("Job No.",CurrentJobPlanningLine."Job No.");
        JobUsageLink.SETRANGE("Job Task No.",CurrentJobPlanningLine."Job Task No.");
        JobUsageLink.SETRANGE("Line No.",CurrentJobPlanningLine."Line No.");
        IF JobUsageLink.FIND('-') THEN
          REPEAT
            JobLedgEntry.GET(JobUsageLink."Entry No.");
            IF ItemLedgEntry2.GET(JobLedgEntry."Ledger Entry No.") THEN
              ItemLedgEntry2.MARK(TRUE);
          UNTIL JobUsageLink.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE TrackedQuantity@4() : Decimal;
    VAR
      FilterReservEntry@1000 : Record 337;
      QtyTracked1@1001 : Decimal;
      QtyTracked2@1002 : Decimal;
    BEGIN
      IF MultipleItemLedgEntries THEN
        EXIT(MultipleSummedUpQty);
      ReservEntry.SETRANGE("Reservation Status",ReservEntry."Reservation Status"::Reservation,
        ReservEntry."Reservation Status"::Tracking);
      IF ReservEntry.FIND('-') THEN
        REPEAT
          QtyTracked1 += ReservEntry."Quantity (Base)";
        UNTIL ReservEntry.NEXT = 0;
      IF IsPlanning THEN
        IF DerivePlanningFilter(ReservEntry,FilterReservEntry) THEN BEGIN
          FilterReservEntry.SETRANGE("Reservation Status",FilterReservEntry."Reservation Status"::Reservation,
            FilterReservEntry."Reservation Status"::Tracking);
          IF FilterReservEntry.FIND('-') THEN
            REPEAT
              QtyTracked2 += FilterReservEntry."Quantity (Base)";
            UNTIL FilterReservEntry.NEXT = 0;
          EXIT((QtyTracked1 + QtyTracked2) * CreateReservEntry.SignFactor(FilterReservEntry));
        END;
      EXIT(QtyTracked1 * CreateReservEntry.SignFactor(ReservEntry));
    END;

    [External]
    PROCEDURE FindRecord@9(Which@1000 : Text[250];VAR OrderTrackingEntry2@1001 : Record 99000799) : Boolean;
    BEGIN
      TempOrderTrackingEntry := OrderTrackingEntry2;
      IF NOT TempOrderTrackingEntry.FIND(Which) THEN
        EXIT(FALSE);
      OrderTrackingEntry2 := TempOrderTrackingEntry;
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE GetNextRecord@8(Steps@1001 : Integer;VAR OrderTrackingEntry2@1002 : Record 99000799) CurrentSteps@1000 : Integer;
    BEGIN
      TempOrderTrackingEntry := OrderTrackingEntry2;
      CurrentSteps := TempOrderTrackingEntry.NEXT(Steps);
      IF CurrentSteps <> 0 THEN
        OrderTrackingEntry2 := TempOrderTrackingEntry;
      EXIT(CurrentSteps);
    END;

    [External]
    PROCEDURE FindRecords@2() : Boolean;
    BEGIN
      EXIT(FindRecordsInner(FALSE));
    END;

    [External]
    PROCEDURE FindRecordsWithoutMessage@30() : Boolean;
    BEGIN
      EXIT(FindRecordsInner(TRUE));
    END;

    LOCAL PROCEDURE FindRecordsInner@32(SuppressMessages@1001 : Boolean) TrackingExists@1000 : Boolean;
    VAR
      Window@1021 : Dialog;
    BEGIN
      TempReservEntryList.DELETEALL;
      TempOrderTrackingEntry.DELETEALL;
      EntryNo := 1;

      WITH TempOrderTrackingEntry DO BEGIN
        IF NOT SuppressMessages THEN
          Window.OPEN(Text000);
        INIT;
        "Entry No." := 0;
        DrillOrdersUp(ReservEntry,1);
        ItemLedgEntry2.SETCURRENTKEY("Entry No.");
        ItemLedgEntry2.MARKEDONLY(TRUE);
        IF ItemLedgEntry2.FIND('-') THEN
          REPEAT
            InsertItemLedgTrackEntry(1,ItemLedgEntry2,ItemLedgEntry2."Remaining Quantity",ItemLedgEntry2);
          UNTIL ItemLedgEntry2.NEXT = 0;
        TrackingExists := FIND('-');
        IF NOT TrackingExists AND NOT SuppressMessages THEN
          MESSAGE(Text008);
        IF DateWarning AND NOT SuppressMessages THEN
          MESSAGE(Text009);
        IF NOT SuppressMessages THEN
          Window.CLOSE;
      END;
      EXIT(TrackingExists);
    END;

    LOCAL PROCEDURE DrillOrdersUp@5(VAR ReservEntry@1000 : Record 337;Level@1001 : Integer);
    VAR
      ReservEntry2@1002 : Record 337;
      ReservEntry3@1003 : Record 337;
      FilterReservEntry@1004 : Record 337;
      ContinueDrillUp@1005 : Boolean;
      IncludePlanningFilter@1006 : Boolean;
    BEGIN
      IF Level > 20 THEN
        EXIT;

      ReservEntry.SETRANGE("Reservation Status",ReservEntry."Reservation Status"::Reservation,
        ReservEntry."Reservation Status"::Tracking);

      IF ReservEntry.FIND('-') THEN BEGIN
        IF Level = 1 THEN
          IF NOT SearchUpIsSet THEN BEGIN
            SearchUp := NOT ReservEntry.Positive;
            SearchUpIsSet := TRUE;
          END;

        REPEAT
          ReservEntry3.GET(ReservEntry."Entry No.",NOT ReservEntry.Positive);
          InsertOrderTrackingEntry(ReservEntry,ReservEntry3,Level);
          WITH ReservEntry3 DO
            CASE "Source Type" OF
              DATABASE::"Item Ledger Entry":
                BEGIN
                  ItemLedgEntry3.GET("Source Ref. No.");
                  DrillItemLedgEntries(Level + 1,ItemLedgEntry3);
                END;
              DATABASE::"Prod. Order Component",
              DATABASE::"Planning Component":
                BEGIN
                  FiltersForTrackingFromComponents(ReservEntry3,ReservEntry2);
                  DrillOrdersUp(ReservEntry2,Level + 1);
                  IF DerivePlanningFilter(ReservEntry3,FilterReservEntry) THEN
                    DrillOrdersUp(FilterReservEntry,Level + 1);
                END;
              DATABASE::"Prod. Order Line",
              DATABASE::"Requisition Line":
                BEGIN
                  FiltersForTrackingFromReqLine(ReservEntry3,ReservEntry2,SearchUp);
                  DrillOrdersUp(ReservEntry2,Level + 1);
                  IF DerivePlanningFilter(ReservEntry3,FilterReservEntry) THEN
                    DrillOrdersUp(FilterReservEntry,Level + 1);
                END;
              DATABASE::"Transfer Line":
                BEGIN
                  FiltersForTrackingFromTransfer(ReservEntry3,ReservEntry2,SearchUp);
                  DrillOrdersUp(ReservEntry2,Level + 1);
                  IF DerivePlanningFilter(ReservEntry3,FilterReservEntry) THEN
                    DrillOrdersUp(FilterReservEntry,Level + 1);
                END;
              ELSE BEGIN
                OnDrillOrdersUpCaseElse(ReservEntry3,ReservEntry2,SearchUp,ContinueDrillUp,IncludePlanningFilter);
                IF ContinueDrillUp THEN
                  DrillOrdersUp(ReservEntry2,Level + 1);
                IF IncludePlanningFilter THEN
                  IF DerivePlanningFilter(ReservEntry3,FilterReservEntry) THEN
                    DrillOrdersUp(FilterReservEntry,Level + 1);
              END;
            END;
        UNTIL ReservEntry.NEXT = 0;
      END;

      IF Level = 1 THEN
        IF DerivePlanningFilter(ReservEntry,FilterReservEntry) THEN BEGIN
          IF NOT SearchUpIsSet THEN BEGIN
            SearchUp := NOT FilterReservEntry.Positive;
            SearchUpIsSet := TRUE;
          END;
          DrillOrdersUp(FilterReservEntry,Level);
        END;
    END;

    LOCAL PROCEDURE FiltersForTrackingFromComponents@26(FromReservationEntry@1000 : Record 337;VAR ToReservationEntry@1001 : Record 337);
    BEGIN
      WITH FromReservationEntry DO BEGIN
        ToReservationEntry.RESET;
        IF "Source Type" = DATABASE::"Prod. Order Component" THEN BEGIN
          ToReservationEntry.SetSourceFilter(DATABASE::"Prod. Order Line","Source Subtype","Source ID",-1,TRUE);
          ToReservationEntry.SetSourceFilter2("Source Batch Name","Source Prod. Order Line");
        END ELSE BEGIN
          ToReservationEntry.SetSourceFilter(DATABASE::"Requisition Line",0,"Source ID","Source Prod. Order Line",TRUE);
          ToReservationEntry.SetSourceFilter2("Source Batch Name",0);
        END;
      END;
    END;

    LOCAL PROCEDURE FiltersForTrackingFromReqLine@27(FromReservationEntry@1000 : Record 337;VAR ToReservationEntry@1001 : Record 337;IsSearchUp@1002 : Boolean);
    VAR
      RequisitionLine@1003 : Record 246;
    BEGIN
      WITH FromReservationEntry DO BEGIN
        ToReservationEntry.RESET;
        IF "Source Type" = DATABASE::"Prod. Order Line" THEN BEGIN
          ToReservationEntry.SetSourceFilter(DATABASE::"Prod. Order Component","Source Subtype","Source ID",-1,TRUE);
          ToReservationEntry.SetSourceFilter2("Source Batch Name","Source Ref. No.");
        END ELSE BEGIN
          RequisitionLine.GET("Source ID","Source Batch Name","Source Ref. No.");
          IF RequisitionLine."Replenishment System" = RequisitionLine."Replenishment System"::Transfer THEN BEGIN
            IF IsSearchUp THEN
              ToReservationEntry.SetSourceFilter(DATABASE::"Requisition Line",1,"Source ID","Source Ref. No.",TRUE)
            ELSE
              ToReservationEntry.SetSourceFilter(DATABASE::"Requisition Line",0,"Source ID","Source Ref. No.",TRUE);
            ToReservationEntry.SetSourceFilter2("Source Batch Name",0);
          END ELSE BEGIN
            ToReservationEntry.SetSourceFilter(DATABASE::"Planning Component",0,"Source ID",-1,TRUE);
            ToReservationEntry.SetSourceFilter2("Source Batch Name","Source Ref. No.");
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE FiltersForTrackingFromTransfer@28(FromReservationEntry@1000 : Record 337;VAR ToReservationEntry@1001 : Record 337;IsSearchUp@1002 : Boolean);
    BEGIN
      WITH FromReservationEntry DO BEGIN
        ToReservationEntry.RESET;
        IF IsSearchUp THEN
          ToReservationEntry.SetSourceFilter("Source Type",0,"Source ID","Source Ref. No.",TRUE)
        ELSE
          ToReservationEntry.SetSourceFilter("Source Type",1,"Source ID","Source Ref. No.",TRUE);
        ToReservationEntry.SETRANGE("Source Batch Name","Source Batch Name");
      END;
    END;

    LOCAL PROCEDURE DrillItemLedgEntries@12(Level@1000 : Integer;ItemLedgEntry4@1001 : Record 32);
    VAR
      ItemLedgEntry5@1002 : Record 32;
      ItemLedgEntry6@1003 : Record 32;
      ItemApplnEntry@1004 : Record 339;
      SignFactor@1005 : Integer;
    BEGIN
      IF Level > 20 THEN
        EXIT;

      IF ItemLedgEntry4.Positive THEN BEGIN
        ItemApplnEntry.SETCURRENTKEY("Inbound Item Entry No.","Outbound Item Entry No.");
        ItemApplnEntry.SETRANGE("Inbound Item Entry No.",ItemLedgEntry4."Entry No.");
        ItemApplnEntry.SETFILTER("Outbound Item Entry No.",'<>0');
        ItemApplnEntry.SETRANGE("Item Ledger Entry No.");
      END ELSE BEGIN
        ItemApplnEntry.SETCURRENTKEY("Outbound Item Entry No.","Item Ledger Entry No.");
        ItemApplnEntry.SETRANGE("Outbound Item Entry No.",ItemLedgEntry4."Entry No.");
        ItemApplnEntry.SETRANGE("Item Ledger Entry No.",ItemLedgEntry4."Entry No.");
        ItemApplnEntry.SETRANGE("Inbound Item Entry No.");
      END;
      IF ItemApplnEntry.FIND('-') THEN
        REPEAT
          IF ItemLedgEntry4.Positive THEN BEGIN
            SignFactor := -1;
            ItemLedgEntry5.GET(ItemApplnEntry."Outbound Item Entry No.")
          END ELSE BEGIN
            SignFactor := 1;
            ItemLedgEntry5.GET(ItemApplnEntry."Inbound Item Entry No.");
          END;
          IF SearchUp = ItemLedgEntry5.Positive THEN BEGIN
            InsertItemLedgTrackEntry(Level,ItemLedgEntry5,ItemApplnEntry.Quantity * SignFactor,ItemLedgEntry4);
            IF (ItemLedgEntry5."Order Type" = ItemLedgEntry5."Order Type"::Production) AND
               (ItemLedgEntry5."Order No." <> '')
            THEN BEGIN
              ItemLedgEntry6.SETCURRENTKEY("Order Type","Order No.","Order Line No.","Entry Type");
              ItemLedgEntry6.SETRANGE("Source Type",ItemLedgEntry4."Source Type"::Item);
              IF ItemLedgEntry5."Entry Type" = ItemLedgEntry5."Entry Type"::Consumption THEN
                ItemLedgEntry6.SETRANGE("Entry Type",ItemLedgEntry4."Entry Type"::Output)
              ELSE
                ItemLedgEntry6.SETRANGE("Entry Type",ItemLedgEntry4."Entry Type"::Consumption);
              IF NOT SearchUp THEN
                ItemLedgEntry6.SETRANGE("Item No.",ItemLedgEntry5."Source No.")
              ELSE
                ItemLedgEntry6.SETRANGE("Item No.",ItemLedgEntry5."Item No.");
              ItemLedgEntry6.SETRANGE("Order Type",ItemLedgEntry6."Order Type"::Production);
              ItemLedgEntry6.SETRANGE("Order No.",ItemLedgEntry5."Order No.");
              IF ItemLedgEntry6.FIND('-') THEN
                REPEAT
                  InsertItemLedgTrackEntry(Level + 1,ItemLedgEntry6,ItemLedgEntry6.Quantity,ItemLedgEntry4);
                  DrillItemLedgEntries(Level + 1,ItemLedgEntry6);
                UNTIL ItemLedgEntry6.NEXT = 0;
            END;
          END;
        UNTIL ItemApplnEntry.NEXT = 0;

      IF (ItemLedgEntry4."Order Type" = ItemLedgEntry4."Order Type"::Production) AND (ItemLedgEntry4."Order No." <> '') THEN BEGIN
        ItemLedgEntry6.SETCURRENTKEY("Order Type","Order No.","Order Line No.","Entry Type");
        ItemLedgEntry6.SETRANGE("Source Type",ItemLedgEntry4."Source Type"::Item);
        IF ItemLedgEntry5."Entry Type" = ItemLedgEntry5."Entry Type"::Consumption THEN
          ItemLedgEntry6.SETRANGE("Entry Type",ItemLedgEntry4."Entry Type"::Output)
        ELSE
          ItemLedgEntry6.SETRANGE("Entry Type",ItemLedgEntry4."Entry Type"::Consumption);
        ItemLedgEntry6.SETRANGE("Order Type",ItemLedgEntry6."Order Type"::Production);
        ItemLedgEntry6.SETRANGE("Order No.",ItemLedgEntry5."Order No.");
        IF NOT SearchUp THEN
          ItemLedgEntry6.SETRANGE("Item No.",ItemLedgEntry5."Source No.")
        ELSE
          ItemLedgEntry6.SETRANGE("Item No.",ItemLedgEntry5."Item No.");
        IF ItemLedgEntry6.FIND('-') THEN
          REPEAT
            IF ItemLedgEntry4."Entry No." <> ItemLedgEntry6."Entry No." THEN BEGIN
              InsertItemLedgTrackEntry(Level + 1,ItemLedgEntry6,ItemLedgEntry6.Quantity,ItemLedgEntry4);
              DrillItemLedgEntries(Level + 1,ItemLedgEntry6);
            END;
          UNTIL ItemLedgEntry6.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertOrderTrackingEntry@3(VAR ReservEntry@1000 : Record 337;VAR ReservEntry2@1001 : Record 337;Level@1002 : Integer);
    BEGIN
      TempReservEntryList := ReservEntry;
      TempReservEntryList.Positive := FALSE;
      IF NOT TempReservEntryList.INSERT THEN
        EXIT;

      TempOrderTrackingEntry.RESET;
      TempOrderTrackingEntry.INIT;

      TempOrderTrackingEntry.Level := Level;
      TempOrderTrackingEntry."For Type" := ReservEntry."Source Type";
      TempOrderTrackingEntry."For Subtype" := ReservEntry."Source Subtype";
      TempOrderTrackingEntry."For ID" := ReservEntry."Source ID";
      TempOrderTrackingEntry."For Batch Name" := ReservEntry."Source Batch Name";
      TempOrderTrackingEntry."For Prod. Order Line" := ReservEntry."Source Prod. Order Line";
      TempOrderTrackingEntry."For Ref. No." := ReservEntry."Source Ref. No.";

      TempOrderTrackingEntry."From Type" := ReservEntry2."Source Type";
      TempOrderTrackingEntry."From Subtype" := ReservEntry2."Source Subtype";
      TempOrderTrackingEntry."From ID" := ReservEntry2."Source ID";
      TempOrderTrackingEntry."From Batch Name" := ReservEntry2."Source Batch Name";
      TempOrderTrackingEntry."From Prod. Order Line" := ReservEntry2."Source Prod. Order Line";
      TempOrderTrackingEntry."From Ref. No." := ReservEntry2."Source Ref. No.";

      IF ReservEntry."Expected Receipt Date" > ReservEntry."Shipment Date" THEN BEGIN
        TempOrderTrackingEntry.Warning := TRUE;
        DateWarning := TRUE;
      END;

      IF OrderTrackingEntryExists THEN BEGIN
        TempOrderTrackingEntry.Quantity += ReservEntry."Quantity (Base)";
        TempOrderTrackingEntry.MODIFY;
        EXIT;
      END;

      TempOrderTrackingEntry."Entry No." := EntryNo;
      TempOrderTrackingEntry."Demanded by" := ReservEngineMgt.CreateForText(ReservEntry);
      TempOrderTrackingEntry."Supplied by" := ReservEngineMgt.CreateFromText(ReservEntry);
      TempOrderTrackingEntry."Item No." := ReservEntry."Item No.";
      TempOrderTrackingEntry."Shipment Date" := ReservEntry."Shipment Date";
      TempOrderTrackingEntry."Expected Receipt Date" := ReservEntry."Expected Receipt Date";
      IF MultipleItemLedgEntries THEN
        TempOrderTrackingEntry.Quantity := MultipleSummedUpQty
      ELSE
        TempOrderTrackingEntry.Quantity := ReservEntry."Quantity (Base)";

      IF SearchUp THEN
        TempOrderTrackingEntry.Name := TempOrderTrackingEntry."Supplied by"
      ELSE
        TempOrderTrackingEntry.Name := TempOrderTrackingEntry."Demanded by";

      IF Level = 1 THEN
        IF SearchUp THEN
          TempOrderTrackingEntry."Demanded by" := Text003
        ELSE
          TempOrderTrackingEntry."Supplied by" := Text003;

      Type := TempOrderTrackingEntry."For Type";
      Subtype := TempOrderTrackingEntry."For Subtype";
      ID := TempOrderTrackingEntry."For ID";
      BatchName := TempOrderTrackingEntry."For Batch Name";
      ProdOrderLineNo := TempOrderTrackingEntry."For Prod. Order Line";
      RefNo := TempOrderTrackingEntry."For Ref. No.";

      CASE Type OF
        DATABASE::"Sales Line":
          IF SalesLine.GET(Subtype,ID,RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := SalesLine."Shipment Date";
            TempOrderTrackingEntry."Ending Date" := SalesLine."Shipment Date";
          END;
        DATABASE::"Purchase Line":
          IF PurchLine.GET(Subtype,ID,RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := PurchLine."Expected Receipt Date";
            TempOrderTrackingEntry."Ending Date" := PurchLine."Expected Receipt Date";
          END;
        DATABASE::"Requisition Line":
          IF ReqLine.GET(ID,BatchName,RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := ReqLine."Due Date";
            TempOrderTrackingEntry."Ending Date" := ReqLine."Due Date";
          END;
        DATABASE::"Item Journal Line":
          IF ItemJnlLine.GET(ID,BatchName,RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := ItemJnlLine."Posting Date";
            TempOrderTrackingEntry."Ending Date" := ItemJnlLine."Posting Date";
          END;
        DATABASE::"Item Ledger Entry":
          IF ItemLedgEntry.GET(RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := WORKDATE;
            TempOrderTrackingEntry."Ending Date" := WORKDATE;
          END;
        DATABASE::"Prod. Order Line":
          IF ProdOrderLine.GET(Subtype,ID,ProdOrderLineNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := ProdOrderLine."Starting Date";
            TempOrderTrackingEntry."Ending Date" := ProdOrderLine."Ending Date";
          END;
        DATABASE::"Prod. Order Component":
          IF ProdOrderComp.GET(Subtype,ID,ProdOrderLineNo,RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := ProdOrderComp."Due Date";
            IF ProdOrderLine.GET(Subtype,ID,ProdOrderLineNo) THEN
              TempOrderTrackingEntry."Ending Date" := ProdOrderLine."Ending Date";
          END;
        DATABASE::"Assembly Header":
          IF AsmHeader.GET(Subtype,ID) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := AsmHeader."Due Date";
            TempOrderTrackingEntry."Ending Date" := AsmHeader."Due Date";
          END;
        DATABASE::"Assembly Line":
          IF AsmLine.GET(Subtype,ID,RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := AsmLine."Due Date";
            TempOrderTrackingEntry."Ending Date" := AsmLine."Due Date";
          END;
        DATABASE::"Planning Component":
          IF PlanningComponent.GET(ID,BatchName,ProdOrderLineNo,RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := PlanningComponent."Due Date";
            IF ReqLine.GET(ID,BatchName,ProdOrderLineNo) THEN
              TempOrderTrackingEntry."Ending Date" := ReqLine."Ending Date";
          END;
        DATABASE::"Service Line":
          IF ServLine.GET(Subtype,ID,RefNo) THEN BEGIN
            TempOrderTrackingEntry."Starting Date" := ServLine."Needed by Date";
            TempOrderTrackingEntry."Ending Date" := ServLine."Needed by Date";
          END;
        DATABASE::"Job Planning Line":
          BEGIN
            JobPlanningLine.SETRANGE("Job No.",ID);
            JobPlanningLine.SETRANGE(Status,Subtype);
            JobPlanningLine.SETRANGE("Job Contract Entry No.",RefNo);
            IF JobPlanningLine.FINDFIRST THEN BEGIN
              TempOrderTrackingEntry."Starting Date" := JobPlanningLine."Planning Date";
              TempOrderTrackingEntry."Ending Date" := JobPlanningLine."Planning Date";
            END;
          END;
        ELSE
          OnInsertOrderTrackingEntry(TempOrderTrackingEntry,Type,Subtype,ID,RefNo,BatchName,ProdOrderLineNo);
      END;

      IF TempOrderTrackingEntry."From Type" = DATABASE::"Requisition Line" THEN
        IF ReqLine.GET(
             TempOrderTrackingEntry."From ID",TempOrderTrackingEntry."From Batch Name",TempOrderTrackingEntry."From Ref. No.")
        THEN
          IF ReqLine."Action Message" = ReqLine."Action Message"::Cancel THEN
            TempOrderTrackingEntry.Name := Text004;

      TempOrderTrackingEntry.INSERT;
      EntryNo := EntryNo + 1;

      OnAfterInsertTrackingEntry(TempOrderTrackingEntry,DateWarning);
    END;

    LOCAL PROCEDURE InsertItemLedgTrackEntry@14(Level@1000 : Integer;ToItemLedgEntry@1001 : Record 32;TrackQuantity@1002 : Decimal;FromItemLedgEntry@1003 : Record 32);
    VAR
      PeggingText@1004 : Text[30];
    BEGIN
      IF TrackQuantity = 0 THEN
        EXIT;
      TempOrderTrackingEntry.RESET;
      TempOrderTrackingEntry.INIT;
      TempOrderTrackingEntry."Entry No." := EntryNo;
      IF SearchUp THEN BEGIN
        TempOrderTrackingEntry."Demanded by" :=
          STRSUBSTNO(
            '%1 %2',FromItemLedgEntry.TABLECAPTION,FromItemLedgEntry."Entry No.");
        TempOrderTrackingEntry."Supplied by" :=
          STRSUBSTNO(
            '%1 %2',ToItemLedgEntry.TABLECAPTION,ToItemLedgEntry."Entry No.");
      END ELSE BEGIN
        TempOrderTrackingEntry."Supplied by" :=
          STRSUBSTNO(
            '%1 %2',FromItemLedgEntry.TABLECAPTION,FromItemLedgEntry."Entry No.");
        TempOrderTrackingEntry."Demanded by" :=
          STRSUBSTNO(
            '%1 %2',ToItemLedgEntry.TABLECAPTION,ToItemLedgEntry."Entry No.");
      END;

      IF Level = 1 THEN BEGIN
        IF ToItemLedgEntry."Entry No." = FromItemLedgEntry."Entry No." THEN
          PeggingText := Text005
        ELSE
          PeggingText := '';

        IF SearchUp THEN
          TempOrderTrackingEntry."Demanded by" := PeggingText + Text006
        ELSE
          TempOrderTrackingEntry."Supplied by" := PeggingText + Text007;
      END;

      TempOrderTrackingEntry."Item No." := ToItemLedgEntry."Item No.";
      TempOrderTrackingEntry.Quantity := TrackQuantity;
      TempOrderTrackingEntry.Level := Level;

      TempOrderTrackingEntry."For Type" := DATABASE::"Item Ledger Entry";
      TempOrderTrackingEntry."For Ref. No." := FromItemLedgEntry."Entry No.";
      TempOrderTrackingEntry."From Type" := DATABASE::"Item Ledger Entry";
      TempOrderTrackingEntry."From Ref. No." := ToItemLedgEntry."Entry No.";

      IF SearchUp THEN
        TempOrderTrackingEntry.Name := TempOrderTrackingEntry."Supplied by"
      ELSE
        TempOrderTrackingEntry.Name := TempOrderTrackingEntry."Demanded by";

      Type := TempOrderTrackingEntry."For Type";
      RefNo := ToItemLedgEntry."Entry No.";

      TempOrderTrackingEntry."Starting Date" := 0D;
      TempOrderTrackingEntry."Ending Date" := 0D;

      OnBeforeTempOrderTrackingEntryInsert(TempOrderTrackingEntry,ToItemLedgEntry,FromItemLedgEntry);
      TempOrderTrackingEntry.INSERT;
      EntryNo := EntryNo + 1;
    END;

    LOCAL PROCEDURE OrderTrackingEntryExists@1() : Boolean;
    VAR
      OrderTrackingEntry2@1000 : Record 99000799;
    BEGIN
      OrderTrackingEntry2 := TempOrderTrackingEntry;

      TempOrderTrackingEntry.SETRANGE(Level,TempOrderTrackingEntry.Level);
      TempOrderTrackingEntry.SETRANGE("For Type",TempOrderTrackingEntry."For Type");
      TempOrderTrackingEntry.SETRANGE("For Subtype",TempOrderTrackingEntry."For Subtype");
      TempOrderTrackingEntry.SETRANGE("For ID",TempOrderTrackingEntry."For ID");
      TempOrderTrackingEntry.SETRANGE("For Batch Name",TempOrderTrackingEntry."For Batch Name");
      TempOrderTrackingEntry.SETRANGE("For Prod. Order Line",TempOrderTrackingEntry."For Prod. Order Line");
      TempOrderTrackingEntry.SETRANGE("For Ref. No.",TempOrderTrackingEntry."For Ref. No.");

      TempOrderTrackingEntry.SETRANGE("From Type",TempOrderTrackingEntry."From Type");
      TempOrderTrackingEntry.SETRANGE("From Subtype",TempOrderTrackingEntry."From Subtype");
      TempOrderTrackingEntry.SETRANGE("From ID",TempOrderTrackingEntry."From ID");
      TempOrderTrackingEntry.SETRANGE("From Batch Name",TempOrderTrackingEntry."From Batch Name");
      TempOrderTrackingEntry.SETRANGE("From Prod. Order Line",TempOrderTrackingEntry."From Prod. Order Line");
      TempOrderTrackingEntry.SETRANGE("From Ref. No.",TempOrderTrackingEntry."From Ref. No.");

      IF TempOrderTrackingEntry.FIND('-') THEN BEGIN
        TempOrderTrackingEntry.RESET;
        EXIT(TRUE);
      END;
      TempOrderTrackingEntry.RESET;
      TempOrderTrackingEntry := OrderTrackingEntry2;
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE DerivePlanningFilter@13(VAR FromReservEntry@1001 : Record 337;VAR ToReservEntry@1002 : Record 337) OK@1000 : Boolean;
    VAR
      FilterReqLine@1003 : Record 246;
      FilterPlanningComponent@1004 : Record 99000829;
    BEGIN
      WITH ToReservEntry DO BEGIN
        OK := FALSE;
        SETRANGE("Source Type",DATABASE::"Planning Component");
        IF FromReservEntry.GETFILTER("Source Type") = GETFILTER("Source Type") THEN BEGIN
          EVALUATE(FilterPlanningComponent."Line No.",FromReservEntry.GETFILTER("Source Ref. No."));
          EVALUATE(FilterPlanningComponent."Worksheet Line No.",FromReservEntry.GETFILTER("Source Prod. Order Line"));
          //**4PS.so
          //IF NOT FilterPlanningComponent.GET(
          //     FromReservEntry.GETRANGEMIN("Source ID"),FromReservEntry.GETRANGEMIN("Source Batch Name"),
          //**4PS.eo

          // IF NOT FilterPlanningComponent.GET(FromReservEntry.GETFILTER("Source ID"),FromReservEntry.GETFILTER("Source Batch Name"), //**4PS.o
          IF NOT FilterPlanningComponent.GET(GetFilterValue(FromReservEntry.GETFILTER("Source ID")), GetFilterValue(FromReservEntry.GETFILTER("Source Batch Name")), //**4PS.n
               FilterPlanningComponent."Worksheet Line No.",FilterPlanningComponent."Line No.")
          THEN
            EXIT(FALSE);

          CASE FilterPlanningComponent."Ref. Order Type" OF
            FilterPlanningComponent."Ref. Order Type"::"Prod. Order":
              SetSourceFilter(
                DATABASE::"Prod. Order Component",FilterPlanningComponent."Ref. Order Status",
                FilterPlanningComponent."Ref. Order No.",FilterPlanningComponent."Line No.",TRUE);
            FilterPlanningComponent."Ref. Order Type"::Assembly:
              SetSourceFilter(
                DATABASE::"Assembly Line",FilterPlanningComponent."Ref. Order Status",
                FilterPlanningComponent."Ref. Order No.",FilterPlanningComponent."Line No.",TRUE);
          END;
          SETRANGE("Source Prod. Order Line",FilterPlanningComponent."Ref. Order Line No.");
          OK := FIND('-');
        END ELSE BEGIN
          SETRANGE("Source Type",DATABASE::"Requisition Line");
          IF FromReservEntry.GETFILTER("Source Type") = GETFILTER("Source Type") THEN BEGIN
            EVALUATE(FilterReqLine."Line No.",FromReservEntry.GETFILTER("Source Ref. No."));
            //**4PS.so
            //IF NOT FilterReqLine.GET(FromReservEntry.GETRANGEMIN("Source ID"),
            //     FromReservEntry.GETRANGEMIN("Source Batch Name"),FilterReqLine."Line No.")
            //**4PS.eo

            //**4PS.sn
            IF NOT FilterReqLine.GET(GetFilterValue(FromReservEntry.GETFILTER("Source ID")),
                  GetFilterValue(FromReservEntry.GETFILTER("Source Batch Name")),FilterReqLine."Line No.")
            //**4PS.en
            THEN
              EXIT(FALSE);
            IF FilterReqLine."Action Message" > FilterReqLine."Action Message"::New THEN
              CASE FilterReqLine."Ref. Order Type" OF
                FilterReqLine."Ref. Order Type"::Purchase:
                  BEGIN
                    SetSourceFilter(DATABASE::"Purchase Line",1,FilterReqLine."Ref. Order No.",FilterReqLine."Ref. Line No.",TRUE);
                    OK := FIND('-');
                  END;
                FilterReqLine."Ref. Order Type"::"Prod. Order":
                  BEGIN
                    SetSourceFilter(
                      DATABASE::"Prod. Order Line",FilterReqLine."Ref. Order Status",FilterReqLine."Ref. Order No.",-1,TRUE);
                    SETRANGE("Source Prod. Order Line",FilterReqLine."Ref. Line No.");
                    OK := FIND('-');
                  END;
                FilterReqLine."Ref. Order Type"::Transfer:
                  BEGIN
                    SetSourceFilter(DATABASE::"Transfer Line",1,FilterReqLine."Ref. Order No.",FilterReqLine."Ref. Line No.",TRUE);
                    SETRANGE("Source Prod. Order Line",0);
                    OK := FIND('-');
                  END;
              END;
          END;
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertTrackingEntry@17(VAR OrderTrackingEntry@1000 : Record 99000799;VAR DateWarning@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeTempOrderTrackingEntryInsert@31(VAR TempOrderTrackingEntry@1000 : TEMPORARY Record 99000799;ToItemLedgerEntry@1001 : Record 32;FromItemLedgerEntry@1002 : Record 32);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnDrillOrdersUpCaseElse@29(VAR ReservationEntry3@1000 : Record 337;VAR ReservationEntry2@1001 : Record 337;SearchUp@1002 : Boolean;VAR ContinueDrillUp@1003 : Boolean;VAR IncludePlanningFilter@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnInsertOrderTrackingEntry@35(OrderTrackingEntry@1000 : Record 99000799;Type@1001 : Option;Subtype@1002 : Integer;ID@1003 : Code[20];RefNo@1004 : Integer;BatchName@1005 : Code[20];ProdOrderLineNo@1006 : Integer);
    BEGIN
    END;

    LOCAL PROCEDURE GetFilterValue@1100527015(Filter@1100527000 : Text) : Text;
    BEGIN
      //**4PS
      IF (STRPOS(Filter, '''') = 1) AND (STRPOS(COPYSTR(Filter, STRLEN(Filter), 1), '''') = 1) THEN
        EXIT(COPYSTR(Filter, 2, STRLEN(Filter) - 2));
      EXIT(Filter);
    END;

    BEGIN
    {
      4PS, Renumbered objects
    }
    END.
  }
}

