OBJECT Page 6304 Power BI Report Selection
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Power BI Reports Selection;
               NOR=Valg av Power BI-rapporter;
               SVE=Val av Power BI-rapporter];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table6302;
    PageType=List;
    SourceTableTemporary=Yes;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Manage,Get Reports;
                                NOR=Ny,Prosess,Rapport,Administrer,Hent rapport;
                                SVE=Ny,Process,Rapport,Hantera,H„mta rapporter];
    OnOpenPage=BEGIN
                 IF NOT TryLoadReportsList THEN
                   ShowErrorMessage(GETLASTERRORTEXT);

                 IsSaaS := AzureADMgt.IsSaaS;
               END;

    OnQueryClosePage=BEGIN
                       SaveAndClose;
                     END;

    ActionList=ACTIONS
    {
      { 5       ;    ;ActionContainer;
                      ActionContainerType=Reports }
      { 7       ;1   ;Action    ;
                      Name=EnableReport;
                      CaptionML=[ENU=Enable;
                                 NOR=Aktiver;
                                 SVE=Aktivera];
                      ToolTipML=[ENU=Enables the report selection.;
                                 NOR=Aktiverer rapportvalget.;
                                 SVE=Aktiverar rapportval.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=NOT Enabled;
                      PromotedIsBig=Yes;
                      Image=Report;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 Enabled := TRUE;
                                 CurrPage.UPDATE;
                               END;

                      Gesture=LeftSwipe }
      { 8       ;1   ;Action    ;
                      Name=DisableReport;
                      CaptionML=[ENU=Disable;
                                 NOR=Deaktiver;
                                 SVE=Inaktivera];
                      ToolTipML=[ENU=Disables the report selection.;
                                 NOR=Deaktiverer rapportvalget.;
                                 SVE=Inaktiverar rapportval.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=Enabled;
                      PromotedIsBig=Yes;
                      Image=Report;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 Enabled := FALSE;
                                 CurrPage.UPDATE;
                               END;

                      Gesture=RightSwipe }
      { 9       ;1   ;Action    ;
                      Name=Refresh;
                      CaptionML=[ENU=Refresh List;
                                 NOR=Oppdater liste;
                                 SVE=Uppdatera lista];
                      ToolTipML=[ENU=Update the report list with any newly added reports.;
                                 NOR=Oppdater rapportlisten med nylig tillagte rapporter.;
                                 SVE=Uppdatera rapportlistan med rapporter som har lagts till nyligen.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Category5;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 IsErrorMessageVisible := FALSE;
                                 IsUrlFieldVisible := FALSE;

                                 IF NOT TryLoadReportsList THEN
                                   ShowErrorMessage(GETLASTERRORTEXT);
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=MyOrganization;
                      CaptionML=[ENU=My Organization;
                                 NOR=Min organisasjon;
                                 SVE=Min organisation];
                      ToolTipML=[ENU=Browse content packs that other people in your organization have published.;
                                 NOR=Bla gjennom innholdspakker som andre i organisasjonen har publisert.;
                                 SVE=Bl„ddra i inneh†llspaket som andra i organisationen har publicerat.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Image=PowerBI;
                      PromotedCategory=Category5;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 // Opens a new browser tab to Power BI's content pack list, set to the My Organization tab.
                                 HYPERLINK(PowerBIServiceMgt.GetContentPacksMyOrganizationUrl);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=Services;
                      CaptionML=[ENU=Services;
                                 NOR=Tjenester;
                                 SVE=Tj„nster];
                      ToolTipML=[ENU=Choose content packs from online services that you use.;
                                 NOR=Velg innholdspakker fra elektroniske tjenester du bruker.;
                                 SVE=V„lj inneh†llspaket fr†n de onlinetj„nster du anv„nder.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Image=PowerBI;
                      PromotedCategory=Category5;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 // Opens a new browser tab to AppSource's content pack list, filtered to NAV reports.
                                 HYPERLINK(PowerBIServiceMgt.GetContentPacksServicesUrl);
                               END;
                                }
      { 12      ;1   ;Action    ;
                      Name=ConnectionInfo;
                      CaptionML=[ENU=Connection Information;
                                 NOR=Tilkoblingsinformasjon;
                                 SVE=Anslutningsinformation];
                      ToolTipML=[ENU=Show information for connecting to Power BI content packs.;
                                 NOR=Vis informasjon for † koble til Power BI-innholdspakker.;
                                 SVE=Visa information f”r anslutning till Power BI-inneh†llspaket.];
                      ApplicationArea=#All;
                      RunObject=Page 6316;
                      Promoted=Yes;
                      Visible=NOT IsSaaS;
                      Image=Setup;
                      PromotedCategory=Category5;
                      PromotedOnly=Yes }
      { 18      ;1   ;Action    ;
                      Name=CleanDeployments;
                      CaptionML=[ENU=Default Reports;
                                 NOR=Standardrapporter;
                                 SVE=Standardrapporter];
                      ToolTipML=[ENU=Manage your deployed default reports.;
                                 NOR=Behandle distribuerte standardrapporter.;
                                 SVE=Hantera dina distribuerade standardrapporter.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Image=Setup;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 PAGE.RUNMODAL(PAGE::"Power BI Deployments");
                                 // TODO: Set this button's visibility to equal "IsSaaS" so it's not visible for on-prem, where we won't have OOB uploads anyways.
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                Visible=HasReports AND NOT IsErrorMessageVisible AND NOT IsUrlFieldVisible;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=ReportName;
                CaptionML=[ENU=Report Name;
                           NOR=Rapportnavn;
                           SVE=Rapportnamn];
                ToolTipML=[ENU=Specifies the name of the report.;
                           NOR=Angir navnet p† rapporten.;
                           SVE=Anger rapportens namn.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ReportName;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              // Event handler for when the user clicks the report name hyperlink. This automatically selects
                              // and enables the report and closes the window, so the user immediately sees the clicked report
                              // on the parent page.
                              Enabled := TRUE;

                              // OnDrillDown returns a LookupCancel Action when the page closes.
                              // IsPgclosedOkay is used to tell the caller of this page that inspite of the LookupCancel
                              // the action should be treated like a LookupOk
                              IsPgClosedOkay := TRUE;
                              SaveAndClose;
                            END;
                             }

    { 4   ;2   ;Field     ;
                Name=Enabled;
                ToolTipML=[ENU=Specifies that the report selection is enabled.;
                           NOR=Angir at rapportvalget er aktivert.;
                           SVE=Anger att rapportval „r aktiverat.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Enabled }

    { 6   ;1   ;Group     ;
                Visible=NOT HasReports AND NOT IsErrorMessageVisible AND NOT IsUrlFieldVisible;
                GroupType=Group }

    { 13  ;2   ;Field     ;
                Name=NoReportsError;
                CaptionML=[ENU=There are no reports available from Power BI.;
                           NOR=Det finnes ingen rapporter tilgjengelig fra Power BI.;
                           SVE=Det finns inga rapporter att tillg† fr†n Power BI.];
                ToolTipML=[ENU=Specifies that the user needs to select Power BI reports.;
                           NOR=Angir at brukeren m† velge Power BI-rapporter.;
                           SVE=Anger att anv„ndaren m†ste v„lja Power BI-rapporter.];
                ApplicationArea=#Basic,#Suite }

    { 14  ;1   ;Group     ;
                Visible=IsErrorMessageVisible AND NOT IsUrlFieldVisible;
                GroupType=Group }

    { 15  ;2   ;Field     ;
                Name=ErrorMessage;
                ToolTipML=[ENU=Specifies an error that occurred.;
                           NOR=Spesifiserer feilen som oppsto.;
                           SVE=Anger att ett fel intr„ffat.];
                ApplicationArea=#All;
                SourceExpr=ErrorMessageText;
                Editable=FALSE;
                ShowCaption=No }

    { 17  ;1   ;Group     ;
                Visible=IsUrlFieldVisible;
                GroupType=Group }

    { 16  ;2   ;Field     ;
                Name=ErrorLink;
                ToolTipML=[ENU=Specifies an error that occurred and how to resolve it.;
                           NOR=Spesifiserer feilen som oppsto, samt hvordan du l›ser den.;
                           SVE=Anger att ett fel intr„ffat och hur du ska l”sa det.];
                ApplicationArea=#All;
                SourceExpr=ErrorUrlText;
                Editable=FALSE;
                Style=StrongAccent;
                StyleExpr=TRUE;
                OnDrillDown=BEGIN
                              HYPERLINK(ErrorUrlPath);
                            END;

                ShowCaption=No }

  }
  CODE
  {
    VAR
      AzureADMgt@1002 : Codeunit 6300;
      PowerBIServiceMgt@1005 : Codeunit 6301;
      Context@1001 : Text[30];
      NameFilter@1004 : Text;
      IsPgClosedOkay@1003 : Boolean;
      IsSaaS@1006 : Boolean;
      HasReports@1007 : Boolean;
      IsErrorMessageVisible@1000 : Boolean;
      IsUrlFieldVisible@1009 : Boolean;
      ErrorMessageText@1008 : Text;
      ErrorUrlText@1010 : Text;
      ErrorUrlPath@1011 : Text;

    [External]
    PROCEDURE SetContext@2(ParentContext@1000 : Text[30]);
    BEGIN
      // Sets the ID of the parent page that reports are being selected for.
      Context := ParentContext;
    END;

    [External]
    PROCEDURE SetNameFilter@10(ParentFilter@1000 : Text);
    BEGIN
      // Sets the value to filter report names by.
      // This should be called by the parent page before opening this page.
      NameFilter := ParentFilter;
    END;

    LOCAL PROCEDURE FilterReports@21();
    BEGIN
      // Filters the report collection by name, if the parent page has provided a value to filter by.
      // This filter will display any report that has the value anywhere in the name, case insensitive.
      IF NameFilter <> '' THEN
        SETFILTER(ReportName,'%1','@*' + NameFilter + '*');
    END;

    [TryFunction]
    LOCAL PROCEDURE TryLoadReportsList@1();
    BEGIN
      LoadReportsList;
    END;

    LOCAL PROCEDURE LoadReportsList@6();
    VAR
      ExceptionMessage@1000 : Text;
      ExceptionDetails@1001 : Text;
    BEGIN
      // Clears and retrieves a list of all reports in the user's Power BI account.
      RESET;
      DELETEALL;
      PowerBIServiceMgt.GetReports(Rec,ExceptionMessage,ExceptionDetails,Context);

      HasReports := NOT ISEMPTY;
      IF ISEMPTY THEN
        INSERT; // Hack to prevent empty list error.

      // Set sort order, scrollbar position, and filters.
      SETCURRENTKEY(ReportName);
      FINDFIRST;
      FilterReports;
    END;

    LOCAL PROCEDURE SaveAndClose@5();
    VAR
      PowerBiReportConfiguration@1001 : Record 6301;
      TempPowerBiReportBuffer@1000 : TEMPORARY Record 6302;
    BEGIN
      // use a temp buffer record for saving to not disturb the position, filters, etc. of the source table
      // ShareTable = TRUE makes a shallow copy of the record, which is OK since no modifications are made to the records themselves
      TempPowerBiReportBuffer.COPY(Rec,TRUE);

      // Clear out all old records before re-adding (easiest way to remove invalid rows, e.g. deleted reports).
      PowerBiReportConfiguration.SETFILTER("User Security ID",USERSECURITYID);
      PowerBiReportConfiguration.SETFILTER(Context,Context);
      PowerBiReportConfiguration.DELETEALL;
      PowerBiReportConfiguration.RESET;

      TempPowerBiReportBuffer.SETRANGE(Enabled,TRUE);
      IF TempPowerBiReportBuffer.FIND('-') THEN
        REPEAT
          PowerBiReportConfiguration.INIT;
          PowerBiReportConfiguration."User Security ID" := USERSECURITYID;
          PowerBiReportConfiguration."Report ID" := TempPowerBiReportBuffer.ReportID;
          PowerBiReportConfiguration.Context := Context;
          PowerBiReportConfiguration.EmbedUrl := TempPowerBiReportBuffer.EmbedUrl;
          PowerBiReportConfiguration.INSERT;
        UNTIL TempPowerBiReportBuffer.NEXT = 0;

      IsPgClosedOkay := TRUE;
      CurrPage.CLOSE;
    END;

    [External]
    PROCEDURE IsPageClosedOkay@3() : Boolean;
    BEGIN
      EXIT(IsPgClosedOkay);
    END;

    LOCAL PROCEDURE ShowErrorMessage@7(TextToShow@1000 : Text);
    BEGIN
      // Dealing with 401 Unauthorized error URL, per page 6303.
      IF TextToShow = PowerBIServiceMgt.GetUnauthorizedErrorText THEN BEGIN
        ErrorUrlText := TextToShow;
        ErrorUrlPath := PowerBIServiceMgt.GetPowerBIUrl;
        IsUrlFieldVisible := TRUE;
      END ELSE BEGIN
        IF TextToShow = '' THEN
          TextToShow := PowerBIServiceMgt.GetGenericError;

        ErrorMessageText := TextToShow;
        IsErrorMessageVisible := TRUE;
      END;
    END;

    BEGIN
    END.
  }
}

