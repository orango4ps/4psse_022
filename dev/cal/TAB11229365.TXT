OBJECT Table 11229365 User Filter on Page
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=User Filter on Page;
  }
  FIELDS
  {
    { 10  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              NOR=Bruker-ID;
                                                              SVE=Anv„ndar-ID] }
    { 20  ;   ;Page ID             ;Integer       ;CaptionML=[ENU=Page ID;
                                                              NOR=Side-ID;
                                                              SVE=Sid-ID] }
    { 30  ;   ;Favorite filter     ;Text250       ;CaptionML=ENU=Favorite Cluster }
  }
  KEYS
  {
    {    ;User ID,Page ID                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528500 : TextConst 'ENU=The filtering is %1 too long to be stored.';

    PROCEDURE StoreMyFilters@1100528500(Variant@1100528500 : Variant;PageIDString@1100528502 : Text);
    VAR
      UserFilterOnPage@1100528501 : Record 11229365;
      RecRef@1100528503 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Variant);
      IF STRLEN(RecRef.GETVIEW(FALSE)) > MAXSTRLEN(UserFilterOnPage."Favorite filter") THEN
        ERROR(Text001,STRLEN(RecRef.GETVIEW(FALSE))-MAXSTRLEN(UserFilterOnPage."Favorite filter"));
      IF UserFilterOnPage.GET(USERID,GetPageID(PageIDString)) THEN BEGIN
        UserFilterOnPage."Favorite filter" := RecRef.GETVIEW(FALSE);
        UserFilterOnPage.MODIFY;
      END ELSE BEGIN
        UserFilterOnPage."User ID" := USERID;
        UserFilterOnPage."Page ID" := GetPageID(PageIDString);
        UserFilterOnPage."Favorite filter" := RecRef.GETVIEW(FALSE);
        UserFilterOnPage.INSERT;
      END;
    END;

    PROCEDURE GetMyFilters@1100528501(VAR RecRef@1100528500 : RecordRef;PageIDString@1100528501 : Text);
    VAR
      UserFilterOnPage@1100528502 : Record 11229365;
    BEGIN
      IF UserFilterOnPage.GET(USERID,GetPageID(PageIDString)) THEN
        RecRef.SETVIEW(UserFilterOnPage."Favorite filter");
    END;

    LOCAL PROCEDURE GetPageID@1100528502(PageIDString@1100528500 : Text) PageID : Integer;
    BEGIN
      EVALUATE(PageID,DELCHR(PageIDString,'=',DELCHR(PageIDString,'=','0123456789')));
    END;

    BEGIN
    END.
  }
}

