OBJECT Codeunit 233 Gen. Jnl.-B.Post
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=232;
    OnRun=BEGIN
            GenJnlBatch.COPY(Rec);
            Code;
            COPY(GenJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals?;NOR=Vil du bokfre kladdene?;SVE=Vill du bokfra journalerna?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;NOR=Kladdene ble bokfrt p riktig mte.;SVE=Journalerna r bokfrda.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";NOR="Det er ikke mulig  bokfre alle kladdene. ";SVE="Det var inte mjligt att bokfra alla journaler. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;NOR=Kladdene som ikke ble bokfrt, er merket.;SVE=Journalerna som inte blev bokfrda r nu markerade.';
      GenJnlTemplate@1004 : Record 80;
      GenJnlBatch@1005 : Record 232;
      GenJnlLine@1006 : Record 81;
      GenJnlPostBatch@1007 : Codeunit 13;
      GenJnlManagement@1219 : Codeunit 230;
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    VAR
      ConfirmManagement@1001 : Codeunit 27;
      OrderByDocNoAndLineNo@1000 : Boolean;
      GenJournalTemplatePerRole@1100525000 : Record 11020419;
    BEGIN
      // If simple view is used then order gen. journal lines by doc no. and line no.
      IF GenJnlManagement.GetJournalSimplePageModePreference(PAGE::"General Journal") THEN
        OrderByDocNoAndLineNo := TRUE;

      WITH GenJnlBatch DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        GenJnlTemplate.TESTFIELD("Force Posting Report",FALSE);
        GenJournalTemplatePerRole.CheckPermission(TABLECAPTION, "Journal Template Name",TRUE); //**4PS.n

        IF NOT ConfirmManagement.ConfirmProcess(Text000,TRUE) THEN
          EXIT;

        FIND('-');
        REPEAT
          GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlLine.SETRANGE("Journal Batch Name",Name);
          IF OrderByDocNoAndLineNo THEN
            GenJnlLine.SETCURRENTKEY("Document No.","Line No.");
          IF GenJnlLine.FINDFIRST THEN BEGIN
            CLEAR(GenJnlPostBatch);
            IF GenJnlPostBatch.RUN(GenJnlLine) THEN
              MARK(FALSE)
            ELSE BEGIN
              MARK(TRUE);
              JnlWithErrors := TRUE;
            END;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE BEGIN
          MARKEDONLY(TRUE);
          MESSAGE(
            Text002 +
            Text003);
        END;

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP(0);
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}

