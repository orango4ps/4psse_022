OBJECT Codeunit 6085774 Sales Doc. - Line Validation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    TableNo=6085596;
    OnRun=VAR
            Document@6085588 : Record 6085590;
            Field@6085587 : Record 6085580;
            Currency@6085586 : Record 4;
            SalesDocMgt@6085585 : Codeunit 6085770;
            CaptureMgt@6085584 : Codeunit 6085576;
            Quantity@6085583 : Decimal;
            UnitCost@6085582 : Decimal;
            LineAmount@6085581 : Decimal;
            DiscAmount@6085580 : Decimal;
            DiscPct@6085579 : Decimal;
            LineAmount2@6085578 : Decimal;
            AmountRoundingPrecision@6085577 : Decimal;
            LineDescription@6085590 : Text[250];
            LineAccountNo@6085589 : Code[250];
            CurrencyCode@6085576 : Code[10];
          BEGIN
            IF NOT Document.GET("Document No.") THEN
              EXIT;

            LineAccountNo := SalesDocMgt.GetLineAccountNo(Document,"Line No.");
            LineDescription := SalesDocMgt.GetLineDescription(Document,"Line No.");
            Quantity := SalesDocMgt.GetLineQuantity(Document,Rec."Line No.");
            UnitCost := SalesDocMgt.GetLineUnitCost(Document,Rec."Line No.");
            DiscPct := SalesDocMgt.GetLineDiscPct(Document,Rec."Line No.");
            DiscAmount := SalesDocMgt.GetLineDiscAmount(Document,Rec."Line No.");
            LineAmount := SalesDocMgt.GetLineAmount(Document,Rec."Line No.");
            CurrencyCode := SalesDocMgt.GetCurrencyCode(Document);

            IF (LineAccountNo = '') AND (Quantity = 0) AND (UnitCost = 0) AND (LineAmount = 0) AND (DiscPct = 0) AND
              (DiscAmount = 0) AND (LineDescription = '')
            THEN BEGIN
              Skip := TRUE;
              EXIT;
            END;

            Field.SETRANGE("Template No.","Template No.");
            Field.SETRANGE(Type,Field.Type::Line);
            IF Field.FINDSET THEN
              REPEAT
                IF NOT CaptureMgt.IsValidValue(Field,"Document No.","Line No.") THEN BEGIN
                  OK := FALSE;
                  EXIT;
                END;
              UNTIL Field.NEXT = 0;

            IF CurrencyCode = '' THEN BEGIN
              Currency.InitRoundingPrecision;
              AmountRoundingPrecision := Currency."Amount Rounding Precision"
            END ELSE BEGIN
              IF NOT Currency.GET(CurrencyCode) THEN BEGIN
                OK := FALSE;
                EXIT;
              END;

              IF Currency."Amount Rounding Precision" <> 0 THEN
                AmountRoundingPrecision := Currency."Amount Rounding Precision"
              ELSE
                AmountRoundingPrecision := 0.01;
            END;

            LineAmount2 := ROUND(Quantity * UnitCost,AmountRoundingPrecision);

            IF DiscAmount <> 0 THEN
              LineAmount2 := LineAmount2 - ROUND(DiscAmount,AmountRoundingPrecision)
            ELSE
              IF DiscPct <> 0 THEN BEGIN
                DiscAmount :=  ROUND(LineAmount2 * DiscPct / 100,AmountRoundingPrecision);
                LineAmount2 := LineAmount2 - DiscAmount;
              END;

            OK := LineAmount = LineAmount2;
          END;

  }
  CODE
  {

    BEGIN
    {
      This codeunit validates lines on sales documents
    }
    END.
  }
}

