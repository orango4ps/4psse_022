OBJECT Report 11012347 Compress Discount Term History
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Rabattbedingungshistorie komprimieren;
               ENU=Compress Discount Term History;
               NLD=Kortingscondities verdichten;
               NOR=Komprimer historikk for rabattvilk†r;
               SVE=Kompr. historik rabattvillkor];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   CheckCentralMgt;
                 END;

    OnPostReport=BEGIN
                   MESSAGE(Text001, NumProcessed1+NumProcessed2, NumDeleted1+NumDeleted2);
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 5431;    ;DataItem;DiscountHistory1    ;
               DataItemTable=Table11012312;
               DataItemTableView=SORTING(Discount Level,Discount Reference,Vendor,Discount Group,Item Code,Starting Date)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               BeforeDiscountHistory1;
                             END;

               OnAfterGetRecord=BEGIN
                                  ProcessDiscountHistory1;
                                END;

               OnPostDataItem=BEGIN
                                AfterDiscountHistory1;
                              END;

               ReqFilterFields=Vendor,Discount Group,Item Code,Discount Level,Discount Reference }

    { 1100525000;;DataItem;TmpDiscountHistory2;
               DataItemTable=Table11012312;
               DataItemTableView=SORTING(Discount Level,Discount Reference,Vendor,Discount Group,Item Code,Starting Date)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               BeforeDiscountHistory2;
                             END;

               OnAfterGetRecord=BEGIN
                                  ProcessDiscountHistory2;
                                END;

               OnPostDataItem=BEGIN
                                AfterDiscountHistory2;
                              END;

               Temporary=Yes }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PrevDiscountHistory@1210190017 : Record 11012312;
      NoOfRecs@1210190002 : Integer;
      Counter@1210190001 : Integer;
      NoOfDeletes@1100409001 : Integer;
      Window@1210190000 : Dialog;
      Text000@1210190003 : TextConst 'DEU=Rabattbedingungshistorie komprimieren (ohne Enddatum)... @1@@@@@@@@@@@@@;ENU=Compress Discount Term History (without Ending Date)... @1@@@@@@@@@@@@@;NLD=Verdichten kortingcondities (zonder einddatum)... @1@@@@@@@@@@@@@;NOR=Komprimer Rabattvilk†rshistorikk (Uten Sluttdato)... @1@@@@@@@@@@@@@';
      Text001@1100409000 : TextConst 'DEU=Geprft: %1\\Gel”scht: %2;ENU=Checked: %1\\Deleted: %2;NLD="Gecontroleerd: %1\\Verwijderd: %2 ";NOR=Kontrollert: %1\\Fjernet: %2';
      NumProcessed1@1100525000 : Integer;
      NumProcessed2@1100525001 : Integer;
      NumDeleted1@1100525002 : Integer;
      NumDeleted2@1100525003 : Integer;
      Text002@1100525005 : TextConst 'DEU=Rabattbedingungshistorie komprimieren (mit Enddatum)... @1@@@@@@@@@@@@@;ENU=Compress Discount Term History (with Ending Date)... @1@@@@@@@@@@@@@;NLD=Verdichten kortingcondities (met einddatum)... @1@@@@@@@@@@@@@;NOR=Komprimer rabattvilk†rshistorikk (med Sluttdato)... @1@@@@@@@@@@@@@';
      CentralMgt@1100528900 : Boolean;

    PROCEDURE BeforeDiscountHistory1@1100525000();
    BEGIN
      SetFilterDiscountHistory1;
      StartProgress(1);
    END;

    PROCEDURE ProcessDiscountHistory1@1100525001();
    BEGIN
      ShowProgress;

      WITH DiscountHistory1 DO BEGIN
        IF SameDiscountTerm(PrevDiscountHistory, DiscountHistory1) THEN BEGIN
          DELETE(CentralMgt);  //C054829
          NoOfDeletes += 1;
        END ELSE BEGIN
          PrevDiscountHistory.COPY(DiscountHistory1);
        END;
      END;
    END;

    PROCEDURE AfterDiscountHistory1@1100525002();
    BEGIN
      EndProgress(1);
    END;

    PROCEDURE BeforeDiscountHistory2@1100525004();
    BEGIN
      SetFilterDiscountHistory2;
      StartProgress(2);
    END;

    PROCEDURE ProcessDiscountHistory2@1100525005();
    VAR
      lvCompress@1100525000 : Boolean;
      DiscountTermHistory@1100528900 : Record 11012312;
    BEGIN
      ShowProgress;

      WITH TmpDiscountHistory2 DO BEGIN
        IF SameDiscountTerm(PrevDiscountHistory, TmpDiscountHistory2) THEN BEGIN
          lvCompress := FALSE;
          IF ("Starting Date" >= PrevDiscountHistory."Starting Date") AND
             ("Starting Date" <= PrevDiscountHistory."Ending Date") AND
             (PrevDiscountHistory."Ending Date" <= "Ending Date") THEN
            lvCompress := TRUE;
          IF ("Starting Date" = PrevDiscountHistory."Ending Date") OR
             ("Starting Date"-1 = PrevDiscountHistory."Ending Date") THEN  //C054829
            lvCompress := TRUE;
          IF lvCompress = TRUE THEN BEGIN
            PrevDiscountHistory.GET(Vendor, "Discount Group", "Item Code",
              "Discount Level", "Discount Reference",
              PrevDiscountHistory."Starting Date");
            PrevDiscountHistory.VALIDATE("Ending Date", "Ending Date");
            PrevDiscountHistory.MODIFY(CentralMgt);  //C054829

            //C054829.sn
            DiscountTermHistory.GET(Vendor, "Discount Group", "Item Code",
              "Discount Level", "Discount Reference","Starting Date");
            DiscountTermHistory.DELETE(CentralMgt);
            //C054829.en
            NoOfDeletes += 1;
          END ELSE BEGIN
            PrevDiscountHistory.COPY(TmpDiscountHistory2);
          END;
        END ELSE BEGIN
          PrevDiscountHistory.COPY(TmpDiscountHistory2);
        END;
      END;
    END;

    PROCEDURE AfterDiscountHistory2@1100525006();
    BEGIN
      EndProgress(2);
    END;

    LOCAL PROCEDURE SetFilterDiscountHistory1@1100528906();
    BEGIN
      //C054829
      DiscountHistory1.SETRANGE("Ending Date", 0D);
      NoOfRecs := DiscountHistory1.COUNT;
    END;

    LOCAL PROCEDURE SetFilterDiscountHistory2@1100528901();
    VAR
      DiscountTermHistory@1100528900 : Record 11012312;
    BEGIN
      //C054829
      DiscountTermHistory.COPY(DiscountHistory1);
      DiscountTermHistory.SETFILTER("Ending Date", '<>%1', 0D);

      IF DiscountTermHistory.FINDSET THEN BEGIN
        REPEAT
          TmpDiscountHistory2.COPY(DiscountTermHistory);
          TmpDiscountHistory2.INSERT;
        UNTIL DiscountTermHistory.NEXT = 0;
      END;

      NoOfRecs := TmpDiscountHistory2.COUNT;
    END;

    LOCAL PROCEDURE SameDiscountTerm@1100528903(PrevDiscountHistory@1100528900 : Record 11012312;CurrDiscountHistory@1100528901 : Record 11012312) : Boolean;
    BEGIN
      //C054829
      IF (PrevDiscountHistory.Vendor                       = CurrDiscountHistory.Vendor                      ) AND
         (PrevDiscountHistory."Discount Group"             = CurrDiscountHistory."Discount Group"            ) AND
         (PrevDiscountHistory."Item Code"                  = CurrDiscountHistory."Item Code"                 ) AND
         (PrevDiscountHistory."Discount Level"             = CurrDiscountHistory."Discount Level"            ) AND
         (PrevDiscountHistory."Discount Reference"         = CurrDiscountHistory."Discount Reference"        ) AND
         (PrevDiscountHistory."Discount Percentage 1"      = CurrDiscountHistory."Discount Percentage 1"     ) AND
         (PrevDiscountHistory."Discount Percentage 2"      = CurrDiscountHistory."Discount Percentage 2"     ) AND
         (PrevDiscountHistory."Discount Percentage 3"      = CurrDiscountHistory."Discount Percentage 3"     ) AND
         (PrevDiscountHistory."Purchase Price"             = CurrDiscountHistory."Purchase Price"            ) AND
         (PrevDiscountHistory.Currency                     = CurrDiscountHistory.Currency                    ) AND
         (PrevDiscountHistory."Sales Discount Group"       = CurrDiscountHistory."Sales Discount Group"      ) AND
         (PrevDiscountHistory."Maximum % Price Difference" = CurrDiscountHistory."Maximum % Price Difference") AND
         (PrevDiscountHistory."Price Agreement (Purchase)" = CurrDiscountHistory."Price Agreement (Purchase)") AND
         (PrevDiscountHistory."Price Agreement (Sales)"    = CurrDiscountHistory."Price Agreement (Sales)"   ) AND
         (PrevDiscountHistory."Sales Discount Percentage"  = CurrDiscountHistory."Sales Discount Percentage" ) AND
         (PrevDiscountHistory."Sales Price"                = CurrDiscountHistory."Sales Price"               ) THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    LOCAL PROCEDURE StartProgress@1100528904(iAction@1100528900 : Integer);
    BEGIN
      //C054829
      Counter:= 0;
      NoOfDeletes := 0;

      PrevDiscountHistory.INIT;
      CLEAR(PrevDiscountHistory);

      CASE iAction OF
        1: Window.OPEN(Text000);
        2: Window.OPEN(Text002);
      END;
    END;

    LOCAL PROCEDURE ShowProgress@1100528900();
    BEGIN
      //C054829
      Counter += 1;
      IF Counter MOD 100 = 0 THEN
        Window.UPDATE(1, ROUND(Counter / NoOfRecs * 10000, 1));
    END;

    LOCAL PROCEDURE EndProgress@1100528902(iAction@1100528900 : Integer);
    BEGIN
      //C054829
      Window.CLOSE;

      CASE iAction OF
        1:
          BEGIN
            NumProcessed1 := Counter;
            NumDeleted1 := NoOfDeletes;
          END;
        2:
          BEGIN
            NumProcessed2 := Counter;
            NumDeleted2 := NoOfDeletes;
          END;
      END;
    END;

    LOCAL PROCEDURE CheckCentralMgt@1100528905();
    VAR
      InventorySetup@1100528900 : Record 313;
    BEGIN
      //C054829
      InventorySetup.GET;
      IF InventorySetup."Central Management" AND InventorySetup."Discount Terms" THEN
        CentralMgt := TRUE;
    END;

    BEGIN
    {
      db, 17-04-13: redesign report (C006604)
      - split selection due to check on ending date
    }
    END.
  }
  RDLDATA
  {
  }
}

