OBJECT Table 49 Invoice Post. Buffer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00,NAVSE.FI.NO,4PSSE;
  }
  PROPERTIES
  {
    ReplicateData=No;
    CaptionML=[ENU=Invoice Post. Buffer;
               NOR=Fakturabokf.buffer;
               SVE=Fakturabokf”ringsbuffert];
  }
  FIELDS
  {
    { 1   ;   ;Type                ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Prepmt. Exch. Rate Difference,G/L Account,Item,Resource,Fixed Asset;
                                                                    NOR=Valutakursdifferanse for forskudd,Finanskonto,Vare,Ressurs,Aktiva;
                                                                    SVE=F”rskottsbet. valutakursdiff,Redov.konto,Artikel,Resurs,Anl.tillg†ng];
                                                   OptionString=Prepmt. Exch. Rate Difference,G/L Account,Item,Resource,Fixed Asset }
    { 2   ;   ;G/L Account         ;Code20        ;TableRelation="G/L Account";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=G/L Account;
                                                              NOR=Finanskonto;
                                                              SVE=Redovisningskonto] }
    { 4   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Global dimensjon 1 - kode;
                                                              SVE=Global dimension 1 kod];
                                                   CaptionClass='1,1,1' }
    { 5   ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Global dimensjon 2 - kode;
                                                              SVE=Global dimension 2 kod];
                                                   CaptionClass='1,1,2' }
    { 6   ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Job No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr] }
    { 7   ;   ;Amount              ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp];
                                                   AutoFormatType=1 }
    { 8   ;   ;VAT Amount          ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Amount;
                                                              NOR=Mva-bel›p;
                                                              SVE=Momsbelopp];
                                                   AutoFormatType=1 }
    { 10  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              NOR=Bokf›ringsgruppe - firma;
                                                              SVE=Gen. r”relsebokf”ringsmall] }
    { 11  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              NOR=Bokf›ringsgruppe - vare;
                                                              SVE=Produktbokf”ringsmall] }
    { 12  ;   ;VAT Calculation Type;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Calculation Type;
                                                              NOR=Mva-beregningstype;
                                                              SVE=Momsber„kningstyp];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                                    NOR=Normal mva.,Snudd avregning,Full mva.,Salgsmva.;
                                                                    SVE=Normal moms,Omv„nd EG-moms,Enbart moms,Oms„ttningsskatt];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax }
    { 14  ;   ;VAT Base Amount     ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Base Amount;
                                                              NOR=Mva-grunnlagsbel›p;
                                                              SVE=Nettobelopp];
                                                   AutoFormatType=1 }
    { 17  ;   ;System-Created Entry;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=System-Created Entry;
                                                              NOR=Opprettet automatisk;
                                                              SVE=Systemkreerad trans.] }
    { 18  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Tax Area Code;
                                                              NOR=Mva-omr†dekode;
                                                              SVE=Skatteomr†deskod] }
    { 19  ;   ;Tax Liable          ;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Tax Liable;
                                                              NOR=Skyldig mva.;
                                                              SVE=Skattepliktig] }
    { 20  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Tax Group Code;
                                                              NOR=Mva-gruppekode;
                                                              SVE=Skattegruppskod] }
    { 21  ;   ;Quantity            ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=1:5 }
    { 22  ;   ;Use Tax             ;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Use Tax;
                                                              NOR=Amerikansk mva.;
                                                              SVE=Anv„nd skatt] }
    { 23  ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Bus. Posting Group;
                                                              NOR=Mva-bokf›ringsgruppe - firma;
                                                              SVE=Moms r”relsebokf”ringsmall] }
    { 24  ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              NOR=Mva-bokf›ringsgruppe - vare;
                                                              SVE=Moms produktbokf”ringsmall] }
    { 25  ;   ;Amount (ACY)        ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Amount (ACY);
                                                              NOR=Bel›p (till.val);
                                                              SVE=Belopp (AVAL)];
                                                   AutoFormatType=1 }
    { 26  ;   ;VAT Amount (ACY)    ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Amount (ACY);
                                                              NOR=Mva-bel›p (till.val);
                                                              SVE=Momsbelopp (AVAL)];
                                                   AutoFormatType=1 }
    { 29  ;   ;VAT Base Amount (ACY);Decimal      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Base Amount (ACY);
                                                              NOR=Mva-gr.lagsbel. (t.val);
                                                              SVE=Nettobelopp (AVAL)];
                                                   AutoFormatType=1 }
    { 31  ;   ;VAT Difference      ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Difference;
                                                              NOR=Mva-differanse;
                                                              SVE=Momsdifferens];
                                                   AutoFormatType=1 }
    { 32  ;   ;VAT %               ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT %;
                                                              NOR=Mva-%;
                                                              SVE=Momssats %];
                                                   DecimalPlaces=1:1 }
    { 35  ;   ;VAT Base Before Pmt. Disc.;Decimal ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT Base Before Pmt. Disc.;
                                                              NOR=Mva-grunnlag f›r kont.rab.];
                                                   AutoFormatType=1 }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Dimension Set ID;
                                                              NOR=Dimensjonssett-ID;
                                                              SVE=Dimensionsupps„ttnings-ID];
                                                   Editable=No }
    { 1000;   ;Additional Grouping Identifier;Code20;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Additional Grouping Identifier;
                                                              NOR=ID for ekstra gruppering] }
    { 1700;   ;Deferral Code       ;Code10        ;TableRelation="Deferral Template"."Deferral Code";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Deferral Code;
                                                              NOR=Periodiseringskode;
                                                              SVE=Periodiseringskod] }
    { 1701;   ;Deferral Line No.   ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Deferral Line No.;
                                                              NOR=Periodiseringslinjenr.;
                                                              SVE=Periodiseringsradnr] }
    { 5600;   ;FA Posting Date     ;Date          ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=FA Posting Date;
                                                              NOR=Aktivabokf.dato;
                                                              SVE=Anl. bokf”ringsdatum] }
    { 5601;   ;FA Posting Type     ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=FA Posting Type;
                                                              NOR=Aktivabokf.type;
                                                              SVE=Anl. bokf”ringstyp];
                                                   OptionCaptionML=[ENU=" ,Acquisition Cost,Maintenance,,Appreciation";
                                                                    NOR=" ,Anskaffelseskost,Vedlikehold,,Oppskrivning";
                                                                    SVE=" ,Anskaffningskostnad,Underh†ll,,V„rdering"];
                                                   OptionString=[ ,Acquisition Cost,Maintenance,,Appreciation] }
    { 5602;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Depreciation Book Code;
                                                              NOR=Avskrivningstabl†kode;
                                                              SVE=Avskrivningsregelkod] }
    { 5603;   ;Salvage Value       ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Salvage Value;
                                                              NOR=Skrapverdi;
                                                              SVE=teranskaffningsv„rde];
                                                   AutoFormatType=1 }
    { 5605;   ;Depr. until FA Posting Date;Boolean;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Depr. until FA Posting Date;
                                                              NOR=Avskr. frem til aktivabokf.dato;
                                                              SVE=Avskr. till anl. bokf.datum] }
    { 5606;   ;Depr. Acquisition Cost;Boolean     ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Depr. Acquisition Cost;
                                                              NOR=Avskr.anskaffelseskost;
                                                              SVE=Avskr. anskaffningskostnad] }
    { 5609;   ;Maintenance Code    ;Code10        ;TableRelation=Maintenance;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Maintenance Code;
                                                              NOR=Vedlikeholdskode;
                                                              SVE=Underh†llskod] }
    { 5610;   ;Insurance No.       ;Code20        ;TableRelation=Insurance;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Insurance No.;
                                                              NOR=Forsikringsnr.;
                                                              SVE=F”rs„kringsnr] }
    { 5611;   ;Budgeted FA No.     ;Code20        ;TableRelation="Fixed Asset";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Budgeted FA No.;
                                                              NOR=Budsjettert aktivanr.;
                                                              SVE=Budgeterat anl.nr] }
    { 5612;   ;Duplicate in Depreciation Book;Code10;
                                                   TableRelation="Depreciation Book";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Duplicate in Depreciation Book;
                                                              NOR=Duplikat i avskrivningstabl†;
                                                              SVE=Dubblett i avskrivningsregel] }
    { 5613;   ;Use Duplication List;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Use Duplication List;
                                                              NOR=Bruk duplikatoversikt;
                                                              SVE=Anv„nd dubblettlista] }
    { 5614;   ;Fixed Asset Line No.;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Fixed Asset Line No.;
                                                              NOR=Aktivalinjenr.;
                                                              SVE=Anl„ggningstillg†ng radnr] }
    { 10604;  ;VAT Code            ;Code10        ;TableRelation="VAT Code".Code;
                                                   CaptionML=[ENU=VAT Code;
                                                              NOR=Mva-kode;
                                                              SVE=Momskod];
                                                   Description=NAVNO (10604) }
    { 11200;  ;Auto. Acc. Group    ;Code10        ;TableRelation="Automatic Acc. Header";
                                                   CaptionML=[ENU=Auto. Acc. Group;
                                                              SVE=Automatkonteringsmall];
                                                   Description=NAVSE, NAVFI }
    { 11203;  ;VAT Base Amount (LCY);Decimal      ;CaptionML=[ENU=VAT Base Amount (LCY);
                                                              SVE=Nettobelopp (BVA)];
                                                   Description=NAVSE (11203) }
    { 11012000;;Description        ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 11012005;;Description 2      ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 11012010;;Bal. Account No.   ;Code20        ;CaptionML=[ENU=Bal. Account No.;
                                                              NOR=Motkonto;
                                                              SVE=Motkonto] }
    { 11012020;;Element            ;Code20        ;CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
    { 11012030;;Service Order No.  ;Code20        ;TableRelation="Service Order".No.;
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 11012040;;Service Contract No.;Code20       ;TableRelation=Service-Contract.No.;
                                                   CaptionML=[ENU=Service Contract No.;
                                                              NOR=Servicekontraktnr;
                                                              SVE=Servicekontraktnr];
                                                   Editable=No }
    { 11012041;;Service Location No.;Code20       ;CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr] }
    { 11012050;;Origin Type        ;Option        ;CaptionML=[ENU=Origin Type;
                                                              NOR=Opprinnelig type;
                                                              SVE=Ursprunglig typ];
                                                   OptionCaptionML=[ENU=" ,Project,Service,Plant";
                                                                    NOR=" ,Prosjekt,Service,Maskin";
                                                                    SVE=" ,Projekt,Service,Maskin"];
                                                   OptionString=[ ,Project,Service,Plant] }
    { 11012060;;Intercompany Transaction;Boolean  ;CaptionML=[ENU=Intercompany Transaction;
                                                              NOR=Konsernintern transaksjon;
                                                              SVE=Koncernintern transaktion] }
    { 11012061;;Receiving Company  ;Text30        ;TableRelation=Company;
                                                   CaptionML=[ENU=Receiving Company;
                                                              NOR=Mottakselskap;
                                                              SVE=Mottagningsf”retag] }
    { 11012070;;Cost Component     ;Code20        ;TableRelation="Cost Component".Code;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Cost Component;
                                                              NOR=Kostnadskomponent;
                                                              SVE=Kostnadskomponent] }
    { 11012080;;Employee No.       ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=Anst„llningsnr] }
    { 11012090;;Buy-Back Item (Plant Order);Boolean;
                                                   CaptionML=[ENU=Buy-Back Item (Plant Order);
                                                              NOR=Tilbakekj›psartikkel (Maskinordre);
                                                              SVE=terk”psartikel (Maskinorder)] }
    { 11012100;;Block VAT Posting  ;Boolean       ;CaptionML=[ENU=Block VAT Posting;
                                                              NOR=Sperr mva-bokf›ring;
                                                              SVE=Sp„rra momsbokf”ring] }
    { 11012110;;Retention Amount   ;Decimal       ;CaptionML=[ENU=Retention Amount;
                                                              NOR=Tilbakeholdt bel›p;
                                                              SVE=Kvarst†ende belopp];
                                                   Description=Retention;
                                                   AutoFormatType=1 }
    { 11012120;;Retention Amount (ACY);Decimal    ;CaptionML=[ENU=Retention Amount (ACY);
                                                              NOR=Tilbakeholdt bel›p (AVAL);
                                                              SVE=Kvarst†ende belopp (AVAL)];
                                                   Description=Retention;
                                                   AutoFormatType=1 }
    { 11012130;;Applies-to Retention ID;Integer   ;CaptionML=[ENU=Applies-to Retention ID;
                                                              NOR=Utlignings-gjenv‘rende-ID;
                                                              SVE=G„ller kvarst†ende ID];
                                                   Description=Retention }
    { 11012140;;Purchase Order     ;Code20        ;TableRelation="Purchase Header";
                                                   CaptionML=[ENU=Purchase Order;
                                                              NOR=Innkj›psordre;
                                                              SVE=Ink”psorder] }
    { 11012150;;Warranty Object    ;Code10        ;CaptionML=[ENU=Warranty Object;
                                                              NOR=Garantiobjekt;
                                                              SVE=Garantiobjekt] }
    { 11012160;;Extension Contract ;Code10        ;CaptionML=[ENU=Extension Contract;
                                                              NOR=ETA;
                                                              SVE=ŽTA] }
    { 11012170;;Cost Type Cost Plus Line;Option   ;CaptionML=[ENU=Cost Type Cost Plus Line;
                                                              NOR=Kostnadstype, selvkostrad;
                                                              SVE=Kostnadstyp, sj„lvkostnadsrad];
                                                   OptionCaptionML=[ENU=Labor,Material,Subcontracting,Plant,Sundry,Revenue;
                                                                    NOR=Arbeidskraft,Materiale,Underentrepen›r,Maskin,Diverse,Inntekt;
                                                                    SVE=Arbetskraft,Material,Underentreprenad,Maskin,Diverse,Int„kt];
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry,Revenue;
                                                   Editable=No }
    { 11012180;;Plant Invoice      ;Boolean       ;CaptionML=[ENU=Plant Invoice;
                                                              NOR=Maskinfaktura;
                                                              SVE=Maskinfaktura];
                                                   Editable=No }
    { 11012190;;Rental Unit        ;Code20        ;CaptionML=[ENU=Rental Unit;
                                                              NOR=Leieenhet;
                                                              SVE=Uthyrningsenhet] }
    { 11012191;;Rental Unit Line Type;Option      ;CaptionML=[ENU=Rental Unit Line Type;
                                                              NOR=Leieenhetsradtype;
                                                              SVE=Uthyrningsenhetsradtyp];
                                                   OptionCaptionML=[ENU=" ,Rent,Service Cost,Utilities,One-off Costs,Deposit";
                                                                    NOR=" ,Leie,Servicekost,Verkt›y";
                                                                    SVE=" ,Hyra,Servicekostnad,Verktyg,Eng†ngskostnad,Deposition"];
                                                   OptionString=[ ,Rent,ServiceCost,Utilities,One-off Costs,Deposit] }
    { 11012200;;Buffer Line No.    ;Integer       ;CaptionML=[ENU=Buffer Line No.;
                                                              NOR=Bufferradnr;
                                                              SVE=Buffertradnr] }
    { 11012210;;Posting Date       ;Date          ;CaptionML=[ENU=Posting Date;
                                                              NOR=Bokf›ringsdato;
                                                              SVE=Bokf”ringsdatum] }
    { 11012220;;Advance Payment    ;Boolean       ;CaptionML=ENU=Advance Payment;
                                                   Editable=No }
    { 11012620;;Generated Retention Line;Boolean  ;CaptionML=ENU=Generated Retention Line }
    { 11012900;;Expense Allowance Scheme;Boolean  ;CaptionML=[ENU=Expense Allowance Scheme;
                                                              NOR=Skjema for utgiftskvote;
                                                              SVE=Schema f”r kostnadsplan] }
    { 11128011;;Periodic Template Code;Code10     ;DataClassification=ToBeClassified }
    { 11128012;;Periodic Starting Date;Date       ;DataClassification=ToBeClassified }
  }
  KEYS
  {
    {    ;Type,G/L Account,Gen. Bus. Posting Group,Gen. Prod. Posting Group,VAT Bus. Posting Group,VAT Prod. Posting Group,Dimension Set ID,Job No.,Fixed Asset Line No.,Deferral Code,Description,Bal. Account No.,Service Order No.,Service Contract No.,Buy-Back Item (Plant Order),Warranty Object,Buffer Line No.,FA Posting Type,Description 2,Auto. Acc. Group;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimMgt@1000 : Codeunit 408;

    [External]
    PROCEDURE PrepareSales@1(VAR SalesLine@1000 : Record 37);
    VAR
      PlantNumber@1100527001 : Record 11012552;
      PlantType@1100527000 : Record 11012551;
    BEGIN
      CLEAR(Rec);
      Type := SalesLine.Type;
      "System-Created Entry" := TRUE;
      "Gen. Bus. Posting Group" := SalesLine."Gen. Bus. Posting Group";
      "Gen. Prod. Posting Group" := SalesLine."Gen. Prod. Posting Group";
      "VAT Bus. Posting Group" := SalesLine."VAT Bus. Posting Group";
      "VAT Prod. Posting Group" := SalesLine."VAT Prod. Posting Group";
      "VAT Calculation Type" := SalesLine."VAT Calculation Type";
      "Global Dimension 1 Code" := SalesLine."Shortcut Dimension 1 Code";
      "Global Dimension 2 Code" := SalesLine."Shortcut Dimension 2 Code";
      "Dimension Set ID" := SalesLine."Dimension Set ID";
      "Advance Payment" := SalesLine."Advance Payment";
      "Job No." := SalesLine."Job No.";
      "VAT %" := SalesLine."VAT %";
      "VAT Difference" := SalesLine."VAT Difference";
      IF Type = Type::"Fixed Asset" THEN BEGIN
        "FA Posting Date" := SalesLine."FA Posting Date";
        "Depreciation Book Code" := SalesLine."Depreciation Book Code";
        "Depr. until FA Posting Date" := SalesLine."Depr. until FA Posting Date";
        "Duplicate in Depreciation Book" := SalesLine."Duplicate in Depreciation Book";
        "Use Duplication List" := SalesLine."Use Duplication List";
      END;

      IF "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" THEN
        SetSalesTaxForSalesLine(SalesLine);

      DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Global Dimension 1 Code","Global Dimension 2 Code");
      //>>NAVSE,NAVFI
      "Auto. Acc. Group" := SalesLine."Auto. Acc. Group";
      //<<NAVSE,NAVFI

      IF SalesLine."Line Discount %" = 100 THEN BEGIN
        "VAT Base Amount" := 0;
        "VAT Base Amount (ACY)" := 0;
        "VAT Amount" := 0;
        "VAT Amount (ACY)" := 0;
      END;

      //**4PS.sn
      "Cost Component" := SalesLine."Cost Component";
      Element := SalesLine.Element;
      "Service Order No." := SalesLine."Service Order No.";
      "Service Contract No." := SalesLine."Service Contract No.";
      IF "Service Contract No." <> '' THEN
        "Origin Type" := "Origin Type"::Service
      ELSE
        IF "Job No." <> '' THEN
          "Origin Type" := "Origin Type"::Project;

      "Extension Contract" := SalesLine."Extension Contract";
      "Plant Invoice" := SalesLine."Plant Invoice";
      "Rental Unit" := SalesLine."Rental Unit";
      "Rental Unit Line Type" := SalesLine."Rental Unit Line Type";
      "Service Location No." := SalesLine."Service Location No.";
      "Cost Type Cost Plus Line" := SalesLine."Cost Type Cost Plus Line";

      IF SalesLine."Currency Code" = '' THEN
        "Retention Amount" := SalesLine.RetentionAmount(0)
      ELSE BEGIN
        "Retention Amount" := SalesLine.RetentionAmountLCY(0);
        "Retention Amount (ACY)" := SalesLine.RetentionAmount(0);
      END;
      IF NOT (SalesLine."Document Type" IN [SalesLine."Document Type"::"Return Order",SalesLine."Document Type"::"Credit Memo"]) THEN BEGIN
        "Retention Amount" := - "Retention Amount";
        "Retention Amount (ACY)" := - "Retention Amount (ACY)";
      END;
      "Applies-to Retention ID" := SalesLine."Applies-to Retention ID";

      IF SalesLine."Plant Type" <> '' THEN BEGIN
        PlantType.GET(SalesLine."Plant Type");
        IF PlantType."Department Code" <> '' THEN
          "Global Dimension 1 Code" := PlantType."Department Code";
        IF (SalesLine."Plant No." <> '') AND (SalesLine."Plant No." <> '0') THEN BEGIN
          IF PlantNumber.GET(SalesLine."Plant Type", SalesLine."Plant No.") THEN BEGIN
            IF PlantNumber."Department Code" <> '' THEN
              "Global Dimension 1 Code" := PlantNumber."Department Code";
          END;
        END;
      END;

      DimMgt.ValidateShortcutDimValues(1, "Global Dimension 1 Code", "Dimension Set ID");
      //**4PS.en

      OnAfterInvPostBufferPrepareSales(SalesLine,Rec);
    END;

    [External]
    PROCEDURE CalcDiscount@2(PricesInclVAT@1000 : Boolean;DiscountAmount@1001 : Decimal;DiscountAmountACY@1002 : Decimal);
    VAR
      CurrencyLCY@1003 : Record 4;
      CurrencyACY@1004 : Record 4;
      GLSetup@1005 : Record 98;
    BEGIN
      CurrencyLCY.InitRoundingPrecision;
      GLSetup.GET;
      IF GLSetup."Additional Reporting Currency" <> '' THEN
        CurrencyACY.GET(GLSetup."Additional Reporting Currency")
      ELSE
        CurrencyACY := CurrencyLCY;
      "VAT Amount" := ROUND(
          CalcVATAmount(PricesInclVAT,DiscountAmount,"VAT %"),
          CurrencyLCY."Amount Rounding Precision",
          CurrencyLCY.VATRoundingDirection);
      "VAT Amount (ACY)" := ROUND(
          CalcVATAmount(PricesInclVAT,DiscountAmountACY,"VAT %"),
          CurrencyACY."Amount Rounding Precision",
          CurrencyACY.VATRoundingDirection);

      IF PricesInclVAT AND ("VAT %" <> 0) THEN BEGIN
        "VAT Base Amount" := DiscountAmount - "VAT Amount";
        "VAT Base Amount (ACY)" := DiscountAmountACY - "VAT Amount (ACY)";
      END ELSE BEGIN
        "VAT Base Amount" := DiscountAmount;
        "VAT Base Amount (ACY)" := DiscountAmountACY;
      END;
      Amount := "VAT Base Amount";
      "Amount (ACY)" := "VAT Base Amount (ACY)";
      "VAT Base Before Pmt. Disc." := "VAT Base Amount"
    END;

    LOCAL PROCEDURE CalcVATAmount@3(ValueInclVAT@1000 : Boolean;Value@1001 : Decimal;VATPercent@1002 : Decimal) : Decimal;
    BEGIN
      IF VATPercent = 0 THEN
        EXIT(0);
      IF ValueInclVAT THEN
        EXIT(Value / (1 + (VATPercent / 100)) * (VATPercent / 100));

      EXIT(Value * (VATPercent / 100));
    END;

    [External]
    PROCEDURE SetAccount@4(AccountNo@1000 : Code[20];VAR TotalVAT@1004 : Decimal;VAR TotalVATACY@1003 : Decimal;VAR TotalAmount@1002 : Decimal;VAR TotalAmountACY@1001 : Decimal);
    BEGIN
      TotalVAT := TotalVAT - "VAT Amount";
      TotalVATACY := TotalVATACY - "VAT Amount (ACY)";
      TotalAmount := TotalAmount - Amount;
      TotalAmountACY := TotalAmountACY - "Amount (ACY)";
      "G/L Account" := AccountNo;
    END;

    [External]
    PROCEDURE SetAmounts@5(TotalVAT@1003 : Decimal;TotalVATACY@1002 : Decimal;TotalAmount@1001 : Decimal;TotalAmountACY@1000 : Decimal;VATDifference@1004 : Decimal;TotalVATBase@1006 : Decimal;TotalVATBaseACY@1005 : Decimal);
    BEGIN
      Amount := TotalAmount;
      "VAT Base Amount" := TotalVATBase;
      "VAT Amount" := TotalVAT;
      "Amount (ACY)" := TotalAmountACY;
      "VAT Base Amount (ACY)" := TotalVATBaseACY;
      "VAT Amount (ACY)" := TotalVATACY;
      "VAT Difference" := VATDifference;
      "VAT Base Before Pmt. Disc." := TotalAmount;
    END;

    [External]
    PROCEDURE PreparePurchase@6(VAR PurchLine@1000 : Record 39);
    BEGIN
      CLEAR(Rec);
      Type := PurchLine.Type;
      "System-Created Entry" := TRUE;
      "Gen. Bus. Posting Group" := PurchLine."Gen. Bus. Posting Group";
      "Gen. Prod. Posting Group" := PurchLine."Gen. Prod. Posting Group";
      "VAT Bus. Posting Group" := PurchLine."VAT Bus. Posting Group";
      "VAT Prod. Posting Group" := PurchLine."VAT Prod. Posting Group";
      "VAT Calculation Type" := PurchLine."VAT Calculation Type";
      "Global Dimension 1 Code" := PurchLine."Shortcut Dimension 1 Code";
      "Global Dimension 2 Code" := PurchLine."Shortcut Dimension 2 Code";
      "Dimension Set ID" := PurchLine."Dimension Set ID";
      "Job No." := PurchLine."Job No.";
      "VAT %" := PurchLine."VAT %";
      "VAT Difference" := PurchLine."VAT Difference";
      IF Type = Type::"Fixed Asset" THEN BEGIN
        "FA Posting Date" := PurchLine."FA Posting Date";
        "Depreciation Book Code" := PurchLine."Depreciation Book Code";
        "Depr. until FA Posting Date" := PurchLine."Depr. until FA Posting Date";
        "Duplicate in Depreciation Book" := PurchLine."Duplicate in Depreciation Book";
        "Use Duplication List" := PurchLine."Use Duplication List";
        "FA Posting Type" := PurchLine."FA Posting Type";
        "Depreciation Book Code" := PurchLine."Depreciation Book Code";
        "Salvage Value" := PurchLine."Salvage Value";
        "Depr. Acquisition Cost" := PurchLine."Depr. Acquisition Cost";
        "Maintenance Code" := PurchLine."Maintenance Code";
        "Insurance No." := PurchLine."Insurance No.";
        "Budgeted FA No." := PurchLine."Budgeted FA No.";
      END;
      //**4PS.sn
      Quantity := PurchLine."Qty. to Invoice (Base)"; //C-02732
      "Cost Component" := PurchLine."Cost Component";
      Element := PurchLine.Element;
      "Service Order No." := PurchLine."Service Order No.";
      "Service Contract No." := PurchLine."Service Contract No.";
      "Employee No." := PurchLine."Employee No.";
      IF "Service Contract No." <> '' THEN
        "Origin Type" := "Origin Type"::Service
      ELSE
        IF "Job No." <> '' THEN
          "Origin Type" := "Origin Type"::Project;
      "Extension Contract" := PurchLine."Extension Contract";
      "Service Location No." := PurchLine."Service Location No.";
      "Generated Retention Line" := PurchLine."Generated Retention Line"; //DP01406
      "Applies-to Retention ID" := PurchLine."Applies-to Retention ID";
      //**4PS.en

      IF "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" THEN
        SetSalesTaxForPurchLine(PurchLine);

      DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Global Dimension 1 Code","Global Dimension 2 Code");
      //>>NAVSE,NAVFI
      "Auto. Acc. Group" := PurchLine."Auto. Acc. Group";
      //<<NAVSE,NAVFI

      IF PurchLine."Line Discount %" = 100 THEN BEGIN
        "VAT Base Amount" := 0;
        "VAT Base Amount (ACY)" := 0;
        "VAT Amount" := 0;
        "VAT Amount (ACY)" := 0;
      END;

      OnAfterInvPostBufferPreparePurchase(PurchLine,Rec);
    END;

    [External]
    PROCEDURE CalcDiscountNoVAT@7(DiscountAmount@1001 : Decimal;DiscountAmountACY@1002 : Decimal);
    BEGIN
      "VAT Base Amount" := DiscountAmount;
      "VAT Base Amount (ACY)" := DiscountAmountACY;
      Amount := "VAT Base Amount";
      "Amount (ACY)" := "VAT Base Amount (ACY)";
      "VAT Base Before Pmt. Disc." := "VAT Base Amount";
    END;

    [External]
    PROCEDURE SetSalesTaxForPurchLine@8(PurchaseLine@1000 : Record 39);
    BEGIN
      "Tax Area Code" := PurchaseLine."Tax Area Code";
      "Tax Liable" := PurchaseLine."Tax Liable";
      "Tax Group Code" := PurchaseLine."Tax Group Code";
      "Use Tax" := PurchaseLine."Use Tax";
      Quantity := PurchaseLine."Qty. to Invoice (Base)";
    END;

    [External]
    PROCEDURE SetSalesTaxForSalesLine@15(SalesLine@1000 : Record 37);
    BEGIN
      "Tax Area Code" := SalesLine."Tax Area Code";
      "Tax Liable" := SalesLine."Tax Liable";
      "Tax Group Code" := SalesLine."Tax Group Code";
      "Use Tax" := FALSE;
      Quantity := SalesLine."Qty. to Invoice (Base)";
    END;

    [External]
    PROCEDURE ReverseAmounts@9();
    BEGIN
      Amount := -Amount;
      "VAT Base Amount" := -"VAT Base Amount";
      "Amount (ACY)" := -"Amount (ACY)";
      "VAT Base Amount (ACY)" := -"VAT Base Amount (ACY)";
      "VAT Amount" := -"VAT Amount";
      "VAT Amount (ACY)" := -"VAT Amount (ACY)";
    END;

    [External]
    PROCEDURE SetAmountsNoVAT@10(TotalAmount@1001 : Decimal;TotalAmountACY@1000 : Decimal;VATDifference@1004 : Decimal);
    BEGIN
      Amount := TotalAmount;
      "VAT Base Amount" := TotalAmount;
      "VAT Amount" := 0;
      "Amount (ACY)" := TotalAmountACY;
      "VAT Base Amount (ACY)" := TotalAmountACY;
      "VAT Amount (ACY)" := 0;
      "VAT Difference" := VATDifference;
    END;

    [External]
    PROCEDURE PrepareService@11(VAR ServiceLine@1000 : Record 5902);
    BEGIN
      CLEAR(Rec);
      CASE ServiceLine.Type OF
        ServiceLine.Type::Item:
          Type := Type::Item;
        ServiceLine.Type::Resource:
          Type := Type::Resource;
        ServiceLine.Type::"G/L Account":
          Type := Type::"G/L Account";
      END;
      "System-Created Entry" := TRUE;
      "Gen. Bus. Posting Group" := ServiceLine."Gen. Bus. Posting Group";
      "Gen. Prod. Posting Group" := ServiceLine."Gen. Prod. Posting Group";
      "VAT Bus. Posting Group" := ServiceLine."VAT Bus. Posting Group";
      "VAT Prod. Posting Group" := ServiceLine."VAT Prod. Posting Group";
      "VAT Calculation Type" := ServiceLine."VAT Calculation Type";
      "Global Dimension 1 Code" := ServiceLine."Shortcut Dimension 1 Code";
      "Global Dimension 2 Code" := ServiceLine."Shortcut Dimension 2 Code";
      "Dimension Set ID" := ServiceLine."Dimension Set ID";
      "Job No." := ServiceLine."Job No.";
      "VAT %" := ServiceLine."VAT %";
      "VAT Difference" := ServiceLine."VAT Difference";
      IF "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" THEN BEGIN
        "Tax Area Code" := ServiceLine."Tax Area Code";
        "Tax Group Code" := ServiceLine."Tax Group Code";
        "Tax Liable" := ServiceLine."Tax Liable";
        "Use Tax" := FALSE;
        Quantity := ServiceLine."Qty. to Invoice (Base)";
      END;

      OnAfterInvPostBufferPrepareService(ServiceLine,Rec);
    END;

    [External]
    PROCEDURE FillPrepmtAdjBuffer@81(VAR TempInvoicePostBuffer@1005 : TEMPORARY Record 49;InvoicePostBuffer@1006 : Record 49;GLAccountNo@1001 : Code[20];AdjAmount@1003 : Decimal;RoundingEntry@1004 : Boolean);
    VAR
      PrepmtAdjInvPostBuffer@1002 : Record 49;
    BEGIN
      WITH PrepmtAdjInvPostBuffer DO BEGIN
        INIT;
        Type := Type::"Prepmt. Exch. Rate Difference";
        "G/L Account" := GLAccountNo;
        Amount := AdjAmount;
        IF RoundingEntry THEN
          "Amount (ACY)" := AdjAmount
        ELSE
          "Amount (ACY)" := 0;
        "Dimension Set ID" := InvoicePostBuffer."Dimension Set ID";
        "Global Dimension 1 Code" := InvoicePostBuffer."Global Dimension 1 Code";
        "Global Dimension 2 Code" := InvoicePostBuffer."Global Dimension 2 Code";
        "System-Created Entry" := TRUE;
        InvoicePostBuffer := PrepmtAdjInvPostBuffer;

        TempInvoicePostBuffer := InvoicePostBuffer;
        IF TempInvoicePostBuffer.FIND THEN BEGIN
          TempInvoicePostBuffer.Amount += InvoicePostBuffer.Amount;
          TempInvoicePostBuffer."Amount (ACY)" += InvoicePostBuffer."Amount (ACY)";
          TempInvoicePostBuffer.MODIFY;
        END ELSE BEGIN
          TempInvoicePostBuffer := InvoicePostBuffer;
          TempInvoicePostBuffer.INSERT;
        END;
      END;
    END;

    [External]
    PROCEDURE Update@12(InvoicePostBuffer@1000 : Record 49;VAR InvDefLineNo@1001 : Integer;VAR DeferralLineNo@1002 : Integer);
    BEGIN
      OnBeforeInvPostBufferUpdate(Rec,InvoicePostBuffer);

      Rec := InvoicePostBuffer;

      //**4PS.sn NAV2017 moved from cod 80/90
      SETRANGE("Extension Contract","Extension Contract");
      SETRANGE("Service Location No.","Service Location No.");
      SETRANGE("Expense Allowance Scheme","Expense Allowance Scheme");
      SETRANGE("Plant Invoice","Plant Invoice");
      SETRANGE("Rental Unit","Rental Unit");
      SETRANGE("Rental Unit Line Type","Rental Unit Line Type");
      SETRANGE("Service Location No.","Service Location No.");
      SETRANGE("Cost Type Cost Plus Line","Cost Type Cost Plus Line");
      //**4PS.en

      IF FIND THEN BEGIN
        Amount += InvoicePostBuffer.Amount;
        "VAT Amount" += InvoicePostBuffer."VAT Amount";
        "VAT Base Amount" += InvoicePostBuffer."VAT Base Amount";
        "Amount (ACY)" += InvoicePostBuffer."Amount (ACY)";
        "VAT Amount (ACY)" += InvoicePostBuffer."VAT Amount (ACY)";
        "VAT Difference" += InvoicePostBuffer."VAT Difference";
        "VAT Base Amount (ACY)" += InvoicePostBuffer."VAT Base Amount (ACY)";
        Quantity += InvoicePostBuffer.Quantity;
        "VAT Base Before Pmt. Disc." += InvoicePostBuffer."VAT Base Before Pmt. Disc.";
        //**4PS.sn
        "Retention Amount" += InvoicePostBuffer."Retention Amount";
        "Retention Amount (ACY)" += InvoicePostBuffer."Retention Amount (ACY)";
        //**4PS.en
        IF NOT InvoicePostBuffer."System-Created Entry" THEN
          "System-Created Entry" := FALSE;
        IF "Deferral Code" = '' THEN
          AdjustRoundingForUpdate;
        OnBeforeInvPostBufferModify(Rec,InvoicePostBuffer);
        MODIFY;
        OnAfterInvPostBufferModify(Rec,InvoicePostBuffer);
        InvDefLineNo := "Deferral Line No.";
      END ELSE BEGIN
        IF "Deferral Code" <> '' THEN BEGIN
          DeferralLineNo := DeferralLineNo + 1;
          "Deferral Line No." := DeferralLineNo;
          InvDefLineNo := "Deferral Line No.";
        END;
      //INSERT; //**4PS.o
      //**4PS.sn
        "Buffer Line No." := 1;
        WHILE NOT INSERT DO
          "Buffer Line No." := "Buffer Line No." + 1;
      //**4PS.en
      END;
      RESET; //**4PS.n

      OnAfterInvPostBufferUpdate(Rec,InvoicePostBuffer);
    END;

    [External]
    PROCEDURE UpdateVATBase@17(VAR TotalVATBase@1001 : Decimal;VAR TotalVATBaseACY@1000 : Decimal);
    BEGIN
      TotalVATBase := TotalVATBase - "VAT Base Amount";
      TotalVATBaseACY := TotalVATBaseACY - "VAT Base Amount (ACY)"
    END;

    LOCAL PROCEDURE AdjustRoundingForUpdate@20();
    BEGIN
      AdjustRoundingFieldsPair(Amount,"Amount (ACY)");
      AdjustRoundingFieldsPair("VAT Amount","VAT Amount (ACY)");
      AdjustRoundingFieldsPair("VAT Base Amount","VAT Base Amount (ACY)");
    END;

    LOCAL PROCEDURE AdjustRoundingFieldsPair@21(VAR Value1@1000 : Decimal;VAR Value2@1001 : Decimal);
    BEGIN
      IF (Value1 = 0) AND (Value2 <> 0) THEN
        Value2 := 0;
      IF (Value1 <> 0) AND (Value2 = 0) THEN
        Value1 := 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInvPostBufferPrepareSales@13(VAR SalesLine@1000 : Record 37;VAR InvoicePostBuffer@1001 : Record 49);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInvPostBufferPreparePurchase@14(VAR PurchaseLine@1000 : Record 39;VAR InvoicePostBuffer@1001 : Record 49);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInvPostBufferPrepareService@16(VAR ServiceLine@1000 : Record 5902;VAR InvoicePostBuffer@1001 : Record 49);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInvPostBufferModify@23(VAR InvoicePostBuffer@1000 : Record 49;FromInvoicePostBuffer@1001 : Record 49);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInvPostBufferUpdate@19(VAR InvoicePostBuffer@1000 : Record 49;VAR FromInvoicePostBuffer@1001 : Record 49);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInvPostBufferUpdate@18(VAR InvoicePostBuffer@1000 : Record 49;VAR FromInvoicePostBuffer@1001 : Record 49);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInvPostBufferModify@22(VAR InvoicePostBuffer@1000 : Record 49;FromInvoicePostBuffer@1001 : Record 49);
    BEGIN
    END;

    BEGIN
    {
      4PS
      D. Borkent, 15-08-2005:
      - Fields (Tax Area Code, Tax Liable, Tax Group Code, Use Tax) deleted from index
        because of limit (252 bytes) when collecting invoices per serviceorder/contract
    }
    END.
  }
}

