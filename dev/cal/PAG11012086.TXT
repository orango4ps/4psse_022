OBJECT Page 11012086 Prognosis Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Prognosis Lines;
               NOR=Prognoserader;
               SVE=Prognosrader];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11012035;
    DelayedInsert=Yes;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 JobsSetup.GET;
                 SpecificationsVisible := JobsSetup."Prognosis Specifications";
                 PrognosisLineModified := FALSE;
               END;

    OnAfterGetRecord=BEGIN
                       SetPreviousDateFilter;
                       CalculateValues(Rec);
                     END;

    OnNewRecord=BEGIN
                  SetValuesToZero;
                END;

    OnModifyRecord=BEGIN
                     IF LastLevel THEN
                       UpdateRow;
                   END;

    OnAfterGetCurrRecord=BEGIN
                           LastLevel := Level = 3;
                           IF LastLevel THEN
                             UpdateRow;

                           SetPreviousDateFilter;
                           CalculateValues(Rec);

                           LaborEditable := LastLevel AND ("Cost Type" = "Cost Type"::Labor);

                           IF PrognosisLineModified THEN
                             IF LastLevel THEN
                               UpdateFactBoxes()
                             ELSE
                               PrognosisLineModified := FALSE
                           ELSE
                             UpdateFactBoxes();

                           CalcFlowFieldsOfPrognosisLine(Rec);

                           CALCFIELDS("Project Related Name", "Cost Object Description");
                           IF "Project Related Name" <> '' THEN
                             "Cost Object Description" := "Project Related Name";
                         END;

    ActionList=ACTIONS
    {
      { 1100525005;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100525003;1 ;ActionGroup;
                      CaptionML=[ENU=Line;
                                 NOR=RAD;
                                 SVE=Rad] }
      { 1100525001;2 ;Action    ;
                      Name=Specifications;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=Specifications;
                                 NOR=Spesifikasjoner;
                                 SVE=Specifikationer];
                      Promoted=Yes;
                      Visible=SpecificationsVisible;
                      Enabled=LastLevel;
                      PromotedIsBig=Yes;
                      Image=SetupList;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowDetailLines();
                               END;
                                }
      { 1100529702;2 ;Action    ;
                      Name=New Line;
                      ShortCutKey=Ctrl+N;
                      CaptionML=[ENU=New Line;
                                 NOR=Ny linje;
                                 SVE=Ny rad];
                      Promoted=Yes;
                      Enabled=NotFixed;
                      PromotedIsBig=Yes;
                      Image=NewDocument;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 InsertRow;
                               END;
                                }
      { 1100529701;2 ;Action    ;
                      Name=Delete row;
                      ShortCutKey=Ctrl+D;
                      CaptionML=ENU=Delete row;
                      Promoted=Yes;
                      Enabled=DeleteEnabled;
                      PromotedIsBig=Yes;
                      Image=Delete;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DeleteRow;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                IndentationColumnName=Level;
                ShowAsTree=Yes;
                GroupType=Repeater }

    { 1100529700;2;Field  ;
                Name=Level Description;
                CaptionML=ENU=Level Description;
                SourceExpr=TreeDescription;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 2   ;2   ;Field     ;
                SourceExpr=Element;
                Visible=ElementVisible;
                Editable=FALSE;
                HideValue=HideElementValue }

    { 4   ;2   ;Field     ;
                SourceExpr="Element Description";
                Visible=ElementVisible;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="Cost Object";
                Editable=FALSE;
                LookupPageID=Cost Object List;
                HideValue=HideCostObjectValue }

    { 8   ;2   ;Field     ;
                SourceExpr="Unit Cost Object";
                Visible=FALSE;
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr="Cost Object Description";
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                CaptionML=[NOR=Budsjett;
                           SVE=Budget];
                SourceExpr="Budget Amount";
                Style=Strong;
                StyleExpr=SumLine }

    { 14  ;2   ;Field     ;
                CaptionML=[NOR=Total kost;
                           SVE=Totalkostnad];
                SourceExpr="Total Cost";
                Visible=FALSE;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 16  ;2   ;Field     ;
                CaptionML=[NOR=Tillatt;
                           SVE=TillÜten];
                SourceExpr="Allowed Costs";
                Style=Strong;
                StyleExpr=SumLine }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Available;
                           NOR=Disponibelt;
                           SVE=Disponibelt];
                SourceExpr=GetAvailable;
                Visible=FALSE;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Result;
                           NOR=Resultat;
                           SVE=Resultat];
                SourceExpr=Result;
                Visible=FALSE;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 24  ;2   ;Field     ;
                SourceExpr="Previous Prognosis";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 1100485010;2;Field  ;
                SourceExpr="Risc Coverage";
                Visible=FALSE;
                Editable=LastLevel;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO("Risc Coverage"));
                           END;
                            }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           NOR=Antall;
                           SVE=Antal];
                SourceExpr=Quantity;
                Visible=FALSE;
                Editable=LastLevel;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO(Quantity));
                           END;
                            }

    { 28  ;2   ;Field     ;
                SourceExpr="Unit of Measure";
                Visible=FALSE;
                Editable=LastLevel;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO("Unit of Measure"));
                           END;
                            }

    { 1100529703;2;Field  ;
                Name=RateCode;
                CaptionML=[ENU=Rate Code;
                           NOR=Avgiftskode;
                           SVE=Avgiftskod];
                SourceExpr=RateCode;
                Visible=FALSE;
                Editable=LaborEditable;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO("Rate Code"));
                           END;

                OnLookup=VAR
                           BudgetRate@1100529700 : Record 11012000;
                         BEGIN
                           BudgetRate.SETRANGE("Project Filter", "Project No.");
                           BudgetRate.SETRANGE("Starting Date", 0D, "Prognosis Date");
                           BudgetRate.SETFILTER("Ending Date", '%1|>=%2', 0D, "Prognosis Date");
                           IF BudgetRate.GET("Rate Code") THEN;

                           IF PAGE.RUNMODAL(0, BudgetRate) = ACTION::LookupOK THEN BEGIN
                             RateCode := BudgetRate.Code;
                             SaveAndRefresh(FIELDNO("Rate Code"));
                           END
                         END;
                          }

    { 30  ;2   ;Field     ;
                CaptionML=[ENU=Price/Rate;
                           NOR=Pris/gebyr;
                           SVE=Pris/avgift];
                SourceExpr=PriceDec;
                Visible=FALSE;
                Editable=LastLevel;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO(Price));
                           END;
                            }

    { 1100525002;2;Field  ;
                CaptionML=[ENU=Amount;
                           NOR=Belõp;
                           SVE=Belopp];
                SourceExpr=Amount;
                Editable=LastLevel;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO(Amount));
                           END;

                OnDrillDown=BEGIN
                              IF SumLine THEN
                                EXIT;
                              SaveAndRefresh(FIELDNO(Amount));
                              AmountOnDrillDown;
                              FillData(PrognosisGlobal);
                              CurrPage.UPDATE(FALSE);
                            END;
                             }

    { 1100528500;2;Field  ;
                Lookup=No;
                DrillDown=No;
                SourceExpr="Specification Line Exists";
                Visible=FALSE }

    { 1210190004;2;Field  ;
                SourceExpr="Total Risc Coverage Amount" }

    { 1210190027;2;Field  ;
                SourceExpr="Total Release Risc Coverage";
                Visible=FALSE }

    { 1100525000;2;Field  ;
                SourceExpr="Ovh. Surcharge";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 32  ;2   ;Field     ;
                SourceExpr="Amount incl. Surcharge";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO("Amount incl. Surcharge"));
                           END;
                            }

    { 1100485004;2;Field  ;
                CaptionML=[NOR=Kalkulert Innkjõpsresultat;
                           SVE=Kalkylerat inkîpsresultat];
                SourceExpr="Estimated Purchase Result";
                Visible=FALSE }

    { 34  ;2   ;Field     ;
                CaptionML=[ENU=Prognosis End Result;
                           NOR=Forventet Sluttresultat;
                           SVE=FîrvÑntat slutresultat];
                SourceExpr="Prognosis End Result";
                Editable=LastLevel;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO("Prognosis End Result"));
                           END;
                            }

    { 1100529000;2;Field  ;
                CaptionML=ENU=Difference with Prev Prognosis End Result;
                SourceExpr="Diff. with Prev. End Result";
                StyleExpr=DiffPrevPrognLineStyle }

    { 1100529704;2;Field  ;
                SourceExpr="Prognosis End Result Hours";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 1100485002;2;Field  ;
                SourceExpr="Prognosis Total Cost";
                Editable=LastLevel;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO("Prognosis Total Cost"))
                           END;
                            }

    { 1100529705;2;Field  ;
                SourceExpr="Prognosis Total Hours";
                Visible=FALSE;
                Editable=LastLevel;
                Style=Strong;
                StyleExpr=SumLine;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO("Prognosis Total Hours"))
                           END;
                            }

    { 1210190000;2;Field  ;
                SourceExpr=Comment;
                Editable=LastLevel;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO(Comment));
                           END;
                            }

    { 1100528900;2;Field  ;
                SourceExpr="Product Discount (Purchase)";
                Visible=FALSE }

    { 1100485006;2;Field  ;
                CaptionML=[NOR=èpne innkjõp;
                           SVE=ôppna inkîp];
                SourceExpr="Open (Purchase)";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 1100485008;2;Field  ;
                CaptionML=[NOR=Mottatt/ikke fakturert;
                           SVE=Mottaget/inte fakturerat];
                SourceExpr="Received/Not Invoiced";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=SumLine }

    { 1100525004;2;Field  ;
                SourceExpr="Open Ovh. Surch. (Purchase)";
                Visible=FALSE;
                Editable=FALSE }

    { 1100525006;2;Field  ;
                SourceExpr="Open (Inventory)";
                Visible=FALSE;
                Editable=FALSE }

    { 1100525008;2;Field  ;
                SourceExpr="Open Ovh. Surch. (Inventory)";
                Visible=FALSE;
                Editable=FALSE }

    { 1100525010;2;Field  ;
                SourceExpr="Open Ovh. Surch. (Hours)";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190002;2;Field  ;
                CaptionML=[NOR=Faktiske koster;
                           SVE=Verkliga kostnader];
                SourceExpr="Actual Costs" }

    { 22  ;2   ;Field     ;
                SourceExpr="Previous Date";
                Visible=FALSE }

    { 1100485000;2;Field  ;
                SourceExpr="Prev Prognosis End Result" }

    { 1100528300;2;Field  ;
                SourceExpr="Prev Prognosis Total Cost";
                Visible=FALSE }

    { 1100529001;2;Field  ;
                CaptionML=[NOR=Opprinnelig budsjett;
                           SVE=Ursprunglig budget];
                SourceExpr="Original Budget Amount";
                Visible=FALSE }

    { 1100529002;2;Field  ;
                CaptionML=[NOR=Justeringer;
                           SVE=Justeringar];
                SourceExpr=Adjustments;
                Visible=FALSE }

    { 1100529003;2;Field  ;
                CaptionML=[NOR=Direktenr.;
                           SVE=Extension];
                SourceExpr=Extension;
                Visible=FALSE }

    { 1100525007;2;Field  ;
                CaptionML=[NOR=Alternativer;
                           SVE=Alternativ];
                SourceExpr=Options;
                Visible=FALSE }

    { 1100529004;2;Field  ;
                CaptionML=[NOR=Innkjõpsbudsjett;
                           SVE=Inkîpsbudget];
                SourceExpr="Purchase Budget" }

    { 1100529005;2;Field  ;
                CaptionML=[NOR=Innkjõp;
                           SVE=Inkîp];
                SourceExpr="Purchase Amount" }

    { 1100528400;2;Field  ;
                SourceExpr="Purchase Amount (Get Orders)";
                Visible=FALSE }

    { 1100528401;2;Field  ;
                CaptionML=[NOR=Innkjõp (Innkjõpshandling);
                           SVE=Inkîp (inkîpsÜtgÑrd)];
                SourceExpr="Purchase (Purchase Action)";
                Visible=FALSE }

    { 1100527300;2;Field  ;
                CaptionML=ENU=Purchase (without Purchase Action);
                SourceExpr="Purchase Amount" - "Purchase (Purchase Action)";
                Visible=FALSE }

    { 1100529006;2;Field  ;
                CaptionML=[NOR=Reelle Kostnader (Innkjõphandling);
                           SVE=Verkliga kostnader (inkîpsÜtgÑrd)];
                SourceExpr="Actual Costs (Purchase Action)";
                Visible=FALSE }

    { 1100529007;2;Field  ;
                CaptionML=ENU=Actual Costs (without Purchase);
                SourceExpr="Actual Costs" - "Actual Costs (Purchase Action)";
                Visible=FALSE }

    { 1100528200;2;Field  ;
                SourceExpr="Actual Costs (Not P. O. Rel.)";
                Visible=FALSE }

    { 1100529900;2;Field  ;
                SourceExpr="Received/To Process";
                Visible=FALSE;
                Editable=LastLevel;
                OnValidate=BEGIN
                             SaveAndRefresh(FIELDNO("Received/To Process"));
                           END;
                            }

    { 1100529600;2;Field  ;
                CaptionML=[NOR=Innkjõpsresultat;
                           SVE=Inkîpsresultat];
                SourceExpr="Purchase Result";
                Visible=FALSE }

    { 1100527450;2;Field  ;
                CaptionML=ENU=Hours Available;
                SourceExpr=GetAvailableHours;
                Visible=FALSE;
                Editable=FALSE }

    { 1100527451;2;Field  ;
                SourceExpr="Budget Hours";
                Visible=FALSE }

    { 1100527452;2;Field  ;
                SourceExpr="Actual Hours";
                Visible=FALSE }

    { 1100529603;2;Field  ;
                SourceExpr="Remaining Budget";
                Visible=FALSE }

    { 1100527350;2;Field  ;
                SourceExpr="Allowed %";
                Visible=FALSE }

    { 1100529707;0;Container;
                ContainerType=FactBoxArea }

    { 1100529706;1;Part   ;
                Name=BudgetFB;
                CaptionML=[ENU=Budget;
                           NOR=Budsjett;
                           SVE=Budget];
                SubPageLink=Project No.=FIELD(Project No.);
                PagePartID=Page11129844 }

    { 1100529708;1;Part   ;
                Name=CostsFB;
                CaptionML=[ENU=Costs;
                           NOR=Kostnader;
                           SVE=Kostnader];
                PagePartID=Page11129845;
                PartType=Page }

    { 1100529709;1;Part   ;
                Name=HoursFB;
                CaptionML=[ENU=Hours;
                           NOR="Timer ";
                           SVE=Timmar];
                PagePartID=Page11129846;
                PartType=Page }

    { 1100529601;1;Part   ;
                CaptionML=[ENU=Prognosis;
                           NOR=Prognose;
                           SVE=Prognos];
                SubPageLink=Project No.=FIELD(Project No.),
                            Prognosis Date=FIELD(Prognosis Date);
                PagePartID=Page11130128;
                PartType=Page }

    { 1100529602;1;Part   ;
                CaptionML=[ENU=Prognosis End Result;
                           NOR=Forventet Sluttresultat;
                           SVE=FîrvÑntat slutresultat];
                SubPageLink=Project No.=FIELD(Project No.),
                            Prognosis Date=FIELD(Prognosis Date);
                PagePartID=Page11130129;
                PartType=Page }

  }
  CODE
  {
    VAR
      JobsSetup@1100529601 : Record 315;
      RateCode@1100529711 : Code[10];
      PriceDec@1100529706 : Decimal;
      SpecificationsVisible@1100525008 : Boolean INDATASET;
      PrognosisGlobal@1100529702 : Record 11012034;
      LastLevel@1100529701 : Boolean INDATASET;
      SumLine@1100529705 : Boolean;
      LaborEditable@1100529703 : Boolean INDATASET;
      ElementVisible@1100529704 : Boolean INDATASET;
      NotFixed@1100529700 : Boolean INDATASET;
      DeleteEnabled@1100529709 : Boolean INDATASET;
      Text001@1100529710 : TextConst 'ENU=Remove prognosis line %1 %2 %3?';
      HideCostObjectValue@1100529603 : Boolean;
      HideElementValue@1100529602 : Boolean;
      ViewPrognPerElementByCostObj@1100529604 : Boolean;
      DiffPrevPrognLineStyle@1100529600 : Text INDATASET;
      PrognosisLineModified@1100527350 : Boolean;
      DiffWithPrevPrognosisEndResult@1100529000 : Decimal;

    PROCEDURE FillData@1100529700(Prognosis@1100529700 : Record 11012034);
    VAR
      PrognosisLine@1100529701 : Record 11012035;
      PrognosisSumLineTMP@1100529702 : TEMPORARY Record 11012035;
      PrognosisSumLineTMP_SecondLevel@1100529703 : TEMPORARY Record 11012035;
      CurrPrognosisLine@1100527000 : Record 11012035;
      Job@1100528400 : Record 11072003;
    BEGIN
      IF NOT Job.GET(Prognosis."Project No.") THEN
        EXIT;
      JobsSetup.GET;
      ViewPrognPerElementByCostObj := FALSE;
      PrognosisGlobal.GET(Prognosis."Project No.", Prognosis."Prognosis Date");
      NotFixed := NOT PrognosisGlobal.Fixed;
      CurrPrognosisLine.COPY(Rec);
      Rec.RESET; // To make sure that all temporary prognosis lines can be found.
      Rec.DELETEALL;
      WITH PrognosisLine DO
        IF Job."Prognosis per Element" OR
          (PrognosisGlobal."Prognosis Level" = PrognosisGlobal."Prognosis Level"::Element)
        THEN BEGIN
          ElementVisible := TRUE;
          IF JobsSetup."View Progn. per Element by CO" THEN BEGIN
            ViewPrognPerElementByCostObj := TRUE;
            SETCURRENTKEY("Project No.", "Prognosis Date", "Cost Type", "Cost Object", Element);
            SETRANGE("Project No.", Prognosis."Project No.");
            SETRANGE("Prognosis Date", Prognosis."Prognosis Date");
            SETFILTER("Cost Type", '<>%1', "Cost Type"::Revenue);
            IF FINDSET THEN
              REPEAT
                Rec.INIT;
                Rec."Project No." := "Project No.";
                Rec."Prognosis Date" := "Prognosis Date";
                Rec."Main Project No." := "Main Project No.";
                Rec."Cost Type" := "Cost Type";
                Rec."Cost Object" := '';
                Rec.Element := '';
                Rec.Level := 1;
                IF Rec.INSERT THEN BEGIN
                  PrognosisSumLineTMP := Rec;
                  PrognosisSumLineTMP.INSERT;
                END;
                SETRANGE("Cost Type", "Cost Type");
                REPEAT
                  Rec.INIT;
                  Rec."Project No." := "Project No.";
                  Rec."Prognosis Date" := "Prognosis Date";
                  Rec."Main Project No." := "Main Project No.";
                  Rec."Cost Type" := "Cost Type";
                  Rec."Cost Object" := "Cost Object";
                  Rec.Element := '-';
                  Rec.Level := 2;
                  IF Rec.INSERT THEN BEGIN
                    PrognosisSumLineTMP_SecondLevel := Rec;
                    PrognosisSumLineTMP_SecondLevel.INSERT;
                  END;
                  SETRANGE("Cost Object", "Cost Object");
                  REPEAT
                    Rec.INIT;
                    Rec.COPY(PrognosisLine);
                    Rec.Level := 3;
                    IF Rec.INSERT THEN BEGIN
                      CalculateValues(Rec);
                      UpdateSumRow(PrognosisSumLineTMP_SecondLevel, Rec, 1);
                      UpdateSumRow(PrognosisSumLineTMP, Rec, 1);
                    END
                  UNTIL NEXT = 0;
                  Rec := PrognosisSumLineTMP_SecondLevel;
                  Rec.MODIFY;
                  SETRANGE("Cost Object");
                UNTIL NEXT = 0;
                Rec := PrognosisSumLineTMP;
                Rec.MODIFY;
                SETFILTER("Cost Type", '<>%1', "Cost Type"::Revenue);
              UNTIL NEXT = 0;
          END ELSE BEGIN
            SETCURRENTKEY("Project No.", "Prognosis Date", Element, "Cost Type", "Cost Object");
            SETRANGE("Project No.", Prognosis."Project No.");
            SETRANGE("Prognosis Date", Prognosis."Prognosis Date");
            SETFILTER("Cost Type", '<>%1', "Cost Type"::Revenue);
            IF FINDSET THEN
              REPEAT
                Rec.INIT;
                Rec."Project No." := "Project No.";
                Rec."Prognosis Date" := "Prognosis Date";
                Rec."Main Project No." := "Main Project No.";
                Rec.Element := Element;
                Rec."Cost Type" := -1;
                Rec."Cost Object" := '';
                Rec.Level := 1;
                IF Rec.INSERT THEN BEGIN
                  PrognosisSumLineTMP := Rec;
                  PrognosisSumLineTMP.INSERT;
                END;
                SETRANGE(Element, Element);
                REPEAT
                  Rec.INIT;
                  Rec."Project No." := "Project No.";
                  Rec."Prognosis Date" := "Prognosis Date";
                  Rec."Main Project No." := "Main Project No.";
                  Rec.Element := Element;
                  Rec."Cost Type" := "Cost Type";
                  Rec."Cost Object" := '-';
                  Rec.Level := 2;
                  IF Rec.INSERT THEN BEGIN
                    PrognosisSumLineTMP_SecondLevel := Rec;
                    PrognosisSumLineTMP_SecondLevel.INSERT;
                  END;
                  SETRANGE("Cost Type", "Cost Type");
                  REPEAT
                    Rec.INIT;
                    Rec.COPY(PrognosisLine);
                    Rec.Level := 3;
                    IF Rec.INSERT THEN BEGIN
                      CalculateValues(Rec);
                      UpdateSumRow(PrognosisSumLineTMP_SecondLevel, Rec, 1);
                      UpdateSumRow(PrognosisSumLineTMP, Rec, 1);
                    END
                  UNTIL NEXT = 0;
                  Rec := PrognosisSumLineTMP_SecondLevel;
                  Rec.MODIFY;
                  SETFILTER("Cost Type", '<>%1', "Cost Type"::Revenue);
                UNTIL NEXT = 0;
                Rec := PrognosisSumLineTMP;
                Rec.MODIFY;
                SETRANGE(Element);
              UNTIL NEXT = 0;
            END;
        END ELSE BEGIN
          ElementVisible := FALSE;
          SETCURRENTKEY("Project No.", "Prognosis Date", "Cost Type", "Cost Object");
          SETRANGE("Project No.", Prognosis."Project No.");
          SETRANGE("Prognosis Date", Prognosis."Prognosis Date");
          SETFILTER("Cost Type", '<>%1', "Cost Type"::Revenue);
          IF FINDSET THEN
            REPEAT
              Rec.INIT;
              Rec."Project No." := "Project No.";
              Rec."Prognosis Date" := "Prognosis Date";
              Rec.Element := Element;
              Rec."Main Project No." := "Main Project No.";
              Rec."Cost Type" := "Cost Type";
              Rec."Cost Object" := '-';
              Rec.Level := 2;
              IF Rec.INSERT THEN BEGIN
                PrognosisSumLineTMP := Rec;
                PrognosisSumLineTMP.INSERT;
              END;
              SETRANGE("Cost Type", "Cost Type");
                REPEAT
                  Rec.INIT;
                  Rec.COPY(PrognosisLine);
                  Rec.Level := 3;
                  IF Rec.INSERT THEN BEGIN
                    CalculateValues(Rec);
                    UpdateSumRow(PrognosisSumLineTMP, Rec, 1);
                  END
                UNTIL NEXT = 0;
              Rec := PrognosisSumLineTMP;
              Rec.MODIFY;
              SETFILTER("Cost Type", '<>%1', "Cost Type"::Revenue);
            UNTIL NEXT = 0;
        END;
      Rec.RESET;
      Rec.COPYFILTERS(CurrPrognosisLine);
      IF ViewPrognPerElementByCostObj THEN
        Rec.SETCURRENTKEY("Project No.", "Prognosis Date", "Cost Type", "Cost Object", Level, Element)
      ELSE
        Rec.SETCURRENTKEY("Project No.", "Prognosis Date", Element, "Cost Type", Level, "Cost Object");
      IF Rec.FINDFIRST THEN;
      IF PrognosisLine.FINDFIRST THEN;
      Rec := CurrPrognosisLine;
    END;

    PROCEDURE UpdateData@1100528401(Prognosis@1100529700 : Record 11012034);
    VAR
      ChangedPrognosisLine@1100528401 : Record 11012035;
      xChangedPrognosisLine@1100528402 : Record 11012035;
      PrognosisSumLineTMP@1100528403 : TEMPORARY Record 11012035;
      PrognosisSumLineTMP_SecondLevel@1100528404 : TEMPORARY Record 11012035;
      CurrPrognosisLine@1100528400 : Record 11012035;
      PrognosisLineWithCurrentFilters@1100528406 : Record 11012035;
      Job@1100528405 : Record 11072003;
    BEGIN
      PrognosisLineWithCurrentFilters.COPY(Rec);
      IF NOT Job.GET(Prognosis."Project No.") THEN
        EXIT;
      PrognosisGlobal.GET(Prognosis."Project No.", Prognosis."Prognosis Date");
      NotFixed := NOT PrognosisGlobal.Fixed;
      xChangedPrognosisLine := xRec;

      IF Job."Prognosis per Element" OR
        (PrognosisGlobal."Prognosis Level" = PrognosisGlobal."Prognosis Level"::Element)
      THEN BEGIN
        ElementVisible := TRUE;
        GetPrognosisSumLine(2, xChangedPrognosisLine, PrognosisSumLineTMP_SecondLevel);
        PrognosisSumLineTMP_SecondLevel.INSERT;
        GetPrognosisSumLine(1, xChangedPrognosisLine, PrognosisSumLineTMP);
        PrognosisSumLineTMP.INSERT;

        CalculateValues(xChangedPrognosisLine);
        UpdateSumRow(PrognosisSumLineTMP_SecondLevel, xChangedPrognosisLine, -1);
        UpdateSumRow(PrognosisSumLineTMP, xChangedPrognosisLine, -1);

        ChangedPrognosisLine.GET(
          xChangedPrognosisLine."Project No.", xChangedPrognosisLine."Prognosis Date", xChangedPrognosisLine."Cost Type",
          xChangedPrognosisLine.Element, xChangedPrognosisLine."Cost Object");
        COPY(ChangedPrognosisLine);
        Level := 3;
        MODIFY;

        CalculateValues(Rec);
        UpdateSumRow(PrognosisSumLineTMP_SecondLevel, Rec, 1);
        UpdateSumRow(PrognosisSumLineTMP, Rec, 1);

        CurrPrognosisLine := Rec;
        Rec := PrognosisSumLineTMP_SecondLevel;
        MODIFY;
        Rec := PrognosisSumLineTMP;
        MODIFY;
        Rec := CurrPrognosisLine;
      END ELSE BEGIN
        ElementVisible := FALSE;
        GetPrognosisSumLine(2, xChangedPrognosisLine, PrognosisSumLineTMP_SecondLevel);
        PrognosisSumLineTMP_SecondLevel.INSERT;

        CalculateValues(xChangedPrognosisLine);
        UpdateSumRow(PrognosisSumLineTMP_SecondLevel, xChangedPrognosisLine, -1);

        ChangedPrognosisLine.GET(
          xChangedPrognosisLine."Project No.", xChangedPrognosisLine."Prognosis Date", xChangedPrognosisLine."Cost Type",
          xChangedPrognosisLine.Element, xChangedPrognosisLine."Cost Object");
        COPY(ChangedPrognosisLine);
        Level := 3;
        MODIFY;

        CalculateValues(Rec);
        UpdateSumRow(PrognosisSumLineTMP_SecondLevel, Rec, 1);

        CurrPrognosisLine := Rec;
        Rec := PrognosisSumLineTMP_SecondLevel;
        MODIFY;
        Rec := CurrPrognosisLine;
      END;
      Rec.RESET;
      Rec.COPYFILTERS(PrognosisLineWithCurrentFilters);
      IF ViewPrognPerElementByCostObj THEN
        Rec.SETCURRENTKEY("Project No.", "Prognosis Date", "Cost Type", "Cost Object", Level, Element)
      ELSE
        Rec.SETCURRENTKEY("Project No.", "Prognosis Date", Element, "Cost Type", Level, "Cost Object");
    END;

    LOCAL PROCEDURE GetPrognosisSumLine@1100528403(RequiredLevel@1100528402 : Integer;PrognosisLineThirdLevel@1100528400 : Record 11012035;VAR PrognosisLineRequiredLevel@1100528401 : Record 11012035);
    VAR
      CurrPrognosisLine@1100528403 : Record 11012035;
    BEGIN
      CurrPrognosisLine.COPY(Rec);
      RESET; // To be able to find the sum line if it is not within the current filters.
      FILTERGROUP(2);
      SETRANGE("Project No.", PrognosisLineThirdLevel."Project No.");
      SETRANGE("Prognosis Date", PrognosisLineThirdLevel."Prognosis Date");
      IF ViewPrognPerElementByCostObj THEN BEGIN
        SETRANGE("Cost Type", PrognosisLineThirdLevel."Cost Type");
        CASE RequiredLevel OF
          1:
            BEGIN
              SETRANGE("Cost Object", '');
              SETRANGE(Element, '');
            END;
          2:
            BEGIN
              SETRANGE("Cost Object", "Cost Object");
              SETRANGE(Element, '-');
            END;
        END;
      END ELSE BEGIN
        CASE RequiredLevel OF
          1:
            BEGIN
              SETRANGE("Cost Type", -1);
              SETRANGE("Cost Object", '');
            END;
          2:
            BEGIN
              SETRANGE("Cost Type", PrognosisLineThirdLevel."Cost Type");
              SETRANGE("Cost Object", '-');
            END;
        END;
        SETRANGE(Element, PrognosisLineThirdLevel.Element);
      END;
      SETRANGE(Level, RequiredLevel);
      FINDFIRST;
      PrognosisLineRequiredLevel := Rec;
      SETRANGE("Project No.");
      SETRANGE("Prognosis Date");
      SETRANGE("Cost Type");
      SETRANGE(Element);
      SETRANGE("Cost Object");
      SETRANGE(Level);
      FILTERGROUP(0);
      COPY(CurrPrognosisLine);
    END;

    LOCAL PROCEDURE UpdateSumRow@1100529704(VAR PrognosisSumLineTMP@1100529701 : TEMPORARY Record 11012035;PrognosisLine@1100529700 : Record 11012035;Sign@1100528400 : Integer);
    BEGIN
      WITH PrognosisSumLineTMP DO BEGIN
        "Budget Amount" += Sign * PrognosisLine."Budget Amount";
        "Actual Costs" += Sign * PrognosisLine."Actual Costs";
        "Open (Purchase)" += Sign * PrognosisLine."Open (Purchase)";
        "Received/Not Invoiced" += Sign * PrognosisLine."Received/Not Invoiced";
        "Total Cost" += Sign * PrognosisLine."Total Cost";
        "Allowed Costs" += Sign * PrognosisLine."Allowed Costs";
        "Estimated Purchase Result" += Sign * PrognosisLine."Estimated Purchase Result";
        "Original Budget Amount" += Sign * PrognosisLine."Original Budget Amount";
        Adjustments += Sign * PrognosisLine.Adjustments;
        Extension += Sign * PrognosisLine.Extension;
        Options += Sign * PrognosisLine.Options;
        "Purchase Budget" += Sign * PrognosisLine."Purchase Budget";
        "Purchase Amount" += Sign * PrognosisLine."Purchase Amount";
        "Purchase Amount (Get Orders)" += Sign * PrognosisLine."Purchase Amount (Get Orders)";
        "Actual Costs (Purchase Action)" += Sign * PrognosisLine."Actual Costs (Purchase Action)";
        "Actual Costs (Not P. O. Rel.)" += Sign * PrognosisLine."Actual Costs (Not P. O. Rel.)";
        "Purchase (Purchase Action)" += Sign * PrognosisLine."Purchase (Purchase Action)";
        "Previous Prognosis" += Sign * PrognosisLine."Previous Prognosis";
        "Risc Coverage" += Sign * PrognosisLine."Risc Coverage";
        Amount += Sign * PrognosisLine.Amount;
        "Total Risc Coverage Amount" += Sign * PrognosisLine."Total Risc Coverage Amount";
        "Total Release Risc Coverage" += Sign * PrognosisLine."Total Release Risc Coverage";
        "Ovh. Surcharge" += Sign * PrognosisLine."Ovh. Surcharge";
        "Amount incl. Surcharge" += Sign * PrognosisLine."Amount incl. Surcharge";
        "Prognosis End Result" += Sign * PrognosisLine."Prognosis End Result";
        "Prognosis End Result Hours" += Sign * PrognosisLine."Prognosis End Result Hours";
        "Prognosis Total Cost" += Sign * PrognosisLine."Prognosis Total Cost";
        "Prognosis Total Hours" += Sign * PrognosisLine."Prognosis Total Hours";
        "Open Ovh. Surch. (Purchase)" += Sign * PrognosisLine."Open Ovh. Surch. (Purchase)";
        "Open (Inventory)" += Sign * PrognosisLine."Open (Inventory)";
        "Open Ovh. Surch. (Inventory)" += Sign * PrognosisLine."Open Ovh. Surch. (Inventory)";
        "Open Ovh. Surch. (Hours)" += Sign * PrognosisLine."Open Ovh. Surch. (Hours)";
        "Previous Date" := PrognosisLine."Previous Date";
        "Prev Prognosis End Result" += Sign * PrognosisLine."Prev Prognosis End Result";
        "Prev Prognosis Total Cost" += Sign * PrognosisLine."Prev Prognosis Total Cost";
        "Received/To Process" += Sign * PrognosisLine."Received/To Process";
        Quantity += Sign * PrognosisLine.Quantity;
        "Purchase Result" += Sign * PrognosisLine."Purchase Result";
        "Product Discount (Purchase)" += Sign * PrognosisLine."Product Discount (Purchase)";
        "Budget Hours" += Sign * PrognosisLine."Budget Hours";
        "Allowed Hours" += Sign * PrognosisLine."Allowed Hours";
        "Open Hours" += Sign * PrognosisLine."Open Hours";
        "Total Hours" += Sign * PrognosisLine."Total Hours";
        "Actual Hours" += Sign* PrognosisLine."Actual Hours";
        "Remaining Budget" += Sign * PrognosisLine."Remaining Budget";
        CalculateAllowedPercentage(PrognosisSumLineTMP);
        MODIFY;
      END;
    END;

    LOCAL PROCEDURE TreeDescription@1100529702() : Text;
    BEGIN
      IF ViewPrognPerElementByCostObj THEN BEGIN
        CASE Level OF
          1: EXIT(FIELDCAPTION("Cost Type") + ': ' + FORMAT("Cost Type"));
          2: EXIT(FIELDCAPTION("Cost Object") + ': ' + "Cost Object");
          3: EXIT(FIELDCAPTION(Element) + ': ' + Element);
        END
      END ELSE
        CASE Level OF
          1: EXIT(FIELDCAPTION(Element) + ': ' + Element);
          2: EXIT(FIELDCAPTION("Cost Type") + ': ' + FORMAT("Cost Type"));
          3: EXIT("Cost Object");
        END
    END;

    LOCAL PROCEDURE UpdateRow@1100529703();
    VAR
      PrognosisLine@1100529700 : Record 11012035;
      Level2@1100529701 : Integer;
      Result2@1100528400 : Decimal;
      DiffWithPrevEndResult@1100528401 : Decimal;
    BEGIN
      IF NOT PrognosisLine.GET("Project No.","Prognosis Date","Cost Type",Element,"Cost Object") THEN
        EXIT;
      Level2 := Level;
      Result2 := Result;
      DiffWithPrevEndResult := "Diff. with Prev. End Result";
      TRANSFERFIELDS(PrognosisLine);
      Level := Level2;
      Result := Result2;
      "Diff. with Prev. End Result" := DiffWithPrevEndResult;
      MODIFY(FALSE);
    END;

    PROCEDURE SetValuesToZero@4();
    BEGIN
      PriceDec := 0;
      RateCode := '';
      DiffPrevPrognLineStyle := 'Standard';
    END;

    PROCEDURE CalculateValues@10(VAR PrognosisLine@11012000 : Record 11012035);
    VAR
      PrognosisManagement@1100527300 : Codeunit 11020217;
    BEGIN
      LastLevel := (PrognosisLine.Level = 3) OR (PrognosisLine.Level = 0);
      DeleteEnabled := LastLevel AND NotFixed;
      SumLine := NOT LastLevel;
      IF ViewPrognPerElementByCostObj THEN BEGIN
        HideCostObjectValue := FALSE;
        HideElementValue := SumLine;
      END ELSE BEGIN
        HideCostObjectValue := SumLine;
        HideElementValue := FALSE;
      END;
      IF LastLevel THEN
        PrognosisManagement.CalculateValues(PrognosisLine, NOT PrognosisLine."Fix Prognosis Line");
      IF PrognosisLine."Cost Type" = PrognosisLine."Cost Type"::Labor THEN
        PriceDec := PrognosisLine.Rate
      ELSE
        PriceDec := PrognosisLine.Price;
      RateCode := PrognosisLine."Rate Code";
      CalcFlowFieldsOfPrognosisLine(PrognosisLine);
      CalculateAllowedPercentage(PrognosisLine);
      IF PrognosisLine.ISTEMPORARY THEN BEGIN
        // Table fields Result and Diff. with Prev. End Result are used instead of global variables to avoid refresh
        // problems when function UpdateData updates Result or Diff. with Prev. End Result of line with Level = 1 or
        // Level = 2 because Received/To Process or Prognosis End Result of line with Level = 3 has been changed.
        PrognosisLine.Result :=
          PrognosisLine."Allowed Costs" - PrognosisLine."Total Cost" - PrognosisLine."Received/To Process";
        PrognosisLine."Diff. with Prev. End Result" :=
          PrognosisLine."Prognosis End Result" - PrognosisLine."Prev Prognosis End Result";
        IF LastLevel THEN
          IF PrognosisLine."Diff. with Prev. End Result" >= 0 THEN
            DiffPrevPrognLineStyle := 'Standard'
          ELSE
            DiffPrevPrognLineStyle := 'Attention'
        ELSE
          IF PrognosisLine."Diff. with Prev. End Result" >= 0 THEN
            DiffPrevPrognLineStyle := 'Strong'
          ELSE
            DiffPrevPrognLineStyle := 'Unfavorable';
      END;
    END;

    PROCEDURE ShowDetailLines@11136552();
    BEGIN
      CurrPage.SAVERECORD;
      AmountOnDrillDown;
      FillData(PrognosisGlobal);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SaveAndRefresh@1100525000(Field@1100529701 : Integer);
    VAR
      PrognosisLine@1100529700 : Record 11012035;
    BEGIN
      IF PrognosisLine.GET("Project No.", "Prognosis Date", "Cost Type", Element, "Cost Object") THEN BEGIN
        CASE Field OF
          FIELDNO("Risc Coverage")          : PrognosisLine.VALIDATE("Risc Coverage", "Risc Coverage");
          FIELDNO(Quantity)                 : PrognosisLine.VALIDATE(Quantity, Quantity);
          FIELDNO("Rate Code")              : PrognosisLine.VALIDATE("Rate Code", RateCode);
          FIELDNO("Unit of Measure")        : PrognosisLine.VALIDATE("Unit of Measure", "Unit of Measure");
          FIELDNO(Price)                    : IF PrognosisLine."Cost Type" = "Cost Type"::Labor THEN
                                                PrognosisLine.VALIDATE(Rate, PriceDec)
                                              ELSE
                                                PrognosisLine.VALIDATE(Price, PriceDec);
          FIELDNO(Amount)                   : PrognosisLine.VALIDATE(Amount, Amount);
          FIELDNO("Amount incl. Surcharge") : PrognosisLine.VALIDATE("Amount incl. Surcharge", "Amount incl. Surcharge");
          FIELDNO("Prognosis End Result")   : PrognosisLine.VALIDATE("Prognosis End Result", "Prognosis End Result");
          FIELDNO("Prognosis Total Cost")   : PrognosisLine.VALIDATE("Prognosis Total Cost", "Prognosis Total Cost");
          FIELDNO("Prognosis Total Hours")  : PrognosisLine.VALIDATE("Prognosis Total Hours", "Prognosis Total Hours");
          FIELDNO("Received/To Process")    : PrognosisLine.VALIDATE("Received/To Process", "Received/To Process");
          FIELDNO(Comment)                  : PrognosisLine.VALIDATE(Comment, Comment);
        END;
        IF Field IN [FIELDNO(Quantity), FIELDNO(Price), FIELDNO(Amount), FIELDNO("Rate Code")] THEN BEGIN
          IF PrognosisLine."Cost Type" = "Cost Type"::Labor THEN
            PrognosisLine.UpdatePrognosisSpecification(xRec.Quantity, PrognosisLine.Quantity, xRec.Rate, PrognosisLine.Rate, xRec.Amount, PrognosisLine.Amount)
          ELSE
            PrognosisLine.UpdatePrognosisSpecification(xRec.Quantity, PrognosisLine.Quantity, xRec.Price, PrognosisLine.Price, xRec.Amount, PrognosisLine.Amount);
        END;
        PrognosisLine.MODIFY(TRUE);
        PrognosisLineModified := TRUE;
        UpdateData(PrognosisGlobal);
      END;
    END;

    LOCAL PROCEDURE InsertRow@1100529705();
    VAR
      InsertNewPrognosisLines@1100529700 : Page 11129909;
      PrognosisLineTMP@1100529701 : TEMPORARY Record 11012035;
    BEGIN
      PrognosisLineTMP.INIT;
      PrognosisLineTMP."Project No." := PrognosisGlobal."Project No.";
      PrognosisLineTMP."Prognosis Date":= PrognosisGlobal."Prognosis Date";
      PrognosisLineTMP.Element:= Element;
      PrognosisLineTMP."Cost Type":= "Cost Type";
      PrognosisLineTMP.INSERT;

      InsertNewPrognosisLines.SetParametr(PrognosisLineTMP, ElementVisible);
      InsertNewPrognosisLines.LOOKUPMODE(TRUE);
      IF InsertNewPrognosisLines.RUNMODAL = ACTION::LookupOK THEN BEGIN
        FillData(PrognosisGlobal);
      END;
    END;

    LOCAL PROCEDURE DeleteRow@1100529701();
    VAR
      PrognosisLine@1100529700 : Record 11012035;
    BEGIN
      IF PrognosisLine.GET("Project No.", "Prognosis Date", "Cost Type", Element, "Cost Object") THEN
        IF CONFIRM(Text001, FALSE, Element, "Cost Type", "Cost Object") THEN BEGIN
          IF PrognosisLine.DELETE(TRUE) THEN
            Rec.DELETE;
          FillData(PrognosisGlobal);
        END
    END;

    LOCAL PROCEDURE CalcFlowFieldsOfPrognosisLine@1100528406(VAR PrognosisLine@1100528401 : Record 11012035);
    VAR
      PrognosisManagement@1100528402 : Codeunit 11020217;
      LastLevel@1100528400 : Boolean;
    BEGIN
      LastLevel := PrognosisLine.Level = 3;
      IF LastLevel THEN
        PrognosisLine.CALCFIELDS("Previous Prognosis", "Prev Prognosis End Result", "Prev Prognosis Total Cost")
      ELSE
        PrognosisManagement.CalcPreviousPrognosisSums(PrognosisLine, ViewPrognPerElementByCostObj);

      PrognosisLine.CALCFIELDS("Project Related Name", "Cost Object Description", "Element Description", "Specification Line Exists");
      IF PrognosisLine."Project Related Name" <> '' THEN
        PrognosisLine."Cost Object Description" := PrognosisLine."Project Related Name";
    END;

    LOCAL PROCEDURE UpdateFactBoxes@1100527350();
    BEGIN
      CurrPage.BudgetFB.PAGE.SetParamerts(Rec, ViewPrognPerElementByCostObj);
      CurrPage.CostsFB.PAGE.SetParamerts(Rec, ViewPrognPerElementByCostObj);
      CurrPage.HoursFB.PAGE.SetParamerts(Rec, ViewPrognPerElementByCostObj);
    END;

    BEGIN
    END.
  }
}

