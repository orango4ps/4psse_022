OBJECT Codeunit 11012092 Exchange XML Format Mgt (FSA)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE FormatBoolean@1100485004(IBoolean@1100485000 : Boolean) : Text[30];
    BEGIN
      EXIT(FORMAT(IBoolean, 0, 2));  //* 0/1
    END;

    [External]
    PROCEDURE FormatDecimal@1100485000(IDecimal@1100485000 : Decimal) : Text[30];
    BEGIN
      EXIT(FORMAT(IDecimal, 0, 9));    //* XML-Format (9): Decimalsign '.'
    END;

    [External]
    PROCEDURE FormatOptionNo@1100485003(IOption@1100485000 : Option) : Text[30];
    BEGIN
      EXIT(FORMAT(IOption, 0, 9));  //* XML-Format (9): Option No. (0..)
    END;

    [External]
    PROCEDURE FormatDate@1100485001(IDate@1100485000 : Date) : Text[30];
    BEGIN
      //EXIT(FORMAT(IDate, 0, '<Year4>-<Month,2>-<Day,2>'));
      EXIT(FORMAT(IDate, 0, 9));  //* XML-Format (9): YYYY-MM-DD
    END;

    [External]
    PROCEDURE FormatTime@1100485002(ITime@1100485000 : Time) : Text[30];
    BEGIN
      //* Format: HH:MM:SS
      EXIT(FORMAT(ITime, 0, '<Hours24>:<Minutes,2>:<Seconds,2>'));
    END;

    [External]
    PROCEDURE FormatDateAndTimeToDateTime@1100485007(IDate@1100485000 : Date;ITime@1100485001 : Time) : Text[30];
    BEGIN
      EXIT(FormatDate(IDate) + ' ' + FormatTime(ITime));
    END;

    [External]
    PROCEDURE EvaluateDecimal@1100485009(IText@1100485000 : Text[30];VAR ODecimal@1100485001 : Decimal) : Boolean;
    BEGIN
      ODecimal := 0;
      IF EVALUATE(ODecimal, IText, 9) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE EvaluateDate@1100485005(IText@1100485000 : Text[30];VAR ODate@1100485001 : Date) : Boolean;
    BEGIN
      ODate := 0D;
      IF EVALUATE(ODate, IText, 9) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE EvaluateTime@1100485006(IText@1100485000 : Text[30];VAR OTime@1100485001 : Time) : Boolean;
    BEGIN
      OTime := 0T;
      IF EVALUATE(OTime, IText, 9) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE EvaluateDateTime@1100485008(IText@1100485000 : Text[30];VAR ODate@1100485001 : Date;VAR OTime@1100485002 : Time) : Boolean;
    VAR
      lvTextDate@1100485003 : Text[30];
      lvTextTime@1100485004 : Text[30];
      lvPos@1100485005 : Integer;
    BEGIN
      ODate := 0D;
      OTime := 0T;
      IText := DELCHR(IText, '<>', ' ');
      lvPos := STRPOS(IText, ' ');
      IF lvPos > 1 THEN BEGIN
        lvTextDate := COPYSTR(IText,1, lvPos-1);
        lvTextTime := COPYSTR(IText,lvPos+1);
      END;
      IF EvaluateDate(lvTextDate, ODate) THEN BEGIN
        IF EvaluateTime(lvTextTime, OTime) THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

