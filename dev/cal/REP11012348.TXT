OBJECT Report 11012348 Create 2ba Discount File
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=2ba Rabattdatei erstellen;
               ENU=Create 2ba Discount File;
               NLD=2ba kortingsbestand aanmaken;
               NOR=Opprette 2ba rabattfil;
               SVE=Skapa 2ba rabattfil];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  "2BASetup"@1100528300 : Record 11012742;
                BEGIN
                  "2BASetup".GET;
                  "2BASetup".TESTFIELD("Pathname Condition File");
                  FileName := "2BASetup"."Pathname Condition File";
                END;

    UseRequestPage=No;
  }
  DATASET
  {
    { 5431;    ;DataItem;                    ;
               DataItemTable=Table11012312;
               DataItemTableView=SORTING(Vendor,Discount Group,Item Code,Discount Level,Discount Reference,Starting Date);
               OnPreDataItem=BEGIN
                               // Conditions without StartDate are not considered valid by 2BA-Unifeed Local Service
                               SETFILTER("Starting Date", '>%1', 0D);
                               //C017763.sn
                               SETRANGE("Discount Level", "Discount Level"::Company);
                               SETFILTER("Ending Date", '%1|%2..', 0D, TODAY);
                               //C017763.en

                               CLEAR(DOMDocument);

                               DOMDocument := DOMDocument.XmlDocument;

                               AppendElement(DOMDocument, 'NewDataSet', '', FALSE, NewDataSetElement);

                               Window.OPEN(Text000 + Text001 + Text002 + Text003);
                               Window.UPDATE(1, TABLECAPTION);

                               MaxLines := COUNT;
                             END;

               OnAfterGetRecord=VAR
                                  ICConditionsElement@1100528301 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
                                  DummyElement@1100528300 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
                                  ETIMVendorLocalMgt@1100528500 : Record 11012313;
                                BEGIN
                                  IF NOT ETIMVendorLocalMgt.GET(Vendor) THEN
                                    ETIMVendorLocalMgt.INIT;  //C017784
                                  IF ETIMVendorLocalMgt."Export Discount Terms" THEN BEGIN  //C017784
                                    LinesRead += 1;
                                    IF ((LinesRead MOD 100) = 0) THEN BEGIN
                                      Window.UPDATE(2, LinesRead);
                                      Window.UPDATE(3, 100 * ROUND(LinesRead / MaxLines * 100, 1));
                                    END;
                                    AppendElement(NewDataSetElement, 'ICConditions', '', FALSE, ICConditionsElement);
                                    AppendElement(ICConditionsElement, 'Leverancier', GetVendorGLN(Vendor), FALSE, DummyElement);
                                    AppendElement(ICConditionsElement, 'ArtikelCodeLeverancier', "Item Code", TRUE, DummyElement);
                                    AppendElement(ICConditionsElement, 'Kortinggroep', "Discount Group", TRUE, DummyElement);
                                    AppendElement(ICConditionsElement, 'NettoPrijs', FormatDecimal("Purchase Price"), TRUE, DummyElement);
                                    AppendElement(ICConditionsElement, 'Omschrijving', Description, TRUE, DummyElement);
                                    AppendElement(ICConditionsElement, 'Korting1', FormatPercentage("Discount Percentage 1"), TRUE, DummyElement);
                                    AppendElement(ICConditionsElement, 'Korting2', FormatPercentage("Discount Percentage 2"), TRUE, DummyElement);
                                    AppendElement(ICConditionsElement, 'Korting3', FormatPercentage("Discount Percentage 3"), TRUE, DummyElement);
                                    AppendElement(ICConditionsElement, 'Ingangsdatum', FormatDate("Starting Date"), FALSE, DummyElement);
                                    AppendElement(ICConditionsElement, 'EindDatum', FormatDate("Ending Date"), FALSE, DummyElement);
                                  END;
                                END;

               OnPostDataItem=VAR
                                XMLDOMManagement4PS@1100525000 : Codeunit 11020220;
                              BEGIN
                                XMLDOMManagement4PS.SaveXMLToClientFile(DOMDocument, FileName);

                                Window.CLOSE;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DOMDocument@1100528302 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      NewDataSetElement@1100528301 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      FileName@1100528300 : Text[250];
      Text000@1100528306 : TextConst 'DEU=2ba-Rabattdatei erstellen ...\;ENU=Create 2ba Discount File ...\;NLD=2ba kortingsbestand aanmaken ...\';
      Text001@1100528305 : TextConst 'DEU=Datei             #1#######################\\;ENU=File Name     #1#######################\\;NLD=Bestand        #1#######################\\;NOR=Filnavn     #1#######################\\;SVE=Filnamn       #1#######################\\';
      Text002@1100528304 : TextConst 'DEU=Datensatz     #2#######################\\;ENU=Record          #2#######################\\;NLD=Record         #2#######################\\;NOR=Post          #2#######################\\;SVE=Post          #2#######################\\';
      Text003@1100528303 : TextConst 'DEU=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;NOR=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;SVE=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Window@1100528310 : Dialog;
      LinesRead@1100528308 : Integer;
      MaxLines@1100528309 : Integer;

    PROCEDURE GetVendorGLN@1100526204(VendorCode@1100526200 : Code[15]) : Code[15];
    VAR
      ETIMVendorCentralMgt@1100528300 : Record 11012304;
    BEGIN
      // Convert short vendor codes to their GLN equivalent (e.g. TKF -> 8712345678901)
      ETIMVendorCentralMgt.SETCURRENTKEY("Alternative Vendor Code");
      ETIMVendorCentralMgt.SETRANGE("Alternative Vendor Code", VendorCode);

      CASE ETIMVendorCentralMgt.FINDFIRST OF
        TRUE: EXIT(ETIMVendorCentralMgt.Code);
        FALSE: EXIT(VendorCode);
      END;
    END;

    LOCAL PROCEDURE AppendElement@1100526202(ParentNode@1100526200 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Name@1100526201 : Text[80];Value@1100526202 : Text[1024];Optional@1100526205 : Boolean;VAR Element@1100526203 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement");
    VAR
      TextNode@1100526204 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlText";
    BEGIN
      IF (NOT Optional) OR (Value <> '') THEN BEGIN
        Element := DOMDocument.CreateElement(Name);
        ParentNode.AppendChild(Element);

        IF Value <> '' THEN BEGIN
          TextNode := DOMDocument.CreateTextNode(Value);
          Element.AppendChild(TextNode);
        END;
      END;
    END;

    LOCAL PROCEDURE FormatPercentage@1100526217(Percentage@1100526200 : Decimal) : Text[30];
    BEGIN
      IF Percentage = 0 THEN
        EXIT('');
      EXIT(FORMAT(Percentage * 100, 0, '<Integer>'));
    END;

    LOCAL PROCEDURE FormatDate@1100526229(Date@1100526200 : Date) : Text[8];
    BEGIN
      IF Date = 0D THEN
        EXIT('');
      EXIT(FORMAT(Date, 0, '<Year4><Month,2><Day,2>'));
    END;

    LOCAL PROCEDURE FormatDecimal@1100526200(Decimal@1100526200 : Decimal) : Text[30];
    BEGIN
      IF Decimal = 0 THEN
        EXIT('');
      EXIT(FORMAT(Decimal, 0, '<Integer><Decimals,3>'));
    END;

    BEGIN
    {
      <NewDataSet>                                                       File should start with this
        <ICConditions>                                                   Start of Conditionrecord
          <Leverancier>8711389000001</Leverancier>                       GLN of Vendor
          <Kortinggroep>*</Kortinggroep>                                 (Opt) Discount group to which discount applies, wildcard allowed
          <Omschrijving>Alles Technische Unie</Omschrijving>             (Opt) Description of condition
          <Korting1>2000</Korting1>                                      (Opt) Discount percentage 1 expressed with 2 decimals without
          <Korting2>1000</Korting2>                                      (Opt) Discount percentage 2   ""
          <Korting3>500</Korting3>                                       (Opt) Discount percentage 3   ""
          <Ingangsdatum>20090101</Ingangsdatum>                          Start date in format YYYYMMDD, record only valid if "
          <EindDatum>20200101</EindDatum>                                End date in format YYYYMMDD, record only valid if "v
        </ICConditions>                                                  End of Conditionrecord
        <ICConditions>
          <Leverancier>8711891990013</Leverancier>
          <Kortinggroep>1927</Kortinggroep>                              Discount only on exact group 1927
          <Omschrijving>Solar Nederland B.V.:051100 (1927)</Omschrijving>
          <Korting1>5100</Korting1>
          <Ingangsdatum>20090101</Ingangsdatum>
          <EindDatum>20200101</EindDatum>
        </ICConditions>
        <ICConditions>
          <Leverancier>8711891990013</Leverancier>
          <ArtikelCodeLeverancier>12343</ArtikelCodeLeverancier>         (Opt) Item number (instead of Discount group) to which discount applies
          <Omschrijving>Artikel Solar</Omschrijving>
          <NettoPrijs>5,25</NettoPrijs>                                  (Opt) Exact Net Price for this item (insteaf of discount1, disc
          <Ingangsdatum>20100101</Ingangsdatum>
          <EindDatum>20101231</EindDatum>
        </ICConditions>
      </NewDataSett>
    }
    END.
  }
  RDLDATA
  {
  }
}

