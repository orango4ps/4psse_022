OBJECT Table 11020427 Project Unit Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lvProjRec@1100485000 : Record 11072003;
               ProjectPrincipal@1100525000 : Record 11012005;
             BEGIN
               TestProjectStatus();
               IF "Price List" = '' THEN BEGIN
                 IF (ProjectPrincipal.GET("Project No.", "Customer No.")) THEN
                   "Price List" := ProjectPrincipal."Price List"
                 ELSE
                   IF lvProjRec.GET("Project No.") THEN
                     "Price List":= lvProjRec."Price List";
               END;
             END;

    OnModify=BEGIN
               TestProjectStatus();
             END;

    OnDelete=BEGIN
               TestProjectStatus();
             END;

    OnRename=BEGIN
               TestProjectStatus();
             END;

    CaptionML=[ENU=Project Unit Price;
               NOR=Enhetspris for prosjekt;
               SVE=Projektenhetspris];
    LookupPageID=Page11072519;
    DrillDownPageID=Page11072519;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Job No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projekt Nr.] }
    { 15  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 20  ;   ;Customer No.        ;Code20        ;FieldClass=Normal;
                                                   TableRelation="Project Principal".Principal WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Customer;
                                                              NOR=Kundens finansielle status;
                                                              SVE=Customer] }
    { 30  ;   ;Unit Price          ;Code20        ;TableRelation="Unit Price".Code WHERE (Customer No.=FIELD(Customer No.),
                                                                                          Price List=FIELD(Price List));
                                                   OnValidate=VAR
                                                                UnitPrice@1100525000 : Record 11020420;
                                                              BEGIN
                                                                CALCFIELDS("Unit Price Description", "Unit Price Description 2");
                                                                UnitPrice.SETRANGE("Customer No.", "Customer No.");
                                                                UnitPrice.SETRANGE("Price List", "Price List");
                                                                IF UnitPrice.FINDFIRST THEN
                                                                  VALIDATE(Element, UnitPrice.Element);
                                                              END;

                                                   CaptionML=[ENU=Unit Price;
                                                              NOR=Enhetspris;
                                                              SVE=Styckpris] }
    { 40  ;   ;Unit Price Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit Price".Description WHERE (Customer No.=FIELD(Customer No.),
                                                                                                      Price List=FIELD(Price List),
                                                                                                      Code=FIELD(Unit Price),
                                                                                                      Start Date=FIELD(Reference Date)));
                                                   CaptionML=[ENU=Unit Price Description;
                                                              NOR=Beskrivelse av Enhetspris;
                                                              SVE=Beskrivning av enhetspris];
                                                   Editable=No }
    { 45  ;   ;Unit Price Description 2;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit Price"."Description 2" WHERE (Customer No.=FIELD(Customer No.),
                                                                                                          Price List=FIELD(Price List),
                                                                                                          Code=FIELD(Unit Price),
                                                                                                          Start Date=FIELD(Reference Date)));
                                                   CaptionML=[ENU=Unit Price Description 2;
                                                              NOR=Beskrivelse 2 av Enhetspris;
                                                              SVE=Beskrivning 2 av enhetspris];
                                                   Editable=No }
    { 48  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 50  ;   ;Price List          ;Code10        ;TableRelation="Price List".Code WHERE (Customer No.=FIELD(Customer No.),
                                                                                          Status=CONST(Open));
                                                   CaptionML=[ENU=Price List;
                                                              NOR=Price List;
                                                              SVE=Prislista] }
    { 60  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Quantity] }
    { 70  ;   ;Reference Date Principal;Date      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Principal"."Ref. Date Unit Price" WHERE (Project No.=FIELD(Project No.),
                                                                                                                        Principal=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Reference Date Principal;
                                                              SVE=Referensdataprojekt];
                                                   Editable=No }
    { 80  ;   ;Unit of Measure     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit Price"."Unit of Measure" WHERE (Customer No.=FIELD(Customer No.),
                                                                                                            Price List=FIELD(Price List),
                                                                                                            Code=FIELD(Unit Price)));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet];
                                                   Editable=No }
    { 85  ;   ;RefDateFilter       ;Text30        ;CaptionML=[ENU=RefDateFilter;
                                                              NOR=RefDateFilter;
                                                              SVE=RefDateFilter] }
    { 90  ;   ;Reference Date      ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Max("Unit Price"."Start Date" WHERE (Customer No.=FIELD(Customer No.),
                                                                                                    Price List=FIELD(Price List),
                                                                                                    Code=FIELD(Unit Price),
                                                                                                    Start Date=FIELD(UPPERLIMIT(FILTER(RefDateFilter)))));
                                                   CaptionML=[ENU=Reference Date;
                                                              NOR=Referansedato;
                                                              SVE=Referensdatum];
                                                   Editable=No }
    { 120 ;   ;Sales Price         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit Price".Price WHERE (Customer No.=FIELD(Customer No.),
                                                                                                Price List=FIELD(Price List),
                                                                                                Code=FIELD(Unit Price),
                                                                                                Start Date=FIELD(Reference Date)));
                                                   CaptionML=[ENU=Sales Price;
                                                              NOR=Salgspris;
                                                              SVE=F”rs„ljningspris];
                                                   Editable=No }
    { 130 ;   ;Sales Discount %    ;Decimal       ;CaptionML=[ENU=Sales Discount %;
                                                              NOR=Salgsrabattprosent;
                                                              SVE=F”rs„ljningsrabattprocent];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 140 ;   ;Sales Surcharge %   ;Decimal       ;CaptionML=[ENU=Sales Surcharge %;
                                                              NOR=Salgstilleggsgebyr %;
                                                              SVE=F”rs„ljningstill„ggsavgiftsprocent];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 200 ;   ;Element             ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                ElementFormat@1100525000 : Codeunit 11012022;
                                                              BEGIN
                                                                ElementFormat.FormatElem(Element,2,'');
                                                              END;

                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
    { 210 ;   ;Extension Contract No.;Code10      ;TableRelation="Extension Contract"."Contract No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Extension Contract No.;
                                                              NOR=ETA-nr;
                                                              SVE=ŽTA-nr] }
  }
  KEYS
  {
    {    ;Project No.,Line No.                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'ENU=%1 %2 has %3 %4.;NOR=%1 %2 har %3 %4.;SVE=%1 %2 har %3 %4.';

    PROCEDURE DoCalcFields@1100485000();
    VAR
      lvJobRec@1100485000 : Record 11072003;
      RefDate@1100525000 : Date;
    BEGIN
      CALCFIELDS("Reference Date Principal");
      RefDate := "Reference Date Principal";
      IF "Unit Price" = '' THEN
        EXIT;
      IF "Reference Date Principal" = 0D THEN BEGIN
        lvJobRec.GET("Project No.");
        lvJobRec.TESTFIELD("Ref. Date Unit Price");
        RefDate := lvJobRec."Ref. Date Unit Price";
      END;
      RefDateFilter := STRSUBSTNO('..%1', RefDate);
      CALCFIELDS("Unit Price Description", "Unit of Measure", "Reference Date");
      CALCFIELDS("Sales Price");
    END;

    PROCEDURE SalesAmount@1100485001() : Decimal;
    BEGIN
      DoCalcFields();
      EXIT("Sales Price" * Quantity);
    END;

    PROCEDURE CalculatedCostAmount@1100485006() : Decimal;
    BEGIN
      DoCalcFields();
      EXIT(CalculatedCostPrice * Quantity);
    END;

    PROCEDURE CalculatedSalesAmount@1100485005() : Decimal;
    BEGIN
      DoCalcFields();
      EXIT(CalculatedSalesPrice * Quantity);
    END;

    PROCEDURE TotalSalesAmount@1100485002() : Decimal;
    VAR
      lvProjectUnitPrice@1100485000 : Record 11020427;
      lvAmount@1100485001 : Decimal;
    BEGIN
      lvAmount := 0;
      lvProjectUnitPrice.SETRANGE("Project No.", "Project No.");
      IF lvProjectUnitPrice.FINDSET THEN BEGIN
        REPEAT
          lvAmount += lvProjectUnitPrice.SalesAmount();
        UNTIL lvProjectUnitPrice.NEXT = 0;
      END;
      EXIT(lvAmount);
    END;

    PROCEDURE TotalCalculatedCostAmount@1100485003() : Decimal;
    VAR
      lvProjectUnitPrice@1100485000 : Record 11020427;
      lvAmount@1100485001 : Decimal;
    BEGIN
      lvAmount := 0;
      lvProjectUnitPrice.SETRANGE("Project No.", "Project No.");
      IF lvProjectUnitPrice.FINDSET THEN BEGIN
        REPEAT
          lvAmount += lvProjectUnitPrice.CalculatedCostAmount();
        UNTIL lvProjectUnitPrice.NEXT = 0;
      END;
      EXIT(lvAmount);
    END;

    PROCEDURE TotalCalculatedSalesAmount@1100485004() : Decimal;
    VAR
      lvProjectUnitPrice@1100485000 : Record 11020427;
      lvAmount@1100485001 : Decimal;
    BEGIN
      lvAmount := 0;
      lvProjectUnitPrice.SETRANGE("Project No.", "Project No.");
      IF lvProjectUnitPrice.FINDSET THEN BEGIN
        REPEAT
          lvAmount += lvProjectUnitPrice.CalculatedSalesAmount();
        UNTIL lvProjectUnitPrice.NEXT = 0;
      END;
      EXIT(lvAmount);
    END;

    PROCEDURE TestProjectStatus@1100525000();
    VAR
      JobRec@1100525000 : Record 11072003;
    BEGIN
      JobRec.GET("Project No.");
      IF (JobRec."Project Status" > JobRec."Project Status"::"Administrative Finished") THEN
        ERROR(Text001,JobRec.TABLECAPTION,"Project No.",JobRec.FIELDCAPTION("Project Status"),JobRec."Project Status");
    END;

    PROCEDURE CalculatedCostPrice@1100485007() : Decimal;
    VAR
      lvUnitPrice@1100485000 : Record 11020420;
    BEGIN
      lvUnitPrice."Total Cost Price" := 0;
      IF lvUnitPrice.GET("Customer No.", "Price List", "Unit Price", "Reference Date") THEN
        lvUnitPrice.CALCFIELDS("Total Cost Price");
      EXIT(lvUnitPrice."Total Cost Price");
    END;

    PROCEDURE CalculatedSalesPrice@1100485008() : Decimal;
    VAR
      lvUnitPrice@1100485000 : Record 11020420;
      Discount@1100528500 : Decimal;
      Surcharge@1100528501 : Decimal;
    BEGIN
      lvUnitPrice."Total Sales Price" := 0;
      IF lvUnitPrice.GET("Customer No.", "Price List", "Unit Price", "Reference Date") THEN
        lvUnitPrice.CALCFIELDS("Total Sales Price");
      //EXIT(lvUnitPrice."Total Sales Price"); //DP00659.o
      //DP00659.sn
      Discount := "Sales Discount %"/100 * lvUnitPrice."Total Sales Price";
      Surcharge := "Sales Surcharge %"/100 * lvUnitPrice."Total Sales Price";
      EXIT(lvUnitPrice."Total Sales Price" - Discount + Surcharge);
      //DP00659.en
    END;

    BEGIN
    {
      There is order in the calculation of flowfields:
      1. "Customer No.","Reference Date Project"
      2. "Unit Price Description", "Unit of Measure", "Reference Date"
      3. "Cost Price"
    }
    END.
  }
}

