OBJECT Codeunit 1297 Http Web Request Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      HttpWebRequest@1030 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest";
      TraceLogEnabled@1011 : Boolean;
      InvalidUrlErr@1025 : TextConst 'ENU=The URL is not valid.;NOR=URL-adressen er ikke gyldig.;SVE=Webbadressen „r ogiltig.';
      NonSecureUrlErr@1026 : TextConst 'ENU=The URL is not secure.;NOR=URL-adressen er ikke sikker.;SVE=URL:en „r inte s„ker.';
      GlobalSkipCheckHttps@1018 : Boolean;
      GlobalProgressDialogEnabled@1023 : Boolean;
      InternalErr@1001 : TextConst 'ENU=The remote service has returned the following error message:\\;NOR=Den eksterne tjenesten har returnert f›lgende feilmelding:\\;SVE=Fj„rrtj„nsten har returnerat f”ljande felmeddelande:\\';
      NoCookieForYouErr@1040 : TextConst 'ENU=The web request has no cookies.;NOR=Nettforesp›rselen har ingen informasjonskapsler.;SVE=Webbf”rfr†gan har inga cookies.';
      TimeoutErr@1000 : TextConst 'ENU=The server timed out waiting for the request.;NOR=Serveren ble tidsavbrutt mens den ventet p† foresp›rselen.';
      FaultDetailErr@1100528300 : TextConst 'ENU=Fault Details:\\';

    [Internal]
    PROCEDURE InvokeJSONRequest@66(RequestJson@1002 : Text;VAR ResponseJson@1001 : Text;VAR HttpError@1009 : Text) : Boolean;
    BEGIN
      ResponseJson := '';
      HttpError := '';

      IF ProcessJsonRequestResponse(RequestJson,ResponseJson) THEN
        EXIT(TRUE);

      HttpError := ParseFaultJsonResponse(ResponseJson);
      EXIT(FALSE);
    END;

    [Internal]
    PROCEDURE GetResponse@6(VAR ResponseInStream@1005 : InStream;VAR HttpStatusCode@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";VAR ResponseHeaders@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection") : Boolean;
    VAR
      WebRequestHelper@1001 : Codeunit 1299;
      HttpWebResponse@1006 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebResponse";
    BEGIN
      EXIT(WebRequestHelper.GetWebResponse(HttpWebRequest,HttpWebResponse,ResponseInStream,HttpStatusCode,
          ResponseHeaders,GlobalProgressDialogEnabled));
    END;

    [Internal]
    PROCEDURE GetResponseStream@23(VAR ResponseInStream@1005 : InStream) : Boolean;
    VAR
      WebRequestHelper@1001 : Codeunit 1299;
      HttpWebResponse@1006 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebResponse";
      HttpStatusCode@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
    BEGIN
      EXIT(WebRequestHelper.GetWebResponse(HttpWebRequest,HttpWebResponse,ResponseInStream,HttpStatusCode,
          ResponseHeaders,GlobalProgressDialogEnabled));
    END;

    LOCAL PROCEDURE GetResponseJson@37(VAR ResponseJson@1004 : Text) : Boolean;
    VAR
      WebRequestHelper@1001 : Codeunit 1299;
      HttpWebResponse@1006 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebResponse";
      HttpStatusCode@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
      ResponseInStream@1003 : InStream;
    BEGIN
      ResponseJson := '';
      CreateInstream(ResponseInStream);

      IF NOT WebRequestHelper.GetWebResponse(
           HttpWebRequest,HttpWebResponse,ResponseInStream,HttpStatusCode,ResponseHeaders,GlobalProgressDialogEnabled)
      THEN
        EXIT(FALSE);

      SetJSONHeaders(ResponseJson,ResponseHeaders);
      EXIT(SetJSONContent(ResponseJson,ResponseInStream));
    END;

    [TryFunction]
    [Internal]
    PROCEDURE ProcessFaultResponse@14(SupportInfo@1001 : Text);
    BEGIN
      ProcessFaultXMLResponse(SupportInfo,'','','');
    END;

    [TryFunction]
    [Internal]
    PROCEDURE ProcessFaultXMLResponse@16(SupportInfo@1001 : Text;NodePath@1011 : Text;Prefix@1008 : Text;NameSpace@1007 : Text);
    VAR
      TempReturnTempBlob@1004 : TEMPORARY Record 99008535;
      WebRequestHelper@1002 : Codeunit 1299;
      XMLDOMMgt@1003 : Codeunit 6224;
      WebException@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebException";
      XmlDoc@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ResponseInputStream@1000 : InStream;
      ErrorText@1009 : Text;
      ServiceURL@1010 : Text;
      FaultDetail@1100528300 : Text;
    BEGIN
      ErrorText := WebRequestHelper.GetWebResponseError(WebException,ServiceURL);

      IF NOT ISNULL(WebException.Response) THEN BEGIN
        ResponseInputStream := WebException.Response.GetResponseStream;

        TraceLogStreamToTempFile(ResponseInputStream,'WebExceptionResponse',TempReturnTempBlob);

        IF NodePath <> '' THEN
          IF TryLoadXMLResponse(ResponseInputStream,XmlDoc) THEN
            IF Prefix = '' THEN
              ErrorText := XMLDOMMgt.FindNodeText(XmlDoc.DocumentElement,NodePath)
            ELSE
              ErrorText := XMLDOMMgt.FindNodeTextWithNamespace(XmlDoc.DocumentElement,NodePath,Prefix,NameSpace);
      END;

      IF ErrorText = '' THEN BEGIN //**4PS.c
        ErrorText := WebException.Message;
        ErrorText += STRSUBSTNO('\\%1%2', FaultDetailErr, XmlDoc.InnerXml); //**4PS.n
      END; //**4PS.n

      ErrorText := InternalErr + ErrorText;

      IF SupportInfo <> '' THEN
        ErrorText += '\\' + SupportInfo;

      ERROR(ErrorText);
    END;

    LOCAL PROCEDURE ProcessJsonRequestResponse@38(RequestJson@1007 : Text;VAR ResponseJson@1005 : Text) : Boolean;
    VAR
      JSONMgt@1001 : Codeunit 5459;
      Content@1000 : Text;
    BEGIN
      JSONMgt.InitializeObject(RequestJson);
      Initialize(JSONMgt.GetValue('ServiceURL') + JSONMgt.GetValue('URLRequestPath'));
      SetMethod(JSONMgt.GetValue('Method'));
      GetJSONHeaders(RequestJson);

      Content := JSONMgt.GetValue('Content');
      IF Content <> '' THEN
        AddBodyAsText(Content);

      IF GetResponseJson(ResponseJson) THEN
        EXIT(TRUE);

      ProcessFaultJsonResponse(ResponseJson);
      EXIT(FALSE);
    END;

    [TryFunction]
    LOCAL PROCEDURE ProcessFaultJsonResponse@39(VAR ResponseJson@1005 : Text);
    VAR
      WebRequestHelper@1000 : Codeunit 1299;
      WebException@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebException";
      ServiceURL@1003 : Text;
      HttpError@1001 : Text;
    BEGIN
      ResponseJson := '';
      HttpError := WebRequestHelper.GetWebResponseError(WebException,ServiceURL);
      ParseWebResponseError(ResponseJson,WebException);

      IF ResponseJson = '' THEN
        ERROR(HttpError);
    END;

    LOCAL PROCEDURE ParseFaultJsonResponse@45(ResponseJson@1000 : Text) : Text;
    VAR
      JSONMgt@1001 : Codeunit 5459;
      code@1002 : Text;
      name@1003 : Text;
      description@1004 : Text;
    BEGIN
      IF JSONMgt.GetJsonWebResponseError(ResponseJson,code,name,description) THEN
        EXIT(STRSUBSTNO('Http error %1 (%2)\%3',code,name,description));
    END;

    [TryFunction]
    [External]
    PROCEDURE CheckUrl@28(Url@1000 : Text);
    VAR
      Uri@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      UriKind@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.UriKind";
    BEGIN
      IF NOT Uri.TryCreate(Url,UriKind.Absolute,Uri) THEN
        ERROR(InvalidUrlErr);

      IF NOT GlobalSkipCheckHttps AND NOT (Uri.Scheme = 'https') THEN
        ERROR(NonSecureUrlErr);
    END;

    [Internal]
    PROCEDURE GetUrl@25() : Text;
    BEGIN
      EXIT(HttpWebRequest.RequestUri.AbsoluteUri);
    END;

    [Internal]
    PROCEDURE GetUri@27() : Text;
    BEGIN
      EXIT(HttpWebRequest.RequestUri.PathAndQuery);
    END;

    [Internal]
    PROCEDURE GetMethod@32() : Text;
    BEGIN
      EXIT(HttpWebRequest.Method);
    END;

    LOCAL PROCEDURE TraceLogStreamToTempFile@33(VAR ToLogInStream@1000 : InStream;Name@1005 : Text;VAR TraceLogTempBlob@1001 : Record 99008535);
    VAR
      Trace@1002 : Codeunit 1292;
    BEGIN
      IF TraceLogEnabled THEN
        Trace.LogStreamToTempFile(ToLogInStream,Name,TraceLogTempBlob);
    END;

    [TryFunction]
    PROCEDURE TryLoadXMLResponse@15(ResponseInputStream@1002 : InStream;VAR XmlDoc@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      XMLDOMManagement@1001 : Codeunit 6224;
    BEGIN
      XMLDOMManagement.LoadXMLDocumentFromInStream(ResponseInputStream,XmlDoc);
    END;

    [External]
    PROCEDURE SetTraceLogEnabled@1(Enabled@1000 : Boolean);
    BEGIN
      TraceLogEnabled := Enabled;
    END;

    [External]
    PROCEDURE DisableUI@18();
    BEGIN
      GlobalProgressDialogEnabled := FALSE;
    END;

    [Internal]
    PROCEDURE Initialize@11(URL@1000 : Text);
    VAR
      PermissionManager@1002 : Codeunit 9002;
    BEGIN
      IF NOT PermissionManager.SoftwareAsAService THEN
        OnOverrideUrl(URL);

      HttpWebRequest := HttpWebRequest.Create(URL);
      SetDefaults;
    END;

    LOCAL PROCEDURE SetDefaults@12();
    VAR
      CookieContainer@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.CookieContainer";
    BEGIN
      HttpWebRequest.Method := 'GET';
      HttpWebRequest.KeepAlive := TRUE;
      HttpWebRequest.AllowAutoRedirect := TRUE;
      HttpWebRequest.UseDefaultCredentials := TRUE;
      HttpWebRequest.Timeout := 60000;
      HttpWebRequest.Accept('application/xml');
      HttpWebRequest.ContentType('application/xml');
      CookieContainer := CookieContainer.CookieContainer;
      HttpWebRequest.CookieContainer := CookieContainer;

      GlobalSkipCheckHttps := TRUE;
      GlobalProgressDialogEnabled := GUIALLOWED;
      TraceLogEnabled := TRUE;
    END;

    [Internal]
    PROCEDURE AddBodyAsText@26(BodyText@1001 : Text);
    VAR
      Encoding@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
    BEGIN
      // Assume UTF8
      AddBodyAsTextWithEncoding(BodyText,Encoding.UTF8);
    END;

    [Internal]
    PROCEDURE AddBodyAsAsciiText@21(BodyText@1001 : Text);
    VAR
      Encoding@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
    BEGIN
      AddBodyAsTextWithEncoding(BodyText,Encoding.ASCII);
    END;

    LOCAL PROCEDURE AddBodyAsTextWithEncoding@20(BodyText@1001 : Text;Encoding@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding");
    VAR
      RequestStr@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Stream";
      StreamWriter@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamWriter";
    BEGIN
      RequestStr := HttpWebRequest.GetRequestStream;
      StreamWriter := StreamWriter.StreamWriter(RequestStr,Encoding);
      StreamWriter.Write(BodyText);
      StreamWriter.Flush;
      StreamWriter.Close;
      StreamWriter.Dispose;
    END;

    [Internal]
    PROCEDURE SetTimeout@7(NewTimeout@1000 : Integer);
    BEGIN
      HttpWebRequest.Timeout := NewTimeout;
    END;

    [Internal]
    PROCEDURE SetMethod@4(Method@1000 : Text);
    BEGIN
      HttpWebRequest.Method := Method;
    END;

    [Internal]
    PROCEDURE SetDecompresionMethod@9(DecompressionMethod@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.DecompressionMethods");
    BEGIN
      HttpWebRequest.AutomaticDecompression := DecompressionMethod;
    END;

    [Internal]
    PROCEDURE SetContentType@5(ContentType@1000 : Text);
    BEGIN
      HttpWebRequest.ContentType := ContentType;
    END;

    [Internal]
    PROCEDURE SetReturnType@2(ReturnType@1000 : Text);
    BEGIN
      HttpWebRequest.Accept := ReturnType;
    END;

    [Internal]
    PROCEDURE SetProxy@22(ProxyUrl@1000 : Text);
    VAR
      WebProxy@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebProxy";
    BEGIN
      IF ProxyUrl = '' THEN
        EXIT;

      WebProxy := WebProxy.WebProxy(ProxyUrl);

      HttpWebRequest.Proxy := WebProxy;
    END;

    [External]
    PROCEDURE SetExpect@24(expectValue@1000 : Boolean);
    BEGIN
      HttpWebRequest.ServicePoint.Expect100Continue := expectValue;
    END;

    [Internal]
    PROCEDURE SetContentLength@29(ContentLength@1000 : BigInteger);
    BEGIN
      HttpWebRequest.ContentLength := ContentLength;
    END;

    [Internal]
    PROCEDURE AddSecurityProtocolTls12@30();
    VAR
      Convert@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      SecurityProtocolType@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.SecurityProtocolType";
      SecurityProtocol@1002 : Integer;
    BEGIN
      SecurityProtocol := Convert.ToInt32(SecurityProtocolType.Tls12);
      AddSecurityProtocol(SecurityProtocol);
    END;

    LOCAL PROCEDURE AddSecurityProtocol@31(SecurityProtocol@1000 : Integer);
    VAR
      TypeHelper@1001 : Codeunit 10;
      Convert@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      ServicePointManager@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.ServicePointManager";
      CurrentSecurityProtocol@1004 : Integer;
    BEGIN
      CurrentSecurityProtocol := Convert.ToInt32(ServicePointManager.SecurityProtocol);
      IF TypeHelper.BitwiseAnd(CurrentSecurityProtocol,SecurityProtocol) <> SecurityProtocol THEN
        ServicePointManager.SecurityProtocol := TypeHelper.BitwiseOr(CurrentSecurityProtocol,SecurityProtocol);
    END;

    [Internal]
    PROCEDURE AddHeader@10(Key@1000 : Text;Value@1001 : Text);
    BEGIN
      CASE Key OF
        'Accept':
          SetReturnType(Value);
        'Content-Type':
          SetContentType(Value);
        ELSE
          HttpWebRequest.Headers.Add(Key,Value);
      END;
    END;

    [Internal]
    PROCEDURE AddBody@3(BodyFilePath@1000 : Text);
    VAR
      FileManagement@1001 : Codeunit 419;
      FileStream@1003 : DotNet "'mscorlib'.System.IO.FileStream";
      FileMode@1004 : DotNet "'mscorlib'.System.IO.FileMode";
    BEGIN
      IF BodyFilePath = '' THEN
        EXIT;

      FileManagement.IsAllowedPath(BodyFilePath,FALSE);

      FileStream := FileStream.FileStream(BodyFilePath,FileMode.Open);
      FileStream.CopyTo(HttpWebRequest.GetRequestStream);
    END;

    [Internal]
    PROCEDURE AddBodyBlob@19(VAR TempBlob@1000 : Record 99008535);
    VAR
      RequestStr@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Stream";
      BlobStr@1001 : InStream;
    BEGIN
      IF NOT TempBlob.Blob.HASVALUE THEN
        EXIT;

      RequestStr := HttpWebRequest.GetRequestStream;
      TempBlob.Blob.CREATEINSTREAM(BlobStr);
      COPYSTREAM(RequestStr,BlobStr);
      RequestStr.Flush;
      RequestStr.Close;
      RequestStr.Dispose;
    END;

    [External]
    PROCEDURE AddBasicAuthentication@60(BasicUserId@1000 : Text;BasicUserPassword@1001 : Text);
    VAR
      Credential@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.NetworkCredential";
    BEGIN
      HttpWebRequest.UseDefaultCredentials(FALSE);
      Credential := Credential.NetworkCredential;
      Credential.UserName := BasicUserId;
      Credential.Password := BasicUserPassword;
      HttpWebRequest.Credentials := Credential;
    END;

    [Internal]
    PROCEDURE SetUserAgent@8(UserAgent@1000 : Text);
    BEGIN
      HttpWebRequest.UserAgent := UserAgent;
    END;

    [Internal]
    PROCEDURE SetCookie@53(VAR Cookie@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.Cookie");
    BEGIN
      HttpWebRequest.CookieContainer.Add(Cookie);
    END;

    [Internal]
    PROCEDURE GetCookie@13(VAR Cookie@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.Cookie");
    VAR
      CookieCollection@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.CookieCollection";
    BEGIN
      IF NOT HasCookie THEN
        ERROR(NoCookieForYouErr);
      CookieCollection := HttpWebRequest.CookieContainer.GetCookies(HttpWebRequest.RequestUri);
      Cookie := CookieCollection.Item(0);
    END;

    [Internal]
    PROCEDURE HasCookie@34() : Boolean;
    BEGIN
      EXIT(HttpWebRequest.CookieContainer.Count > 0);
    END;

    [External]
    PROCEDURE CreateInstream@17(VAR InStr@1000 : InStream);
    VAR
      TempBlob@1001 : Record 99008535;
    BEGIN
      TempBlob.INIT;
      TempBlob.Blob.CREATEINSTREAM(InStr);
    END;

    [Integration]
    PROCEDURE OnOverrideUrl@36(VAR Url@1000 : Text);
    BEGIN
      // Provides an option to rewrite URL in non SaaS environments.
    END;

    [Internal]
    PROCEDURE SendRequestAndReadTextResponse@47(VAR ResponseBody@1004 : Text;VAR ErrorMessage@1008 : Text;VAR ErrorDetails@1006 : Text;VAR HttpStatusCode@1010 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";VAR ResponseHeaders@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection") : Boolean;
    VAR
      TempBlob@1003 : TEMPORARY Record 99008535;
      ResponseInStream@1001 : InStream;
      TextLine@1000 : Text;
    BEGIN
      IF NOT SendRequestAndReadResponse(TempBlob,ErrorMessage,ErrorDetails,HttpStatusCode,ResponseHeaders) THEN
        EXIT(FALSE);

      TempBlob.Blob.CREATEINSTREAM(ResponseInStream);
      WHILE ResponseInStream.READTEXT(TextLine) > 0 DO
        ResponseBody += TextLine;

      EXIT(TRUE);
    END;

    [Internal]
    PROCEDURE SendRequestAndReadResponse@35(VAR TempBlob@1004 : Record 99008535;VAR ErrorMessage@1008 : Text;VAR ErrorDetails@1003 : Text;VAR HttpStatusCode@1010 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";VAR ResponseHeaders@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection") : Boolean;
    VAR
      WebRequestHelper@1012 : Codeunit 1299;
      WebException@1009 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebException";
      WebExceptionResponse@1015 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebResponse";
      ResponseInStream@1001 : InStream;
      WebExceptionResponseText@1016 : Text;
      TextLine@1000 : Text;
      ServiceUrl@1007 : Text;
    BEGIN
      TempBlob.INIT;
      TempBlob.Blob.CREATEINSTREAM(ResponseInStream);

      CLEARLASTERROR;
      IF GetResponse(ResponseInStream,HttpStatusCode,ResponseHeaders) THEN
        EXIT(TRUE);

      ErrorMessage := GETLASTERRORTEXT;

      WebRequestHelper.GetWebResponseError(WebException,ServiceUrl);
      WebExceptionResponse := WebException.Response;
      IF SYSTEM.ISNULL(WebExceptionResponse) THEN
        EXIT(FALSE);

      HttpStatusCode := WebExceptionResponse.StatusCode;
      ResponseHeaders := WebExceptionResponse.Headers;
      WebExceptionResponse.GetResponseStream.CopyTo(ResponseInStream);
      WHILE ResponseInStream.READTEXT(TextLine) > 0 DO
        WebExceptionResponseText += TextLine;

      ErrorDetails := WebExceptionResponseText;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE GetJSONHeaders@40(RequestJson@1000 : Text);
    VAR
      JSONMgt@1002 : Codeunit 5459;
      Name@1003 : Text;
      Value@1004 : Text;
    BEGIN
      JSONMgt.InitializeObject(RequestJson);
      IF JSONMgt.SelectTokenFromRoot('Header') THEN
        IF JSONMgt.ReadProperties THEN
          WHILE JSONMgt.GetNextProperty(Name,Value) DO
            AddHeader(Name,Value);
    END;

    LOCAL PROCEDURE SetJSONHeaders@44(VAR ResponseJson@1001 : Text;ResponseHeaders@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection");
    VAR
      JSONMgt@1004 : Codeunit 5459;
      HeaderJson@1003 : Text;
      i@1002 : Integer;
    BEGIN
      IF ResponseHeaders.Count > 0 THEN BEGIN
        FOR i := 0 TO ResponseHeaders.Count - 1 DO
          JSONMgt.SetValue(ResponseHeaders.GetKey(i),ResponseHeaders.Item(i));
        HeaderJson := JSONMgt.WriteObjectToString;
        JSONMgt.InitializeObject(ResponseJson);
        JSONMgt.AddJson('Header',HeaderJson);
        ResponseJson := JSONMgt.WriteObjectToString;
      END;
    END;

    LOCAL PROCEDURE SetJSONContent@51(VAR ResponseJson@1000 : Text;ResponseInStream@1002 : InStream) : Boolean;
    VAR
      JSONMgt@1003 : Codeunit 5459;
      ContentJson@1004 : Text;
      Content@1001 : Text;
    BEGIN
      IF ResponseInStream.READ(Content) = 0 THEN
        EXIT(TRUE);

      IF NOT JSONMgt.InitializeFromString(Content) THEN
        EXIT(FALSE);

      ContentJson := JSONMgt.WriteObjectToString;
      JSONMgt.InitializeObject(ResponseJson);
      JSONMgt.AddJson('Content',ContentJson);
      ResponseJson := JSONMgt.WriteObjectToString;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseWebResponseError@41(VAR ResponseJson@1000 : Text;WebException@1004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebException");
    VAR
      JSONMgt@1002 : Codeunit 5459;
      HttpWebResponse@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebResponse";
      Convert@1007 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      WebExceptionStatus@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebExceptionStatus";
      ResponseInputStream@1003 : InStream;
      ErrorDescription@1006 : Text;
      StatusCode@1008 : Text;
      StatusCodeString@1009 : Text;
      WebExceptionResponse@1011 : Boolean;
    BEGIN
      IF ISNULL(WebException) THEN
        EXIT;

      CASE TRUE OF
        WebException.Status.Equals(WebExceptionStatus.Timeout):
          BEGIN
            StatusCode := '408';
            StatusCodeString := 'Request Timeout';
            ErrorDescription := TimeoutErr;
          END;
        NOT ISNULL(WebException.Response):
          BEGIN
            WebExceptionResponse := TRUE;
            HttpWebResponse := WebException.Response;
            StatusCode := FORMAT(Convert.ToInt32(HttpWebResponse.StatusCode));
            StatusCodeString := HttpWebResponse.StatusCode.ToString;
            ErrorDescription := HttpWebResponse.StatusDescription;
          END;
        ELSE
          EXIT;
      END;

      JSONMgt.SetJsonWebResponseError(ResponseJson,StatusCode,StatusCodeString,ErrorDescription);

      // Try to get more details
      IF WebExceptionResponse THEN BEGIN
        ResponseInputStream := HttpWebResponse.GetResponseStream;
        IF SetJSONContent(ResponseJson,ResponseInputStream) THEN
          IF JSONMgt.InitializeFromString(ResponseJson) THEN BEGIN
            ErrorDescription := JSONMgt.GetValue('Content.error_description');
            IF ErrorDescription <> '' THEN BEGIN
              JSONMgt.SetValue('Error.description',ErrorDescription);
              ResponseJson := JSONMgt.WriteObjectToString;
            END;
          END;
      END;
    END;

    BEGIN
    END.
  }
}

