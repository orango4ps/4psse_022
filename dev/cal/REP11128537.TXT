OBJECT Report 11128537 Import Huldt & Lillevik
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO10.00 (15000066),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Huldt & Lillevik;
               NOR=Importer Huldt & Lillevik];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PayrollSetup.GET;
                 END;

    OnPreReport=BEGIN
                  ServerTempFile := FileMgt.UploadFileSilent(Filename);

                  TxtFile.TEXTMODE := TRUE;
                  TxtFile.OPEN(ServerTempFile);
                  IF PostDate = 0D THEN
                    ERROR(Text15000000);
                  IF Filename = '' THEN
                    ERROR(Text15000001);

                  COMMIT;
                  PayrollTools.CheckJournal(NextLine,DefGenJnlLine);
                  NextLine := 10000;

                  WHILE TxtFile.LEN <> TxtFile.POS DO BEGIN
                    TxtFile.READ(FileData);
                    ImportAccountNo := DELCHR(COPYSTR(FileData,1,7),'<','0');
                    ImportGlDimCode1 := DELCHR(COPYSTR(FileData,8,7),'<','0');
                    ImportGlDimCode2 := DELCHR(COPYSTR(FileData,15,8),'<','0');
                    EVALUATE(ImportNumber,COPYSTR(FileData,60,10));
                    ImportSign := COPYSTR(FileData,80,1);
                    EVALUATE(ImportAmount,COPYSTR(FileData,81,9));
                    IF ImportSign = '-' THEN
                      ImportAmount := -ImportAmount / 100
                    ELSE
                      ImportAmount := ImportAmount / 100;
                    InsertJournalLine;
                  END;
                END;

    OnPostReport=BEGIN
                   TxtFile.CLOSE;
                   IF PayrollSetup."Save Payroll File" THEN
                     NewFilename;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   PostDate := WORKDATE;
                   IF DocumentText = '' THEN
                     DocumentText := Text15000002;
                   Filename := PayrollSetup."File Name";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1080000;2;Field     ;
                  Name=PostDate;
                  CaptionML=[ENU=Posting date;
                             NOR=Bokf›ringsdato;
                             SVE=Bokf”ringsdatum];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostDate }

      { 1080002;2;Field     ;
                  Name=DocumentText;
                  CaptionML=[ENU=Document text;
                             NOR=Bilagstekst];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentText }

      { 1080004;2;Field     ;
                  Name=FileName;
                  AssistEdit=Yes;
                  CaptionML=[ENU=File Name;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Filename;
                  OnAssistEdit=BEGIN
                                 Filename := FileMgt.OpenFileDialog(Text15000003,Filename,Text15000004);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PayrollSetup@1080000 : Record 11128513;
      GenJnlLine@1080001 : Record 81;
      DefGenJnlLine@1080002 : Record 81;
      PrevGenJnlLine@1080003 : Record 81;
      PayrollTools@1080005 : Codeunit 11128517;
      FileMgt@1080008 : Codeunit 419;
      TxtFile@1080031 : File;
      DocumentText@1080009 : Text[50];
      Filename@1080010 : Text[250];
      FileData@1080032 : Text[90];
      ServerTempFile@1080033 : Text[1024];
      file1@1080034 : Text[250];
      file2@1080035 : Text[250];
      NextLine@1080012 : Integer;
      ImportNumber@1080036 : Integer;
      ImportAccountNo@1080013 : Code[20];
      ImportGlDimCode1@1080014 : Code[10];
      ImportGlDimCode2@1080015 : Code[10];
      ImportSign@1080037 : Code[1];
      ImportAmount@1080017 : Decimal;
      Text15000000@1080022 : TextConst 'ENU=Please specify Posting Date.;NOR=Angi bokf›ringsdato.';
      Text15000001@1080023 : TextConst 'ENU=Please specify File Name.;NOR=Angi filnavn.';
      Text15000002@1080024 : TextConst 'ENU=Payroll;NOR=L›nnskostnad';
      Text15000003@1080025 : TextConst 'ENU=Import from Payroll File.;NOR=Les inn fra l›nnsfil.';
      Text15000004@1080026 : TextConst 'ENU=Text Files (*.txt)|*.txt|All Files (*.*)|*.*;NOR=Tekstfiler (*.txt)|*.txt|Alle filer (*.*)|*.*';
      PostDate@1080038 : Date;

    PROCEDURE Initialize@1080000(SetGenJnlLine@1080000 : Record 81);
    BEGIN
      DefGenJnlLine := SetGenJnlLine;
    END;

    LOCAL PROCEDURE InsertJournalLine@1080001();
    BEGIN
      PrevGenJnlLine := GenJnlLine;
      CLEAR(GenJnlLine);
      GenJnlLine.VALIDATE("Journal Template Name",DefGenJnlLine."Journal Template Name");
      GenJnlLine.VALIDATE("Journal Batch Name",DefGenJnlLine."Journal Batch Name");
      GenJnlLine.SetUpNewLine(PrevGenJnlLine,0,FALSE);
      GenJnlLine.VALIDATE("Line No.",NextLine);
      NextLine := NextLine + 10000;
      CASE PayrollSetup."Post to" OF
        PayrollSetup."Post to"::"G/L Account":
          GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"G/L Account");
        PayrollSetup."Post to"::"Bank Account":
          GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
      END;
      GenJnlLine.VALIDATE("Posting Date",PostDate);
      GenJnlLine.VALIDATE("Account No.",ImportAccountNo);
      GenJnlLine.VALIDATE(Quantity,ImportNumber);
      GenJnlLine.VALIDATE(Amount,ImportAmount);
      GenJnlLine.VALIDATE(Description,DocumentText);
      IF PayrollSetup."Import Department and Project" THEN BEGIN
        GenJnlLine.VALIDATE("Shortcut Dimension 1 Code",ImportGlDimCode1);
        GenJnlLine.VALIDATE("Shortcut Dimension 2 Code",ImportGlDimCode2);
      END;
      GenJnlLine.INSERT;
    END;

    PROCEDURE NewFilename@1080002();
    BEGIN
      IF FileMgt.ClientFileExists(Filename) THEN BEGIN
        IF (Filename[STRLEN(Filename)] = '~') OR
           (Filename[STRLEN(Filename) - 1] = '~')
        THEN
          EXIT;

        file1 := COPYSTR(Filename,1,STRLEN(Filename) - 1) + '~';
        file2 := COPYSTR(Filename,1,STRLEN(Filename) - 2) + '~~';
        IF FileMgt.ClientFileExists(file2) THEN
          FileMgt.DeleteClientFile(file2);
        IF FileMgt.ClientFileExists(file1) THEN
          FileMgt.MoveFile(file1,file2);
        FileMgt.MoveFile(Filename,file1);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

