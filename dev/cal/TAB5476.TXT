OBJECT Table 5476 Sales Invoice Line Aggregate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               UpdateCalculatedFields;
             END;

    OnModify=BEGIN
               UpdateCalculatedFields;
             END;

    OnRename=BEGIN
               UpdateCalculatedFields;
             END;

    CaptionML=[ENU=Sales Invoice Line Aggregate;
               NOR=Salgsfakturalinje - aggregering];
  }
  FIELDS
  {
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 5   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "API Type" := Type;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=" ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)";
                                                                    NOR=" ,Finanskonto,Vare,Ressurs,Aktiva,Gebyr (vare)";
                                                                    SVE=" ,Redov.konto,Artikel,Resurs,Anl.tillg†ng,Omkostnad (artikel)"];
                                                   OptionString=[ ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)] }
    { 6   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 10  ;   ;Shipment Date       ;Date          ;CaptionML=[ENU=Shipment Date;
                                                              NOR=Forsendelsesdato;
                                                              SVE=Utleveransdatum] }
    { 11  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 12  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 15  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5 }
    { 17  ;   ;Qty. to Invoice     ;Decimal       ;CaptionML=[ENU=Qty. to Invoice;
                                                              NOR=Fakturer (antall);
                                                              SVE=Ant. att fakturera];
                                                   DecimalPlaces=0:5 }
    { 18  ;   ;Qty. to Ship        ;Decimal       ;AccessByPermission=TableData 110=R;
                                                   CaptionML=[ENU=Qty. to Ship;
                                                              NOR=Levere (antall);
                                                              SVE=Ant. att utleverera];
                                                   DecimalPlaces=0:5 }
    { 22  ;   ;Unit Price          ;Decimal       ;CaptionML=[ENU=Unit Price;
                                                              NOR=Salgspris;
                                                              SVE=A-pris];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 25  ;   ;VAT %               ;Decimal       ;CaptionML=[ENU=VAT %;
                                                              NOR=Mva-%;
                                                              SVE=Momssats %];
                                                   DecimalPlaces=0:5 }
    { 27  ;   ;Line Discount %     ;Decimal       ;CaptionML=[ENU=Line Discount %;
                                                              NOR=Linjerabatt-%;
                                                              SVE=Radrabatt %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 28  ;   ;Line Discount Amount;Decimal       ;CaptionML=[ENU=Line Discount Amount;
                                                              NOR=Linjerabattbel›p;
                                                              SVE=Radrabatt];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 29  ;   ;Amount              ;Decimal       ;CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Ej f”rfallet belopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 30  ;   ;Amount Including VAT;Decimal       ;CaptionML=[ENU=Amount Including VAT;
                                                              NOR=Bel›p inkl. mva.;
                                                              SVE=Belopp inkl. moms];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 60  ;   ;Quantity Shipped    ;Decimal       ;AccessByPermission=TableData 110=R;
                                                   CaptionML=[ENU=Quantity Shipped;
                                                              NOR=Levert (antall);
                                                              SVE=Utlevererat antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 61  ;   ;Quantity Invoiced   ;Decimal       ;CaptionML=[ENU=Quantity Invoiced;
                                                              NOR=Fakturert antall;
                                                              SVE=Fakturerat antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 69  ;   ;Inv. Discount Amount;Decimal       ;CaptionML=[ENU=Inv. Discount Amount;
                                                              NOR=Fakturarabattbel›p;
                                                              SVE=Fakturarabatt];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 87  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Code;
                                                              NOR=Mva-gruppekode;
                                                              SVE=Skattegruppskod] }
    { 90  ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              NOR=Mva-bokf›ringsgruppe - vare;
                                                              SVE=Moms produktbokf”ringsmall] }
    { 91  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 99  ;   ;VAT Base Amount     ;Decimal       ;CaptionML=[ENU=VAT Base Amount;
                                                              NOR=Mva-grunnlagsbel›p;
                                                              SVE=Nettobelopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 103 ;   ;Line Amount         ;Decimal       ;CaptionML=[ENU=Line Amount;
                                                              NOR=Linjebel›p;
                                                              SVE=Radbelopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 180 ;   ;Line Discount Calculation;Option   ;OnValidate=BEGIN
                                                                UpdateLineDiscounts;
                                                              END;

                                                   CaptionML=[ENU=Line Discount Calculation;
                                                              NOR=Beregning av linjerabatt];
                                                   OptionCaptionML=[ENU=None,%,Amount;
                                                                    NOR=Ingen,%,Bel›p;
                                                                    SVE=Ingen,%,Belopp];
                                                   OptionString=None,%,Amount }
    { 5407;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item),
                                                                     No.=FILTER(<>'')) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                     ELSE IF (Type=CONST(Resource),
                                                                              No.=FILTER(<>'')) "Resource Unit of Measure".Code WHERE (Resource No.=FIELD(No.))
                                                                              ELSE "Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              NOR=Enhetskode;
                                                              SVE=Enhetskod] }
    { 8000;   ;Document Id         ;GUID          ;CaptionML=[ENU=Document Id;
                                                              NOR=Dokument-ID;
                                                              SVE=Dokument-ID] }
    { 8001;   ;Id                  ;Text50        ;CaptionML=[ENU=Id;
                                                              NOR=ID;
                                                              SVE=ID] }
    { 9020;   ;Tax Code            ;Code50        ;CaptionML=[ENU=Tax Code;
                                                              NOR=Mva-kode] }
    { 9021;   ;Tax Amount          ;Decimal       ;CaptionML=[ENU=Tax Amount;
                                                              NOR=Mva-bel›p;
                                                              SVE=Momsbelopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 9022;   ;Discount Applied Before Tax;Boolean;CaptionML=[ENU=Discount Applied Before Tax;
                                                              NOR=Rabatt tatt i bruk f›r skatt] }
    { 9029;   ;API Type            ;Option        ;OnValidate=BEGIN
                                                                Type := "API Type";
                                                              END;

                                                   CaptionML=[ENU=API Type;
                                                              NOR=API-type];
                                                   OptionCaptionML=[ENU=Comment,Account,Item,Resource,Fixed Asset,Charge;
                                                                    NOR=Merknad,Konto,Vare,Ressurs,Aktiva,Gebyr];
                                                   OptionString=Comment,Account,Item,Resource,Fixed Asset,Charge }
    { 9030;   ;Item Id             ;GUID          ;TableRelation=Item.Id;
                                                   OnValidate=BEGIN
                                                                VALIDATE(Type,Type::Item);
                                                                UpdateNo;
                                                              END;

                                                   CaptionML=[ENU=Item Id;
                                                              NOR=Vare-ID;
                                                              SVE=Artikel-ID] }
    { 9031;   ;Account Id          ;GUID          ;TableRelation="G/L Account".Id;
                                                   OnValidate=BEGIN
                                                                VALIDATE(Type,Type::"G/L Account");
                                                                UpdateNo;
                                                              END;

                                                   CaptionML=[ENU=Account Id;
                                                              NOR=Konto-ID;
                                                              SVE=Konto-ID] }
    { 9032;   ;Unit of Measure Id  ;GUID          ;TableRelation="Unit of Measure".Id;
                                                   OnValidate=BEGIN
                                                                UpdateUnitOfMeasureCode;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Id;
                                                              NOR=Enhets-ID;
                                                              SVE=M†ttenhet-ID] }
    { 9039;   ;Line Tax Amount     ;Decimal       ;CaptionML=[ENU=Line Tax Amount;
                                                              NOR=Mva-bel›p for linje];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 9040;   ;Line Amount Including Tax;Decimal  ;CaptionML=[ENU=Line Amount Including Tax;
                                                              NOR=Linjebel›p inkl. mva];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 9041;   ;Line Amount Excluding Tax;Decimal  ;CaptionML=[ENU=Line Amount Excluding Tax;
                                                              NOR=Linjebel›p eks. mva];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 9042;   ;Prices Including Tax;Boolean       ;CaptionML=[ENU=Prices Including Tax;
                                                              NOR=Priser inkl. mva.] }
    { 9043;   ;Inv. Discount Amount Excl. VAT;Decimal;
                                                   CaptionML=[ENU=Inv. Discount Amount Excl. VAT;
                                                              NOR=Fakturarabattbel›p eks. mva];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 9044;   ;Tax Id              ;GUID          ;OnValidate=VAR
                                                                TempTaxGroupBuffer@1000 : TEMPORARY Record 5480;
                                                              BEGIN
                                                                TempTaxGroupBuffer.GetCodesFromTaxGroupId("Tax Id","Tax Group Code","VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Tax Id;
                                                              NOR=Avgifts-ID] }
    { 9050;   ;Line Discount Value ;Decimal       ;OnValidate=BEGIN
                                                                UpdateLineDiscounts;
                                                              END;

                                                   CaptionML=[ENU=Line Discount Value;
                                                              NOR=Linjerabattverdi];
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;Document Id,Line No.                    ;Clustered=Yes }
    {    ;Id                                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LineDiscountPctMustBePositiveErr@1002 : TextConst '@@@={Locked};ENU=Line discount percentage must be positive.;NOR=Line discount percentage must be positive.';
      LineDiscountPctMustBeBelowHundredErr@1001 : TextConst '@@@={Locked};ENU=Line discount percentage must be below 100.;NOR=Line discount percentage must be below 100.';
      LineDiscountAmtMustBePositiveErr@1000 : TextConst '@@@={Locked};ENU=Line discount amount must be positive.;NOR=Line discount amount must be positive.';

    [External]
    PROCEDURE UpdateItemId@1();
    VAR
      Item@1000 : Record 27;
    BEGIN
      IF ("No." = '') OR (Type <> Type::Item) THEN BEGIN
        CLEAR("Item Id");
        EXIT;
      END;

      IF NOT Item.GET("No.") THEN
        EXIT;

      "Item Id" := Item.Id;
    END;

    [External]
    PROCEDURE UpdateAccountId@12();
    VAR
      GLAccount@1000 : Record 15;
    BEGIN
      IF ("No." = '') OR (Type <> Type::"G/L Account") THEN BEGIN
        CLEAR("Account Id");
        EXIT;
      END;

      IF NOT GLAccount.GET("No.") THEN
        EXIT;

      "Account Id" := GLAccount.Id;
    END;

    [External]
    PROCEDURE UpdateNo@5();
    VAR
      Item@1001 : Record 27;
      GLAccount@1000 : Record 15;
    BEGIN
      CASE Type OF
        Type::Item:
          BEGIN
            Item.SETRANGE(Id,"Item Id");
            IF NOT Item.FINDFIRST THEN
              EXIT;

            "No." := Item."No.";
          END;
        Type::"G/L Account":
          BEGIN
            GLAccount.SETRANGE(Id,"Account Id");
            IF NOT GLAccount.FINDFIRST THEN
              EXIT;

            "No." := GLAccount."No.";
          END;
      END;
    END;

    LOCAL PROCEDURE UpdateCalculatedFields@3();
    BEGIN
      UpdateReferencedRecordIds;
      "API Type" := Type;
    END;

    [External]
    PROCEDURE UpdateReferencedRecordIds@2();
    BEGIN
      UpdateItemId;
      UpdateAccountId;
      UpdateUnitOfMeasureId;
    END;

    LOCAL PROCEDURE UpdateUnitOfMeasureId@6();
    VAR
      UnitOfMeasure@1000 : Record 204;
    BEGIN
      CLEAR("Unit of Measure Id");
      IF "Unit of Measure Code" = '' THEN
        EXIT;

      IF NOT UnitOfMeasure.GET("Unit of Measure Code") THEN
        EXIT;

      "Unit of Measure Id" := UnitOfMeasure.Id;
    END;

    LOCAL PROCEDURE UpdateUnitOfMeasureCode@7();
    VAR
      UnitOfMeasure@1000 : Record 204;
    BEGIN
      IF ISNULLGUID("Unit of Measure Id") THEN BEGIN
        VALIDATE("Unit of Measure Code",'');
        EXIT;
      END;

      UnitOfMeasure.SETRANGE(Id,"Unit of Measure Id");
      UnitOfMeasure.FINDFIRST;
      "Unit of Measure Code" := UnitOfMeasure.Code;
    END;

    [External]
    PROCEDURE UpdateLineDiscounts@4();
    VAR
      Currency@1000 : Record 4;
      LineAmount@1001 : Decimal;
    BEGIN
      IF "Currency Code" = '' THEN
        Currency.InitRoundingPrecision;
      CASE "Line Discount Calculation" OF
        "Line Discount Calculation"::"%":
          BEGIN
            "Line Discount %" := "Line Discount Value";
            "Line Discount Amount" :=
              ROUND(
                ROUND(Quantity * "Unit Price",Currency."Amount Rounding Precision") *
                "Line Discount %" / 100,Currency."Amount Rounding Precision");
          END;
        "Line Discount Calculation"::Amount,"Line Discount Calculation"::None:
          BEGIN
            "Line Discount Amount" := "Line Discount Value";
            LineAmount := Quantity * "Unit Price";
            IF LineAmount <> 0 THEN
              "Line Discount %" :=
                ROUND("Line Discount Amount" / ROUND(LineAmount,Currency."Amount Rounding Precision") * 100,0.00001)
          END;
      END;

      IF "Line Discount %" < 0 THEN
        ERROR(LineDiscountPctMustBePositiveErr);

      IF "Line Discount %" > 100 THEN
        ERROR(LineDiscountPctMustBeBelowHundredErr);

      IF "Line Discount Amount" < 0 THEN
        ERROR(LineDiscountAmtMustBePositiveErr);
    END;

    [External]
    PROCEDURE SetDiscountValue@8();
    BEGIN
      CASE "Line Discount Calculation" OF
        "Line Discount Calculation"::"%":
          "Line Discount Value" := "Line Discount %";
        "Line Discount Calculation"::Amount,"Line Discount Calculation"::None:
          BEGIN
            "Line Discount Calculation" := "Line Discount Calculation"::Amount;
            "Line Discount Value" := "Line Discount Amount";
          END;
      END;
    END;

    BEGIN
    END.
  }
}

