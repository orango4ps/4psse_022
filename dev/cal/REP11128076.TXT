OBJECT Report 11128076 Receive PG Autogiro
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE7.02.01 (12047971);
  }
  PROPERTIES
  {
    CaptionML=[ENU=Receive PG Auto Giro;
               SVE=èterredov. PG Autogiro A04];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF gBankAccountNo = '' THEN
                    ERROR(PEB009);
                  gTextFile.TEXTMODE := TRUE;
                  IF gFileMgt.CanRunDotNetOnClient THEN
                    gServerFileName := gFileMgt.UploadFileSilent(gFileName)
                  ELSE
                    gServerFileName := gFileMgt.UploadFile(PEB007, gFileName);

                  IF gServerFileName = '' THEN
                    CurrReport.QUIT;

                  gTextFile.OPEN(gServerFileName);
                END;

    OnPostReport=BEGIN
                   gTextFile.CLOSE;
                   ERASE(gServerFileName);
                 END;

  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               gGenJnlLine2.SETRANGE("Journal Template Name",gJnlTemplateName);
                               gGenJnlLine2.SETRANGE("Journal Batch Name",gJnlBatchName);
                               IF gGenJnlLine2.FIND('+') THEN
                                 gNextLineNo := gGenJnlLine2."Line No."
                               ELSE
                                 gNextLineNo := 0;
                               gWindow.OPEN(PEB002);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF gTextFile.LEN = gTextFile.POS THEN
                                    CurrReport.BREAK;
                                  gTextFile.READ(gTextLine);
                                  gType := COPYSTR(gTextLine,1,2);
                                  CASE gType OF
                                    '41','42':
                                          BEGIN // Debitering = 41 Kreditering = 42
                                            gGenJnlLine.INIT;
                                            gGenJnlLine."Journal Template Name" := gJnlTemplateName;
                                            gGenJnlLine."Journal Batch Name" := gJnlBatchName;
                                            gNextLineNo := gNextLineNo + 10000;
                                            gGenJnlLine."Line No." := gNextLineNo;
                                            CLEAR(gPostingDate);
                                            gPostingDate := GetPostingDate(COPYSTR(gTextLine,75,6));
                                            gGenJnlLine.VALIDATE("Posting Date",gPostingDate);
                                            gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::Customer);
                                            gGenJnlLine.VALIDATE("Document No.",gDocNo);
                                            gApplyToDocNo := COPYSTR(gTextLine,3,25);
                                            gWindow.UPDATE(1,gApplyToDocNo);
                                            IF InvoiceExist THEN
                                              gGenJnlLine.VALIDATE("Applies-to Doc. No.", gApplyToDocNo)
                                            ELSE
                                              IF CreditMemoExist THEN
                                                gGenJnlLine.VALIDATE("Applies-to Doc. No.", gApplyToDocNo)
                                              ELSE
                                                gGenJnlLine.VALIDATE("Applies-to Doc. No.", '');
                                            gAmountText := COPYSTR(gTextLine,28,13);
                                            gTransferAmount := 0;
                                            IF EVALUATE(gTransferAmount, gAmountText) THEN;
                                            gTransferAmount := ROUND(gTransferAmount / 100);
                                            IF gType = '42' THEN
                                              gTransferAmount := -gTransferAmount;
                                            gGenJnlLine.VALIDATE(Amount, -gTransferAmount);
                                            IF COPYSTR(gTextLine,67,2) = '10' THEN
                                              gGenJnlLine.Description := gApplyToDocNo+' '+PEB003;
                                            IF COPYSTR(gTextLine,67,2) = '12' THEN
                                              gGenJnlLine.Description := gApplyToDocNo+' '+PEB004;
                                            IF COPYSTR(gTextLine,67,2) = '13' THEN
                                              gGenJnlLine.Description := gApplyToDocNo+' '+PEB005;
                                            IF gGenJnlLine."Account No." = '' THEN
                                              gGenJnlLine.Description := PEB006;
                                            IF (COPYSTR(gTextLine,67,2) = '10') OR
                                               (COPYSTR(gTextLine,67,2) = '12') OR
                                               (COPYSTR(gTextLine,67,2) = '13') THEN
                                              gNotCorrectAmount := gNotCorrectAmount + gGenJnlLine.Amount;
                                            gGenJnlLine.INSERT;
                                          END;
                                    '50': BEGIN // TOTALPOST PER DATAMEDIA
                                            gGenJnlLine.INIT;
                                            gGenJnlLine."Journal Template Name" := gJnlTemplateName;
                                            gGenJnlLine."Journal Batch Name" := gJnlBatchName;
                                            gNextLineNo := gNextLineNo + 10000;
                                            gGenJnlLine."Line No." := gNextLineNo;
                                            gGenJnlLine.VALIDATE("Posting Date", gPostingDate);
                                            gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"G/L Account");
                                            gGenJnlLine.VALIDATE("Document No.",gDocNo);

                                            CASE gAccountType OF
                                              gAccountType::"G/L Account":
                                                gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"G/L Account");
                                              gAccountType::"Bank Account":
                                                gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"Bank Account");
                                            END;
                                            gGenJnlLine.VALIDATE("Account No.", gBankAccountNo);

                                            gGenJnlLine."Document Type" := gGenJnlLine."Document Type"::Payment;
                                            gGenJnlLine.VALIDATE("Applies-to Doc. No.", '');
                                            gAmountTextCredit := COPYSTR(gTextLine,32,15);
                                            gAmountText := COPYSTR(gTextLine,60,15);
                                            gTransferAmountCredit := 0;
                                            gTransferAmount := 0;
                                            EVALUATE(gTransferAmountCredit,gAmountTextCredit);
                                            EVALUATE(gTransferAmount, gAmountText);
                                            gTransferAmount := ROUND(gTransferAmount / 100) - ROUND(gTransferAmountCredit / 100);
                                            gTransferAmount := gTransferAmount + gNotCorrectAmount;
                                            gGenJnlLine.VALIDATE(Amount, gTransferAmount);
                                            gGenJnlLine.VALIDATE(Description,PEB007);
                                            gGenJnlLine.INSERT;
                                          END;
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   gDocNo := gGenJnlLine."Document No.";
                   gFileNameVisible := gFileMgt.CanRunDotNetOnClient;
                 END;

    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=Cont;
                  ContainerType=ContentArea }

      { 1100570000;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 12047959;2;Field    ;
                  CaptionML=[ENU=File Name;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=gFileName;
                  Visible=gFileNameVisible;
                  OnAssistEdit=BEGIN
                                 gFileName := gFileMgt.OpenFileDialog(PEB007,gFileName,PEB008);
                               END;
                                }

      { 1000000002;2;Field  ;
                  CaptionML=[ENU=Document No.;
                             NOR=Dokumentnr.;
                             SVE=Verifikationsnr];
                  SourceExpr=gDocNo }

      { 12047958;2;Field    ;
                  CaptionML=[ENU=Account Type;
                             NOR=Kontotype;
                             SVE=Kontotyp];
                  OptionCaptionML=[ENU=G/L Account,,,Bank Account;
                                   NOR=Hovedbokskonto,,,Bankkonto;
                                   SVE=Redov.konto,,,Bankkonto];
                  SourceExpr=gAccountType;
                  OnValidate=BEGIN
                               CLEAR(gBankAccountNo);
                             END;
                              }

      { 12047960;2;Field    ;
                  CaptionML=[ENU=Account No;
                             NOR=Nr;
                             SVE=Kontonr];
                  SourceExpr=gBankAccountNo;
                  OnValidate=BEGIN
                               IF gBankAccountNo = '' THEN
                                 EXIT;
                               CASE gAccountType OF
                                 gAccountType::"G/L Account":
                                   BEGIN
                                     gGLAccount.GET(gBankAccountNo);
                                   END;
                                 gAccountType::"Bank Account":
                                   BEGIN
                                     gBankAccount.GET(gBankAccountNo);
                                   END;
                               END;
                             END;

                  OnLookup=BEGIN
                             CASE gAccountType OF
                               gAccountType::"G/L Account":
                                 BEGIN
                                   IF PAGE.RUNMODAL(0,gGLAccount) = ACTION::LookupOK THEN
                                     gBankAccountNo := gGLAccount."No.";
                                 END;
                               gAccountType::"Bank Account":
                                 BEGIN
                                   IF PAGE.RUNMODAL(0,gBankAccount) = ACTION::LookupOK THEN
                                     gBankAccountNo := gBankAccount."No.";
                                 END;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gFileName@12047960 : Text;
      gTextFile@1000 : File;
      gTextLine@1002 : Text[250];
      gType@1003 : Text[2];
      gPostingDate@1004 : Date;
      gDocNo@1005 : Code[20];
      gGenJnlLine@1006 : Record 81;
      gTransferAmount@1007 : Decimal;
      gTransferAmountCredit@1008 : Decimal;
      gNextLineNo@1009 : Integer;
      gApplyToDocNo@1011 : Code[20];
      gAmountText@1012 : Text[30];
      gAmountTextCredit@1013 : Text[30];
      gJnlTemplateName@1014 : Code[10];
      gJnlBatchName@1015 : Code[10];
      gGenJnlLine2@1016 : Record 81;
      gWindow@1017 : Dialog;
      gNotCorrectAmount@1018 : Decimal;
      PEB001@1020 : TextConst 'ENU=Fill in file name;SVE=Skriv in filnamn.';
      PEB002@1021 : TextConst 'ENU=Document No. #1########;SVE=Dokumentnr #1########';
      PEB003@1022 : TextConst 'ENU="FEL! Uppdr. ej utf., kod=10";SVE="FEL! Uppdr. ej utf., kod=10"';
      PEB004@1023 : TextConst 'ENU="FEL! Uppdr. ej utf., kod=12";SVE="FEL! Uppdr. ej utf., kod=12"';
      PEB005@1024 : TextConst 'ENU="FEL! Uppdr. ej utf., kod=13";SVE="FEL! Uppdr. ej utf., kod=13"';
      PEB006@1025 : TextConst 'ENU=FEL! Fakturan/kreditfakturan Ñr redan reglerad.;SVE=FEL! Fakturan/kreditfakturan Ñr redan reglerad.';
      PEB007@1026 : TextConst 'ENU=Auto Giro Payment;SVE=Autogirobetalning';
      PEB008@1100570002 : TextConst 'ENU=All files|*.*|TXT-files (*.txt)|*.txt|WRI-files (*.wri)|*.wri;SVE=Alla filer|*.*|TXT-filer (*.txt)|*.txt|WRI-filer (*.wri)|*.wri';
      gServerFileName@12047958 : Text;
      gFileMgt@12047959 : Codeunit 419;
      gBankAccountNo@12047964 : Code[20];
      gGLAccount@12047963 : Record 15;
      gBankAccount@12047962 : Record 270;
      gAccountType@12047961 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';
      PEB009@12047965 : TextConst 'ENU=Account No must not be blank;SVE=Kontonr mÜste vara ifyllt';
      gFileNameVisible@12047966 : Boolean INDATASET;

    PROCEDURE SetGenJnlLine@1(NewGLJnlLine@1000 : Record 81);
    BEGIN
      gGenJnlLine := NewGLJnlLine;
      gJnlTemplateName := gGenJnlLine."Journal Template Name";
      gJnlBatchName := gGenJnlLine."Journal Batch Name";
    END;

    PROCEDURE InvoiceExist@2() : Boolean;
    VAR
      CustLedgerEntry@1000 : Record 21;
    BEGIN
      CustLedgerEntry.SETCURRENTKEY("Document No.");
      CustLedgerEntry.SETRANGE("Document Type", CustLedgerEntry."Document Type"::Invoice);
      CustLedgerEntry.SETRANGE("Document No.",gApplyToDocNo);
      CustLedgerEntry.SETRANGE(Open,TRUE);
      IF CustLedgerEntry.FIND('-') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE CreditMemoExist@5() : Boolean;
    VAR
      CustLedgerEntry@1000 : Record 21;
    BEGIN
      CustLedgerEntry.SETCURRENTKEY("Document No.");
      CustLedgerEntry.SETRANGE("Document Type", CustLedgerEntry."Document Type"::"Credit Memo");
      CustLedgerEntry.SETRANGE("Document No.",gApplyToDocNo);
      CustLedgerEntry.SETRANGE(Open,TRUE);
      IF CustLedgerEntry.FIND('-') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE GetPostingDate@12047958(pPostingDate@12047958 : Text) rPostingDate : Date;
    VAR
      Year@12047959 : Integer;
      Month@12047960 : Integer;
      Day@12047961 : Integer;
    BEGIN
      IF NOT EVALUATE(Year,COPYSTR(pPostingDate,1,2)) THEN
        EXIT(0D);
      IF NOT EVALUATE(Month,COPYSTR(pPostingDate,3,2)) THEN
        EXIT(0D);
      IF NOT EVALUATE(Day,COPYSTR(pPostingDate,5,2)) THEN
        EXIT(0D);

      EXIT(DMY2DATE(Day,Month,Year+2000));
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0006 Autogiro
      PEB
      PEB *** End ***
    }
    END.
  }
  RDLDATA
  {
  }
}

