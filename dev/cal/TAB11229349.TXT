OBJECT Table 11229349 Project Capacity Need Detail
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Project No.");
               TESTFIELD("Need Date");
               "User ID" := USERID;
               "Last Date Modified" := TODAY;

               IF "Line Type" = "Line Type"::Main THEN
                 "Detail Line No." := 0;

               UpdateRequestedHoursMain("Requested Hours");
             END;

    OnModify=BEGIN
               "User ID" := USERID;
               "Last Date Modified" := TODAY;
               UpdateRequestedHoursMain("Requested Hours" - xRec."Requested Hours");;
             END;

    OnDelete=BEGIN
               UpdateRequestedHoursMain(-xRec."Requested Hours");
             END;

    CaptionML=ENU=Project Capacity Need Detail;
    LookupPageID=Page11132129;
    DrillDownPageID=Page11132129;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Element             ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element];
                                                   Editable=No }
    { 30  ;   ;Need Date           ;Date          ;CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum] }
    { 35  ;   ;Line Type           ;Option        ;CaptionML=[ENU=Line Type;
                                                              SVE=Rad Typ];
                                                   OptionCaptionML=ENU=Main,Detail;
                                                   OptionString=Main,Detail;
                                                   Editable=No }
    { 36  ;   ;Detail Line No.     ;Integer       ;CaptionML=ENU=Detail Line No.;
                                                   Editable=No }
    { 40  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              NOR=Bruker-ID;
                                                              SVE=AnvÑndar-ID];
                                                   Editable=No }
    { 50  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Oppdateringsdato;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 60  ;   ;Discipline          ;Code10        ;TableRelation=Discipline;
                                                   CaptionML=[ENU=Discipline;
                                                              NOR=Disiplin;
                                                              SVE=Disciplin];
                                                   Editable=No }
    { 70  ;   ;Comment             ;Text80        ;CaptionML=[ENU=Comment;
                                                              SVE=Kommentar] }
    { 120 ;   ;Requested Hours     ;Decimal       ;CaptionML=[ENU=Requested Hours;
                                                              NOR=ùnskede timer;
                                                              SVE=BegÑrda timmar] }
    { 130 ;   ;Project Status      ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job."Project Status" WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Status;
                                                              SVE=Projekt- status];
                                                   OptionCaptionML=[ENU=Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived;
                                                                    SVE=Kalkyl,Fîrberedelse,Produktion,Tekniskt fÑrdig,Administrativt fÑrdig,FÑrdig,Arkiv,Arkiverad];
                                                   OptionString=Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived;
                                                   Editable=No }
    { 140 ;   ;Project Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 150 ;   ;Element Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Element)));
                                                   CaptionML=[ENU=Element Description;
                                                              SVE=Elementbeskrivning];
                                                   Editable=No }
    { 160 ;   ;Discipline Description;Text30      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Discipline.Description WHERE (Code=FIELD(Discipline)));
                                                   CaptionML=[ENU=Discipline Description;
                                                              SVE=Disciplinbeskrivning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.,Element,Discipline,Line Type,Detail Line No.,Need Date;
                                                   Clustered=Yes }
    {    ;Need Date                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RequestedHoursToAdd@1100528500 : Decimal;

    PROCEDURE UpdateProjectCapacityNeedDetail@1100528510(ProjectCapacityNeed@1100528500 : Record 11229348;DateInMatrix@1100528501 : Date;RequestedHours@1100528502 : Decimal);
    VAR
      ProjectCapacityNeedDetail@1100528503 : Record 11229349;
    BEGIN
      ProjectCapacityNeedDetail.INIT;
      IF ProjectCapacityNeedDetail.GET(
           ProjectCapacityNeed."Project No.",ProjectCapacityNeed.Element,ProjectCapacityNeed.Discipline,
           ProjectCapacityNeed."Line Type",ProjectCapacityNeed."Detail Line No.",DateInMatrix) THEN
      BEGIN
        IF (RequestedHours = 0) AND (ProjectCapacityNeedDetail.Comment = '') THEN
          ProjectCapacityNeedDetail.DELETE(TRUE)
        ELSE BEGIN
          ProjectCapacityNeedDetail."Requested Hours" := RequestedHours;
          ProjectCapacityNeedDetail.MODIFY(TRUE);
        END;
      END ELSE BEGIN
        IF RequestedHours <> 0 THEN BEGIN
          ProjectCapacityNeedDetail."Project No." := ProjectCapacityNeed."Project No.";
          ProjectCapacityNeedDetail.Element := ProjectCapacityNeed.Element;
          ProjectCapacityNeedDetail.Discipline := ProjectCapacityNeed.Discipline;
          ProjectCapacityNeedDetail."Line Type" := ProjectCapacityNeed."Line Type";
          ProjectCapacityNeedDetail."Detail Line No." := ProjectCapacityNeed."Detail Line No.";
          ProjectCapacityNeedDetail."Need Date" := DateInMatrix;
          ProjectCapacityNeedDetail."Requested Hours" := RequestedHours;
          ProjectCapacityNeedDetail.INSERT(TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE UpdateRequestedHoursMain@1100528504(RequestedHoursToAdd@1100528501 : Decimal);
    VAR
      ProjectCapacityNeedDetail@1100528500 : Record 11229349;
      PresentDetailRequestedHours@1100528502 : Decimal;
      NewRequestedHours@1100528503 : Decimal;
    BEGIN
      IF "Line Type" <> "Line Type"::Detail THEN
        EXIT;

      ProjectCapacityNeedDetail := Rec;
      ProjectCapacityNeedDetail.SETRECFILTER;
      ProjectCapacityNeedDetail.SETRANGE("Detail Line No.");
      ProjectCapacityNeedDetail.CALCSUMS("Requested Hours");
      PresentDetailRequestedHours := ProjectCapacityNeedDetail."Requested Hours";

      NewRequestedHours := PresentDetailRequestedHours + RequestedHoursToAdd;

      IF ProjectCapacityNeedDetail.GET("Project No.",Element,Discipline,"Line Type"::Main,0,"Need Date") THEN BEGIN
        IF (NewRequestedHours = 0) AND (ProjectCapacityNeedDetail.Comment = '') THEN
          ProjectCapacityNeedDetail.DELETE(TRUE)
        ELSE BEGIN
          ProjectCapacityNeedDetail."Requested Hours" := NewRequestedHours;
          ProjectCapacityNeedDetail.MODIFY(TRUE);
        END;
      END ELSE BEGIN
        IF NewRequestedHours <> 0 THEN BEGIN
          ProjectCapacityNeedDetail := Rec;
          ProjectCapacityNeedDetail."Line Type" := ProjectCapacityNeedDetail."Line Type"::Main;
          ProjectCapacityNeedDetail."Detail Line No." := 0;
          ProjectCapacityNeedDetail.Comment := '';
          ProjectCapacityNeedDetail."Requested Hours" := NewRequestedHours;
          ProjectCapacityNeedDetail.INSERT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

