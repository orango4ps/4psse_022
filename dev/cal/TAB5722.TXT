OBJECT Table 5722 Item Category
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Code);
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               UpdatePresentationOrderAfterTheTrigger;
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    OnDelete=BEGIN
               IF "Has Children" THEN
                 ERROR(DeleteWithChildrenErr);
               UpdateDeletedCategoryItems;
               DeleteAssignedAttributes;
             END;

    OnRename=BEGIN
               UpdatePresentationOrderAfterTheTrigger;
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    CaptionML=[ENU=Item Category;
               NOR=Varekategori;
               SVE=Artikel Kategorisering];
    LookupPageID=Page5730;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Parent Category     ;Code20        ;TableRelation="Item Category";
                                                   OnValidate=VAR
                                                                ItemCategory@1000 : Record 5722;
                                                                ItemAttributeManagement@1002 : Codeunit 7500;
                                                                ParentCategory@1001 : Code[20];
                                                              BEGIN
                                                                ParentCategory := "Parent Category";
                                                                WHILE ItemCategory.GET(ParentCategory) DO BEGIN
                                                                  IF ItemCategory.Code = Code THEN
                                                                    ERROR(CyclicInheritanceErr);
                                                                  ParentCategory := ItemCategory."Parent Category";
                                                                END;
                                                                ItemAttributeManagement.UpdateCategoryAttributesAfterChangingParentCategory(Code,"Parent Category",xRec."Parent Category");
                                                              END;

                                                   CaptionML=[ENU=Parent Category;
                                                              NOR=Overordnet kategori] }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 9   ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              NOR=Innrykking;
                                                              SVE=Indrag];
                                                   MinValue=0 }
    { 10  ;   ;Presentation Order  ;Integer       ;CaptionML=[ENU=Presentation Order;
                                                              NOR=Presentasjonsrekkef›lge;
                                                              SVE=Presentationsordning] }
    { 11  ;   ;Has Children        ;Boolean       ;CaptionML=[ENU=Has Children;
                                                              NOR=Har underordnede;
                                                              SVE=Har underordnade] }
    { 12  ;   ;Last Modified Date Time;DateTime   ;CaptionML=[ENU=Last Modified Date Time;
                                                              NOR=Dato/klokkeslett for siste endring;
                                                              SVE=Senast „ndrad, datum och tid];
                                                   Editable=No }
    { 8000;   ;Id                  ;GUID          ;CaptionML=[ENU=Id;
                                                              NOR=ID;
                                                              SVE=ID] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Parent Category                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CyclicInheritanceErr@1001 : TextConst 'ENU=An item category cannot be a parent of itself or any of its children.;NOR=En vare varekategori kan ikke v‘re en overordnet for seg selv eller underordnede.';
      ItemCategoryManagement@1002 : Codeunit 5722;
      DeleteWithChildrenErr@1003 : TextConst 'ENU=You cannot delete this item category because it has child item categories.;NOR=Du kan ikke slette denne varekategorien fordi den har underordnede varekategorier.';
      DeleteItemInheritedAttributesQst@1004 : TextConst '@@@=%1 - item category code;ENU="One or more items belong to item category ''''%1''''.\\Do you want to delete the inherited item attributes for the items in question? ";NOR=n eller flere varer tilh›rer varekategorien %1.\\Vil du slette de arvede vareattributtene for de aktuelle varene?;SVE=En eller flera artiklar tillh”r artikelkategorin %1.\\Vill du ta bort de „rvda artikelattributen f”r de ber”rda artiklarna?';

    [External]
    PROCEDURE HasChildren@1() : Boolean;
    VAR
      ItemCategory@1000 : Record 5722;
    BEGIN
      ItemCategory.SETRANGE("Parent Category",Code);
      EXIT(NOT ItemCategory.ISEMPTY)
    END;

    LOCAL PROCEDURE UpdatePresentationOrderAfterTheTrigger@2();
    BEGIN
      IF BINDSUBSCRIPTION(ItemCategoryManagement) THEN;
    END;

    [External]
    PROCEDURE GetStyleText@3() : Text;
    BEGIN
      IF Indentation = 0 THEN
        EXIT('Strong');

      IF "Has Children" THEN
        EXIT('Strong');

      EXIT('');
    END;

    LOCAL PROCEDURE UpdateDeletedCategoryItems@6();
    VAR
      CategoryItem@1001 : Record 27;
      TempCategoryItemAttributeValue@1000 : TEMPORARY Record 7501;
      ItemAttributeManagement@1003 : Codeunit 7500;
      DeleteItemInheritedAttributes@1002 : Boolean;
    BEGIN
      CategoryItem.SETRANGE("Item Category Code",Code);
      IF CategoryItem.ISEMPTY THEN
        EXIT;
      DeleteItemInheritedAttributes := CONFIRM(STRSUBSTNO(DeleteItemInheritedAttributesQst,Code));
      IF DeleteItemInheritedAttributes THEN
        TempCategoryItemAttributeValue.LoadCategoryAttributesFactBoxData(Code);
      IF CategoryItem.FIND('-') THEN
        REPEAT
          CategoryItem.VALIDATE("Item Category Code",'');
          CategoryItem.MODIFY;
          IF DeleteItemInheritedAttributes THEN
            ItemAttributeManagement.DeleteItemAttributeValueMapping(CategoryItem,TempCategoryItemAttributeValue);
        UNTIL CategoryItem.NEXT = 0;
    END;

    LOCAL PROCEDURE DeleteAssignedAttributes@4();
    VAR
      ItemAttributeValueMapping@1000 : Record 7505;
    BEGIN
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::"Item Category");
      ItemAttributeValueMapping.SETRANGE("No.",Code);
      ItemAttributeValueMapping.DELETEALL;
    END;

    PROCEDURE GetSelectionFilter@11012002(VAR ItemCategory@1100525000 : Record 5722) : Code[250];
    VAR
      SelectionFilter@1003 : Code[250];
      RecordCount@1004 : Integer;
    BEGIN
      //**4PS
      RecordCount := ItemCategory.COUNT;
      IF RecordCount > 0 THEN BEGIN
        ItemCategory.FINDSET;
        WHILE RecordCount > 0 DO BEGIN
          RecordCount := RecordCount - 1;
          IF SelectionFilter <> '' THEN
            SelectionFilter := SelectionFilter + '|';
          SelectionFilter := SelectionFilter + ItemCategory.Code;
          IF RecordCount > 0 THEN
            ItemCategory.NEXT;
        END;
      END;
      EXIT(SelectionFilter);
    END;

    BEGIN
    END.
  }
}

