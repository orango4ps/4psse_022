OBJECT Table 11020420 Unit Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Creation Date" := TODAY;
               "Created by" := USERID;
               "Modification Date" := 0D;
               "Modified by" := '';
             END;

    OnModify=BEGIN
               "Modification Date" := TODAY;
               "Modified by" := USERID;
             END;

    OnDelete=VAR
               lvUPLineRec@1100485000 : Record 11020421;
             BEGIN
               lvUPLineRec.SETRANGE("Customer No.", "Customer No.");
               lvUPLineRec.SETRANGE("Price List", "Price List");
               lvUPLineRec.SETRANGE("Unit Price", Code);
               lvUPLineRec.SETRANGE("Start Date", "Start Date");
               lvUPLineRec.DELETEALL;
             END;

    CaptionML=[ENU=Unit Price;
               NOR=Enhetspris;
               SVE=A-pris];
    LookupPageID=Page11072522;
    DrillDownPageID=Page11072522;
  }
  FIELDS
  {
    { 10  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer;
                                                              NOR=Kundens finansielle status;
                                                              SVE=Kund];
                                                   Editable=No }
    { 20  ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Kundnavn;
                                                              SVE=Kundnamn];
                                                   Editable=No }
    { 30  ;   ;Price List          ;Code10        ;TableRelation="Price List".Code WHERE (Customer No.=FIELD(Customer No.));
                                                   OnValidate=VAR
                                                                lvPriceListRec@1100485000 : Record 11020422;
                                                              BEGIN
                                                                IF "Price List" <> '' THEN BEGIN
                                                                  lvPriceListRec.GET("Customer No.", "Price List");
                                                                  "Start Date" := lvPriceListRec."Start Date";
                                                                  VALIDATE("Reference Date", lvPriceListRec."Start Date");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Price List;
                                                              NOR=Prisliste;
                                                              SVE=Prislista] }
    { 40  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 50  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 55  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 60  ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettelsesdato;
                                                              SVE=Uppl„ggningsdatum];
                                                   Editable=No }
    { 70  ;   ;Created by          ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Created by;
                                                              NOR=Opprettet av;
                                                              SVE=Skapades av];
                                                   Editable=No }
    { 80  ;   ;Modification Date   ;Date          ;CaptionML=[ENU=Modification Date;
                                                              NOR=Endringsdato;
                                                              SVE=Žndringsdatum];
                                                   Editable=No }
    { 90  ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=Žndrad av];
                                                   Editable=No }
    { 100 ;   ;Total Cost Price    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Unit Price Line"."Cost Price Amount" WHERE (Customer No.=FIELD(Customer No.),
                                                                                                                Price List=FIELD(Price List),
                                                                                                                Unit Price=FIELD(Code),
                                                                                                                Start Date=FIELD(Start Date)));
                                                   CaptionML=[ENU=Total Cost Price;
                                                              NOR=Total selvkost;
                                                              SVE=Totalt sj„lvkostnadspris];
                                                   Editable=No }
    { 110 ;   ;Total Sales Price   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Unit Price Line"."Sales Amount" WHERE (Customer No.=FIELD(Customer No.),
                                                                                                           Price List=FIELD(Price List),
                                                                                                           Unit Price=FIELD(Code),
                                                                                                           Start Date=FIELD(Start Date)));
                                                   CaptionML=[ENU=Total Sales Price;
                                                              NOR=Total salgspris;
                                                              SVE=Totalt f”rs„ljningspris];
                                                   Editable=No }
    { 120 ;   ;Margin              ;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS("Total Sales Price");
                                                                IF "Total Sales Price" <> 0 THEN BEGIN
                                                                  "Margin %" := (Margin/"Total Sales Price") * 100;
                                                                END ELSE BEGIN
                                                                  "Margin %" := 0;
                                                                END;
                                                                Price := "Total Sales Price" + Margin;

                                                                CALCFIELDS("Total Cost Price");
                                                                "Net Margin" := Price - "Total Cost Price";
                                                                "Net Margin %" := 0;
                                                                IF "Total Cost Price" <> 0 THEN
                                                                  "Net Margin %" := "Net Margin"/"Total Cost Price" * 100;
                                                              END;

                                                   CaptionML=[ENU=Difference;
                                                              NOR=Differanse;
                                                              SVE=Differens] }
    { 125 ;   ;Margin %            ;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS("Total Sales Price");
                                                                Margin := "Total Sales Price" * ("Margin %"/100);
                                                                Price := "Total Sales Price" + Margin;

                                                                CALCFIELDS("Total Cost Price");
                                                                "Net Margin" := Price - "Total Cost Price";
                                                                "Net Margin %" := 0;
                                                                IF "Total Cost Price" <> 0 THEN
                                                                  "Net Margin %" := "Net Margin"/"Total Cost Price" * 100;
                                                              END;

                                                   CaptionML=[ENU=Difference %;
                                                              NOR=Differanseprosent;
                                                              SVE=Differensprocent] }
    { 130 ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet] }
    { 140 ;   ;Price               ;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS("Total Sales Price");
                                                                Margin := Price - "Total Sales Price";
                                                                IF "Total Sales Price" <> 0 THEN BEGIN
                                                                  "Margin %" := (Margin/"Total Sales Price") * 100;
                                                                END ELSE BEGIN
                                                                  "Margin %" := 0;
                                                                END;

                                                                CALCFIELDS("Total Cost Price");
                                                                "Net Margin" := Price - "Total Cost Price";
                                                                "Net Margin %" := 0;
                                                                IF "Total Cost Price" <> 0 THEN
                                                                  "Net Margin %" := "Net Margin"/"Total Cost Price" * 100;
                                                              END;

                                                   CaptionML=[ENU=Sales Price;
                                                              NOR=Salgspris;
                                                              SVE=F”rs„ljningspris] }
    { 150 ;   ;Start Date          ;Date          ;CaptionML=[ENU=Start Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 160 ;   ;Reference Date      ;Date          ;OnValidate=BEGIN
                                                                //After the validate, update the prices on the lines with function UpdateSalesRates();
                                                              END;

                                                   CaptionML=[ENU=Reference Date;
                                                              NOR=Referansedato;
                                                              SVE=Referensdatum] }
    { 170 ;   ;Net Margin          ;Decimal       ;CaptionML=ENU=Net Margin;
                                                   Editable=No }
    { 180 ;   ;Net Margin %        ;Decimal       ;CaptionML=ENU=Net Margin %;
                                                   Editable=No }
    { 190 ;   ;Update Price        ;Option        ;CaptionML=[ENU=Update based on;
                                                              NOR=Oppdatering basert p†;
                                                              SVE=Uppdatering baserad p†];
                                                   OptionCaptionML=[ENU=Difference Amount,Difference Percentage,Sales Price;
                                                                    NOR=Differansebel›p,Differanseprosent,Salgspris;
                                                                    SVE=Differensbelopp,Differensprocent,F”rs„ljningspris];
                                                   OptionString=Amount,Percentage,Sales Price }
    { 200 ;   ;Element             ;Code20        ;TableRelation="Base Element".Code;
                                                   OnValidate=VAR
                                                                ElementFormat@1100525000 : Codeunit 11012022;
                                                              BEGIN
                                                                ElementFormat.FormatElem(Element,2,'');
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
  }
  KEYS
  {
    {    ;Customer No.,Price List,Code,Start Date ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=%1 Unit Prices succesfully copied.;NOR=%1 Enhetspris er kopiert.;SVE=%1 enhetspris har kopierats.';

    PROCEDURE CopyUnitPrices@1100485000(iFromCust@1100485000 : Code[20];iFromPriceList@1100485001 : Code[10];iFromUnitPrice@1100485002 : Code[250];iFromStartDate@1100485012 : Date;iFromUnitPriceIsFilter@1100485003 : Boolean;iToCust@1100485006 : Code[20];iToPriceList@1100485005 : Code[10];iToUnitPrice@1100485004 : Code[20];iToStartDate@1100485013 : Date;iToRefDate@1100485014 : Date;iUpdateType@1100485015 : 'FromPerc,FromValue,FromPrice');
    VAR
      lvUnitPriceRec@1100485007 : Record 11020420;
      lvUnitPriceLineRec@1100485008 : Record 11020421;
      lvNewUnitPriceRec@1100485010 : Record 11020420;
      lvNewUnitPriceLineRec@1100485009 : Record 11020421;
      lvCount@1100485011 : Integer;
    BEGIN
      IF NOT iFromUnitPriceIsFilter THEN
        lvUnitPriceRec.GET(iFromCust, iFromPriceList, iFromUnitPrice, iFromStartDate); //check whether exists
      lvUnitPriceRec.SETRANGE("Customer No.", iFromCust);
      lvUnitPriceRec.SETRANGE("Price List", iFromPriceList);
      lvUnitPriceRec.SETFILTER(Code, iFromUnitPrice);
      lvUnitPriceRec.SETRANGE("Start Date", iFromStartDate);
      IF lvUnitPriceRec.FINDSET(FALSE, FALSE) THEN BEGIN
        REPEAT
          //Copy the unit price
          lvNewUnitPriceRec.COPY(lvUnitPriceRec);
          lvNewUnitPriceRec."Customer No." := iToCust;
          lvNewUnitPriceRec.VALIDATE("Price List", iToPriceList); //Update the reference date a.o.
          lvNewUnitPriceRec."Start Date" := iToStartDate;
          lvNewUnitPriceRec."Reference Date" := iToRefDate;
          IF NOT iFromUnitPriceIsFilter AND (iToUnitPrice <> '') THEN
            lvNewUnitPriceRec.Code := iToUnitPrice
          ELSE
            lvNewUnitPriceRec.Code := lvUnitPriceRec.Code;
          lvNewUnitPriceRec.INSERT(TRUE);
          lvCount += 1;

          //copy the unit price lines
          lvUnitPriceLineRec.SETRANGE("Customer No.", iFromCust);
          lvUnitPriceLineRec.SETRANGE("Price List", iFromPriceList);
          lvUnitPriceLineRec.SETRANGE("Unit Price", lvUnitPriceRec.Code);
          lvUnitPriceLineRec.SETRANGE("Start Date", iFromStartDate);
          IF lvUnitPriceLineRec.FINDSET(FALSE, FALSE) THEN BEGIN
            REPEAT
              lvNewUnitPriceLineRec.COPY(lvUnitPriceLineRec);
              lvNewUnitPriceLineRec."Customer No." := iToCust;
              lvNewUnitPriceLineRec.VALIDATE("Price List", iToPriceList);
              lvNewUnitPriceLineRec."Unit Price" := lvNewUnitPriceRec.Code;
              lvNewUnitPriceLineRec."Start Date" := iToStartDate;
              lvNewUnitPriceLineRec.UpdateSalesRates(); //update according to new reference date
              lvNewUnitPriceLineRec.INSERT;
            UNTIL lvUnitPriceLineRec.NEXT = 0;
          END;
          CASE iUpdateType OF
            iUpdateType::FromPerc:lvNewUnitPriceRec.VALIDATE("Margin %");
            iUpdateType::FromValue:lvNewUnitPriceRec.VALIDATE(Margin);
            iUpdateType::FromPrice:lvNewUnitPriceRec.VALIDATE(Price);
          END;
          lvNewUnitPriceRec.MODIFY;
        UNTIL lvUnitPriceRec.NEXT = 0;
      END;
      MESSAGE(Text001, lvCount);
    END;

    PROCEDURE UpdateSalesRates@1100485001();
    VAR
      lvUnitPriceLineRec@1100485000 : Record 11020421;
    BEGIN
      //Must be executed when the reference date has changed
      lvUnitPriceLineRec.SETRANGE("Customer No.", "Customer No.");
      lvUnitPriceLineRec.SETRANGE("Price List", "Price List");
      lvUnitPriceLineRec.SETRANGE("Unit Price", Code);
      lvUnitPriceLineRec.SETRANGE("Start Date", "Start Date");
      IF lvUnitPriceLineRec.FINDSET(TRUE, FALSE) THEN BEGIN
        REPEAT
          lvUnitPriceLineRec.UpdateSalesRates;
          lvUnitPriceLineRec.MODIFY;
        UNTIL lvUnitPriceLineRec.NEXT = 0;
      END;
    END;

    PROCEDURE UpdateSalesPrice@1100485002();
    BEGIN
      CASE "Update Price" OF
        "Update Price"::Amount: VALIDATE(Margin);
        "Update Price"::Percentage: VALIDATE("Margin %");
        "Update Price"::"Sales Price": VALIDATE(Price);
      END;
    END;

    BEGIN
    END.
  }
}

