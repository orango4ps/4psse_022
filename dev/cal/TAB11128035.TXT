OBJECT Table 11128035 Svefaktura Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Initalize;
             END;

    OnModify=BEGIN
               Initalize;
             END;

  }
  FIELDS
  {
    { 1   ;   ;key                 ;Code2         ;CaptionML=SVE=Nyckel }
    { 5   ;   ;Svefaktura version  ;Option        ;InitValue=Svefaktura 1;
                                                   OptionString=Svefaktura 1,Svefaktura 3 }
    { 6   ;   ;Export object type  ;Option        ;OptionString=Codeunit,Report }
    { 7   ;   ;Object Invoice export;Integer      ;TableRelation=IF (Export object type=CONST(Codeunit)) Object.ID WHERE (Type=CONST(Codeunit))
                                                                 ELSE IF (Export object type=CONST(Report)) Object.ID WHERE (Type=CONST(Report)) }
    { 8   ;   ;Object Credit export;Integer       ;TableRelation=IF (Export object type=CONST(Codeunit)) Object.ID WHERE (Type=CONST(Codeunit))
                                                                 ELSE IF (Export object type=CONST(Report)) Object.ID WHERE (Type=CONST(Report)) }
    { 100 ;No ;headers             ;Code2          }
    { 110 ;   ;HeadNote TransportMethod;Boolean   ;CaptionML=ENU=TransportMethod }
    { 111 ;   ;HeadNote ServiceOrder;Boolean      ;CaptionML=ENU=ServiceOrder }
    { 1000;No ;lines               ;Code2         ;CaptionML=SVE=Rader }
    { 1010;   ;LineNote TransportMethod;Boolean   ;CaptionML=ENU=TransportMethod }
    { 1011;   ;LineNote ServiceOrder;Boolean      ;CaptionML=ENU=ServiceOrder }
    { 1012;   ;LineNote ShipmentDate;Boolean      ;CaptionML=ENU=ShipmentDate }
    { 1013;   ;LineNote Job        ;Boolean       ;CaptionML=[ENU=Job;
                                                              SVE=Projekt] }
  }
  KEYS
  {
    {    ;key                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE Initalize@111280009();
    BEGIN
      CASE "Svefaktura version" OF
        "Svefaktura version"::"Svefaktura 1" :BEGIN
            IF "Object Invoice export"=0 THEN BEGIN
              "Export object type"  := "Export object type"::Codeunit;
              "Object Invoice export"  := 11128033;
            END;
            IF "Object Credit export"=0 THEN BEGIN
              "Export object type"  := "Export object type"::Codeunit;
              "Object Credit export"  := 11128034;
            END;
          END;
        "Svefaktura version"::"Svefaktura 3" :BEGIN
            IF "Object Invoice export"=0 THEN BEGIN
              "Export object type"  := "Export object type"::Codeunit;
              "Object Invoice export"  := 11128035;
            END;
            IF "Object Credit export"=0 THEN BEGIN
              "Export object type"  := "Export object type"::Codeunit;
              "Object Credit export"  := 11128036;
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE GetInvoiceExportID@111280000(VAR ObjType@111280000 : 'Codeunit,Report';VAR ObjID@111280001 : Integer);
    VAR
      SalesReceivablesSetup@111280002 : Record 311;
    BEGIN
      SalesReceivablesSetup.GET;
      IF NOT GET THEN INIT;
      IF "Object Invoice export" = 0 THEN
        Initalize;
      IF SalesReceivablesSetup."Svefaktura Invoice Report No." <> 0 THEN BEGIN
        ObjType:=ObjType::Report;
        ObjID:=SalesReceivablesSetup."Svefaktura Invoice Report No.";
        EXIT;
      END;
      ObjType:="Export object type";
      ObjID:="Object Invoice export";
    END;

    LOCAL PROCEDURE GetCreditExportID@111280001(VAR ObjType@111280001 : 'Codeunit,Report';VAR ObjID@111280000 : Integer);
    VAR
      SalesReceivablesSetup@111280002 : Record 311;
    BEGIN
      SalesReceivablesSetup.GET;
      IF NOT GET THEN INIT;
      IF "Object Credit export" = 0 THEN
        Initalize;
      IF SalesReceivablesSetup."Svefaktura Cr.Memo Report No." <> 0 THEN BEGIN
        ObjType:=ObjType::Report;
        ObjID:=SalesReceivablesSetup."Svefaktura Cr.Memo Report No.";
        EXIT;
      END;
      ObjType:="Export object type";
      ObjID:="Object Credit export";
    END;

    PROCEDURE ExportInvoice@111280005(SalesInvoiceHeader@111280000 : Record 112);
    VAR
      ObjType@111280002 : 'Codeunit,Report';
      ObjID@111280001 : Integer;
    BEGIN
      GetInvoiceExportID(ObjType,ObjID);
      CASE ObjType OF
        ObjType::Report: REPORT.RUNMODAL(ObjID,GUIALLOWED,FALSE,SalesInvoiceHeader);
        ObjType::Codeunit: CODEUNIT.RUN(ObjID,SalesInvoiceHeader);
      END;
    END;

    PROCEDURE ExportCreditMemo@111280006(SalesCrMemoHeader@111280000 : Record 114);
    VAR
      ObjType@111280002 : 'Codeunit,Report';
      ObjID@111280001 : Integer;
    BEGIN
      GetCreditExportID(ObjType,ObjID);
      CASE ObjType OF
        ObjType::Report: REPORT.RUNMODAL(ObjID,GUIALLOWED,FALSE,SalesCrMemoHeader);
        ObjType::Codeunit: CODEUNIT.RUN(ObjID,SalesCrMemoHeader);
      END;
    END;

    BEGIN
    {
      190904 ORANGO.DL support for Svefatura 3 (PEPPOL Biz 3.0) with code from NAV2018
    }
    END.
  }
}

