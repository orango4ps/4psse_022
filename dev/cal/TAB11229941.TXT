OBJECT Table 11229941 DPT Default Subsite
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               SetQuoteRequestSubsite;
             END;

    OnDelete=VAR
               DocumentSetupPeripheralApp@1100528302 : Record 11072451;
               DPTDefaultSubsiteDocType@1100528300 : Record 11229942;
               DPTHeaderText@1100528301 : Record 11229943;
             BEGIN
               DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", Code);
               DPTDefaultSubsiteDocType.DELETEALL(TRUE);

               DPTHeaderText.SETRANGE("Table No.", DATABASE::"DPT Default Subsite");
               DPTHeaderText.SETRANGE("Field No.", FIELDNO(Code));
               DPTHeaderText.SETRANGE("Source No.", '');
               DPTHeaderText.SETRANGE("Subsite Code", Code);
               DPTHeaderText.DELETEALL;

               DocumentSetupPeripheralApp.SETRANGE("Document Portal Subsite", Code);
               DocumentSetupPeripheralApp.DELETEALL(TRUE);
             END;

    CaptionML=ENU=DPT Default Subsite;
  }
  FIELDS
  {
    { 10  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=" ,Project,Service Location";
                                                                    SVE=,Project,Serviceplats];
                                                   OptionString=[ ,Project,ServiceLocation];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                DPTInterface@1100527800 : Codeunit 11012451;
                                                              BEGIN
                                                                DPTInterface.TestSubsiteCode(Code);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Subsite Structure   ;Option        ;OnValidate=BEGIN
                                                                ProcessSecurityTypeChange;
                                                              END;

                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType }
    { 50  ;   ;Quote Request Subsite;Boolean      ;CaptionML=ENU=Quote Request Subsite;
                                                   Editable=No }
    { 60  ;   ;Template            ;Text50        ;TableRelation=IF (Template Type=CONST(Redirect)) "DPT Default Subsite Doc Type"."Document Type" WHERE (Source Type=FIELD(Source Type),
                                                                                                                                                          Subsite Code=FIELD(Code));
                                                   CaptionML=[ENU=Template;
                                                              NOR=Mal;
                                                              SVE=Mall] }
    { 70  ;   ;Template Type       ;Option        ;OnValidate=BEGIN
                                                                CheckTemplateType;
                                                                SetQuoteRequestSubsite;
                                                              END;

                                                   CaptionML=[ENU=Template Type;
                                                              NOR=Maltype;
                                                              SVE=Malltyp];
                                                   OptionCaptionML=ENU=Default,Page,Redirect,Purchase Action;
                                                   OptionString=Default,Page,Redirect,PurchAction }
    { 90  ;   ;Use Document CheckIn/-Out;Boolean  ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                SetUseDocumentVersionDocType;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out }
    { 100 ;   ;Use Doc CheckIn/-Out Comp List;Boolean;
                                                   InitValue=Yes;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out in Company Lists }
  }
  KEYS
  {
    {    ;Source Type,Code                        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OnlyAllowedOnceTxt@1100528300 : TextConst 'ENU=%1 %2 is only allowed once.';

    PROCEDURE ProcessSecurityTypeChange@1100528300();
    VAR
      DPTDefRespPersPermProject@1100528300 : Record 11072333;
      DPTDefAuthorityPermProject@1100528301 : Record 11072335;
    BEGIN
      IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) THEN BEGIN
        DPTDefRespPersPermProject.SETRANGE(Type, DPTDefRespPersPermProject.Type::Access);
        DPTDefRespPersPermProject.SETRANGE("Subsite Code", Code);
        DPTDefRespPersPermProject.SETFILTER("Document Type", '<>%1', '');
        DPTDefRespPersPermProject.DELETEALL;

        DPTDefAuthorityPermProject.SETRANGE(Type, DPTDefRespPersPermProject.Type::Access);
        DPTDefAuthorityPermProject.SETRANGE("Subsite Code", Code);
        DPTDefAuthorityPermProject.SETFILTER("Document Type", '<>%1', '');
        DPTDefAuthorityPermProject.DELETEALL;
      END ELSE BEGIN
        DPTDefRespPersPermProject.AddPortalPermissions(Code);
        DPTDefAuthorityPermProject.AddPortalPermissions(Code);
      END;
    END;

    PROCEDURE CreateQuoteRequestSubsite@1100528303();
    VAR
      DPTDefaultSubsite@1100528300 : Record 11229941;
    BEGIN
      DPTDefaultSubsite.SETRANGE("Source Type", DPTDefaultSubsite."Source Type"::Project);
      DPTDefaultSubsite.SETRANGE("Quote Request Subsite", TRUE);
      IF (NOT DPTDefaultSubsite.ISEMPTY) THEN
        EXIT;

      DPTDefaultSubsite.INIT;
      DPTDefaultSubsite."Source Type" := DPTDefaultSubsite."Source Type"::Project;
      DPTDefaultSubsite.Code := 'Quote';
      DPTDefaultSubsite.Description := DPTDefaultSubsite.FIELDCAPTION("Quote Request Subsite");
      DPTDefaultSubsite."Quote Request Subsite" := TRUE;
      DPTDefaultSubsite."Template Type" := DPTDefaultSubsite."Template Type"::PurchAction;
      DPTDefaultSubsite.INSERT;
    END;

    LOCAL PROCEDURE "---Locals---"@1100528301();
    BEGIN
    END;

    LOCAL PROCEDURE CheckTemplateType@1100528307();
    VAR
      DPTDefaultSubsite@1100528300 : Record 11229941;
    BEGIN
      IF ("Template Type" = "Template Type"::PurchAction) THEN BEGIN
        DPTDefaultSubsite.SETRANGE("Source Type", "Source Type"::Project);
        DPTDefaultSubsite.SETRANGE("Template Type", "Template Type"::PurchAction);
        IF (NOT DPTDefaultSubsite.ISEMPTY) THEN
          ERROR(OnlyAllowedOnceTxt, FIELDCAPTION("Template Type"), "Template Type"::PurchAction);
      END;
    END;

    LOCAL PROCEDURE SetQuoteRequestSubsite@1100528306();
    BEGIN
      "Quote Request Subsite" := FALSE;
      IF ("Template Type" = "Template Type"::PurchAction) THEN BEGIN
        "Quote Request Subsite" := TRUE;
        "Subsite Structure" := "Subsite Structure"::OneDocLibrary;
        "Template Type" := "Template Type"::PurchAction;
        Template := '';
        "Use Doc CheckIn/-Out Comp List" := FALSE;
        "Use Document CheckIn/-Out" := FALSE;
      END;
    END;

    LOCAL PROCEDURE SetUseDocumentVersionDocType@1100528302();
    VAR
      DPTDefaultSubsiteDocType@1100528300 : Record 11229942;
    BEGIN
      DPTDefaultSubsiteDocType.SETRANGE("Source Type", "Source Type");
      DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", Code);
      DPTDefaultSubsiteDocType.MODIFYALL("Use Document CheckIn/-Out", "Use Document CheckIn/-Out", TRUE);
    END;

    BEGIN
    END.
  }
}

