OBJECT Codeunit 5636 FA. Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=5621;
    EventSubscriberInstance=Manual;
    OnRun=BEGIN
            FAJnlLine.COPY(Rec);
            Code;
            COPY(FAJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;NOR=kan ikke filtreres n†r du bokf›rer gjentakelseskladder;SVE=kan inte filtreras n„r †terkommande journaler bokf”rs';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?;NOR=Vil du bokf›re kladdelinjene?;SVE=Vill du bokf”ra journalraderna?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;NOR=Kladdelinjene ble bokf›rt uten problemer.;SVE=Journalraderna „r bokf”rda.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;NOR=Kladdelinjene ble bokf›rt p† riktig m†te. Du er n† i kladd %1.;SVE=Journalraderna „r bokf”rda. Du „r nu i journal %1.';
      FAJnlTemplate@1005 : Record 5619;
      FAJnlLine@1006 : Record 5621;
      FAJnlPostBatch@1007 : Codeunit 5633;
      TempJnlBatchName@1008 : Code[10];
      PreviewMode@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    VAR
      GenJournalTemplatePerRole@1100525000 : Record 11020419;
    BEGIN
      WITH FAJnlLine DO BEGIN
        GenJournalTemplatePerRole.CheckPermission(TABLECAPTION, "Journal Template Name",TRUE); //**4PS.n

        FAJnlTemplate.GET("Journal Template Name");
        FAJnlTemplate.TESTFIELD("Force Posting Report",FALSE);
        IF FAJnlTemplate.Recurring AND (GETFILTER("FA Posting Date") <> '') THEN
          FIELDERROR("FA Posting Date",Text000);

        IF NOT PreviewMode THEN
          IF NOT CONFIRM(Text001,FALSE) THEN
            EXIT;

        TempJnlBatchName := "Journal Batch Name";

        FAJnlPostBatch.SetPreviewMode(PreviewMode);
        FAJnlPostBatch.RUN(FAJnlLine);

        IF NOT PreviewMode THEN BEGIN
          IF "Line No." = 0 THEN
            MESSAGE(Text002)
          ELSE
            IF TempJnlBatchName = "Journal Batch Name" THEN
              MESSAGE(Text003)
            ELSE
              MESSAGE(
                Text004,
                "Journal Batch Name");

          IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
            RESET;
            FILTERGROUP := 2;
            SETRANGE("Journal Template Name","Journal Template Name");
            SETRANGE("Journal Batch Name","Journal Batch Name");
            FILTERGROUP := 0;
            "Line No." := 1;
          END;
        END;
      END;
    END;

    [External]
    PROCEDURE Preview@2(VAR FAJournalLine@1000 : Record 5621);
    VAR
      GenJnlPostPreview@1001 : Codeunit 19;
      FAJnlPost@1002 : Codeunit 5636;
    BEGIN
      BINDSUBSCRIPTION(FAJnlPost);
      GenJnlPostPreview.Preview(FAJnlPost,FAJournalLine);
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@3(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      FAJnlPost@1003 : Codeunit 5636;
    BEGIN
      FAJnlPost := Subscriber;
      PreviewMode := TRUE;
      Result := FAJnlPost.RUN(RecVar);
    END;

    BEGIN
    {
      4PS MVOS 20100810: Check permission Journal Template before posting.
    }
    END.
  }
}

