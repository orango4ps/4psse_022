OBJECT Codeunit 241 Item Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=83;
    OnRun=BEGIN
            //**4PS.sn
            MARKEDONLY(TRUE);  //db, 17-08-07 (melding 6999)
            IF NOT FINDFIRST THEN
              MARKEDONLY(FALSE);
            //**4PS.en
            ItemJnlLine.COPY(Rec);
            Code;
            COPY(ItemJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;NOR=kan ikke filtreres n†r du bokf›rer gjentakelseskladder;SVE=kan inte filtreras n„r †terkommande journaler bokf”rs';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?;NOR=Vil du bokf›re kladdelinjene?;SVE=Vill du bokf”ra journalraderna?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;NOR=Kladdelinjene ble bokf›rt uten problemer.;SVE=Journalraderna „r bokf”rda.';
      Text004@1004 : TextConst 'ENU="The journal lines were successfully posted. ";NOR="Kladdelinjene ble bokf›rt p† riktig m†te. ";SVE="Det gick bra att bokf”ra raderna. "';
      Text005@1005 : TextConst 'ENU=You are now in the %1 journal.;NOR=Du er n† i kladd %1.;SVE=Du „r nu i journalen %1.';
      ItemJnlTemplate@1006 : Record 82;
      ItemJnlLine@1007 : Record 83;
      TempJnlBatchName@1009 : Code[10];

    LOCAL PROCEDURE Code@1();
    VAR
      ItemJnlPostBatch@1003 : Codeunit 23;
      HideDialog@1000 : Boolean;
      SuppressCommit@1002 : Boolean;
      IsHandled@1001 : Boolean;
    BEGIN
      HideDialog := FALSE;
      SuppressCommit := FALSE;
      IsHandled := FALSE;
      OnBeforeCode(ItemJnlLine,HideDialog,SuppressCommit,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH ItemJnlLine DO BEGIN
        ItemJnlTemplate.GET("Journal Template Name");
        ItemJnlTemplate.TESTFIELD("Force Posting Report",FALSE);
        IF ItemJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT HideDialog THEN
          IF NOT CONFIRM(Text001,FALSE) THEN
            EXIT;

        TempJnlBatchName := "Journal Batch Name";

        ItemJnlPostBatch.SetSuppressCommit(SuppressCommit);
        ItemJnlPostBatch.RUN(ItemJnlLine);

        IF NOT HideDialog THEN
          IF "Line No." = 0 THEN
            MESSAGE(Text002)
          ELSE
            IF TempJnlBatchName = "Journal Batch Name" THEN
              MESSAGE(Text003)
            ELSE
              MESSAGE(
                Text004 +
                Text005,
                "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCode@2(VAR ItemJournalLine@1000 : Record 83;VAR HideDialog@1001 : Boolean;VAR SuppressCommit@1003 : Boolean;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

