OBJECT Codeunit 5942 ServContractQuote-Tmpl. Upd.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    TableNo=5965;
    OnRun=BEGIN
            ServiceContractTemplate.RESET;
            IF NOT ServiceContractTemplate.FINDFIRST THEN
              EXIT;

            TESTFIELD("Contract No.");

            IF PAGE.RUNMODAL(PAGE::"Service Contract Template List",ServiceContractTemplate) = ACTION::LookupOK THEN
              ApplyTemplate(Rec,ServiceContractTemplate);
          END;

  }
  CODE
  {
    VAR
      ServiceContractTemplate@1001 : Record 5968;

    [External]
    PROCEDURE ApplyTemplate@1(VAR ServiceContractHeader@1000 : Record 5965;ServiceContractTemplate@1001 : Record 5968);
    VAR
      ContractServiceDiscount@1003 : Record 5972;
      TemplateContractServiceDiscount@1002 : Record 5972;
    BEGIN
      OnBeforeApplyTemplate(ServiceContractHeader,ServiceContractTemplate);
      WITH ServiceContractHeader DO BEGIN
        Description := ServiceContractTemplate.Description;
        VALIDATE("Contract Group Code",ServiceContractTemplate."Contract Group Code");
        VALIDATE("Service Order Type",ServiceContractTemplate."Service Order Type");
        VALIDATE("Service Period",ServiceContractTemplate."Default Service Period");
        VALIDATE("Price Update Period",ServiceContractTemplate."Price Update Period");
        VALIDATE("Response Time (Hours)",ServiceContractTemplate."Default Response Time (Hours)");
        VALIDATE("Max. Labor Unit Price",ServiceContractTemplate."Max. Labor Unit Price");
        VALIDATE("Invoice after Service",ServiceContractTemplate."Invoice after Service");
        VALIDATE("Invoice Period",ServiceContractTemplate."Invoice Period");
        VALIDATE("Price Inv. Increase Code",ServiceContractTemplate."Price Inv. Increase Code");
        VALIDATE("Allow Unbalanced Amounts",ServiceContractTemplate."Allow Unbalanced Amounts");
        VALIDATE("Contract Lines on Invoice",ServiceContractTemplate."Contract Lines on Invoice");
        VALIDATE("Combine Invoices",ServiceContractTemplate."Combine Invoices");
        VALIDATE("Automatic Credit Memos",ServiceContractTemplate."Automatic Credit Memos");
        VALIDATE(Prepaid,ServiceContractTemplate.Prepaid);
        VALIDATE("Serv. Contract Acc. Gr. Code",ServiceContractTemplate."Serv. Contract Acc. Gr. Code");
        "Template No." := ServiceContractTemplate."No.";

        CreateDim(
          DATABASE::"Service Contract Template",ServiceContractTemplate."No.",
          0,'',0,'',0,'',0,'');
        CreateDim(
          DATABASE::"Service Contract Template","Template No.",
          DATABASE::Customer,"Bill-to Customer No.",
          DATABASE::"Salesperson/Purchaser","Salesperson Code",
          DATABASE::"Responsibility Center","Responsibility Center",
          DATABASE::"Service Order Type","Service Order Type");

        ContractServiceDiscount.RESET;
        ContractServiceDiscount.SETRANGE("Contract Type","Contract Type");
        ContractServiceDiscount.SETRANGE("Contract No.","Contract No.");
        ContractServiceDiscount.DELETEALL;

        TemplateContractServiceDiscount.RESET;
        TemplateContractServiceDiscount.SETRANGE("Contract Type",TemplateContractServiceDiscount."Contract Type"::Template);
        TemplateContractServiceDiscount.SETRANGE("Contract No.",ServiceContractTemplate."No.");
        IF TemplateContractServiceDiscount.FIND('-') THEN
          REPEAT
            ContractServiceDiscount := TemplateContractServiceDiscount;
            ContractServiceDiscount."Contract Type" := "Contract Type";
            ContractServiceDiscount."Contract No." := "Contract No.";
            ContractServiceDiscount.INSERT;
          UNTIL TemplateContractServiceDiscount.NEXT = 0;
      END;
      OnAfterApplyTemplate(ServiceContractHeader,ServiceContractTemplate);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterApplyTemplate@2(VAR ServiceContractHeader@1000 : Record 5965;ServiceContractTemplate@1001 : Record 5968);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeApplyTemplate@4(VAR ServiceContractHeader@1000 : Record 5965;ServiceContractTemplate@1001 : Record 5968);
    BEGIN
    END;

    BEGIN
    END.
  }
}

