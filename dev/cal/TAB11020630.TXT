OBJECT Table 11020630 Project Forecast Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Project@1100529400 : Record 11072003;
             BEGIN
               //**4PS.sn BI042 KD 19-08-16
               Project.GET("Project No.");
               "Forecast by Currency" := Project."Project Forecast by Currency";
               //**4PS.en BI042 KD 19-08-16
             END;

    OnDelete=VAR
               ProjectForecastLine@1100525000 : Record 11020631;
               ProjectForecastTotalLine@1100525001 : Record 11020633;
               ProjectForecastinPeriod@1100409000 : Record 11020632;
             BEGIN
               ProjectForecastLine.SETRANGE("Project No.", "Project No.");
               ProjectForecastLine.SETRANGE("Forecast Date", "Forecast Date");
               //ProjectForecastLine.DELETEALL(TRUE);  //C003395.o
               ProjectForecastLine.DELETEALL;  //C003395.n Runtrigger FALSE to prevent LogUpdate

               ProjectForecastTotalLine.SETRANGE("Project No.", "Project No.");
               ProjectForecastTotalLine.SETRANGE("Forecast Date", "Forecast Date");
               //ProjectForecastTotalLine.DELETEALL(TRUE); //C003395.o
               ProjectForecastTotalLine.DELETEALL;  //C003395.n

               //C003395.sn
               ProjectForecastinPeriod.SETRANGE("Project No.", "Project No.");
               ProjectForecastinPeriod.SETRANGE("Forecast Date", "Forecast Date");
               ProjectForecastinPeriod.DELETEALL;
               //C003395.en
             END;

    OnRename=BEGIN
               ERROR(Text002);
             END;

    CaptionML=[ENU=Project Forecast Header;
               NOR=Prosjektprognosetittel;
               SVE=Projektprognosrubrik];
    LookupPageID=Page11124951;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   OnValidate=VAR
                                                                Project@1100525000 : Record 11072003;
                                                              BEGIN
                                                                Project.GET("Project No.");
                                                                //Project.TESTFIELD("Settlement Method", Project."Settlement Method"::"Fixed Price"); //call 27464
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Forecast Date       ;Date          ;CaptionML=[ENU=Forecast Date;
                                                              NOR=Prognosedato;
                                                              SVE=Prognosdatum];
                                                   NotBlank=Yes }
    { 30  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 40  ;   ;Main Project No.    ;Code20        ;CaptionML=[ENU=Main Project No.;
                                                              NOR=Hovedprosjektnr.;
                                                              SVE=Huvudprojektnr];
                                                   Editable=No }
    { 50  ;   ;Department Code     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Avdelingskode;
                                                              SVE=Mottagande avdelningskod];
                                                   Editable=No;
                                                   CaptionClass='1,1,1' }
    { 60  ;   ;Fixed               ;Boolean       ;CaptionML=[ENU=Fixed;
                                                              NOR=Fast;
                                                              SVE=Fast] }
    { 100 ;   ;Period Type Filter  ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Period Type;
                                                              NOR=Periodetype;
                                                              SVE=Periodtyp];
                                                   OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                                                    NOR=Dag,Uke,MÜned,Kvartal,èr,bokfõringsperiode;
                                                                    SVE=Dag,Vecka,MÜnad,Kvartal,èr,Bokfîringsperiod];
                                                   OptionString=Day,Week,Month,Quarter,Year,Accounting Period }
    { 101 ;   ;Cost Type Filter    ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Cost Type Filter;
                                                              NOR=Kostnadstype, filter;
                                                              SVE=Filter fîr kostnadstyp];
                                                   OptionCaptionML=[ENU=Labor,Material,Subcontracting,Plant,Sundry,Revenue;
                                                                    NOR=Arbeidskraft,Materiale,Underentrepenõr,Maskin,Diverse,Inntekt;
                                                                    SVE=Arbetskraft,Material,Underentreprenad,Maskin,Diverse,IntÑkt];
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry,Revenue }
    { 110 ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=éndrad av];
                                                   Editable=No }
    { 120 ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Oppdateringsdato;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 250 ;   ;Ovh. Surcharges Calculated;Boolean ;CaptionML=[ENU=Ovh. Surcharges Calculated;
                                                              NOR=Overheadkoster beregnet;
                                                              SVE=Ovh. TillÑggsavgifter har berÑknats];
                                                   Editable=No }
    { 251 ;   ;Altered After Calculation;Boolean  ;CaptionML=[ENU=Forecast Altered After Calculation Overhead Surcharge;
                                                              NOR=Prognose endret etter beregning av overheadkost];
                                                   Editable=No }
    { 260 ;   ;Forecast by Currency;Boolean       ;CaptionML=ENU=Forecast by Currency;
                                                   Description=BI042 }
    { 270 ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Description=BI042 }
  }
  KEYS
  {
    {    ;Project No.,Forecast Date               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text002@1100525000 : TextConst 'ENU=Renaming is not allowed.;NOR=Ikke tillatt Ü gi nytt navn.;SVE=Att ge nytt namn Ñr inte tillÜtet.';

    PROCEDURE UpdateLog@1100525001(ProjectNo@1100525000 : Code[20];ForecastDate@1100525001 : Date);
    VAR
      ProjectForecastHeader@1100525002 : Record 11020630;
    BEGIN
      IF ProjectForecastHeader.GET(ProjectNo, ForecastDate) THEN BEGIN
        IF (ProjectForecastHeader."Last Date Modified" <> TODAY) OR
           (ProjectForecastHeader."Modified by" <> UPPERCASE(USERID)) THEN BEGIN
          ProjectForecastHeader."Last Date Modified" := TODAY;
          ProjectForecastHeader."Modified by" := USERID;
          ProjectForecastHeader.MODIFY;
        END;
      END;
    END;

    PROCEDURE GetPeriodStartDate@1100528700(Date@1100528703 : Date;PeriodType@1100528700 : Option) : Date;
    VAR
      Period@1100528702 : Record 2000000007;
      PeriodFormManagement@1100528701 : Codeunit 359;
    BEGIN
      Period."Period Type" := PeriodType;
      Period."Period Start" := Date;
      Period."Period End" := 0D;
      PeriodFormManagement.FindDate('=<', Period, PeriodType);
      EXIT(Period."Period Start");
    END;

    PROCEDURE GetDateFilterMatrix@1100525000(ForecastDate@1100525000 : Date;PeriodType@1100525001 : 'Day,Week,Month,Quarter,Year,Accounting Period') : Text;
    VAR
      CalendarPeriod@1100525002 : Record 2000000007;
      AccountingPeriod@1100525003 : Record 50;
      DateStart@1100525004 : Date;
      DateEnd@1100525005 : Date;
    BEGIN
      IF ForecastDate = 0D THEN
        ForecastDate := TODAY;
      DateStart := GetPeriodStartDate(ForecastDate, PeriodType);
      IF PeriodType <> PeriodType::"Accounting Period" THEN BEGIN
        CalendarPeriod.SETRANGE("Period Type", PeriodType);
        CalendarPeriod.FINDLAST;
        DateEnd := NORMALDATE(CalendarPeriod."Period End");
      END ELSE BEGIN
        IF AccountingPeriod.FINDLAST THEN
          DateEnd := AccountingPeriod."Starting Date" - 1;
      END;
      IF DateEnd >= DateStart THEN
        EXIT(STRSUBSTNO('%1..%2', DateStart, DateEnd));
    END;

    PROCEDURE CalcTotalAmountsLCY@1100529400(CostType@1100529400 : 'Labor,Material,Subcontracting,Plant,Sundry,Revenue,Total Cost') AmountLoc : Decimal;
    VAR
      ProjectForecastTotalLine@1100529401 : Record 11020633;
      Currency@1100529402 : Record 4;
      ProjectCurrExchRate@1100529403 : Record 11020628;
    BEGIN
      //**4PS.sn BI042 KD 26-08-16
      ProjectForecastTotalLine.SETRANGE("Project No.", "Project No.");
      ProjectForecastTotalLine.SETRANGE("Forecast Date", "Forecast Date");
      ProjectForecastTotalLine.SETRANGE("Cost Type", CostType);
      ProjectForecastTotalLine.SETRANGE("Currency Code", '');
      IF ProjectForecastTotalLine.FINDFIRST THEN BEGIN
        ProjectForecastTotalLine.CALCFIELDS(Amount);
        AmountLoc := ProjectForecastTotalLine.Amount;
      END;

      IF NOT "Forecast by Currency" THEN
        EXIT;

      Currency.FINDSET;
      REPEAT
        ProjectForecastTotalLine.SETRANGE("Currency Code", Currency.Code);
        IF ProjectForecastTotalLine.FINDFIRST THEN BEGIN
          ProjectForecastTotalLine.CALCFIELDS(Amount);
          IF ProjectForecastTotalLine.Amount <> 0 THEN
            AmountLoc += ProjectCurrExchRate.ExchangeAmtFCYToLCY(
              ProjectForecastTotalLine.Amount,FALSE,FALSE,
              ProjectForecastTotalLine."Currency Code",ProjectForecastTotalLine."Project No.",
              '',ProjectForecastTotalLine."Forecast Date");
        END;
      UNTIL Currency.NEXT = 0;
      EXIT(AmountLoc);
      //**4PS.en BI042 KD 26-08-16
    END;

    BEGIN
    {
      4PS 19-08-16 UKR-C26352 KD: Foreign currency in the forecast (BI042) - Added fields "Forecast by Currency", Description; trigger OnInsert changed; trigger CalcTotalAmountsLCY added
    }
    END.
  }
}

