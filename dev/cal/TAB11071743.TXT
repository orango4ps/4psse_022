OBJECT Table 11071743 Maintenance Scheme Version
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11071744=imd;
    OnInsert=BEGIN
               TESTFIELD("Index Date");
               IF "Line Type" IN ["Line Type"::"Standard Object", "Line Type"::Object] THEN
                 TESTFIELD("Belongs to Scheme");
               CentralDataManagement.EditMaintenanceSchemeVersion(Rec, 1, xRec);
             END;

    OnModify=BEGIN
               CentralDataManagement.EditMaintenanceSchemeVersion(Rec, 2, xRec);
             END;

    OnDelete=VAR
               MaintSchemeVersionLine@1210190000 : Record 11071744;
               MaintSchemeLineProperty@1100525000 : Record 11071954;
             BEGIN
               MaintSchemeVersionLine.SETRANGE("Line Type", "Line Type");
               MaintSchemeVersionLine.SETRANGE("Type Code", Code);
               MaintSchemeVersionLine.SETRANGE("Belongs to Scheme", "Belongs to Scheme");
               MaintSchemeVersionLine.SETRANGE("Index Date", "Index Date");
               MaintSchemeVersionLine.DELETEALL(TRUE);

               MaintSchemeLineProperty.SETRANGE("Line Type", "Line Type");
               MaintSchemeLineProperty.SETRANGE("Type Code", Code);
               MaintSchemeLineProperty.SETRANGE("Belongs to Scheme", "Belongs to Scheme");
               MaintSchemeLineProperty.SETRANGE("Index Date", "Index Date");
               MaintSchemeLineProperty.DELETEALL(TRUE);

               CentralDataManagement.EditMaintenanceSchemeVersion(Rec, 4, xRec);
             END;

    OnRename=VAR
               lvUpdateNormsPricesCU@1100526200 : Codeunit 11012836;
             BEGIN
               IF ("Index Date" <> 0D) AND ("Index Date" <> xRec."Index Date") THEN
                 lvUpdateNormsPricesCU.UpdateActivityLinesByIndexDate("Line Type",Code,"Index Date",xRec."Index Date",'');
             END;

    CaptionML=[ENU=Maintenance Scheme Version;
               NOR=Vedlikeholdsskjema, versjon;
               SVE=Version av underh†llsschema];
    LookupPageID=Page11125249;
    DrillDownPageID=Page11125249;
  }
  FIELDS
  {
    { 5   ;   ;Line Type           ;Option        ;CaptionML=[ENU=Line Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Maintenance Scheme,Modification,Standard Object,Standard Object Deinstallation,Standard Object Installation,Object Installation,Object;
                                                                    NOR=Vedlikeholdsskjema,Endring,Standardobjekt,Avinstallering av standardobjekt,Installering av standardobjekt,Installering av objekt,Objekt;
                                                                    SVE=Underh†llsschema,Žndring,Standardobjekt,Standardobjekt avinstallation,Standardobjekt installation,Objektinstallation,Objekt];
                                                   OptionString=Maintenance Scheme,Modification,Standard Object,Standard Object Deinstallation,Standard Object Installation,Object Installation,Object }
    { 10  ;   ;Code                ;Code20        ;TableRelation=IF (Line Type=CONST(Maintenance Scheme)) "Maintenance Scheme".Code
                                                                 ELSE IF (Line Type=CONST(Modification)) "Object Modification".No.
                                                                 ELSE IF (Line Type=CONST(Standard Object)) "Standard Object".No.
                                                                 ELSE IF (Line Type=CONST(Standard Object Deinstallation)) "Standard Object".No.
                                                                 ELSE IF (Line Type=CONST(Standard Object Installation)) "Standard Object".No.
                                                                 ELSE IF (Line Type=CONST(Object Installation)) Service-Object.No.
                                                                 ELSE IF (Line Type=CONST(Object)) Service-Object.No.;
                                                   OnValidate=BEGIN
                                                                IF Code <> xRec.Code THEN BEGIN
                                                                  MainRec := xRec;
                                                                  IF MainRec.FIND THEN
                                                                    CentralDataManagement.EditMaintenanceSchemeVersion(Rec, 3, xRec);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme".Description WHERE (Code=FIELD(Code)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 30  ;   ;Condition           ;Code10        ;TableRelation=Condition.Code;
                                                   CaptionML=[ENU=Condition;
                                                              NOR=Betingelse;
                                                              SVE=Villkor] }
    { 31  ;   ;Condition Description;Text30       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Condition.Description WHERE (Code=FIELD(Condition)));
                                                   CaptionML=[ENU=Condition Description;
                                                              NOR=Tilstandsbeskrivelse;
                                                              SVE=Villkorsbeskrivning];
                                                   Editable=No }
    { 40  ;   ;Index Date          ;Date          ;OnValidate=BEGIN
                                                                IF "Index Date" <> xRec."Index Date" THEN BEGIN
                                                                  MainRec := xRec;
                                                                  IF MainRec.FIND THEN
                                                                    CentralDataManagement.EditMaintenanceSchemeVersion(Rec, 3, xRec);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum] }
    { 50  ;   ;Maintenance Interval;DateFormula   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme"."Maintenance Interval" WHERE (Code=FIELD(Code)));
                                                   OnValidate=BEGIN
                                                                EvaluateTimeQuantity;
                                                              END;

                                                   CaptionML=[ENU=Maintenance Interval;
                                                              NOR=Vedlikeholdsintervall;
                                                              SVE=Underh†llsintervall];
                                                   Editable=No }
    { 60  ;   ;Frequency per Year  ;Decimal       ;FieldClass=FlowField;
                                                   InitValue=1;
                                                   CalcFormula=Lookup("Maintenance Scheme"."Frequency per Year" WHERE (Code=FIELD(Code)));
                                                   OnValidate=VAR
                                                                SchemeLineRec@1100525000 : Record 11071744;
                                                              BEGIN
                                                                SchemeLineRec.SETRANGE("Line Type", SchemeLineRec."Line Type"::"Maintenance Scheme");
                                                                SchemeLineRec.SETRANGE("Type Code", Code);
                                                                SchemeLineRec.SETRANGE("Index Date", "Index Date");
                                                                UpdMaintSchemeLinesTimeQuant(SchemeLineRec);

                                                                SchemeLineRec.SETRANGE("Line Type",SchemeLineRec."Line Type"::"Standard Object");
                                                                SchemeLineRec.SETRANGE("Belongs to Scheme",Code);
                                                                SchemeLineRec.SETRANGE("Type Code");
                                                                SchemeLineRec.SETRANGE("Index Date","Index Date");
                                                                UpdMaintSchemeLinesTimeQuant(SchemeLineRec);
                                                              END;

                                                   CaptionML=[ENU=Frequency per Year;
                                                              NOR=Frekvens per †r;
                                                              SVE=Frekvens per †r];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 100 ;   ;Object Group        ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme"."Object Group" WHERE (Code=FIELD(Code)));
                                                   CaptionML=[ENU=Object Group;
                                                              NOR=Objektgruppe;
                                                              SVE=Objektgrupp];
                                                   Editable=No }
    { 110 ;   ;Belongs to Scheme   ;Code20        ;TableRelation=IF (Line Type=CONST(Maintenance Scheme)) "Maintenance Scheme"
                                                                 ELSE IF (Line Type=CONST(Standard Object)) "Maintenance Scheme"
                                                                 ELSE IF (Line Type=CONST(Object)) "Maintenance Scheme";
                                                   OnValidate=BEGIN
                                                                IF "Belongs to Scheme" <> xRec."Belongs to Scheme" THEN BEGIN
                                                                  MainRec := xRec;
                                                                  IF MainRec.FIND THEN
                                                                    CentralDataManagement.EditMaintenanceSchemeVersion(Rec, 3, xRec);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Belongs to scheme;
                                                              NOR=H›rer til skjema;
                                                              SVE=Tillh”r schema];
                                                   Editable=No }
    { 120 ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme".Description WHERE (Code=FIELD(Belongs to Scheme)));
                                                   CaptionML=[ENU=Maintenance Scheme Description;
                                                              NOR=Beskrivelse av vedlikeholdskjema;
                                                              SVE=Beskrivning av underh†llsschema];
                                                   Editable=No }
    { 130 ;   ;Expired             ;Boolean       ;OnValidate=VAR
                                                                MaintenanceSchemeObjects@1100528600 : Record 11071745;
                                                              BEGIN
                                                                IF "Line Type" = "Line Type"::"Maintenance Scheme" THEN
                                                                  IF xRec.Expired <> Expired THEN
                                                                    IF NOT Expired THEN
                                                                      IF MaintenanceSchemeObjects.GET(Code) THEN
                                                                        MaintenanceSchemeObjects.TESTFIELD(Expired, FALSE);
                                                              END;

                                                   CaptionML=[ENU=Expired;
                                                              NOR=Utg†tt;
                                                              SVE=Utg†ngna] }
  }
  KEYS
  {
    {    ;Line Type,Code,Belongs to Scheme,Index Date;
                                                   Clustered=Yes }
    {    ;Line Type,Belongs to Scheme,Index Date   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      MainRec@1100528601 : Record 11071743;
      CentralDataManagement@1100528600 : Codeunit 11012010;

    PROCEDURE EvaluateTimeQuantity@1100485000();
    VAR
      HelpTxt1@1100485000 : Text[30];
      HelpTxt2@1100485001 : Text[30];
      HelpDec1@1100485002 : Decimal;
    BEGIN
      //db, 12-10-10
      HelpTxt1 := FORMAT("Maintenance Interval");
      HelpTxt2 := DELCHR(HelpTxt1, '=', '0123456789');

      IF (HelpTxt1 = '') OR (HelpTxt2 = '') THEN EXIT;

      EVALUATE(HelpDec1, COPYSTR(HelpTxt1, 1, STRPOS(HelpTxt1, HelpTxt2)-1));
      IF HelpDec1 = 0 THEN EXIT;

      CASE HelpTxt2 OF
        'D':
          "Frequency per Year" := 365 / HelpDec1;
        'W':
          "Frequency per Year" := 52 / HelpDec1;
        'M':
          "Frequency per Year" := 12 / HelpDec1;
        'K','Q':
          "Frequency per Year" := 4 / HelpDec1;
        'J','Y':
          "Frequency per Year" := 1 / HelpDec1;
      END;

      VALIDATE("Frequency per Year");
    END;

    PROCEDURE UpdMaintSchemeLinesTimeQuant@1100525005(VAR SchemeLineRec@1100525000 : Record 11071744);
    BEGIN
      //db, 12-10-10
      //SchemeLineRec.SETFILTER("Cost Type", '%1|%2', SchemeLineRec."Cost Type"::Labor, SchemeLineRec."Cost Type"::Subcontracting);
      IF SchemeLineRec.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          SchemeLineRec."Maintenance Interval" := "Maintenance Interval";  //db, 15-11-10: M23918
      //    IF SchemeLineRec."Cost Type" IN [SchemeLineRec."Cost Type"::Labor, SchemeLineRec."Cost Type"::Subcontracting] THEN
            SchemeLineRec.VALIDATE("Frequency per Year","Frequency per Year");
          SchemeLineRec.MODIFY;
        UNTIL SchemeLineRec.NEXT = 0;
      END;
    END;

    PROCEDURE NewerVersionExists@1100528600() : Boolean;
    VAR
      MaintenanceHeader@1100528600 : Record 11071743;
    BEGIN
      MaintenanceHeader := Rec;
      MaintenanceHeader.SETRECFILTER;
      MaintenanceHeader.SETFILTER("Index Date", '>%1', "Index Date");
      MaintenanceHeader.SETRANGE(Expired, FALSE);
      EXIT(NOT MaintenanceHeader.ISEMPTY);
    END;

    PROCEDURE GetDescription@1100528601() : Text[100];
    VAR
      ObjectModification@1100528600 : Record 11071700;
      StandardObject@1100528602 : Record 11020334;
      ServiceObject@1100528601 : Record 11071691;
    BEGIN
      CASE "Line Type" OF
        "Line Type"::"Maintenance Scheme":
          BEGIN
            CALCFIELDS(Description);
            EXIT(Description);
          END;
        "Line Type"::Modification:
          BEGIN
            IF ObjectModification.GET(Code) THEN
              EXIT(ObjectModification.Description);
          END;
        "Line Type"::"Standard Object",
        "Line Type"::"Standard Object Deinstallation",
        "Line Type"::"Standard Object Installation":
          BEGIN
            IF StandardObject.GET(Code) THEN
              EXIT(StandardObject.Description);
          END;
        "Line Type"::"Object Installation",
        "Line Type"::Object:
          BEGIN
            IF ServiceObject.GET(Code) THEN
              EXIT(ServiceObject.Description);
          END;
      END;
    END;

    BEGIN
    END.
  }
}

