OBJECT Table 99000779 Production BOM Version
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    DataCaptionFields=Production BOM No.,Version Code,Description;
    OnInsert=BEGIN
               ProdBOMHeader.GET("Production BOM No.");
               IF "Version Code" = '' THEN BEGIN
                 ProdBOMHeader.TESTFIELD("Version Nos.");
                 NoSeriesMgt.InitSeries(ProdBOMHeader."Version Nos.",xRec."No. Series",0D,"Version Code","No. Series");
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               ProdBOMLine@1000 : Record 99000772;
             BEGIN
               ProdBOMLine.SETRANGE("Production BOM No.","Production BOM No.");
               ProdBOMLine.SETRANGE("Version Code","Version Code");
               ProdBOMLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               IF Status = Status::Certified THEN
                 ERROR(Text001,TABLECAPTION,FIELDCAPTION(Status),FORMAT(Status));
             END;

    CaptionML=[DEU=Fert.-StÅcklistenversion;
               ENU=Production BOM Version;
               NLD=Productiestuklijstversie;
               NOR=Produksjonsstykklisteversjon;
               SVE=Prod.struktursversion];
    LookupPageID=Page99000800;
    DrillDownPageID=Page99000800;
  }
  FIELDS
  {
    { 1   ;   ;Production BOM No.  ;Code20        ;TableRelation="Production BOM Header";
                                                   CaptionML=[DEU=Fert.-StÅcklistennr.;
                                                              ENU=Production BOM No.;
                                                              NLD=Prod.-stuklijstnr.;
                                                              NOR=Produksjonsstykklistenr.;
                                                              SVE=Prod.struktursnr];
                                                   NotBlank=Yes }
    { 2   ;   ;Version Code        ;Code20        ;CaptionML=[DEU=Versionscode;
                                                              ENU=Version Code;
                                                              NLD=Versiecode;
                                                              NOR=Versjonskode;
                                                              SVE=Versionskod] }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 10  ;   ;Starting Date       ;Date          ;CaptionML=[DEU=Startdatum;
                                                              ENU=Starting Date;
                                                              NLD=Begindatum;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 21  ;   ;Unit of Measure Code;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                                ItemUnitOfMeasure@1001 : Record 5404;
                                                              BEGIN
                                                                IF (Status = Status::Certified) AND ("Unit of Measure Code" <> xRec."Unit of Measure Code") THEN
                                                                  FIELDERROR(Status);
                                                                Item.SETRANGE("Production BOM No.","Production BOM No.");
                                                                IF Item.FINDSET THEN
                                                                  REPEAT
                                                                    ItemUnitOfMeasure.GET(Item."No.","Unit of Measure Code");
                                                                  UNTIL Item.NEXT = 0;
                                                              END;

                                                   CaptionML=[DEU=Einheitencode;
                                                              ENU=Unit of Measure Code;
                                                              NLD=Eenheidscode;
                                                              NOR=Enhetskode;
                                                              SVE=Enhetskod] }
    { 22  ;   ;Last Date Modified  ;Date          ;CaptionML=[DEU=Korrigiert am;
                                                              ENU=Last Date Modified;
                                                              NLD=Gewijzigd op;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 45  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                ProdBOMHeader@1000 : Record 99000771;
                                                                PlanningAssignment@1001 : Record 99000850;
                                                                ProdBOMCheck@1002 : Codeunit 99000769;
                                                                SkipCommit@1003 : Boolean;
                                                              BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN BEGIN
                                                                  ProdBOMCheck.ProdBOMLineCheck("Production BOM No.","Version Code");
                                                                  TESTFIELD("Unit of Measure Code");
                                                                  ProdBOMHeader.GET("Production BOM No.");
                                                                  ProdBOMHeader."Low-Level Code" := 0;
                                                                  ProdBOMCheck.Code(ProdBOMHeader,"Version Code");
                                                                  PlanningAssignment.NewBOM("Production BOM No.");
                                                                END;
                                                                OnValidateStatusBeforeModify(Rec,xRec,CurrFieldNo);
                                                                MODIFY(TRUE);
                                                                SkipCommit := FALSE;
                                                                OnValidateStatusBeforeCommit(Rec,SkipCommit);
                                                                IF NOT SkipCommit THEN
                                                                  COMMIT;
                                                              END;

                                                   CaptionML=[DEU=Status;
                                                              ENU=Status;
                                                              NLD=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[DEU=Neu,Zertifiziert,In Entwicklung,Abgelaufen;
                                                                    ENU=New,Certified,Under Development,Closed;
                                                                    NLD=Nieuw,Gecertificeerd,In ontwikkeling,Afgesloten;
                                                                    NOR=Ny,Sertifisert,Under utvikling,Lukket;
                                                                    SVE=Ny,GodkÑnd,Under utveckling,Avslutad];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 50  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Nummernserie;
                                                              ENU=No. Series;
                                                              NLD=Nr.-reeks;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Production BOM No.,Version Code         ;Clustered=Yes }
    {    ;Production BOM No.,Starting Date         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProdBOMHeader@1000 : Record 99000771;
      ProdBOMVersion@1001 : Record 99000779;
      NoSeriesMgt@1002 : Codeunit 396;
      Text001@1003 : TextConst 'DEU=Sie kînnen den %1 nicht umbenennen, wenn der %2 %3 ist.;ENU=You cannot rename the %1 when %2 is %3.;NLD=U kunt de %1 niet hernoemen als %2 %3 is.;NOR=Du kan ikke gi %1 nytt navn nÜr %2 er %3.;SVE=Du kan inte byta namn pÜ %1 nÑr %2 Ñr %3.';

    [External]
    PROCEDURE AssistEdit@2(OldProdBOMVersion@1000 : Record 99000779) : Boolean;
    BEGIN
      WITH ProdBOMVersion DO BEGIN
        ProdBOMVersion := Rec;
        ProdBOMHeader.GET("Production BOM No.");
        ProdBOMHeader.TESTFIELD("Version Nos.");
        IF NoSeriesMgt.SelectSeries(ProdBOMHeader."Version Nos.",OldProdBOMVersion."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("Version Code");
          Rec := ProdBOMVersion;
          EXIT(TRUE);
        END;
      END;
    END;

    [External]
    PROCEDURE Caption@1() : Text[100];
    VAR
      ProdBOMHeader@1000 : Record 99000771;
    BEGIN
      IF GETFILTERS = '' THEN
        EXIT('');

      IF NOT ProdBOMHeader.GET("Production BOM No.") THEN
        EXIT('');

      EXIT(
        STRSUBSTNO('%1 %2 %3',
          "Production BOM No.",ProdBOMHeader.Description,"Version Code"));
    END;

    [Integration]
    LOCAL PROCEDURE OnValidateStatusBeforeModify@3(VAR ProductionBOMVersion@1000 : Record 99000779;VAR xProductionBOMVersion@1001 : Record 99000779;CallingFieldNo@1002 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnValidateStatusBeforeCommit@4(VAR ProductionBOMVersion@1000 : Record 99000779;VAR SkipCommit@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

