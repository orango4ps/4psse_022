OBJECT Page 11126113 Plan Maint. Proposal Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Wartungsvorschlagszeilen planen;
               ENU=Plan Maintenance Proposal Lines;
               NLD=Onderhoudsvoorstelregels plannen;
               NOR=Plan for vedlikehold, forslagsrader;
               SVE=Fîrslagsrader fîr planerat underhÜll];
    SourceTable=Table11012823;
    PageType=NavigatePage;
    OnInit=BEGIN
             MinimalUnoccupiedHoursEnable := TRUE;
             MinimalUnoccupiedHours := 1;
           END;

    OnOpenPage=BEGIN
                 //**4PS.sn
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS.en
                 UpdateEditable;
                 WizardStep := 1;
                 ShowStep(TRUE);
               END;

    OnNewRecord=BEGIN
                  //**4PS.sn
                  DPA_SetDefaultDepartment;
                  //**4PS.en
                END;

    OnInsertRecord=VAR
                     xRecRef@1100525000 : RecordRef;
                   BEGIN
                     //**4PS.sn
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS.en
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528618;1 ;Action    ;
                      Name=Back;
                      CaptionML=[DEU=< &ZurÅck;
                                 ENU=< &Back;
                                 NLD=< &Vorige;
                                 NOR=< &Tilbake;
                                 SVE=< &Tillbaka];
                      Enabled=BackEnable;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PreCheckStatus(FALSE);
                                 ShowStep(FALSE);
                                 PostCheckStatus(FALSE);
                                 WizardStep -= 1;
                                 ShowStep(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528617;1 ;Action    ;
                      Name=Next;
                      CaptionML=[DEU=&Weiter >;
                                 ENU=&Next >;
                                 NLD=Vo&lgende >;
                                 NOR=&Neste >;
                                 SVE=&NÑsta >];
                      Enabled=NextEnable;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PreCheckStatus(TRUE);
                                 ShowStep(FALSE);
                                 PostCheckStatus(TRUE);
                                 WizardStep += 1;
                                 ShowStep(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528616;1 ;Action    ;
                      Name=Finish;
                      CaptionML=[DEU=B&eenden;
                                 ENU=&Finish;
                                 NLD=Voltooie&n;
                                 NOR=&Avslutt;
                                 SVE=&Slutfîr];
                      Enabled=FinishEnable;
                      InFooterBar=Yes;
                      Image=Completed;
                      OnAction=BEGIN
                                 PostCheckStatus(TRUE);
                                 FinishWizard;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528606;1;Group  ;
                CaptionML=[DEU=Planmethode;
                           ENU=Plan Method;
                           NLD=Planningsmethode;
                           NOR=Planlegg Metode;
                           SVE=Planmetod];
                Visible=Step1Visible;
                GroupType=Group }

    { 1100528609;2;Field  ;
                CaptionML=[DEU=Planmethode;
                           ENU=Plan Method;
                           NLD=Planningsmethode;
                           NOR=Planlegg Metode;
                           SVE=Planmetod];
                OptionCaptionML=[DEU=Mitarbeiter,Cluster;
                                 ENU=Employee,Cluster;
                                 NLD=Medewerker,Cluster;
                                 NOR=Ansatt,Klynge];
                SourceExpr=PlanMethod }

    { 1100528602;1;Group  ;
                CaptionML=[DEU=Employee;
                           ENU=Employee;
                           NLD=Medewerker;
                           NOR=Ansatt;
                           SVE=Personal];
                Visible=Step2Visible;
                GroupType=Group }

    { 1100525000;2;Field  ;
                CaptionML=[DEU=Wartungsvorschlagszeilen planen;
                           ENU=Plan Maintenance Proposal Lines;
                           NLD=Onderhoudsvoorstelregels plannen;
                           NOR=Plan for vedlikehold, forslagsrader;
                           SVE=Fîrslagsrader fîr planerat underhÜll];
                Style=Strong;
                StyleExpr=TRUE }

    { 1100528603;2;Field  ;
                CaptionML=[DEU=Planungsdatum ab;
                           ENU=Plan Date From;
                           NLD=Plandatum vanaf;
                           NOR=Plandato fra;
                           SVE=Plandatum frÜn];
                SourceExpr=PlanDateFrom }

    { 1100528608;2;Field  ;
                CaptionML=[DEU=Planungsdatum bis einschl.;
                           ENU=Plan Date Until;
                           NLD=Plandatum t/m;
                           NOR=Plandato til;
                           SVE=Plandatum till];
                SourceExpr=PlanDateUntil }

    { 1100528601;2;Field  ;
                CaptionML=[DEU=Mitarbeiternr.;
                           ENU=Employee No.;
                           NLD=Werknemer;
                           NOR=Ansattnr;
                           SVE=AnstÑllningsnr];
                SourceExpr=ResourceNo;
                TableRelation="Resource (WOP)" WHERE (Type=CONST(Employee));
                OnValidate=BEGIN
                             ResourceNoOnAfterValidate;
                           END;

                OnLookup=VAR
                           ResourceWOP@1100528602 : Record 11229278;
                           MaintenanceManagement@1100528600 : Codeunit 11012829;
                         BEGIN
                           MaintenanceManagement.GetResourceDSByMaintenanceProposalLineDS(
                             MaintenanceProposalLine, ResourceWOP, PlanDateFrom, PlanDateUntil, 1, '', '', '');
                           IF PAGE.RUNMODAL(0, ResourceWOP) = ACTION::LookupOK THEN BEGIN
                             ResourceNo := ResourceWOP."No.";
                             ResourceNoOnAfterValidate;
                           END;
                         END;
                          }

    { 1100528607;2;Field  ;
                CaptionML=[DEU=Name Mitarbeiter;
                           ENU=Employee Name;
                           NLD=Werknemer naam;
                           NOR=Navn pÜ ansatt;
                           SVE=Namn pÜ anstÑlld];
                SourceExpr=EmployeeName;
                Editable=FALSE }

    { 1100528604;1;Group  ;
                CaptionML=[DEU=Cluster;
                           ENU=Cluster;
                           NLD=Cluster;
                           NOR=Klynge;
                           SVE=Kluster];
                Visible=Step3Visible;
                GroupType=Group }

    { 1100528611;2;Field  ;
                CaptionML=[DEU=Cluster-Sequ.;
                           ENU=Cluster Seqn.;
                           NLD=Clustervolgnr.;
                           NOR=Klyngesekvens];
                SourceExpr=ClusterSeqn;
                OnValidate=VAR
                             ServiceClusterSeq@1100528600 : Record 11071966;
                           BEGIN
                             IF ClusterSeqn <> 0 THEN BEGIN
                               MaintenanceProposalLine.CALCFIELDS("Contract Cluster");
                               IF MaintenanceProposalLine."Contract Cluster" THEN
                                 ServiceClusterSeq.GET(
                                   ServiceClusterSeq.Type::Contract, MaintenanceProposalLine.GetClusterContractNo, ServiceClusterSeq."Source Type"::" ", MaintenanceProposalLine.Cluster, ClusterSeqn)
                               ELSE
                                 ServiceClusterSeq.GET(
                                   ServiceClusterSeq.Type::" ", '', ServiceClusterSeq."Source Type"::" ", MaintenanceProposalLine.Cluster, ClusterSeqn);
                               ClusterPlanDateFrom := ServiceClusterSeq."Execution Period Start Date";
                               IF ClusterPlanDateFrom < TODAY THEN
                                 ClusterPlanDateFrom := TODAY;
                               ClusterPlanDateUntil := ServiceClusterSeq."Execution Period Ending Date";
                               IF ClusterPlanDateFrom > ClusterPlanDateUntil THEN
                                 ERROR(Text002);
                             END;
                           END;

                OnLookup=VAR
                           ServiceClusterSeq@1100528600 : Record 11071966;
                         BEGIN
                           MaintenanceProposalLine.CALCFIELDS("Contract Cluster");
                           IF MaintenanceProposalLine."Contract Cluster" THEN BEGIN
                             ServiceClusterSeq.SETRANGE(Type, ServiceClusterSeq.Type::Contract);
                             ServiceClusterSeq.SETRANGE("Type Code", MaintenanceProposalLine.GetClusterContractNo);
                           END ELSE BEGIN
                             ServiceClusterSeq.SETRANGE("Type Code", '');
                             ServiceClusterSeq.SETRANGE(Type, ServiceClusterSeq.Type::" ");
                           END;
                           ServiceClusterSeq.SETRANGE("Source Type", ServiceClusterSeq."Source Type"::" ");
                           ServiceClusterSeq.SETRANGE("Cluster Code", MaintenanceProposalLine.Cluster);
                           IF PAGE.RUNMODAL(0, ServiceClusterSeq) = ACTION::LookupOK THEN BEGIN
                             ClusterSeqn := ServiceClusterSeq."Seq. No.";
                             ClusterPlanDateFrom := ServiceClusterSeq."Execution Period Start Date";
                             ClusterPlanDateUntil := ServiceClusterSeq."Execution Period Ending Date";
                           END;
                         END;
                          }

    { 1100528613;2;Field  ;
                CaptionML=[DEU=Planungsdatum ab;
                           ENU=Plan Date From;
                           NLD=Plandatum vanaf;
                           NOR=Plandato fra;
                           SVE=Plandatum frÜn];
                SourceExpr=ClusterPlanDateFrom;
                Editable=false }

    { 1100528614;2;Field  ;
                CaptionML=[DEU=Planungsdatum bis einschl.;
                           ENU=Plan Date Until;
                           NLD=Plandatum t/m;
                           NOR=Plandato til;
                           SVE=Plandatum till];
                SourceExpr=ClusterPlanDateUntil;
                Editable=false }

    { 1100528615;1;Group  ;
                CaptionML=[DEU=öbriges;
                           ENU=Other;
                           NLD=Overig;
                           NOR=ùvrige;
                           SVE=ôvriga];
                Visible=Step4Visible;
                GroupType=Group }

    { 1100528605;2;Field  ;
                CaptionML=[DEU=Fahrtzeiten;
                           ENU=Travel Time;
                           NLD=Reistijd;
                           NOR=Reisetid;
                           SVE=Restid];
                SourceExpr=TravelDuration }

    { 1100528612;2;Field  ;
                CaptionML=[DEU=Zuordnen mehreren Tagen;
                           ENU=Assign to Several Days;
                           NLD=Toekennen aan meerdere dagen;
                           NOR=Tilordne flere dager;
                           SVE=Tilldela till flera dagar];
                SourceExpr=OccupySeveralDays;
                OnValidate=BEGIN
                             OccupySeveralDaysOnAfterValida;
                           END;
                            }

    { 1100528610;2;Field  ;
                Name=MinimalUnoccupiedHours;
                CaptionML=[DEU=Min. Anzahl freier Stunden;
                           ENU=Minimal Unoccupied Hours;
                           NLD=Minimaal aantal vrije uren;
                           NOR=Laveste antall ubemannede timer;
                           SVE=LÑgsta obemannade timmar];
                SourceExpr=MinimalUnoccupiedHours;
                Enabled=MinimalUnoccupiedHoursEnable }

  }
  CODE
  {
    VAR
      MaintenanceProposalLine@1100528604 : Record 11071696;
      ResourceNo@1100528600 : Code[20];
      EmployeeName@1100528603 : Text[100];
      PlanDateFrom@1100528601 : Date;
      PlanDateUntil@1100528607 : Date;
      TravelDuration@1100528602 : Duration;
      Text000@1100528605 : TextConst 'DEU=Mitarbeiter nicht eingegeben;ENU=Employee No. not filled;NLD=Werknemer niet gevuld;SVE=AnstÑlld Ñr inte ifyllt';
      Text001@1100528606 : TextConst 'DEU=''Planungsdatum ab'' und ''Planungsdatum bis einschl.'' mÅssen beide eingegeben worden sein;ENU=Plan Date From and Plan Date To must be filled;NLD=Plandatum vanaf en Plandatum t/m dienen beide gevuld te zijn;NOR=Plandato fra og Plandato til mÜ vëre fylt ut;SVE=Plandatum frÜn och plandatum till mÜste fyllas i';
      Text002@1100528608 : TextConst 'DEU=''Planungsdatum ab'' muss vor dem ''Planungsdatum bis einschl.'' liegen;ENU=Plan Date From must occur before Plan Date Until;NLD=Plandatum vanaf moet liggen voor Plandatum t/m;NOR=Plandato fra mÜ vëre fõr plandato til;SVE=Plandatum frÜn mÜste intrÑffa fîre plandatum till';
      MinimalUnoccupiedHours@1100528609 : Decimal;
      OccupySeveralDays@1100528610 : Boolean;
      MinimalUnoccupiedHoursEnable@19064514 : Boolean INDATASET;
      PlanMethod@1100528611 : 'Employee,Cluster Seqn.';
      ClusterSeqn@1100528612 : Integer;
      Text003@1100528613 : TextConst 'DEU=Die gewÑhlten Zeilen gehîren nicht zum selben Cluster;ENU=Selected Lines do not belong to same Cluster;NLD=Geselecteerde regels behoren niet tot hetzelfde cluster;NOR=Valgte rader tilhõrer ikke samme klynge';
      ClusterPlanDateFrom@1100528614 : Date;
      ClusterPlanDateUntil@1100528615 : Date;
      WizardStep@1100528616 : Integer;
      Step1Visible@1100528619 : Boolean INDATASET;
      Step2Visible@1100528618 : Boolean INDATASET;
      Step3Visible@1100528617 : Boolean INDATASET;
      Step4Visible@1100528623 : Boolean INDATASET;
      FinishEnable@1100528622 : Boolean INDATASET;
      BackEnable@1100528621 : Boolean INDATASET;
      NextEnable@1100528620 : Boolean INDATASET;
      Text004@1100528624 : TextConst 'DEU=Cluster-Sequ. muss eingestellt werden;ENU=Cluster Seqn. must be set;NLD=Clustervolgnr. dient gevuld te zijn;NOR=Klyngesekvens mÜ vëre satt';
      DPA_IsHardFilter@1100525000 : Boolean;

    PROCEDURE SetSelection@1100528603(VAR IMaintenanceProposalLine@1100528600 : Record 11071696);
    BEGIN
      MaintenanceProposalLine.COPY(IMaintenanceProposalLine);
      MaintenanceProposalLine.FINDFIRST;
    END;

    PROCEDURE UpdateEditable@1100528600();
    BEGIN
      MinimalUnoccupiedHoursEnable := OccupySeveralDays;
    END;

    LOCAL PROCEDURE ResourceNoOnAfterValidate@19040646();
    VAR
      Resource@1100528600 : Record 11229278;
      ResourceMgtWorkOrderPlan@1100528601 : Codeunit 11229277;
    BEGIN
      IF ResourceNo <> '' THEN BEGIN
        ResourceMgtWorkOrderPlan.TestResourceWithinCompanyPlanGroup(ResourceNo);
        Resource.GET(ResourceNo);  //DP00465.c
        EmployeeName := Resource.Name;  //DP00465.c
      END;
    END;

    LOCAL PROCEDURE OccupySeveralDaysOnAfterValida@19062635();
    BEGIN
      UpdateEditable;
    END;

    PROCEDURE PreCheckStatus@1100528602(IForward@1100528600 : Boolean);
    VAR
      MaintenanceProposalLine2@1100528601 : Record 11071696;
    BEGIN
      CASE WizardStep OF
        2:
          BEGIN
            IF IForward THEN BEGIN
              IF ResourceNo = '' THEN
                ERROR(Text000);
              IF (PlanDateFrom = 0D) OR (PlanDateUntil = 0D) THEN
                ERROR(Text001);
              IF PlanDateFrom > PlanDateUntil THEN
                ERROR(Text002);
            END;
          END;
        3:
          BEGIN
            IF IForward THEN BEGIN
              IF ClusterSeqn = 0 THEN
                ERROR(Text004);
              MaintenanceProposalLine2.COPY(MaintenanceProposalLine);
              MaintenanceProposalLine2.SETFILTER(Cluster, '<>%1', MaintenanceProposalLine2.Cluster);
              IF NOT MaintenanceProposalLine2.ISEMPTY THEN
                ERROR(Text003);
              IF ClusterSeqn <> 0 THEN
                MaintenanceProposalLine.SETRANGE("Cluster Seqn.", ClusterSeqn);
            END;
          END;
      END;
    END;

    PROCEDURE PostCheckStatus@1100528604(IForward@1100528600 : Boolean);
    VAR
      MaintenanceProposalHeader@1100528601 : Record 11071695;
    BEGIN
      CASE WizardStep OF
        1:
          BEGIN
            IF IForward THEN BEGIN
              CASE PlanMethod OF
                PlanMethod::Employee:
                  BEGIN
                    ClusterSeqn := 0;
                    ClusterPlanDateFrom := 0D;
                    ClusterPlanDateUntil := 0D;
                  END;
                PlanMethod::"Cluster Seqn.":
                  BEGIN
                    MaintenanceProposalHeader.GET(MaintenanceProposalLine."Maintenance Proposal");
                    MaintenanceProposalHeader.TESTFIELD("Created from", MaintenanceProposalHeader."Created from"::Contract);
                    ResourceNo := '';
                    EmployeeName := '';
                    PlanDateFrom := 0D;
                    PlanDateUntil := 0D;
                  END;
              END;
              UpdateEditable;
              IF PlanMethod = PlanMethod::"Cluster Seqn." THEN
                WizardStep += 1;
            END;
          END;
        2:
          IF IForward THEN
            IF PlanMethod = PlanMethod::Employee THEN
              WizardStep += 1;
        3:
          IF NOT IForward THEN
            IF PlanMethod = PlanMethod::"Cluster Seqn." THEN
              WizardStep -= 1;
        4:
          IF NOT IForward THEN
            IF PlanMethod = PlanMethod::Employee THEN
                WizardStep -= 1;
      END;
    END;

    LOCAL PROCEDURE ShowStep@2(Visible@1001 : Boolean);
    BEGIN
      CASE WizardStep OF
        1:
          BEGIN
            Step1Visible := Visible;
            IF Visible THEN BEGIN
              FinishEnable := FALSE;
              BackEnable := FALSE;
              NextEnable := TRUE;
            END;
          END;
        2:
          BEGIN
            Step2Visible := Visible;
            IF Visible THEN BEGIN
              NextEnable := TRUE;
              BackEnable := TRUE;
              FinishEnable := FALSE;
            END;
          END;
        3:
          BEGIN
            Step3Visible := Visible;
            IF Visible THEN BEGIN
              FinishEnable := FALSE;
              BackEnable := TRUE;
              NextEnable := TRUE;
            END;
          END;
        4:
          BEGIN
            Step4Visible := Visible;
            IF Visible THEN BEGIN
              FinishEnable := TRUE;
              BackEnable := TRUE;
              NextEnable := FALSE;
            END;
          END;
      END;
    END;

    PROCEDURE FinishWizard@1100528606();
    VAR
      MaintenanceManagement@1100528600 : Codeunit 11012829;
    BEGIN
      MaintenanceManagement.PlanMaintPropLinesByResource(
        MaintenanceProposalLine, ResourceNo, PlanDateFrom, PlanDateUntil, TravelDuration,
        MinimalUnoccupiedHours, OccupySeveralDays, ClusterSeqn);
      CurrPage.CLOSE;
    END;

    PROCEDURE SetResourceNo@1100528601(IResourceNo@1100528600 : Code[20]);
    BEGIN
      ResourceNo := IResourceNo;
      ResourceNoOnAfterValidate;
    END;

    BEGIN
    END.
  }
}

