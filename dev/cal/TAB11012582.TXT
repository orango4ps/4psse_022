OBJECT Table 11012582 Plant Profile Answer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lvPlantQuestionListRec@1210190000 : Record 11012581;
               lvPlantQuestionListRec2@1210190001 : Record 11012581;
               lvPlantProfileAnswerRec@1210190003 : Record 11012582;
               lvLineNo@1210190002 : Integer;
             BEGIN
               CASE Level OF
                 Level::PlantType:
                   BEGIN
                     TESTFIELD("Plant Type");
                     "Plant No." := '';
                     "Material Inventory Line No." := 0;
                   END;
                 Level::PlantNumber:
                   BEGIN
                     TESTFIELD("Plant Type");
                     TESTFIELD("Plant No.");
                     "Material Inventory Line No." := 0;
                   END;
                 Level::MaterialInventory:
                   BEGIN
                     TESTFIELD("Plant Type");
                     TESTFIELD("Plant No.");
                     TESTFIELD("Material Inventory Line No.");
                   END;
               ELSE
                 FIELDERROR(Level);
               END;
               lvPlantQuestionListRec.GET("Profile Code","Line No.");
               lvPlantQuestionListRec.TESTFIELD(Type,lvPlantQuestionListRec.Type::Answer);

               lvLineNo := QuestionLineNo();
               lvPlantQuestionListRec2.GET("Profile Code",lvLineNo);

               IF NOT lvPlantQuestionListRec2."Multiple Answers" THEN BEGIN
                 lvPlantQuestionListRec2.SETRANGE("Profile Code","Profile Code");
                 lvPlantQuestionListRec2.SETRANGE(Type,lvPlantQuestionListRec2.Type::Question);
                 lvPlantQuestionListRec2.SETFILTER("Line No.", '>%1',lvLineNo);
                 IF lvPlantQuestionListRec2.FIND('-') THEN
                   lvPlantProfileAnswerRec.SETRANGE("Line No.",lvLineNo,(lvPlantQuestionListRec2."Line No." - 1))
                 ELSE
                   lvPlantProfileAnswerRec.SETFILTER("Line No.", '>=%1',lvPlantQuestionListRec2."Line No.");
                 lvPlantProfileAnswerRec.SETRANGE(Level, Level);
                 lvPlantProfileAnswerRec.SETRANGE("Plant Type", "Plant Type");
                 lvPlantProfileAnswerRec.SETRANGE("Plant No.", "Plant No.");
                 lvPlantProfileAnswerRec.SETRANGE("Material Inventory Line No.", "Material Inventory Line No.");
                 lvPlantProfileAnswerRec.SETRANGE("Profile Code","Profile Code");
                 IF lvPlantProfileAnswerRec.FIND('-') THEN
                   ERROR(Text000,lvPlantQuestionListRec.FIELDCAPTION("Multiple Answers"));
               END;

               "Question Line No." := lvLineNo;

               UpdateModifiedBy; //C037249.n
             END;

    OnModify=BEGIN
               UpdateModifiedBy; //C037249.n
             END;

    CaptionML=[ENU=Plant Profile Answer;
               NOR=Maskin, profilsvar;
               SVE=Maskin, profilsvar];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 2   ;   ;Profile Code        ;Code10        ;TableRelation="Plant Question Profile";
                                                   CaptionML=[ENU=Profile Code;
                                                              NOR=Profilkode;
                                                              SVE=Profilkod];
                                                   NotBlank=Yes }
    { 3   ;   ;Line No.            ;Integer       ;TableRelation="Plant Question List"."Line No." WHERE (Profile Code=FIELD(Profile Code),
                                                                                                         Line No.=FIELD(Line No.),
                                                                                                         Type=CONST(Answer));
                                                   CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer] }
    { 4   ;   ;Answer              ;Text50        ;CaptionML=[ENU=Technical Specification;
                                                              NOR=Tekniske Spesifikasjoner;
                                                              SVE=Tekniska specifikationer] }
    { 5   ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr];
                                                   Editable=No }
    { 6   ;   ;Level               ;Option        ;CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   OptionCaptionML=ENU=Plant Type,Plant Number,Material Inventory;
                                                   OptionString=PlantType,PlantNumber,MaterialInventory }
    { 7   ;   ;Material Inventory Line No.;Integer;TableRelation=IF (Level=CONST(MaterialInventory)) "Plant - Material Inventory"."Line No." WHERE (Plant Type=FIELD(Plant Type),
                                                                                                                                                    Plant No.=FIELD(Plant No.));
                                                   CaptionML=ENU=Material Inventory Line No. }
    { 10  ;   ;Feature             ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Question List".Description WHERE (Profile Code=FIELD(Profile Code),
                                                                                                               Line No.=FIELD(Line No. Question),
                                                                                                               Type=CONST(Question)));
                                                   CaptionML=[ENU=Feature;
                                                              NOR=Funksjon;
                                                              SVE=Funktion];
                                                   Description=Only for first answer of a question (because "Line No. Question" is only filled for first);
                                                   Editable=No }
    { 11  ;   ;Line No. Question   ;Integer       ;TableRelation="Plant Question List"."Line No." WHERE (Profile Code=FIELD(Profile Code));
                                                   CaptionML=[ENU=Line No. Question;
                                                              NOR=Radnr, Sp›rsm†l;
                                                              SVE=Radnr, fr†ga];
                                                   Description=May only be filled in the first answer of a question;
                                                   Editable=No }
    { 12  ;   ;Feature (Question)  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Question List".Description WHERE (Profile Code=FIELD(Profile Code),
                                                                                                               Line No.=FIELD(Question Line No.),
                                                                                                               Type=CONST(Question)));
                                                   CaptionML=[ENU=Feature (Question);
                                                              NOR=Funksjon (sp›rsm†l);
                                                              SVE=Funktion (fr†ga)];
                                                   Description=For each answer of a question ("Question Line No." is filled for each);
                                                   Editable=No }
    { 13  ;   ;Question Line No.   ;Integer       ;TableRelation="Plant Question List"."Line No." WHERE (Profile Code=FIELD(Profile Code));
                                                   CaptionML=[ENU=Question Line No.;
                                                              NOR=Sp›rsm†l, radnr;
                                                              SVE=Fr†ga, radnr];
                                                   Description=Must be filled in each answer of a question;
                                                   Editable=Yes }
    { 15  ;   ;Profile Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Question Profile".Description WHERE (Code=FIELD(Profile Code)));
                                                   CaptionML=[ENU=Profile Description;
                                                              NOR=Profilbeskrivelse;
                                                              SVE=Profile Description];
                                                   Editable=No }
    { 30  ;   ;Plant Type Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Description WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Plant Type Description;
                                                              NOR=Maskintype Beskrivelse;
                                                              SVE=Maskintyp beskrivning];
                                                   Editable=No }
    { 31  ;   ;Plant No. Description;Text50       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=[ENU=Plant No. Description;
                                                              NOR=Maskinnr., Beskrivelse;
                                                              SVE=Maskinnr, beskrivning];
                                                   Editable=No }
    { 40  ;   ;Copy To Plant No.   ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Plant No.", ''); //mg, 12-04-11: M25005
                                                              END;

                                                   CaptionML=[ENU=Copy To Plant No.;
                                                              NOR=Kopier til maskinnr.;
                                                              SVE=Kopiera till Maskinnr] }
    { 50  ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=Žndrad av];
                                                   Description=C037249;
                                                   Editable=No }
    { 51  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Description=C037249;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Level,Plant Type,Plant No.,Material Inventory Line No.,Profile Code,Line No.;
                                                   Clustered=Yes }
    {    ;Profile Code,Line No.                    }
    {    ;Plant Type,Plant No.,Material Inventory Line No.,Level,Profile Code,Line No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This Question does not allow %1.;NOR=Dette sp›rsm†let tillater ikke %1.;SVE=Denna fr†ga till†ter inte %1.';

    PROCEDURE GetQuestion@1() : Text[50];
    VAR
      lvPlantQuestionListRec@1210190000 : Record 11012581;
    BEGIN
      IF lvPlantQuestionListRec.GET("Profile Code", QuestionLineNo()) THEN
        EXIT(lvPlantQuestionListRec.Description)
      ELSE
        EXIT('');
    END;

    PROCEDURE QuestionLineNo@3() : Integer;
    VAR
      lvPlantQuestionListRec@1210190000 : Record 11012581;
    BEGIN
      WITH lvPlantQuestionListRec DO BEGIN
        RESET;
        SETRANGE("Profile Code",Rec."Profile Code");
        SETFILTER("Line No.",'<%1',Rec."Line No.");
        SETRANGE(Type,Type::Question);
        IF FIND('+') THEN
          EXIT("Line No.");
      END;
      EXIT(-1);
    END;

    PROCEDURE UpdateModifiedBy@1100529600();
    BEGIN
      //C037249.sn
      "Modified by" := USERID;
      "Last Date Modified" := TODAY;
      //C037249.en
    END;

    PROCEDURE MaterialInventoryDescription@1100529000() : Text;
    VAR
      PlantMaterialInventory@1100529000 : Record 11229926;
    BEGIN
      IF Level = Level::MaterialInventory THEN BEGIN
        IF CURRENTCOMPANY <> COMPANYNAME THEN
          PlantMaterialInventory.CHANGECOMPANY(CURRENTCOMPANY);
        IF PlantMaterialInventory.GET("Plant Type", "Plant No.", "Material Inventory Line No.") THEN
          EXIT(PlantMaterialInventory.Description);
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

