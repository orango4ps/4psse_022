OBJECT Codeunit 5899 Calc. Inventory Value-Check
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5804=r;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text004@1000 : TextConst 'ENU=Checking items #1##########;NOR=Sjekker varer  #1##########;SVE=Artikelkontroll  #1##########';
      InvtSetup@1017 : Record 313;
      TempErrorBuf@1004 : TEMPORARY Record 5890;
      PostingDate@1008 : Date;
      CalculatePer@1001 : 'Item Ledger Entry,Item';
      ByLocation@1019 : Boolean;
      ByVariant@1018 : Boolean;
      Text007@1006 : TextConst 'ENU=You have to run the Adjust Cost - Item Entries batch job, before you can revalue item %1.;NOR=Du mÜ kjõre den satsvise jobben Juster kostverdi - vareposter fõr du kan revaluere varen %1.;SVE=Du mÜste kîra batchjobbet Justera kost. - artikeltrans. innan du kan omvÑrdera artikel %1.';
      Text009@1009 : TextConst 'ENU=You must not revalue items with Costing Method %1, if Calculate Per is Item Ledger Entry.;NOR=Du kan ikke revaluere varer med lagermetode %1, hvis Beregn per er Varepost.;SVE=Du kan inte omvÑrdera artiklar med vÑrderingsprincipen %1 om BerÑkna per Ñr Artikeltransaktion.';
      Text011@1016 : TextConst 'ENU=You must not enter a %1 if you revalue items with Costing Method %2 and if Average Cost Calc. Type is %3 in Inventory Setup.;NOR=Du mÜ ikke angi %1 hvis du revaluerer varer med lagermetode %2 og hvis Beregn.type for gj.snittskost er %3 i Lageroppsett.;SVE=Om du omvÑrderar artiklar med vÑrderingsprincipen %2 och Genoms. kost.ber.typ Ñr %3 i lagerinstÑllningarna fÜr du inte ange %1.';
      Text012@1015 : TextConst 'ENU=The By Location field must not be filled in if you revalue items with Costing Method %1 and if Average Cost Calc. Type is %2 in Inventory Setup.;NOR=Feltet Per lokasjon mÜ ikke fylles ut hvis du revaluerer varer med lagermetode %1 og hvis Beregn.type for gj.snittskost er %2 i Lageroppsett.;SVE=Om du omvÑrderar artiklar med vÑrderingsprincipen %1 och Genoms. kost.ber.typ Ñr %2 i lagerinstÑllningarna fÜr inte fÑltet Per lagerstÑlle fyllas i.';
      Text014@1013 : TextConst 'ENU=The By Variant field must not be filled in if you revalue items with Costing Method %1 and if Average Cost Calc. Type is %2 in Inventory Setup.;NOR=Feltet Per variant mÜ ikke fylles ut hvis du revaluerer varer med lagermetode %1 og hvis Beregn.type for gj.snittskost er %2 i Lageroppsett.;SVE=Om du omvÑrderar artiklar med vÑrderingsprincipen %1 och Genoms. kost.ber.typ Ñr %2 i lagerinstÑllningarna fÜr inte fÑltet Per variant fyllas i.';
      Text015@1012 : TextConst 'ENU=You must fill in a Location filter and a Variant filter or select the By Location field and the By Variant field, if you revalue items with Costing Method %1, and if Average Cost Calc. Type is %2 in Inventory Setup.;NOR=Du mÜ fylle ut et lokasjons- og et variantfilter eller velge feltene Per lokasjon og Per variant hvis du revaluerer varer med lagermetode %1 og hvis Beregn.type for gj.snittskost er %2 i Lageroppsett.;SVE=Om du omvÑrderar artiklar med vÑrderingsprincipen %1 och Genoms. kost.ber.typ Ñr %2 i lagerinstÑllningarna mÜste du fylla i ett lagerstÑllefilter och ett variantfilter eller markera fÑlten Per lagerstÑlle och Per variant.';
      ShowDialog@1002 : Boolean;
      TestMode@1003 : Boolean;
      ErrorCounter@1007 : Integer;
      Text020@1011 : TextConst 'ENU=Open Outbound Entry %1 found.;NOR=èpen utgÜende post %1 funnet.;SVE=Den îppna utgÜende transaktionen %1 har hittats.';

    [External]
    PROCEDURE SetProperties@1(NewPostingDate@1000 : Date;NewCalculatePer@1003 : Option;NewByLocation@1002 : Boolean;NewByVariant@1001 : Boolean;NewShowDialog@1005 : Boolean;NewTestMode@1006 : Boolean);
    BEGIN
      TempErrorBuf.DELETEALL;
      CLEARALL;

      PostingDate := NewPostingDate;
      CalculatePer := NewCalculatePer;
      ByLocation := NewByLocation;
      ByVariant := NewByVariant;
      ShowDialog := NewShowDialog;
      TestMode := NewTestMode;

      InvtSetup.GET;
    END;

    [External]
    PROCEDURE RunCheck@3(VAR Item@1000 : Record 27;VAR NewErrorBuf@1002 : Record 5890);
    VAR
      Item2@1004 : Record 27;
      Window@1001 : Dialog;
    BEGIN
      WITH Item2 DO BEGIN
        COPY(Item);

        CheckCalculatePer(Item2);

        IF FINDSET THEN BEGIN
          IF ShowDialog THEN
            Window.OPEN(Text004,"No.");
          REPEAT
            IF ShowDialog THEN
              Window.UPDATE(1,"No.");

            //**4PS.so db, 18-01-10: M17763
            //IF FindOpenOutboundEntry(Item2) THEN
            //  IF NOT TestMode THEN
            //    ERROR(Text018,"No.");
            //**4PS.eo
            IF NOT Adjusted(Item2) THEN
              AddError(
                STRSUBSTNO(Text007,"No."),DATABASE::Item,"No.",0);
          UNTIL NEXT = 0;
          IF ShowDialog THEN
            Window.CLOSE;
        END;
      END;

      TempErrorBuf.RESET;
      IF TempErrorBuf.FINDSET THEN
        REPEAT
          NewErrorBuf := TempErrorBuf;
          NewErrorBuf.INSERT;
        UNTIL TempErrorBuf.NEXT = 0;
    END;

    LOCAL PROCEDURE Adjusted@10(Item@1000 : Record 27) : Boolean;
    VAR
      AvgCostAdjmt@1002 : Record 5804;
    BEGIN
      IF Item."Costing Method" = Item."Costing Method"::Average THEN BEGIN
        AvgCostAdjmt.SETCURRENTKEY("Item No.","Cost Is Adjusted");
        AvgCostAdjmt.SETFILTER("Item No.",Item."No.");
        AvgCostAdjmt.SETRANGE("Cost Is Adjusted",FALSE);
        AvgCostAdjmt.SETRANGE("Valuation Date",0D,PostingDate);
        EXIT(AvgCostAdjmt.ISEMPTY);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckCalculatePer@6(VAR Item@1000 : Record 27);
    VAR
      Item2@1001 : Record 27;
    BEGIN
      WITH Item2 DO BEGIN
        COPYFILTERS(Item);

        FILTERGROUP(2);
        SETRANGE("Costing Method","Costing Method"::Average);
        FILTERGROUP(0);

        IF FINDFIRST THEN
          CASE CalculatePer OF
            CalculatePer::"Item Ledger Entry":
              AddError(
                STRSUBSTNO(Text009,"Costing Method"),DATABASE::Item,"No.",0);
            CalculatePer::Item:
              BEGIN
                IF InvtSetup."Average Cost Calc. Type" = InvtSetup."Average Cost Calc. Type"::Item THEN BEGIN
                  IF GETFILTER("Location Filter") <> '' THEN
                    AddError(
                      STRSUBSTNO(
                        Text011,
                        FIELDCAPTION("Location Filter"),"Costing Method",InvtSetup."Average Cost Calc. Type"),DATABASE::Item,"No.",0);
                  IF GETFILTER("Variant Filter") <> '' THEN
                    AddError(
                      STRSUBSTNO(
                        Text011,
                        FIELDCAPTION("Variant Filter"),"Costing Method",InvtSetup."Average Cost Calc. Type"),DATABASE::Item,"No.",0);
                  IF ByLocation THEN
                    AddError(
                      STRSUBSTNO(
                        Text012,
                        "Costing Method",InvtSetup."Average Cost Calc. Type"),DATABASE::Item,"No.",0);
                  IF ByVariant THEN
                    AddError(
                      STRSUBSTNO(
                        Text014,
                        "Costing Method",InvtSetup."Average Cost Calc. Type"),DATABASE::Item,"No.",0);
                END ELSE
                  IF ((GETFILTER("Location Filter") = '') AND (NOT ByLocation)) OR
                     ((GETFILTER("Variant Filter") = '') AND (NOT ByVariant))
                  THEN
                    AddError(
                      STRSUBSTNO(
                        Text015,
                        "Costing Method",InvtSetup."Average Cost Calc. Type"),DATABASE::Item,"No.",0);
              END;
          END;
      END;
    END;

    LOCAL PROCEDURE FindOpenOutboundEntry@2(VAR Item@1000 : Record 27) : Boolean;
    VAR
      ItemLedgEntry@1102601000 : Record 32;
    BEGIN
      ItemLedgEntry.RESET;
      ItemLedgEntry.SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code","Posting Date");
      ItemLedgEntry.SETRANGE("Item No.",Item."No.");
      ItemLedgEntry.SETRANGE(Open,TRUE);
      ItemLedgEntry.SETRANGE(Positive,FALSE);
      ItemLedgEntry.SETRANGE("Posting Date",0D,PostingDate);

      Item.COPYFILTER("Variant Filter",ItemLedgEntry."Variant Code");
      Item.COPYFILTER("Location Filter",ItemLedgEntry."Location Code");

      IF ItemLedgEntry.FINDSET THEN BEGIN
        REPEAT
          AddError(
            STRSUBSTNO(Text020,ItemLedgEntry."Entry No."),
            DATABASE::"Item Ledger Entry",ItemLedgEntry."Item No.",ItemLedgEntry."Entry No.");
        UNTIL ItemLedgEntry.NEXT = 0;
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE AddError@4(Text@1004 : Text;SourceTable@1001 : Integer;SourceNo@1002 : Code[20];SourceRefNo@1003 : Integer);
    BEGIN
      IF TestMode THEN BEGIN
        ErrorCounter := ErrorCounter + 1;
        TempErrorBuf.INIT;
        TempErrorBuf."Error No." := ErrorCounter;
        TempErrorBuf."Error Text" := COPYSTR(Text,1,MAXSTRLEN(TempErrorBuf."Error Text"));
        TempErrorBuf."Source Table" := SourceTable;
        TempErrorBuf."Source No." := SourceNo;
        TempErrorBuf."Source Ref. No." := SourceRefNo;
        TempErrorBuf.INSERT;
      END ELSE
        ERROR(Text);
    END;

    BEGIN
    END.
  }
}

