OBJECT Table 12013590 Exflow Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF510000;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ExFPurchDocHeader@1100285000 : Record 12013587;
             BEGIN
               ExFPurchDocHeader.RESET;
               ExFPurchDocHeader.SETRANGE("Journal Template Name", "Journal Template Name");
               ExFPurchDocHeader.SETRANGE("Journal Batch Name", Name);
               ExFPurchDocHeader.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=ExFlow Journal Batch;
               SVE=ExFlowjournal];
    LookupPageID=Page12013596;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Exflow Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              SVE=Mallnamn f”r journal];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              SVE=Namn];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
    { 6   ;   ;Predefined Account  ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Predefined Account");

                                                                IF "Predefined Account" <> '' THEN
                                                                  "Predefined Purch. Code" := '';
                                                              END;

                                                   CaptionML=[ENU=Predefined Account;
                                                              SVE=Generellt f”rslagskonto] }
    { 12  ;   ;Invoice Nos.        ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Invoice Nos.;
                                                              SVE=Fakturanr-serie] }
    { 13  ;   ;Posted Invoice Nos. ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Posted Invoice Nos.;
                                                              SVE=Fakturanr-serie (bokf”rd)] }
    { 14  ;   ;Credit Memo Nos.    ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Credit Memo Nos.;
                                                              SVE=Kreditnotanr-serie] }
    { 15  ;   ;Posted Credit Memo Nos.;Code10     ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Posted Credit Memo Nos.;
                                                              SVE=Kreditnotanr-serie (bokf”rd)] }
    { 100 ;   ;No. of Documents    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Exflow Purch. Document Header" WHERE (Journal Template Name=FIELD(Journal Template Name),
                                                                                                            Journal Batch Name=FIELD(Name)));
                                                   CaptionML=[ENU=No. of Documents;
                                                              SVE=Antal dokument];
                                                   Editable=No }
    { 12013587;;Import Type        ;Option        ;OnValidate=BEGIN
                                                                IF ("Import Type" = "Import Type"::RSO) OR ("Import Type" = "Import Type"::WEBService) THEN BEGIN
                                                                  "Use Import folder filter" := TRUE;
                                                                  "OCR Import Folder" := 'STREAM';
                                                                END ELSE IF "OCR Import Folder" = 'STREAM' THEN BEGIN
                                                                  "Use Import folder filter" := FALSE;
                                                                  "OCR Import Folder" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Import Type;
                                                              SVE=Import typ];
                                                   OptionCaptionML=[ENU=None,Image File,XML File,ReadSoft Online,WebService;
                                                                    SVE=Ingen,bildfil,XML-fil,ReadSoft Online,Webbtj„nst];
                                                   OptionString=None,Image File,XML File,RSO,WEBService }
    { 12013588;;Image Import Folder;Text250       ;OnValidate=BEGIN
                                                                IF "Image Import Folder" <> '' THEN BEGIN
                                                                  TESTFIELD("Import Type","Import Type"::"Image File");
                                                                  EndingSlash("Image Import Folder");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Image Import Folder;
                                                              SVE=Import Katalog PDF bilder] }
    { 12013589;;OCR Import Folder  ;Text250       ;OnValidate=BEGIN
                                                                IF "OCR Import Folder" <> '' THEN BEGIN
                                                                  TESTFIELD("Import Type","Import Type"::"XML File");
                                                                  EndingSlash("OCR Import Folder");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=OCR Import Folder;
                                                              SVE=OCR Import Katalog] }
    { 12013590;;Default Document Type;Option      ;CaptionML=[ENU=Default Document Type;
                                                              SVE=Standard dokument typ];
                                                   OptionCaptionML=[ENU=" ,Invoice,Credit Memo";
                                                                    SVE=" ,Faktura,Kreditnota"];
                                                   OptionString=[ ,Invoice,Credit Memo] }
    { 12013591;;User ID            ;Code100       ;CaptionML=[ENU=User ID Filter (OCR);
                                                              SVE=Anv„ndar ID Filter (OCR)] }
    { 12013595;;Only PO Matched Documents;Boolean ;CaptionML=[ENU=Only Purchase Order Matched Documents;
                                                              SVE=Enbart Ink”psorder matchade dokument] }
    { 12013600;;Automatically Create Documents;Boolean;
                                                   CaptionML=[ENU=Automatically Create Documents;
                                                              SVE=Skapa dokumenten automatiskt] }
    { 12013621;;Predefined Purch. Code;Code10     ;TableRelation="EX Standard Purchase Code".Code;
                                                   OnValidate=BEGIN
                                                                IF "Predefined Purch. Code" <> '' THEN
                                                                  "Predefined Account" := '';
                                                              END;

                                                   CaptionML=[ENU=Predefined ExFlow Purch. Code;
                                                              SVE=Generell ExFlow f”rslagskod] }
    { 12013622;;Diffline Purch. Code;Code10       ;TableRelation="EX Standard Purchase Code".Code;
                                                   CaptionML=[ENU=Diff.Line Purch. Code;
                                                              SVE=Diffrad f”rslagskod] }
    { 12013637;;OCR Import Type    ;Option        ;InitValue=XMLPort;
                                                   OnValidate=BEGIN
                                                                IF "OCR Import Type" <> xRec."OCR Import Type" THEN BEGIN
                                                                  "OCR Import Port No." := 0;
                                                                  "OCR Import File Suffix" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=OCR Import Type;
                                                              SVE=OCR Importtyp];
                                                   OptionCaptionML=[ENU=,,,,Dataport,,XMLPort;
                                                                    SVE=,,,,Dataport,,XMLPort];
                                                   OptionString=,,,,Dataport,,XMLPort }
    { 12013638;;OCR Import Port No.;Integer       ;TableRelation=Object.ID WHERE (Type=FIELD(OCR Import Type));
                                                   CaptionML=[ENU=OCR XML Port No.;
                                                              SVE=OCR XML portnr] }
    { 12013639;;OCR Import File Suffix;Text10     ;CaptionML=[ENU=OCR Import File Suffix;
                                                              SVE=OCR Importtyp „ndelse] }
    { 12013640;;Use Import folder filter;Boolean  ;OnValidate=BEGIN
                                                                IF NOT "Use Import folder filter" THEN
                                                                  EXIT;

                                                                CASE "Import Type" OF
                                                                  "Import Type"::"Image File" : TESTFIELD("Image Import Folder");
                                                                  "Import Type"::None         : ERROR(text001, FIELDCAPTION("Use Import folder filter"), FIELDCAPTION("Import Type"), "Import Type"::"Image File", "Import Type"::"XML File");
                                                                  "Import Type"::RSO          : ERROR(text001, FIELDCAPTION("Use Import folder filter"), FIELDCAPTION("Import Type"), "Import Type"::"Image File", "Import Type"::"XML File");
                                                                  "Import Type"::WEBService   : ERROR(text001, FIELDCAPTION("Use Import folder filter"), FIELDCAPTION("Import Type"), "Import Type"::"Image File", "Import Type"::"XML File");
                                                                  "Import Type"::"XML File"   : TESTFIELD("OCR Import Folder");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Use import folder as filter;
                                                              SVE=Use import folder as filter] }
    { 12013643;;RSO download error ;Boolean       ;CaptionML=[ENU=ReadSoft Online batch download errors;
                                                              SVE=ReadSoft Online batch nedladdningsfel];
                                                   Editable=No }
    { 12013644;;Verify at import   ;Option        ;CaptionML=[ENU=Verify documents at import;
                                                              SVE=Verifiera dokument vid import];
                                                   OptionCaptionML=[ENU=New documents only,All documents;
                                                                    SVE=Endast nya dokument,Alla dokument];
                                                   OptionString=NewDocs,Always }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      text001@1100285000 : TextConst 'ENU=Can only use %1 for %2: %3 and %4;SVE=Kan endast anv„nda %1 f”r %2: %3 och %4';

    PROCEDURE SetupNewBatch@1();
    VAR
      ExFlowSetup@1100285000 : Record 12013601;
    BEGIN
      "Default Document Type" := "Default Document Type"::Invoice;

      ExFlowSetup.GET;
      VALIDATE("Import Type",ExFlowSetup."Default Import Type");
    END;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    PROCEDURE EndingSlash@1000000000(VAR Path@1000000000 : Text[250]);
    BEGIN
      IF Path <> '' THEN
        IF Path[STRLEN(Path)] <> '\' THEN
          Path := Path + '\';
    END;

    BEGIN
    END.
  }
}

