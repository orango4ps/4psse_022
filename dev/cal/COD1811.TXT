OBJECT Codeunit 1811 Excel Post Processor
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    TableNo=8614;
    OnRun=BEGIN
            CASE "Table ID" OF
              DATABASE::Item:
                PostProcessItem(Rec);
              ELSE
                EXIT;
            END;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE PostProcessItem@1(ConfigPackageRecord@1004 : Record 8614);
    VAR
      Item@1002 : Record 27;
      AdjustItemInventory@1005 : Codeunit 1327;
      QuantityOnInventory@1000 : Decimal;
      ErrorText@1006 : Text;
    BEGIN
      IF NOT FindItemAndGetInventory(ConfigPackageRecord,Item,QuantityOnInventory) THEN
        EXIT;

      IF (Item."Base Unit of Measure" = '') OR
         (Item."Gen. Prod. Posting Group" = '') OR
         (Item."Inventory Posting Group" = '')
      THEN
        EXIT;

      ErrorText := AdjustItemInventory.PostAdjustmentToItemLedger(Item,QuantityOnInventory);
      IF ErrorText <> '' THEN
        ERROR(ErrorText);
    END;

    LOCAL PROCEDURE FindItemAndGetInventory@2(ConfigPackageRecord@1000 : Record 8614;VAR Item@1001 : Record 27;VAR QuantityOnInventory@1002 : Decimal) : Boolean;
    VAR
      ConfigPackageData@1003 : Record 8615;
    BEGIN
      WITH ConfigPackageRecord DO BEGIN
        ConfigPackageData.GET("Package Code","Table ID","No.",Item.FIELDNO("No."));
        IF NOT Item.GET(ConfigPackageData.Value) THEN
          EXIT(FALSE);
        IF NOT ConfigPackageData.GET("Package Code","Table ID","No.",Item.FIELDNO(Inventory)) THEN
          EXIT(FALSE);
        IF NOT EVALUATE(QuantityOnInventory,ConfigPackageData.Value) THEN
          EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

