OBJECT Table 11126005 Accident QA
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 QASetup.GET;
                 QASetup.TESTFIELD("Accident Nos.");
                 NoSeriesMgt.InitSeries(QASetup."Accident Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               InitRecord();

               ProjectSiteMgt.LogAccidentQA(1, Rec); //**4PS.n
             END;

    OnModify=BEGIN
               IF "E-Mail Sent to QA" AND ("QA Employee" <> xRec."QA Employee") THEN
                 "E-Mail Sent to QA" := FALSE;
               //CheckAutomMailToQAEmployee(); //mg.o, 19-07-11: C-029342 Moved to OnTimer in form due to NAV client crashes

               ProjectSiteMgt.LogAccidentQA(2, Rec); //**4PS.n
             END;

    OnDelete=VAR
               AccidentWitness@1100525000 : Record 11126006;
               AccidentText@1100525001 : Record 11126007;
               AccSelectedOption@1100525002 : Record 11126009;
             BEGIN
               AccidentWitness.SETRANGE("Accident No.", "No.");
               AccidentWitness.DELETEALL;

               AccidentText.SETRANGE("Accident No.", "No.");
               AccidentText.DELETEALL;

               AccSelectedOption.SETRANGE("Accident No.", "No.");
               AccSelectedOption.DELETEALL;

               ProjectSiteMgt.LogAccidentQA(3, Rec); //**4PS.n
             END;

    OnRename=BEGIN
               IF (Status = Status::Finished) OR HasDocumentLinks() THEN
                 ERROR(Text000);
             END;

    CaptionML=[ENU=Accident (QA);
               NOR=Ulykke (QA);
               SVE=Olycksfall (kvalitetssÑkring)];
    LookupPageID=Page11126006;
    DrillDownPageID=Page11126006;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  QASetup.GET;
                                                                  NoSeriesMgt.TestManual(QASetup."Accident Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Description] }
    { 30  ;   ;Status              ;Option        ;InitValue=Received;
                                                   OnValidate=BEGIN
                                                                IF Status <> xRec.Status THEN
                                                                  "Status Date" := TODAY;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=,Received,,,In Process,,,Finished;
                                                                    NOR=,Mottatt,,,Under prosessering,,,Avsluttet;
                                                                    SVE=,Mottagen,,,Under behandling,,,Avslutad];
                                                   OptionString=,Received,,,InProcess,,,Finished }
    { 35  ;   ;Status Date         ;Date          ;CaptionML=[ENU=Status Date;
                                                              NOR=Statusdato;
                                                              SVE=Status datum] }
    { 40  ;   ;Source Type         ;Option        ;InitValue=Project;
                                                   OnValidate=BEGIN
                                                                IF "No." <> '' THEN //C019513
                                                                  IF "Source Type" <> xRec."Source Type" THEN BEGIN
                                                                    "Source No." := '';
                                                                    "Project Element" := '';
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=" ,Project,Service Order";
                                                                    NOR=" ,Prosjekt,Serviceordre";
                                                                    SVE=" ,Projekt,Serviceorder"];
                                                   OptionString=[ ,Project,ServiceOrder] }
    { 45  ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Project)) Job
                                                                 ELSE IF (Source Type=CONST(ServiceOrder)) "Service Order";
                                                   OnValidate=BEGIN
                                                                IF "Source No." <> '' THEN BEGIN
                                                                  TESTFIELD("Source Type");
                                                                  IF "Source No." <> xRec."Source No." THEN BEGIN
                                                                    GetDefaultResponsiblePersons;
                                                                    "Project Element" := '';
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Source No.;
                                                              NOR=Kildenr.;
                                                              SVE=Ursprungsnr] }
    { 50  ;   ;Employee Company    ;Text30        ;TableRelation=Company;
                                                   OnValidate=BEGIN
                                                                IF "Employee Company" <> xRec."Employee Company" THEN
                                                                  "Employee No." := '';
                                                              END;

                                                   CaptionML=[ENU=Employee Company;
                                                              NOR=Ansattes selskap;
                                                              SVE=Den anstÑlldes fîretag] }
    { 55  ;   ;Employee No.        ;Code20        ;OnValidate=BEGIN
                                                                IF "Employee No." <> '' THEN BEGIN
                                                                  TESTFIELD("Employee Company");
                                                                  IF "Employee Company" <> COMPANYNAME THEN
                                                                    Employee.CHANGECOMPANY("Employee Company");
                                                                  IF NOT Employee.GET("Employee No.") THEN
                                                                    ERROR(Text001, FIELDCAPTION("Employee No."), "Employee No.", "Employee Company");
                                                                  IF "Employee No." <> xRec."Employee No." THEN
                                                                    FillEmployeeData(Employee);
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupEmployee();
                                                            END;

                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=AnstÑllningsnr] }
    { 60  ;   ;Name                ;Text30        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 65  ;   ;Address             ;Text100       ;OnValidate=VAR
                                                                DummyFaxNo@1100525000 : Text[30];
                                                              BEGIN
                                                              END;

                                                   CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress] }
    { 66  ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2;
                                                              SVE=Adress 2] }
    { 70  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr.;
                                                              SVE=Postnr] }
    { 71  ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              NOR=Sted;
                                                              SVE=Ort] }
    { 73  ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              NOR=Fylke/delstat;
                                                              SVE=Delstat];
                                                   Editable=Yes }
    { 75  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country Code;
                                                              NOR=Landskode;
                                                              SVE=Landskod] }
    { 80  ;   ;Nationality         ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Nationality;
                                                              NOR=Nasjonalitet;
                                                              SVE=Nationalitet] }
    { 90  ;   ;Birth Date          ;Date          ;CaptionML=[ENU=Birth Date;
                                                              NOR=Fõdselsdato;
                                                              SVE=Fîdelsedatum] }
    { 100 ;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              NOR=Telefonnr;
                                                              SVE=Telefonnr] }
    { 110 ;   ;Employer Name       ;Text100       ;CaptionML=[ENU=Employer;
                                                              NOR=Arbeidsgiver;
                                                              SVE=Arbetsgivare] }
    { 120 ;   ;Job Title           ;Text50        ;CaptionML=[ENU=Project Title;
                                                              NOR=Stilling;
                                                              SVE=Yrkestitel] }
    { 130 ;   ;Employment Date     ;Date          ;CaptionML=[ENU=Employment Date;
                                                              NOR=Ansettelsesdato;
                                                              SVE=AnstÑllningsdatum] }
    { 200 ;   ;Date/Time           ;DateTime      ;CaptionML=[ENU=Date/Time;
                                                              NOR=Dato/tid;
                                                              SVE=Datum/tid] }
    { 210 ;   ;First Aid by        ;Text50        ;CaptionML=[ENU=First Aid by;
                                                              NOR=Fõrstehjelp av;
                                                              SVE=Fîrsta hjÑlpen frÜn] }
    { 215 ;   ;Medical Treatment by;Text50        ;CaptionML=[ENU=Medical Treatment by;
                                                              NOR=Medisinsk behandling av;
                                                              SVE=Medicinsk behandling av] }
    { 220 ;   ;Other Company Involved;Boolean     ;OnValidate=BEGIN
                                                                IF NOT "Other Company Involved" THEN BEGIN
                                                                  TESTFIELD("Other Company", '');
                                                                  TESTFIELD("Involved in which Way", '');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Other Company Involved;
                                                              NOR=Annet selskap involvert;
                                                              SVE=Annat fîretag inblandat] }
    { 221 ;   ;Other Company       ;Text50        ;OnValidate=BEGIN
                                                                IF "Other Company" <> '' THEN
                                                                  TESTFIELD("Other Company Involved", TRUE);
                                                              END;

                                                   CaptionML=[ENU=Other Company;
                                                              NOR=Annet selskap;
                                                              SVE=Annat fîretag] }
    { 225 ;   ;Involved in which Way;Text50       ;OnValidate=BEGIN
                                                                IF "Involved in which Way" <> '' THEN
                                                                  TESTFIELD("Other Company Involved", TRUE);
                                                              END;

                                                   CaptionML=[ENU=Involved in which Way;
                                                              NOR=Involvert pÜ hvilken mÜte;
                                                              SVE=Involverad pÜ valfritt sÑtt] }
    { 230 ;   ;Material Damage     ;Boolean       ;CaptionML=[ENU=Material Damage;
                                                              NOR=Materielle skader;
                                                              SVE=Materialskada] }
    { 235 ;   ;Estimated Damage Amount;Decimal    ;CaptionML=[ENU=Estimated Damage Amount;
                                                              NOR=Estimert skadeomfang;
                                                              SVE=Kalkylerat skadebelopp];
                                                   AutoFormatType=1 }
    { 240 ;   ;Hospital Admission  ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Hospital Admission" THEN
                                                                  TESTFIELD(Hospital, '');
                                                              END;

                                                   CaptionML=[ENU=Hospital Admission;
                                                              NOR=Sykehus innleggelse;
                                                              SVE=InlÑggning pÜ sjukhus] }
    { 245 ;   ;Hospital            ;Text50        ;OnValidate=BEGIN
                                                                IF Hospital <> '' THEN
                                                                  TESTFIELD("Hospital Admission", TRUE);
                                                              END;

                                                   CaptionML=[ENU=Hospital;
                                                              NOR=Sykehus;
                                                              SVE=Sjukhus] }
    { 250 ;   ;Job Type during Accident;Text50    ;CaptionML=[ENU=Job Type during Accident;
                                                              NOR=Jobbtype under ulykke;
                                                              SVE=Jobbtyp under olycka] }
    { 260 ;   ;Extra Info Injury   ;Text50        ;CaptionML=[ENU=Extra Info Injury;
                                                              NOR=Ekstra infomasjon om skade;
                                                              SVE=Extra skadeinformation] }
    { 270 ;   ;Absence Days        ;Decimal       ;CaptionML=[ENU=Absence Days;
                                                              NOR=Fravërsdager;
                                                              SVE=FrÜnvarodagar] }
    { 300 ;   ;Reason Modify Risk Inventory;Boolean;
                                                   CaptionML=[ENU=Reason Modify Risk Inventory;
                                                              NOR=èrsak for endring av risikolager;
                                                              SVE=Orsak fîr Ñndring av risklager] }
    { 305 ;   ;Reason Process Task Risk Anal.;Boolean;
                                                   CaptionML=[ENU=Reason Process Task Risk Analysis;
                                                              NOR=èrsak for prosessoppgave risikoanalyse;
                                                              SVE=Orsak fîr riskanalys av processuppgift] }
    { 400 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serie] }
    { 410 ;   ;Site Manager        ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=Site Manager;
                                                              NOR=Prosjektleder;
                                                              SVE=Arbetschef] }
    { 420 ;   ;Project Manager     ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=Project Manager;
                                                              NOR=Prosjektleder;
                                                              SVE=Platschef] }
    { 430 ;   ;QA Employee         ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=QA Employee;
                                                              NOR=Kvalitetssikringsansatt;
                                                              SVE=KvalitetssÑkringsanstÑlld] }
    { 440 ;   ;E-Mail Sent to QA   ;Boolean       ;CaptionML=ENU=Email Sent to QA;
                                                   Editable=No }
    { 450 ;   ;Project Element     ;Code20        ;TableRelation=IF (Source Type=CONST(Project)) "Project Element".Element WHERE (Project No.=FIELD(Source No.));
                                                   OnValidate=BEGIN
                                                                IF "Project Element" <> '' THEN
                                                                  TESTFIELD("Source Type", "Source Type"::Project);
                                                              END;

                                                   CaptionML=[ENU=Project Element;
                                                              NOR=Prosjektelement;
                                                              SVE=Projektelement] }
    { 460 ;   ;No. of Witnesses    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Accident Witness" WHERE (Accident No.=FIELD(No.)));
                                                   CaptionML=[ENU=No. of Witnesses;
                                                              NOR=Antall vitner;
                                                              SVE=Antal vittnen];
                                                   Editable=No }
    { 470 ;   ;Description Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Accident Text Line" WHERE (Accident No.=FIELD(No.),
                                                                                                 Type=CONST(Description)));
                                                   CaptionML=[ENU=Description Text Lines Exist;
                                                              NOR=Tekstrader for beskrivelse eksisterer;
                                                              SVE=Beskrivning textrader finns];
                                                   Editable=No }
    { 480 ;   ;Env. Damage Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Accident Text Line" WHERE (Accident No.=FIELD(No.),
                                                                                                 Type=CONST(EnvironmentDamage)));
                                                   CaptionML=[ENU=Env. Damage Text Lines Exist;
                                                              NOR=Miljõskadetekstlinjer eksisterer;
                                                              SVE=Miljî Skada textrader finns];
                                                   Editable=No }
    { 490 ;   ;Prev. Measure Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Accident Text Line" WHERE (Accident No.=FIELD(No.),
                                                                                                 Type=CONST(PreventiveMeasures)));
                                                   CaptionML=[ENU=Prev. Measure Text Lines Exist;
                                                              NOR=Tekstrader for preventive tiltak eksisterer;
                                                              SVE=Fîreg. MÑtning textrader finns];
                                                   Editable=No }
    { 500 ;   ;Facts Victim Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Accident Text Line" WHERE (Accident No.=FIELD(No.),
                                                                                                 Type=CONST(FactsVictim)));
                                                   CaptionML=[ENU=Facts Victim Text Lines Exist;
                                                              NOR=Offerfaktatekstrader eksisterer;
                                                              SVE=Fakta offer, textrader finns];
                                                   Editable=No }
    { 510 ;   ;Explanation Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Accident Text Line" WHERE (Accident No.=FIELD(No.),
                                                                                                 Type=CONST(Explanation)));
                                                   CaptionML=[ENU=Explanation Text Lines Exist;
                                                              NOR=Forklaringstekstrader eksisterer;
                                                              SVE=Fîrklaring textrader finns];
                                                   Editable=No }
    { 520 ;   ;Prop. Measure Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Accident Text Line" WHERE (Accident No.=FIELD(No.),
                                                                                                 Type=CONST(ProposedMeasures)));
                                                   CaptionML=[ENU=Prop. Measure Text Lines Exist;
                                                              NOR=Forslag til mÜling, tekstrader eksisterer;
                                                              SVE=Fîrslag MÑtning textrader finns];
                                                   Editable=No }
    { 530 ;   ;Judgement Text Lines Exist;Boolean ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Accident Text Line" WHERE (Accident No.=FIELD(No.),
                                                                                                 Type=CONST(Judgement)));
                                                   CaptionML=[ENU=Judgement Text Lines Exist;
                                                              NOR=Vurdering tekstrader eksisterer;
                                                              SVE=VÑrdering textrader finns];
                                                   Editable=No }
    { 540 ;   ;Foll.-up Info Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Accident Text Line" WHERE (Accident No.=FIELD(No.),
                                                                                                 Type=CONST(FolowupInfo)));
                                                   CaptionML=[ENU=Foll.-up Info Text Lines Exist;
                                                              NOR=Oppfõlgingsinfo. Tekstrader eksisterer;
                                                              SVE=Uppfîljning, textrader finns];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Source No.                               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      QASetup@1100525003 : Record 11126001;
      Employee@1100525000 : Record 5200;
      Text000@1100525006 : TextConst 'ENU=Rename not allowed.;NOR=Ikke tillatt Ü bytte navn.;SVE=Det Ñr inte tillÜtet att byta namn.';
      Text001@1100525001 : TextConst 'ENU=%1 ''%2'' niet present in company ''%3''.;NOR=%1 Ô%2Ô ikke tilstede i selskap ''%3''.;SVE=%1 ''%2'' finns inte i fîretaget ''%3''.';
      PostCode@1100525004 : Record 225;
      NoSeriesMgt@1100525005 : Codeunit 396;
      ProjectSiteMgt@1100525007 : Codeunit 11012282;

    PROCEDURE InitRecord@1100525001();
    BEGIN
      QASetup.GET;
      Status := Status::Received;
      "Status Date" := TODAY;
      "Employee Company" := COMPANYNAME;
      "QA Employee" := QASetup."Default QA Employee";
      "E-Mail Sent to QA" := FALSE;
    END;

    PROCEDURE AssistEdit@1100525003(OldAccident@1100525000 : Record 11126005) : Boolean;
    VAR
      Accident@1100525001 : Record 11126005;
    BEGIN
      WITH Accident DO BEGIN
        Accident := Rec;
        QASetup.GET;
        QASetup.TESTFIELD("Accident Nos.");
        IF NoSeriesMgt.SelectSeries(QASetup."Accident Nos.", OldAccident."No. Series", "No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := Accident;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE FillEmployeeData@1100525000(Empl@1100525002 : Record 5200);
    VAR
      Employer@1100525000 : Record 11012013;
      Vend@1100525001 : Record 23;
    BEGIN
      Name := COPYSTR(Empl."Full Name", 1, MAXSTRLEN(Name));
      Address := Empl.Address;
      "Address 2" := Empl."Address 2";
      "Post Code" := Empl."Post Code";
      City := Empl.City;
      "Country/Region Code" := Empl."Country/Region Code";
      "Birth Date" := Empl."Birth Date";
      "Phone No." := Empl."Phone No.";
      "Job Title" := Empl."Job Title";
      "Employment Date" := Empl."Employment Date";

      "Employer Name" := '';
      IF Empl.Employer <> '' THEN BEGIN
        IF NOT Empl.External THEN BEGIN
          IF Employer.GET(Empl.Employer) THEN
            "Employer Name" := Employer.Description;
        END ELSE BEGIN
          IF Vend.GET(Empl.Employer) THEN
            "Employer Name" := Vend.Name;
        END;
      END
    END;

    PROCEDURE LookupEmployee@1100525005();
    VAR
      Empl@1100525000 : Record 5200;
    BEGIN
      TESTFIELD("Employee Company");
      IF "Employee Company" <> COMPANYNAME THEN
        Empl.CHANGECOMPANY("Employee Company");
      Empl."No." := "Employee No.";
      IF PAGE.RUNMODAL(0, Empl) = ACTION::LookupOK THEN BEGIN
        IF (Empl."No." <> '') AND (Empl."No." <> "Employee No.") THEN
          VALIDATE("Employee No.", Empl."No.");
      END;
    END;

    PROCEDURE GetSourceNoCaption@1100525002() : Text[100];
    BEGIN
      IF "Source Type" = "Source Type"::" " THEN
        EXIT(FIELDCAPTION("Source No."));
      EXIT(FORMAT("Source Type"));
    END;

    PROCEDURE GetSourceDescription@1100525004() : Text[100];
    VAR
      Proj@1100525000 : Record 11072003;
      ServOrder@1100525001 : Record 11012823;
    BEGIN
      IF "Source No." <> '' THEN BEGIN
        CASE "Source Type" OF
          "Source Type"::Project:
            BEGIN
              IF Proj.GET("Source No.") THEN
                EXIT(Proj.Description);
            END;
          "Source Type"::ServiceOrder:
            BEGIN
              IF ServOrder.GET("Source No.") THEN
                EXIT(ServOrder.Description);
            END;

        END;
      END;
      EXIT('');
    END;

    PROCEDURE HasDocumentLinks@1100525006() : Boolean;
    VAR
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
      RecRef@1100525001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      EXIT(DocumentLinkMgt.EntityHasDocumentLinks(RecRef));
    END;

    PROCEDURE GetDefaultResponsiblePersons@1100525008();
    VAR
      Proj@1100525000 : Record 11072003;
      ProjRespPerson@1100525001 : Record 11012040;
    BEGIN
      QASetup.GET;
      IF "Source No." <> '' THEN BEGIN
        CASE "Source Type" OF
          "Source Type"::Project:
            BEGIN
              IF Proj.GET("Source No.") THEN BEGIN
                "Site Manager" := Proj."Site Manager";
                "Project Manager" := Proj."Project Manager";
                ProjRespPerson.SETRANGE("Project No.", "Source No.");
                ProjRespPerson.SETRANGE(Responsibility, QASetup."Responsibility QA");
                IF ProjRespPerson.FINDFIRST THEN
                  "QA Employee" := ProjRespPerson."Employee No.";
              END;
            END;
        END;
      END;
      IF "QA Employee" = '' THEN
        "QA Employee" := QASetup."Default QA Employee";
    END;

    PROCEDURE GetNameResponsible@1100525007(EmplNo@1100525002 : Code[20]) : Text[100];
    VAR
      Empl@1100525001 : Record 5200;
    BEGIN
      IF EmplNo <> '' THEN BEGIN
        IF Empl.GET(EmplNo) THEN
          EXIT(Empl."Full Name");
      END;
      EXIT('');
    END;

    PROCEDURE CheckAutomMailToQAEmployee@1100525010();
    BEGIN
      IF "E-Mail Sent to QA" THEN
        EXIT;
      IF ("No." = '') OR (Name = '') OR ("QA Employee" = '') OR (Description = '') OR ("Date/Time"  = 0DT) THEN
        EXIT;
      QASetup.GET;
      IF NOT QASetup."Send E-Mail to QA (Accident)" THEN
        EXIT;
      SendMailToQA();
    END;

    PROCEDURE SendMailToQA@1100525009();
    BEGIN
      IF CODEUNIT.RUN(CODEUNIT::"Accident Mail Management", Rec) THEN BEGIN
        IF NOT "E-Mail Sent to QA" THEN BEGIN
          "E-Mail Sent to QA" := TRUE;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE GetContact@1210190000(VAR Contact@1210190000 : Record 5050;GiveMessage@1100528700 : Boolean) : Boolean;
    VAR
      Job@1210190002 : Record 11072003;
      ServiceOrder@1210190001 : Record 11012823;
    BEGIN
      CASE "Source Type" OF
        "Source Type"::Project:
          BEGIN
            IF Job.GET("Source No.") THEN
              EXIT(Contact.GetContactByCustomer(Job."Bill-to Customer No.", Contact, GiveMessage));
          END;
        "Source Type"::ServiceOrder:
          BEGIN
            IF ServiceOrder.GET("Source No.") THEN
              EXIT(Contact.GetContactByCustomer(ServiceOrder."Customer No.", Contact, GiveMessage));
          END;
      END;
    END;

    PROCEDURE EditDocuments@1100528702(ContactNo@1210190003 : Code[20];ContactPersonNo@1210190004 : Code[20];SendMail@1210190002 : Boolean;Recipient@1100528700 : Text[120]);
    VAR
      Job@1210190005 : Record 11072003;
      ServiceOrder@1210190006 : Record 11012823;
      DocumentTemplate@1210190001 : Record 11012401;
      DocumentMailing@1210190000 : Codeunit 11012278;
      RecRef@1100525000 : RecordRef;
      DocumentLinkRIDs@1100525001 : ARRAY [5] OF RecordID;
    BEGIN
      RecRef.GETTABLE(Rec);
      DocumentLinkRIDs[1] := RecRef.RECORDID;

      CASE "Source Type" OF
        "Source Type"::Project:
          BEGIN
            IF Job.GET("Source No.") THEN BEGIN
              RecRef.GETTABLE(Job);
              DocumentLinkRIDs[2] := RecRef.RECORDID;
              DocumentTemplate.Type := DocumentTemplate.Type::Job;
            END;
          END;
        "Source Type"::ServiceOrder:
          BEGIN
            IF ServiceOrder.GET("Source No.") THEN BEGIN
              RecRef.GETTABLE(ServiceOrder);
              DocumentLinkRIDs[2] := RecRef.RECORDID;
              DocumentTemplate.Type := DocumentTemplate.Type::"Service Order";
            END;
          END;
      END;

      DocumentMailing.EditDocuments(
        STRSUBSTNO('%1', DocumentTemplate.Type), DocumentLinkRIDs, '', ContactNo, ContactPersonNo, SendMail, Recipient);
    END;

    BEGIN
    END.
  }
}

