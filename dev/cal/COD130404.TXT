OBJECT Codeunit 130404 CAL Test Project Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FileMgt@1000 : Codeunit 419;
      FileDialogFilterTxt@1001 : TextConst '@@@={Locked};ENU=Test Project file (*.xml)|*.xml|All Files (*.*)|*.*;NLD=Test Project file (*.xml)|*.xml|All Files (*.*)|*.*';
      XMLDOMMgt@1002 : Codeunit 6224;

    PROCEDURE Export@10(CALTestSuiteName@1007 : Code[10]) : Boolean;
    VAR
      CALTestSuite@1006 : Record 130400;
      CALTestLine@1001 : Record 130401;
      ProjectXML@1101 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      DocumentElement@1005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TestNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLDataFile@1000 : Text;
      FileFilter@1002 : Text;
      ToFile@1003 : Text;
    BEGIN
      XMLDOMMgt.LoadXMLDocumentFromText(
        STRSUBSTNO(
          '<?xml version="1.0" encoding="UTF-16" standalone="yes"?><%1></%1>','CALTests'),
        ProjectXML);

      CALTestSuite.GET(CALTestSuiteName);
      DocumentElement := ProjectXML.DocumentElement;
      XMLDOMMgt.AddAttribute(DocumentElement,CALTestSuite.FIELDNAME(Name),CALTestSuite.Name);
      XMLDOMMgt.AddAttribute(DocumentElement,CALTestSuite.FIELDNAME(Description),CALTestSuite.Description);

      CALTestLine.SETRANGE("Test Suite",CALTestSuite.Name);
      CALTestLine.SETRANGE("Line Type",CALTestLine."Line Type"::Codeunit);
      IF CALTestLine.FINDSET THEN
        REPEAT
          TestNode := ProjectXML.CreateElement('Codeunit');
          XMLDOMMgt.AddAttribute(TestNode,'ID',FORMAT(CALTestLine."Test Codeunit"));
          DocumentElement.AppendChild(TestNode);
        UNTIL CALTestLine.NEXT = 0;

      XMLDataFile := FileMgt.ServerTempFileName('');
      FileMgt.IsAllowedPath(XMLDataFile,FALSE);
      FileFilter := GetFileDialogFilter;
      ToFile := 'PROJECT.xml';
      ProjectXML.Save(XMLDataFile);

      FileMgt.DownloadHandler(XMLDataFile,'Download','',FileFilter,ToFile);

      EXIT(TRUE);
    END;

    PROCEDURE Import@1();
    VAR
      CALTestSuite@1004 : Record 130400;
      AllObjWithCaption@1010 : Record 2000000058;
      CALTestManagement@1011 : Codeunit 130401;
      ProjectXML@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      DocumentElement@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TestNode@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TestNodes@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      ServerFileName@1000 : Text;
      NodeCount@1007 : Integer;
      TestID@1008 : Integer;
    BEGIN
      ServerFileName := FileMgt.ServerTempFileName('.xml');
      FileMgt.IsAllowedPath(ServerFileName,FALSE);
      IF UploadXMLPackage(ServerFileName) THEN BEGIN
        XMLDOMMgt.LoadXMLDocumentFromFile(ServerFileName,ProjectXML);
        DocumentElement := ProjectXML.DocumentElement;

        CALTestSuite.Name :=
          COPYSTR(
            GetAttribute(GetElementName(CALTestSuite.FIELDNAME(Name)),DocumentElement),1,
            MAXSTRLEN(CALTestSuite.Name));
        CALTestSuite.Description :=
          COPYSTR(
            GetAttribute(GetElementName(CALTestSuite.FIELDNAME(Description)),DocumentElement),1,
            MAXSTRLEN(CALTestSuite.Description));
        IF NOT CALTestSuite.GET(CALTestSuite.Name) THEN
          CALTestSuite.INSERT;

        TestNodes := DocumentElement.ChildNodes;
        FOR NodeCount := 0 TO (TestNodes.Count - 1) DO BEGIN
          TestNode := TestNodes.Item(NodeCount);
          IF EVALUATE(TestID,FORMAT(GetAttribute('ID',TestNode))) THEN BEGIN
            AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Codeunit);
            AllObjWithCaption.SETRANGE("Object ID",TestID);
            CALTestManagement.AddTestCodeunits(CALTestSuite,AllObjWithCaption);
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE GetAttribute@2(AttributeName@1001 : Text;VAR XMLNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode") : Text;
    VAR
      XMLAttributes@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamedNodeMap";
      XMLAttributeNode@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      XMLAttributes := XMLNode.Attributes;
      XMLAttributeNode := XMLAttributes.GetNamedItem(AttributeName);
      IF ISNULL(XMLAttributeNode) THEN
        EXIT('');
      EXIT(FORMAT(XMLAttributeNode.InnerText));
    END;

    LOCAL PROCEDURE GetElementName@22(NameIn@1000 : Text) : Text;
    BEGIN
      NameIn := DELCHR(NameIn,'=','Ã¯''`');
      NameIn := CONVERTSTR(NameIn,'<>,./\+&()%:','            ');
      NameIn := CONVERTSTR(NameIn,'-','_');
      NameIn := DELCHR(NameIn,'=',' ');
      IF NameIn[1] IN ['0','1','2','3','4','5','6','7','8','9'] THEN
        NameIn := '_' + NameIn;
      EXIT(NameIn);
    END;

    LOCAL PROCEDURE GetFileDialogFilter@15() : Text;
    BEGIN
      EXIT(FileDialogFilterTxt);
    END;

    LOCAL PROCEDURE UploadXMLPackage@23(ServerFileName@1000 : Text) : Boolean;
    BEGIN
      EXIT(UPLOAD('Import project','',GetFileDialogFilter,'',ServerFileName));
    END;

    BEGIN
    END.
  }
}

