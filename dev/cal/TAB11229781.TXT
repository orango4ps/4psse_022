OBJECT Table 11229781 Estimate Usage
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Estimate Usage;
  }
  FIELDS
  {
    { 10  ;   ;Estimate No.        ;Code20        ;TableRelation=Estimate;
                                                   CaptionML=[ENU=Estimate No.;
                                                              NOR=Kalkylenr;
                                                              SVE=Kalkylnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 15  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Description WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 20  ;   ;Estimate Lines      ;Integer       ;CaptionML=[ENU=Estimate Lines;
                                                              NOR=Kalkylerader;
                                                              SVE=Kalkylrader];
                                                   Editable=No }
    { 30  ;   ;Recipe Lines        ;Integer       ;CaptionML=[ENU=Recipe Lines;
                                                              NOR=Malrader;
                                                              SVE=Mallrader];
                                                   Editable=No }
    { 40  ;   ;Empty Estimate Lines;Integer       ;CaptionML=ENU=Empty Estimate Lines;
                                                   Editable=No }
    { 50  ;   ;Empty Recipe Lines  ;Integer       ;CaptionML=ENU=Empty Recipe Lines;
                                                   Editable=No }
    { 60  ;   ;Empty Estimate Lines %;Decimal     ;CaptionML=ENU=Empty Estimate Lines %;
                                                   Editable=No }
    { 70  ;   ;Empty Recipe Lines %;Decimal       ;CaptionML=ENU=Empty Recipe Lines %;
                                                   Editable=No }
    { 80  ;   ;Total Empty Lines % ;Decimal       ;CaptionML=ENU=Total Empty Lines %;
                                                   Editable=No }
    { 90  ;   ;Label               ;Code10        ;CaptionML=ENU=Lable }
    { 100 ;   ;Status              ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Status WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   Editable=No }
    { 110 ;   ;Creation Date       ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate."Creation Date" WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettet den;
                                                              SVE=Uppl„ggningsdatum];
                                                   Editable=No }
    { 120 ;   ;Responsible Employee;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate."Responsible Employee" WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Responsible Employee;
                                                              NOR=Ansvarlig Ansatt;
                                                              SVE=Ansvarig];
                                                   Editable=No }
    { 130 ;   ;Modified by         ;Code50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate."Modified by" WHERE (No.=FIELD(Estimate No.)));
                                                   OnLookup=VAR
                                                              UserCode@1210190000 : Code[50];
                                                              UserMgt@1210190001 : Codeunit 418;
                                                            BEGIN
                                                              UserCode := "Modified by";
                                                              UserMgt.LookupUserID(UserCode);
                                                            END;

                                                   CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=Žndrad av];
                                                   Editable=No }
    { 140 ;   ;Last Date Modified  ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate."Last Date Modified" WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 150 ;   ;Estimator           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Estimator WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Estimator;
                                                              NOR=Kalkylat›r;
                                                              SVE=Kalkylator];
                                                   Editable=No }
    { 160 ;   ;Name Estimator      ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Estimator)));
                                                   CaptionML=[ENU=Name Estimator;
                                                              NOR=Navn p† Kalkyleansvarlig;
                                                              SVE=Namn p† kalkylansvarig];
                                                   Editable=No }
    { 170 ;   ;Fixed               ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Fixed WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Fixed;
                                                              NOR=Fast;
                                                              SVE=Fast];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Estimate No.                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EstimateSetup@1100525000 : Record 11012150;
      Text001@1100525001 : TextConst 'ENU=Estimate    #1###### @2@@@@@@@@@@@@@;SVE=Kalkyl      #1###### @2@@@@@@@@@@@@@';

    PROCEDURE GetCurrentUsage@1100409000();
    VAR
      Estimate@1100409000 : Record 11012151;
      EstimateLineNew@1100409001 : Record 11072072;
      RecipeLineNew@1100409003 : Record 11072075;
      EstimateUsage@1100528500 : Record 11229781;
      EstimateUsageExist@1100528501 : Boolean;
      Threshold@1100528502 : Decimal;
      NoOfRecords@1100528503 : Integer;
      Counter@1100528504 : Integer;
      Window@1100528505 : Dialog;
    BEGIN
      EstimateSetup.GET;
      Threshold := 0;

      Window.OPEN(Text001);

      Estimate.SETRANGE("RAW Estimate",FALSE);

      NoOfRecords := Estimate.COUNT;
      IF Estimate.FINDSET THEN
        REPEAT
          Counter := Counter + 1;
          Window.UPDATE(1,Estimate."No.");
          Window.UPDATE(2,ROUND(Counter / NoOfRecords * 10000,1));

          EstimateUsageExist := EstimateUsage.GET(Estimate."No.");

          EstimateUsage.INIT;
          EstimateUsage."Estimate No." := Estimate."No.";

          IF Estimate."No." <> EstimateSetup."Estimate Source" THEN BEGIN
            EstimateLineNew.SETRANGE("Estimate No.", Estimate."No.");
            EstimateUsage."Estimate Lines" := EstimateLineNew.COUNT;

            RecipeLineNew.SETRANGE("Estimate No.", Estimate."No.");
            EstimateUsage."Recipe Lines" := RecipeLineNew.COUNT;

            CountZeroLines(Estimate,EstimateUsage);

            IF EstimateUsage."Estimate Lines" <> 0 THEN
              EstimateUsage."Empty Estimate Lines %" := ROUND(EstimateUsage."Empty Estimate Lines" / EstimateUsage."Estimate Lines" * 100);
            IF EstimateUsage."Recipe Lines" <> 0 THEN
              EstimateUsage."Empty Recipe Lines %" := ROUND(EstimateUsage."Empty Recipe Lines" / EstimateUsage."Recipe Lines" * 100);
            IF (EstimateUsage."Estimate Lines" + EstimateUsage."Recipe Lines") <> 0 THEN
              EstimateUsage."Total Empty Lines %" := ROUND(
                (EstimateUsage."Empty Estimate Lines" + EstimateUsage."Empty Recipe Lines") /
                (EstimateUsage."Estimate Lines" + EstimateUsage."Recipe Lines") * 100);
          END;

          IF EstimateUsageExist THEN
            IF EstimateUsage."Total Empty Lines %" > Threshold THEN
              EstimateUsage.MODIFY
            ELSE
              EstimateUsage.DELETE
          ELSE
            IF EstimateUsage."Total Empty Lines %" > Threshold THEN
              EstimateUsage.INSERT;
        UNTIL Estimate.NEXT = 0;

      Window.CLOSE;
    END;

    LOCAL PROCEDURE CountZeroLines@1(Estimate@1100485003 : Record 11012151;VAR EstimateUsage@1100528500 : Record 11229781);
    VAR
      EstimateLineNew@1100409004 : Record 11072072;
      RecipeLineNew@1100409002 : Record 11072075;
      EmptyRecipe@1100409000 : 'ZeroHeader,ZeroLines';
    BEGIN
      EmptyRecipe := EmptyRecipe::ZeroHeader;

      RecipeLineNew.SETRANGE("Estimate No.", Estimate."No.");
      RecipeLineNew.SETRANGE(Quantity, 0);
      RecipeLineNew.SETRANGE("Quantity (Sales)", 0);
      IF RecipeLineNew.FINDSET THEN
        REPEAT
          IF (RecipeLineNew.Norm <> 0) OR (RecipeLineNew."Net Cost Price" <> 0) OR
             (RecipeLineNew."Norm (Sales)" <> 0) OR (RecipeLineNew."Net Sales Price" <> 0) THEN
           EstimateUsage."Empty Recipe Lines" := EstimateUsage."Empty Recipe Lines" + 1;
        UNTIL RecipeLineNew.NEXT = 0;
      RecipeLineNew.RESET;

      EstimateLineNew.SETRANGE("Estimate No.", Estimate."No.");
      EstimateLineNew.SETFILTER(Recipe, '<>%1', '');
      IF EstimateLineNew.FINDSET THEN
        REPEAT
          EstimateLineNew.CALCFIELDS("Recipe Lines Exist");
          IF NOT EstimateLineNew."Recipe Lines Exist" THEN
            EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
        UNTIL EstimateLineNew.NEXT = 0;

      EstimateLineNew.SETRANGE("Estimate No.", Estimate."No.");
      IF EmptyRecipe = EmptyRecipe::ZeroLines THEN
        EstimateLineNew.SETFILTER(Recipe, '%1', '')
      ELSE
        EstimateLineNew.SETRANGE(Recipe);
      EstimateLineNew.SETRANGE(Quantity, 0);
      EstimateLineNew.SETRANGE("Quantity (Sales)", 0);
      IF EstimateLineNew.FINDSET THEN
        REPEAT
          IF (EstimateLineNew.Norm <> 0) OR (EstimateLineNew."Net Cost Price" <> 0) OR
             (EstimateLineNew."Norm (Sales)" <> 0) OR (EstimateLineNew."Net Sales Price" <> 0) THEN BEGIN
            EstimateLineNew.CALCFIELDS(
              "Quantity Model Exist","Quantity Sheet Exist","Quantity Sheet Relation Exist");
            IF NOT EstimateSetup."Quantity Sheet with Relations" AND
               (NOT EstimateLineNew."Quantity Model Exist") AND
               (NOT EstimateLineNew."Quantity Sheet Exist") THEN
              EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
            IF EstimateSetup."Quantity Sheet with Relations" AND
               (NOT EstimateLineNew."Quantity Model Exist") AND
               (NOT EstimateLineNew."Quantity Sheet Relation Exist") THEN
              EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
          END ELSE
            IF (EstimateLineNew.Recipe <> '') AND (EmptyRecipe = EmptyRecipe::ZeroHeader) THEN
               EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
        UNTIL EstimateLineNew.NEXT = 0;
    END;

    PROCEDURE DeleteZeroLines@1100525000(VAR EstimateUsage2@1100525009 : Record 11229781);
    VAR
      EstimateUsage@1100525000 : Record 11229781;
      EstimateLineNew@1100525006 : Record 11072072;
      NoOfRecords@1100525003 : Integer;
      Counter@1100525002 : Integer;
      Window@1100525001 : Dialog;
      UpdateEstNew@1100525007 : Codeunit 11012229;
      EmptyRecipe@1100525008 : 'ZeroHeader,ZeroLines';
    BEGIN
      EstimateUsage.COPY(EstimateUsage2);

      Window.OPEN(Text001);

      NoOfRecords := EstimateUsage.COUNT;
      IF EstimateUsage.FINDSET(TRUE,FALSE) THEN
        REPEAT
          Counter := Counter + 1;
          Window.UPDATE(1,EstimateUsage."Estimate No.");
          Window.UPDATE(2,ROUND(Counter / NoOfRecords * 10000,1));

          EstimateLineNew.SETRANGE("Estimate No.", EstimateUsage."Estimate No.");
          UpdateEstNew.DeleteZeroLines(EstimateLineNew,FALSE,EmptyRecipe::ZeroHeader,NoOfRecords=1);

          EstimateUsage.DELETE;
          COMMIT;
        UNTIL EstimateUsage.NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    {
      4PS, 03-02-2015, DP00717
    }
    END.
  }
}

