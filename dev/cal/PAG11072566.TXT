OBJECT Page 11072566 Export Filter Subform (Mobile)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Export Filtersatz (Mobile);
               ENU=Export Filter Set(Mobile);
               NLD=Export filterset (Mobile);
               SVE=Export filtersats (mobil)];
    InsertAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11020441;
    PageType=ListPart;
    OnAfterGetRecord=BEGIN
                       FieldFilterOnFormat(FORMAT("Field Filter"));
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Group  ;
                GroupType=Repeater }

    { 1100525003;2;Field  ;
                SourceExpr="Table No." }

    { 1100525001;2;Field  ;
                CaptionML=[DEU=Tabellenname;
                           ENU=Table Name;
                           NLD=Tabelnaam;
                           NOR=Tabellnavn;
                           SVE=Tabellnamn];
                SourceExpr=GetTableCaption() }

    { 1100525005;2;Field  ;
                SourceExpr="Field No." }

    { 1100525009;2;Field  ;
                SourceExpr="Field Caption" }

    { 1100525007;2;Field  ;
                SourceExpr="Field Filter" }

  }
  CODE
  {
    VAR
      FieldRec@1100525002 : Record 2000000041;
      HoldTableNo@1100525000 : Integer;
      HoldTableCaption@1100525001 : Text[100];
      Text001@1100525003 : TextConst 'DEU=%1 von %2 Feldfiltern kopiert;ENU=%1 of %2 field filters copied;NLD=%1 van %2 veldfilters gekopieerd;SVE=%1 av %2 f„ltfilter har kopierats';
      TextCostCenter@1100525004 : TextConst 'DEU=Kostenstelle;ENU=Cost Center;NLD=Kostenplaats;NOR=Kostsen.;SVE=Kostnads- st„lle';
      TextCostObject@1100525005 : TextConst 'DEU=Kostentr„ger;ENU=Cost Object;NLD=Kostendrager;NOR=Kostnadsobjekt;SVE=Kostnadsobjekt';

    PROCEDURE GetTableCaption@1100525000() RetTableCaption : Text[100];
    VAR
      lvRecRef@1100525001 : RecordRef;
    BEGIN
      RetTableCaption := '';
      IF ("Table No." <> 0) THEN BEGIN
        IF ("Sub Table No." <> 0) THEN BEGIN
          CASE "Table No." OF
            DATABASE::"Dimension Value":
              CASE "Sub Table No." OF
               1:
                 BEGIN
                   HoldTableNo := "Table No.";
                   HoldTableCaption := TextCostCenter;
                 END;
               2:
                 BEGIN
                   HoldTableNo := "Table No.";
                   HoldTableCaption := TextCostObject;
                 END;
              END;
          END;
        END;
        IF ("Table No." <> HoldTableNo) THEN BEGIN
          lvRecRef.OPEN("Table No.");
          HoldTableNo := "Table No.";
          HoldTableCaption := lvRecRef.CAPTION;
          lvRecRef.CLOSE;
        END;
        RetTableCaption := HoldTableCaption;
      END;
    END;

    PROCEDURE CopyFrom@1100525001(IFilterSetCodeTo@1100525000 : Code[20]);
    VAR
      lvFieldFilterRec@1100525002 : Record 11020441;
      lvFilterSetCodeFrom@1100525001 : Code[20];
    BEGIN
      IF IFilterSetCodeTo = '' THEN
        EXIT;

      lvFilterSetCodeFrom := SelectFilterSetCode(IFilterSetCodeTo);
      IF lvFilterSetCodeFrom <> '' THEN BEGIN
        lvFieldFilterRec.SETRANGE("Filter Set", lvFilterSetCodeFrom);
        CopyFieldFilters(lvFieldFilterRec, IFilterSetCodeTo);
      END;
    END;

    PROCEDURE CopyTo@1100525002(IFilterSetCodeFrom@1100525000 : Code[20]);
    VAR
      lvFieldFilterRec@1100525003 : Record 11020441;
      lvFilterSetCodeTo@1100525001 : Code[20];
    BEGIN
      IF IFilterSetCodeFrom = '' THEN
        EXIT;

      lvFilterSetCodeTo := SelectFilterSetCode(IFilterSetCodeFrom);
      IF lvFilterSetCodeTo <> '' THEN BEGIN
        lvFieldFilterRec.COPY(Rec);
        CurrPage.SETSELECTIONFILTER(lvFieldFilterRec);
        CopyFieldFilters(lvFieldFilterRec, lvFilterSetCodeTo);
      END;
    END;

    LOCAL PROCEDURE SelectFilterSetCode@1100525003(ICurrFilterSetCode@1100525002 : Code[20]) : Code[20];
    VAR
      lvFilterSetRec@1100525001 : Record 11020440;
    BEGIN
      lvFilterSetRec.FILTERGROUP(6);
      lvFilterSetRec.SETFILTER(Code, '<>%1', ICurrFilterSetCode);
      lvFilterSetRec.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0, lvFilterSetRec) = ACTION::LookupOK THEN
        EXIT(lvFilterSetRec.Code);

      EXIT('');
    END;

    LOCAL PROCEDURE CopyFieldFilters@1100525004(VAR IFieldFilterRec@1100525000 : Record 11020441;IFilterSetCodeTo@1100525001 : Code[20]);
    VAR
      lvFieldFilterRec2@1100525002 : Record 11020441;
      lvCounterRecords@1100525003 : Integer;
      lvCounterInserted@1100525004 : Integer;
    BEGIN
      WITH IFieldFilterRec DO BEGIN
        IF FINDSET(FALSE, FALSE) THEN BEGIN
          REPEAT
            lvCounterRecords := lvCounterRecords + 1;
            IF NOT lvFieldFilterRec2.GET(IFilterSetCodeTo, "Table No.", "Field No.") THEN BEGIN
              lvFieldFilterRec2 := IFieldFilterRec;
              lvFieldFilterRec2."Filter Set" := IFilterSetCodeTo;
              lvFieldFilterRec2.INSERT;
              lvCounterInserted := lvCounterInserted + 1;
            END;
          UNTIL NEXT = 0;
        END;
        MESSAGE(Text001, lvCounterInserted, lvCounterRecords);
      END;
    END;

    LOCAL PROCEDURE FieldFilterOnFormat@19013386(Text@19008261 : Text[1024]);
    VAR
      lvFieldFilterRec@1100525002 : Record 11020441;
    BEGIN
      IF ("Table No." <> 0) AND ("Field No." <> 0) AND ("Field Filter" <> '') THEN BEGIN
        IF FieldRec.GET("Table No.", "Field No.") THEN BEGIN
          IF FieldRec.Type IN [FieldRec.Type::Boolean, FieldRec.Type::Option] THEN BEGIN
            lvFieldFilterRec := Rec;
            IF CODEUNIT.RUN(CODEUNIT::"Export Filter Set Mgt (Mobile)", lvFieldFilterRec) THEN
              Text := lvFieldFilterRec."Field Filter";
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

