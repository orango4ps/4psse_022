OBJECT Page 11126469 All Service Inspection Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Alle Serviceinspektionszeilen;
               ENU=All Service Inspection Lines;
               NLD=Alle service inspectieregels];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11071751;
    PageType=List;
    SourceTableTemporary=Yes;
    OnAfterGetRecord=BEGIN
                       Cancelled :=
                         IsCancelled(
                           LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme, LevelBaseInspectionList, LevelIndexDate,
                           WORKDATE, CancellationLevel);
                       NewDescription :=
                         GetDescription(
                           LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme, LevelBaseInspectionList, LevelIndexDate,
                           WORKDATE, DescriptionLevel);
                       LowerValue := GetLowerUpperValue(
                         LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme, LevelBaseInspectionList, LevelIndexDate,
                         WORKDATE, 0, LowerValueLevel);
                       UpperValue := GetLowerUpperValue(
                         LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme, LevelBaseInspectionList, LevelIndexDate,
                         WORKDATE, 1, UpperValueLevel);
                       DescriptionOnFormat;
                     END;

  }
  CONTROLS
  {
    { 1100528600;0;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Group  ;
                Name=Group;
                IndentationColumnName=DescriptionIndentationValue;
                IndentationControls=NewDescription;
                GroupType=Repeater }

    { 1100528602;2;Field  ;
                SourceExpr="Inspection Type" }

    { 1100528603;2;Field  ;
                SourceExpr="Service Contract No." }

    { 1100528604;2;Field  ;
                SourceExpr=Code }

    { 1100528605;2;Field  ;
                SourceExpr="Index Date" }

    { 1100528607;2;Field  ;
                SourceExpr=Type }

    { 1100528608;2;Field  ;
                SourceExpr="Inspection Code" }

    { 1100528606;2;Field  ;
                Name=NewDescription;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=NewDescription;
                Editable=NewDescriptionEditable;
                OnValidate=BEGIN
                             IF NewDescription = Description THEN
                               NewDescription := '';
                             SetDescription(NewDescription, LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme,
                               LevelBaseInspectionList, LevelIndexDate);
                             NewDescription :=
                               GetDescription(
                                 LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme, LevelBaseInspectionList, LevelIndexDate,
                                 WORKDATE, DescriptionLevel);
                           END;
                            }

    { 1100528615;2;Field  ;
                CaptionML=[DEU=Beschreibungsebene;
                           ENU=Description Level;
                           NLD=Omschrijving niveau];
                OptionCaptionML=[DEU=" ,Basis-Inspektionsliste,Standardobjekt,Vertragsobjekt,Standardobjekt (Meldungen),Objekt (Meldungen)";
                                 ENU=" ,Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)";
                                 NLD=" ,Stam inspectielijst,Standaard object,Contract object,Standaard Object (Meldingen),Object (Meldingen)"];
                SourceExpr=DescriptionLevel;
                Visible=FALSE;
                Editable=FALSE }

    { 1100528610;2;Field  ;
                SourceExpr="Field Type" }

    { 1100528611;2;Field  ;
                SourceExpr="Unit Of Measure" }

    { 1100528612;2;Field  ;
                SourceExpr=Mandatory }

    { 1100528613;2;Field  ;
                CaptionML=[DEU=Storniert;
                           ENU=Cancelled;
                           NLD=Vervallen;
                           NOR=Avbrutt;
                           SVE=Annullerad];
                SourceExpr=Cancelled;
                Editable=CancelledEditable;
                OnValidate=BEGIN
                             SetAsCancelled(Cancelled, LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme,
                               LevelBaseInspectionList, LevelIndexDate);
                             Cancelled :=
                               IsCancelled(
                                LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme, LevelBaseInspectionList,
                                LevelIndexDate, WORKDATE, CancellationLevel);
                           END;
                            }

    { 1100528614;2;Field  ;
                CaptionML=[DEU=Stornierungsebene;
                           ENU=Cancellation Level;
                           NLD=Vervallen niveau];
                OptionCaptionML=[DEU=" ,Basis-Inspektionsliste,Standardobjekt,Vertragsobjekt,Standardobjekt (Meldungen),Objekt (Meldungen)";
                                 ENU=" ,Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)";
                                 NLD=" ,Stam inspectielijst,Standaard object,Contract object,Standaard Object (Meldingen),Object (Meldingen)"];
                SourceExpr=CancellationLevel;
                Visible=FALSE;
                Editable=FALSE }

    { 1100528616;2;Field  ;
                CaptionML=[DEU=Untergrenze;
                           ENU=Lower Value;
                           NLD=Onderwaarde];
                BlankZero=Yes;
                SourceExpr=LowerValue;
                Editable=LowerValueEditable;
                OnValidate=BEGIN
                             SetLowerUpperValue(0, LowerValue, LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme,
                               LevelBaseInspectionList, LevelIndexDate);
                             LowerValue := GetLowerUpperValue(
                               LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme, LevelBaseInspectionList, LevelIndexDate,
                               WORKDATE, 0, LowerValueLevel);
                           END;
                            }

    { 1100528618;2;Field  ;
                CaptionML=[DEU=Unteres Wertlevel;
                           ENU=Lower Value Level;
                           NLD=Onderwaarde niveau];
                OptionCaptionML=[DEU=" ,Basis-Inspektionsliste,Standardobjekt,Vertragsobjekt,Standardobjekt (Meldungen),Objekt (Meldungen)";
                                 ENU=" ,Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)";
                                 NLD=" ,Stam inspectielijst,Standaard object,Contract object,Standaard Object (Meldingen),Object (Meldingen)"];
                SourceExpr=LowerValueLevel;
                Visible=FALSE;
                Editable=FALSE }

    { 1100528617;2;Field  ;
                CaptionML=[DEU=Obergrenze;
                           ENU=Upper Value;
                           NLD=Bovenwaarde];
                BlankZero=Yes;
                SourceExpr=UpperValue;
                Editable=UpperValueEditable;
                OnValidate=BEGIN
                             SetLowerUpperValue(1, UpperValue, LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme,
                               LevelBaseInspectionList, LevelIndexDate);
                             UpperValue := GetLowerUpperValue(
                               LevelInspectionType, LevelServiceContractNo, LevelCode, LevelMaintenanceScheme, LevelBaseInspectionList, LevelIndexDate,
                               WORKDATE, 1, UpperValueLevel);
                           END;
                            }

    { 1100528619;2;Field  ;
                CaptionML=[DEU=Oberes Wertlevel;
                           ENU=Upper Value Level;
                           NLD=Bovenwaarde niveau];
                OptionCaptionML=[DEU=" ,Basis-Inspektionsliste,Standardobjekt,Vertragsobjekt,Standardobjekt (Meldungen),Objekt (Meldungen)";
                                 ENU=" ,Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)";
                                 NLD=" ,Stam inspectielijst,Standaard object,Contract object,Standaard Object (Meldingen),Object (Meldingen)"];
                SourceExpr=UpperValueLevel;
                Visible=FALSE;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Cancelled@1100528608 : Boolean;
      CancellationLevel@1100528607 : ' ,Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)';
      CancelledEditable@1100528610 : Boolean INDATASET;
      LevelInspectionType@1100528606 : Option;
      LevelServiceContractNo@1100528605 : Code[20];
      LevelCode@1100528604 : Code[20];
      LevelMaintenanceScheme@1100528603 : Code[20];
      LevelBaseInspectionList@1100528602 : Code[20];
      LevelIndexDate@1100528601 : Date;
      DescriptionEmphasize@1100528609 : Boolean INDATASET;
      DescriptionIndentationValue@1100528600 : Integer INDATASET;
      NewDescription@1100528611 : Text[50];
      DescriptionLevel@1100528612 : ' ,Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)';
      LowerValue@1100528613 : Decimal;
      LowerValueLevel@1100528615 : ' ,Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)';
      UpperValue@1100528614 : Decimal;
      UpperValueLevel@1100528616 : ' ,Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)';
      LowerValueEditable@1100528617 : Boolean;
      UpperValueEditable@1100528618 : Boolean;
      NewDescriptionEditable@1100528619 : Boolean;

    PROCEDURE ShowOptionValues@1100528601();
    VAR
      BaseServiceInspectionOption@1100528600 : Record 11071749;
      BaseServiceInspectionOptionPAGE@1100528601 : Page 11125256;
    BEGIN
      TESTFIELD("Field Type", "Field Type"::Option);
      BaseServiceInspectionOption.SETRANGE("Base Inspection Line Code", "Inspection Code");
      BaseServiceInspectionOptionPAGE.SETTABLEVIEW(BaseServiceInspectionOption);
      BaseServiceInspectionOptionPAGE.EDITABLE := FALSE;
      BaseServiceInspectionOptionPAGE.RUNMODAL;
    END;

    PROCEDURE SetObjectSelection@1210190000(IInspectionType@1210190000 : 'Base Inspection List,Standard Object,Contract Object,Standard Object (Calls),Object (Calls)';IServiceContractNo@1100528601 : Code[20];ICode@1210190004 : Code[20];IMaintenanceScheme@1100528602 : Code[20];IBaseInspectionList@1100528603 : Code[20];IIndexDate@1100528600 : Date);
    VAR
      ServiceInspectionManagement@1210190001 : Codeunit 11012844;
      CentralDataManagement@1100528604 : Codeunit 11012010;
    BEGIN
      RESET;
      DELETEALL;
      ServiceInspectionManagement.CollectTmpInspVersionLines(
        IInspectionType, IServiceContractNo, ICode, IMaintenanceScheme, IBaseInspectionList, TODAY,
        IInspectionType, IServiceContractNo, ICode, IMaintenanceScheme, IBaseInspectionList, TRUE, Rec);
      LevelInspectionType := IInspectionType;
      LevelServiceContractNo := IServiceContractNo;
      LevelCode := ICode;
      LevelMaintenanceScheme := IMaintenanceScheme;
      LevelIndexDate := IIndexDate;
      LevelBaseInspectionList := IBaseInspectionList;

      CASE IInspectionType OF
        IInspectionType::"Base Inspection List",
        IInspectionType::"Standard Object",
        IInspectionType::"Standard Object (Calls)":
        IF NOT CentralDataManagement.ServiceInspectionFormEditable THEN
          CurrPage.EDITABLE(FALSE);
      END;
      CancelledEditable := TRUE;
      LowerValueEditable := TRUE;
      UpperValueEditable := TRUE;
      NewDescriptionEditable := TRUE;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE DescriptionOnFormat@19023855();
    BEGIN
      DescriptionEmphasize := FALSE;
      DescriptionIndentationValue := 0;
      IF Type = Type::Group THEN
        DescriptionEmphasize := TRUE
      ELSE
        DescriptionIndentationValue := 1;
    END;

    BEGIN
    END.
  }
}

