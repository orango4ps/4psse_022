OBJECT Report 11124900 Create Request Collective-List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Sammellisten erstellen (Anfrage);
               ENU=Create Collective-Lists (Request);
               NLD=Verzamelstaten (aanvraag) aanmaken];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ResourcePlanningSetup.GET;
                 END;

    OnPreReport=BEGIN
                  Counter := 0;
                  IF GUIALLOWED THEN
                    Window.OPEN(Text001 + '\\' + "Resource Request".TABLECAPTION + ' #1##########');
                END;

    OnPostReport=BEGIN
                   IF GUIALLOWED THEN BEGIN
                     Window.CLOSE;
                     MESSAGE(Text002, Counter);
                   END;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1100529000;;DataItem;                  ;
               DataItemTable=Table11124901;
               DataItemTableView=SORTING(Source Type,Source No.)
                                 WHERE(Source Type=CONST(Customer),
                                       Status=CONST(Finished));
               OnAfterGetRecord=VAR
                                  AdditionalInfoRequestWO@1100529000 : Record 11124915;
                                  WorkOrder@1100529002 : Record 11229279;
                                  Resource@1100529003 : Record 11229278;
                                  ResourceRequestItemLine@1100528300 : Record 11124908;
                                  Item@1100528301 : Record 27;
                                  TransportCostRequestWO@1100529004 : Record 11124913;
                                  CollectiveListNo@1100529001 : Code[20];
                                BEGIN
                                  IF GUIALLOWED THEN
                                    Window.UPDATE(1, "No.");
                                  TESTFIELD("Source No.");

                                  AdditionalInfoRequestWO.SETCURRENTKEY("Request No.","Work Order No.");
                                  AdditionalInfoRequestWO.SETRANGE("Request No.", "No.");
                                  AdditionalInfoRequestWO.SETRANGE(Chargeable, TRUE);
                                  AdditionalInfoRequestWO.SETRANGE("Collective List No.", '');
                                  IF AdditionalInfoRequestWO.FINDSET(TRUE) THEN BEGIN
                                    CollectiveListNo := SearchOpenCollectiveListNo("Resource Request");
                                    IF CollectiveListNo = '' THEN
                                      CollectiveListNo := InsertCollectiveList("Resource Request");
                                    REPEAT
                                      WorkOrder.GET(AdditionalInfoRequestWO."Work Order No.");
                                      WorkOrder.TESTFIELD("Resource No.");
                                      Resource.GET(WorkOrder."Resource No.");
                                      InsertCollectiveListLineResource(CollectiveListNo, "Resource Request", AdditionalInfoRequestWO, WorkOrder, Resource);
                                      AdditionalInfoRequestWO."Collective List No." := CollectiveListNo;
                                      AdditionalInfoRequestWO.MODIFY;
                                    UNTIL AdditionalInfoRequestWO.NEXT = 0;
                                  END;

                                  ResourceRequestItemLine.SETRANGE("Request No.", "No.");
                                  ResourceRequestItemLine.SETRANGE("Collective List No.", '');
                                  IF ResourceRequestItemLine.FINDSET(TRUE) THEN BEGIN
                                    IF CollectiveListNo = '' THEN BEGIN
                                      CollectiveListNo := SearchOpenCollectiveListNo("Resource Request");
                                      IF CollectiveListNo = '' THEN
                                        CollectiveListNo := InsertCollectiveList("Resource Request");
                                    END;
                                    REPEAT
                                      IF ResourceRequestItemLine."Item No." <> '' THEN
                                        Item.GET(ResourceRequestItemLine."Item No.")
                                      ELSE
                                        Item.INIT;
                                      InsertCollectiveListLineItem(CollectiveListNo, "Resource Request", ResourceRequestItemLine, Item);
                                      ResourceRequestItemLine."Collective List No." := CollectiveListNo;
                                      ResourceRequestItemLine.MODIFY;
                                    UNTIL ResourceRequestItemLine.NEXT = 0;
                                  END;

                                  TransportCostRequestWO.SETRANGE("Request No.", "No.");
                                  TransportCostRequestWO.SETRANGE("Collective List No.", '');
                                  IF TransportCostRequestWO.FINDSET(TRUE) THEN BEGIN
                                    IF CollectiveListNo = '' THEN BEGIN
                                      CollectiveListNo := SearchOpenCollectiveListNo("Resource Request");
                                      IF CollectiveListNo = '' THEN
                                        CollectiveListNo := InsertCollectiveList("Resource Request");
                                    END;
                                    REPEAT
                                      InsertCollectiveListLineTransportCost(CollectiveListNo, "Resource Request", TransportCostRequestWO);
                                      TransportCostRequestWO."Collective List No." := CollectiveListNo;
                                      TransportCostRequestWO.MODIFY;
                                    UNTIL TransportCostRequestWO.NEXT = 0;
                                  END;

                                  IF CollectiveListNo <> '' THEN
                                    Counter := Counter + 1;
                                END;

               ReqFilterFields=No.,Source No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text001@1100529001 : TextConst 'DEU=Sammellisten erstellen;ENU=Create Collective-Lists;NLD=Verzamelstaten aanmaken;SVE=Skapa samlingsfakturor';
      ResourcePlanningSetup@1100529004 : Record 11124900;
      Window@1100529002 : Dialog;
      Counter@1100529000 : Integer;
      Text002@1100529003 : TextConst 'DEU=Verarbeitete Anfragen: %1;ENU=Processed requests: %1;NLD=Verwerkte aanvragen: %1';

    LOCAL PROCEDURE SearchOpenCollectiveListNo@1100529000(ResourceRequest@1100529001 : Record 11124901) : Code[10];
    VAR
      ResourceReqCollectiveList@1100529000 : Record 11124904;
      Customer@1100529002 : Record 18;
    BEGIN
      Customer.GET(ResourceRequest."Source No.");
      ResourceReqCollectiveList.SETCURRENTKEY("Customer No.");
      ResourceReqCollectiveList.SETRANGE("Customer No.", ResourceRequest."Source No.");
      ResourceReqCollectiveList.SETRANGE(Status, ResourceReqCollectiveList.Status::Open);
      ResourceReqCollectiveList.SETRANGE("Collect Invoices by", Customer."Collect Rsrc. Req. Invoices by");
      IF Customer."Collect Rsrc. Req. Invoices by" = Customer."Collect Rsrc. Req. Invoices by"::Request THEN
        ResourceReqCollectiveList.SETRANGE("Request No.", ResourceRequest."No.");
      IF ResourceReqCollectiveList.FINDFIRST THEN
        EXIT(ResourceReqCollectiveList."No.");
      EXIT('');
    END;

    LOCAL PROCEDURE InsertCollectiveList@1100529003(ResourceRequest@1100529001 : Record 11124901) : Code[20];
    VAR
      ResourceReqCollectiveList@1100529000 : Record 11124904;
    BEGIN
      ResourceReqCollectiveList.INIT;
      ResourceReqCollectiveList."No." := '';
      ResourceReqCollectiveList.INSERT(TRUE);
      ResourceReqCollectiveList.TESTFIELD("No.");
      ResourceReqCollectiveList.VALIDATE("Customer No.", ResourceRequest."Source No.");
      IF ResourceReqCollectiveList."Collect Invoices by" = ResourceReqCollectiveList."Collect Invoices by"::Request THEN
        ResourceReqCollectiveList.VALIDATE("Request No.", ResourceRequest."No.");
      ResourceReqCollectiveList.MODIFY(TRUE);
      EXIT(ResourceReqCollectiveList."No.");
    END;

    LOCAL PROCEDURE InsertCollectiveListLineResource@1100529002(CollectiveListNo@1100529003 : Code[20];ResourceRequest@1100529000 : Record 11124901;AdditionalInfoRequestWO@1100529001 : Record 11124915;WorkOrder@1100529005 : Record 11229279;Resource@1100529006 : Record 11229278);
    VAR
      ResourceReqCollListLine@1100529002 : Record 11124905;
    BEGIN
      ResourceReqCollListLine.INIT;
      ResourceReqCollListLine."Collective List No." := CollectiveListNo;
      ResourceReqCollListLine."Line No." := 0;
      ResourceReqCollListLine."Request No." := "Resource Request"."No.";
      ResourceReqCollListLine."Line Type" := ResourceReqCollListLine."Line Type"::Resource;
      ResourceReqCollListLine."Work Order No." := AdditionalInfoRequestWO."Work Order No.";
      ResourceReqCollListLine."Resource No." := WorkOrder."Resource No.";
      ResourceReqCollListLine.Description := ResourceDescription(WorkOrder, Resource);
      ResourceReqCollListLine."Global Dimension 2 Code" := ResourceCostObject(Resource);
      ResourceReqCollListLine."Unit of Measure" := ResourcePlanningSetup."Unit of Measure Hours";
      IF AdditionalInfoRequestWO."Quantity Agreement" THEN
        ResourceReqCollListLine.Quantity := AdditionalInfoRequestWO."Fixed Quantity"
      ELSE
        ResourceReqCollListLine.Quantity := AdditionalInfoRequestWO."Actual Hours";
      IF AdditionalInfoRequestWO."Agreement Type" = AdditionalInfoRequestWO."Agreement Type"::Amount THEN BEGIN
        ResourceReqCollListLine.Amount := AdditionalInfoRequestWO."Fixed Amount";
        IF ResourceReqCollListLine.Quantity = 0 THEN
          ResourceReqCollListLine.Quantity := 1;
        ResourceReqCollListLine.Rate := ROUND(ResourceReqCollListLine.Amount / ResourceReqCollListLine.Quantity);
      END ELSE BEGIN
        IF AdditionalInfoRequestWO."Agreement Type" = AdditionalInfoRequestWO."Agreement Type"::Rate THEN
          ResourceReqCollListLine.Rate := AdditionalInfoRequestWO."Special Rate"
        ELSE
          ResourceReqCollListLine.Rate := AdditionalInfoRequestWO.GetResourceRate();
        IF AdditionalInfoRequestWO."Agreement Type" = AdditionalInfoRequestWO."Agreement Type"::Discount THEN
          ResourceReqCollListLine."Discount %" := AdditionalInfoRequestWO."Special Discount %";
        ResourceReqCollListLine.VALIDATE(Quantity);  // Calculate amount
      END;
      ResourceReqCollListLine."VAT Prod. Posting Group" := ResourceVatProdPostingGroup(Resource);
      ResourceReqCollListLine.INSERT(TRUE);
    END;

    LOCAL PROCEDURE InsertCollectiveListLineItem@1100528300(CollectiveListNo@1100529003 : Code[20];ResourceRequest@1100529000 : Record 11124901;ResourceRequestItemLine@1100529001 : Record 11124908;Item@1100529006 : Record 27);
    VAR
      ResourceReqCollListLine@1100529002 : Record 11124905;
    BEGIN
      ResourceReqCollListLine.INIT;
      ResourceReqCollListLine."Collective List No." := CollectiveListNo;
      ResourceReqCollListLine."Line No." := 0;
      ResourceReqCollListLine."Request No." := "Resource Request"."No.";
      ResourceReqCollListLine."Item No." := ResourceRequestItemLine."Item No.";
      ResourceReqCollListLine."Line Type" := ResourceReqCollListLine."Line Type"::Item;
      ResourceReqCollListLine.Description := ResourceRequestItemLine.Description;
      ResourceReqCollListLine."Description 2" := ResourceRequestItemLine."Description 2";
      IF ResourceRequestItemLine."Cost Object" <> '' THEN
        ResourceReqCollListLine."Global Dimension 2 Code" := ResourceRequestItemLine."Cost Object"
      ELSE
        ResourceReqCollListLine."Global Dimension 2 Code" := Item."Global Dimension 2 Code";
      ResourceReqCollListLine."Unit of Measure" := ResourceRequestItemLine."Unit of Measure";
      ResourceReqCollListLine.Quantity := ResourceRequestItemLine.Quantity;
      ResourceReqCollListLine.Rate := ResourceRequestItemLine."Unit Price";
      ResourceReqCollListLine.Amount := ResourceRequestItemLine.Amount;
      ResourceReqCollListLine."VAT Prod. Posting Group" := ItemVatProdPostingGroup(Item);
      ResourceReqCollListLine.INSERT(TRUE);
    END;

    LOCAL PROCEDURE InsertCollectiveListLineTransportCost@1100529006(CollectiveListNo@1100529003 : Code[20];ResourceRequest@1100529000 : Record 11124901;TransportCostRequestWO@1100529001 : Record 11124913);
    VAR
      ResourceReqCollListLine@1100529002 : Record 11124905;
    BEGIN
      ResourceReqCollListLine.INIT;
      ResourceReqCollListLine."Collective List No." := CollectiveListNo;
      ResourceReqCollListLine."Line No." := 0;
      ResourceReqCollListLine."Request No." := "Resource Request"."No.";
      ResourceReqCollListLine."Line Type" := ResourceReqCollListLine."Line Type"::Transport;
      ResourceReqCollListLine."Global Dimension 2 Code" := TransportCostRequestWO."Cost Object";
      ResourceReqCollListLine.Description := TransportCostRequestWO.Description;
      ResourceReqCollListLine."Unit of Measure" := TransportCostRequestWO."Unit of Measure";
      ResourceReqCollListLine.Quantity := TransportCostRequestWO.Quantity;
      ResourceReqCollListLine.Rate := TransportCostRequestWO."Unit Price";
      ResourceReqCollListLine.Amount := TransportCostRequestWO.Amount;
      ResourceReqCollListLine."VAT Prod. Posting Group" := TransportVatProdPostingGroup(TransportCostRequestWO."Cost Object");
      ResourceReqCollListLine.INSERT(TRUE);
    END;

    LOCAL PROCEDURE ResourceDescription@1100529004(WorkOrder@1100529001 : Record 11229279;Resource@1100529000 : Record 11229278) LineDescription : Text;
    VAR
      ResourceGroup@1100529002 : Record 11229384;
    BEGIN
      IF WorkOrder."Resource Group No." <> '' THEN BEGIN
        IF ResourceGroup.GET(WorkOrder."Resource Group No.") THEN BEGIN
          IF Resource.Type <> Resource.Type::Plant THEN
            LineDescription := ResourceGroup.Name
          ELSE BEGIN
            IF (ResourceGroup."Source No." <> Resource."Source No.") OR (ResourceGroup.Company <> Resource.Company) THEN
              LineDescription := ResourceGroup.Name;
          END;
        END;
      END;
      IF LineDescription = '' THEN
        LineDescription := Resource.Name;
    END;

    LOCAL PROCEDURE ResourceCostObject@1100529001(Resource@1100529002 : Record 11229278) CostObject : Code[20];
    VAR
      Employee@1100529001 : Record 5200;
      PlantType@1100529000 : Record 11012551;
      ResourceRequestType@1100529004 : Record 11124907;
      Vendor@1100529003 : Record 23;
      DimVal@1100529005 : Record 349;
      DimensionMgt@1100529006 : Codeunit 408;
    BEGIN
      CostObject := '';
      CASE Resource.Type OF
        Resource.Type::Employee:
          BEGIN
            IF Resource.Company <> COMPANYNAME THEN
              Employee.CHANGECOMPANY(Resource.Company);
            IF Employee.GET(Resource."Source No.") THEN
              CostObject := Employee."Global Dimension 2 Code";
          END;
        Resource.Type::Plant:
          BEGIN
            IF Resource.Company <> COMPANYNAME THEN
              PlantType.CHANGECOMPANY(Resource.Company);
            IF PlantType.GET(Resource."Source No.") THEN
              CostObject := PlantType."Cost Object";
          END;
        Resource.Type::Subcontractor:
          BEGIN
            IF ResourceRequestType.GET("Resource Request"."Request Type") THEN BEGIN
              IF ResourceRequestType."Cost Object Subcontracting" <> '' THEN
                CostObject := ResourceRequestType."Cost Object Subcontracting";
            END;
            IF CostObject = '' THEN BEGIN
              IF Resource.Company <> COMPANYNAME THEN
                Vendor.CHANGECOMPANY(Resource.Company);
              IF Vendor.GET(Resource."Source No.") THEN
                CostObject := Vendor."Global Dimension 2 Code";
            END;
          END;
      END;
      IF CostObject <> '' THEN BEGIN
        DimensionMgt.GetDimValueRec(2, CostObject, DimVal, FALSE, '');
        CostObject := DimVal.Code;  //Empty if not exist
      END;
    END;

    LOCAL PROCEDURE ResourceVatProdPostingGroup@18(Resource@1100529000 : Record 11229278) : Code[10];
    VAR
      ResourceRequestType@1100529003 : Record 11124907;
      PlantType@1100529001 : Record 11012551;
      Customer@1100529004 : Record 18;
      VATProductPostingGroup@1100529002 : Record 324;
    BEGIN
      ResourceRequestType.GET("Resource Request"."Request Type");
      CASE Resource.Type OF
        Resource.Type::Plant:
          BEGIN
            IF Resource.Company <> COMPANYNAME THEN
              PlantType.CHANGECOMPANY(Resource.Company);
            PlantType.GET(Resource."Source No.");
            IF PlantType."VAT Prod. Posting Group" <> '' THEN BEGIN
              IF VATProductPostingGroup.GET(PlantType."VAT Prod. Posting Group") THEN
                EXIT(PlantType."VAT Prod. Posting Group");
            END;
            IF ResourceRequestType."VAT Prod. Posting Group Plant" <> '' THEN
              EXIT(ResourceRequestType."VAT Prod. Posting Group Plant");
          END;
        Resource.Type::Employee,
        Resource.Type::Subcontractor:
          IF ResourceRequestType."VAT Prod. Posting Group Labor" <> '' THEN
            EXIT(ResourceRequestType."VAT Prod. Posting Group Labor");
      END;
      Customer.GET("Resource Request"."Source No.");
      EXIT(Customer."VAT Prod. Posting Group");
    END;

    LOCAL PROCEDURE ItemVatProdPostingGroup@1100529900(Item@1100528301 : Record 27) : Code[10];
    VAR
      ResourceRequestType@1100529003 : Record 11124907;
      Customer@1100529004 : Record 18;
    BEGIN
      IF Item."VAT Prod. Posting Group" <> '' THEN
        EXIT(Item."VAT Prod. Posting Group");
      ResourceRequestType.GET("Resource Request"."Request Type");
      IF ResourceRequestType."VAT Prod. Posting Group Item" <> '' THEN
        EXIT(ResourceRequestType."VAT Prod. Posting Group Item");
      Customer.GET("Resource Request"."Source No.");
      EXIT(Customer."VAT Prod. Posting Group");
    END;

    LOCAL PROCEDURE TransportVatProdPostingGroup@1100529008(CostObject@1100528301 : Code[20]) : Code[10];
    VAR
      ResourceRequestType@1100529003 : Record 11124907;
      Customer@1100529004 : Record 18;
      DimVal@1100529001 : Record 349;
      DimensionMgt@1100529000 : Codeunit 408;
    BEGIN
      DimensionMgt.GetDimValueRec(2, CostObject, DimVal, FALSE, '');
      IF DimVal."VAT Prod. Posting Group" <> '' THEN
        EXIT(DimVal."VAT Prod. Posting Group");
      ResourceRequestType.GET("Resource Request"."Request Type");
      IF ResourceRequestType."VAT Prod. Posting Group Trans." <> '' THEN
        EXIT(ResourceRequestType."VAT Prod. Posting Group Trans.");
      Customer.GET("Resource Request"."Source No.");
      EXIT(Customer."VAT Prod. Posting Group");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

