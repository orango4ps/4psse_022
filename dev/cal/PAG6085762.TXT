OBJECT Page 6085762 Table Filter Field List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.5.00.04;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Tabellenfilter;
               ENU=Table Filters;
               NLD=Tabelfilterveldoverzicht;
               SVE=Tabellfilter];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table2000000041;
    PageType=List;
    OnAfterGetRecord=VAR
                       TableFilterField@161024012 : Record 6085762;
                       RecIDMgt@1160040000 : Codeunit 6085604;
                     BEGIN
                       TableFilterField.SETRANGE("Table Filter GUID",TableGUID);
                       TableFilterField.SETRANGE("Field No.","No.");
                       IF NOT TableFilterField.FINDFIRST THEN
                         CLEAR(TableFilterField);
                       TableFilterField.GetValues(Value,FilterType);
                       IF Type = Type::Option THEN
                         Value := RecIDMgt.IntToOptionText(TableNo,"No.",Value);
                     END;

  }
  CONTROLS
  {
    { 161024012;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 161024013;1;Group   ;
                Name=Group;
                GroupType=Repeater }

    { 161024014;2;Field   ;
                Name=No.;
                CaptionML=[DEU=Nr.;
                           ENU=No.;
                           NLD=Nr.;
                           SVE=Nr];
                SourceExpr="No.";
                Editable=FALSE }

    { 161024015;2;Field   ;
                Name=Field Caption;
                CaptionML=[DEU=Caption;
                           ENU=Caption;
                           NLD=Opschrift;
                           SVE=Rubrik];
                SourceExpr="Field Caption";
                Editable=FALSE }

    { 161024017;2;Field   ;
                Name=FilterTypeColumn;
                CaptionML=[DEU=Filterart;
                           ENU=Filter Type;
                           NLD=Filtersoort;
                           SVE=Filtertyp];
                OptionCaptionML=[DEU=Fester Filter,Belegfeld;
                                 ENU=Fixed Filter,Document Field;
                                 NLD=Vast filter,Documentveld];
                SourceExpr=FilterType;
                Visible=ShowType }

    { 161024016;2;Field   ;
                Name=Value;
                CaptionML=[DEU=Filter;
                           ENU=Filter;
                           NLD=Filter;
                           SVE=Filtrera];
                SourceExpr=Value;
                OnValidate=BEGIN
                             SetValue(Value);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=BEGIN
                           EXIT(LookupValue(Text));
                         END;
                          }

  }
  CODE
  {
    VAR
      Text001@161024015 : TextConst 'DEU=Der Prim„rschlssel der Tabelle darf nicht mehr als ein Feld enthalten.;ENU=The primary key of the table cannot contain more than one field.;NLD=De primaire sleutel van de tabel kan niet meer dan ‚‚n veld bevatten.';
      Text002@161024014 : TextConst 'DEU=Lookup in diesem Feld ist nicht m”glich.;ENU=Lookup in this field is not possible.;NLD=Opzoeken is niet mogelijk in dit veld.';
      TableGUID@161024018 : GUID;
      Value@161024017 : Text[250];
      FilterType@161024016 : 'FixedFilter,DocumentField';
      TemplateNo@161024013 : Code[20];
      TemplFieldType@161024012 : 'Header,Line';
      ShowType@161024019 : Boolean INDATASET;

    PROCEDURE SetParam@161024012(NewTemplateNo@1160040001 : Code[20];NewTemplFieldType@1160040002 : 'Header,Line';NewGUID@161024012 : GUID;NewShowType@1160040000 : Boolean);
    BEGIN
      TemplateNo := NewTemplateNo;
      TableGUID := NewGUID;
      TemplFieldType := NewTemplFieldType;
      ShowType := NewShowType;
    END;

    PROCEDURE GetTableFilterID@161024013() : GUID;
    VAR
      TableFilterField@161024012 : Record 6085762;
      EmptyGUID@161024013 : GUID;
    BEGIN
      TableFilterField.SETRANGE("Table Filter GUID",TableGUID);
      IF NOT TableFilterField.ISEMPTY THEN
        EXIT(TableGUID);
    END;

    PROCEDURE SetValue@1160040003(NewValue@1160040000 : Text[250]);
    VAR
      TableFilterField@1160040001 : Record 6085762;
    BEGIN
      TableFilterField.SETRANGE("Table Filter GUID",TableGUID);
      TableFilterField.SETRANGE("Field No.","No.");
      IF (NewValue = '') AND (FilterType = 0) THEN BEGIN
        IF TableFilterField.FINDFIRST THEN
          TableFilterField.DELETE;
        EXIT;
      END;

      IF NOT TableFilterField.FINDFIRST THEN BEGIN
        TableFilterField."Table Filter GUID" := TableGUID;
        TableFilterField."Table No." := TableNo;
        TableFilterField."Field No." := "No.";
        TableFilterField.INSERT;
      END;

      IF TableFilterField.SetValues(NewValue,FilterType,TemplateNo,TemplFieldType) THEN
        TableFilterField.MODIFY(TRUE)
      ELSE
        TableFilterField.DELETE;
    END;

    PROCEDURE LookupValue@161024018(VAR NewValue@1160040003 : Text[250]) : Boolean;
    VAR
      TempLookupRecID@161024018 : Record 6085760;
      TableFilterField@161024017 : Record 6085762;
      TemplField@161024016 : Record 6085580;
      RecIDMgt@161024015 : Codeunit 6085604;
      RecRef@161024014 : RecordRef;
      KeyRef@161024013 : KeyRef;
      FieldRef@161024012 : FieldRef;
    BEGIN
      IF FilterType = 0 THEN BEGIN
        IF Type = Type::Option THEN BEGIN
          RecIDMgt.LookupOptionString(TableNo,"No.",NewValue);
          EXIT(TRUE);
        END;

        TempLookupRecID."Table No." := GetTableNo;

        IF TempLookupRecID."Table No." = 0 THEN
          ERROR(Text002);

        TempLookupRecID."Record ID Tree ID" :=
          RecIDMgt.GetRecIDTreeID2(TempLookupRecID."Table No.","No.",TableGUID,NewValue);

        CODEUNIT.RUN(CODEUNIT::"Record ID Lookup",TempLookupRecID);

        IF TempLookupRecID."Lookup Mode" = TempLookupRecID."Lookup Mode"::OK THEN BEGIN
          IF RecIDMgt.GetTableNoFromRecID(TempLookupRecID."Record ID Tree ID") = 0 THEN
            EXIT;
          RecRef.OPEN(RecIDMgt.GetTableNoFromRecID(TempLookupRecID."Record ID Tree ID"));
          KeyRef := RecRef.KEYINDEX(RecRef.CURRENTKEYINDEX);
          IF KeyRef.FIELDCOUNT > 1 THEN
            ERROR(Text001);
          FieldRef := KeyRef.FIELDINDEX(1);
          NewValue := RecIDMgt.GetKeyValue(TempLookupRecID."Record ID Tree ID",FieldRef.NUMBER);
          EXIT(TRUE);
        END;
      END ELSE BEGIN
        TemplField.SETRANGE("Template No.",TemplateNo);
        TemplField.SETRANGE(Type,TemplFieldType);
        IF NewValue <> '' THEN
          IF TemplField.GET(TemplateNo,TemplFieldType,NewValue) THEN;
        IF PAGE.RUNMODAL(0,TemplField) = ACTION::LookupOK THEN BEGIN
          NewValue := TemplField.Code;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE GetTableNo@1160040000() : Integer;
    VAR
      RecRef@1160040000 : RecordRef;
      KeyRef@1160040001 : KeyRef;
      FieldRef@1160040002 : FieldRef;
    BEGIN
      IF RelationTableNo <> 0 THEN
        EXIT(RelationTableNo);

      RecRef.OPEN(TableNo);
      KeyRef := RecRef.KEYINDEX(RecRef.CURRENTKEYINDEX);
      FieldRef := KeyRef.FIELDINDEX(1);
      IF "No." =  FieldRef.NUMBER THEN
        EXIT(TableNo)
      ELSE
        EXIT(RelationTableNo);
    END;

    BEGIN
    END.
  }
}

