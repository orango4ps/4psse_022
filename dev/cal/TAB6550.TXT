OBJECT Table 6550 Whse. Item Tracking Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               TESTFIELD("Quantity Handled (Base)",0);
             END;

    CaptionML=[ENU=Whse. Item Tracking Line;
               NOR=Varesporingslinje - lager];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              NOR=L›penr.;
                                                              SVE=L”pnr] }
    { 2   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr] }
    { 3   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod] }
    { 4   ;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                IF "Quantity (Base)" < 0 THEN
                                                                  FIELDERROR("Quantity (Base)",Text004);

                                                                IF "Quantity (Base)" < "Quantity Handled (Base)" THEN
                                                                  FIELDERROR("Quantity (Base)",STRSUBSTNO(Text002,FIELDCAPTION("Quantity Handled (Base)")));

                                                                CheckSerialNoQty;

                                                                InitQtyToHandle;
                                                              END;

                                                   CaptionML=[ENU=Quantity (Base);
                                                              NOR=Antall (lagerenhet);
                                                              SVE=Antal (bas)];
                                                   DecimalPlaces=0:5 }
    { 7   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp] }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=[ENU=Source Subtype;
                                                              NOR=Kildeundertype;
                                                              SVE=Ursprungssubtyp];
                                                   OptionCaptionML=[ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    NOR=0,1,2,3,4,5,6,7,8,9,10;
                                                                    SVE=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=[ENU=Source ID;
                                                              NOR=Kilde-ID;
                                                              SVE=Ursprungs-ID] }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=[ENU=Source Batch Name;
                                                              NOR=Navn p† kildebunke;
                                                              SVE=Ursprungsjnlnamn] }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=[ENU=Source Prod. Order Line;
                                                              NOR=Kildeprod.ordrelinje;
                                                              SVE=Ursprungsprod.orderrad] }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=[ENU=Source Ref. No.;
                                                              NOR=Kilderef.nr.;
                                                              SVE=Ursprungsref.nr] }
    { 24  ;   ;Serial No.          ;Code50        ;OnValidate=BEGIN
                                                                IF "Serial No." <> xRec."Serial No." THEN BEGIN
                                                                  TESTFIELD("Quantity Handled (Base)",0);
                                                                  IF IsReclass("Source Type","Source Batch Name") THEN
                                                                    "New Serial No." := "Serial No.";
                                                                  CheckSerialNoQty;
                                                                  InitExpirationDate;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Serial No.;
                                                              NOR=Serienr.;
                                                              SVE=Serienr] }
    { 29  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              NOR=Antall per enhet;
                                                              SVE=Antal per enhet];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 40  ;   ;Warranty Date       ;Date          ;CaptionML=[ENU=Warranty Date;
                                                              NOR=Garantidato;
                                                              SVE=Garantidatum] }
    { 41  ;   ;Expiration Date     ;Date          ;OnValidate=BEGIN
                                                                "New Expiration Date" := "Expiration Date";
                                                              END;

                                                   CaptionML=[ENU=Expiration Date;
                                                              NOR=Utg†r den;
                                                              SVE=Utg†ngsdatum] }
    { 50  ;   ;Qty. to Handle (Base);Decimal      ;OnValidate=BEGIN
                                                                IF "Qty. to Handle (Base)" < 0 THEN
                                                                  FIELDERROR("Qty. to Handle (Base)",Text004);

                                                                IF "Qty. to Handle (Base)" > ("Quantity (Base)" - "Quantity Handled (Base)")
                                                                THEN
                                                                  ERROR(
                                                                    Text001,
                                                                    "Quantity (Base)" - "Quantity Handled (Base)");

                                                                "Qty. to Handle" := CalcQty("Qty. to Handle (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Handle (Base);
                                                              NOR=Ant. som skal h†ndt. (l.enh);
                                                              SVE=Ant. att hantera (bas)];
                                                   DecimalPlaces=0:5 }
    { 51  ;   ;Qty. to Invoice (Base);Decimal     ;CaptionML=[ENU=Qty. to Invoice (Base);
                                                              NOR=Ant. som skal fakt. (l.enh.);
                                                              SVE=Ant. att faktureras (bas)];
                                                   DecimalPlaces=0:5 }
    { 52  ;   ;Quantity Handled (Base);Decimal    ;CaptionML=[ENU=Quantity Handled (Base);
                                                              NOR=H†ndtert antall (l.enh.);
                                                              SVE=Antal hanterat (bas)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 53  ;   ;Quantity Invoiced (Base);Decimal   ;CaptionML=[ENU=Quantity Invoiced (Base);
                                                              NOR=Fakturert antall (l.enh.);
                                                              SVE=Antal fakturerat (bas)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 60  ;   ;Qty. to Handle      ;Decimal       ;CaptionML=[ENU=Qty. to Handle;
                                                              NOR=Ant. som skal h†ndt.;
                                                              SVE=Ant. att hantera];
                                                   DecimalPlaces=0:5 }
    { 70  ;   ;Buffer Status       ;Option        ;CaptionML=[ENU=Buffer Status;
                                                              NOR=Bufferstatus;
                                                              SVE=Bufferstatus];
                                                   OptionCaptionML=[ENU=" ,MODIFY";
                                                                    NOR=" ,ENDRE"];
                                                   OptionString=[ ,MODIFY];
                                                   Editable=No }
    { 71  ;   ;Buffer Status2      ;Option        ;CaptionML=[ENU=Buffer Status2;
                                                              NOR=Bufferstatus 2;
                                                              SVE=Buffertstatus2];
                                                   OptionCaptionML=[ENU=,ExpDate blocked;
                                                                    NOR=,ExpDate sperret;
                                                                    SVE=,ExpDate sp„rrat];
                                                   OptionString=,ExpDate blocked;
                                                   Editable=No }
    { 80  ;   ;New Serial No.      ;Code50        ;CaptionML=[ENU=New Serial No.;
                                                              NOR=Nytt serienr.;
                                                              SVE=Nytt serienr] }
    { 81  ;   ;New Lot No.         ;Code50        ;CaptionML=[ENU=New Lot No.;
                                                              NOR=Nytt partinr.;
                                                              SVE=Nytt partinr] }
    { 90  ;   ;Source Type Filter  ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Source Type Filter;
                                                              NOR=Kildetypefilter;
                                                              SVE=Ursprungstypsfilter];
                                                   OptionCaptionML=[ENU=" ,Receipt,Shipment,Internal Put-away,Internal Pick,Production,Movement Worksheet,Assembly";
                                                                    NOR=" ,Mottak,Levering,Intern plassering,Intern plukk,Produksjon,Flytteforslag,Montering"];
                                                   OptionString=[ ,Receipt,Shipment,Internal Put-away,Internal Pick,Production,Movement Worksheet,Assembly] }
    { 91  ;   ;Qty. Registered (Base);Decimal     ;CaptionML=[ENU=Qty. Registered (Base);
                                                              NOR=Registrert ant. (l.enh.)] }
    { 92  ;   ;Put-away Qty. (Base);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Activity Line"."Qty. Outstanding (Base)" WHERE (Activity Type=FILTER(Put-away),
                                                                                                                              Whse. Document Type=FIELD(Source Type Filter),
                                                                                                                              Whse. Document No.=FIELD(Source ID),
                                                                                                                              Whse. Document Line No.=FIELD(Source Ref. No.),
                                                                                                                              Serial No.=FIELD(Serial No.),
                                                                                                                              Lot No.=FIELD(Lot No.),
                                                                                                                              Action Type=FILTER(' '|Take)));
                                                   CaptionML=[ENU=Put-away Qty. (Base);
                                                              NOR=Plasseringsant. (l.enh.)] }
    { 93  ;   ;Pick Qty. (Base)    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Activity Line"."Qty. Outstanding (Base)" WHERE (Activity Type=FILTER(Pick|Movement),
                                                                                                                              Whse. Document Type=FIELD(Source Type Filter),
                                                                                                                              Whse. Document No.=FIELD(Source ID),
                                                                                                                              Whse. Document Line No.=FIELD(Source Ref. No.),
                                                                                                                              Serial No.=FIELD(Serial No.),
                                                                                                                              Lot No.=FIELD(Lot No.),
                                                                                                                              Action Type=FILTER(' '|Place)));
                                                   CaptionML=[ENU=Pick Qty. (Base);
                                                              NOR=Plukkant. (l.enh.);
                                                              SVE=Plockningsant. (bas)] }
    { 94  ;   ;Created by Whse. Activity Line;Boolean;
                                                   CaptionML=[ENU=Created by Whse. Activity Line;
                                                              NOR=Opprettet av lageraktivitetslinje] }
    { 5400;   ;Lot No.             ;Code50        ;OnValidate=BEGIN
                                                                IF "Lot No." <> xRec."Lot No." THEN BEGIN
                                                                  TESTFIELD("Quantity Handled (Base)",0);
                                                                  IF IsReclass("Source Type","Source Batch Name") THEN
                                                                    "New Lot No." := "Lot No.";
                                                                  InitExpirationDate;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Lot No.;
                                                              NOR=Partinr.;
                                                              SVE=Partinr] }
    { 5401;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              NOR=Variantkode;
                                                              SVE=Variantkod] }
    { 6505;   ;New Expiration Date ;Date          ;CaptionML=[ENU=New Expiration Date;
                                                              NOR=Ny utl›psdato;
                                                              SVE=Nytt utg†ngsdatum] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Source ID,Source Type,Source Subtype,Source Batch Name,Source Prod. Order Line,Source Ref. No.,Location Code;
                                                   SumIndexFields=Quantity (Base),Qty. to Handle (Base),Qty. to Invoice (Base),Quantity Handled (Base),Quantity Invoiced (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Serial No.,Lot No.                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=You cannot handle more than %1 units.;NOR=Du kan ikke h†ndtere mer enn %1 enheter.;SVE=Du kan inte hantera mer „n %1 enheter.';
      Text002@1000 : TextConst 'ENU=must not be less than %1;NOR=kan ikke v‘re mindre enn %1;SVE=f†r inte vara mindre „n %1';
      Text003@1002 : TextConst 'ENU=%1 must be 0 or 1 when %2 is stated.;NOR=%1 m† v‘re 0 eller 1n†r %2 er oppgitt.';
      Text004@1004 : TextConst 'ENU=must not be negative;NOR=kan ikke v‘re negativ';
      UOMMgt@1003 : Codeunit 5402;

    LOCAL PROCEDURE CheckSerialNoQty@1();
    BEGIN
      IF "Serial No." <> '' THEN
        IF NOT ("Quantity (Base)" IN [0,1]) THEN
          ERROR(Text003,FIELDCAPTION("Quantity (Base)"),FIELDCAPTION("Serial No."));
    END;

    LOCAL PROCEDURE CalcQty@14(BaseQty@1000 : Decimal) : Decimal;
    BEGIN
      IF "Qty. per Unit of Measure" = 0 THEN
        "Qty. per Unit of Measure" := 1;
      EXIT(ROUND(BaseQty / "Qty. per Unit of Measure",UOMMgt.QtyRndPrecision));
    END;

    [External]
    PROCEDURE InitQtyToHandle@15();
    BEGIN
      "Qty. to Handle (Base)" := "Quantity (Base)" - "Quantity Handled (Base)";
      "Qty. to Handle" := CalcQty("Qty. to Handle (Base)");

      OnAfterInitQtyToHandle(Rec,xRec,CurrFieldNo);
    END;

    [External]
    PROCEDURE InitExpirationDate@2();
    VAR
      Location@1003 : Record 14;
      ItemTrackingMgt@1001 : Codeunit 6500;
      ExpDate@1000 : Date;
      WarDate@1004 : Date;
    BEGIN
      IF ("Serial No." = xRec."Serial No.") AND ("Lot No." = xRec."Lot No.") THEN
        EXIT;

      "Expiration Date" := 0D;
      "Buffer Status2" := 0;

      Location.INIT;
      IF "Location Code" <> '' THEN
        Location.GET("Location Code");

      IF ItemTrackingMgt.GetWhseExpirationDate("Item No.","Variant Code",Location,"Lot No.","Serial No.",ExpDate) THEN BEGIN
        "Expiration Date" := ExpDate;
        "Buffer Status2" := "Buffer Status2"::"ExpDate blocked";
      END;

      IF IsReclass("Source Type","Source Batch Name") THEN BEGIN
        "New Expiration Date" := "Expiration Date";
        IF ItemTrackingMgt.GetWhseWarrantyDate("Item No.","Variant Code",Location,"Lot No.","Serial No.",WarDate) THEN
          "Warranty Date" := WarDate;
      END;
    END;

    [External]
    PROCEDURE IsReclass@4(SourceType@1001 : Integer;SourceBatchName@1002 : Code[10]) : Boolean;
    VAR
      WhseJnlLine@1000 : Record 7311;
    BEGIN
      IF SourceType = DATABASE::"Warehouse Journal Line" THEN
        EXIT(WhseJnlLine.IsReclass(SourceBatchName));

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE LookUpTrackingSummary@50(VAR WhseItemTrackingLine@1000 : Record 6550;TrackingType@1001 : 'SerialNo,LotNo';MaxQuantity@1002 : Decimal;SignFactor@1003 : Integer;SearchForSupply@1004 : Boolean);
    VAR
      TempTrackingSpecification@1005 : TEMPORARY Record 336;
      WhseJnlLine@1006 : Record 7311;
      WhseWorksheetLine@1007 : Record 7326;
      WhseInternalPutawayLine@1010 : Record 7332;
      InternalMovementLine@1011 : Record 7347;
      ItemTrackingDataCollection@1008 : Codeunit 6501;
      BinCode@1009 : Code[20];
      IsHandled@1012 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeLookUpTrackingSummary(WhseItemTrackingLine,TrackingType,MaxQuantity,SignFactor,TRUE,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH WhseItemTrackingLine DO BEGIN
        CASE "Source Type" OF
          DATABASE::"Warehouse Journal Line":
            BEGIN
              WhseJnlLine.GET("Source Batch Name","Source ID","Location Code","Source Ref. No.");
              BinCode := WhseJnlLine."Bin Code";
            END;
          DATABASE::"Whse. Worksheet Line":
            BEGIN
              WhseWorksheetLine.GET("Source Batch Name","Source ID","Location Code","Source Ref. No.");
              BinCode := WhseWorksheetLine."From Bin Code";
            END;
          DATABASE::"Whse. Internal Put-away Line":
            BEGIN
              WhseInternalPutawayLine.GET("Source ID","Source Ref. No.");
              BinCode := WhseInternalPutawayLine."From Bin Code";
            END;
          DATABASE::"Internal Movement Line":
            BEGIN
              InternalMovementLine.GET("Source ID","Source Ref. No.");
              BinCode := InternalMovementLine."From Bin Code";
            END;
          ELSE
            EXIT;
        END;

        TempTrackingSpecification.INIT;
        TempTrackingSpecification.SetItemData(
          "Item No.",Description,"Location Code","Variant Code",BinCode,"Qty. per Unit of Measure");
        TempTrackingSpecification.SetSource(
          "Source Type","Source Subtype","Source ID","Source Ref. No.","Source Batch Name","Source Prod. Order Line");
        TempTrackingSpecification."Quantity (Base)" := "Quantity (Base)";
        TempTrackingSpecification."Qty. to Handle" := "Qty. to Handle";
        TempTrackingSpecification."Qty. to Handle (Base)" := "Qty. to Handle (Base)";
        CLEAR(ItemTrackingDataCollection);
        ItemTrackingDataCollection.AssistEditTrackingNo(
          TempTrackingSpecification,SearchForSupply,SignFactor,TrackingType,MaxQuantity);
        VALIDATE("Quantity (Base)",TempTrackingSpecification."Quantity (Base)");
        CASE TrackingType OF
          TrackingType::SerialNo:
            IF TempTrackingSpecification."Serial No." <> '' THEN
              VALIDATE("Serial No.",TempTrackingSpecification."Serial No.");
          TrackingType::LotNo:
            IF TempTrackingSpecification."Lot No." <> '' THEN
              VALIDATE("Lot No.",TempTrackingSpecification."Lot No.");
        END;
      END;

      OnAfterLookUpTrackingSummary(WhseItemTrackingLine,TrackingType,TempTrackingSpecification);
    END;

    [External]
    PROCEDURE ClearTrackingFilter@35();
    BEGIN
      SETRANGE("Serial No.");
      SETRANGE("Lot No.");
    END;

    [External]
    PROCEDURE CopyTrackingFromItemLedgEntry@7(ItemLedgerEntry@1000 : Record 32);
    BEGIN
      "Serial No." := ItemLedgerEntry."Serial No.";
      "Lot No." := ItemLedgerEntry."Lot No.";
      "Warranty Date" := ItemLedgerEntry."Warranty Date";
      "Expiration Date" := ItemLedgerEntry."Expiration Date";

      OnAfterCopyTrackingFromItemLedgEntry(Rec,ItemLedgerEntry);
    END;

    [External]
    PROCEDURE CopyTrackingFromReservEntry@6(ReservEntry@1000 : Record 337);
    BEGIN
      "Serial No." := ReservEntry."Serial No.";
      "Lot No." := ReservEntry."Lot No.";
      "Warranty Date" := ReservEntry."Warranty Date";
      "Expiration Date" := ReservEntry."Expiration Date";

      OnAfterCopyTrackingFromReservEntry(Rec,ReservEntry);
    END;

    [External]
    PROCEDURE SetSource@21(SourceType@1000 : Integer;SourceSubtype@1001 : Integer;SourceID@1002 : Code[20];SourceRefNo@1003 : Integer;SourceBatchName@1004 : Code[10];SourceProdOrderLine@1005 : Integer);
    BEGIN
      "Source Type" := SourceType;
      "Source Subtype" := SourceSubtype;
      "Source ID" := SourceID;
      "Source Ref. No." := SourceRefNo;
      "Source Batch Name" := SourceBatchName;
      "Source Prod. Order Line" := SourceProdOrderLine;
    END;

    [External]
    PROCEDURE SetSourceFilter@31(SourceType@1011 : Integer;SourceSubtype@1010 : Integer;SourceID@1009 : Code[20];SourceRefNo@1008 : Integer;SourceKey@1000 : Boolean);
    BEGIN
      IF SourceKey THEN
        SETCURRENTKEY(
          "Source ID","Source Type","Source Subtype","Source Batch Name",
          "Source Prod. Order Line","Source Ref. No.");
      SETRANGE("Source Type",SourceType);
      IF SourceSubtype >= 0 THEN
        SETRANGE("Source Subtype",SourceSubtype);
      SETRANGE("Source ID",SourceID);
      IF SourceRefNo >= 0 THEN
        SETRANGE("Source Ref. No.",SourceRefNo);
    END;

    [External]
    PROCEDURE SetSourceFilter2@33(SourceBatchName@1007 : Code[10];SourceProdOrderLine@1006 : Integer);
    BEGIN
      SETRANGE("Source Batch Name",SourceBatchName);
      IF SourceProdOrderLine >= 0 THEN
        SETRANGE("Source Prod. Order Line",SourceProdOrderLine);
    END;

    [External]
    PROCEDURE SetTracking@5(SerialNo@1001 : Code[50];LotNo@1000 : Code[50];WarrantyDate@1002 : Date;ExpirationDate@1003 : Date);
    BEGIN
      "Serial No." := SerialNo;
      "Lot No." := LotNo;
      "Warranty Date" := WarrantyDate;
      "Expiration Date" := ExpirationDate;
    END;

    [External]
    PROCEDURE SetTrackingFilter@34(SerialNo@1000 : Code[50];LotNo@1001 : Code[50]);
    BEGIN
      SETRANGE("Serial No.",SerialNo);
      SETRANGE("Lot No.",LotNo);
    END;

    [External]
    PROCEDURE SetTrackingFilterFromRelation@9(WhseItemEntryRelation@1000 : Record 6509);
    BEGIN
      SETRANGE("Serial No.",WhseItemEntryRelation."Serial No.");
      SETRANGE("Lot No.",WhseItemEntryRelation."Lot No.");
    END;

    [External]
    PROCEDURE SetTrackingFilterFromReservEntry@10(ReservEntry@1000 : Record 337);
    BEGIN
      SETRANGE("Serial No.",ReservEntry."Serial No.");
      SETRANGE("Lot No.",ReservEntry."Lot No.");
    END;

    [External]
    PROCEDURE SetTrackingFilterFromSpec@8(WhseItemTrackingLine@1000 : Record 6550);
    BEGIN
      SETRANGE("Serial No.",WhseItemTrackingLine."Serial No.");
      SETRANGE("Lot No.",WhseItemTrackingLine."Lot No.");
    END;

    [External]
    PROCEDURE TrackingExists@3() : Boolean;
    BEGIN
      EXIT(("Lot No." <> '') OR ("Serial No." <> ''));
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromItemLedgEntry@11(VAR WhseItemTrackingLine@1000 : Record 6550;ItemLedgerEntry@1001 : Record 32);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromReservEntry@12(VAR WhseItemTrackingLine@1000 : Record 6550;ReservationEntry@1001 : Record 337);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitQtyToHandle@16(VAR WhseItemTrackingLine@1000 : Record 6550;xWhseItemTrackingLine@1001 : Record 6550;CallingFieldNo@1002 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterLookUpTrackingSummary@17(VAR WhseItemTrackingLine@1000 : Record 6550;TrackingType@1001 : 'SerialNo,LotNo';VAR TempTrackingSpecification@1002 : TEMPORARY Record 336);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeLookUpTrackingSummary@13(VAR WhseItemTrackingLine@1004 : Record 6550;TrackingType@1003 : 'SerialNo,LotNo';MaxQuantity@1002 : Decimal;SignFactor@1001 : Integer;SearchForSupply@1000 : Boolean;VAR IsHandled@1005 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

