OBJECT Codeunit 1265 Bank Data Conv. Serv. Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1260=r;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      MissingCredentialsQst@1000 : TextConst 'ENU=The %1 is missing the user name or password. Do you want to open the %1 page?;NOR=%1 mangler brukernavn eller passord. Vil du †pne %1-siden?;SVE=I %1 saknas anv„ndarnamn eller l”senord. Vill du ”ppna sidan %1?';
      MissingCredentialsErr@1001 : TextConst 'ENU=The user name and password must be filled in %1 page.;NOR=Brukernavn og passord m† angis p† %1-siden.;SVE=Anv„ndarnamn och l”senord m†ste fyllas i p† sidan %1.';
      ResultPathTxt@1002 : TextConst '@@@={Locked};ENU="/amc:%1/return/syslog[syslogtype[text()=""error""]]";NOR="/amc:%1/return/syslog[syslogtype[text()=""error""]]";SVE="/amc:%1/return/syslog[syslogtype[text()=""error""]]"';
      FinstaPathTxt@1003 : TextConst '@@@={Locked};ENU=/amc:%1/return/finsta/transactions;NOR=/amc:%1/return/finsta/transactions;SVE=/amc:%1/return/finsta/transactions';
      FinstaPathAPIXXTxt@1008 : TextConst '@@@={Locked};ENU=/amc:%1/return/finsta/statement/finstatransus;NOR=/amc:%1/return/finsta/statement/finstatransus';
      HeaderErrPathTxt@1005 : TextConst '@@@={Locked};ENU="/amc:%1/return/header/result[text()=""error""]";NOR="/amc:%1/return/header/result[text()=""error""]";SVE="/amc:%1/return/header/result[text()=""error""]"';
      ConvErrPathTxt@1004 : TextConst '@@@={Locked};ENU="/amc:%1/return/pack/convertlog[syslogtype[text()=""error""]]";NOR="/amc:%1/return/pack/convertlog[syslogtype[text()=""error""]]";SVE="/amc:%1/return/pack/convertlog[syslogtype[text()=""error""]]"';
      DataPathTxt@1006 : TextConst '@@@={Locked};ENU=/amc:%1/return/pack/data/text();NOR=/amc:%1/return/pack/data/text();SVE=/amc:%1/return/pack/data/text()';
      ApiVersionTxt@1007 : TextConst '@@@={Locked};ENU=nav02;NOR=nav02';

    [External]
    PROCEDURE InitDefaultURLs@11(VAR BankDataConvServiceSetup@1000 : Record 1260);
    BEGIN
      BankDataConvServiceSetup."Sign-up URL" := 'https://amcbanking.com/store/amc-banking/microsoft-dynamics-nav/version-2015-2016/';
      BankDataConvServiceSetup."Service URL" := 'https://nav.amcbanking.com/' + BankDataConvApiVersion;
      BankDataConvServiceSetup."Support URL" := 'http://www.amcbanking.dk/nav/support';
      BankDataConvServiceSetup."Namespace API Version" := BankDataConvApiVersion;
    END;

    [External]
    PROCEDURE SetURLsToDefault@2(VAR BankDataConvServiceSetup@1000 : Record 1260);
    BEGIN
      InitDefaultURLs(BankDataConvServiceSetup);
      BankDataConvServiceSetup.MODIFY;
    END;

    [External]
    PROCEDURE GetNamespace@1() : Text;
    BEGIN
      EXIT('http://' + BankDataConvApiVersion + '.soap.xml.link.amc.dk/');
    END;

    PROCEDURE GetSupportURL@3(XmlNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode") : Text;
    VAR
      BankDataConvServiceSetup@1003 : Record 1260;
      XMLDOMMgt@1002 : Codeunit 6224;
      SupportURL@1001 : Text;
    BEGIN
      SupportURL := XMLDOMMgt.FindNodeText(XmlNode,'url');
      IF SupportURL <> '' THEN
        EXIT(SupportURL);

      BankDataConvServiceSetup.GET;
      EXIT(BankDataConvServiceSetup."Support URL");
    END;

    [External]
    PROCEDURE CheckCredentials@4();
    VAR
      BankDataConvServiceSetup@1000 : Record 1260;
      CompanyInformationMgt@1001 : Codeunit 1306;
    BEGIN
      IF NOT BankDataConvServiceSetup.GET OR (NOT BankDataConvServiceSetup.HasPassword) OR (NOT BankDataConvServiceSetup.HasUserName)
      THEN BEGIN
        IF CompanyInformationMgt.IsDemoCompany THEN BEGIN
          BankDataConvServiceSetup.DELETEALL(TRUE);
          BankDataConvServiceSetup.INIT;
          BankDataConvServiceSetup.INSERT(TRUE);
        END ELSE
          IF CONFIRM(STRSUBSTNO(MissingCredentialsQst,BankDataConvServiceSetup.TABLECAPTION),TRUE) THEN BEGIN
            COMMIT;
            PAGE.RUNMODAL(PAGE::"Bank Data Conv. Service Setup",BankDataConvServiceSetup);
          END;

        IF NOT BankDataConvServiceSetup.GET OR NOT BankDataConvServiceSetup.HasPassword THEN
          ERROR(MissingCredentialsErr,BankDataConvServiceSetup.TABLECAPTION);
      END;
    END;

    [External]
    PROCEDURE GetErrorXPath@5(ResponseNode@1000 : Text) : Text;
    BEGIN
      EXIT(STRSUBSTNO(ResultPathTxt,ResponseNode));
    END;

    [External]
    PROCEDURE GetFinstaXPath@7(ResponseNode@1000 : Text) : Text;
    BEGIN
      IF BankDataConvApiVersion = ApiVersionTxt THEN
        EXIT(STRSUBSTNO(FinstaPathTxt,ResponseNode));
      EXIT(STRSUBSTNO(FinstaPathAPIXXTxt,ResponseNode));
    END;

    [External]
    PROCEDURE GetHeaderErrXPath@8(ResponseNode@1000 : Text) : Text;
    BEGIN
      EXIT(STRSUBSTNO(HeaderErrPathTxt,ResponseNode));
    END;

    [External]
    PROCEDURE GetConvErrXPath@9(ResponseNode@1000 : Text) : Text;
    BEGIN
      EXIT(STRSUBSTNO(ConvErrPathTxt,ResponseNode));
    END;

    [External]
    PROCEDURE GetDataXPath@6(ResponseNode@1000 : Text) : Text;
    BEGIN
      EXIT(STRSUBSTNO(DataPathTxt,ResponseNode));
    END;

    [EventSubscriber(Table,1400,OnRegisterServiceConnection)]
    [External]
    PROCEDURE HandleBankDataConvRegisterServiceConnection@10(VAR ServiceConnection@1000 : Record 1400);
    VAR
      BankDataConvServiceSetup@1001 : Record 1260;
      RecRef@1002 : RecordRef;
    BEGIN
      IF NOT BankDataConvServiceSetup.GET THEN
        BankDataConvServiceSetup.INSERT(TRUE);
      RecRef.GETTABLE(BankDataConvServiceSetup);

      ServiceConnection.Status := ServiceConnection.Status::Enabled;
      WITH BankDataConvServiceSetup DO BEGIN
        IF "Service URL" = '' THEN
          ServiceConnection.Status := ServiceConnection.Status::Disabled;

        ServiceConnection.InsertServiceConnection(
          ServiceConnection,RecRef.RECORDID,TABLENAME,"Service URL",PAGE::"Bank Data Conv. Service Setup");
      END;
    END;

    [External]
    PROCEDURE BankDataConvApiVersion@12() : Text[10];
    VAR
      BankDataConvServiceSetup@1000 : Record 1260;
    BEGIN
      IF BankDataConvServiceSetup.GET THEN
        IF BankDataConvServiceSetup."Namespace API Version" <> '' THEN
          EXIT(BankDataConvServiceSetup."Namespace API Version");

      EXIT(ApiVersionTxt);
    END;

    BEGIN
    END.
  }
}

