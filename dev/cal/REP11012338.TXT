OBJECT Report 11012338 Copy Sub-Estimate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kopieren Teilkalkulation;
               ENU=Copy Sub-Estimate;
               NLD=Deelcalculatie kopi‰ren;
               NOR=Kopier delkalkyle;
               SVE=Kopiera delkalkyl];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4767;    ;DataItem;Origin              ;
               DataItemTable=Table11072074;
               DataItemTableView=SORTING(Estimate No.,Sub-Estimate No.);
               OnPreDataItem=BEGIN
                               //db.sn, 09-07-15: C024532
                               SETRANGE("Estimate No.", EstFrom);
                               SETRANGE("Sub-Estimate No.", SubEstFrom);
                               CheckSelection;
                               CheckLevel(1);
                               //db.en, 09-07-15: C024532
                             END;

               OnAfterGetRecord=BEGIN
                                  EstFrom := "Estimate No.";
                                  SubEstFrom := "Sub-Estimate No.";
                                  StartUpdate;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528900;;Container;
                  ContainerType=ContentArea }

      { 1100528901;1;Group  ;
                  GroupType=Group }

      { 1100525011;2;Field  ;
                  CaptionClass=Text998;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100525010;2;Field  ;
                  Name=EstFrom;
                  CaptionML=[DEU=Kalkulation;
                             ENU=Estimate;
                             NLD=Calculatie;
                             NOR=Kalkyle;
                             SVE=Kalkyl];
                  SourceExpr=EstFrom;
                  OnValidate=BEGIN
                               OnValidateEstFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstFrom;
                           END;
                            }

      { 1100525009;2;Field  ;
                  Name=SubEstFrom;
                  CaptionML=[DEU=Teilkalkulation;
                             ENU=Sub-Estimate;
                             NLD=Deelcalculatie;
                             NOR=Delkalkyle;
                             SVE=Delkalkyl];
                  SourceExpr=SubEstFrom;
                  OnValidate=BEGIN
                               OnValidateSubEstFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupSubEstFrom;
                           END;
                            }

      { 1100525008;2;Field  ;
                  CaptionClass=Text999;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100525007;2;Field  ;
                  Name=EstTo;
                  CaptionML=[DEU=Kalkulation;
                             ENU=Estimate;
                             NLD=Calculatie;
                             NOR=Kalkyle;
                             SVE=Kalkyl];
                  SourceExpr=EstTo;
                  OnValidate=BEGIN
                               OnValidateEstTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstTo;
                           END;
                            }

      { 1100525006;2;Field  ;
                  Name=SubEstTo;
                  CaptionML=[DEU=Teilkalkulation;
                             ENU=Sub-Estimate;
                             NLD=Deelcalculatie;
                             NOR=Delkalkyle;
                             SVE=Delkalkyl];
                  SourceExpr=SubEstTo;
                  OnValidate=BEGIN
                               OnValidateSubEstTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupSubEstTo;
                           END;
                            }

      { 1100525005;2;Field  ;
                  CaptionML=[DEU=Beschreibung Teilkalkulation;
                             ENU=Description Sub-Estimate;
                             NLD=Omschrijving deelcalculatie];
                  SourceExpr=SubEstToTxt }

      { 1100525004;2;Field  ;
                  Name=NormGroupTo;
                  CaptionML=[DEU=Normensatz;
                             ENU=Norm Group;
                             NLD=Normenset;
                             NOR=Normgruppe;
                             SVE=Normgrupp];
                  SourceExpr=NormGroupTo;
                  OnValidate=BEGIN
                               OnValidateNormGroupTo;
                             END;
                              }

      { 1100525003;2;Field  ;
                  Name=NormGroupSalesTo;
                  CaptionML=[DEU=Kopieren in;
                             ENU=Norm Group (Sales);
                             NLD=Normenset (verkoop);
                             SVE=Kopiera till];
                  SourceExpr=NormGroupSalesTo;
                  OnValidate=BEGIN
                               OnValidateNormGroupSalesTo;
                             END;
                              }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Nur Struktur kopieren;
                             ENU=Skip Lines;
                             NLD=Alleen structuur kopi‰ren;
                             NOR=Hopp over rader;
                             SVE=Hoppa ”ver rader];
                  SourceExpr=SkipLines }

      { 1100525001;2;Field  ;
                  CaptionML=[DEU=Preisvereinbarung kopieren;
                             ENU=Copy Price Agreement;
                             NLD=Prijsafspraak kopi‰ren;
                             NOR=Kopier prisavtale;
                             SVE=Kopiera prisavtal];
                  SourceExpr=CopyPriceAgreement }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=šbertragen Bemerkungen;
                             ENU=Transfer Comment Lines;
                             NLD=Overzetten opmerkingen;
                             NOR=Overf›r kommentarrader;
                             SVE=™verf”r kommentarsrader];
                  SourceExpr=TransferCommentLines }

      { 1100409001;2;Field  ;
                  CaptionML=[DEU=Verkauf von Kosten initialisieren;
                             ENU=Iintialize Sales From Cost;
                             NLD=Initialiseren verkoop vanuit kostprijs];
                  SourceExpr=InitSalesFromCost }

      { 1100409000;2;Field  ;
                  CaptionML=[DEU=Kopieren Auszugsblatt;
                             ENU=Copy Estimate Quantity Sheet;
                             NLD=Kopieer uittrekstaten;
                             NOR=Kopier kalkulert antallsspesifikasjon;
                             SVE=Kopiera kalkylerad antalsspecifikation];
                  SourceExpr=CopyEstQuanSheet }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      EstFrom@1100528918 : Code[20];
      EstTo@1100528917 : Code[20];
      SubEstFrom@1100528916 : Code[20];
      SubEstTo@1100528915 : Code[20];
      EstFromTxt@1100528914 : Text[50];
      EstToTxt@1100528913 : Text[50];
      SubEstFromTxt@1100528912 : Text[50];
      SubEstToTxt@1100528911 : Text[50];
      EstRec@1100528910 : Record 11012151;
      SubEstRec@1100528909 : Record 11072074;
      FormatMgt@1100528908 : Codeunit 11012022;
      SkipLines@1100528907 : Boolean;
      NormGroupFrom@1100528906 : Code[20];
      NormGroupTo@1100528905 : Code[20];
      NormGroupFromTxt@1100528904 : Text[50];
      NormGroupToTxt@1100528903 : Text[50];
      NormGroupRec@1100528902 : Record 11012164;
      CopyPriceAgreement@1100528901 : Boolean;
      TransferCommentLines@1100528900 : Boolean;
      Text000@1100528919 : TextConst 'DEU=Fehlende šbereinstimmung Ursprung-Bestimmung;ENU=Mismatch selection origin-destination;NLD=Ongeldige selectie bron-bestemming';
      Text998@1100525005 : TextConst 'DEU=Kopieren von;ENU=Copy From;NLD=Kopieer van;NOR=Kopier fra;SVE=Kopiera fr†n';
      Text999@1100528920 : TextConst 'DEU=Kopieren in;ENU=Copy To;NLD=Kopieer naar;NOR=Kopier til;SVE=Kopiera till';
      NormGroupSalesFrom@1100525003 : Code[20];
      NormGroupSalesTo@1100525002 : Code[20];
      NormGroupSalesFromTxt@1100525001 : Text[50];
      NormGroupSalesToTxt@1100525000 : Text[50];
      CopyEstQuanSheet@1100409000 : Boolean;
      InitSalesFromCost@1100409002 : Boolean;
      EstSetup@1100409001 : Record 11012150;

    PROCEDURE FillDefaults@1(lEstFrom@11012000 : Code[20];lSubEstFrom@11012001 : Code[20]);
    BEGIN
      EstFrom := lEstFrom;
      SubEstFrom := lSubEstFrom;

      EstSetup.GET;
      InitSalesFromCost := EstSetup."Init Sales From Cost";  //db, 03-02-15: C020959

      IF EstRec.GET(EstFrom) THEN BEGIN
        EstFromTxt := EstRec.Description;
        //db.sn, 10-02-15: C009772
        NormGroupFrom := EstRec."Norm Group";
        IF NormGroupRec.GET(NormGroupFrom) THEN
          NormGroupFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        NormGroupSalesFrom := EstRec."Norm Group (Sales)";
        IF NormGroupRec.GET(NormGroupFrom) THEN
          NormGroupSalesFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        //db.en, 10-02-15: C009772
      END;

      IF SubEstRec.GET(EstFrom,SubEstFrom) THEN BEGIN
        SubEstFromTxt := SubEstRec.Description;
        //db.sn, 10-02-15: C009772
        IF SubEstRec."Norm Group" <> '' THEN BEGIN
          NormGroupFrom := SubEstRec."Norm Group";
          IF NormGroupRec.GET(NormGroupFrom) THEN
            NormGroupFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        END;
        IF SubEstRec."Norm Group (Sales)" <> '' THEN BEGIN
          NormGroupSalesFrom := SubEstRec."Norm Group (Sales)";
          IF NormGroupRec.GET(NormGroupFrom) THEN
            NormGroupSalesFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        END;
        //db.en, 10-02-15: C009772
      END;

      EstTo := EstFrom;
      SubEstTo := SubEstFrom;
      EstToTxt := EstFromTxt;
      SubEstToTxt := SubEstFromTxt;

      CheckDefaultNormGroup;  //db, 10-02-15: C009772
    END;

    PROCEDURE ValidateEstimate@1210190001();
    VAR
      EstLineRec@1210190000 : Record 11072072;
      lvText000@1210190001 : TextConst 'DEU=Es gibt bereits Kalkulationszeilen in angegebener Bestimmung.;ENU=Estimate Lines present in Destination.;NLD=Er zijn reeds calculatieregels aanwezig in opgegeven bestemming.;NOR=Det finnes kalkylerader i Destinasjonen.;SVE=Det finns kalkylrader i destinationen.';
      SubEstRec@1100525000 : Record 11072074;
      LineExist@1100525001 : Boolean;
      lvText001@1100525002 : TextConst 'DEU=Es gibt bereits Kalkulationszeilen in angegebener Bestimmung. Fortfahren?;ENU="Estimate Lines present in Destination; Continue ?";NLD="Er zijn reeds calculatieregels aanwezig in opgegeven bestemming; doorgaan ?";NOR=Det finnes kalkylerader i Destinasjonen. Vil du fortsette?;SVE=Det finns kalkylrader i destinationen. Vill du forts„tta?';
    BEGIN
      EstLineRec.RESET;
      EstLineRec.SETRANGE("Estimate No.", EstTo);
      EstLineRec.SETRANGE("Sub-Estimate No.", SubEstTo);
      IF EstLineRec.FINDFIRST THEN BEGIN
        LineExist := TRUE;
        IF NOT SkipLines THEN
          ERROR(lvText000);
      END;

      IF NOT SubEstRec.GET(EstFrom,SubEstFrom) THEN SubEstRec.INIT;
      EstLineRec.RESET;
      EstLineRec.SETRANGE("Estimate No.", EstTo);
      CASE SubEstRec.Level OF
        0,1: EstLineRec.SETRANGE("Chapter (Sub-Estimate)", SubEstTo);
        2: EstLineRec.SETRANGE("Paragraph (Sub-Estimate)", SubEstTo);
      END;
      IF EstLineRec.FINDFIRST THEN BEGIN
        LineExist := TRUE;
        IF NOT SkipLines THEN
          ERROR(lvText000);
      END;

      IF LineExist THEN BEGIN
        IF NOT CONFIRM(lvText001) THEN
          ERROR('');
      END;
    END;

    PROCEDURE StartUpdate@1210190000();
    VAR
      CopyMgt@1210190000 : Codeunit 11012227;
    BEGIN
      EstRec.GET(EstTo);
      EstRec.TESTFIELD(Fixed, FALSE);
      ValidateEstimate;
      CopyMgt.SetSelectionSubLevel(NormGroupFrom, NormGroupTo, NormGroupSalesFrom, NormGroupSalesTo);
      CopyMgt.SetTransferComments(TransferCommentLines);
      CopyMgt.SetCopyQuantitySheet(CopyEstQuanSheet);  //DP00686
      CopyMgt.SetInitSalesFromCost(InitSalesFromCost);  //db, 03-02-15: C020959
      CopyMgt.CopySubEst(EstFrom,SubEstFrom,EstTo,SubEstTo,SubEstToTxt,SkipLines,CopyPriceAgreement);
    END;

    PROCEDURE CheckLevel@1100485000(iAction@1100525000 : Integer);
    VAR
      lvText000@1100485000 : TextConst 'DEU=Quell- und Ziel-Teilkalkulation mssen auf der gleichen Ebene sein;ENU=Source and Target Sub-Estimate should be of the same level;NLD=Herkomst en bestemming van deelcalculatie moeten van hetzelfde niveau zijn';
      lvSubEstRec@1100485001 : TEMPORARY Record 11072074;
    BEGIN
      //db, 09-07-15: C024532
      WITH lvSubEstRec DO BEGIN
        FormatMgt.DetermineLevel(SubEstTo,Chapter,Paragraph,Level);
        IF Level > 0 THEN Level := Level - 1;
        SubEstRec.GET(EstFrom, SubEstFrom);
        IF Level <> SubEstRec.Level THEN BEGIN
          IF iAction = 1 THEN  //db, 21-09-15: C024532
            ERROR(lvText000);
          IF iAction = 0 THEN
            SubEstTo := SubEstFrom;
        END;
      END;
    END;

    PROCEDURE CheckSelection@1100528900();
    BEGIN
      IF ((EstFrom = '') AND (EstTo <> '')) OR
         ((EstFrom <> '') AND (EstTo = '')) OR
         ((SubEstFrom = '') AND (SubEstTo <> '')) THEN
        ERROR(Text000);

      IF (EstFrom = EstTo) AND  (SubEstFrom = SubEstTo) THEN
        ERROR(Text000);  //db, 21-09-15: C024532
    END;

    PROCEDURE "-CopyFrom-"@1100525010();
    BEGIN
    END;

    PROCEDURE OnValidateEstFrom@1100525009();
    BEGIN
      EstRec."No." := EstFrom;
      EstRec.DPA_FindRecWithDPAFilterAppld;

      EstFromTxt := EstRec.Description;
    END;

    PROCEDURE OnLookupEstFrom@1100525008();
    BEGIN
      EstRec.RESET;
      IF PAGE.RUNMODAL(0,EstRec) = ACTION::LookupOK THEN BEGIN
        EstFrom := EstRec."No.";
        EstFromTxt := EstRec.Description;
      END;
    END;

    PROCEDURE OnValidateSubEstFrom@1100525007();
    VAR
      Chapter@1100525002 : Code[20];
      Paragraph@1100525001 : Code[20];
      Level@1100525000 : Integer;
    BEGIN
      FormatMgt.FormatSubEstimate(SubEstFrom,Chapter,Paragraph,Level,EstFrom,FALSE);
      CheckLevel(0);
      SubEstRec.GET(EstFrom, SubEstFrom);
      SubEstFromTxt := SubEstRec.Description;
      SubEstToTxt := SubEstFromTxt;
    END;

    PROCEDURE OnLookupSubEstFrom@1100525002();
    BEGIN
      SubEstRec.RESET;
      SubEstRec.SETRANGE("Estimate No.",EstFrom);
      IF PAGE.RUNMODAL(0,SubEstRec) = ACTION::LookupOK THEN BEGIN
        EstRec.GET(SubEstRec."Estimate No.");
        EstFrom := EstRec."No.";
        EstFromTxt := EstRec.Description;
        SubEstFrom := SubEstRec."Sub-Estimate No.";
        SubEstFromTxt := SubEstRec.Description;
        SubEstToTxt := SubEstFromTxt;
      END;
    END;

    PROCEDURE "-CopyTo-"@1100525011();
    BEGIN
    END;

    PROCEDURE OnValidateEstTo@1100525000();
    BEGIN
      EstRec."No." := EstTo;
      EstRec.DPA_FindRecWithDPAFilterAppld;

      EstToTxt := EstRec.Description;
      CheckDefaultNormGroup;  //db, 10-02-15: C009772
    END;

    PROCEDURE OnLookupEstTo@1100525001();
    BEGIN
      EstRec.RESET;
      IF PAGE.RUNMODAL(0,EstRec) = ACTION::LookupOK THEN BEGIN
        EstTo := EstRec."No.";
        OnValidateEstTo;  //db, 10-02-15: C009772
      END;
    END;

    PROCEDURE OnValidateSubEstTo@1100525003();
    VAR
      Chapter@1100525002 : Code[20];
      Paragraph@1100525001 : Code[20];
      Level@1100525000 : Integer;
    BEGIN
      FormatMgt.FormatSubEstimate(SubEstTo,Chapter,Paragraph,Level,EstTo,FALSE);
      CheckLevel(0);
      IF SubEstRec.GET(EstTo, SubEstTo) THEN
        SubEstToTxt := SubEstRec.Description
      ELSE
        SubEstToTxt := SubEstFromTxt;
    END;

    PROCEDURE OnLookupSubEstTo@1100525004();
    BEGIN
      SubEstRec.RESET;
      SubEstRec.SETRANGE("Estimate No.",EstTo);
      IF PAGE.RUNMODAL(0,SubEstRec) = ACTION::LookupOK THEN BEGIN
        SubEstTo := SubEstRec."Sub-Estimate No.";
        OnValidateSubEstTo;  //db, 10-02-15: C009772
      END;
    END;

    PROCEDURE OnValidateNormGroupTo@1100525005();
    BEGIN
      IF NOT NormGroupRec.GET(NormGroupTo) THEN BEGIN
        NormGroupTo := NormGroupFrom;
        NormGroupToTxt := NormGroupFromTxt;
      END ELSE BEGIN
        NormGroupToTxt := COPYSTR(NormGroupRec.Description, 1, 50);
      END;
    END;

    PROCEDURE OnValidateNormGroupSalesTo@1100525006();
    BEGIN
      IF NOT NormGroupRec.GET(NormGroupSalesTo) THEN BEGIN
        NormGroupSalesTo := NormGroupSalesFrom;
        NormGroupSalesToTxt := NormGroupSalesFromTxt;
      END ELSE BEGIN
        NormGroupSalesToTxt := COPYSTR(NormGroupRec.Description, 1, 50);
      END;
    END;

    PROCEDURE CheckDefaultNormGroup@1100525012();
    VAR
      EstRecTo@1100525000 : Record 11012151;
    BEGIN
      //db, 10-02-15: C009772
      EstRecTo.GET(EstTo);
      NormGroupTo := EstRecTo."Norm Group";
      NormGroupSalesTo := EstRecTo."Norm Group (Sales)";

      OnValidateNormGroupTo;
      OnValidateNormGroupSalesTo;
    END;

    BEGIN
    {
      db, 10-02-15: From-Selection editable; overrule tablefilter by DataItemTableView to disable selection/filter DataItem of this report
    }
    END.
  }
  RDLDATA
  {
  }
}

