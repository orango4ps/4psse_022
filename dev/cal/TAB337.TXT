OBJECT Table 337 Reservation Entry
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ActionMessageEntry@1000 : Record 99000849;
             BEGIN
               ActionMessageEntry.SETCURRENTKEY("Reservation Entry");
               ActionMessageEntry.SETRANGE("Reservation Entry","Entry No.");
               ActionMessageEntry.DELETEALL;
             END;

    CaptionML=[ENU=Reservation Entry;
               NOR=Reservasjonspost;
               SVE=Reservationstransaktion];
    LookupPageID=Page497;
    DrillDownPageID=Page497;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No.;
                                                              NOR=Lõpenr.;
                                                              SVE=Lîpnr] }
    { 2   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr] }
    { 3   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=LagerstÑllekod] }
    { 4   ;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                Quantity := CalcReservationQuantity;
                                                                "Qty. to Handle (Base)" := "Quantity (Base)";
                                                                "Qty. to Invoice (Base)" := "Quantity (Base)";
                                                              END;

                                                   CaptionML=[ENU=Quantity (Base);
                                                              NOR=Antall (lagerenhet);
                                                              SVE=Antal (bas)];
                                                   DecimalPlaces=0:5 }
    { 5   ;   ;Reservation Status  ;Option        ;CaptionML=[ENU=Reservation Status;
                                                              NOR=Reservasjonsstatus;
                                                              SVE=Reservationsstatus];
                                                   OptionCaptionML=[ENU=Reservation,Tracking,Surplus,Prospect;
                                                                    NOR=Reservasjon,Sporing,Overskudd,Prospekt;
                                                                    SVE=Reservation,SpÜrning,ôverskott,Prospect];
                                                   OptionString=Reservation,Tracking,Surplus,Prospect }
    { 7   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 8   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettet den;
                                                              SVE=UpplÑggningsdatum] }
    { 9   ;   ;Transferred from Entry No.;Integer ;TableRelation="Reservation Entry";
                                                   CaptionML=[ENU=Transferred from Entry No.;
                                                              NOR=Overfõrt fra lõpenr.;
                                                              SVE=ôverfîrd frÜn lîpnr] }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp] }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=[ENU=Source Subtype;
                                                              NOR=Kildeundertype;
                                                              SVE=Ursprungssubtyp];
                                                   OptionCaptionML=[ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    NOR=0,1,2,3,4,5,6,7,8,9,10;
                                                                    SVE=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=[ENU=Source ID;
                                                              NOR=Kilde-ID;
                                                              SVE=Ursprungs-ID] }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=[ENU=Source Batch Name;
                                                              NOR=Navn pÜ kildebunke;
                                                              SVE=Ursprungsjnlnamn] }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=[ENU=Source Prod. Order Line;
                                                              NOR=Kildeprod.ordrelinje;
                                                              SVE=Ursprungsprod.orderrad] }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=[ENU=Source Ref. No.;
                                                              NOR=Kilderef.nr.;
                                                              SVE=Ursprungsref.nr] }
    { 16  ;   ;Item Ledger Entry No.;Integer      ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[ENU=Item Ledger Entry No.;
                                                              NOR=Varepostnr.;
                                                              SVE=Artikeltrans.lîpnr];
                                                   Editable=No }
    { 22  ;   ;Expected Receipt Date;Date         ;CaptionML=[ENU=Expected Receipt Date;
                                                              NOR=Forventet mottaksdato;
                                                              SVE=FîrvÑntat inleveransdatum] }
    { 23  ;   ;Shipment Date       ;Date          ;CaptionML=[ENU=Shipment Date;
                                                              NOR=Forsendelsesdato;
                                                              SVE=Utleveransdatum] }
    { 24  ;   ;Serial No.          ;Code50        ;CaptionML=[ENU=Serial No.;
                                                              NOR=Serienr.;
                                                              SVE=Serienr] }
    { 25  ;   ;Created By          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Created By");
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Created By;
                                                              NOR=Opprettet av;
                                                              SVE=Skapad av] }
    { 27  ;   ;Changed By          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Changed By");
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Changed By;
                                                              NOR=Endret av;
                                                              SVE=éndrat av] }
    { 28  ;   ;Positive            ;Boolean       ;CaptionML=[ENU=Positive;
                                                              NOR=Positiv;
                                                              SVE=Positiv];
                                                   Editable=No }
    { 29  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                Quantity := ROUND("Quantity (Base)" / "Qty. per Unit of Measure",UOMMgt.QtyRndPrecision);
                                                              END;

                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              NOR=Antall per enhet;
                                                              SVE=Antal per enhet];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 30  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5 }
    { 31  ;   ;Action Message Adjustment;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Action Message Entry".Quantity WHERE (Reservation Entry=FIELD(Entry No.),
                                                                                                          Calculation=CONST(Sum)));
                                                   CaptionML=[ENU=Action Message Adjustment;
                                                              NOR=Justering av handlingsmelding;
                                                              SVE=ètgÑrdsmedd.justering];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 32  ;   ;Binding             ;Option        ;CaptionML=[ENU=Binding;
                                                              NOR=Bundet til;
                                                              SVE=Bindning];
                                                   OptionCaptionML=[ENU=" ,Order-to-Order";
                                                                    NOR=" ,Ordre-til-ordre";
                                                                    SVE=" ,Order-till-Order"];
                                                   OptionString=[ ,Order-to-Order];
                                                   Editable=No }
    { 33  ;   ;Suppressed Action Msg.;Boolean     ;CaptionML=[ENU=Suppressed Action Msg.;
                                                              NOR=Utelatt handlingsmelding;
                                                              SVE=Undertryckt ÜtgÑrdsmedd.] }
    { 34  ;   ;Planning Flexibility;Option        ;CaptionML=[ENU=Planning Flexibility;
                                                              NOR=Planleggingsfleksibilitet;
                                                              SVE=Planeringsflexibilitet];
                                                   OptionCaptionML=[ENU=Unlimited,None;
                                                                    NOR=Ubegrenset,Ingen;
                                                                    SVE=ObegrÑnsad,Ingen];
                                                   OptionString=Unlimited,None }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;CaptionML=[ENU=Appl.-to Item Entry;
                                                              NOR=Utligningsvarepost;
                                                              SVE=Koppla till artikellîpnr] }
    { 40  ;   ;Warranty Date       ;Date          ;CaptionML=[ENU=Warranty Date Vendor;
                                                              NOR=Garantidato;
                                                              SVE=Garantidatum];
                                                   Editable=No }
    { 41  ;   ;Expiration Date     ;Date          ;CaptionML=[ENU=Expiration Date Vendor;
                                                              NOR=UtgÜr den;
                                                              SVE=UtgÜngsdatum];
                                                   Editable=No }
    { 50  ;   ;Qty. to Handle (Base);Decimal      ;CaptionML=[ENU=Qty. to Handle (Base);
                                                              NOR=Ant. som skal hÜndt. (l.enh);
                                                              SVE=Ant. att hantera (bas)];
                                                   DecimalPlaces=0:5 }
    { 51  ;   ;Qty. to Invoice (Base);Decimal     ;CaptionML=[ENU=Qty. to Invoice (Base);
                                                              NOR=Ant. som skal fakt. (l.enh.);
                                                              SVE=Ant. att faktureras (bas)];
                                                   DecimalPlaces=0:5 }
    { 53  ;   ;Quantity Invoiced (Base);Decimal   ;CaptionML=[ENU=Quantity Invoiced (Base);
                                                              NOR=Fakturert antall (l.enh.);
                                                              SVE=Antal fakturerat (bas)];
                                                   DecimalPlaces=0:5 }
    { 80  ;   ;New Serial No.      ;Code50        ;CaptionML=[ENU=New Serial No.;
                                                              NOR=Nytt serienr.;
                                                              SVE=Nytt serienr];
                                                   Editable=No }
    { 81  ;   ;New Lot No.         ;Code50        ;CaptionML=[ENU=New Lot No.;
                                                              NOR=Nytt partinr.;
                                                              SVE=Nytt partinr];
                                                   Editable=No }
    { 900 ;   ;Disallow Cancellation;Boolean      ;CaptionML=[ENU=Disallow Cancellation;
                                                              NOR=Ikke tillat annullering;
                                                              SVE=TillÜt inte annullering] }
    { 5400;   ;Lot No.             ;Code50        ;CaptionML=[ENU=Lot No.;
                                                              NOR=Partinr.;
                                                              SVE=Partinr] }
    { 5401;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              NOR=Variantkode;
                                                              SVE=Variantkod] }
    { 5811;   ;Appl.-from Item Entry;Integer      ;CaptionML=[ENU=Appl.-from Item Entry;
                                                              NOR=Utlignet fra-varepost;
                                                              SVE=Koppla frÜn artikellîpnr];
                                                   MinValue=0 }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=[ENU=Correction;
                                                              NOR=Korreksjon;
                                                              SVE=RÑttningstransaktion] }
    { 6505;   ;New Expiration Date ;Date          ;CaptionML=[ENU=New Expiration Date;
                                                              NOR=Ny utlõpsdato;
                                                              SVE=Nytt utgÜngsdatum];
                                                   Editable=No }
    { 6510;   ;Item Tracking       ;Option        ;CaptionML=[ENU=Item Tracking;
                                                              NOR=Varesporing;
                                                              SVE=ArtikelspÜrning];
                                                   OptionCaptionML=[ENU=None,Lot No.,Lot and Serial No.,Serial No.;
                                                                    NOR=Ingen,Partinummer,Parti- og serienummer,Serienummer;
                                                                    SVE=Inget,Partinr.,Parti- och serienr,Serienr.];
                                                   OptionString=None,Lot No.,Lot and Serial No.,Serial No.;
                                                   Editable=No }
    { 6511;   ;Untracked Surplus   ;Boolean       ;CaptionML=[ENU=Untracked Surplus;
                                                              NOR=Ikke-sporet overskudd] }
    { 11012010;;Warranty Code Vendor;Code10       ;TableRelation="Service Warranty";
                                                   CaptionML=[ENU=Warranty Code Vendor;
                                                              SVE=Grantikod leveranîr] }
    { 11012020;;Warranty Start Date Vendor;Date   ;CaptionML=[ENU=Warranty Start Date Vendor;
                                                              SVE=Garantistartdatum leverantîr] }
    { 11012030;;Warranty Period Vendor;DateFormula;CaptionML=[ENU=Warranty Period Vendor;
                                                              SVE=Grantiperiod leverantîr] }
    { 11012040;;Warranty Code Customer;Code10     ;TableRelation="Service Warranty";
                                                   CaptionML=[ENU=Warranty Code Customer;
                                                              SVE=Grantikod kund] }
    { 11012050;;Warranty Start Date Customer;Date ;CaptionML=[ENU=Warranty Start Date Customer;
                                                              SVE=Garantistartdatum kund] }
    { 11012060;;Warranty Period Customer;DateFormula;
                                                   CaptionML=[ENU=Warranty Period Customer;
                                                              SVE=Grantiperiod kund] }
    { 11012070;;Warranty Date Customer;Date       ;CaptionML=[ENU=Warranty Date Customer;
                                                              SVE=Grantidatum kund];
                                                   Editable=No }
    { 11012080;;Good Customs       ;Boolean       ;CaptionML=[ENU=Good Customs;
                                                              SVE=Varutull] }
    { 11012090;;Shipment with T1   ;Boolean       ;CaptionML=[ENU=Shipment with T1;
                                                              SVE=Frakt med T1] }
    { 11012100;;Customs Destination Code;Code20   ;CaptionML=[ENU=Customs Destination Code;
                                                              SVE=Tulls destinationskod] }
  }
  KEYS
  {
    {    ;Entry No.,Positive                      ;Clustered=Yes }
    {    ;Source ID,Source Ref. No.,Source Type,Source Subtype,Source Batch Name,Source Prod. Order Line,Reservation Status,Shipment Date,Expected Receipt Date;
                                                   SumIndexFields=Quantity (Base),Quantity,Qty. to Handle (Base),Qty. to Invoice (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Variant Code,Location Code     ;MaintainSIFTIndex=No }
    {    ;Item No.,Variant Code,Location Code,Reservation Status,Shipment Date,Expected Receipt Date,Serial No.,Lot No.;
                                                   SumIndexFields=Quantity (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Source Type,Source Subtype,Reservation Status,Location Code,Variant Code,Shipment Date,Expected Receipt Date,Serial No.,Lot No.;
                                                   SumIndexFields=Quantity (Base),Quantity;
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Variant Code,Location Code,Item Tracking,Reservation Status,Lot No.,Serial No.;
                                                   SumIndexFields=Quantity (Base);
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    { No ;Lot No.                                  }
    { No ;Serial No.                               }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Positive,Item No.,Description,Quantity }
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Line;NOR=Linje;SVE=Rad';
      UOMMgt@1001 : Codeunit 5402;

    [External]
    PROCEDURE TextCaption@1() : Text[255];
    VAR
      ItemLedgEntry@1000 : Record 32;
      SalesLine@1007 : Record 37;
      ReqLine@1006 : Record 246;
      PurchLine@1005 : Record 39;
      ItemJnlLine@1004 : Record 83;
      ProdOrderLine@1003 : Record 5406;
      ProdOrderComp@1002 : Record 5407;
      AssemblyHeader@1011 : Record 900;
      AssemblyLine@1012 : Record 901;
      TransLine@1001 : Record 5741;
      ServLine@1009 : Record 5902;
      ExtensionTextCaption@1008 : Text[255];
      JobJnlLine@1010 : Record 11072008;
      ProjectDeliveryNoteLine@1100528600 : Record 11012673;
      ProjectInventoryDelivery@1100528601 : Record 11012671;
      InvApprLine@1100529000 : Record 11012032;
    BEGIN
      CASE "Source Type" OF
        DATABASE::"Item Ledger Entry":
          EXIT(ItemLedgEntry.TABLECAPTION);
        DATABASE::"Sales Line":
          EXIT(SalesLine.TABLECAPTION);
        DATABASE::"Requisition Line":
          EXIT(ReqLine.TABLECAPTION);
        DATABASE::"Purchase Line":
          EXIT(PurchLine.TABLECAPTION);
        DATABASE::"Item Journal Line":
          EXIT(ItemJnlLine.TABLECAPTION);
        DATABASE::"Job Journal Line":
          EXIT(JobJnlLine.TABLECAPTION);
        DATABASE::"Prod. Order Line":
          EXIT(ProdOrderLine.TABLECAPTION);
        DATABASE::"Prod. Order Component":
          EXIT(ProdOrderComp.TABLECAPTION);
        DATABASE::"Assembly Header":
          EXIT(AssemblyHeader.TABLECAPTION);
        DATABASE::"Assembly Line":
          EXIT(AssemblyLine.TABLECAPTION);
        DATABASE::"Transfer Line":
          EXIT(TransLine.TABLECAPTION);
        DATABASE::"Service Line":
          EXIT(ServLine.TABLECAPTION);
        //**4PS.sn DP00121
        DATABASE::"Project Delivery Note Line":
          EXIT(ProjectDeliveryNoteLine.TABLECAPTION);
        DATABASE::"Project Inventory Delivery":
          EXIT(ProjectInventoryDelivery.TABLECAPTION);
        DATABASE::"Invoice Approval Line":  //C004829
          EXIT(InvApprLine.TABLECAPTION);
        //**4PS.en
        ELSE BEGIN
          OnAfterTextCaption("Source Type",ExtensionTextCaption);
          IF ExtensionTextCaption <> '' THEN
            EXIT(ExtensionTextCaption);
          EXIT(Text001);
        END;
      END;
    END;

    [External]
    PROCEDURE SummEntryNo@3() : Integer;
    VAR
      ReturnValue@1000 : Integer;
      IsHandled@1001 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeSummEntryNo(Rec,ReturnValue,IsHandled);
      IF IsHandled THEN
        EXIT(ReturnValue);

      CASE "Source Type" OF
        DATABASE::"Item Ledger Entry":
          EXIT(1);
        DATABASE::"Purchase Line":
          EXIT(11 + "Source Subtype");
        DATABASE::"Requisition Line":
          EXIT(21);
        DATABASE::"Sales Line":
          EXIT(31 + "Source Subtype");
        DATABASE::"Item Journal Line":
          EXIT(41 + "Source Subtype");
        DATABASE::"Job Journal Line":
          EXIT(51 + "Source Subtype");
        DATABASE::"Prod. Order Line":
          EXIT(61 + "Source Subtype");
        DATABASE::"Prod. Order Component":
          EXIT(71 + "Source Subtype");
        DATABASE::"Transfer Line":
          EXIT(101 + "Source Subtype");
        DATABASE::"Service Line":
          EXIT(110);
        DATABASE::"Assembly Header":
          EXIT(141 + "Source Subtype");
        DATABASE::"Assembly Line":
          EXIT(151 + "Source Subtype");
        ELSE
          EXIT(0);
      END;
    END;

    [External]
    PROCEDURE HasSamePointer@24(ReservEntry@1000 : Record 337) : Boolean;
    BEGIN
      EXIT(
        ("Source Type" = ReservEntry."Source Type") AND
        ("Source Subtype" = ReservEntry."Source Subtype") AND
        ("Source ID" = ReservEntry."Source ID") AND
        ("Source Batch Name" = ReservEntry."Source Batch Name") AND
        ("Source Prod. Order Line" = ReservEntry."Source Prod. Order Line") AND
        ("Source Ref. No." = ReservEntry."Source Ref. No."));
    END;

    [External]
    PROCEDURE HasSamePointerWithSpec@58(TrackingSpecification@1000 : Record 336) : Boolean;
    BEGIN
      EXIT(
        ("Source Type" = TrackingSpecification."Source Type") AND
        ("Source Subtype" = TrackingSpecification."Source Subtype") AND
        ("Source ID" = TrackingSpecification."Source ID") AND
        ("Source Batch Name" = TrackingSpecification."Source Batch Name") AND
        ("Source Prod. Order Line" = TrackingSpecification."Source Prod. Order Line") AND
        ("Source Ref. No." = TrackingSpecification."Source Ref. No."));
    END;

    [External]
    PROCEDURE SetPointer@2(RowID@1000 : Text[250]);
    VAR
      ItemTrackingMgt@1001 : Codeunit 6500;
      StrArray@1002 : ARRAY [6] OF Text[100];
    BEGIN
      ItemTrackingMgt.DecomposeRowID(RowID,StrArray);
      EVALUATE("Source Type",StrArray[1]);
      EVALUATE("Source Subtype",StrArray[2]);
      "Source ID" := StrArray[3];
      "Source Batch Name" := StrArray[4];
      EVALUATE("Source Prod. Order Line",StrArray[5]);
      EVALUATE("Source Ref. No.",StrArray[6]);
    END;

    [External]
    PROCEDURE SetPointerFilter@15();
    BEGIN
      SETCURRENTKEY(
        "Source ID","Source Ref. No.","Source Type","Source Subtype",
        "Source Batch Name","Source Prod. Order Line","Reservation Status",
        "Shipment Date","Expected Receipt Date");
      SETRANGE("Source ID","Source ID");
      SETRANGE("Source Ref. No.","Source Ref. No.");
      SETRANGE("Source Type","Source Type");
      SETRANGE("Source Subtype","Source Subtype");
      SETRANGE("Source Batch Name","Source Batch Name");
      SETRANGE("Source Prod. Order Line","Source Prod. Order Line");
    END;

    [External]
    PROCEDURE Lock@4();
    VAR
      Rec2@1000 : Record 337;
    BEGIN
      Rec2.SETCURRENTKEY("Item No.");
      IF "Item No." <> '' THEN
        Rec2.SETRANGE("Item No.","Item No.");
      Rec2.LOCKTABLE;
      IF Rec2.FINDLAST THEN;
    END;

    [External]
    PROCEDURE SetItemData@29(ItemNo@1000 : Code[20];ItemDescription@1001 : Text[100];LocationCode@1002 : Code[10];VariantCode@1003 : Code[10];QtyPerUoM@1004 : Decimal);
    BEGIN
      "Item No." := ItemNo;
      Description := ItemDescription;
      "Location Code" := LocationCode;
      "Variant Code" := VariantCode;
      "Qty. per Unit of Measure" := QtyPerUoM;
    END;

    [External]
    PROCEDURE SetSource@21(SourceType@1000 : Integer;SourceSubtype@1001 : Integer;SourceID@1002 : Code[20];SourceRefNo@1003 : Integer;SourceBatchName@1004 : Code[10];SourceProdOrderLine@1005 : Integer);
    BEGIN
      "Source Type" := SourceType;
      "Source Subtype" := SourceSubtype;
      "Source ID" := SourceID;
      "Source Ref. No." := SourceRefNo;
      "Source Batch Name" := SourceBatchName;
      "Source Prod. Order Line" := SourceProdOrderLine;
    END;

    [External]
    PROCEDURE SetSourceFilter@31(SourceType@1011 : Integer;SourceSubtype@1010 : Integer;SourceID@1009 : Code[20];SourceRefNo@1008 : Integer;SourceKey@1000 : Boolean);
    BEGIN
      IF SourceKey THEN
        SETCURRENTKEY(
          "Source ID","Source Ref. No.","Source Type","Source Subtype",
          "Source Batch Name","Source Prod. Order Line");
      SETRANGE("Source Type",SourceType);
      IF SourceSubtype >= 0 THEN
        SETRANGE("Source Subtype",SourceSubtype);
      SETRANGE("Source ID",SourceID);
      IF SourceRefNo >= 0 THEN
        SETRANGE("Source Ref. No.",SourceRefNo);
    END;

    [External]
    PROCEDURE SetSourceFilter2@9(SourceBatchName@1007 : Code[10];SourceProdOrderLine@1006 : Integer);
    BEGIN
      SETRANGE("Source Batch Name",SourceBatchName);
      SETRANGE("Source Prod. Order Line",SourceProdOrderLine);
    END;

    [External]
    PROCEDURE ClearTracking@20();
    BEGIN
      "Serial No." := '';
      "Lot No." := '';
      "Warranty Date" := 0D;
      "Expiration Date" := 0D;

      OnAfterClearTracking(Rec);
    END;

    [External]
    PROCEDURE ClearTrackingFilter@14();
    BEGIN
      SETRANGE("Serial No.");
      SETRANGE("Lot No.");
    END;

    [External]
    PROCEDURE CopyTrackingFromItemLedgEntry@25(ItemLedgerEntry@1000 : Record 32);
    BEGIN
      "Serial No." := ItemLedgerEntry."Serial No.";
      "Lot No." := ItemLedgerEntry."Lot No.";

      OnAfterCopyTrackingFromItemLedgEntry(Rec,ItemLedgerEntry);
    END;

    [External]
    PROCEDURE CopyTrackingFromReservEntry@26(ReservationEntry@1000 : Record 337);
    BEGIN
      "Serial No." := ReservationEntry."Serial No.";
      "Lot No." := ReservationEntry."Lot No.";

      OnAfterCopyTrackingFromReservEntry(Rec,ReservationEntry);
    END;

    [External]
    PROCEDURE CopyTrackingFromSpec@19(TrackingSpecification@1000 : Record 336);
    BEGIN
      "Serial No." := TrackingSpecification."Serial No.";
      "Lot No." := TrackingSpecification."Lot No.";
      "Warranty Date" := TrackingSpecification."Warranty Date";
      "Expiration Date" := TrackingSpecification."Expiration Date";

      OnAfterCopyTrackingFromTrackingSpec(Rec,TrackingSpecification);
    END;

    [External]
    PROCEDURE CopyTrackingFromWhseActivLine@27(WarehouseActivityLine@1000 : Record 5767);
    BEGIN
      "Serial No." := WarehouseActivityLine."Serial No.";
      "Lot No." := WarehouseActivityLine."Lot No.";
      "Expiration Date" := WarehouseActivityLine."Expiration Date";

      OnAfterCopyTrackingFromWhseActivLine(Rec,WarehouseActivityLine);
    END;

    [External]
    PROCEDURE SetTrackingFilter@12(SerialNo@1007 : Code[50];LotNo@1006 : Code[50]);
    BEGIN
      SETRANGE("Serial No.",SerialNo);
      SETRANGE("Lot No.",LotNo);
    END;

    [External]
    PROCEDURE SetTrackingFilterBlank@23();
    BEGIN
      SETRANGE("Serial No.",'');
      SETRANGE("Lot No.",'');
    END;

    [External]
    PROCEDURE SetTrackingFilterFromItemJnlLine@8(ItemJournalLine@1000 : Record 83);
    BEGIN
      SETRANGE("Serial No.",ItemJournalLine."Serial No.");
      SETRANGE("Lot No.",ItemJournalLine."Lot No.");

      OnAfterSetTrackingFilterFromItemJnlLine(Rec,ItemJournalLine);
    END;

    [External]
    PROCEDURE SetTrackingFilterFromReservEntry@17(ReservEntry@1000 : Record 337);
    BEGIN
      SETRANGE("Serial No.",ReservEntry."Serial No.");
      SETRANGE("Lot No.",ReservEntry."Lot No.");

      OnAfterSetTrackingFilterFromReservEntry(Rec,ReservEntry);
    END;

    [External]
    PROCEDURE SetTrackingFilterFromSpec@16(TrackingSpecification@1000 : Record 336);
    BEGIN
      SETRANGE("Serial No.",TrackingSpecification."Serial No.");
      SETRANGE("Lot No.",TrackingSpecification."Lot No.");

      OnAfterSetTrackingFilterFromTrackingSpec(Rec,TrackingSpecification);
    END;

    [External]
    PROCEDURE SetTrackingFilterFromWhseSpec@18(WhseItemTrackingLine@1000 : Record 6550);
    BEGIN
      SETRANGE("Serial No.",WhseItemTrackingLine."Serial No.");
      SETRANGE("Lot No.",WhseItemTrackingLine."Lot No.");

      OnAfterSetTrackingFilterFromWhseSpec(Rec,WhseItemTrackingLine);
    END;

    [External]
    PROCEDURE UpdateItemTracking@5();
    VAR
      ItemTrackingMgt@1000 : Codeunit 6500;
    BEGIN
      "Item Tracking" := ItemTrackingMgt.ItemTrackingOption("Lot No.","Serial No.");
    END;

    [External]
    PROCEDURE UpdateActionMessageEntries@13(OldReservEntry@1000 : Record 337);
    VAR
      ActionMessageEntry@1002 : Record 99000849;
      ActionMessageEntry2@1003 : Record 99000849;
      OldReservEntry2@1004 : Record 337;
    BEGIN
      IF OldReservEntry."Reservation Status" = OldReservEntry."Reservation Status"::Surplus THEN BEGIN
        ActionMessageEntry.FilterFromReservEntry(OldReservEntry);
        IF ActionMessageEntry.FINDSET THEN
          REPEAT
            ActionMessageEntry2 := ActionMessageEntry;
            ActionMessageEntry2.TransferFromReservEntry(Rec);
            ActionMessageEntry2.MODIFY;
          UNTIL ActionMessageEntry.NEXT = 0;
        MODIFY;
      END ELSE
        IF OldReservEntry2.GET(OldReservEntry."Entry No.",NOT OldReservEntry.Positive) THEN BEGIN
          IF HasSamePointer(OldReservEntry2) THEN BEGIN
            OldReservEntry2.DELETE;
            DELETE;
          END ELSE
            MODIFY;
        END ELSE
          MODIFY;
    END;

    [External]
    PROCEDURE ClearItemTrackingFields@7();
    BEGIN
      "Lot No." := '';
      "Serial No." := '';
      UpdateItemTracking;

      OnAfterClearItemTrackingFields(Rec);
    END;

    LOCAL PROCEDURE CalcReservationQuantity@62() : Decimal;
    VAR
      ReservEntry@1001 : Record 337;
    BEGIN
      IF "Qty. per Unit of Measure" = 1 THEN
        EXIT("Quantity (Base)");

      ReservEntry.SETFILTER("Entry No.",'<>%1',"Entry No.");
      ReservEntry.SetSourceFilter("Source Type","Source Subtype","Source ID","Source Ref. No.",FALSE);
      ReservEntry.SetSourceFilter2("Source Batch Name","Source Prod. Order Line");
      ReservEntry.SETRANGE("Reservation Status","Reservation Status"::Reservation);
      ReservEntry.CALCSUMS("Quantity (Base)",Quantity);
      EXIT(
        ROUND((ReservEntry."Quantity (Base)" + "Quantity (Base)") / "Qty. per Unit of Measure",UOMMgt.QtyRndPrecision) -
        ReservEntry.Quantity);
    END;

    [External]
    PROCEDURE ClearApplFromToItemEntry@6();
    BEGIN
      IF Positive THEN
        "Appl.-to Item Entry" := 0
      ELSE
        "Appl.-from Item Entry" := 0;
    END;

    PROCEDURE IsResidualSurplus@161() : Boolean;
    BEGIN
      EXIT(
        ("Item Tracking" = "Item Tracking"::None) AND
        ("Reservation Status" = "Reservation Status"::Surplus) AND NOT Positive AND
        ("Source Type" = DATABASE::"Sales Line") AND ("Source Subtype" = 1));
    END;

    [External]
    PROCEDURE TestItemFields@10(ItemNo@1000 : Code[20];VariantCode@1001 : Code[10];LocationCode@1002 : Code[10]);
    BEGIN
      TESTFIELD("Item No.",ItemNo);
      TESTFIELD("Variant Code",VariantCode);
      TESTFIELD("Location Code",LocationCode);
    END;

    [External]
    PROCEDURE TrackingExists@11() : Boolean;
    BEGIN
      EXIT(("Serial No." <> '') OR ("Lot No." <> ''));
    END;

    [External]
    PROCEDURE TransferReservations@22(VAR OldReservEntry@1006 : Record 337;ItemNo@1000 : Code[20];VariantCode@1002 : Code[10];LocationCode@1003 : Code[10];TransferAll@1007 : Boolean;TransferQty@1015 : Decimal;QtyPerUOM@1008 : Decimal;SourceType@1009 : Integer;SourceSubtype@1010 : Option;SourceID@1011 : Code[20];SourceBatchName@1012 : Code[10];SourceProdOrderLine@1013 : Integer;SourceRefNo@1014 : Integer);
    VAR
      NewReservEntry@1004 : Record 337;
      CreateReservEntry@1005 : Codeunit 99000830;
      Status@1001 : 'Reservation,Tracking,Surplus,Prospect';
    BEGIN
      IF TransferAll THEN BEGIN
        OldReservEntry.FINDSET;
        OldReservEntry.TESTFIELD("Qty. per Unit of Measure",QtyPerUOM);
        REPEAT
          OldReservEntry.TestItemFields(ItemNo,VariantCode,LocationCode);

          NewReservEntry := OldReservEntry;
          NewReservEntry.SetSource(SourceType,SourceSubtype,SourceID,SourceRefNo,SourceBatchName,SourceProdOrderLine);

          NewReservEntry.UpdateActionMessageEntries(OldReservEntry);
        UNTIL OldReservEntry.NEXT = 0;
      END ELSE
        FOR Status := Status::Reservation TO Status::Prospect DO BEGIN
          IF TransferQty = 0 THEN
            EXIT;
          OldReservEntry.SETRANGE("Reservation Status",Status);
          IF OldReservEntry.FINDSET THEN
            REPEAT
              OldReservEntry.TestItemFields(ItemNo,VariantCode,LocationCode);

              TransferQty :=
                CreateReservEntry.TransferReservEntry(
                  SourceType,SourceSubtype,SourceID,SourceBatchName,SourceProdOrderLine,SourceRefNo,
                  QtyPerUOM,OldReservEntry,TransferQty);
            UNTIL (OldReservEntry.NEXT = 0) OR (TransferQty = 0);
        END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromItemLedgEntry@36(VAR ReservationEntry@1000 : Record 337;ItemLedgerEntry@1001 : Record 32);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromReservEntry@37(VAR ReservationEntry@1000 : Record 337;FromReservationEntry@1001 : Record 337);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromTrackingSpec@38(VAR ReservationEntry@1000 : Record 337;TrackingSpecification@1001 : Record 336);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyTrackingFromWhseActivLine@39(VAR ReservationEntry@1000 : Record 337;WarehouseActivityLine@1001 : Record 5767);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterClearItemTrackingFields@35(VAR ReservationEntry@1000 : Record 337);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterClearTracking@30(VAR ReservationEntry@1000 : Record 337);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetTrackingFilterFromItemJnlLine@28(VAR ReservationEntry@1000 : Record 337;ItemJournalLine@1001 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetTrackingFilterFromReservEntry@32(VAR ReservationEntry@1000 : Record 337;FromReservationEntry@1001 : Record 337);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetTrackingFilterFromTrackingSpec@33(VAR ReservationEntry@1000 : Record 337;TrackingSpecification@1001 : Record 336);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetTrackingFilterFromWhseSpec@34(VAR ReservationEntry@1000 : Record 337;WhseItemTrackingLine@1001 : Record 6550);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTextCaption@48(SourceType@1000 : Integer;VAR NewTextCaption@1001 : Text[255]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSummEntryNo@40(ReservationEntry@1000 : Record 337;VAR ReturnValue@1001 : Integer;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

