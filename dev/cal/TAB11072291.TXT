OBJECT Table 11072291 Service Order SUP Set
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Service Order Unit Price Set;
    LookupPageID=Page11126770;
    DrillDownPageID=Page11126770;
  }
  FIELDS
  {
    { 10  ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   OnValidate=VAR
                                                                ServiceOrder@1100528600 : Record 11012823;
                                                                ServiceOrderExtension@1100528601 : Record 11071727;
                                                              BEGIN
                                                                ServiceOrder.GET("Service Order No.");
                                                                ServiceOrderExtension.GetServOrderExtension("Service Order No.");
                                                                "Price Book Type" := ServiceOrder."Price Book Type";
                                                                "Price Book Code" := ServiceOrderExtension."Price Book Code";
                                                                "Price Book Index Date" := ServiceOrderExtension."Price Book Index Date";
                                                              END;

                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 20  ;   ;Room Code           ;Code20        ;TableRelation="SUP Room".Code;
                                                   OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                                CustomerNo@1100528601 : Code[20];
                                                                PriceBookType@1100528604 : Option;
                                                                PriceBookCode@1100528603 : Code[20];
                                                                PriceBookIndexDate@1100528602 : Date;
                                                              BEGIN
                                                                IF "Room Code" <> '' THEN BEGIN
                                                                  ServiceUnitPriceManagement.GetPriceBookInfo("Service Order No.", PriceBookType, PriceBookCode, PriceBookIndexDate);
                                                                  CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo(PriceBookType, PriceBookCode);
                                                                  ServiceUnitPriceManagement.ValidateRoom(CustomerNo, "Room Code");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              CustomerNo@1100528601 : Code[20];
                                                              ServiceLocationType@1100528602 : Code[20];
                                                              PriceBookType@1100528605 : Option;
                                                              PriceBookCode@1100528606 : Code[20];
                                                              PriceBookIndexDate@1100528603 : Date;
                                                            BEGIN
                                                              ServiceUnitPriceManagement.GetPriceBookInfo("Service Order No.", PriceBookType, PriceBookCode, PriceBookIndexDate);
                                                              CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo(PriceBookType, PriceBookCode);
                                                              ServiceLocationType := ServiceUnitPriceManagement.GetServiceLocationTypeFromServiceOrder("Service Order No.");
                                                              IF ServiceUnitPriceManagement.LookupRoom(CustomerNo, ServiceLocationType, "Room Code") THEN
                                                                VALIDATE("Room Code");
                                                            END;

                                                   CaptionML=ENU=Room Code }
    { 30  ;   ;Unit Price Set      ;Code20        ;TableRelation="Service Unit Price Set".Code;
                                                   OnValidate=VAR
                                                                ServiceUnitPriceSet@1100528600 : Record 11072284;
                                                              BEGIN
                                                                IF "Unit Price Set" <> '' THEN BEGIN
                                                                  ServiceUnitPriceSet.SETRANGE("Price Book Type", "Price Book Type");
                                                                  ServiceUnitPriceSet.SETRANGE("Price Book Code", "Price Book Code");
                                                                  ServiceUnitPriceSet.SETRANGE("Price Book Index Date", "Price Book Index Date");
                                                                  ServiceUnitPriceSet.SETRANGE(Code, "Unit Price Set");
                                                                  ServiceUnitPriceSet.FINDFIRST;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPriceSet@1100528605 : Record 11072284;
                                                              SUPRoomRelationLine@1100528602 : Record 11071983;
                                                              ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              CustomerNo@1100528603 : Code[20];
                                                            BEGIN
                                                              ServiceUnitPriceSet.SETRANGE("Price Book Type", "Price Book Type");
                                                              ServiceUnitPriceSet.SETRANGE("Price Book Code", "Price Book Code");
                                                              ServiceUnitPriceSet.SETRANGE("Price Book Index Date", "Price Book Index Date");

                                                              CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo("Price Book Type", "Price Book Code");
                                                              SUPRoomRelationLine.SETRANGE("Customer No.", CustomerNo);
                                                              SUPRoomRelationLine.SETRANGE("Room Code", "Room Code");
                                                              SUPRoomRelationLine.SETRANGE("Index Date", "Price Book Index Date");
                                                              SUPRoomRelationLine.SETFILTER("Unit Price Set Code", '<>%1', '');
                                                              IF SUPRoomRelationLine.FINDSET THEN BEGIN
                                                                REPEAT
                                                                  ServiceUnitPriceSet.SETRANGE(Code, SUPRoomRelationLine."Unit Price Set Code");
                                                                  IF ServiceUnitPriceSet.FINDFIRST THEN
                                                                    ServiceUnitPriceSet.MARK(TRUE);
                                                                UNTIL SUPRoomRelationLine.NEXT = 0;
                                                                ServiceUnitPriceSet.SETRANGE(Code);
                                                                ServiceUnitPriceSet.MARKEDONLY(TRUE);
                                                              END;
                                                              IF PAGE.RUNMODAL(0, ServiceUnitPriceSet) = ACTION::LookupOK THEN
                                                                VALIDATE("Unit Price Set", ServiceUnitPriceSet.Code);
                                                            END;

                                                   CaptionML=[ENU=Unit Price Set;
                                                              SVE=A-pris grupp];
                                                   NotBlank=Yes }
    { 35  ;   ;Unit Price Set Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Unit Price Set".Description WHERE (Price Book Type=FIELD(Price Book Type),
                                                                                                                  Price Book Code=FIELD(Price Book Code),
                                                                                                                  Price Book Index Date=FIELD(Price Book Index Date),
                                                                                                                  Code=FIELD(Unit Price Set)));
                                                   CaptionML=ENU=Unit Price Set Description;
                                                   Editable=No }
    { 40  ;   ;Price Book Type     ;Option        ;CaptionML=[ENU=Price Book Type;
                                                              SVE=Enhetspristyp];
                                                   OptionCaptionML=[ENU=,Internal,Customer;
                                                                    NOR=,Internt,Kunde;
                                                                    SVE=,Intern,Kund];
                                                   OptionString=,Internal,Customer;
                                                   Editable=No }
    { 50  ;   ;Price Book Code     ;Code20        ;CaptionML=[ENU=Price Book Code;
                                                              SVE=Enhetspriskod];
                                                   Editable=No }
    { 60  ;   ;Price Book Index Date;Date         ;CaptionML=[ENU=Price Book Index Date;
                                                              SVE=Indexdatum f”r enhetspris];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Order No.,Room Code,Unit Price Set;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetRoomDescription@1100528600() : Text[50];
    VAR
      ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
      CustomerNo@1100528601 : Code[20];
    BEGIN
      CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo("Price Book Type", "Price Book Code");
      EXIT(ServiceUnitPriceManagement.GetRoomDescription(CustomerNo, "Room Code"));
    END;

    BEGIN
    END.
  }
}

