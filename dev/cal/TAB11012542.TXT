OBJECT Table 11012542 Plant Translation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,No.;
    OnInsert=BEGIN
               CheckKeyFields();
             END;

    OnRename=BEGIN
               CheckKeyFields();
             END;

    CaptionML=[ENU=Plant Translation;
               NOR=Maskin Oversettelse;
               SVE=Maskin (maskin), ”vers„ttning];
    LookupPageID=Page35;
  }
  FIELDS
  {
    { 5   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Plant Type,Plant No.,Plant Group;
                                                                    NOR=Maskintype,Maskinnr.,Maskingruppe];
                                                   OptionString=PlantType,PlantNo,PlantGroup }
    { 10  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(PlantGroup)) "Grouping Level"
                                                                 ELSE "Plant Type";
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 20  ;   ;No.                 ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "No." <> '' THEN
                                                                  TESTFIELD(Type, Type::PlantNo);
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 30  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              NOR=Spr†kkode;
                                                              SVE=Spr†kkod];
                                                   NotBlank=Yes }
    { 40  ;   ;Translation         ;Text50        ;CaptionML=[ENU=Translation;
                                                              NOR=Oversettelse;
                                                              SVE=™vers„ttning] }
  }
  KEYS
  {
    {    ;Type,Code,No.,Language Code             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529000 : TextConst 'ENU=No language selected;NOR=Intet spr†k valgt';

    PROCEDURE CheckKeyFields@1100529001();
    BEGIN
      //DP00241 (27089)
      CASE Type OF
        Type::PlantType:
          BEGIN
            TESTFIELD(Code);
            TESTFIELD("No.", '');
          END;
        Type::PlantNo:
          BEGIN
            TESTFIELD(Code);
            TESTFIELD("No.");
          END;
        Type::PlantGroup:
          BEGIN
            TESTFIELD(Code);
            TESTFIELD("No.", '');
          END;
      END;
    END;

    PROCEDURE GetDescription@1100529000() : Text[100];
    VAR
      PlantType@1100529000 : Record 11012551;
      PlantNumber@1100529001 : Record 11012552;
      GroupingLevel@1100529002 : Record 11012576;
    BEGIN
      //DP00241 (27089)
      CASE Type OF
        Type::PlantType:
          BEGIN
            IF PlantType.GET(Code) THEN
              EXIT(PlantType.Description);
          END;
        Type::PlantNo:
          BEGIN
            IF PlantNumber.GET(Code, "No.") THEN
              EXIT(PlantNumber.Description);
            IF PlantType.GET(Code) THEN
              EXIT(PlantType.Description);
          END;
        Type::PlantGroup:
          BEGIN
            IF GroupingLevel.GET(Code) THEN
              EXIT(GroupingLevel.Description);
          END;
      END;
      EXIT('');
    END;

    PROCEDURE SelectLanguageTmpTable@1100529003(CurrLanguage@1100529000 : Code[10]) : Code[10];
    VAR
      Language@1100529001 : Record 8;
    BEGIN
      //DP00241 (27089)
      Language.Code := CurrLanguage;
      IF PAGE.RUNMODAL(0, Language) = ACTION::LookupOK THEN BEGIN
        IF (Language.Code <> '') AND (Language.Code <> CurrLanguage) THEN BEGIN
          CurrLanguage := Language.Code;
        END;
      END;
      IF CurrLanguage = '' THEN
        ERROR(Text001);
      EXIT(CurrLanguage);
    END;

    PROCEDURE BuildTmpTable@1100529002(VAR TmpRec@1100529007 : TEMPORARY Record 11012542;CurrType@1100529008 : Option;CurrLanguage@1100529009 : Code[10]);
    VAR
      PlantType@1100529000 : Record 11012551;
      PlantNumber@1100529001 : Record 11012552;
      GroupLevel@1100529002 : Record 11012576;
      PlantTranslation@1100529003 : Record 11012542;
      CurrPlantTranslation@1100529004 : Record 11012542;
      Window@1100529005 : Dialog;
      Counter@1100529006 : Integer;
    BEGIN
      //DP00241 (27089)
      //* NOTE: TmpRec must be temporary in the calling process!
      IF CurrLanguage = '' THEN
        EXIT;
      CurrPlantTranslation := TmpRec;

      WITH TmpRec DO BEGIN
        RESET;
        DELETEALL;
        CASE CurrType OF
          Type::PlantType:
            BEGIN
              Window.OPEN(PlantType.TABLECAPTION + ' #1############################');
              IF PlantType.FINDSET THEN BEGIN
                REPEAT
                  Counter := Counter + 1;
                  IF (Counter MOD 100) = 1 THEN
                    Window.UPDATE(1, PlantType.Code);
                  INIT;
                  Type := CurrType;
                  Code := PlantType.Code;
                  "No." := '';
                  "Language Code" := CurrLanguage;
                  IF PlantTranslation.GET(Type, Code, "No.", "Language Code") THEN
                    Translation := PlantTranslation.Translation;
                  INSERT;
                UNTIL PlantType.NEXT = 0;
              END;
              Window.CLOSE;
            END;
          Type::PlantNo:
            BEGIN
              Window.OPEN(PlantNumber.TABLECAPTION + ' #1############################');
              IF PlantNumber.FINDSET THEN BEGIN
                REPEAT
                  Counter := Counter + 1;
                  IF (Counter MOD 100) = 1 THEN
                    Window.UPDATE(1, PlantNumber."Plant Type" + '-' + PlantNumber."No.");
                  INIT;
                  Type := CurrType;
                  Code := PlantNumber."Plant Type";
                  "No." := PlantNumber."No.";
                  "Language Code" := CurrLanguage;
                  IF PlantTranslation.GET(Type, Code, "No.", "Language Code") THEN
                    Translation := PlantTranslation.Translation;
                  INSERT;
                UNTIL PlantNumber.NEXT = 0;
              END;
              Window.CLOSE;
            END;
          Type::PlantGroup:
            BEGIN
              Window.OPEN(GroupLevel.TABLECAPTION + ' #1############################');
              IF GroupLevel.FINDSET THEN BEGIN
                REPEAT
                  Counter := Counter + 1;
                  IF (Counter MOD 100) = 1 THEN
                    Window.UPDATE(1, GroupLevel.Code);
                  INIT;
                  Type := CurrType;
                  Code := GroupLevel.Code;
                  "No." := '';
                  "Language Code" := CurrLanguage;
                  IF PlantTranslation.GET(Type, Code, "No.", "Language Code") THEN
                    Translation := PlantTranslation.Translation;
                  INSERT;
                UNTIL GroupLevel.NEXT = 0;
              END;
              Window.CLOSE;
            END;
        END;
        IF NOT GET(CurrType, CurrPlantTranslation.Code, CurrPlantTranslation."No.", CurrLanguage) THEN
          IF FINDFIRST THEN;
      END;
    END;

    PROCEDURE UpdateTranslationFromTmpRecord@1100529016();
    VAR
      PlantTranslation@1100529000 : Record 11012542;
    BEGIN
      //DP00241 (27089)
      IF "Language Code" = '' THEN
        EXIT;
      IF PlantTranslation.GET(Type, Code, "No.", "Language Code") THEN BEGIN
        IF Translation = '' THEN
          PlantTranslation.DELETE
        ELSE BEGIN
          PlantTranslation.Translation := Translation;
          PlantTranslation.MODIFY;
        END;
      END ELSE BEGIN
        IF Translation <> '' THEN BEGIN
          PlantTranslation.INIT;
          PlantTranslation.Type := Type;
          PlantTranslation.Code := Code;
          PlantTranslation."No." := "No.";
          PlantTranslation."Language Code" := "Language Code";
          PlantTranslation.Translation := Translation;
          PlantTranslation.INSERT;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

