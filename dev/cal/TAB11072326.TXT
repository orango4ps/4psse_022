OBJECT Table 11072326 SUP Cluster Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=SUP Cluster Line;
    LookupPageID=Page11126835;
    DrillDownPageID=Page11126835;
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=" ,Customer Template,Customer,Service Contract Template,Service Contract";
                                                   OptionString=[ ,Customer Template,Customer,Service Contract Template,Service Contract] }
    { 20  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Customer Template)) "Customer Template".Code
                                                                 ELSE IF (Type=CONST(Customer)) Customer.No.
                                                                 ELSE IF (Type=CONST(Service Contract Template)) "Service-Contract Template".Code
                                                                 ELSE IF (Type=CONST(Service Contract)) Service-Contract.No.;
                                                   OnValidate=VAR
                                                                CustomerTemplate@1100528600 : Record 5105;
                                                                Customer@1100528601 : Record 18;
                                                                ServiceContractTemplate@1100528602 : Record 11071754;
                                                                ServiceContract@1100528603 : Record 11012812;
                                                              BEGIN
                                                                IF Code <> '' THEN BEGIN
                                                                  CASE Type OF
                                                                    Type::"Customer Template":
                                                                      BEGIN
                                                                        CustomerTemplate.GET(Code);
                                                                        CustomerTemplate.TESTFIELD("Price Book Code");
                                                                        CustomerTemplate.TESTFIELD("Price Book Index Date");
                                                                        VALIDATE("Price Book Code", CustomerTemplate."Price Book Code");
                                                                        VALIDATE("Price Book Index Date", CustomerTemplate."Price Book Index Date");
                                                                      END;
                                                                    Type::Customer:
                                                                      BEGIN
                                                                        Customer.GET(Code);
                                                                        Customer.TESTFIELD("Price Book Code");
                                                                        Customer.TESTFIELD("Price Book Index Date");
                                                                        VALIDATE("Price Book Code", Customer."Price Book Code");
                                                                        VALIDATE("Price Book Index Date", Customer."Price Book Index Date");
                                                                      END;
                                                                    Type::"Service Contract Template":
                                                                      BEGIN
                                                                        ServiceContractTemplate.GET(Code);
                                                                        ServiceContractTemplate.TESTFIELD("Price Book Code");
                                                                        ServiceContractTemplate.TESTFIELD("Price Book Index Date");
                                                                        VALIDATE("Price Book Code", ServiceContractTemplate."Price Book Code");
                                                                        VALIDATE("Price Book Index Date", ServiceContractTemplate."Price Book Index Date");
                                                                      END;
                                                                    Type::"Service Contract":
                                                                      BEGIN
                                                                        ServiceContract.GET(Code);
                                                                        ServiceContract.TESTFIELD("Price Book Code");
                                                                        ServiceContract.TESTFIELD("Price Book Index Date");
                                                                        VALIDATE("Price Book Code", ServiceContract."Price Book Code");
                                                                        VALIDATE("Price Book Index Date", ServiceContract."Price Book Index Date");
                                                                      END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 30  ;   ;SUP Cluster         ;Code10        ;TableRelation="SUP Cluster";
                                                   CaptionML=ENU=SUP Cluster }
    { 35  ;   ;SUP Cluster Description;Text30     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SUP Cluster".Description WHERE (Code=FIELD(SUP Cluster)));
                                                   CaptionML=ENU=SUP Cluster Description;
                                                   Editable=No }
    { 40  ;   ;Price Book Code     ;Code20        ;TableRelation="Service Price Book".Code;
                                                   CaptionML=[ENU=Price Book Code;
                                                              SVE=Enhetspriskod];
                                                   Editable=No }
    { 50  ;   ;Price Book Index Date;Date         ;TableRelation="Service Price Book"."Index Date" WHERE (Code=FIELD(Price Book Code));
                                                   CaptionML=[ENU=Price Book Index Date;
                                                              SVE=Indexdatum f”r enhetspris];
                                                   Editable=No }
    { 60  ;   ;Unit Price Type     ;Option        ;OnValidate=BEGIN
                                                                IF "Unit Price Type" <> xRec."Unit Price Type" THEN
                                                                  VALIDATE("Unit Price Code", '');
                                                              END;

                                                   CaptionML=ENU=Unit Price Set Code;
                                                   OptionCaptionML=ENU=Set,Price;
                                                   OptionString=Unit Price Set,Unit Price }
    { 70  ;   ;Unit Price Code     ;Code20        ;TableRelation=IF (Unit Price Type=CONST(Unit Price Set)) "Service Unit Price Set".Code WHERE (Price Book Code=FIELD(Price Book Code),
                                                                                                                                                 Price Book Index Date=FIELD(Price Book Index Date))
                                                                                                                                                 ELSE IF (Unit Price Type=CONST(Unit Price)) "Service Unit Price".Code WHERE (Price Book Code=FIELD(Price Book Code),
                                                                                                                                                                                                                              Index Date=FIELD(Price Book Index Date));
                                                   OnValidate=BEGIN
                                                                IF "Unit Price Code" <> '' THEN BEGIN
                                                                  IF "Unit Price Type" = "Unit Price Type"::"Unit Price" THEN
                                                                    Quantity := 1;
                                                                END ELSE
                                                                  Quantity := 0;
                                                              END;

                                                   CaptionML=[ENU=Unit Price Code;
                                                              NOR=Enhetspriskode;
                                                              SVE=Enhetspriskod] }
    { 80  ;   ;Room Code           ;Code20        ;TableRelation="SUP Room".Code;
                                                   OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                                CustomerNo@1100528601 : Code[20];
                                                                PriceBookType@1100528602 : Option;
                                                              BEGIN
                                                                IF "Room Code" <> '' THEN BEGIN
                                                                  PriceBookType := ServiceUnitPriceManagement.GetPriceBookType("Price Book Code", "Price Book Index Date");
                                                                  CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo(PriceBookType, "Price Book Code");
                                                                  ServiceUnitPriceManagement.ValidateRoom(CustomerNo, "Room Code");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              CustomerNo@1100528601 : Code[20];
                                                              PriceBookType@1100528602 : Option;
                                                            BEGIN
                                                              PriceBookType := ServiceUnitPriceManagement.GetPriceBookType("Price Book Code", "Price Book Index Date");
                                                              CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo(PriceBookType, "Price Book Code");
                                                              IF ServiceUnitPriceManagement.LookupRoom(CustomerNo, '', "Room Code") THEN
                                                                VALIDATE("Room Code");
                                                            END;

                                                   CaptionML=ENU=Room Code }
    { 90  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF Quantity <> 0 THEN
                                                                  TESTFIELD("Unit Price Type", "Unit Price Type"::"Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes }
  }
  KEYS
  {
    {    ;Type,Code,SUP Cluster,Unit Price Type,Unit Price Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetUnitPriceData@1100528600(VAR OServiceUnitPrice@1100528600 : Record 11071769);
    VAR
      ServiceUnitPriceSet@1100528601 : Record 11072284;
    BEGIN
      OServiceUnitPrice.INIT;
      IF "Unit Price Code" = '' THEN
        EXIT;
      CASE "Unit Price Type" OF
        "Unit Price Type"::"Unit Price Set":
          IF ServiceUnitPriceSet.GET("Price Book Code", "Price Book Index Date", "Unit Price Code") THEN
            OServiceUnitPrice.Description := ServiceUnitPriceSet.Description;
        "Unit Price Type"::"Unit Price":
          IF NOT OServiceUnitPrice.GET("Price Book Code", "Price Book Index Date", "Unit Price Code") THEN
            OServiceUnitPrice.INIT;
      END;
    END;

    BEGIN
    END.
  }
}

