OBJECT Table 11072600 Job Task
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00 (1001);
  }
  PROPERTIES
  {
    OnInsert=VAR
               Job@1000 : Record 11072003;
               Cust@1001 : Record 18;
             BEGIN
               LOCKTABLE;
               Job.GET("Job No.");
               IF Job.Blocked = Job.Blocked::All THEN
                 Job.TestBlocked;
               Job.TESTFIELD("Bill-to Customer No.");
               Cust.GET(Job."Bill-to Customer No.");

               DimMgt.InsertJobTaskDim("Job No.","Job Task No.","Global Dimension 1 Code","Global Dimension 2 Code");

               CALCFIELDS("Schedule (Total Cost)","Usage (Total Cost)");
               Job.UpdateOverBudgetValue("Job No.",TRUE,"Usage (Total Cost)");
               Job.UpdateOverBudgetValue("Job No.",FALSE,"Schedule (Total Cost)");
             END;

    OnModify=BEGIN
               CALCFIELDS("Schedule (Total Cost)","Usage (Total Cost)");
               Job.UpdateOverBudgetValue("Job No.",TRUE,"Usage (Total Cost)");
               Job.UpdateOverBudgetValue("Job No.",FALSE,"Schedule (Total Cost)");
             END;

    OnDelete=VAR
               JobPlanningLine@1000 : Record 11020401;
               JobWIPTotal@1002 : Record 1021;
               JobTaskDim@1001 : Record 11020400;
             BEGIN
               IF JobLedgEntriesExist THEN
                 ERROR(CannotDeleteAssociatedEntriesErr,TABLECAPTION);

               JobPlanningLine.SETCURRENTKEY("Job No.","Job Task No.");
               JobPlanningLine.SETRANGE("Job No.","Job No.");
               JobPlanningLine.SETRANGE("Job Task No.","Job Task No.");
               JobPlanningLine.DELETEALL(TRUE);

               JobWIPTotal.DeleteEntriesForJobTask(Rec);

               JobTaskDim.SETRANGE("Job No.","Job No.");
               JobTaskDim.SETRANGE("Job Task No.","Job Task No.");
               IF NOT JobTaskDim.ISEMPTY THEN
                 JobTaskDim.DELETEALL;

               CALCFIELDS("Schedule (Total Cost)","Usage (Total Cost)");
               Job.UpdateOverBudgetValue("Job No.",TRUE,"Usage (Total Cost)");
               Job.UpdateOverBudgetValue("Job No.",FALSE,"Schedule (Total Cost)");
             END;

    CaptionML=[DEU=Projektaufgabe;
               ENU=Job Task;
               NLD=Projecttaak;
               NOR=Prosjektoppgave;
               SVE=Projektaktivitet];
    LookupPageID=Page11072602;
    DrillDownPageID=Page11072602;
  }
  FIELDS
  {
    { 1   ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=[DEU=Projektnr.;
                                                              ENU=Job No.;
                                                              NLD=Projectnr.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Job Task No.        ;Code20        ;OnValidate=VAR
                                                                Job@1000 : Record 11072003;
                                                                Cust@1001 : Record 18;
                                                              BEGIN
                                                                IF "Job Task No." = '' THEN
                                                                  EXIT;
                                                                Job.GET("Job No.");
                                                                Job.TESTFIELD("Bill-to Customer No.");
                                                                Cust.GET(Job."Bill-to Customer No.");
                                                                "Job Posting Group" := Job."Job Posting Group";
                                                              END;

                                                   CaptionML=[DEU=Projektaufgabennr.;
                                                              ENU=Job Task No.;
                                                              NLD=Projecttaaknr.;
                                                              NOR=Prosjektoppgavenr.;
                                                              SVE=Projektaktivitetsnr];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Job Task Type       ;Option        ;OnValidate=BEGIN
                                                                IF (xRec."Job Task Type" = "Job Task Type"::Posting) AND
                                                                   ("Job Task Type" <> "Job Task Type"::Posting)
                                                                THEN
                                                                  IF JobLedgEntriesExist OR JobPlanningLinesExist THEN
                                                                    ERROR(CannotChangeAssociatedEntriesErr,FIELDCAPTION("Job Task Type"),TABLECAPTION);

                                                                IF "Job Task Type" <> "Job Task Type"::Posting THEN BEGIN
                                                                  "Job Posting Group" := '';
                                                                  IF "WIP-Total" = "WIP-Total"::Excluded THEN
                                                                    "WIP-Total" := "WIP-Total"::" ";
                                                                END;

                                                                Totaling := '';
                                                              END;

                                                   CaptionML=[DEU=Projektaufgabenart;
                                                              ENU=Job Task Type;
                                                              NLD=Soort projecttaak;
                                                              NOR=Type prosjektoppgave;
                                                              SVE=Typ av projektaktivitet];
                                                   OptionCaptionML=[DEU=Konto,öberschrift,Summe,Von-Summe,Bis-Summe;
                                                                    ENU=Posting,Heading,Total,Begin-Total,End-Total;
                                                                    NLD=Rekening,Titel,Totaal,Begintotaal,Eindtotaal;
                                                                    NOR=Konto,Overskrift,Sum,Fra-sum,Til-sum;
                                                                    SVE=Konto,Rubrik,Summa,FrÜn-summa,Till-summa];
                                                   OptionString=Posting,Heading,Total,Begin-Total,End-Total }
    { 6   ;   ;WIP-Total           ;Option        ;OnValidate=VAR
                                                                Job@1003 : Record 11072003;
                                                              BEGIN
                                                                CASE "WIP-Total" OF
                                                                  "WIP-Total"::Total:
                                                                    BEGIN
                                                                      Job.GET("Job No.");
                                                                      "WIP Method" := Job."WIP Method";
                                                                    END;
                                                                  "WIP-Total"::Excluded:
                                                                    BEGIN
                                                                      TESTFIELD("Job Task Type","Job Task Type"::Posting);
                                                                      "WIP Method" := ''
                                                                    END;
                                                                  ELSE
                                                                    "WIP Method" := ''
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=WIP-Summe;
                                                              ENU=WIP-Total;
                                                              NLD=OHW-totaal;
                                                              NOR=Arbeidskapital-total;
                                                              SVE=PIA, totalt];
                                                   OptionCaptionML=[DEU=" ,Summe,Ausschlie·lich";
                                                                    ENU=" ,Total,Excluded";
                                                                    NLD=" ,Totaal,Uitgesloten";
                                                                    NOR=" ,Total,Eksludert";
                                                                    SVE=" ,Summa,Exklusive"];
                                                   OptionString=[ ,Total,Excluded] }
    { 7   ;   ;Job Posting Group   ;Code20        ;TableRelation="Job Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Job Posting Group" <> '' THEN
                                                                  TESTFIELD("Job Task Type","Job Task Type"::Posting);
                                                              END;

                                                   CaptionML=[DEU=Projektbuchungsgruppe;
                                                              ENU=Job Posting Group;
                                                              NLD=Projectboekingsgroep;
                                                              NOR=Prosjektbokfõringsgruppe;
                                                              SVE=Projektbokfîringsmall] }
    { 9   ;   ;WIP Method          ;Code20        ;TableRelation="Job WIP Method".Code WHERE (Valid=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                IF "WIP Method" <> '' THEN
                                                                  TESTFIELD("WIP-Total","WIP-Total"::Total);
                                                              END;

                                                   CaptionML=[DEU=WIP-Methode;
                                                              ENU=WIP Method;
                                                              NLD=OHW-methode;
                                                              NOR=Arbeidskapital-metode;
                                                              SVE=PIA-metod] }
    { 10  ;   ;Schedule (Total Cost);Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Total Cost (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                 Job Task No.=FIELD(Job Task No.),
                                                                                                                 Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                 Schedule Line=CONST(Yes),
                                                                                                                 Planning Date=FIELD(Planning Date Filter)));
                                                   CaptionML=[DEU=Einstandsbetrag Budget;
                                                              ENU=Budget (Total Cost);
                                                              NLD=Budget (totale kosten);
                                                              NOR=Budsjett (kostbelõp);
                                                              SVE=Budget (total kostnad)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 11  ;   ;Schedule (Total Price);Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Line Amount (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                  Job Task No.=FIELD(Job Task No.),
                                                                                                                  Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                  Schedule Line=CONST(Yes),
                                                                                                                  Planning Date=FIELD(Planning Date Filter)));
                                                   CaptionML=[DEU=Verkaufsbetrag Budget;
                                                              ENU=Budget (Total Price);
                                                              NLD=Budget (totale prijs);
                                                              NOR=Budsjett (salgsbelõp);
                                                              SVE=Budget (totalpris)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 12  ;   ;Usage (Total Cost)  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Ledger Entry"."Total Cost (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                Job Task No.=FIELD(Job Task No.),
                                                                                                                Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                Entry Type=CONST(Usage),
                                                                                                                Posting Date=FIELD(Posting Date Filter)));
                                                   CaptionML=[DEU=Verbrauch (Einstandsbetrag);
                                                              ENU=Usage (Total Cost);
                                                              NLD=Gebruik (totale kostprijs);
                                                              NOR=Forbruk (kostbelõp);
                                                              SVE=Fîrbrukning (totalkostnad)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 13  ;   ;Usage (Total Price) ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Ledger Entry"."Line Amount (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                 Job Task No.=FIELD(Job Task No.),
                                                                                                                 Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                 Entry Type=CONST(Usage),
                                                                                                                 Posting Date=FIELD(Posting Date Filter)));
                                                   CaptionML=[DEU=Verbrauch (Verkaufspreis);
                                                              ENU=Usage (Total Price);
                                                              NLD=Gebruik (totale verkoopprijs);
                                                              NOR=Forbruk (totalpris);
                                                              SVE=Fîrbrukning (totalpris)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 14  ;   ;Contract (Total Cost);Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Total Cost (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                 Job Task No.=FIELD(Job Task No.),
                                                                                                                 Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                 Contract Line=CONST(Yes),
                                                                                                                 Planning Date=FIELD(Planning Date Filter)));
                                                   CaptionML=[DEU=Fakturierbar (Gesamtkosten);
                                                              ENU=Billable (Total Cost);
                                                              NLD=Factureerbaar (totale kosten);
                                                              NOR=Fakturerbar (kostbelõp);
                                                              SVE=Fakturerbart (totalkostnad)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 15  ;   ;Contract (Total Price);Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Line Amount (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                  Job Task No.=FIELD(Job Task No.),
                                                                                                                  Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                  Contract Line=CONST(Yes),
                                                                                                                  Planning Date=FIELD(Planning Date Filter)));
                                                   CaptionML=[DEU=Fakturierbar (Verkaufsbetrag);
                                                              ENU=Billable (Total Price);
                                                              NLD=Factureerbaar (totale prijs);
                                                              NOR=Fakturerbar (salgsbelõp);
                                                              SVE=Fakturerbart (totalt pris)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 16  ;   ;Contract (Invoiced Price);Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Job Ledger Entry"."Line Amount (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                  Job Task No.=FIELD(Job Task No.),
                                                                                                                  Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                  Entry Type=CONST(Sale),
                                                                                                                  Posting Date=FIELD(Posting Date Filter)));
                                                   CaptionML=[DEU=Fakturierbar (Fakturierter VK-Preis);
                                                              ENU=Billable (Invoiced Price);
                                                              NLD=Factureerbaar (gefactureerde prijs);
                                                              NOR=Fakturerbar (fakturert pris);
                                                              SVE=Fakturerbart (fakturerat pris)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 17  ;   ;Contract (Invoiced Cost);Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Job Ledger Entry"."Total Cost (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                 Job Task No.=FIELD(Job Task No.),
                                                                                                                 Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                 Entry Type=CONST(Sale),
                                                                                                                 Posting Date=FIELD(Posting Date Filter)));
                                                   CaptionML=[DEU=Fakturierbar (Einstandsbetrag fakturiert);
                                                              ENU=Billable (Invoiced Cost);
                                                              NLD=Factureerbaar (gefactureerde kosten);
                                                              NOR=Fakturerbar (fakturert kost);
                                                              SVE=Fakturerbart (fakturerad kostnad)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 19  ;   ;Posting Date Filter ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Buchungsdatumsfilter;
                                                              ENU=Posting Date Filter;
                                                              NLD=Boekingsdatumfilter;
                                                              NOR=Bokfõringsdatofilter;
                                                              SVE=Datumfilter bokfîring] }
    { 20  ;   ;Planning Date Filter;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Planungsdatumsfilter;
                                                              ENU=Planning Date Filter;
                                                              NLD=Planningsdatumfilter;
                                                              NOR=Filter for planleggingsdato;
                                                              SVE=Filter fîr planeringsdatum] }
    { 21  ;   ;Totaling            ;Text250       ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   OnValidate=BEGIN
                                                                IF Totaling <> '' THEN
                                                                  IF NOT ("Job Task Type" IN ["Job Task Type"::Total,"Job Task Type"::"End-Total"]) THEN
                                                                    FIELDERROR("Job Task Type");
                                                                VALIDATE("WIP-Total");
                                                                CALCFIELDS(
                                                                  "Schedule (Total Cost)",
                                                                  "Schedule (Total Price)",
                                                                  "Usage (Total Cost)",
                                                                  "Usage (Total Price)",
                                                                  "Contract (Total Cost)",
                                                                  "Contract (Total Price)",
                                                                  "Contract (Invoiced Price)",
                                                                  "Contract (Invoiced Cost)");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=ZusammenzÑhlung;
                                                              ENU=Totaling;
                                                              NLD=Samentelling;
                                                              NOR=Sammentelling;
                                                              SVE=Summeringsintervall] }
    { 22  ;   ;New Page            ;Boolean       ;CaptionML=[DEU=Neue Seite;
                                                              ENU=New Page;
                                                              NLD=Nieuwe pagina;
                                                              NOR=Sideskift;
                                                              SVE=Ny sida] }
    { 23  ;   ;No. of Blank Lines  ;Integer       ;CaptionML=[DEU=Leerzeilen;
                                                              ENU=No. of Blank Lines;
                                                              NLD=Aantal blanco regels;
                                                              NOR=Ant. tomme linjer;
                                                              SVE=Tomma rader];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 24  ;   ;Indentation         ;Integer       ;CaptionML=[DEU=EinrÅckung;
                                                              ENU=Indentation;
                                                              NLD=Indentatie;
                                                              NOR=Innrykking;
                                                              SVE=Indrag];
                                                   MinValue=0 }
    { 34  ;   ;Recognized Sales Amount;Decimal    ;CaptionML=[DEU=Betrag deklarierte VerkÑufe;
                                                              ENU=Recognized Sales Amount;
                                                              NLD=Verantwoorde omzet;
                                                              NOR=Fõrt salgsbelõp;
                                                              SVE=Bokfîrt fîrsÑljningsbelopp];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 37  ;   ;Recognized Costs Amount;Decimal    ;CaptionML=[DEU=Betrag deklarierte Kosten;
                                                              ENU=Recognized Costs Amount;
                                                              NLD=Verantwoorde totale kosten;
                                                              NOR=Fõrt kostbelõp;
                                                              SVE=Bokfîrt kostnadsbelopp];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 56  ;   ;Recognized Sales G/L Amount;Decimal;CaptionML=[DEU=Sachkonto fÅr deklarierte VerkÑufe;
                                                              ENU=Recognized Sales G/L Amount;
                                                              NLD=Verantw. omzetbedrag GB;
                                                              NOR=Hovedbokskonto - bokfõrt salg;
                                                              SVE=Redovisningskonto fîr bokfîrd fîrsÑljning];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 57  ;   ;Recognized Costs G/L Amount;Decimal;CaptionML=[DEU=Sachkonto fÅr deklarierte Kosten;
                                                              ENU=Recognized Costs G/L Amount;
                                                              NLD=Verantw. totale kosten GB;
                                                              NOR=Hovedbokskonto - bokfõrte kostnader;
                                                              SVE=Redovisningskonto fîr bokfîrda kostnader];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 60  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 1;
                                                              ENU=Global Dimension 1 Code;
                                                              NLD=Code globale dimensie 1;
                                                              NOR=Global dimensjon 1 - kode;
                                                              SVE=Global dimension 1 kod];
                                                   CaptionClass='1,1,1' }
    { 61  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 2;
                                                              ENU=Global Dimension 2 Code;
                                                              NLD=Code globale dimensie 2;
                                                              NOR=Global Dimensjon 2-kode;
                                                              SVE=Global dimension 2 kod];
                                                   CaptionClass='1,1,2' }
    { 62  ;   ;Outstanding Orders  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line"."Outstanding Amt. Ex. VAT (LCY)" WHERE (Document Type=CONST(Order),
                                                                                                                           Job No.=FIELD(Job No.),
                                                                                                                           Job Task No.=FIELD(Job Task No.),
                                                                                                                           Job Task No.=FIELD(FILTER(Totaling))));
                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=[DEU=Auftragsbestand;
                                                              ENU=Outstanding Orders;
                                                              NLD=Openstaande orders;
                                                              NOR=èpne bestillinger;
                                                              SVE=Totalt utestÜende orderbelopp] }
    { 63  ;   ;Amt. Rcd. Not Invoiced;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line"."A. Rcd. Not Inv. Ex. VAT (LCY)" WHERE (Document Type=CONST(Order),
                                                                                                                           Job No.=FIELD(Job No.),
                                                                                                                           Job Task No.=FIELD(Job Task No.),
                                                                                                                           Job Task No.=FIELD(FILTER(Totaling))));
                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=[DEU=Nicht fakt. Lieferbetrag;
                                                              ENU=Amt. Rcd. Not Invoiced;
                                                              NLD=Ontv./Niet gefact. bedrag;
                                                              NOR=Mottatt belõp ikke fakturert;
                                                              SVE=Inlevererat bel. ej faktrd] }
    { 64  ;   ;Remaining (Total Cost);Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Remaining Total Cost (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                           Job Task No.=FIELD(Job Task No.),
                                                                                                                           Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                           Schedule Line=CONST(Yes),
                                                                                                                           Planning Date=FIELD(Planning Date Filter)));
                                                   CaptionML=[DEU=Rest (Einstandsbetrag);
                                                              ENU=Remaining (Total Cost);
                                                              NLD=Resterend (totale kostprijs);
                                                              NOR=GjenstÜende (total kost);
                                                              SVE=èterstÜende (total kostnad)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 65  ;   ;Remaining (Total Price);Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Remaining Line Amount (LCY)" WHERE (Job No.=FIELD(Job No.),
                                                                                                                            Job Task No.=FIELD(Job Task No.),
                                                                                                                            Job Task No.=FIELD(FILTER(Totaling)),
                                                                                                                            Schedule Line=CONST(Yes),
                                                                                                                            Planning Date=FIELD(Planning Date Filter)));
                                                   CaptionML=[DEU=Rest (Verkaufsbetrag);
                                                              ENU=Remaining (Total Price);
                                                              NLD=Resterend (totale prijs);
                                                              NOR=GjenstÜende (total pris);
                                                              SVE=èterstÜende (totalt pris)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 66  ;   ;Start Date          ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Min("Job Planning Line"."Planning Date" WHERE (Job No.=FIELD(Job No.),
                                                                                                              Job Task No.=FIELD(Job Task No.)));
                                                   CaptionML=[DEU=Startdatum;
                                                              ENU=Start Date;
                                                              NLD=Begindatum;
                                                              NOR=Startdato;
                                                              SVE=Startdatum];
                                                   Editable=No }
    { 67  ;   ;End Date            ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Max("Job Planning Line"."Planning Date" WHERE (Job No.=FIELD(Job No.),
                                                                                                              Job Task No.=FIELD(Job Task No.)));
                                                   CaptionML=[DEU=Enddatum;
                                                              ENU=End Date;
                                                              NLD=Einddatum;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Job No.,Job Task No.                    ;Clustered=Yes }
    {    ;Job Task No.                             }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Job Task No.,Description,Job Task Type   }
  }
  CODE
  {
    VAR
      CannotDeleteAssociatedEntriesErr@1000 : TextConst '@@@="%1=The job task table name.";DEU=%1 kann nicht gelîscht werden, weil mindestens ein Posten verknÅpft ist.;ENU=You cannot delete %1 because one or more entries are associated.;NLD=U kunt %1 niet verwijderen omdat er een of meer posten aan gekoppeld zijn.;NOR=Kan ikke fjerne %1 fordi det finnes minst en tilknyttet post.;SVE=Det gÜr inte att ta bort %1 eftersom det finns minst en kopplad transaktion.';
      CannotChangeAssociatedEntriesErr@1002 : TextConst '@@@="%1 = The field name you are trying to change; %2 = The job task table name.";DEU=%1 kann nicht geÑndert werden, weil mindestens ein Eintrag mit %2 verknÅpft ist.;ENU=You cannot change %1 because one or more entries are associated with this %2.;NLD=U kunt %1 niet wijzigen omdat aan deze %2 een of meer posten zijn gekoppeld.;NOR=Kan ikke endre %1 fordi det finnes minst en post som er tilknyttet denne %2.;SVE=Det gÜr inte att Ñndra %1 eftersom det finns minst en transaktion som Ñr kopplad till denna %2.';
      Job@1003 : Record 11072003;
      DimMgt@1001 : Codeunit 408;

    [External]
    PROCEDURE CalcEACTotalCost@6() : Decimal;
    BEGIN
      IF "Job No." <> Job."No." THEN
        IF NOT Job.GET("Job No.") THEN
          EXIT(0);

      IF Job."Apply Usage Link" THEN
        EXIT("Usage (Total Cost)" + "Remaining (Total Cost)");

      EXIT(0);
    END;

    [External]
    PROCEDURE CalcEACTotalPrice@7() : Decimal;
    BEGIN
      IF "Job No." <> Job."No." THEN
        IF NOT Job.GET("Job No.") THEN
          EXIT(0);

      IF Job."Apply Usage Link" THEN
        EXIT("Usage (Total Price)" + "Remaining (Total Price)");

      EXIT(0);
    END;

    LOCAL PROCEDURE JobLedgEntriesExist@2() : Boolean;
    VAR
      JobLedgEntry@1001 : Record 11072005;
    BEGIN
      JobLedgEntry.SETCURRENTKEY("Job No.","Job Task No.");
      JobLedgEntry.SETRANGE("Job No.","Job No.");
      JobLedgEntry.SETRANGE("Job Task No.","Job Task No.");
      EXIT(JobLedgEntry.FINDFIRST)
    END;

    LOCAL PROCEDURE JobPlanningLinesExist@5() : Boolean;
    VAR
      JobPlanningLine@1000 : Record 11020401;
    BEGIN
      JobPlanningLine.SETCURRENTKEY("Job No.","Job Task No.");
      JobPlanningLine.SETRANGE("Job No.","Job No.");
      JobPlanningLine.SETRANGE("Job Task No.","Job Task No.");
      EXIT(JobPlanningLine.FINDFIRST)
    END;

    [External]
    PROCEDURE Caption@3() : Text[250];
    VAR
      Job@1000 : Record 11072003;
    BEGIN
      IF NOT Job.GET("Job No.") THEN
        EXIT('');
      EXIT(STRSUBSTNO('%1 %2 %3 %4',
          Job."No.",
          Job.Description,
          "Job Task No.",
          Description));
    END;

    [External]
    PROCEDURE InitWIPFields@13();
    VAR
      JobWIPTotal@1000 : Record 1021;
    BEGIN
      JobWIPTotal.SETRANGE("Job No.","Job No.");
      JobWIPTotal.SETRANGE("Job Task No.","Job Task No.");
      JobWIPTotal.SETRANGE("Posted to G/L",FALSE);
      JobWIPTotal.DELETEALL(TRUE);

      "Recognized Sales Amount" := 0;
      "Recognized Costs Amount" := 0;

      OnInitWIPFieldsOnBeforeModify(Rec);
      MODIFY;
    END;

    [External]
    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;ShortcutDimCode@1001 : Code[20]);
    VAR
      JobTask2@1002 : Record 11072600;
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF JobTask2.GET("Job No.","Job Task No.") THEN BEGIN
        DimMgt.SaveJobTaskDim("Job No.","Job Task No.",FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveJobTaskTempDim(FieldNumber,ShortcutDimCode);
    END;

    [External]
    PROCEDURE ClearTempDim@1();
    BEGIN
      DimMgt.DeleteJobTaskTempDim;
    END;

    [External]
    PROCEDURE ApplyPurchaseLineFilters@4(VAR PurchLine@1000 : Record 39;JobNo@1002 : Code[20];JobTaskNo@1001 : Code[20]);
    BEGIN
      PurchLine.SETCURRENTKEY("Document Type","Job No.","Job Task No.");
      PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
      PurchLine.SETRANGE("Job No.",JobNo);
      IF "Job Task Type" IN ["Job Task Type"::Total,"Job Task Type"::"End-Total"] THEN
        PurchLine.SETFILTER("Job Task No.",Totaling)
      ELSE
        PurchLine.SETRANGE("Job Task No.",JobTaskNo);
    END;

    [Integration]
    LOCAL PROCEDURE OnInitWIPFieldsOnBeforeModify@8(VAR JobTask@1000 : Record 11072600);
    BEGIN
    END;

    BEGIN
    END.
  }
}

