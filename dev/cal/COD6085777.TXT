OBJECT Codeunit 6085777 Sales Doc. - Reopen
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            SalesHeader@1000000001 : Record 36;
            TemplField@1000000005 : Record 6085580;
            SalesInvHeader@1000000002 : Record 112;
            SalesInvLine@1160040000 : Record 113;
            SalesCrMemoHeader@1000000003 : Record 114;
            SalesCrMemoLine@1160040001 : Record 115;
          BEGIN
            // *********************************************************************************************************************************
            // REOPEN A SALES DOCUMENT. IF THIS IS AN OPEN OR POSTED INVOICE IT CAN'T BE REOPEN.
            // *********************************************************************************************************************************

            CASE Status OF
              Status::Open:
                ERROR(Text004,Rec.TABLECAPTION);

              Status::Registered:
                BEGIN
                  TESTFIELD("Created Doc. Type",DATABASE::"Sales Header");

                  IF "Created Doc. Subtype" IN [SalesHeader."Document Type"::Order,SalesHeader."Document Type"::"Credit Memo"] THEN BEGIN
                    IF SalesHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN BEGIN
                      IF NOT CONFIRM(Text001,TRUE,SalesHeader."Document Type","Created Doc. ID") THEN
                        ERROR('');
                      SalesHeader.DELETE(TRUE);
                    END ELSE BEGIN
                      IF "Created Doc. Subtype" = SalesHeader."Document Type"::Order THEN BEGIN
                        SalesInvHeader.SETCURRENTKEY("Order No.");
                        SalesInvHeader.SETRANGE("Order No.","Created Doc. ID");
                        IF SalesInvHeader.FINDFIRST THEN BEGIN
                          // CHECK IF THE ORIGINAL DOCUMENT HAS JUST BEEN DELETE
                          SalesInvLine.SETRANGE("Document No.",SalesInvHeader."No.");
                          SalesInvLine.SETFILTER(Type,'>0');
                          IF NOT SalesInvLine.ISEMPTY THEN
                            ERROR(Text002);
                        END;
                      END ELSE BEGIN
                        SalesCrMemoHeader.SETCURRENTKEY("Pre-Assigned No.");
                        SalesCrMemoHeader.SETRANGE("Pre-Assigned No.","Created Doc. ID");
                        IF SalesCrMemoHeader.FINDFIRST THEN BEGIN
                          // CHECK IF THE ORIGINAL DOCUMENT HAS JUST BEEN DELETE
                          SalesCrMemoLine.SETRANGE("Document No.",SalesCrMemoHeader."No.");
                          SalesCrMemoLine.SETFILTER(Type,'>0');
                          IF NOT SalesCrMemoLine.ISEMPTY THEN
                            ERROR(Text002);
                        END;
                      END;

                      IF NOT CONFIRM(Text003,TRUE) THEN
                        ERROR('');
                    END;
                  END ELSE
                    IF SalesHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN
                      IF NOT CONFIRM(Text005,FALSE,SalesHeader."Document Type",SalesHeader."No.") THEN
                        ERROR('');
                END;

              Status::Rejected:
                IF NOT CONFIRM(STRSUBSTNO(Text003),TRUE) THEN
                  ERROR('');
            END;

            DeleteComments(-1);

            VALIDATE(Status,Status::Open);
            VALIDATE(OK,FALSE);
            MODIFY(TRUE);
          END;

  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'DEU=Dieser Beleg wurde bereits mit %1 %2 verknÅpft.\\Mîchten Sie %1 %2 lîschen, damit dieser Beleg wieder geîffnet werden kann?;ENU=This document is already linked to %1 %2.\\Do you want to delete %1 %2 so this document can be reopened?;NLD="Dit document is reeds gelinkt aan %1 %2.\\Wilt u %1 %2 verwijderen zodat dit document opnieuw kan geopend worden? "';
      Text002@1000000001 : TextConst 'DEU=Dieser Beleg wurde bereits gebucht und kann nicht wieder geîffnet werden.;ENU=This document has already been posted and cannot be reopened.;NLD=Dit document is al geboekt en kan niet worden heropend.';
      Text003@1000000002 : TextConst 'DEU=Mîchten Sie den Beleg wieder îffnen?;ENU=Do you want to reopen the document?;NLD=Wilt u het document heropenen?';
      Text004@1000000003 : TextConst 'DEU=%1 ist bereits geîffnet.;ENU=%1 is already open.;NLD=%1 is al open.';
      Text005@161024012 : TextConst 'DEU=Dieser Beleg wurde bereits mit %1 %2 verknÅpft.\\Mîchten Sie den Beleg trotzdem îffnen?;ENU=This document is already linked to %1 %2.\\Do you want to reopen the document anyway?;NLD=Dit document is al gekoppeld aan %1 %2.\\Wilt u het document toch heropenen?';

    BEGIN
    END.
  }
}

