OBJECT Table 11071808 Service Survey Surcharge
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceSurvey@1100528600 : Record 11071803;
             BEGIN
               IF ServiceSurvey.GET("Survey No.") THEN
                 ServiceSurvey.TESTFIELD("Summary Sheet for Surcharge", FALSE);
             END;

    CaptionML=[ENU=Service Survey Surcharge;
               NOR=Servicekartlegging, tilleggsavgift;
               SVE=Srv.kartl„ggning till„ggsavg.];
  }
  FIELDS
  {
    { 10  ;   ;Survey No.          ;Code20        ;TableRelation="Service Survey";
                                                   CaptionML=[ENU=Survey No.;
                                                              NOR=Kartlegging nr.;
                                                              SVE=Kartl„ggningsnr] }
    { 15  ;   ;Surcharge Type      ;Option        ;CaptionML=[ENU=Surcharge Type;
                                                              NOR=Tilleggsgebyrtype;
                                                              SVE=Till„ggsavgiftstyp];
                                                   OptionCaptionML=[ENU=Cost,Sales;
                                                                    NOR=Kostnad,Salg;
                                                                    SVE=Kostnad,F”rs„ljning];
                                                   OptionString=Cost,Sales }
    { 17  ;   ;Service Type        ;Code20        ;TableRelation="Service Type";
                                                   CaptionML=[ENU=Service Type;
                                                              NOR=Servicetype;
                                                              SVE=Servicetyp] }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer] }
    { 30  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                DimensionValue@1100528601 : Record 349;
                                                                DimensionManagement@1100528600 : Codeunit 408;
                                                                OptionFieldTools@1210190002 : Codeunit 11012271;
                                                                RecordRef@1210190000 : RecordRef;
                                                                FieldRef@1210190001 : FieldRef;
                                                                OptionCaption1@1210190003 : Text[250];
                                                                OptionCaption2@1210190004 : Text[250];
                                                              BEGIN
                                                                IF ("Cost Object" <> xRec."Cost Object") OR (Description = '') THEN BEGIN
                                                                  DimensionManagement.GetDimValueRec(2, "Cost Object", DimensionValue, TRUE, '');
                                                                  IF "Cost Type Filter" <> "Cost Type Filter"::" " THEN BEGIN
                                                                    IF (DimensionValue."Cost Type" <> DimensionValue."Cost Type"::Sundry) AND
                                                                      ("Cost Type Filter" -1 <> DimensionValue."Cost Type")
                                                                    THEN BEGIN
                                                                      RecordRef.GETTABLE(DimensionValue);
                                                                      FieldRef := RecordRef.FIELD(DimensionValue.FIELDNO("Cost Type"));
                                                                      OptionFieldTools.OptionValueToReadableOption(FieldRef.OPTIONCAPTION, "Cost Type Filter" -1, OptionCaption1);
                                                                      OptionFieldTools.OptionValueToReadableOption(FieldRef.OPTIONCAPTION, "Cost Type"::Sundry, OptionCaption2);
                                                                      DimensionValue.FIELDERROR("Cost Type", STRSUBSTNO(Text000, OptionCaption1, OptionCaption2));
                                                                    END;
                                                                  END ELSE
                                                                    DimensionValue.TESTFIELD("Cost Type", DimensionValue."Cost Type"::Sundry);
                                                                  Description := DimensionValue.Name;
                                                                  "Cost Type" := DimensionValue."Cost Type";
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              DimensionValue@1210190000 : Record 349;
                                                            BEGIN
                                                              DimensionValue.SETRANGE("Global Dimension No.", 2);
                                                              IF "Cost Type Filter" <> "Cost Type Filter"::" " THEN
                                                                DimensionValue.SETFILTER("Cost Type", '%1|%2', "Cost Type Filter" -1, DimensionValue."Cost Type"::Sundry)
                                                              ELSE
                                                                DimensionValue.SETRANGE("Cost Type", DimensionValue."Cost Type"::Sundry);
                                                              IF "Cost Object" <> '' THEN BEGIN
                                                                DimensionValue.SETRANGE(Code, "Cost Object");
                                                                IF NOT DimensionValue.FINDFIRST THEN
                                                                  DimensionValue.INIT;
                                                                DimensionValue.SETRANGE(Code);
                                                              END;
                                                              IF PAGE.RUNMODAL(0, DimensionValue) = ACTION::LookupOK THEN
                                                                VALIDATE("Cost Object", DimensionValue.Code);
                                                            END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   NotBlank=Yes;
                                                   CaptionClass='1,1,2' }
    { 40  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 50  ;   ;Cost Type           ;Option        ;InitValue=Sundry;
                                                   CaptionML=[ENU=Cost Type;
                                                              NOR=Kostnadstype;
                                                              SVE=Kostnadstyp];
                                                   OptionCaptionML=[ENU=Labor,Material,Subcontracting,Plant,Sundry;
                                                                    NOR=Arbeidskraft,Materiale,Underentrepen›r,Maskin,Diverse;
                                                                    SVE=Arbetskraft,Material,Underentreprenad,Maskin,Diverse];
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry;
                                                   Editable=No }
    { 60  ;   ;Surcharge Percentage;Decimal       ;CaptionML=[ENU=Surcharge Percentage;
                                                              NOR=Tilleggsgebyrsprosent;
                                                              SVE=Till„ggsavgiftsprocent] }
    { 70  ;   ;Cost Type Filter    ;Option        ;OnValidate=VAR
                                                                DimensionValue@1100529600 : Record 349;
                                                              BEGIN
                                                                IF "Cost Object Filter" <> '' THEN
                                                                  IF ("Cost Type Filter" <> xRec."Cost Type Filter") AND ("Cost Type Filter" <> "Cost Type Filter"::" ") THEN BEGIN
                                                                    DimensionValue.SETRANGE("Global Dimension No.", 2);
                                                                    DimensionValue.SETRANGE("Cost Type", "Cost Type Filter" - 1);
                                                                    DimensionValue.SETRANGE(Code, "Cost Object Filter");
                                                                    IF DimensionValue.ISEMPTY THEN
                                                                      "Cost Object Filter" := '';
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Cost Type Filter;
                                                              NOR=Kostnadstype, filter;
                                                              SVE=Filter f”r kostnadstyp];
                                                   OptionCaptionML=[ENU=" ,Labor,Material,Subcontracting,Plant,Sundry";
                                                                    NOR=" ,Arbeidskraft,Materiale,Underentrepen›r,Maskin,Diverse";
                                                                    SVE=" ,Arbetskraft,Material,Underentreprenad,Maskin,Diverse"];
                                                   OptionString=[ ,Labor,Material,Subcontracting,Plant,Sundry] }
    { 80  ;   ;Cost Object Filter  ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnLookup=VAR
                                                              DimensionValue@1100529600 : Record 349;
                                                            BEGIN
                                                              DimensionValue.SETRANGE("Global Dimension No.", 2);
                                                              IF "Cost Type Filter" <> "Cost Type Filter"::" " THEN
                                                                DimensionValue.SETRANGE("Cost Type", "Cost Type Filter" - 1);
                                                              IF "Cost Object Filter" <> '' THEN BEGIN
                                                                DimensionValue.SETRANGE(Code, "Cost Object Filter");
                                                                IF DimensionValue.FINDFIRST THEN;
                                                                DimensionValue.SETRANGE(Code);
                                                              END;
                                                              IF PAGE.RUNMODAL(0, DimensionValue) = ACTION::LookupOK THEN BEGIN
                                                                "Cost Object Filter" := DimensionValue.Code;
                                                                IF "Cost Type Filter" <> "Cost Type Filter"::" " THEN
                                                                  "Cost Type Filter" := DimensionValue."Cost Type" + 1;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Global Dimension 2 Filter;
                                                              NOR=Kostnadsobjekt, filter;
                                                              SVE=Filter f”r kostnadsb„rare];
                                                   CaptionClass='1,3,2' }
  }
  KEYS
  {
    {    ;Survey No.,Surcharge Type,Line No.      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=needs to be %1 or %2;NOR=M† v‘re %1 eller %2;SVE=m†ste vara %1 eller %2';

    PROCEDURE UpdateDetailLines@1100528600();
    VAR
      ServSurvLocationObject@1100528601 : Record 11071813;
      ServSurvLocObjDetail@1100528600 : Record 11071807;
    BEGIN
      ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
      ServSurvLocationObject.SETRANGE("Objects Created", FALSE);
      IF ServSurvLocationObject.FINDSET(TRUE) THEN
        REPEAT
          ServSurvLocObjDetail.SETRANGE("Survey No.", ServSurvLocationObject."Survey No.");
          ServSurvLocObjDetail.SETRANGE("Location Code", ServSurvLocationObject."Location Code");
          ServSurvLocObjDetail.SETRANGE("Seq. No.", ServSurvLocationObject."Seq. No.");
          IF ServSurvLocObjDetail.FINDSET(TRUE) THEN
            REPEAT
              ServSurvLocObjDetail.UpdateTotals();
              ServSurvLocObjDetail.MODIFY(TRUE);
            UNTIL ServSurvLocObjDetail.NEXT = 0;
          ServSurvLocationObject.UpdateTotals;
          ServSurvLocationObject.MODIFY(TRUE);
        UNTIL ServSurvLocationObject.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

