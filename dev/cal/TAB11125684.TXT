OBJECT Table 11125684 Comment Line RAW
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckTableNo;
               CheckPartGroup;
               CheckActivity;
               CheckEstimateNo;
               CheckElementNo;
               CheckEstElemLine;
               CheckEstimateLine;
             END;

    OnRename=BEGIN
               CheckTableNo;
               CheckPartGroup;
               CheckActivity;
               CheckEstimateNo;
               CheckElementNo;
               CheckEstElemLine;
               CheckEstimateLine;
             END;

    CaptionML=ENU=Comment Line RAW;
  }
  FIELDS
  {
    { 10  ;   ;Table No.           ;Integer       ;OnValidate=BEGIN
                                                                CheckTableNo;
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr.;
                                                              SVE=Tabellnr] }
    { 20  ;   ;Part Group          ;Code10        ;TableRelation="Estimate Group".No. WHERE (Type=CONST(Part));
                                                   OnValidate=BEGIN
                                                                CheckPartGroup;
                                                              END;

                                                   CaptionML=[ENU=Part Group;
                                                              NOR=Del gruppe;
                                                              SVE=Delgrupp] }
    { 21  ;   ;Activity            ;Code20        ;TableRelation="Constr. Spec. Activity RAW".Activity WHERE (Part Group=FIELD(Part Group));
                                                   OnValidate=BEGIN
                                                                CheckActivity;
                                                              END;

                                                   CaptionML=[ENU=Activity;
                                                              NOR=Aktivitet;
                                                              SVE=Aktivitet];
                                                   NotBlank=Yes }
    { 30  ;   ;Estimate No.        ;Code20        ;TableRelation=Estimate;
                                                   OnValidate=BEGIN
                                                                CheckEstimateNo;
                                                              END;

                                                   CaptionML=[ENU=Estimate No.;
                                                              NOR=Kalkylenr;
                                                              SVE=Kalkylnr] }
    { 31  ;   ;Tender Paragraph No.;Text20        ;TableRelation="Estimate Paragraph RAW"."Tender Paragraph No." WHERE (Estimate No.=FIELD(Estimate No.));
                                                   OnValidate=BEGIN
                                                                CheckElementNo;
                                                              END;

                                                   CaptionML=[ENU=Tender Paragraph;
                                                              NOR=Elementnr;
                                                              SVE=Elementnr];
                                                   NotBlank=Yes;
                                                   Description=DP01676 }
    { 32  ;   ;Tender Paragraph Line;Integer      ;TableRelation="Estimate Paragraph RAW"."Line No." WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                                            Tender Paragraph No.=FIELD(Tender Paragraph No.));
                                                   OnValidate=BEGIN
                                                                CheckEstElemLine;
                                                              END;

                                                   CaptionML=ENU=Estimate Tender Paragraph Line. }
    { 33  ;   ;Estimate Line No.   ;Integer       ;TableRelation="Estimate Line RAW"."Line No." WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                                       Tender Paragraph No.=FIELD(Tender Paragraph No.));
                                                   OnValidate=BEGIN
                                                                CheckEstimateLine;
                                                              END;

                                                   CaptionML=ENU=Estimate Line No.. }
    { 90  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 100 ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum] }
    { 110 ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 120 ;   ;Comment Code        ;Code20        ;TableRelation="Comment Code";
                                                   CaptionML=[ENU=Comment Code;
                                                              NOR=Kommentarkode;
                                                              SVE=Kommentarskod] }
    { 130 ;   ;Comment             ;Text80        ;CaptionML=[ENU=Comment;
                                                              NOR=Kommentar;
                                                              SVE=Kommentar] }
    { 140 ;   ;Created by          ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Created by;
                                                              NOR=Opprettet av;
                                                              SVE=Skapad av];
                                                   Editable=No }
    { 150 ;   ;Time Created        ;Time          ;OnValidate=VAR
                                                                DateTime@1100525000 : DateTime;
                                                              BEGIN
                                                                IF "Time Created" <> 0T THEN BEGIN
                                                                  DateTime := CREATEDATETIME(TODAY, "Time Created");
                                                                  DateTime := ROUNDDATETIME(DateTime);
                                                                  "Time Created" := DT2TIME(DateTime);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Time;
                                                              NOR=Tidspunkt;
                                                              SVE=Tid] }
    { 160 ;   ;Line Break          ;Boolean       ;CaptionML=[ENU=Line Break;
                                                              NOR=Radbrytning;
                                                              SVE=Radbrytning] }
  }
  KEYS
  {
    {    ;Table No.,Part Group,Activity,Estimate No.,Tender Paragraph No.,Tender Paragraph Line,Estimate Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE CheckTableNo@1100529000();
    BEGIN
      CASE "Table No." OF
        DATABASE::"Constr. Spec. Activity RAW",
        DATABASE::"Estimate Paragraph RAW",
        DATABASE::"Estimate Line RAW":
          EXIT;  //OK
        ELSE
          FIELDERROR("Table No.");
      END;
    END;

    LOCAL PROCEDURE CheckPartGroup@1100529006();
    BEGIN
      CASE "Table No." OF
        DATABASE::"Constr. Spec. Activity RAW":
          TESTFIELD("Part Group");
        ELSE
          TESTFIELD("Part Group", '');

      END;
    END;

    LOCAL PROCEDURE CheckActivity@1100529007();
    BEGIN
      CASE "Table No." OF
        DATABASE::"Constr. Spec. Activity RAW":
          TESTFIELD(Activity);
        ELSE
          TESTFIELD(Activity, '');
      END;
    END;

    LOCAL PROCEDURE CheckEstimateNo@1100529008();
    BEGIN
      CASE "Table No." OF
        DATABASE::"Estimate Paragraph RAW",
        DATABASE::"Estimate Line RAW":
          TESTFIELD("Estimate No.");
        ELSE
          TESTFIELD("Estimate No.", '');
      END;
    END;

    LOCAL PROCEDURE CheckElementNo@1100525002();
    BEGIN
      CASE "Table No." OF
        DATABASE::"Estimate Paragraph RAW",
        DATABASE::"Estimate Line RAW":
         TESTFIELD("Tender Paragraph No.")
        ELSE
          TESTFIELD("Tender Paragraph No.", '');
      END;
    END;

    LOCAL PROCEDURE CheckEstElemLine@1100525003();
    BEGIN
      CASE "Table No." OF
        DATABASE::"Estimate Paragraph RAW":
          EXIT;  //OK
        ELSE
          TESTFIELD("Tender Paragraph Line", 0);
      END;
    END;

    LOCAL PROCEDURE CheckEstimateLine@1100525008();
    BEGIN
      CASE "Table No." OF
        DATABASE::"Estimate Line RAW":
          EXIT;  //OK
        ELSE
          TESTFIELD("Estimate Line No.", 0);
      END;
    END;

    PROCEDURE SetUpNewLine@1();
    BEGIN
      Date := WORKDATE;
      VALIDATE("Time Created", TIME);
      "Created by" := USERID;
    END;

    PROCEDURE SetCommentLineFilters@1100528600(VAR OCommentLineRAW@1100528600 : Record 11125684);
    BEGIN
      OCommentLineRAW.FILTERGROUP(2);
      OCommentLineRAW.SETRANGE("Table No.", "Table No.");
      OCommentLineRAW.SETRANGE("Part Group", '');
      OCommentLineRAW.SETRANGE(Activity, '');
      OCommentLineRAW.SETRANGE("Estimate No.", '');
      OCommentLineRAW.SETRANGE("Tender Paragraph No.", '');
      OCommentLineRAW.SETRANGE("Tender Paragraph Line", 0);
      OCommentLineRAW.SETRANGE("Estimate Line No.", 0);
      CASE "Table No." OF
        DATABASE::"Constr. Spec. Activity RAW":
          BEGIN
            OCommentLineRAW.SETRANGE("Part Group", "Part Group");
            OCommentLineRAW.SETRANGE(Activity, Activity);
          END;
        DATABASE::"Estimate Paragraph RAW":
          BEGIN
            OCommentLineRAW.SETRANGE("Estimate No.", "Estimate No.");
            OCommentLineRAW.SETRANGE("Tender Paragraph No.", "Tender Paragraph No.");
            OCommentLineRAW.SETRANGE("Tender Paragraph Line", "Tender Paragraph Line");
          END;
        DATABASE::"Estimate Line RAW":
          BEGIN
            OCommentLineRAW.SETRANGE("Estimate No.", "Estimate No.");
            OCommentLineRAW.SETRANGE("Tender Paragraph No.", "Tender Paragraph No.");
            OCommentLineRAW.SETRANGE("Estimate Line No.", "Estimate Line No.");
          END;
      ELSE
        EXIT;
      END;
      OCommentLineRAW.FILTERGROUP(0);
    END;

    PROCEDURE EditCommentLines@1210190000(EditableMode@1100529000 : Boolean);
    VAR
      CommentLineRAW@1100485000 : Record 11125684;
      CommentsRAW@1100529001 : Page 11125705;
    BEGIN
      SetCommentLineFilters(CommentLineRAW);
      CommentsRAW.EDITABLE(EditableMode);
      CommentsRAW.SETTABLEVIEW(CommentLineRAW);
      CommentsRAW.RUNMODAL;
    END;

    PROCEDURE HasActivityComment@1100525000(PartGroup@1100525000 : Code[10];Activity@1100525001 : Code[10]) : Boolean;
    VAR
      CommentLineRAW@1100525002 : Record 11125684;
    BEGIN
      CommentLineRAW.SETRANGE("Table No.", DATABASE::"Constr. Spec. Activity RAW");
      CommentLineRAW.SETRANGE("Part Group", PartGroup);
      CommentLineRAW.SETRANGE(Activity, Activity);
      CommentLineRAW.SETRANGE("Estimate No.", '');
      CommentLineRAW.SETRANGE("Tender Paragraph No.", '');
      CommentLineRAW.SETRANGE("Tender Paragraph Line", 0);
      CommentLineRAW.SETRANGE("Estimate Line No.", 0);
      EXIT(NOT CommentLineRAW.ISEMPTY);
    END;

    PROCEDURE HasEstElemLineComment@1100525001(EstimateNo@1100525000 : Code[20];ElementNo@1100525001 : Text[6];EstElemLine@1100525002 : Integer) : Boolean;
    VAR
      CommentLineRAW@1100525003 : Record 11125684;
    BEGIN
      CommentLineRAW.SETRANGE("Table No.", DATABASE::"Estimate Paragraph RAW");
      CommentLineRAW.SETRANGE("Part Group", '');
      CommentLineRAW.SETRANGE(Activity, '');
      CommentLineRAW.SETRANGE("Estimate No.", EstimateNo);
      CommentLineRAW.SETRANGE("Tender Paragraph No.", ElementNo);
      CommentLineRAW.SETRANGE("Tender Paragraph Line", EstElemLine);
      CommentLineRAW.SETRANGE("Estimate Line No.", 0);
      EXIT(NOT CommentLineRAW.ISEMPTY);
    END;

    PROCEDURE HasEstimateLineComment@1100525015(EstimateNo@1100525000 : Code[20];ElementNo@1100525001 : Text[6];EstimateLine@1100525002 : Integer) : Boolean;
    VAR
      CommentLineRAW@1100525003 : Record 11125684;
    BEGIN
      CommentLineRAW.SETRANGE("Table No.", DATABASE::"Estimate Paragraph RAW");
      CommentLineRAW.SETRANGE("Part Group", '');
      CommentLineRAW.SETRANGE(Activity, '');
      CommentLineRAW.SETRANGE("Estimate No.", EstimateNo);
      CommentLineRAW.SETRANGE("Tender Paragraph No.", ElementNo);
      CommentLineRAW.SETRANGE("Tender Paragraph Line", 0);
      CommentLineRAW.SETRANGE("Estimate Line No.", EstimateLine);
      EXIT(NOT CommentLineRAW.ISEMPTY);
    END;

    PROCEDURE CopyComment@1100525004(OrigTableNo@1100525000 : Integer;OrigPartGroup@1100525001 : Code[10];OrigActivity@1100525002 : Code[10];OrigEstimateNo@1100525003 : Code[20];OrigElementNo@1100525004 : Text[6];OrigEstElemLine@1100525005 : Integer;OrigEstLine@1100525015 : Integer;DestTableNo@1100525011 : Integer;DestPartGroup@1100525010 : Code[10];DestActivity@1100525009 : Code[10];DestEstimateNo@1100525008 : Code[20];DestElementNo@1100525007 : Text[6];DestEstElemLine@1100525006 : Integer;DestEstLine@1100525016 : Integer);
    VAR
      OrigCommentLineRAW@1100525012 : Record 11125684;
      DestCommentLineRAW@1100525013 : Record 11125684;
      NextLineNo@1100525014 : Integer;
    BEGIN
      OrigCommentLineRAW.SETRANGE("Table No.", OrigTableNo);
      OrigCommentLineRAW.SETRANGE("Part Group", '');
      OrigCommentLineRAW.SETRANGE(Activity, '');
      OrigCommentLineRAW.SETRANGE("Estimate No.", '');
      OrigCommentLineRAW.SETRANGE("Tender Paragraph No.", '');
      OrigCommentLineRAW.SETRANGE("Tender Paragraph Line", 0);
      OrigCommentLineRAW.SETRANGE("Estimate Line No.", 0);
      CASE OrigTableNo OF
        DATABASE::"Constr. Spec. Activity RAW":
          BEGIN
            OrigCommentLineRAW.SETRANGE("Part Group", OrigPartGroup);
            OrigCommentLineRAW.SETRANGE(Activity, OrigActivity);
          END;
        DATABASE::"Estimate Paragraph RAW":
          BEGIN
            OrigCommentLineRAW.SETRANGE("Estimate No.", OrigEstimateNo);
            OrigCommentLineRAW.SETRANGE("Tender Paragraph No.", OrigElementNo);
            OrigCommentLineRAW.SETRANGE("Tender Paragraph Line", OrigEstElemLine);
          END;
        DATABASE::"Estimate Line RAW":
          BEGIN
            OrigCommentLineRAW.SETRANGE("Estimate No.", OrigEstimateNo);
            OrigCommentLineRAW.SETRANGE("Tender Paragraph No.", OrigElementNo);
            OrigCommentLineRAW.SETRANGE("Estimate Line No.", OrigEstLine);
          END;
      END;
      IF OrigCommentLineRAW.FINDSET THEN BEGIN
        REPEAT
          DestCommentLineRAW.TRANSFERFIELDS(OrigCommentLineRAW, FALSE);
          DestCommentLineRAW."Table No." := DestTableNo;
          DestCommentLineRAW."Part Group" := '';
          DestCommentLineRAW.Activity := '';
          DestCommentLineRAW."Estimate No." := '';
          DestCommentLineRAW."Tender Paragraph No." := '';
          DestCommentLineRAW."Tender Paragraph Line" := 0;
          DestCommentLineRAW."Estimate Line No." := 0;
          CASE DestTableNo OF
            DATABASE::"Constr. Spec. Activity RAW":
              BEGIN
                DestCommentLineRAW."Part Group" := DestPartGroup;
                DestCommentLineRAW.Activity := DestActivity;
              END;
            DATABASE::"Estimate Paragraph RAW":
              BEGIN
                DestCommentLineRAW."Estimate No." := DestEstimateNo;
                DestCommentLineRAW."Tender Paragraph No." := DestElementNo;
                DestCommentLineRAW."Tender Paragraph Line" := DestEstElemLine;
              END;
            DATABASE::"Estimate Line RAW":
              BEGIN
                DestCommentLineRAW."Estimate No." := DestEstimateNo;
                DestCommentLineRAW."Tender Paragraph No." := DestElementNo;
                DestCommentLineRAW."Estimate Line No." := DestEstLine;
              END;
          END;
          DestCommentLineRAW.SetUpNewLine();
          IF NextLineNo = 0 THEN
            NextLineNo := GetNextLineNo(DestCommentLineRAW);
          DestCommentLineRAW."Line No." := NextLineNo;
          DestCommentLineRAW.INSERT(TRUE);
          NextLineNo := NextLineNo + 10000;
        UNTIL OrigCommentLineRAW.NEXT = 0;
      END;
    END;

    PROCEDURE CopyCommentRecipeToEstLine@1100525009(OrigRecipeGroup@1100525001 : Code[10];OrigRecipeNo@1100525003 : Code[20];OrigRecipeLineNo@1100525005 : Integer;DestEstLine@1100525011 : Record 11125698);
    VAR
      OrigCommentLineGroup@1100525012 : Record 11020595;
      DestCommentLineRAW@1100525013 : Record 11125684;
      NextLineNo@1100525014 : Integer;
    BEGIN
      OrigCommentLineGroup.SETRANGE("Table Name", OrigCommentLineGroup."Table Name"::Estimate);
      OrigCommentLineGroup.SETRANGE(Group, OrigRecipeGroup);
      OrigCommentLineGroup.SETRANGE("No.", OrigRecipeNo);
      OrigCommentLineGroup.SETRANGE("Estimate No.", '');
      OrigCommentLineGroup.SETRANGE("Sub-Estimate No.", '');
      OrigCommentLineGroup.SETRANGE("Part Group", '');
      OrigCommentLineGroup.SETRANGE(Part, '');
      OrigCommentLineGroup.SETRANGE("Estimate Line No.", 0);
      OrigCommentLineGroup.SETRANGE("Recipe Line No.", OrigRecipeLineNo);
      OrigCommentLineGroup.SETRANGE("Part Line No.", 0);
      IF OrigCommentLineGroup.FINDSET THEN BEGIN
        REPEAT
          DestCommentLineRAW.INIT;
          DestCommentLineRAW."Table No." := DATABASE::"Estimate Line RAW";
          DestCommentLineRAW."Part Group" := '';
          DestCommentLineRAW.Activity := '';
          DestCommentLineRAW."Estimate No." := DestEstLine."Estimate No.";
          DestCommentLineRAW."Tender Paragraph No." := DestEstLine."Tender Paragraph No.";
          DestCommentLineRAW."Tender Paragraph Line" := 0;
          DestCommentLineRAW."Estimate Line No." := DestEstLine."Line No.";
          DestCommentLineRAW.Code := OrigCommentLineGroup.Code;
          DestCommentLineRAW."Comment Code" := OrigCommentLineGroup."Comment Code";
          DestCommentLineRAW.Comment := OrigCommentLineGroup.Comment;
          DestCommentLineRAW."Line Break" := OrigCommentLineGroup."Line Break";
          DestCommentLineRAW.SetUpNewLine();
          IF NextLineNo = 0 THEN
            NextLineNo := GetNextLineNo(DestCommentLineRAW);
          DestCommentLineRAW."Line No." := NextLineNo;
          DestCommentLineRAW.INSERT(TRUE);
          NextLineNo := NextLineNo + 10000;
        UNTIL OrigCommentLineGroup.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE GetNextLineNo@1100525005(CommentLineRAW@1100525000 : Record 11125684) : Integer;
    BEGIN
      WITH CommentLineRAW DO BEGIN
        SETRANGE("Table No.", "Table No.");
        SETRANGE("Part Group", "Part Group");
        SETRANGE(Activity, Activity);
        SETRANGE("Estimate No.", "Estimate No.");
        SETRANGE("Tender Paragraph No.", "Tender Paragraph No.");
        SETRANGE("Tender Paragraph Line", "Tender Paragraph Line");
        SETRANGE("Estimate Line No.", "Estimate Line No.");
        IF NOT FINDLAST THEN
          "Line No." := 0;
        EXIT("Line No." + 10000);
      END;
    END;

    PROCEDURE DeleteActivityComment@1100525007(PartGroup@1100525000 : Code[10];Activity@1100525001 : Code[10]);
    VAR
      CommentLineRAW@1100525002 : Record 11125684;
    BEGIN
      CommentLineRAW.SETRANGE("Table No.", DATABASE::"Constr. Spec. Activity RAW");
      CommentLineRAW.SETRANGE("Part Group", PartGroup);
      CommentLineRAW.SETRANGE(Activity, Activity);
      CommentLineRAW.DELETEALL;
    END;

    PROCEDURE DeleteEstElemLineComment@1100525006(EstimateNo@1100525000 : Code[20];ElementNo@1100525001 : Text[6];EstElemLine@1100525002 : Integer);
    VAR
      CommentLineRAW@1100525003 : Record 11125684;
    BEGIN
      CommentLineRAW.SETRANGE("Table No.", DATABASE::"Estimate Paragraph RAW");
      CommentLineRAW.SETRANGE("Estimate No.", EstimateNo);
      CommentLineRAW.SETRANGE("Tender Paragraph No.", ElementNo);
      CommentLineRAW.SETRANGE("Tender Paragraph Line", EstElemLine);
      CommentLineRAW.DELETEALL;
    END;

    PROCEDURE DeleteEstimateLineComment@1100525010(EstimateNo@1100525000 : Code[20];ElementNo@1100525001 : Text[6];EstLineNo@1100525002 : Integer);
    VAR
      CommentLineRAW@1100525003 : Record 11125684;
    BEGIN
      CommentLineRAW.SETRANGE("Table No.", DATABASE::"Estimate Paragraph RAW");
      CommentLineRAW.SETRANGE("Estimate No.", EstimateNo);
      CommentLineRAW.SETRANGE("Tender Paragraph No.", ElementNo);
      CommentLineRAW.SETRANGE("Estimate Line No.", EstLineNo);
      CommentLineRAW.DELETEALL;
    END;

    BEGIN
    {
      //DP0186
    }
    END.
  }
}

