OBJECT Codeunit 6085642 Doc. (UIC) Blob Storage Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CurrentCompanyName@1000 : Text[50];
      Text0001@6085573 : TextConst 'DEU=Datenbank;ENU=Database;NLD=Database;SVE=Databas';

    PROCEDURE HasTiffFile@11(VAR Document@1000 : Record 6085601) : Boolean;
    BEGIN
      Document.CALCFIELDS("TIFF Image File");
      EXIT(Document."TIFF Image File".HASVALUE);
    END;

    PROCEDURE HasPdfFile@4(VAR Document@1000 : Record 6085601) : Boolean;
    BEGIN
      Document.CALCFIELDS("PDF File");
      EXIT(Document."PDF File".HASVALUE);
    END;

    PROCEDURE HasEmailFile@18(VAR Document@1000 : Record 6085601) : Boolean;
    VAR
      EMail@1001 : Record 6085609;
    BEGIN
      IF CurrentCompanyName <> '' THEN
        EMail.CHANGECOMPANY(CurrentCompanyName);

      IF EMail.GET(Document."E-Mail GUID") THEN BEGIN
        EMail.CALCFIELDS("E-Mail File");
        EXIT(EMail."E-Mail File".HASVALUE);
      END;
    END;

    PROCEDURE ClearTiffFile@14(VAR Document@1001 : Record 6085601) : Boolean;
    BEGIN
      CLEAR(Document."TIFF Image File");
      Document.MODIFY;
    END;

    PROCEDURE ClearPdfFile@13(VAR Document@1001 : Record 6085601) : Boolean;
    BEGIN
      CLEAR(Document."PDF File");
      Document.MODIFY;
    END;

    PROCEDURE ClearEmailFile@5(VAR Document@1001 : Record 6085601) : Boolean;
    VAR
      EMail@1000 : Record 6085609;
      DCSetup@1002 : Record 6085573;
    BEGIN
      DCSetup.GET;

      IF CurrentCompanyName <> '' THEN
        EMail.CHANGECOMPANY(CurrentCompanyName);

      IF EMail.GET(Document."E-Mail GUID") AND
        ((NOT EmailHasMoreDocuments(Document)) OR (DCSetup."Document Storage Type" <> DCSetup."Document Storage Type"::Database))
      THEN
        EMail.DELETE;
    END;

    PROCEDURE GetTiffFile@22(VAR Document@1001 : Record 6085601;VAR File@1000 : TEMPORARY Record 6085608) : Boolean;
    VAR
      ReadStream@1002 : InStream;
    BEGIN
      Document.CALCFIELDS("TIFF Image File");
      IF NOT Document."TIFF Image File".HASVALUE THEN
        EXIT(FALSE);

      Document."TIFF Image File".CREATEINSTREAM(ReadStream);
      File.CreateFromStream(Document.Code + '.tiff',ReadStream);
      EXIT(TRUE);
    END;

    PROCEDURE GetPdfFile@23(VAR Document@1001 : Record 6085601;VAR File@1000 : TEMPORARY Record 6085608) : Boolean;
    VAR
      ReadStream@1002 : InStream;
    BEGIN
      Document.CALCFIELDS("PDF File");
      IF NOT Document."PDF File".HASVALUE THEN
        EXIT(FALSE);

      Document."PDF File".CREATEINSTREAM(ReadStream);
      File.CreateFromStream(Document.Code + '.pdf',ReadStream);
      EXIT(TRUE);
    END;

    PROCEDURE GetEmailFile@25(VAR Document@1001 : Record 6085601;VAR TempFile@1000 : Record 6085608) : Boolean;
    VAR
      EMail@1002 : Record 6085609;
      ReadStream@1003 : InStream;
    BEGIN
      IF CurrentCompanyName <> '' THEN
        EMail.CHANGECOMPANY(CurrentCompanyName);

      IF EMail.GET(Document."E-Mail GUID") THEN BEGIN
        EMail.CALCFIELDS("E-Mail File");
        IF EMail."E-Mail File".HASVALUE THEN BEGIN
          EMail."E-Mail File".CREATEINSTREAM(ReadStream);
          TempFile.CreateFromStream(Document.GetEmailGUIDAsText + '.eml',ReadStream);
        END;
        EXIT(EMail."E-Mail File".HASVALUE);
      END;
    END;

    PROCEDURE SetTiffFile@27(VAR Document@1001 : Record 6085601;VAR TempFile@1000 : TEMPORARY Record 6085608) : Boolean;
    VAR
      ReadStream@1003 : InStream;
      WriteStream@1002 : OutStream;
    BEGIN
      Document."TIFF Image File".CREATEOUTSTREAM(WriteStream);
      TempFile.GetDataStream(ReadStream);
      COPYSTREAM(WriteStream,ReadStream);
      EXIT(Document.MODIFY);
    END;

    PROCEDURE SetPdfFile@26(VAR Document@1001 : Record 6085601;VAR TempFile@1000 : TEMPORARY Record 6085608) : Boolean;
    VAR
      ReadStream@1003 : InStream;
      WriteStream@1002 : OutStream;
    BEGIN
      Document."PDF File".CREATEOUTSTREAM(WriteStream);
      TempFile.GetDataStream(ReadStream);
      COPYSTREAM(WriteStream,ReadStream);
      EXIT(Document.MODIFY);
    END;

    PROCEDURE SetEmailFile@19(VAR Document@1001 : Record 6085601;VAR TempFile@1004 : Record 6085608) : Boolean;
    VAR
      EMail@1003 : Record 6085609;
      ReadStream@1000 : InStream;
      WriteStream@1002 : OutStream;
    BEGIN
      IF CurrentCompanyName <> '' THEN
        EMail.CHANGECOMPANY(CurrentCompanyName);

      IF EMail.GET(Document."E-Mail GUID") THEN BEGIN
        EMail."E-Mail File".CREATEOUTSTREAM(WriteStream);
        TempFile.GetDataStream(ReadStream);
        COPYSTREAM(WriteStream,ReadStream);
        EXIT(EMail.MODIFY);
      END ELSE BEGIN
        EMail.INIT;
        EMail.GUID := Document."E-Mail GUID";
        EMail."E-Mail File".CREATEOUTSTREAM(WriteStream);
        TempFile.GetDataStream(ReadStream);
        COPYSTREAM(WriteStream,ReadStream);
        EXIT(EMail.INSERT);
      END;
    END;

    LOCAL PROCEDURE EmailHasMoreDocuments@1160040019(Document@1000 : Record 6085601) : Boolean;
    VAR
      Document2@1160040000 : Record 6085601;
      EmptyGuid@1160040001 : GUID;
    BEGIN
      IF CurrentCompanyName <> '' THEN
        Document2.CHANGECOMPANY(CurrentCompanyName);

      WITH Document DO BEGIN
        IF "E-Mail GUID" = EmptyGuid THEN
          EXIT;

        Document2.SETCURRENTKEY("E-Mail GUID");
        Document2.SETRANGE("E-Mail GUID","E-Mail GUID");
        Document2.SETFILTER(Code,'<>%1',Code);
        EXIT(NOT Document2.ISEMPTY);
      END;
    END;

    PROCEDURE SetCurrentCompany@21(NewCompanyName@1000 : Text[50]);
    BEGIN
      CurrentCompanyName := NewCompanyName;
    END;

    BEGIN
    END.
  }
}

