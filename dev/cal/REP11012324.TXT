OBJECT Report 11012324 Remove Discount History
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Rabatthistorie l”schen;
               ENU=Remove Discount History;
               NLD=Kortingshistorie verwijderen;
               NOR=Fjerne Rabatthistorikk;
               SVE=Ta bort rabatthistorik];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   GetItemSetup;
                 END;

    OnPostReport=BEGIN
                   StoreItemSetup;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 5431;    ;DataItem;                    ;
               DataItemTable=Table11012312;
               OnPreDataItem=BEGIN
                               Status.OPEN(Text001);
                               NoOfLines := COUNT;
                               StatusCounter := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  StatusCounter := StatusCounter+1;
                                  Status.UPDATE(1,ROUND(StatusCounter/NoOfLines*10000,1));
                                  HistLimitDate := ItemSetup."Discount History From";

                                  IF ("Ending Date" <> 0D) THEN BEGIN
                                    IF ("Ending Date" < HistLimitDate) THEN BEGIN
                                      DELETE(TRUE);  //db, 02-01-14: C010943
                                      DeleteDeliveryGroup(Vendor,"Discount Group");//SR, 17-12-2014: C013498
                                    END;
                                  END ELSE BEGIN
                                    DiscHistRec.SETRANGE(Vendor, Vendor);
                                    IF "Discount Group" = '' THEN
                                      DiscHistRec.SETFILTER("Discount Group", '%1', '')
                                    ELSE
                                      DiscHistRec.SETRANGE("Discount Group", "Discount Group");
                                    IF "Item Code" = '' THEN
                                      DiscHistRec.SETFILTER("Item Code", '%1', '')
                                    ELSE
                                      DiscHistRec.SETRANGE("Item Code", "Item Code");
                                    DiscHistRec.SETRANGE("Discount Level", "Discount Level");
                                    DiscHistRec.SETRANGE("Discount Reference", "Discount Reference");
                                    DiscHistRec.SETFILTER("Starting Date", '%1..', HistLimitDate);
                                    DiscHistRec.SETFILTER("Ending Date", '<%1', HistLimitDate);  //db, 17-04-13: C006604
                                    IF DiscHistRec.ISEMPTY THEN BEGIN
                                      DiscHistRec.SETRANGE("Starting Date");
                                      IF DiscHistRec.FINDLAST THEN
                                        HistLimitDate := DiscHistRec."Starting Date";
                                    END;
                                    DiscHistRec.SETFILTER("Starting Date", '<%1', HistLimitDate);
                                    DiscHistRec.DELETEALL(TRUE);  //db, 02-01-14: C010943
                                    DeleteDeliveryGroup(Vendor,"Discount Group"); //SR, 17-12-2014: C013498
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Status.CLOSE;
                              END;

               ReqFilterFields=Vendor,Discount Group,Item Code,Discount Level,Discount Reference }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  Name=Discount History Period;
                  CaptionML=[DEU=Rabatthistorieperiode;
                             ENU=Discount History Period;
                             NLD=Kortingshistorieperiode;
                             NOR=Periode for Rabatthistorikk;
                             SVE=Rabatthistoriksperiod];
                  SourceExpr=ItemSetup."Discount History Period";
                  Editable=FALSE }

      { 1100525003;2;Field  ;
                  Name=Discount History From;
                  CaptionML=[DEU=Rabatthistorie ab;
                             ENU=Discount History From;
                             NLD=Kortingshistorie vanaf;
                             NOR=Rabatthistorikk fra;
                             SVE=Rabatthistorik fr†n];
                  SourceExpr=ItemSetup."Discount History From";
                  Editable=FALSE }

      { 1100525004;2;Field  ;
                  Name=Discount History From (Prop.);
                  CaptionML=[DEU=Rabatthistorie ab (Vorschlag);
                             ENU=Discount History From (Proposal);
                             NLD=Kortingshistorie vanaf (voorstel);
                             NOR=Rabatthistorikk fra (forslag);
                             SVE=Rabatthistorik fr†n (f”rslag)];
                  SourceExpr=DiscountHistoryNew;
                  OnValidate=BEGIN
                               GetHistoryFilter;
                             END;
                              }

      { 1100525005;2;Field  ;
                  Name=Remove Discount History;
                  CaptionML=[DEU=Rabatthistorie l”schen;
                             ENU=Remove Discount History;
                             NLD=Verwijderen kortingshistorie;
                             NOR=Fjerne Rabatthistorikk;
                             SVE=Ta bort rabatthistorik];
                  SourceExpr=HistFilter;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DiscHistRec@1100485000 : Record 11012312;
      ItemSetup@1100485001 : Record 11012300;
      HistLimitDate@1100485002 : Date;
      HistFilter@1100485008 : Text[30];
      DiscountHistoryNew@1100525002 : Date;
      Status@1100485005 : Dialog;
      NoOfLines@1100485004 : Integer;
      StatusCounter@1100485003 : Integer;
      Text001@1100485006 : TextConst 'DEU=Aktualisieren Rabatthistorie @1@@@@@@@@@@@@@@@@@@;ENU=Update Discount History @1@@@@@@@@@@@@@@@@@@;NLD=Bijwerken kortingshistorie @1@@@@@@@@@@@@@@@@@@;NOR=Oppdater rabatthistorikk @1@@@@@@@@@@@@@@@@@@;SVE=Uppdatera rabatthistorik @1@@@@@@@@@@@@@@@@@@';

    PROCEDURE GetHistoryFilter@1100525000();
    BEGIN
      HistFilter := STRSUBSTNO('..%1', DiscountHistoryNew - 1);
    END;

    PROCEDURE GetItemSetup@1100525001();
    VAR
      PeriodTxt@1100525000 : Text[30];
      RefDate@1100525001 : Date;
      YearNo@1100525002 : Integer;
    BEGIN
      ItemSetup.GET;
      ItemSetup.TESTFIELD("Discount History Period");
      PeriodTxt := '-' + FORMAT(ItemSetup."Discount History Period");
      YearNo := DATE2DMY(TODAY,3);
      RefDate := DMY2DATE(1, 1, YearNo);
      DiscountHistoryNew := CALCDATE(PeriodTxt,RefDate);
      GetHistoryFilter;
    END;

    PROCEDURE StoreItemSetup@1100525002();
    BEGIN
      ItemSetup.GET;
      ItemSetup."Discount History From" := DiscountHistoryNew;
      ItemSetup.MODIFY;
    END;

    LOCAL PROCEDURE DeleteDeliveryGroup@1100529100(VendorNo@1100529101 : Code[20];DeliveryGroupCode@1100529102 : Code[20]);
    VAR
      DeliveryGroup@1100529100 : Record 11012320;
    BEGIN
      //SR, 17-12-2014: C013498
      DeliveryGroup.SETRANGE(Vendor, VendorNo);
      DeliveryGroup.SETRANGE(Code, DeliveryGroupCode);
      DeliveryGroup.DELETEALL(TRUE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

