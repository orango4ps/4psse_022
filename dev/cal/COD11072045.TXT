OBJECT Codeunit 11072045 Insurance Jnl.-Post Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00 (5652);
  }
  PROPERTIES
  {
    TableNo=11072092;
    Permissions=TableData 11072088=rimd,
                TableData 11072093=rimd;
    OnRun=BEGIN
            GLSetup.GET;
            RunWithCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      FA@1001 : Record 5600;
      Insurance@1002 : Record 11072087;
      InsuranceJnlLine@1003 : Record 11072092;
      InsCoverageLedgEntry@1004 : Record 11072088;
      InsCoverageLedgEntry2@1005 : Record 11072088;
      InsuranceReg@1006 : Record 11072093;
      InsuranceJnlCheckLine@1008 : Codeunit 11072044;
      MakeInsCoverageLedgEntry@1009 : Codeunit 11072050;
      NextEntryNo@1011 : Integer;

    [External]
    PROCEDURE RunWithCheck@3(VAR InsuranceJnlLine2@1000 : Record 11072092);
    BEGIN
      InsuranceJnlLine.COPY(InsuranceJnlLine2);
      Code(TRUE);
      InsuranceJnlLine2 := InsuranceJnlLine;
    END;

    [External]
    PROCEDURE RunWithOutCheck@1(VAR InsuranceJnlLine2@1000 : Record 11072092);
    BEGIN
      InsuranceJnlLine.COPY(InsuranceJnlLine2);
      Code(FALSE);
      InsuranceJnlLine2 := InsuranceJnlLine;
    END;

    LOCAL PROCEDURE Code@2(CheckLine@1000 : Boolean);
    BEGIN
      WITH InsuranceJnlLine DO BEGIN
        IF "Insurance No." = '' THEN
          EXIT;
        IF CheckLine THEN
          InsuranceJnlCheckLine.RunCheck(InsuranceJnlLine);
        Insurance.GET("Insurance No.");
        Insurance.TESTFIELD(Blocked,FALSE);
        FA.GET("FA No.");
        FA.TESTFIELD("Budgeted Asset",FALSE);
        FA.TESTFIELD(Blocked,FALSE);
        FA.TESTFIELD(Inactive,FALSE);
        MakeInsCoverageLedgEntry.CopyFromJnlLine(InsCoverageLedgEntry,InsuranceJnlLine);
        MakeInsCoverageLedgEntry.CopyFromInsuranceCard(InsCoverageLedgEntry,Insurance);
      END;
      IF NextEntryNo = 0 THEN BEGIN
        InsCoverageLedgEntry.LOCKTABLE;
        IF InsCoverageLedgEntry2.FINDLAST THEN
          NextEntryNo := InsCoverageLedgEntry2."Entry No.";
        InsuranceReg.LOCKTABLE;
        IF InsuranceReg.FINDLAST THEN
          InsuranceReg."No." := InsuranceReg."No." + 1
        ELSE
          InsuranceReg."No." := 1;
        InsuranceReg.INIT;
        InsuranceReg."From Entry No." := NextEntryNo + 1;
        InsuranceReg."Creation Date" := TODAY;
        InsuranceReg."Creation Time" := TIME;
        InsuranceReg."Source Code" := InsuranceJnlLine."Source Code";
        InsuranceReg."Journal Batch Name" := InsuranceJnlLine."Journal Batch Name";
        InsuranceReg."User ID" := USERID;
      END;
      NextEntryNo := NextEntryNo + 1;
      InsCoverageLedgEntry."Entry No." := NextEntryNo;
      InsCoverageLedgEntry."Dimension Set ID" := InsuranceJnlLine."Dimension Set ID";
      InsCoverageLedgEntry.INSERT;
      IF InsuranceReg."To Entry No." = 0 THEN BEGIN
        InsuranceReg."To Entry No." := NextEntryNo;
        InsuranceReg.INSERT;
      END ELSE BEGIN
        InsuranceReg."To Entry No." := NextEntryNo;
        InsuranceReg.MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}

