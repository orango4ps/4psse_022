OBJECT Table 11012865 Consignment Roster
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LicenceCu.CheckLicenceConsignment;
               TESTFIELD("Start Date");  //db, 01-10-07
               TESTFIELD("Start Time");  //db, 01-10-07
             END;

    OnModify=BEGIN
               LicenceCu.CheckLicenceConsignment;
             END;

    CaptionML=[ENU=Consignment Roster;
               NOR=Fullservicepakkegruppe;
               SVE=Fullservicepaketsgrupp];
  }
  FIELDS
  {
    { 1   ;   ;Watch Code          ;Code10        ;TableRelation=Watch;
                                                   CaptionML=[ENU=Watch Code;
                                                              NOR=Obs-kode;
                                                              SVE=Obs-kod] }
    { 2   ;   ;Consignment Code    ;Code10        ;TableRelation=Consignment;
                                                   CaptionML=[ENU=Consignment Code;
                                                              NOR=Kode for fullservicepakke;
                                                              SVE=Fullservicepaketskod] }
    { 3   ;   ;Start Date          ;Date          ;TableRelation="Consignment Time"."Start Date" WHERE (Consignment Code=FIELD(Consignment Code));
                                                   CaptionML=[ENU=Start Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 4   ;   ;Start Time          ;Time          ;TableRelation="Consignment Time"."Start Time" WHERE (Consignment Code=FIELD(Consignment Code),
                                                                                                        Start Date=FIELD(Start Date));
                                                   CaptionML=[ENU=Start Time;
                                                              NOR=Starttid;
                                                              SVE=Starttid] }
    { 5   ;   ;Employee No.        ;Code20        ;TableRelation=Employee.No.;
                                                   OnValidate=VAR
                                                                EmplRec@1210190000 : Record 5200;
                                                              BEGIN
                                                                IF "Employee No." <> '' THEN BEGIN
                                                                  EmplRec.GET("Employee No.");
                                                                  EmplRec.TESTFIELD(Status,EmplRec.Status::Active);  //db, 09-08-11: M28442
                                                                END;

                                                                CheckAvailabilityEmployee;
                                                              END;

                                                   CaptionML=[ENU=Employee;
                                                              NOR=Ansatt;
                                                              SVE=Personal] }
    { 6   ;   ;Name                ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 7   ;   ;End Date            ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Consignment Time"."End Date" WHERE (Consignment Code=FIELD(Consignment Code),
                                                                                                           Start Date=FIELD(Start Date),
                                                                                                           Start Time=FIELD(Start Time)));
                                                   CaptionML=[ENU=End Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum];
                                                   Editable=No }
    { 8   ;   ;End Time            ;Time          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Consignment Time"."End Time" WHERE (Consignment Code=FIELD(Consignment Code),
                                                                                                           Start Date=FIELD(Start Date),
                                                                                                           Start Time=FIELD(Start Time)));
                                                   CaptionML=[ENU=End Time;
                                                              NOR=Sluttid;
                                                              SVE=Sluttid];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Watch Code,Consignment Code,Start Date,Start Time;
                                                   Clustered=Yes }
    {    ;Employee No.                             }
    {    ;Consignment Code                         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'ENU=Employee %1 already assigned to Watch %2 and Consignment %3: Continue?;NOR=Ansatt %1 er allerede tilordnet Observering %2 og fullservicepakke %3: Fortsette?;SVE=Anst„lld %1 „r redan tilldelad till observera %2 och konsignation %3: Forts„tta?';
      LicenceCu@1100485001 : Codeunit 11012131;
      Text001@1100528600 : TextConst 'ENU=Note: Employee %1 is not available from %2 %3 to %4 %5 due to %6.';

    PROCEDURE CheckAvailabilityEmployee@1100485000();
    VAR
      ConsignmentRoster@1100485000 : Record 11012865;
      EmployeeAbsence@1100528600 : Record 5207;
      AbsenceStartDT@1100528601 : DateTime;
      AbsenceEndDT@1100528602 : DateTime;
      ConsignmentStartDT@1100528603 : DateTime;
      ConsignmentEndDT@1100528604 : DateTime;
    BEGIN
      ConsignmentRoster.SETCURRENTKEY("Employee No.");
      ConsignmentRoster.SETRANGE("Employee No.", "Employee No.");
      ConsignmentRoster.SETRANGE("Start Date", "Start Date");
      ConsignmentRoster.SETFILTER("Watch Code", '<>%1', "Watch Code");
      ConsignmentRoster.SETFILTER("Consignment Code", '<>%1', "Consignment Code");
      IF ConsignmentRoster.FINDFIRST THEN BEGIN
        IF NOT CONFIRM(Text000, FALSE,
          "Employee No.", ConsignmentRoster."Watch Code", ConsignmentRoster."Consignment Code")
        THEN
          ERROR('');
      END;
      CALCFIELDS("End Date", "End Time");
      ConsignmentStartDT := CREATEDATETIME("Start Date", "Start Time");
      IF "End Time" <> 0T THEN
        ConsignmentEndDT := CREATEDATETIME("End Date", "End Time")
      ELSE
        ConsignmentEndDT := CREATEDATETIME("End Date", 235959.999T);

      EmployeeAbsence.SETCURRENTKEY("Employee No.");
      EmployeeAbsence.SETRANGE("Employee No.", "Employee No.");
      EmployeeAbsence.SETRANGE(Plannable, FALSE);
      EmployeeAbsence.SETFILTER("To Date", '>=%1', "Start Date");
      EmployeeAbsence.SETFILTER("From Date", '<=%1', "End Date");
      IF EmployeeAbsence.FINDFIRST THEN
        REPEAT
          AbsenceStartDT := CREATEDATETIME(EmployeeAbsence."From Date", EmployeeAbsence."From Time");
          IF EmployeeAbsence."Until Time" <> 0T THEN
            AbsenceEndDT := CREATEDATETIME(EmployeeAbsence."To Date", EmployeeAbsence."Until Time")
          ELSE
            AbsenceEndDT := CREATEDATETIME(EmployeeAbsence."To Date", 235959.999T);

          IF ((AbsenceStartDT >= ConsignmentStartDT) AND (AbsenceStartDT < ConsignmentEndDT)) OR
             ((AbsenceEndDT > ConsignmentStartDT) AND (AbsenceEndDT <= ConsignmentEndDT)) OR
             ((AbsenceStartDT < ConsignmentStartDT) AND (AbsenceEndDT > ConsignmentEndDT))
          THEN
            MESSAGE(Text001, "Employee No.", EmployeeAbsence."From Date", EmployeeAbsence."From Time",
              EmployeeAbsence."To Date", EmployeeAbsence."Until Time", EmployeeAbsence.Description);
        UNTIL EmployeeAbsence.NEXT = 0;
    END;

    PROCEDURE GetURLForCard@1100525010() : Text;
    VAR
      Consignment@1100525000 : Record 11012863;
    BEGIN
      Consignment.GET("Consignment Code");
      EXIT(GETURL(CLIENTTYPE::Windows, COMPANYNAME, OBJECTTYPE::Page, PAGE::"Consignment Roster", Consignment));
    END;

    BEGIN
    END.
  }
}

