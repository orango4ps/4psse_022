OBJECT Report 6085584 Export Purchase Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.5.00.07;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Einkaufsrechnungen exportieren;
               ENU=Export Purchase Invoices;
               NLD=Exporteer inkoopfacturen];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DCSetup.GET;

                  DCSetup.CALCFIELDS("PDF Signature Certificate");
                  DCSetup.TESTFIELD("PDF Signature Certificate");

                  IF XMLOutputPath = '' THEN
                    ERROR(Text0002);

                  XmlDoc.CreateNode(InvoicesNode,'element','PURCHASEINVOICES','');
                END;

    OnPostReport=BEGIN
                   XmlDoc.CreateXmlDeclaration(XmlDeclNode,'1.0','UTF-8','yes');

                   XmlDoc.AppendChild(InvoicesNode);
                   XmlDoc.InsertBefore(XmlDeclNode,InvoicesNode);
                   SignAndStoreDocument(XmlDoc,XMLOutputPath);
                   CLEAR(XmlDoc);
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 3733;    ;DataItem;Purch. Inv. Header  ;
               DataItemTable=Table122;
               ReqFilterFields=Posting Date }

    { 6357;1   ;DataItem;DC Document         ;
               DataItemTable=Table6085590;
               DataItemTableView=SORTING(Created Doc. Type,Created Doc. Subtype,Created Doc. ID,Created Doc. Ref. No.)
                                 WHERE(Created Doc. Type=CONST(38),
                                       Created Doc. Subtype=CONST(2));
               OnAfterGetRecord=BEGIN
                                  ExportDocument("DC Document","Purch. Inv. Header",InvoicesNode);
                                END;

               DataItemLink=Created Doc. ID=FIELD(Pre-Assigned No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 6085573; ;Container ;
                  Name=ContentArea;
                  CaptionML=[DEU=Exportieren;
                             ENU=Export;
                             NLD=Exporteren;
                             NOR=Eksport;
                             SVE=Exportera];
                  ContainerType=ContentArea }

      { 6085574;1;Field     ;
                  Name=Export File Path;
                  CaptionML=[DEU=Dateipfad exportieren;
                             ENU=Export File Path;
                             NLD=Uitvoerpad Exporteer];
                  SourceExpr=XMLOutputPath;
                  OnAssistEdit=VAR
                                 FileSysMgt@6085573 : Codeunit 6085584;
                               BEGIN
                                 XMLOutputPath := FileSysMgt.SaveFileDialog(Text0001,'Invoices.xml','XML File (*.xml)|.xml');
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DCSetup@1000 : Record 6085573;
      XmlLib@1160040002 : Codeunit 6085573;
      XmlDoc@1002 : Codeunit 6192800;
      InvoicesNode@1160040003 : Codeunit 6192801;
      Text0001@1160040001 : TextConst 'DEU=Dateispeicherort ausw„hlen;ENU=Select file location;NLD=Selecteer bestandslocatie';
      XmlDeclNode@1001 : Codeunit 6192801;
      XMLOutputPath@1160040000 : Text[250];
      Text0002@1160040006 : TextConst 'DEU=XML-Ausgabedateipfad muss angegeben werden.;ENU=XML Output File Path must be filled out.;NLD=XML-bestandspad moet ingevuld worden.';

    PROCEDURE ExportDocument@1160040011(Document@1160040004 : Record 6085590;PurchInvHeader@1160040001 : Record 122;VAR InvoicesNode@1160040000 : Codeunit 6192801);
    VAR
      InvoiceNode@1160040002 : Codeunit 6192801;
      NewNode@1160040003 : Codeunit 6192801;
      Amount@1160040006 : Decimal;
      VATAmount@1160040005 : Decimal;
      VATPercent@1160040007 : Decimal;
    BEGIN
      CalculateVATAmount(PurchInvHeader,Amount,VATAmount,VATPercent);
      XmlLib.CreateXmlNode(InvoicesNode,'INVOICE','',InvoiceNode);
      XmlLib.CreateXmlNode(InvoiceNode,'ID',PurchInvHeader."No.",NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'VENDORNAME',PurchInvHeader."Pay-to Name",NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'VATNUMBER',PurchInvHeader."VAT Registration No.",NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'POSTINGDATE',FORMAT(PurchInvHeader."Posting Date"),NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'INVOICENUMBER',PurchInvHeader."No.",NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'AMOUNT',FORMAT(Amount,0,'<Integer><Decimals,3>'),NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'VATPERCENT',FORMAT(VATPercent,0,'<Integer><Decimals,3>'),NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'VATAMOUNT',FORMAT(VATAmount,0,'<Integer><Decimals,3>'),NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'DOCUMENTPATH',Document.Filename,NewNode);
      XmlLib.CreateXmlNode(InvoiceNode,'HASH',Document."PDF File Hash",NewNode);
    END;

    PROCEDURE CalculateVATAmount@1160040001(PurchInvHeader@1160040000 : Record 122;VAR Amount@1160040003 : Decimal;VAR VATAmount@1160040004 : Decimal;VAR VATPercent@1160040002 : Decimal);
    VAR
      PurchInvLine@1160040001 : Record 123;
      AmountInclVAT@1160040005 : Decimal;
    BEGIN
      PurchInvLine.SETRANGE("Document No.",PurchInvHeader."No.");

      IF PurchInvLine.FIND('-') THEN
        REPEAT
          Amount := Amount + PurchInvLine.Amount;
          AmountInclVAT := AmountInclVAT + PurchInvLine."Amount Including VAT";
        UNTIL PurchInvLine.NEXT = 0;

      VATAmount := AmountInclVAT - Amount;
      VATPercent := (VATAmount/Amount) * 100;
    END;

    PROCEDURE SignAndStoreDocument@1160040000(XmlDoc@1160040003 : Codeunit 6192800;OutputPath@1160040002 : Text[250]);
    VAR
      CryptMgt@1002 : Codeunit 6085599;
      FileSysMgt@1000 : Codeunit 6085584;
      ServerTempFilePath@1001 : Text;
      CertificateExportPath@1160040001 : Text[250];
    BEGIN
      CertificateExportPath := STRSUBSTNO('%1\%2',FileSysMgt.GetTempFolderPath,'Certificate.pfx');
      ServerTempFilePath := FileSysMgt.GetTempFilename('Certificate','.xml');

      DCSetup.CALCFIELDS("PDF Signature Certificate");
      DCSetup."PDF Signature Certificate".EXPORT(CertificateExportPath);

      XmlDoc.Save(ServerTempFilePath);
      CryptMgt.SignXmlDocument(ServerTempFilePath,ServerTempFilePath,CertificateExportPath,DCSetup."PDF Signature Cert. Password");

      FileSysMgt.DownloadToFile(ServerTempFilePath,OutputPath);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

