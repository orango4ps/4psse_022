OBJECT Table 11071873 Project Site Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TestCompanyQuantity;
               TestCombinationExist;
               TESTFIELD(Type); //C004638.n
               TESTFIELD(Code); //C004638.n

               ProjectSiteMgt.LogProjectSiteLine(1, Rec); //**4PS.n
             END;

    OnModify=BEGIN
               TESTFIELD("Site Created", FALSE);
               TestCombinationExist;

               ProjectSiteMgt.LogProjectSiteLine(2, Rec); //**4PS.n
             END;

    OnDelete=BEGIN
               TESTFIELD("Site Created", FALSE);

               ProjectSiteMgt.LogProjectSiteLine(3, Rec); //**4PS.n
             END;

    CaptionML=[ENU=Project Site Line;
               NOR=Prosjektplassrad;
               SVE=Projektplatsrad];
  }
  FIELDS
  {
    { 10  ;   ;Seq. No.            ;Integer       ;TableRelation="Project Site Header";
                                                   CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF ("Company Name" = '') THEN
                                                                  "Company Name" := COMPANYNAME;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=" ,Project,Try-Out";
                                                                    NOR=" ,Prosjekt,Prospekt";
                                                                    SVE=" ,Projekt,Anbud"];
                                                   OptionString=,Project,Try-Out;
                                                   NotBlank=Yes }
    { 40  ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                Project@1100528301 : Record 11072003;
                                                                TryOut@1100528300 : Record 11012750;
                                                              BEGIN
                                                                IF (Type = Type::Project) THEN BEGIN
                                                                  Project.CHANGECOMPANY("Company Name");
                                                                  Project.GET(Code);
                                                                END;

                                                                IF (Type = Type::"Try-Out") THEN BEGIN
                                                                  TryOut.CHANGECOMPANY("Company Name");
                                                                  TryOut.GET(Code);
                                                                END;

                                                                TestCombinationExist;
                                                              END;

                                                   OnLookup=VAR
                                                              Project@1000000000 : Record 11072003;
                                                              TryOut@1100538000 : Record 11012750;
                                                            BEGIN
                                                              IF (Type = Type::Project) THEN BEGIN
                                                                Project.CHANGECOMPANY("Company Name");
                                                                Project."No." := Code;
                                                                IF (PAGE.RUNMODAL(0, Project) = ACTION::LookupOK) THEN
                                                                  VALIDATE(Code, Project."No.");
                                                              END;

                                                              IF (Type = Type::"Try-Out") THEN BEGIN
                                                                TryOut.CHANGECOMPANY("Company Name");
                                                                TryOut.Code := Code;
                                                                IF (PAGE.RUNMODAL(0, TryOut) = ACTION::LookupOK) THEN
                                                                  VALIDATE(Code, TryOut.Code);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 50  ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   OnValidate=BEGIN
                                                                TestCombinationExist;
                                                              END;

                                                   CaptionML=[ENU=Company Name;
                                                              NOR=Navn p† selskap;
                                                              SVE=F”retagsnamn];
                                                   NotBlank=Yes }
    { 60  ;   ;Role Code           ;Code20        ;TableRelation="Project Site Role";
                                                   OnValidate=VAR
                                                                ProjectSiteLine@1100538001 : Record 11071873;
                                                              BEGIN
                                                                ProjectSiteLine.SETRANGE("Seq. No.", "Seq. No.");
                                                                ProjectSiteLine.SETRANGE("Role Code", "Role Code");
                                                                IF (ProjectSiteLine.FINDFIRST) THEN
                                                                  ERROR(Text003);
                                                              END;

                                                   CaptionML=[ENU=Role Code;
                                                              NOR=Rollekod;
                                                              SVE=Rollkod];
                                                   NotBlank=Yes }
    { 61  ;   ;Role Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Site Role".Description WHERE (Code=FIELD(Role Code)));
                                                   CaptionML=[ENU=Role Description;
                                                              NOR=Rollebeskrivelse;
                                                              SVE=Rollbeskrivning];
                                                   Editable=No }
    { 70  ;   ;Site Created        ;Boolean       ;CaptionML=[ENU=Site Created;
                                                              NOR=Plass opprettet;
                                                              SVE=Plats skapad];
                                                   Editable=No }
    { 80  ;   ;Sharepoint No.      ;Integer       ;CaptionML=[ENU=Sharepoint No.;
                                                              NOR=Sharepoint No.;
                                                              SVE=SharePoint-nr];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Seq. No.,Line No.                       ;Clustered=Yes }
    {    ;Type,Code,Company Name                   }
    {    ;Seq. No.,Sharepoint No.                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000000003 : TextConst 'ENU=It is only allowed to use a number of companies dat is less than or equal to the number that is set in de the configuration form.';
      Text003@1000000004 : TextConst 'ENU=The Role may only be used once.';
      Text009@1000000015 : TextConst 'ENU=The combination of Company/Project may only occur once.';
      ProjectSiteMgt@1100528300 : Codeunit 11012282;

    PROCEDURE TestCompanyQuantity@1100538004();
    VAR
      ProjectSiteSetup@1100538000 : Record 11071871;
      ProjectSiteLine@1100538001 : Record 11071873;
    BEGIN
      ProjectSiteSetup.GET;

      ProjectSiteLine.SETRANGE("Seq. No.", "Seq. No.");
      IF (ProjectSiteLine.COUNT >= ProjectSiteSetup."Company Quantity") THEN
        ERROR(Text001);
    END;

    PROCEDURE TestCombinationExist@1100538002();
    VAR
      ProjectSiteLine@1100538000 : Record 11071873;
    BEGIN
      IF (Code = '') OR ("Company Name" = '') THEN
        EXIT;

      ProjectSiteLine.SETRANGE(Type, Type);
      ProjectSiteLine.SETRANGE(Code, Code);
      ProjectSiteLine.SETRANGE("Company Name", "Company Name");
      IF (NOT ProjectSiteLine.FINDSET) THEN
        EXIT;

      REPEAT
        IF (ProjectSiteLine."Seq. No." <> "Seq. No.") OR
           (ProjectSiteLine."Line No." <> "Line No.")
        THEN
          ERROR(Text009);
      UNTIL (ProjectSiteLine.NEXT = 0);
    END;

    PROCEDURE GetProjectSiteCode@1100409001() : Code[20];
    VAR
      ProjectSiteHeader@1100528300 : Record 11071872;
    BEGIN
      IF (ProjectSiteHeader.GET("Seq. No.")) THEN
        EXIT(ProjectSiteHeader.Code);
      EXIT('');
    END;

    PROCEDURE GetProjectSiteName@1100528300() : Text[100];
    VAR
      ProjectSiteHeader@1100528300 : Record 11071872;
    BEGIN
      IF (ProjectSiteHeader.GET("Seq. No.")) THEN
        EXIT(ProjectSiteHeader.Name);
      EXIT('');
    END;

    PROCEDURE GetProjectSiteURL@1100528301() : Text[250];
    VAR
      ProjectSiteHeader@1100528300 : Record 11071872;
    BEGIN
      IF (ProjectSiteHeader.GET("Seq. No.")) THEN
        EXIT(ProjectSiteHeader.GetSiteURL);
      EXIT('');
    END;

    PROCEDURE GetLastSharepointNo@1100409000(SeqNo@1100409001 : Integer) : Integer;
    VAR
      ProjectSiteLine@1100409000 : Record 11071873;
    BEGIN
      ProjectSiteLine.SETCURRENTKEY("Seq. No.", "Sharepoint No.");
      ProjectSiteLine.SETRANGE("Seq. No.", SeqNo);
      IF (ProjectSiteLine.FINDLAST) THEN
        EXIT(ProjectSiteLine."Sharepoint No.");
      EXIT(0);
    END;

    PROCEDURE GetProjectSiteLine@1100409002(iType@1100409001 : Option;iCode@1100409002 : Code[20];iCompanyName@1100409003 : Text[30];VAR SeqNo@1100409004 : Integer;VAR LineNo@1100409005 : Integer) : Boolean;
    VAR
      ProjectSiteLine@1100409000 : Record 11071873;
    BEGIN
      ProjectSiteLine.SETCURRENTKEY(Type,Code,"Company Name");
      ProjectSiteLine.SETRANGE(Type, iType);
      ProjectSiteLine.SETRANGE(Code, iCode);
      ProjectSiteLine.SETRANGE("Company Name", iCompanyName);
      IF (ProjectSiteLine.FINDFIRST) THEN BEGIN
        SeqNo := ProjectSiteLine."Seq. No.";
        LineNo := ProjectSiteLine."Line No.";
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

