OBJECT Table 11012861 Watch
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LicenceCu.CheckLicenceConsignment;
             END;

    OnModify=BEGIN
               LicenceCu.CheckLicenceConsignment;
               CheckOverlapWatch;
             END;

    OnDelete=BEGIN
               WatchEmplRec.SETRANGE("Watch Code",Code);
               WatchEmplRec.DELETEALL(TRUE);

               ConsRosterRec.SETRANGE("Watch Code",Code);
               ConsRosterRec.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Watch;
               NOR=Observer;
               SVE=Bevaka];
    LookupPageID=Page11020352;
    DrillDownPageID=Page11020352;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Watch Type          ;Code10        ;TableRelation="Watch Type";
                                                   CaptionML=[ENU=Watch Type;
                                                              NOR=Obs-type;
                                                              SVE=Obs-typ] }
    { 4   ;   ;Discipline Filter   ;Code50        ;TableRelation=Discipline;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Discipline Filter;
                                                              NOR=Disiplinfilter;
                                                              SVE=Disciplinfilter] }
    { 5   ;   ;Territory Filter    ;Code50        ;TableRelation=Territory;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Territory Filter;
                                                              NOR=Distriktsfilter;
                                                              SVE=Distriktsfilter] }
    { 6   ;   ;Global Dimension 1 Filter;Code50   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Global Dimension 1 Filter;
                                                              NOR=Filter for global dimensjon 1;
                                                              SVE=Global dimension 1 filter];
                                                   CaptionClass='1,3,1' }
    { 7   ;   ;Default             ;Boolean       ;CaptionML=[ENU=Default;
                                                              NOR=Standard;
                                                              SVE=Standard] }
    { 8   ;   ;Location Watch      ;Boolean       ;CaptionML=[ENU=Location Watch;
                                                              NOR=Lokasjonsobservering;
                                                              SVE=Plats-obs] }
    { 9   ;   ;Trade Speciality Filter;Code50     ;TableRelation="Trade Speciality".Code WHERE (Discipline=FIELD(Discipline Filter));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Trade Speciality Filter;
                                                              NOR=SNI-nummerfilter;
                                                              SVE=SNI-nummerfilter] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Default                                  }
    {    ;Territory Filter                         }
    {    ;Global Dimension 1 Filter                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      WatchRec@1100485002 : Record 11012861;
      WatchEmplRec@1100485000 : Record 11012862;
      ConsRosterRec@1100485001 : Record 11012865;
      LicenceCu@1100485003 : Codeunit 11012131;
      Text000@1100525000 : TextConst 'ENU=Overlap with %1 %2: \%3 %4 \%5 %6 \%7 %8 \%9 %10;NOR=Overlap med %1 %2: \%3 %4 \%5 %6 \%7 %8 \%9 %10;SVE=™verlappa med %1 %2: \%3 %4 \%5 %6 \%7 %8 \%9 %10';

    PROCEDURE CheckFilters@1100485000(TerritoryCode@1100485000 : Code[1024];DimensionCode@1100485001 : Code[1024];DisciplineCode@1100485006 : Code[1024];TradeSpecCode@1210190000 : Code[1024]) Result : Boolean;
    VAR
      TerritoryRec@1100485002 : Record 286;
      DimValRec@1100485003 : Record 349;
      DisciplineRec@1100485007 : Record 11020204;
      TradeSpecRec@1210190002 : Record 11020285;
      TerritoryOK@1100485004 : Boolean;
      DimensionOK@1100485005 : Boolean;
      DisciplineOK@1100485008 : Boolean;
      TradeSpecOK@1210190001 : Boolean;
    BEGIN
      Result :=
        ("Territory Filter" = '') AND ("Global Dimension 1 Filter" = '') AND
        ("Discipline Filter" = '') AND ("Trade Speciality Filter" = '');  //db, 16-05-11

      IF NOT Result THEN BEGIN
        IF ("Territory Filter" <> '') AND (TerritoryCode <> '') THEN BEGIN
          TerritoryOK := FALSE;
          TerritoryRec.SETFILTER(Code, "Territory Filter");
          IF TerritoryRec.FINDSET(FALSE, FALSE) THEN
            REPEAT
              IF TerritoryRec.Code = TerritoryCode THEN
                TerritoryOK := TRUE;
            UNTIL TerritoryRec.NEXT = 0;
        END ELSE BEGIN
          TerritoryOK := TRUE;
        END;

        IF ("Global Dimension 1 Filter" <> '') AND (DimensionCode <> '') THEN BEGIN
          DimensionOK := FALSE;
          DimValRec.SETFILTER(Code, "Global Dimension 1 Filter");
          DimValRec.SETRANGE("Global Dimension No.", 1);
          IF DimValRec.FINDSET(FALSE, FALSE) THEN
            REPEAT
              IF DimValRec.Code = DimensionCode THEN
                DimensionOK := TRUE;
            UNTIL DimValRec.NEXT = 0;
        END ELSE BEGIN
          DimensionOK := TRUE;
        END;

        IF ("Discipline Filter" <> '') AND (DisciplineCode <> '') THEN BEGIN
          DisciplineOK := FALSE;
          DisciplineRec.SETFILTER(Code, "Discipline Filter");
          IF DisciplineRec.FINDSET(FALSE, FALSE) THEN
            REPEAT
              IF DisciplineRec.Code = DisciplineCode THEN
                DisciplineOK := TRUE;
            UNTIL DisciplineRec.NEXT = 0;
        END ELSE BEGIN
          DisciplineOK := TRUE;
        END;

        //db.sn, 16-05-11
        IF ("Trade Speciality Filter" <> '') AND (TradeSpecCode <> '') THEN BEGIN
          TradeSpecOK := FALSE;
          TradeSpecRec.SETFILTER(Discipline, "Discipline Filter");
          TradeSpecRec.SETFILTER(Code, "Trade Speciality Filter");
          IF TradeSpecRec.FINDSET(FALSE, FALSE) THEN
            REPEAT
              IF TradeSpecRec.Code = TradeSpecCode THEN
                TradeSpecOK := TRUE;
            UNTIL TradeSpecRec.NEXT = 0;
        END ELSE BEGIN
          TradeSpecOK := TRUE;
        END;
        //db.en, 16-05-11

        Result := TerritoryOK AND DimensionOK AND DisciplineOK AND TradeSpecOK;  //db, 16-05-11
      END;
    END;

    LOCAL PROCEDURE CheckOverlapWatch@1100485005();
    VAR
      WatchRec@1100485001 : Record 11012861;
    BEGIN
      WatchRec.SETFILTER(Code, '<>%1', Code);
      WatchRec.SETRANGE("Watch Type", "Watch Type");
      IF WatchRec.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          CheckOverlapFilters(0, WatchRec,
            "Territory Filter", "Global Dimension 1 Filter",
            "Discipline Filter", "Trade Speciality Filter");
        UNTIL WatchRec.NEXT = 0;
      END;
    END;

    PROCEDURE CheckOverlapFilters@1100485001(lvAction@1100485002 : Integer;VAR WatchRec@1100485003 : Record 11012861;TerritoryFilter@1100485001 : Text[1024];DimensionFilter@1100485000 : Text[1024];DisciplineFilter@1100485007 : Text[1024];TradeSpecFilter@1210190000 : Text[1024]);
    VAR
      TerritoryRec@1100485005 : Record 286;
      DimValRec@1100485004 : Record 349;
      DisciplineRec@1100485008 : Record 11020204;
      TradeSpecRec@1210190001 : Record 11020285;
    BEGIN
      IF (TerritoryFilter = '') AND (WatchRec."Territory Filter" <> '') THEN BEGIN
        TerritoryRec.SETFILTER(Code, WatchRec."Territory Filter");
      END ELSE IF (TerritoryFilter <> '') AND (WatchRec."Territory Filter" = '') THEN BEGIN
        TerritoryRec.SETFILTER(Code, TerritoryFilter);
      END ELSE IF (TerritoryFilter <> '') AND (WatchRec."Territory Filter" <> '') THEN BEGIN
        TerritoryRec.SETFILTER(Code, CheckBraces(TerritoryFilter) + '&' + CheckBraces(WatchRec."Territory Filter"));
      END;

      IF (DimensionFilter = '') AND (WatchRec."Global Dimension 1 Filter" <> '') THEN BEGIN
        DimValRec.SETFILTER(Code, WatchRec."Global Dimension 1 Filter");
      END ELSE IF (DimensionFilter <> '') AND (WatchRec."Global Dimension 1 Filter" = '') THEN BEGIN
        DimValRec.SETFILTER(Code, DimensionFilter);
      END ELSE IF (DimensionFilter <> '') AND (WatchRec."Global Dimension 1 Filter" <> '') THEN BEGIN
        DimValRec.SETFILTER(Code, CheckBraces(DimensionFilter) + '&' + CheckBraces(WatchRec."Global Dimension 1 Filter"));
      END;
      DimValRec.SETRANGE("Global Dimension No.", 1);

      IF lvAction = 0 THEN BEGIN
        IF (DisciplineFilter = '') AND (WatchRec."Discipline Filter" <> '') THEN BEGIN
          DisciplineRec.SETFILTER(Code, WatchRec."Discipline Filter");
        END ELSE IF (DisciplineFilter <> '') AND (WatchRec."Discipline Filter" = '') THEN BEGIN
          DisciplineRec.SETFILTER(Code, DisciplineFilter);
        END ELSE IF (DisciplineFilter <> '') AND (WatchRec."Discipline Filter" <> '') THEN BEGIN
          DisciplineRec.SETFILTER(Code, CheckBraces(DisciplineFilter) + '&' + CheckBraces(WatchRec."Discipline Filter"));
        END;
        //db.sn, 16-05-11
        TradeSpecRec.SETFILTER(Discipline, DisciplineRec.GETFILTER(Code));
        IF (TradeSpecFilter = '') AND (WatchRec."Trade Speciality Filter" <> '') THEN BEGIN
          TradeSpecRec.SETFILTER(Code, WatchRec."Trade Speciality Filter");
        END ELSE IF (TradeSpecFilter <> '') AND (WatchRec."Trade Speciality Filter" = '') THEN BEGIN
          TradeSpecRec.SETFILTER(Code, TradeSpecFilter);
        END ELSE IF (TradeSpecFilter <> '') AND (WatchRec."Trade Speciality Filter" <> '') THEN BEGIN
          TradeSpecRec.SETFILTER(Code, CheckBraces(TradeSpecFilter) + '&' + CheckBraces(WatchRec."Trade Speciality Filter"));
        END;
        //db.en, 16-05-11
      END;

      IF TerritoryRec.FINDFIRST AND DimValRec.FINDFIRST THEN BEGIN
        IF lvAction = 0 THEN BEGIN  //check overlap watches
          IF DisciplineRec.FINDFIRST AND TradeSpecRec.FINDFIRST THEN BEGIN  //db, 16-05-11
            IF WatchRec."Territory Filter" = '' THEN CLEAR(TerritoryRec);
            IF WatchRec."Global Dimension 1 Filter" = '' THEN CLEAR(DimValRec);
            IF WatchRec."Discipline Filter" = '' THEN CLEAR(DisciplineRec);
            IF WatchRec."Trade Speciality Filter" = '' THEN CLEAR(TradeSpecRec);  //db, 16-05-11
            MESSAGE(Text000,  //db, 11-07-14: C016567
              WatchRec.TABLECAPTION, WatchRec.Code,
              TerritoryRec.TABLECAPTION, TerritoryRec.Code,
              DimValRec.TABLECAPTION, DimValRec.Code,
              DisciplineRec.TABLECAPTION, DisciplineRec.Code,
              TradeSpecRec.TABLECAPTION, TradeSpecRec.Code);  //db, 16-05-11
          END;
        END;
        IF lvAction = 1 THEN  //check watches per consignment
          WatchRec.MARK := TRUE;
      END;
    END;

    LOCAL PROCEDURE CheckBraces@1100485008(TextIn@1100485000 : Text[1024]) Result : Text[1024];
    BEGIN
      IF STRPOS(TextIn, '|') > 0 THEN
        EXIT('(' + TextIn + ')');
      IF STRPOS(TextIn, '&') > 0 THEN
        EXIT('(' + TextIn + ')');
      IF STRPOS(TextIn, '..') > 0 THEN
        EXIT('(' + TextIn + ')');

      EXIT(TextIn);
    END;

    PROCEDURE LookupDimension@1100485002();
    VAR
      DimValRec@1100485000 : Record 349;
    BEGIN
      DimValRec.SETRANGE("Global Dimension No.", 1);
      DimValRec.SETFILTER(Code, "Global Dimension 1 Filter");
      IF DimValRec.FINDFIRST THEN
        DimValRec.SETRANGE(Code);
      IF PAGE.RUNMODAL(PAGE::"Dimension Value List", DimValRec) = ACTION::LookupOK THEN
        "Global Dimension 1 Filter" := "Global Dimension 1 Filter" + DimValRec.Code;
    END;

    PROCEDURE LookupTerritory@1100485003();
    VAR
      TerritoryRec@1100485001 : Record 286;
    BEGIN
      TerritoryRec.SETFILTER(Code, "Territory Filter");
      IF TerritoryRec.FINDFIRST THEN
        TerritoryRec.SETRANGE(Code);
      IF PAGE.RUNMODAL(PAGE::Territories, TerritoryRec) = ACTION::LookupOK THEN
        "Territory Filter" := "Territory Filter" + TerritoryRec.Code;
    END;

    BEGIN
    END.
  }
}

