OBJECT XMLport 11012275 Import IBIS-Infra Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import IBIS-Infra Budget;
               SVE=Importera IBIS-Infra-budget];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnPreXMLport=BEGIN
                   CheckSelections();

                   PrevLineType := 0;
                   FileLineCounter := 0;
                   Window.OPEN(Text001);

                   BudgetRec.RESET;
                   BudgetRec.SETRANGE("Project No.", gProjectNo);
                   BudgetRec.SETRANGE(Adjustment, gAdjustment);
                   BudgetRec.SETRANGE("Extension Contract", gExtension);
                   BudgetRec.SETRANGE(Option, '');
                   IF BudgetRec.FINDLAST THEN
                     NextBudgLineNo := BudgetRec."Line No." + 10000
                   ELSE
                     NextBudgLineNo := 10000;
                 END;

    OnPostXMLport=VAR
                    lvSumSheetRec@1100525500 : Record 11012064;
                  BEGIN
                    Window.CLOSE;

                    lvSumSheetRec."Project No." := gProjectNo;
                    lvSumSheetRec."Contract No." := gExtension;
                    lvSumSheetRec.ReCalcSummaryLines(gAdjustment);      //Summary Adjustment
                    lvSumSheetRec.UpdateBudgetLines(TRUE, gAdjustment); //Summary Adjustment
                  END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{54FC6621-0436-412F-BB3A-F4B9E18D5A4E}];  ;Root                ;Element ;Text     }

    { [{A72E0EB0-7CC8-44B1-A04A-D8DCF317C6C5}];1 ;BudgetLine          ;Element ;Table   ;
                                                  SourceTable=Table11012001;
                                                  SourceTableView=SORTING(Field1,Field31,Field33,Field38,Field14);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              ImportMeansCode := '';
                                                                              ImportType := '';
                                                                              ImportDescription := '';
                                                                              ImportQuantityTxt := '';
                                                                              ImportUnitOfMeasure := '';
                                                                              ImportLaborTxt := '';
                                                                              ImportMatPriceTxt := '';
                                                                              ImportPlantPriceTxt := '';
                                                                              ImportSubcPriceTxt := '';
                                                                              ImportCostObjectCode := '';
                                                                              ImportDeelFactTxt := '';
                                                                              ImportCurrentQtyTxt := '';
                                                                            END;

                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 FileLineCounter := FileLineCounter + 1;
                                                                                 Window.UPDATE(1, FileLineCounter);

                                                                                 ImportType := DELCHR(ImportType, '<>', ' ');
                                                                                 ImportMeansCode := DELCHR(ImportMeansCode, '<>', ' ');
                                                                                 ImportDescription := DELCHR(ImportDescription, '<>', ' ');
                                                                                 ImportCostObjectCode := DELCHR(ImportCostObjectCode, '<>', ' ');
                                                                                 ImportAmountTxt := DELCHR(ImportAmountTxt, '<>', ' ');

                                                                                 IF (ImportMeansCode = '') AND (ImportAmountTxt = '') THEN
                                                                                   WriteTextLine()
                                                                                 ELSE
                                                                                   CASE ImportType OF
                                                                                     '*':
                                                                                       BEGIN
                                                                                         HoldTenderElement := '';
                                                                                         HoldTenderElementPart := '';
                                                                                         WriteChapterParagraph();
                                                                                         PrevLineType := 1;
                                                                                       END;
                                                                                     '#', ':':
                                                                                       BEGIN
                                                                                         HoldTenderElement := '';
                                                                                         HoldTenderElementPart := '';
                                                                                         WriteTenderElement();
                                                                                         PrevLineType := 2;
                                                                                       END;
                                                                                     '+':
                                                                                       BEGIN
                                                                                         HoldTenderElementPart := '';
                                                                                         WriteTenderElementPart();
                                                                                         PrevLineType := 3;
                                                                                       END;
                                                                                     '':
                                                                                       BEGIN
                                                                                         WriteBudgetLine();
                                                                                         PrevLineType := 4;
                                                                                       END;
                                                                                     '!':
                                                                                       WriteTextLine();
                                                                                     ELSE
                                                                                       currXMLport.SKIP;
                                                                                   END;

                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{2D8A08A2-C615-4BFE-AF80-D289D6CABE06}];2 ;Filler              ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{6C36BA1C-6242-42F9-B250-2C2D4FCE8A9E}];2 ;ImportMeansCode     ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  ImportMeansCode := COPYSTR(ImportMeansCode,1,MAXSTRLEN("Budget Line"."Cost Object"));
                                                                                END;

                                                  Width=22 }

    { [{7DE85BC6-6F33-4208-8ACE-7C455E2E5FE3}];2 ;ImportType          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{3054AF53-56AF-4EC6-85D4-F5228FCED847}];2 ;ImportDescription   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{E51B8839-700D-4EC1-AD82-9DBCC8C334BF}];2 ;ImportQuantityTxt   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{8FEA728D-0492-45DF-ADE9-91EDA4A7B1B0}];2 ;ImportUnitOfMeasure ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{1B35044B-B9B7-4457-B568-1E670EF7A586}];2 ;ImportLaborTxt      ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{190F272D-6D95-46FA-A053-FA3AA58CBD10}];2 ;ImportMatPriceTxt   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{9283FC71-1CB3-485B-97A3-474D426933F9}];2 ;ImportPlantPriceTxt ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{23A425B8-BE32-485D-A369-10BB3C771DC3}];2 ;ImportSubcPriceTxt  ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{47684D69-9EF4-4321-BA98-FCD4D8889D5A}];2 ;ImportAmountTxt     ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{AFE8C51C-94A1-4A3D-809E-4753232E8172}];2 ;ImportCostObjectCode;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=7 }

    { [{103055A0-AA53-451F-AC69-6FD10E60A3BD}];2 ;ImportDeelFactTxt   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{66B2CCED-E923-4BC7-9C37-45816AB36358}];2 ;ImportCurrentQtyTxt ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      ProjRec@1100525537 : Record 11072003;
      AdjustmentRec@1100525536 : Record 11012003;
      ExtensionRec@1100525535 : Record 11012004;
      UnitRec@1100525534 : Record 204;
      TenderElemRec@1100525533 : Record 11012263;
      TenderElemPartRec@1100525532 : Record 11012264;
      BudgetRec@1100525531 : Record 11012001;
      DimValRec@1100525530 : Record 349;
      GWWMgtCU@1100525529 : Codeunit 11012163;
      DimMgt@1100525528 : Codeunit 408;
      Window@1100525527 : Dialog;
      gSelectionsSet@1100525526 : Boolean;
      gOnlyBudgetLines@1100525525 : Boolean;
      FileLineCounter@1100525524 : Integer;
      NextBudgLineNo@1100525523 : Integer;
      PrevLineType@1100525522 : Integer;
      HoldTenderElement@1100525521 : Code[10];
      HoldTenderElementPart@1100525520 : Code[10];
      gProjectNo@1100525519 : Code[20];
      gAdjustment@1100525518 : Code[10];
      gExtension@1100525517 : Code[10];
      gFolder@1100525516 : Text[250];
      gFileNameTxt@1100525515 : Text[100];
      PriceDec@1100525500 : Decimal;
      Text000@1100525503 : TextConst 'ENU=Direct calling not allowed.;SVE=Direktsamtal „r inte till†tet.';
      Text001@1100525502 : TextConst 'ENU=Line No. #1########;SVE=Radnr #1########';
      Text002@1100525501 : TextConst 'ENU=%1 %2 not present;SVE=%1 %2 finns inte';
      Text013@1210190000 : TextConst 'ENU=Cost Object %1 is blocked;SVE=Kostnadsobjekt %1 „r blockerad';

    PROCEDURE SetSelections@1(IProjectNo@11012000 : Code[20];IAdjustment@11012001 : Code[10];IExtension@11012002 : Code[10];IOnlyBudgetLines@1210190000 : Boolean;lFolder@1100485001 : Text[250];IFileNameTxt@1100485000 : Text[100]);
    BEGIN
      gSelectionsSet := TRUE;
      gProjectNo := IProjectNo;
      gAdjustment := IAdjustment;
      gExtension := IExtension;
      gOnlyBudgetLines := IOnlyBudgetLines;
      gFolder := lFolder;
      gFileNameTxt := IFileNameTxt;
    END;

    PROCEDURE CheckSelections@1100485000();
    BEGIN
      IF NOT gSelectionsSet THEN
        ERROR(Text000);

      ProjRec.GET(gProjectNo);
      ProjRec.TESTFIELD("No.");

      IF (gAdjustment <> '') THEN
        AdjustmentRec.GET(gProjectNo, gAdjustment);

      IF (gExtension <> '') THEN
        ExtensionRec.GET(gProjectNo, gExtension);
    END;

    PROCEDURE WriteChapterParagraph@1100485003();
    BEGIN
      WITH TenderElemRec DO BEGIN
        IF gOnlyBudgetLines THEN
          EXIT;

        GWWMgtCU.FormatTenderElement(ImportMeansCode);
        IF GET(gProjectNo, ImportMeansCode) THEN BEGIN
          GWWMgtCU.FillTenderElementLevel("Tender Paragraph", Level);
          IF NOT (Level IN [Level::Chapter, Level::Paragraph, Level::"Sub Paragraph"]) THEN
            FIELDERROR(Level);
          VALIDATE(Description, COPYSTR(ImportDescription, 1, MAXSTRLEN(Description)));
          "Description 2" := '';
          MODIFY;
        END ELSE BEGIN
          INIT;
          Level := -1;  //* Initially fill with a value that does not fulfill!
          VALIDATE("Project No.", gProjectNo);
          VALIDATE("Tender Paragraph", ImportMeansCode);  //* Also Level is filled
          TESTFIELD("Tender Paragraph");
          IF NOT (Level IN [Level::Chapter, Level::Paragraph, Level::"Sub Paragraph"]) THEN
            FIELDERROR(Level);
          VALIDATE(Description, COPYSTR(ImportDescription, 1, MAXSTRLEN(Description)));
          "Description 2" := '';
          INSERT;
        END;
      END;
    END;

    PROCEDURE WriteTenderElement@1100485007();
    BEGIN
      WITH TenderElemRec DO BEGIN
        GWWMgtCU.FormatTenderElement(ImportMeansCode);

        IF gOnlyBudgetLines THEN BEGIN
          GET(gProjectNo, ImportMeansCode);
          TESTFIELD(Level, Level::"Tender Paragraph");
          HoldTenderElement := "Tender Paragraph";
          EXIT;
        END;

        IF GET(gProjectNo, ImportMeansCode) THEN BEGIN
          GWWMgtCU.FillTenderElementLevel("Tender Paragraph", Level);
          TESTFIELD(Level, Level::"Tender Paragraph");
          VALIDATE(Description, COPYSTR(ImportDescription, 1, MAXSTRLEN(Description)));
          "Description 2" := '';
          MODIFY;
        END ELSE BEGIN
          INIT;
          Level := Level::"Tender Paragraph";
          VALIDATE("Project No.", gProjectNo);
          VALIDATE("Tender Paragraph", ImportMeansCode);  //* Also Level is filled
          TESTFIELD("Tender Paragraph");
          TESTFIELD(Level, Level::"Tender Paragraph");
          VALIDATE(Description, COPYSTR(ImportDescription, 1, MAXSTRLEN(Description)));
          "Description 2" := '';
          Quantity := Numeric(ImportQuantityTxt);
          IF ImportUnitOfMeasure <> '' THEN BEGIN
            "Unit of Measure Code" := ImportUnitOfMeasure;
            IF NOT UnitRec.GET("Unit of Measure Code") THEN
              StoreMessage(Description, STRSUBSTNO(Text002, FIELDCAPTION("Unit of Measure Code"), "Unit of Measure Code"));
          END;
          INSERT;
        END;
        HoldTenderElement := "Tender Paragraph";
      END;
    END;

    PROCEDURE WriteTenderElementPart@1100485010();
    BEGIN
      WITH TenderElemPartRec DO BEGIN
        GWWMgtCU.FormatTenderElementPart(ImportMeansCode);

        IF gOnlyBudgetLines THEN BEGIN
          GET(gProjectNo, HoldTenderElement, ImportMeansCode);
          HoldTenderElementPart := "Tender Paragraph Part";
          EXIT;
        END;

        IF GET(gProjectNo, HoldTenderElement, ImportMeansCode) THEN BEGIN
          VALIDATE(Description, COPYSTR(ImportDescription, 1, MAXSTRLEN(Description)));
          "Description 2" := '';
          MODIFY;
        END ELSE BEGIN
          INIT;
          VALIDATE("Project No.", gProjectNo);
          VALIDATE("Tender Paragraph", HoldTenderElement);
          VALIDATE("Tender Paragraph Part", ImportMeansCode);
          TESTFIELD("Tender Paragraph");
          TESTFIELD("Tender Paragraph Part");
          VALIDATE(Description, COPYSTR(ImportDescription, 1, MAXSTRLEN(Description)));
          "Description 2" := '';
          INSERT;
        END;
        HoldTenderElementPart := "Tender Paragraph Part";
      END;
    END;

    PROCEDURE WriteTenderElementAsPart@1100485001();
    BEGIN
      //* IF no Tender Paragraph Part Line after a Tender Paragraph Line then insert a Part with the same code as the Tender Paragraph.

      WITH TenderElemPartRec DO BEGIN

        IF gOnlyBudgetLines THEN BEGIN
          GET(gProjectNo, HoldTenderElement, HoldTenderElement);
          HoldTenderElementPart := "Tender Paragraph Part";
          EXIT;
        END;

        TenderElemRec.GET(gProjectNo, HoldTenderElement);

        IF GET(gProjectNo, HoldTenderElement, HoldTenderElement) THEN BEGIN
          VALIDATE(Description, COPYSTR(TenderElemRec.Description, 1, MAXSTRLEN(Description)));
          "Description 2" := TenderElemRec."Description 2";
          MODIFY;
        END ELSE BEGIN
          INIT;
          VALIDATE("Project No.", gProjectNo);
          VALIDATE("Tender Paragraph", HoldTenderElement);
          VALIDATE("Tender Paragraph Part", HoldTenderElement);
          TESTFIELD("Tender Paragraph");
          TESTFIELD("Tender Paragraph Part");
          VALIDATE(Description, COPYSTR(TenderElemRec.Description, 1, MAXSTRLEN(Description)));
          "Description 2" := TenderElemRec."Description 2";
          INSERT;
        END;
        HoldTenderElementPart := "Tender Paragraph Part";
      END;
    END;

    PROCEDURE WriteBudgetLine@1100485006();
    VAR
      DeelFact@1100485000 : Decimal;
      CostType@1100485001 : 'Labor,Material,Subcontracting,Plant,Sundry';
    BEGIN
      IF (PrevLineType = 2) AND (HoldTenderElement <> '') AND (HoldTenderElementPart = '') THEN
        WriteTenderElementAsPart();

      WITH BudgetRec DO BEGIN
        FOR CostType := CostType::Labor TO CostType::Plant DO BEGIN
          IF ((CostType = CostType::Labor) AND (Numeric(ImportLaborTxt) <> 0)) OR
             ((CostType = CostType::Material) AND (Numeric(ImportMatPriceTxt) <> 0)) OR
             ((CostType = CostType::Subcontracting) AND (Numeric(ImportSubcPriceTxt) <> 0)) OR
             ((CostType = CostType::Plant) AND (Numeric(ImportPlantPriceTxt) <> 0)) THEN
          BEGIN

            CLEAR(BudgetRec);
            "Project No." := gProjectNo;
            "Main Project No." := ProjRec."Main Project";
            Adjustment := gAdjustment;
            "Extension Contract" := gExtension;
            Option := '';
            "Line No." := NextBudgLineNo;

            VALIDATE("Tender Paragraph", HoldTenderElement);
            VALIDATE("Tender Paragraph Part", HoldTenderElementPart);
            Description := COPYSTR(ImportDescription, 1, MAXSTRLEN(Description));

            "Time Quantity" := 1;
            Quantity := Numeric(ImportQuantityTxt);
            IF ImportUnitOfMeasure <> '' THEN BEGIN
              "Unit of Measure" := ImportUnitOfMeasure;
              IF NOT UnitRec.GET("Unit of Measure") THEN
                StoreMessage(Description, STRSUBSTNO(Text002, FIELDCAPTION("Unit of Measure"), "Unit of Measure"));
            END;

            IF Numeric(ImportDeelFactTxt) <> 0 THEN
              DeelFact := Numeric(ImportCurrentQtyTxt) / Numeric(ImportDeelFactTxt)
            ELSE
              DeelFact := 1;

            CASE CostType OF
              CostType::Labor : PriceDec := Numeric(ImportLaborTxt);
              CostType::Plant : PriceDec := Numeric(ImportPlantPriceTxt);
              CostType::Material : PriceDec := Numeric(ImportMatPriceTxt);
              CostType::Subcontracting : PriceDec := Numeric(ImportSubcPriceTxt);
            END;

            "Cost Object" := ImportCostObjectCode;
            DimMgt.GetDimValueRec(2, ImportCostObjectCode, DimValRec, FALSE,'');
            IF (DimValRec.Code = '') THEN BEGIN
              StoreMessage(Description, STRSUBSTNO(Text002, FIELDCAPTION("Cost Object"), ImportCostObjectCode));
              "Cost Type" := CostType;
            END ELSE BEGIN
              IF DimValRec.Blocked THEN  //C-026977
                StoreMessage(Description, STRSUBSTNO(Text013, ImportCostObjectCode));
              "Cost Type" := DimValRec."Cost Type";
            END;

            IF "Cost Type" = "Cost Type"::Labor THEN BEGIN
              "Rate (LCY)" := PriceDec;
              IF Quantity = 0 THEN
                "Amount (LCY)" := ROUND(PriceDec * DeelFact)
              ELSE BEGIN
                //Norm := DeelFact;
                //IF Norm <> 0 THEN
                //  Capacity := 1 / Norm;
                Quantity := Quantity * DeelFact;
                Norm := 1;
                "Production Rate" := 1 / Norm;
                CalcTotals();
              END;
            END ELSE BEGIN
              Quantity := Quantity * DeelFact;
              "Gross Price (LCY)" := PriceDec;
              "Price (LCY)" := PriceDec;
              CalcTotals();
            END;
            "Department Code" := ProjRec."Global Dimension 1 Code";  //*31927
            FillDefaultFscType();  //DP00129

            GetVersionDate;
            UpdateExtContractStatus;
            GetDefaultPurchAction;
            UpdateCostControlStatus(0);  // 0=Action::Insert  //33988.n

            INSERT;
            NextBudgLineNo := NextBudgLineNo + 10000;
          END;
        END;
      END;
    END;

    PROCEDURE WriteTextLine@1100485008();
    BEGIN
      IF (ImportDescription = '') OR (gOnlyBudgetLines AND (PrevLineType <> 4)) THEN
        EXIT;

      CASE PrevLineType OF
        1,2:
          BEGIN
            WITH TenderElemRec DO BEGIN
              IF ("Description 2" = '') THEN BEGIN
                "Description 2" := COPYSTR(ImportDescription, 1, MAXSTRLEN("Description 2"));
                MODIFY;
              END;
            END;
          END;
        3:
          BEGIN
            WITH TenderElemPartRec DO BEGIN
              IF ("Description 2" = '') THEN BEGIN
                "Description 2" := COPYSTR(ImportDescription, 1, MAXSTRLEN("Description 2"));
                MODIFY;
              END;
            END;
          END;
        4:
          BEGIN
            WITH BudgetRec DO BEGIN
              IF ("Description 2" = '') THEN
                "Description 2" := COPYSTR(ImportDescription, 1, MAXSTRLEN("Description 2"))
              ELSE BEGIN
                IF ("External Text" = '') THEN
                  "External Text" := COPYSTR(ImportDescription, 1, MAXSTRLEN("External Text"))
                ELSE
                  "External Text" := COPYSTR("External Text" + ' ' + ImportDescription, 1, MAXSTRLEN("External Text"));
              END;
              MODIFY;
            END;
          END;
      END;
    END;

    PROCEDURE Numeric@2(Text@11012001 : Text[250]) @11012000 : Decimal;
    VAR
      lvValue@11012002 : Decimal;
    BEGIN
      Text := CONVERTSTR(Text, '.', ',');
      IF DELCHR(Text, '=', '-,0123456789') <> '' THEN EXIT(0);
      IF DELCHR(Text, '=', ' ') = '' THEN EXIT(0);
      EVALUATE(lvValue, Text);
      EXIT(lvValue);
    END;

    PROCEDURE StoreMessage@5(lvDesc@11012000 : Text[100];lvMess@11012001 : Text[250]);
    VAR
      ErrorRec@1210190000 : Record 11012051;
      lvError@11012002 : Integer;
    BEGIN
      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"Import Budget");
        IF FINDLAST THEN
          lvError := "Line No." + 1
        ELSE
          lvError := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"Import Budget";
        "Line No." := lvError;
        "Project No." := ProjRec."No.";
        "Error message" := lvMess;
        Description := lvDesc;
        INSERT;
      END;
    END;

    BEGIN
    {
      CC: Dataport 11012275
    }
    END.
  }
}

