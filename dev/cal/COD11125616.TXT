OBJECT Codeunit 11125616 BudgetLine-Explode BOM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11012001;
    OnRun=VAR
            BudgetLine@1100525003 : Record 11012001;
            GenericItem@1100525002 : Record 11125615;
            ExplodeGenericItem@1100525005 : Report 11125617;
          BEGIN
            BudgetLine := Rec;
            BudgetLine.SETRECFILTER;
            BudgetLine.SETRANGE("Line No.");

            IF "Item No." <> '' THEN BEGIN
              OrigLineNo := "Line No.";
              OrigQuantity := Quantity;
              ExplodeItem(BudgetLine);
            END ELSE BEGIN
              GenericItem.SETRANGE(Status,GenericItem.Status::Certified);
              IF PAGE.RUNMODAL(0,GenericItem) = ACTION::LookupOK THEN BEGIN
                GenericItem.TESTFIELD(Status,GenericItem.Status::Certified);
                ExplodeGenericItem.RUNMODAL;
                ExplodeGenItem(BudgetLine,GenericItem,ExplodeGenericItem.GetQuantity);
              END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1100525007 : TextConst 'DEU=Artikel %1 ist keine StÅckliste.;ENU=Item %1 is not a bill of materials.;NLD=Artikel %1 is geen stuklijst.;NOR=Artikkel %1 er ikke en stykkliste;SVE=Artikel %1 Ñr inte en struktur.';
      Text003@1100525005 : TextConst 'DEU=Es ist nicht genÅgend Platz, um die StÅckliste zu entfalten.;ENU=There is not enough space to explode the BOM.;NLD=Er is niet voldoende ruimte om de stuklijst weer te geven.;NOR=Det er ikke nok plass til Ü utfolde stykklisten.;SVE=Det finns inte tillrÑckligt med utrymme fîr att expandera strukturen.';
      OrigLineNo@1100525002 : Integer;
      OrigQuantity@1100525001 : Decimal;
      OrigItem@1100525004 : Code[20];
      LevelBOM@1100525003 : Integer;

    PROCEDURE ExplodeGenItem@1100525000(VAR BudgetLine@1100525000 : Record 11012001;GenericItem@1100525007 : Record 11125615;QuantityGenItem@1100525006 : Decimal);
    VAR
      ToBudgetLine@1100525001 : Record 11012001;
      GenericBOMLine@1100525005 : Record 11125616;
      NoOfBOMComp@1100525003 : Integer;
      NextLineNo@1100525004 : Integer;
      LineSpacing@1100525002 : Integer;
    BEGIN
      //explode for generic item
      WITH BudgetLine DO BEGIN
        GenericBOMLine.SETRANGE("Generic Item No.",GenericItem."No.");
        NoOfBOMComp := GenericBOMLine.COUNT;
        IF NoOfBOMComp = 0 THEN
          ERROR(Text001,GenericItem."No.");

        IF GenericBOMLine.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(BudgetLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToBudgetLine := BudgetLine;
            ToBudgetLine.INIT;
            ToBudgetLine.VALIDATE(Element);
            ToBudgetLine."Line No." := NextLineNo;
            ToBudgetLine."Cost Type" := ToBudgetLine."Cost Type"::Material;
            ToBudgetLine.VALIDATE("Item No.",GenericBOMLine."Item No.");
            ToBudgetLine.VALIDATE(Quantity,ROUND(GenericBOMLine.Quantity * QuantityGenItem,0.00001));
            ToBudgetLine.VALIDATE("Unit of Measure",GenericBOMLine."Unit of Measure Code");
            ToBudgetLine.Description := GenericBOMLine.Description;
            ToBudgetLine."Part of Line" := OrigLineNo;
            ToBudgetLine."Department Code" := BudgetLine."Department Code";
            ToBudgetLine.INSERT;
          UNTIL GenericBOMLine.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE ExplodeItem@1100525001(VAR BudgetLine@1100525000 : Record 11012001);
    VAR
      ToBudgetLine@1100525005 : Record 11012001;
      NoOfBOMComp@1100525003 : Integer;
      LineSpacing@1100525002 : Integer;
      NextLineNo@1100525009 : Integer;
      FromBOMComp@1100525001 : Record 90;
      BomComp@1100525010 : Record 90;
      TempBudgetLine@1100525011 : TEMPORARY Record 11012001;
    BEGIN
      //explode for item
      WITH BudgetLine DO BEGIN
        OrigLineNo := "Line No.";
        OrigQuantity := Quantity;
        IF OrigItem = '' THEN
          OrigItem := "Item No.";

        ToBudgetLine := BudgetLine;
        ToBudgetLine.INIT;
        ToBudgetLine.VALIDATE(Element, Element);
        ToBudgetLine."Cost Type" := "Cost Type"::Material;
        ToBudgetLine.Description := Description;
        ToBudgetLine."Description 2" := "Description 2";
        ToBudgetLine."Original Quantity" := OrigQuantity;
        ToBudgetLine."Part of Line" := "Part of Line";
        ToBudgetLine."Level BOM" := "Level BOM";
        ToBudgetLine."BOM Item No." := OrigItem;
        ToBudgetLine.MODIFY;
        LevelBOM := "Level BOM" + 1;

        FromBOMComp.SETRANGE("Parent Item No.","Item No.");
        FromBOMComp.SETFILTER(Type, '<>%1', FromBOMComp.Type::Resource);
        NoOfBOMComp := FromBOMComp.COUNT;
        IF NoOfBOMComp = 0 THEN BEGIN
          ExplodeItemProdBOM(BudgetLine);
        END;

        IF FromBOMComp.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(BudgetLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToBudgetLine := BudgetLine;
            ToBudgetLine.INIT;
            ToBudgetLine.VALIDATE(Element, Element);
            ToBudgetLine."Line No." := NextLineNo;
            ToBudgetLine."Cost Type" := "Cost Type"::Material;
            IF FromBOMComp.Type = FromBOMComp.Type::Item THEN BEGIN
              FromBOMComp.TESTFIELD("No.");
              ToBudgetLine.VALIDATE("Item No.",FromBOMComp."No.");
              //DP02339.sn
              ToBudgetLine."Original Quantity" := FromBOMComp."Quantity per";
              ToBudgetLine.VALIDATE(Quantity,Quantity * ToBudgetLine."Original Quantity");
              //DP02339.en
            END;
            ToBudgetLine."Unit of Measure" := FromBOMComp."Unit of Measure Code";
            ToBudgetLine.Description := FromBOMComp.Description;
            ToBudgetLine."Part of Line" := OrigLineNo;
            ToBudgetLine."Level BOM" := LevelBOM;
            ToBudgetLine."BOM Item No." := OrigItem;
            //assign fields from header
            ToBudgetLine."Department Code" := BudgetLine."Department Code";
            ToBudgetLine."Main Project No." := BudgetLine."Main Project No.";
            ToBudgetLine."Version Date" := BudgetLine."Version Date";
            ToBudgetLine."Project Planning Activity No." := BudgetLine."Project Planning Activity No.";
            ToBudgetLine.INSERT;

            IF FromBOMComp.Type = FromBOMComp.Type::Item THEN BEGIN
              BomComp.SETRANGE("Parent Item No.", ToBudgetLine."Item No.");
              IF NOT BomComp.ISEMPTY THEN BEGIN
                TempBudgetLine := ToBudgetLine;
                TempBudgetLine.INSERT;
              END;
            END;
          UNTIL FromBOMComp.NEXT = 0;
        END;
      END;

      IF TempBudgetLine.FINDSET THEN BEGIN
        //Explode BOMS
        COMMIT;
        REPEAT
          ExplodeItem(TempBudgetLine);
        UNTIL TempBudgetLine.NEXT = 0;
      END;
    END;

    PROCEDURE ExplodeItemProdBOM@1100525002(VAR BudgetLine@1100525000 : Record 11012001);
    VAR
      Item@1100525006 : Record 27;
      ToBudgetLine@1100525005 : Record 11012001;
      ProductionBOMLine@1100525004 : Record 99000772;
      NoOfBOMComp@1100525003 : Integer;
      LineSpacing@1100525002 : Integer;
      NextLineNo@1100525001 : Integer;
    BEGIN
      //explode for item with prod bom
      WITH BudgetLine DO BEGIN
        Item.GET("Item No.");
        ProductionBOMLine.SETRANGE("Production BOM No.",Item."Production BOM No.");
        ProductionBOMLine.SETRANGE(Type,ProductionBOMLine.Type::Item);
        NoOfBOMComp := ProductionBOMLine.COUNT;
        IF NoOfBOMComp = 0 THEN
          ERROR(Text001,"Item No.");

        IF ProductionBOMLine.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(BudgetLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToBudgetLine := BudgetLine;
            ToBudgetLine.INIT;
            ToBudgetLine.VALIDATE(Element);
            ToBudgetLine."Line No." := NextLineNo;
            ToBudgetLine."Cost Type" := ToBudgetLine."Cost Type"::Material;
            IF ProductionBOMLine.Type = ProductionBOMLine.Type::Item THEN BEGIN
              ProductionBOMLine.TESTFIELD("No.");
              ToBudgetLine.VALIDATE("Item No.", ProductionBOMLine."No.");
              //DP02339.sn
              ToBudgetLine."Original Quantity" := ProductionBOMLine."Quantity per";
              ToBudgetLine.VALIDATE(Quantity,Quantity * ToBudgetLine."Original Quantity");
              //DP02339.en
            END;
            ToBudgetLine."Unit of Measure" := ProductionBOMLine."Unit of Measure Code";
            ToBudgetLine.Description := ProductionBOMLine.Description;
            ToBudgetLine."Part of Line" := OrigLineNo;
            ToBudgetLine."Level BOM" := LevelBOM;
            ToBudgetLine."BOM Item No." := OrigItem;
            //assign fields from header
            ToBudgetLine."Department Code" := BudgetLine."Department Code";
            ToBudgetLine."Main Project No." := BudgetLine."Main Project No.";
            ToBudgetLine."Version Date" := BudgetLine."Version Date";
            ToBudgetLine."Project Planning Activity No." := BudgetLine."Project Planning Activity No.";
            ToBudgetLine.INSERT;
          UNTIL ProductionBOMLine.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE GetLineSpacing@1100525003(VAR BudgetLine@1100525000 : Record 11012001;NoOfBOMComp@1100525002 : Integer) LineSpacing : Decimal;
    VAR
      ToBudgetLine@1100525001 : Record 11012001;
    BEGIN
      LineSpacing := 10000;

      ToBudgetLine := BudgetLine;
      ToBudgetLine.SETRECFILTER;
      ToBudgetLine.SETRANGE("Line No.");
      IF ToBudgetLine.FIND('>') THEN BEGIN
        LineSpacing := (ToBudgetLine."Line No." - BudgetLine."Line No.") DIV (1 + NoOfBOMComp);
        IF LineSpacing = 0 THEN
          ERROR(Text003);
      END;

      EXIT(LineSpacing);
    END;

    BEGIN
    {
      db, 04-02-16: C028603 (use same procedure for exploding different type of items)
      - calculate line spacing and determine next linenumber
      - use init instead of copy
      - validate item and quantity
    }
    END.
  }
}

