OBJECT Report 11012796 Convert Service Unit Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Convert Service Unit Price;
  }
  DATASET
  {
    { 1100528600;;DataItem;                  ;
               DataItemTable=Table11071768;
               DataItemTableView=SORTING(Type,Code,Index Date)
                                 WHERE(Type=CONST(Internal));
               OnAfterGetRecord=VAR
                                  ServiceUnitPrice@1100528600 : Record 11071768;
                                BEGIN
                                  ServiceUnitPrice.SETRANGE(Type, ServiceUnitPrice.Type::Customer);
                                  ServiceUnitPrice.SETRANGE(Code, Code);
                                  IF NOT ServiceUnitPrice.ISEMPTY THEN BEGIN
                                    NewUnitPriceCode := COPYSTR(STRSUBSTNO('%1-I', Code), 1, MAXSTRLEN(NewUnitPriceCode));
                                    ServiceUnitPrice := "Service Price Book";
                                    ServiceUnitPrice.Code := NewUnitPriceCode;
                                    ServiceUnitPrice.INSERT;

                                    TmpServiceUnitPrice := "Service Price Book";
                                    TmpServiceUnitPrice.INSERT;
                                  END ELSE
                                    CurrReport.SKIP;
                                END;

               OnPostDataItem=VAR
                                ServiceUnitPrice@1100528600 : Record 11071768;
                              BEGIN
                                IF TmpServiceUnitPrice.FINDSET THEN
                                  REPEAT
                                    ServiceUnitPrice := TmpServiceUnitPrice;
                                    ServiceUnitPrice.DELETE;
                                  UNTIL TmpServiceUnitPrice.NEXT = 0;
                              END;
                               }

    { 1100528601;1;DataItem;                 ;
               DataItemTable=Table11071769;
               DataItemTableView=SORTING(Price Book Type,Price Book Code,Index Date,Code);
               OnAfterGetRecord=VAR
                                  ServiceUnitPriceLine@1100528600 : Record 11071769;
                                BEGIN
                                  ServiceUnitPriceLine := "Service Unit Price";
                                  ServiceUnitPriceLine."Price Book Code" := NewUnitPriceCode;
                                  ServiceUnitPriceLine.INSERT;

                                  TmpServiceUnitPriceLine := "Service Unit Price";
                                  TmpServiceUnitPriceLine.INSERT;
                                END;

               OnPostDataItem=VAR
                                ServiceUnitPriceLine@1100528600 : Record 11071769;
                              BEGIN
                                IF TmpServiceUnitPriceLine.FINDSET THEN
                                  REPEAT
                                    ServiceUnitPriceLine := TmpServiceUnitPriceLine;
                                    ServiceUnitPriceLine.DELETE;
                                  UNTIL TmpServiceUnitPriceLine.NEXT = 0;
                              END;

               DataItemLink=Price Book Type=FIELD(Type),
                            Price Book Code=FIELD(Code),
                            Index Date=FIELD(Index Date) }

    { 1100528602;2;DataItem;                 ;
               DataItemTable=Table11071770;
               DataItemTableView=SORTING(Price Book Type,Price Book Code,Index Date,Unit Price Code,Line No.);
               OnAfterGetRecord=VAR
                                  UnitPriceBudgetInternal@1100528600 : Record 11071770;
                                BEGIN
                                  UnitPriceBudgetInternal := "SUP Internal Budget Line";
                                  UnitPriceBudgetInternal."Price Book Code" := NewUnitPriceCode;
                                  UnitPriceBudgetInternal.INSERT;

                                  TmpUnitPriceBudgetInternal := "SUP Internal Budget Line";
                                  TmpUnitPriceBudgetInternal.INSERT;
                                END;

               OnPostDataItem=VAR
                                UnitPriceBudgetInternal@1100528600 : Record 11071770;
                              BEGIN
                                IF TmpUnitPriceBudgetInternal.FINDSET THEN
                                  REPEAT
                                    UnitPriceBudgetInternal := TmpUnitPriceBudgetInternal;
                                    UnitPriceBudgetInternal.DELETE;
                                  UNTIL TmpUnitPriceBudgetInternal.NEXT = 0
                              END;

               DataItemLink=Price Book Type=FIELD(Price Book Type),
                            Price Book Code=FIELD(Price Book Code),
                            Index Date=FIELD(Index Date),
                            Unit Price Code=FIELD(Code) }

    { 1100528604;1;DataItem;                 ;
               DataItemTable=Table11071772;
               DataItemTableView=SORTING(Service Order No.,Price Book Type,Price Book Code,Index Date,Room,Unit Price Code);
               OnAfterGetRecord=VAR
                                  UnitPriceServiceOrder@1100528600 : Record 11071772;
                                BEGIN
                                  UnitPriceServiceOrder := "SUP Recording Line";
                                  UnitPriceServiceOrder."Price Book Code" := NewUnitPriceCode;
                                  UnitPriceServiceOrder.INSERT;

                                  TmpUnitPriceServiceOrder := "SUP Recording Line";
                                  TmpUnitPriceServiceOrder.INSERT;
                                END;

               OnPostDataItem=VAR
                                UnitPriceServiceOrder@1100528600 : Record 11071772;
                              BEGIN
                                IF TmpUnitPriceServiceOrder.FINDSET THEN
                                  REPEAT
                                    UnitPriceServiceOrder := TmpUnitPriceServiceOrder;
                                    UnitPriceServiceOrder.ClearSUPRecordingLineNoFromAdviceOrderLine(TRUE);
                                    UnitPriceServiceOrder.DELETE;
                                  UNTIL TmpUnitPriceServiceOrder.NEXT = 0;
                              END;

               DataItemLink=Price Book Type=FIELD(Type),
                            Price Book Code=FIELD(Code),
                            Index Date=FIELD(Index Date) }

    { 1100528606;1;DataItem;                 ;
               DataItemTable=Table11071774;
               DataItemTableView=SORTING(Service Order No.,Price Book Type,Price Book Code,Index Date,Room,Unit Price Code);
               OnAfterGetRecord=VAR
                                  UnitPriceProdRecordingLine@1100528600 : Record 11071774;
                                BEGIN
                                  UnitPriceProdRecordingLine := "SUP Production Line";
                                  UnitPriceProdRecordingLine."Price Book Code" := NewUnitPriceCode;
                                  UnitPriceProdRecordingLine.INSERT;

                                  TmpUnitPriceProdRecordingLine := "SUP Production Line";
                                  TmpUnitPriceProdRecordingLine.INSERT;
                                END;

               OnPostDataItem=VAR
                                UnitPriceProdRecordingLine@1100528600 : Record 11071774;
                              BEGIN
                                IF TmpUnitPriceProdRecordingLine.FINDSET THEN
                                  REPEAT
                                    UnitPriceProdRecordingLine := TmpUnitPriceProdRecordingLine;
                                    UnitPriceProdRecordingLine.DELETE;
                                  UNTIL TmpUnitPriceProdRecordingLine.NEXT = 0;
                              END;

               DataItemLink=Price Book Type=FIELD(Type),
                            Price Book Code=FIELD(Code),
                            Index Date=FIELD(Index Date) }

    { 1100528608;1;DataItem;                 ;
               DataItemTable=Table11071957;
               DataItemTableView=SORTING(Unit Price Type,Unit Price Code,Index Date,Code,Line No.);
               OnAfterGetRecord=VAR
                                  ServiceUnitPriceLineText@1100528600 : Record 11071957;
                                BEGIN
                                  ServiceUnitPriceLineText := "SUP Text";
                                  ServiceUnitPriceLineText."Unit Price Code" := NewUnitPriceCode;
                                  ServiceUnitPriceLineText.INSERT;

                                  TmpServiceUnitPriceLineText := "SUP Text";
                                  TmpServiceUnitPriceLineText.INSERT;
                                END;

               OnPostDataItem=VAR
                                ServiceUnitPriceLineText@1100528600 : Record 11071957;
                              BEGIN
                                IF TmpServiceUnitPriceLineText.FINDSET THEN
                                  REPEAT
                                    ServiceUnitPriceLineText := TmpServiceUnitPriceLineText;
                                    ServiceUnitPriceLineText.DELETE;
                                  UNTIL TmpServiceUnitPriceLineText.NEXT = 0;
                              END;

               DataItemLink=Unit Price Type=FIELD(Type),
                            Unit Price Code=FIELD(Code),
                            Index Date=FIELD(Index Date) }

    { 1100528609;1;DataItem;                 ;
               DataItemTable=Table11012823;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               ServiceOrderExtension@1100528600 : Record 11071727;
                             BEGIN
                               ServiceOrderExtension.GetServOrderExtension("No.");
                               IF ((ServiceOrderExtension."Price Book Code" <> "Service Price Book".Code) OR
                                   (ServiceOrderExtension."Price Book Index Date" <> "Service Price Book"."Index Date"))
                               THEN
                                 CurrReport.SKIP;
                             END;

               DataItemLink=Price Book Type=FIELD(Type) }

    { 1100528610;2;DataItem;                 ;
               DataItemTable=Table11071727;
               DataItemTableView=SORTING(Service Order No.);
               OnAfterGetRecord=BEGIN
                                  "Price Book Code" := NewUnitPriceCode;
                                  MODIFY;
                                END;

               DataItemLink=Service Order No.=FIELD(No.) }

    { 1100528603;;DataItem;SUPRecordingLineLoop;
               DataItemTable=Table11071772;
               DataItemTableView=SORTING(Service Order No.,Price Book Type,Price Book Code,Index Date,Room,Unit Price Code);
               OnAfterGetRecord=BEGIN
                                  IF LastSUPRecordingLine."Service Order No." <> "Service Order No." THEN
                                    SUPRecordingLineLineNo := 0;
                                  SUPRecordingLineLineNo += 10000;
                                  IF "Line No." = 0 THEN BEGIN
                                    "Line No." := SUPRecordingLineLineNo;
                                    MODIFY;
                                  END;
                                  LastSUPRecordingLine := SUPRecordingLineLoop;
                                END;
                                 }

    { 1100528605;;DataItem;SUPProductionLoop ;
               DataItemTable=Table11071773;
               DataItemTableView=SORTING(Service Order No.,Production Date);
               OnAfterGetRecord=BEGIN
                                  IF LastSUPProduction."Service Order No." <> "Service Order No." THEN
                                    SUPProductionSeqNo := 0;
                                  SUPProductionSeqNo += 1;
                                  IF "Seq. No." = 0 THEN BEGIN
                                    "Seq. No." := SUPProductionSeqNo;
                                    MODIFY;
                                  END;
                                  LastSUPProduction := SUPProductionLoop;
                                END;
                                 }

    { 1100528607;;DataItem;SUPProductionLineLoop;
               DataItemTable=Table11071774;
               DataItemTableView=SORTING(Service Order No.,Production Date,Price Book Type,Price Book Code,Index Date,Room,Unit Price Code);
               OnAfterGetRecord=VAR
                                  SUPProduction@1100528600 : Record 11071773;
                                  ServiceOrderCostPlusEntry@1100528601 : Record 11012825;
                                BEGIN
                                  IF "Production Seq. No." = 0 THEN BEGIN
                                    SUPProduction.SETRANGE("Service Order No.", "Service Order No.");
                                    SUPProduction.SETRANGE("Production Date", "Production Date");
                                    SUPProduction.FINDFIRST;
                                    "Production Seq. No." := SUPProduction."Seq. No.";
                                    MODIFY;
                                  END;

                                  IF (LastSUPProductionLine."Service Order No." <> "Service Order No.") OR
                                     (LastSUPProductionLine."Production Seq. No." <> "Production Seq. No.")
                                  THEN
                                    SUPProductionLineLineNo := 0;

                                  IF "Line No." = 0 THEN BEGIN
                                    SUPProductionLineLineNo += 10000;
                                    "Line No." := SUPProductionLineLineNo;
                                    MODIFY;
                                  END;

                                  IF "Cost Plus Entries Created" THEN BEGIN
                                    IF ServiceOrderCostPlusEntry.GET("Service Order No.", "Cost Plus Line No.") THEN BEGIN
                                      ServiceOrderCostPlusEntry."Production Seq. No." := "Production Seq. No.";
                                      ServiceOrderCostPlusEntry."Production Line No." := "Line No.";
                                      ServiceOrderCostPlusEntry."Price Book Code" := '';
                                      ServiceOrderCostPlusEntry."Unit Price Index Date" := 0D;
                                      ServiceOrderCostPlusEntry."Production Recording Date" := 0D;
                                      ServiceOrderCostPlusEntry."Unit Price Room" := '';
                                      ServiceOrderCostPlusEntry."Unit Price Code" := '';
                                      ServiceOrderCostPlusEntry.MODIFY;
                                    END;
                                  END;

                                  LastSUPProductionLine := SUPProductionLineLoop;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TmpServiceUnitPrice@1100528600 : TEMPORARY Record 11071768;
      TmpServiceUnitPriceLine@1100528602 : TEMPORARY Record 11071769;
      TmpUnitPriceBudgetInternal@1100528604 : TEMPORARY Record 11071770;
      TmpUnitPriceServiceOrder@1100528605 : TEMPORARY Record 11071772;
      TmpUnitPriceProdRecordingLine@1100528606 : TEMPORARY Record 11071774;
      TmpServiceUnitPriceLineText@1100528607 : TEMPORARY Record 11071957;
      NewUnitPriceCode@1100528603 : Code[20];
      LastSUPRecordingLine@1100528609 : Record 11071772;
      SUPRecordingLineLineNo@1100528608 : Integer;
      LastSUPProduction@1100528611 : Record 11071773;
      SUPProductionSeqNo@1100528610 : Integer;
      LastSUPProductionLine@1100528612 : Record 11071774;
      SUPProductionLineLineNo@1100528613 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
    <?xml version="1.0" encoding="utf-8"?>
<Report xmlns="http://schemas.microsoft.com/sqlserver/reporting/2016/01/reportdefinition" xmlns:rd="http://schemas.microsoft.com/SQLServer/reporting/reportdesigner">
  <AutoRefresh>0</AutoRefresh>
  <DataSources>
    <DataSource Name="DataSource">
      <ConnectionProperties>
        <DataProvider>SQL</DataProvider>
        <ConnectString />
      </ConnectionProperties>
      <rd:SecurityType>None</rd:SecurityType>
    </DataSource>
  </DataSources>
  <ReportSections>
    <ReportSection>
      <Body>
        <Height>2in</Height>
        <Style />
      </Body>
      <Width>6.5in</Width>
      <Page>
        <Style />
      </Page>
    </ReportSection>
  </ReportSections>
  <Code>Public Function BlankZero(ByVal Value As Decimal)
    if Value = 0 then
        Return ""
    end if
    Return Value
End Function

Public Function BlankPos(ByVal Value As Decimal)
    if Value &gt; 0 then
        Return ""
    end if
    Return Value
End Function

Public Function BlankZeroAndPos(ByVal Value As Decimal)
    if Value &gt;= 0 then
        Return ""
    end if
    Return Value
End Function

Public Function BlankNeg(ByVal Value As Decimal)
    if Value &lt; 0 then
        Return ""
    end if
    Return Value
End Function

Public Function BlankNegAndZero(ByVal Value As Decimal)
    if Value &lt;= 0 then
        Return ""
    end if
    Return Value
End Function
</Code>
  <Language>=User!Language</Language>
  <ConsumeContainerWhitespace>true</ConsumeContainerWhitespace>
  <rd:ReportUnitType>Inch</rd:ReportUnitType>
</Report>
    END_OF_RDLDATA
  }
}

