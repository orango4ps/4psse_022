OBJECT Codeunit 11012687 Gen. Checklist Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1100526000 : TextConst 'DEU=H;ENU=T;NLD=H;SVE=t';

    PROCEDURE ValidateValue@1100526000(FieldType@1100526000 : 'Text,Numeric,Date,Option';OptionSetCode@1100526008 : Code[20];VAR Value@1100526001 : Text;VAR NumericValue@1100526002 : Decimal;VAR DateValue@1100526003 : Date;VAR OptionValueCode@1100526004 : Code[20]);
    VAR
      GenChecklistOptionValue@1100526007 : Record 11125899;
      DummyNumeric@1100526006 : Decimal;
      DummyDate@1100526005 : Date;
    BEGIN
      NumericValue := 0;
      DateValue := 0D;
      CASE FieldType OF
        FieldType::Numeric :
          BEGIN
            IF Value = '' THEN EXIT;
            EVALUATE(DummyNumeric, Value);
            Value := FORMAT(DummyNumeric, 0, 1);
            NumericValue := DummyNumeric;
          END;
        FieldType::Date :
          BEGIN
            IF Value = '' THEN EXIT;
            IF LOWERCASE(Value) = LOWERCASE(Text001) THEN BEGIN
              Value := FORMAT(TODAY);
              DateValue := TODAY;
            END ELSE BEGIN
              EVALUATE(DummyDate, Value);
              Value := FORMAT(DummyDate);
              DateValue := DummyDate;
            END;
          END;
        FieldType::Option :
          BEGIN
            GenChecklistOptionValue.SETRANGE("Option Set Code", OptionSetCode);
            GenChecklistOptionValue.SETFILTER(Description, '@' + Value);
            IF GenChecklistOptionValue.COUNT = 0 THEN
              GenChecklistOptionValue.SETFILTER(Description, '@' + Value + '*');
            IF GenChecklistOptionValue.COUNT = 1 THEN BEGIN
              GenChecklistOptionValue.FINDFIRST;
              Value := GenChecklistOptionValue.Description;
              OptionValueCode := GenChecklistOptionValue.Code;
            END ELSE BEGIN
              Value := '';
              OptionValueCode := '';
            END;
          END;
      END;
    END;

    PROCEDURE LookupValue@1100526001(FieldType@1100526003 : 'Text,Numeric,Date,Option';OptionSetCode@1100526002 : Code[20];VAR Value@1100526001 : Text;VAR OptionValueCode@1100526004 : Code[20]);
    VAR
      GenChecklistOptionValue@1100526000 : Record 11125899;
    BEGIN
      IF FieldType <> FieldType::Option THEN
        EXIT;

      GenChecklistOptionValue.SETRANGE("Option Set Code", OptionSetCode);
      IF GenChecklistOptionValue.GET(OptionSetCode, Value) THEN;
      IF PAGE.RUNMODAL(0, GenChecklistOptionValue) = ACTION::LookupOK THEN BEGIN
        Value := GenChecklistOptionValue.Description;
        OptionValueCode := GenChecklistOptionValue.Code;
      END;
    END;

    BEGIN
    END.
  }
}

