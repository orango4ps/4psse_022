OBJECT Table 11012080 Cust - Cost Object Sales Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               //>> 4PSSE
               "Creation Date" := TODAY;
               "Last Date Modified" := TODAY;
               "Modified by" := USERID;
               //<<
             END;

    OnModify=BEGIN
               //>> 4PSSE
               "Last Date Modified" := TODAY;
               "Modified by" := USERID;
               //<<
             END;

    CaptionML=[ENU=Customer - Cost Object Sales Price;
               NOR=Kunde Í salgspris for Kostnadsobjekt;
               SVE=Kund Í F”rs„ljningspris f”r kostnadsobjekt];
    LookupPageID=Page11012261;
  }
  FIELDS
  {
    { 10  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=FILTER(Labor..Sundry));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Cost Object Description");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   NotBlank=Yes;
                                                   CaptionClass='1,1,2' }
    { 20  ;   ;Customer No.        ;Code20        ;TableRelation=Customer.No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Customer Name");
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr;
                                                              SVE=Kundnr];
                                                   NotBlank=Yes }
    { 25  ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Kundnavn;
                                                              SVE=Betalarens namn];
                                                   Editable=No }
    { 30  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 35  ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 40  ;   ;Sales Price (LCY)   ;Decimal       ;OnValidate=VAR
                                                                CurrencyDate@1100528802 : Date;
                                                                CurrencyExchangeRate@1100528801 : Record 330;
                                                                Currency2@1100528800 : Record 4;
                                                              BEGIN
                                                                // dp00116.n
                                                                Currency2.InitRoundingPrecision;
                                                                IF "Currency Code" <> '' THEN BEGIN
                                                                  CurrencyDate := TODAY;
                                                                  "Sales Price" :=
                                                                  ROUND(
                                                                    CurrencyExchangeRate.ExchangeAmtLCYToFCY(
                                                                      0, '', CurrencyDate, "Currency Code", "Sales Price (LCY)",
                                                                       CurrencyExchangeRate.ExchangeRate(0, '', CurrencyDate, "Currency Code",TRUE),TRUE),
                                                                       Currency2."Unit-Amount Rounding Precision");
                                                                END ELSE BEGIN
                                                                  "Sales Price" := ROUND("Sales Price (LCY)",Currency2."Unit-Amount Rounding Precision");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sales Price (LCY);
                                                              NOR=Totalt i salgspris;
                                                              SVE=Totalt i f”rs„ljningspris];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 41  ;   ;Sales Price         ;Decimal       ;OnValidate=VAR
                                                                CurrencyDate@1100528802 : Date;
                                                                CurrencyExchangeRate@1100528801 : Record 330;
                                                                Currency2@1100528800 : Record 4;
                                                              BEGIN
                                                                // dp00116.n
                                                                Currency2.InitRoundingPrecision;
                                                                IF "Currency Code" <> '' THEN BEGIN
                                                                  CurrencyDate := TODAY;
                                                                  "Sales Price (LCY)" :=
                                                                  ROUND(
                                                                    CurrencyExchangeRate.ExchangeAmtFCYToLCY(
                                                                      0, '', CurrencyDate, "Currency Code", "Sales Price",
                                                                       CurrencyExchangeRate.ExchangeRate(0, '', CurrencyDate, "Currency Code",TRUE),TRUE),
                                                                       Currency2."Unit-Amount Rounding Precision");
                                                                END ELSE BEGIN
                                                                  "Sales Price (LCY)" := ROUND("Sales Price",Currency2."Unit-Amount Rounding Precision");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sales Price;
                                                              NOR=Salgspris;
                                                              SVE=F”rs„ljningspris];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 42  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                // dp00116.n
                                                                VALIDATE("Sales Price");
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 50  ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Code=FIELD(Cost Object),
                                                                                                    Global Dimension No.=CONST(2)));
                                                   CaptionML=[ENU=Cost Object Description;
                                                              NOR=Kostnadsobjekt, Beskrivelse;
                                                              SVE=Kostnadsobjekt, beskrivning];
                                                   Editable=No;
                                                   CaptionClass='1,11012000,2, , ' + Text000 }
    { 11128040;;Creation Date      ;Date          ;CaptionML=SVE=Uppl„ggningsdatum;
                                                   Description=I042 }
    { 11128041;;Last Date Modified ;Date          ;CaptionML=SVE=Uppdateringsdatum;
                                                   Description=I042 }
    { 11128042;;Modified by        ;Code30        ;CaptionML=SVE=Žndrad av;
                                                   Description=I042 }
  }
  KEYS
  {
    {    ;Customer No.,Cost Object,Starting Date  ;Clustered=Yes }
    {    ;Customer No.,Starting Date,Cost Object   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100527350 : TextConst 'ENU=Description;SVE=Beskrivning';

    PROCEDURE SetFilterCustCost@1210190000(CostCode@1210190001 : Code[20];CustomerNo@1210190002 : Code[20];DateFilter@1210190003 : Text[30]);
    BEGIN
      SETRANGE("Customer No.", CustomerNo);
      SETRANGE("Cost Object", CostCode);
      SETFILTER("Starting Date", DateFilter);
    END;

    PROCEDURE DrillDownCurrentPrice@1210190003(CostCode@1210190002 : Code[20];CustomerNo@1210190000 : Code[20];ReferenceDate@1210190001 : Date) : Decimal;
    BEGIN
      SetFilterCustCost(CostCode, CustomerNo, STRSUBSTNO('..%1', ReferenceDate));
      PAGE.RUNMODAL(0, Rec, "Sales Price");
    END;

    PROCEDURE GetCurrentPrice@1210190001(CostCode@1210190002 : Code[20];CustomerNo@1210190000 : Code[20];ReferenceDate@1210190001 : Date) : Decimal;
    BEGIN
      SetFilterCustCost(CostCode, CustomerNo, STRSUBSTNO('..%1', ReferenceDate));

      IF FINDLAST THEN
        //EXIT("Sales Price");
        EXIT("Sales Price (LCY)");
    END;

    PROCEDURE UpdateCurrentPrice@1210190002(CostCode@1210190002 : Code[20];CustomerNo@1210190001 : Code[20];ReferenceDate@1210190000 : Date;NewSalesPrice@1210190003 : Decimal);
    BEGIN
      SetFilterCustCost(CostCode, CustomerNo, STRSUBSTNO('%1', ReferenceDate));

      IF FINDFIRST THEN BEGIN
        IF NewSalesPrice <> 0 THEN BEGIN
          //"Sales Price" := NewSalesPrice;
          VALIDATE("Sales Price (LCY)", NewSalesPrice);
          MODIFY;
        END ELSE
          DELETE;
      END ELSE BEGIN
        INIT;
        "Cost Object" := CostCode;
        "Customer No." := CustomerNo;
        "Starting Date" := ReferenceDate;
        //"Sales Price" := NewSalesPrice;
        VALIDATE("Sales Price (LCY)", NewSalesPrice);
        INSERT;
      END;
    END;

    BEGIN
    {

      4PSSE
      130911 ITERO.DJ New fields: Creation Date, Last Date Modified, Modified by
    }
    END.
  }
}

