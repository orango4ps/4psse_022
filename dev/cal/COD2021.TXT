OBJECT Codeunit 2021 Image Analysis Result
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      JSONManagement@1001 : Codeunit 5459;
      Result@1000 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      Tags@1002 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JArray";
      Color@1003 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      DominantColors@1005 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JArray";
      Faces@1004 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JArray";
      LastAnalysisType@1006 : 'Tags,Faces,Color';

    [External]
    PROCEDURE SetJson@2(JSONMgt@1000 : Codeunit 5459;AnalysisType@1001 : 'Tags,Faces,Color');
    BEGIN
      Tags := Tags.JArray;
      Color := Color.JObject;
      DominantColors := DominantColors.JArray;
      Faces := Faces.JArray;

      LastAnalysisType := AnalysisType;

      JSONMgt.GetJSONObject(Result);
      IF ISNULL(Result) THEN
        EXIT;

      IF NOT JSONManagement.GetArrayPropertyValueFromJObjectByName(Result,'tags',Tags) THEN
        IF NOT JSONManagement.GetArrayPropertyValueFromJObjectByName(Result,'Predictions',Tags) THEN
          IF NOT JSONManagement.GetArrayPropertyValueFromJObjectByName(Result,'predictions',Tags) THEN
            Tags := Tags.JArray;

      IF NOT JSONManagement.GetArrayPropertyValueFromJObjectByName(Result,'faces',Faces) THEN
        Faces := Faces.JArray;

      Color := Color.JObject;
      DominantColors := DominantColors.JArray;
      IF JSONManagement.GetObjectPropertyValueFromJObjectByName(Result,'color',Color) THEN
        JSONManagement.GetArrayPropertyValueFromJObjectByName(Color,'dominantColors',DominantColors)
      ELSE BEGIN
        Color := Color.JObject;
        DominantColors := DominantColors.JArray;
      END;
    END;

    [External]
    PROCEDURE TagCount@7() : Integer;
    BEGIN
      EXIT(Tags.Count);
    END;

    [External]
    PROCEDURE TagName@8(Number@1002 : Integer) : Text;
    VAR
      Tag@1000 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      Name@1001 : Text;
    BEGIN
      JSONManagement.InitializeCollectionFromJArray(Tags);
      IF JSONManagement.GetJObjectFromCollectionByIndex(Tag,Number - 1) THEN BEGIN
        IF NOT JSONManagement.GetStringPropertyValueFromJObjectByName(Tag,'name',Name) THEN
          IF NOT JSONManagement.GetStringPropertyValueFromJObjectByName(Tag,'Tag',Name) THEN
            JSONManagement.GetStringPropertyValueFromJObjectByName(Tag,'tagName',Name);
        EXIT(Name)
      END;
    END;

    [External]
    PROCEDURE TagConfidence@17(Number@1002 : Integer) : Decimal;
    VAR
      Tag@1000 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      Confidence@1001 : Decimal;
      ConfidenceText@1003 : Text;
    BEGIN
      JSONManagement.InitializeCollectionFromJArray(Tags);
      IF JSONManagement.GetJObjectFromCollectionByIndex(Tag,Number - 1) THEN BEGIN
        IF NOT JSONManagement.GetStringPropertyValueFromJObjectByName(Tag,'confidence',ConfidenceText) THEN
          IF NOT JSONManagement.GetStringPropertyValueFromJObjectByName(Tag,'Probability',ConfidenceText) THEN
            IF NOT JSONManagement.GetStringPropertyValueFromJObjectByName(Tag,'probability',ConfidenceText) THEN
              ConfidenceText := '0';
        EVALUATE(Confidence,ConfidenceText);
        EXIT(Confidence)
      END;
    END;

    [External]
    PROCEDURE DominantColorForeground@24() : Text;
    VAR
      ColorText@1001 : Text;
    BEGIN
      JSONManagement.GetStringPropertyValueFromJObjectByName(Color,'dominantColorForeground',ColorText);
      EXIT(ColorText);
    END;

    [External]
    PROCEDURE DominantColorBackground@27() : Text;
    VAR
      ColorText@1001 : Text;
    BEGIN
      JSONManagement.GetStringPropertyValueFromJObjectByName(Color,'dominantColorBackground',ColorText);
      EXIT(ColorText);
    END;

    [External]
    PROCEDURE DominantColorCount@29() : Integer;
    BEGIN
      EXIT(DominantColors.Count);
    END;

    [External]
    PROCEDURE DominantColor@39(Number@1002 : Integer) : Text;
    VAR
      DominantColor@1000 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
    BEGIN
      JSONManagement.InitializeCollectionFromJArray(DominantColors);
      IF JSONManagement.GetJObjectFromCollectionByIndex(DominantColor,Number - 1) THEN
        EXIT(FORMAT(DominantColor));
    END;

    [External]
    PROCEDURE FaceCount@3() : Integer;
    BEGIN
      EXIT(Faces.Count);
    END;

    [External]
    PROCEDURE FaceAge@4(Number@1002 : Integer) : Integer;
    VAR
      Face@1000 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      AgeText@1001 : Text;
      Age@1003 : Integer;
    BEGIN
      JSONManagement.InitializeCollectionFromJArray(Faces);
      IF JSONManagement.GetJObjectFromCollectionByIndex(Face,Number - 1) THEN BEGIN
        JSONManagement.GetStringPropertyValueFromJObjectByName(Face,'age',AgeText);
        EVALUATE(Age,AgeText);
        IF Age < 16 THEN
          EXIT(0);
        EXIT(Age);
      END;
    END;

    [External]
    PROCEDURE FaceGender@5(Number@1002 : Integer) : Text;
    VAR
      Face@1000 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      Gender@1001 : Text;
      AgeText@1003 : Text;
      Age@1004 : Integer;
    BEGIN
      JSONManagement.InitializeCollectionFromJArray(Faces);
      IF JSONManagement.GetJObjectFromCollectionByIndex(Face,Number - 1) THEN BEGIN
        JSONManagement.GetStringPropertyValueFromJObjectByName(Face,'age',AgeText);
        EVALUATE(Age,AgeText);
        IF Age < 16 THEN
          EXIT('');
        JSONManagement.GetStringPropertyValueFromJObjectByName(Face,'gender',Gender);
        EXIT(Gender);
      END;
    END;

    [External]
    PROCEDURE GetLatestAnalysisType@11(VAR AnalysisType@1000 : 'Tags,Faces,Color');
    BEGIN
      AnalysisType := LastAnalysisType;
    END;

    BEGIN
    END.
  }
}

