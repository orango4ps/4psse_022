OBJECT Table 11229899 Base Risk Opportunity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Base Risk Opportunity;
    LookupPageID=Page11126560;
    DrillDownPageID=Page11126560;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=Risk,Opportunity;
                                                   OptionString=Risk,Opportunity }
    { 40  ;   ;Apply To            ;Option        ;CaptionML=[ENU=Apply To;
                                                              NOR=Gjelder];
                                                   OptionCaptionML=ENU=Project,Try-Out,Service Contract,All;
                                                   OptionString=Project,Try-Out,Service Contract,All }
    { 50  ;   ;Group               ;Code20        ;TableRelation="Base Risks Opp. Group";
                                                   CaptionML=[ENU=Group;
                                                              NOR=Gruppe;
                                                              SVE=Grupp] }
    { 60  ;   ;Impact              ;Code20        ;TableRelation="Risk Opportunity Impact";
                                                   OnValidate=VAR
                                                                RiskOpportunityImpact@1100528800 : Record 11229901;
                                                              BEGIN
                                                                IF RiskOpportunityImpact.GET(Impact) THEN BEGIN
                                                                  "Predicted Cost Amount (LCY)" := RiskOpportunityImpact."Predicted Cost Amount (LCY)";
                                                                  "Predicted Sales Amount (LCY)" := RiskOpportunityImpact."Predicted Sales Amount (LCY)";
                                                                  //**4PS.sn BI047 KD 13-07-16
                                                                  "Currency Code" := RiskOpportunityImpact."Currency Code";
                                                                  "Predicted Cost Amount" := RiskOpportunityImpact."Predicted Cost Amount";
                                                                  "Predicted Sales Amount" := RiskOpportunityImpact."Predicted Sales Amount";
                                                                  //**4PS.en BI047 KD 13-07-16
                                                                END ELSE BEGIN
                                                                  "Predicted Cost Amount (LCY)" := 0;
                                                                  "Predicted Sales Amount (LCY)" := 0;
                                                                  //**4PS.sn BI047 KD 13-07-16
                                                                  "Currency Code" := '';
                                                                  "Predicted Cost Amount" := 0;
                                                                  "Predicted Sales Amount" := 0;
                                                                  //**4PS.en BI047 KD 13-07-16
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Impact;
                                                              SVE=Impakt] }
    { 70  ;   ;Predicted Cost Amount (LCY);Decimal;OnValidate=BEGIN
                                                                "Predicted Cost Amount" := CalculateAmount("Predicted Cost Amount (LCY)",TRUE); //**4PS.s BI047 KD 13-07-16
                                                              END;

                                                   CaptionML=ENU=Predicted Cost Amount (LCY);
                                                   AutoFormatType=1 }
    { 75  ;   ;Predicted Sales Amount (LCY);Decimal;
                                                   OnValidate=BEGIN
                                                                "Predicted Sales Amount" := CalculateAmount("Predicted Sales Amount (LCY)",TRUE); //**4PS.s BI047 KD 13-07-16
                                                              END;

                                                   CaptionML=ENU=Predicted Sales Amount (LCY);
                                                   AutoFormatType=1 }
    { 80  ;   ;Probability Code    ;Code20        ;TableRelation="Risk Opportunity Probability";
                                                   OnValidate=VAR
                                                                RiskOpportunityProbability@1100528800 : Record 11229902;
                                                              BEGIN
                                                                IF RiskOpportunityProbability.GET("Probability Code") THEN
                                                                  "Probability %" := RiskOpportunityProbability.Percentage
                                                                ELSE
                                                                  "Probability %" := 0;
                                                              END;

                                                   CaptionML=ENU=Probability Code }
    { 90  ;   ;Probability %       ;Decimal       ;CaptionML=[ENU=Probability %;
                                                              NOR=Sannsynlighets-%;
                                                              SVE=Sannolikhet %];
                                                   MaxValue=100 }
    { 110 ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                //**4PS.sn BI047 KD 13-07-16
                                                                "Predicted Cost Amount" := CalculateAmount("Predicted Cost Amount (LCY)",TRUE);
                                                                "Predicted Sales Amount" := CalculateAmount("Predicted Sales Amount (LCY)",TRUE);
                                                                //**4PS.en BI047 KD 13-07-16
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod];
                                                   Description=BI047 }
    { 120 ;   ;Predicted Cost Amount;Decimal      ;OnValidate=BEGIN
                                                                "Predicted Cost Amount (LCY)" := CalculateAmount("Predicted Cost Amount",FALSE); //**4PS.s BI047 KD 13-07-16
                                                              END;

                                                   CaptionML=ENU=Predicted Cost Amount (FCY);
                                                   Description=BI047;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 130 ;   ;Predicted Sales Amount;Decimal     ;OnValidate=BEGIN
                                                                "Predicted Sales Amount (LCY)" := CalculateAmount("Predicted Sales Amount",FALSE); //**4PS.s BI047 KD 13-07-16
                                                              END;

                                                   CaptionML=ENU=Predicted Sales Amount (FCY);
                                                   Description=BI047;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Currency@1100525001 : Record 4;

    LOCAL PROCEDURE InitCurrency@1100525004();
    BEGIN
      //**4PS.sn BI047 KD 13-07-16
      IF "Currency Code" = Currency.Code THEN
        EXIT;

      Currency.Initialize("Currency Code");
      Currency.TESTFIELD("Amount Rounding Precision");
      //**4PS.en BI047 KD 13-07-16
    END;

    LOCAL PROCEDURE CalculateAmount@1100525000(Amount@1100525002 : Decimal;CalcFCY@1100525000 : Boolean) : Decimal;
    VAR
      LCYCurrency@1100525001 : Record 4;
      CurrencyExchangeRate@1100525003 : Record 330;
    BEGIN
      //**4PS.sn BI047 KD 13-07-16
      IF Amount = 0 THEN
          EXIT(0);

      IF CalcFCY THEN BEGIN
        InitCurrency;

        EXIT(
          ROUND(
            CurrencyExchangeRate.ExchangeAmtLCYToFCY(
              0,'',0D,"Currency Code",Amount,
              CurrencyExchangeRate.ExchangeRate(0,'',0D,"Currency Code",TRUE),TRUE),
            Currency."Amount Rounding Precision"));
      END;

      LCYCurrency.InitRoundingPrecision;

      EXIT(
        ROUND(
          CurrencyExchangeRate.ExchangeAmtFCYToLCY(
            0,'',0D,"Currency Code",Amount,
            CurrencyExchangeRate.ExchangeRate(0,'',0D,"Currency Code",TRUE),TRUE),
          LCYCurrency."Amount Rounding Precision"));
      //**4PS.en BI047 KD 13-07-16
    END;

    BEGIN
    {
      4PS 13-07-16 UKR-C25096 KD: Multi Currency in Risks and opportunities (BI047) - Fields "Currency Code", "Predicted Cost Amount (FCY)", "Predicted Sales Amount (FCY)" added
                                                                                      Fields "Predicted Cost Amount", "Predicted Sales Amount" cahnged - (LCY) added to captions
                                                                                      Triggers InitCurrency, CalculateAmount added
                                                                                      Triggers "Predicted Cost Amount - OnValidate", "Predicted Sales Amount - OnValidate", "Impact - OnValidate" changed
    }
    END.
  }
}

