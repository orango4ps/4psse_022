OBJECT Codeunit 11128017 Offset Mgmt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PSSE;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      txtTooManyTransactions@1100285300 : TextConst 'ENU=There are too many selected transactions. Please reduce the amount.;SVE=Det „r f”r m†nga transaktioner markerade. V„nligen minska p† antalet och f”rs”ker sedan igen.';
      txtAmountDifference@1100285301 : TextConst 'ENU=There is a Net Sum Difference of %1. Transactions can not be set off if the net sum is not 0.;SVE=Markerade transaktioner har en differens p† %1. Transaktioner kan bara kvittas om totalsumman blir 0.';
      txtAlreadySetOff@1100285302 : TextConst 'ENU=Already set of transactions discovered at entry no. %1. Please undo this first.;SVE=Redan kvittade transaktioner uppt„ckta vid l”pnr %1. terst„ll dessa kvittningar f”rst.';

    PROCEDURE OffsetTransactions@1100285300(VAR GLEntry@1100285300 : Record 17);
    VAR
      GeneralLedgerSetup@1100285302 : Record 98;
      NoSeriesManagement@1100285301 : Codeunit 396;
      NewNoSeries@1100285303 : Code[20];
    BEGIN
      GeneralLedgerSetup.GET;
      GeneralLedgerSetup.TESTFIELD("Offset No. Series");

      IF GLEntry.COUNT > 2000 THEN ERROR(txtTooManyTransactions);
      GLEntry.CALCSUMS(Amount);

      IF (GLEntry.Amount <> 0) THEN ERROR(txtAmountDifference, GLEntry.Amount);

      NoSeriesManagement.InitSeries(GeneralLedgerSetup."Offset No. Series", '', TODAY, NewNoSeries, GeneralLedgerSetup."Offset No. Series");

      IF (GLEntry.FINDSET(TRUE)) THEN REPEAT
         IF (GLEntry.Offset) THEN ERROR(txtAlreadySetOff, GLEntry."Entry No.");
         GLEntry.Offset := TRUE;
         GLEntry."Offset No." := NewNoSeries;
         GLEntry."Offset Date" := TODAY;
         GLEntry.MODIFY;
      UNTIL GLEntry.NEXT = 0;
    END;

    PROCEDURE UnOffsetTransactions@1100285301(VAR GLEntry@1100285300 : Record 17);
    VAR
      GLEntry2@1100285301 : Record 17;
    BEGIN

      IF (GLEntry.Offset) THEN BEGIN
         GLEntry2.SETRANGE(Offset, TRUE);
         GLEntry2.SETRANGE("Offset No.", GLEntry."Offset No.");
         IF (GLEntry2.FINDSET(TRUE)) THEN REPEAT
            GLEntry2.Offset := FALSE;
            GLEntry2."Offset No." := '';
            GLEntry2."Offset Date" := 0D;
            GLEntry2.MODIFY;
         UNTIL GLEntry2.NEXT = 0;
      END;
    END;

    BEGIN
    {
      160530 SRFC-017 ITERO.MH New codeunit
    }
    END.
  }
}

