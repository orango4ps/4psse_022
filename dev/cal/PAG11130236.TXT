OBJECT Page 11130236 Transfer Recipe Sales-Job
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=VertriebseingÑnge auf Montageprojekt Åbertragen;
               ENU=Transfer Sales Recipes to Assembly Project;
               NLD=Verkooprecepten overzetten naar assemblageproject];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table37;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 CheckSelection;

                 SeriesNoEditable := TRUE;
                 NewCodeEditable := TRUE;
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190022;1;Group  ;
                Name=Step1;
                CaptionML=[DEU=öbertragen von;
                           ENU=Transfer from;
                           NLD=Overzetten van;
                           NOR=Overfõring fra;
                           SVE=ôverfîring frÜn];
                GroupType=Group }

    { 1100525005;2;Field  ;
                CaptionML=[DEU=öbertragen in;
                           ENU=Transfer to;
                           NLD=Overzetten naar;
                           NOR=Overfõring til;
                           SVE=ôverfîring till];
                Style=Strong;
                StyleExpr=TRUE }

    { 1100525001;2;Field  ;
                Name=CurrSeries;
                AssistEdit=Yes;
                CaptionML=[DEU=Nummernserie;
                           ENU=No. Series;
                           NLD=Nr.-reeks;
                           NOR=Nr.serie;
                           SVE=Nr-serier];
                SourceExpr=CurrSeries;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               IF SeriesNoEditable THEN
                                 CheckSeries(TRUE);
                             END;
                              }

    { 5   ;2   ;Field     ;
                CaptionML=[DEU=Projekt;
                           ENU=Project;
                           NLD=Project;
                           NOR=Prosjekt;
                           SVE=Projekt];
                SourceExpr=ProjTo;
                Editable=NewCodeEditable;
                OnValidate=BEGIN
                             IF ProjTo <> '' THEN BEGIN
                               IF NOT ProjRec.GET(ProjTo) THEN
                                 ERROR(Text000, ProjRec.TABLECAPTION, ProjTo);
                             END;
                           END;

                OnLookup=BEGIN
                           IF PAGE.RUNMODAL(PAGE::"Job List",ProjRec) = ACTION::LookupOK THEN BEGIN
                             ProjTo := ProjRec."No.";
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

  }
  CODE
  {
    VAR
      Text000@11012018 : TextConst 'DEU=%1 %2 existiert nicht.;ENU=%1 %2 does not exist;NLD=%1 %2 niet aanwezig;NOR=%1 %2 finnes ikke;SVE=%1 %2 finns inte.';
      Text001@1100525014 : TextConst 'DEU=Vertriebszeilenauswahl nicht korrekt;ENU=Sales Line Selection not correct;NLD=Verkoopregel selectie niet akkoord';
      Text002@11012020 : TextConst 'DEU=WÑhlen Sie ein Projekt aus.;ENU=Select a project;NLD=Selecteer een project;NOR=Velg et prosjekt;SVE=VÑlj ett projekt';
      Text003@11012021 : TextConst 'DEU=Mîchten Sie Auftrag %1 auf Projekt %2 Åbertragen;ENU=Do you want to transfer Order %1 to Project %2;NLD=Wilt u order %1 overzetten naar project %2';
      NoSeriesMgt@1100525010 : Codeunit 396;
      JobSetup@1210190021 : Record 315;
      SalesHeader@1100525016 : Record 36;
      CurrSeries@1100525002 : Code[20];
      OrderNo@1100525015 : Code[20];
      ProjTo@1100525000 : Code[20];
      ProjRec@11012000 : Record 11072003;
      CloseActionOK@1100528400 : Boolean;
      NewCodeEditable@1100525011 : Boolean INDATASET;
      SeriesNoEditable@1100525001 : Boolean INDATASET;

    PROCEDURE CheckSelection@1100525002();
    BEGIN
      ProjTo := '';
      OrderNo := '';

      IF FINDSET THEN BEGIN
        OrderNo := "Document No.";
        REPEAT
          IF ("Job No." <> '') AND (ProjTo = '')  THEN
            ProjTo := "Job No.";
        UNTIL (NEXT = 0) OR (OrderNo <> "Document No.");
      END;

      IF (ProjTo <> '') OR (OrderNo = '') OR (OrderNo <> "Document No.") THEN
        ERROR(Text001);

      SalesHeader.GET("Document Type","Document No.");
      ProjTo := SalesHeader."Job No.";
    END;

    PROCEDURE FinishWizard@1100525001();
    VAR
      Job@1100528400 : Record 11072003;
      TransferCu@1100525000 : Codeunit 11012228;
      ExtTo@1100525026 : Code[20];
      AdjTo@1100525025 : Code[20];
      HouseModelTo@1100525024 : Code[20];
      PlotTo@1100525023 : Code[20];
      MainGroupTo@1100525022 : Code[20];
      GroupTo@1100525021 : Code[20];
      SubGroupTo@1100525020 : Code[20];
      OptTo@1100525019 : Code[20];
    BEGIN
      IF ProjTo = '' THEN
        ERROR(Text002);

      IF CONFIRM(Text003,FALSE,OrderNo,ProjTo) THEN BEGIN
        COMMIT;
        Job.GenerateAssemblyProject(
          SalesHeader."Sell-to Customer No.", SalesHeader."Bill-to Customer No.",
          ProjTo, CurrSeries, SalesHeader."No.");
        TransferCu.SetProjectOptions(
          ProjTo,ExtTo,AdjTo,PlotTo,HouseModelTo,MainGroupTo,GroupTo,SubGroupTo,OptTo,'');
        TransferCu.CopySalesRecipeToProjBudget(Rec);
      END;
    END;

    PROCEDURE CheckSeries@1210190005(lvAssistEdit@1100485000 : Boolean);
    BEGIN
      JobSetup.GET;
      JobSetup.TESTFIELD("Assembly Project Nos.");
      CurrSeries := JobSetup."Assembly Project Nos.";

      IF lvAssistEdit = TRUE THEN
        NoSeriesMgt.SelectSeries(CurrSeries, CurrSeries, CurrSeries);

      DetermineNewCode(FALSE);

      NewCodeEditable := CheckSeriesManual;
    END;

    PROCEDURE DetermineNewCode@1000000000(lvUpdate@1100485000 : Boolean);
    BEGIN
      IF CheckSeriesDefaultNos THEN BEGIN
        CLEAR(NoSeriesMgt);
        ProjTo := NoSeriesMgt.GetNextNo(CurrSeries,0D,lvUpdate);
      END ELSE BEGIN
        IF CurrSeries <> '' THEN
          ProjTo := '';
      END;
    END;

    PROCEDURE CheckSeriesManual@1100485006() : Boolean;
    VAR
      NoSeries@1100485000 : Record 308;
    BEGIN
      IF NOT NoSeries.GET(CurrSeries) THEN
        EXIT(TRUE);
      EXIT(NoSeries."Manual Nos.");
    END;

    PROCEDURE CheckSeriesDefaultNos@1100525000() : Boolean;
    VAR
      NoSeries@1100485000 : Record 308;
    BEGIN
      IF CurrSeries = '' THEN
        EXIT(FALSE);
      IF NOT NoSeries.GET(CurrSeries) THEN
        EXIT(FALSE);
      EXIT(NoSeries."Default Nos.");
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      FinishWizard;
    END;

    PROCEDURE StdDialogRunModal@1100528402();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

