OBJECT Codeunit 11072104 INSBOU Prcs. Maint. St. Msg Q.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            INSBOUMessageQueueMgt@1100528801 : Codeunit 11072103;
            INSBOUSetup@1100528800 : Record 11012348;
          BEGIN
            INSBOUSetup.GET;
            INSBOUMessageQueue.SETRANGE("INSBOU Message Type", INSBOUMessageQueue."INSBOU Message Type"::MaintenanceStatus);
            INSBOUMessageQueue.SETFILTER(Status, '1|3'); //1=Unprocessed and 3=Error
            IF NOT SendManual THEN BEGIN
              IF INSBOUSetup."No. Of Retries Maint. Status" > 0 THEN
                INSBOUMessageQueue.SETFILTER("No. Of Retries", '<%1', INSBOUSetup."No. Of Retries Maint. Status");
              INSBOUMessageQueue.SETRANGE("Job Queue Entry ID", Rec.ID);
            END;

            IF INSBOUMessageQueue.FINDSET(TRUE) THEN
              REPEAT
                INSBOUMessageQueue.Status := INSBOUMessageQueue.Status::Processing;
                INSBOUMessageQueue.MODIFY;
                CLEARLASTERROR;
                COMMIT;
                IF NOT CODEUNIT.RUN(CODEUNIT::"INSBOU Send Msg Queue Entry", INSBOUMessageQueue) THEN BEGIN
                  INSBOUMessageQueue.GET(INSBOUMessageQueue."Entry No.");
                  INSBOUMessageQueue."Error Message" := COPYSTR(GETLASTERRORTEXT, 1, MAXSTRLEN(INSBOUMessageQueue."Error Message"));
                  INSBOUMessageQueue.Status := INSBOUMessageQueue.Status::Error;
                  INSBOUMessageQueue."Sent On" := TODAY;
                  INSBOUMessageQueue."Sent At" := TIME;
                  INSBOUMessageQueue."Sent By" := USERID;
                  INSBOUMessageQueue."No. Of Retries" += 1;
                  INSBOUMessageQueue.MODIFY;
                END ELSE BEGIN
                  INSBOUMessageQueue.Status := INSBOUMessageQueue.Status::Sent;
                  INSBOUMessageQueue."Error Message" := '';
                  INSBOUMessageQueue."Sent On" := TODAY;
                  INSBOUMessageQueue."Sent At" := TIME;
                  INSBOUMessageQueue."Sent By" := USERID;
                  INSBOUMessageQueue.MODIFY;
                END;
                COMMIT;
              UNTIL INSBOUMessageQueue.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      INSBOUMessageQueue@1100528800 : Record 11072353;
      SetJobQueueEntryParameter@1100528801 : TextConst 'ENU=Set Job Queue Entry Parameter. See documentation for details.';
      SendManual@1100528802 : Boolean;

    PROCEDURE SetManualSendVars@1100528802(ISendManual@1100528800 : Boolean;VAR IINSBOUMessageQueue@1100528801 : Record 11072353);
    BEGIN
      SendManual := ISendManual;
      INSBOUMessageQueue.COPY(IINSBOUMessageQueue);
    END;

    BEGIN
    END.
  }
}

