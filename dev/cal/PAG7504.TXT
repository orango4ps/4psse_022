OBJECT Page 7504 Item Attribute Value List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Attribute Values;
               NOR=Verdier for vareattributt;
               SVE=V„rden p† artikelattribut];
    LinksAllowed=No;
    SourceTable=Table7504;
    DelayedInsert=Yes;
    PageType=ListPart;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 CurrPage.EDITABLE(TRUE);
               END;

    OnDeleteRecord=BEGIN
                     DeleteItemAttributeValueMapping("Attribute ID");
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                AssistEdit=No;
                CaptionML=[ENU=Attribute;
                           NOR=Attributt;
                           SVE=Attribut];
                ToolTipML=[ENU=Specifies the item attribute.;
                           NOR=Angir vareattributtet.;
                           SVE=Anger artikelattributet.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Attribute Name";
                TableRelation="Item Attribute".Name WHERE (Blocked=CONST(No));
                OnValidate=VAR
                             ItemAttributeValue@1003 : Record 7501;
                             ItemAttributeValueMapping@1001 : Record 7505;
                           BEGIN
                             IF xRec."Attribute Name" <> '' THEN
                               DeleteItemAttributeValueMapping(xRec."Attribute ID");

                             IF NOT FindAttributeValue(ItemAttributeValue) THEN
                               InsertItemAttributeValue(ItemAttributeValue,Rec);

                             IF ItemAttributeValue.GET(ItemAttributeValue."Attribute ID",ItemAttributeValue.ID) THEN BEGIN
                               ItemAttributeValueMapping.RESET;
                               ItemAttributeValueMapping.INIT;
                               ItemAttributeValueMapping."Table ID" := DATABASE::Item;
                               ItemAttributeValueMapping."No." := RelatedRecordCode;
                               ItemAttributeValueMapping."Item Attribute ID" := ItemAttributeValue."Attribute ID";
                               ItemAttributeValueMapping."Item Attribute Value ID" := ItemAttributeValue.ID;
                               ItemAttributeValueMapping.INSERT;
                             END;
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=Value;
                CaptionML=[ENU=Value;
                           NOR=Verdi;
                           SVE=V„rde];
                ToolTipML=[ENU=Specifies the value of the item attribute.;
                           NOR=Angir verdien for vareattributtet.;
                           SVE=Anger v„rdet p† artikelattributet.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Value;
                TableRelation=IF (Attribute Type=CONST(Option)) "Item Attribute Value".Value WHERE (Attribute ID=FIELD(Attribute ID),
                                                                                                    Blocked=CONST(No));
                OnValidate=VAR
                             ItemAttributeValue@1003 : Record 7501;
                             ItemAttributeValueMapping@1002 : Record 7505;
                             ItemAttribute@1000 : Record 7500;
                           BEGIN
                             IF NOT FindAttributeValue(ItemAttributeValue) THEN
                               InsertItemAttributeValue(ItemAttributeValue,Rec);

                             ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::Item);
                             ItemAttributeValueMapping.SETRANGE("No.",RelatedRecordCode);
                             ItemAttributeValueMapping.SETRANGE("Item Attribute ID",ItemAttributeValue."Attribute ID");

                             IF ItemAttributeValueMapping.FINDFIRST THEN BEGIN
                               ItemAttributeValueMapping."Item Attribute Value ID" := ItemAttributeValue.ID;
                               OnBeforeItemAttributeValueMappingModify(ItemAttributeValueMapping,ItemAttributeValue,RelatedRecordCode);
                               ItemAttributeValueMapping.MODIFY;
                             END;

                             ItemAttribute.GET("Attribute ID");
                             IF ItemAttribute.Type <> ItemAttribute.Type::Option THEN
                               IF FindAttributeValueFromRecord(ItemAttributeValue,xRec) THEN
                                 IF NOT ItemAttributeValue.HasBeenUsed THEN
                                   ItemAttributeValue.DELETE;
                           END;
                            }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the name of the item or resource's unit of measure, such as piece or hour.;
                           NOR=Angir navnet p† varens eller ressursens enhet, for eksempel stykk eller time.;
                           SVE=Anger namnet p† artikelns eller resursens m†ttenhet, t.ex. styck eller timme.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Unit of Measure" }

  }
  CODE
  {
    VAR
      RelatedRecordCode@1000 : Code[20];

    [External]
    PROCEDURE LoadAttributes@1(ItemNo@1006 : Code[20]);
    VAR
      ItemAttributeValueMapping@1005 : Record 7505;
      TempItemAttributeValue@1004 : TEMPORARY Record 7501;
      ItemAttributeValue@1001 : Record 7501;
    BEGIN
      RelatedRecordCode := ItemNo;
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::Item);
      ItemAttributeValueMapping.SETRANGE("No.",ItemNo);
      IF ItemAttributeValueMapping.FINDSET THEN
        REPEAT
          ItemAttributeValue.GET(ItemAttributeValueMapping."Item Attribute ID",ItemAttributeValueMapping."Item Attribute Value ID");
          TempItemAttributeValue.TRANSFERFIELDS(ItemAttributeValue);
          TempItemAttributeValue.INSERT;
        UNTIL ItemAttributeValueMapping.NEXT = 0;

      PopulateItemAttributeValueSelection(TempItemAttributeValue);
    END;

    LOCAL PROCEDURE DeleteItemAttributeValueMapping@2(AttributeToDeleteID@1002 : Integer);
    VAR
      ItemAttributeValueMapping@1001 : Record 7505;
      ItemAttribute@1000 : Record 7500;
    BEGIN
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::Item);
      ItemAttributeValueMapping.SETRANGE("No.",RelatedRecordCode);
      ItemAttributeValueMapping.SETRANGE("Item Attribute ID",AttributeToDeleteID);
      IF ItemAttributeValueMapping.FINDFIRST THEN BEGIN
        ItemAttributeValueMapping.DELETE;
        OnAfterItemAttributeValueMappingDelete(AttributeToDeleteID,RelatedRecordCode);
      END;

      ItemAttribute.GET(AttributeToDeleteID);
      ItemAttribute.RemoveUnusedArbitraryValues;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterItemAttributeValueMappingDelete@5(AttributeToDeleteID@1000 : Integer;RelatedRecordCode@1001 : Code[20]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeItemAttributeValueMappingModify@3(VAR ItemAttributeValueMapping@1000 : Record 7505;ItemAttributeValue@1001 : Record 7501;RelatedRecordCode@1002 : Code[20]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

