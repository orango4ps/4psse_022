OBJECT Table 11012777 Provisional Work Feature
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ContactFeature@1100528702 : Record 11012752;
               ProvisionalWorkCommentLine@1100528700 : Record 11071940;
               ProvisionalWorkCommentLine2@1100528701 : Record 11071940;
             BEGIN
               ContactFeature.SETRANGE("Feature Group", "Feature Group");
               ContactFeature.SETRANGE(Code, Feature);
               IF ContactFeature.FINDFIRST THEN
                 "Feature Description" := ContactFeature.Description;

               ProvisionalWorkCommentLine.SETRANGE("Table Name", ProvisionalWorkCommentLine."Table Name"::"Contact Feature");
               ProvisionalWorkCommentLine.SETFILTER("Provisional Work No.", '%1', '');
               ProvisionalWorkCommentLine.SETRANGE("Feature Group", "Feature Group");
               ProvisionalWorkCommentLine.SETRANGE(Feature, Feature);
               IF ProvisionalWorkCommentLine.FINDSET THEN BEGIN
                 ProvisionalWorkCommentLine2.INIT;
                 ProvisionalWorkCommentLine2."Table Name" := ProvisionalWorkCommentLine."Table Name"::"Provisional Work Feature";
                 ProvisionalWorkCommentLine2."Provisional Work No." := "Provisional Work No.";
                 ProvisionalWorkCommentLine2."Feature Group" := "Feature Group";
                 ProvisionalWorkCommentLine2.Feature := Feature;
                 ProvisionalWorkCommentLine2.SetUpNewLine;
                 REPEAT
                   ProvisionalWorkCommentLine2."Line No." := ProvisionalWorkCommentLine."Line No.";
                   ProvisionalWorkCommentLine2.Code := ProvisionalWorkCommentLine.Code;
                   ProvisionalWorkCommentLine2.Comment := ProvisionalWorkCommentLine.Comment;
                   ProvisionalWorkCommentLine2."Comment Code" := ProvisionalWorkCommentLine."Comment Code";
                   ProvisionalWorkCommentLine2."Line Break" := ProvisionalWorkCommentLine."Line Break";
                   ProvisionalWorkCommentLine2.INSERT(TRUE);
                 UNTIL ProvisionalWorkCommentLine.NEXT = 0;
               END;
             END;

    OnDelete=VAR
               ProvisionalWorkDocGroupRec@1100485000 : Record 11012778;
               ProvisionalWorkContactRec@1100485001 : Record 11012779;
               ProvisionalWorkSpecDtlsRec@1100485002 : Record 11012776;
               ProvisionalWorkCommentLine@1100528700 : Record 11071940;
             BEGIN
               CALCFIELDS("Feature Checked");
               IF "Feature Checked" THEN
                 IF NOT CONFIRM(Text002, FALSE, Feature) THEN
                   ERROR('');

               ProvisionalWorkDocGroupRec.SETRANGE("Provisional Work No.", "Provisional Work No.");
               ProvisionalWorkDocGroupRec.SETRANGE("Feature Group", "Feature Group");
               ProvisionalWorkDocGroupRec.SETRANGE(Feature, Feature);
               ProvisionalWorkDocGroupRec.DELETEALL(TRUE);

               ProvisionalWorkContactRec.SETRANGE("Provisional Work No.", "Provisional Work No.");
               ProvisionalWorkContactRec.SETRANGE("Feature Group", "Feature Group");
               ProvisionalWorkContactRec.SETRANGE(Feature, Feature);
               IF ProvisionalWorkContactRec.COUNT > 0 THEN BEGIN
                 IF NOT CONFIRM(Text001, FALSE, Feature) THEN
                   ERROR('');
               END;
               ProvisionalWorkContactRec.DELETEALL(TRUE);

               ProvisionalWorkSpecDtlsRec.SETRANGE("Provisional Work No.", "Provisional Work No.");
               ProvisionalWorkSpecDtlsRec.SETRANGE("Feature Group", "Feature Group");
               ProvisionalWorkSpecDtlsRec.SETRANGE(Feature, Feature);
               ProvisionalWorkSpecDtlsRec.DELETEALL(TRUE);

               ProvisionalWorkCommentLine.SETRANGE("Table Name", ProvisionalWorkCommentLine."Table Name"::"Provisional Work Feature");
               ProvisionalWorkCommentLine.SETRANGE("Provisional Work No.", "Provisional Work No.");
               ProvisionalWorkCommentLine.SETRANGE("Feature Group", "Feature Group");
               ProvisionalWorkCommentLine.SETRANGE(Feature, Feature);
               ProvisionalWorkCommentLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Provisional Work Feature;
               NOR=Provisorisk arbeidsfunksjon;
               SVE=Provisorisk arbetsfunktion];
  }
  FIELDS
  {
    { 10  ;   ;Provisional Work No.;Code20        ;TableRelation="Provisional Work";
                                                   CaptionML=[ENU=Provisional Work No.;
                                                              NOR=Provisorisk arbeidsnr.;
                                                              SVE=Prelimin„rt arbetsnr] }
    { 30  ;   ;Feature Group       ;Text50        ;TableRelation="Contact Feature Group".Description WHERE (Provisional Works=CONST(Yes));
                                                   CaptionML=[ENU=Feature Group;
                                                              NOR=Funksjonsgruppe;
                                                              SVE=Funktionsgrupp] }
    { 40  ;   ;Feature             ;Code20        ;TableRelation="Contact Feature".Code WHERE (Feature Group=FIELD(Feature Group));
                                                   CaptionML=[ENU=Feature;
                                                              NOR=Funksjon;
                                                              SVE=Funktion] }
    { 50  ;   ;Feature Description ;Text50        ;CaptionML=[ENU=Feature Description;
                                                              NOR=Funksjonsbeskrivelse;
                                                              SVE=Funktionsbeskrivning] }
    { 60  ;   ;No. of Selected Vendors;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Provisional Work Contact" WHERE (Provisional Work No.=FIELD(Provisional Work No.),
                                                                                                       Feature Group=FIELD(Feature Group),
                                                                                                       Feature=FIELD(Feature)));
                                                   CaptionML=[ENU=No. of Selected Vendors;
                                                              NOR=Antall valgte leverand›rer;
                                                              SVE=Antal utvalda leverant”rer];
                                                   Editable=No }
    { 70  ;   ;No. of Vendors to Request;Integer  ;CaptionML=[ENU=No. of Vendors to Request;
                                                              NOR=Antall leverand›rer for foresp›rsel;
                                                              SVE=Antal leverant”rer f”r f”rfr†gan] }
    { 90  ;   ;Text                ;Text250       ;OnValidate=BEGIN
                                                                "Text Block" := Text <> '';
                                                              END;

                                                   CaptionML=[ENU=Text;
                                                              NOR=Tekst;
                                                              SVE=Ben„mning] }
    { 100 ;   ;Text Block          ;Boolean       ;FieldClass=Normal;
                                                   CaptionML=[ENU=Text Block;
                                                              NOR=Tekstblokk;
                                                              SVE=Textblock];
                                                   Editable=No }
    { 110 ;   ;Feature Checked     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Provisional Work Spec. Dtls." WHERE (Provisional Work No.=FIELD(Provisional Work No.),
                                                                                                           Feature Group=FIELD(Feature Group),
                                                                                                           Feature=FIELD(Feature),
                                                                                                           Checked=CONST(Yes)));
                                                   CaptionML=[ENU=Feature Checked;
                                                              NOR=Funksjon kontrollert;
                                                              SVE=Funktion kontrollerad];
                                                   Editable=No }
    { 120 ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Provisional Work Comment Line" WHERE (Table Name=CONST(Provisional Work Feature),
                                                                                                            Provisional Work No.=FIELD(Provisional Work No.),
                                                                                                            Feature Group=FIELD(Feature Group),
                                                                                                            Feature=FIELD(Feature)));
                                                   CaptionML=[ENU=Comment;
                                                              NOR=Kommentar;
                                                              SVE=Kommentar];
                                                   Editable=No }
    { 200 ;   ;Selected            ;Boolean       ;CaptionML=[ENU=Selected;
                                                              NOR=Valgt;
                                                              SVE=Vald] }
  }
  KEYS
  {
    {    ;Provisional Work No.,Feature Group,Feature;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=There are vendors selected for feature %1. Delete feature?;NOR=Leverandr›rer er valgt for funksjon %1. Fjerne funksjon?;SVE=Det finns leverant”rer markerade f”r funktion %1. Ta bort funktionen?';
      Text002@1210190001 : TextConst 'ENU=Feature is in use %1. Delete feature?;NOR=Funksjon er i bruk %1. Fjerne funksjon?;SVE=Funktionen „r i bruk %1. Ta bort funktionen?';

    PROCEDURE EditComments@1100528700();
    VAR
      ProvisionalWorkCommentSheet@1100528700 : Page 11020652;
      ProvisionalWorkCommentLine@1100528701 : Record 11071940;
    BEGIN
      IF ("Provisional Work No." = '') OR
         ("Feature Group" = '') OR
         (Feature = '') THEN
        EXIT;

      ProvisionalWorkCommentLine.SETRANGE("Table Name", ProvisionalWorkCommentLine."Table Name"::"Provisional Work Feature");
      ProvisionalWorkCommentLine.SETRANGE("Provisional Work No.", "Provisional Work No.");
      ProvisionalWorkCommentLine.SETRANGE("Feature Group", "Feature Group");
      ProvisionalWorkCommentLine.SETRANGE(Feature, Feature);
      ProvisionalWorkCommentSheet.SETTABLEVIEW(ProvisionalWorkCommentLine);
      ProvisionalWorkCommentSheet.RUNMODAL;
    END;

    PROCEDURE DrillDownSelectedVendors@1100528701();
    VAR
      ProvisionalWorkContacts@1100528701 : Page 11020645;
      ProvisionalWorkContact@1100528700 : Record 11012779;
    BEGIN
      ProvisionalWorkContact.FILTERGROUP(10);
      ProvisionalWorkContact.SETRANGE("Provisional Work No.", "Provisional Work No.");
      ProvisionalWorkContact.SETRANGE("Feature Group", "Feature Group");
      ProvisionalWorkContact.SETRANGE(Feature, Feature);
      ProvisionalWorkContact.FILTERGROUP(0);
      ProvisionalWorkContacts.SETTABLEVIEW(ProvisionalWorkContact);
      ProvisionalWorkContacts.RUNMODAL;
    END;

    PROCEDURE AssistEditSelectedVendors@1100528702();
    VAR
      FilterContactsProvWork@1100528700 : Page 11020646;
    BEGIN
      FilterContactsProvWork.SetParameters("Provisional Work No.", "Feature Group", Feature);
      FilterContactsProvWork.LOOKUPMODE(TRUE);
      FilterContactsProvWork.RUNMODAL;
    END;

    PROCEDURE SelectAllFeatures@1100528703();
    VAR
      ProvisionalWorkFeature@1100528701 : Record 11012777;
    BEGIN
      ProvisionalWorkFeature.SETRANGE("Provisional Work No.", "Provisional Work No.");
      IF ProvisionalWorkFeature.FINDSET(TRUE) THEN
        REPEAT
          ProvisionalWorkFeature.VALIDATE(Selected, TRUE);
          ProvisionalWorkFeature.MODIFY(TRUE);
        UNTIL ProvisionalWorkFeature.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

