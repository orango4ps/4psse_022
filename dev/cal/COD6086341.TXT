OBJECT Codeunit 6086341 Mileage Inbox-Process
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.05;
  }
  PROPERTIES
  {
    TableNo=6086353;
    OnRun=BEGIN
            MileageInbox.COPY(Rec);
            Code;
            Rec := MileageInbox;
          END;

  }
  CODE
  {
    VAR
      MileageInbox@1160040004 : Record 6086353;
      MilInboxCheck@1160040001 : Codeunit 6086342;

    LOCAL PROCEDURE Code@1160040000();
    VAR
      Mileage@1160040004 : Record 6086338;
      MilValidate@1160040000 : Codeunit 6086345;
    BEGIN
      WITH MileageInbox DO BEGIN
        IF Status = Status::Accepted THEN
          EXIT;

        CleanupPreviousInboxEntries(MileageInbox);
        COMMIT;

        IF NOT MilInboxCheck.RUN(MileageInbox) THEN BEGIN
          "Error Text" := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN("Error Text"));
          Status := Status::Error;

          IF "Entry No." = 0 THEN BEGIN
            Mileage.SETCURRENTKEY("Mileage GUID");
            Mileage.SETRANGE("Mileage GUID","Mileage GUID");
            IF Mileage.FINDFIRST THEN
              "Mileage Entry No." := Mileage."Entry No.";
          END;
        END ELSE BEGIN
          "Mileage Entry No." := MilInboxCheck.GetMileageEntryNo;
          "Error Text" := '';
          Status := Status::Accepted;
        END;

        "Processed Date/Time" := CURRENTDATETIME;
        "Processed by User ID" := USERID;
        MODIFY;

        IF "Mileage Entry No." <> 0 THEN BEGIN
          Mileage.GET("Mileage Entry No.");
          MilValidate.RUN(Mileage);
        END;

      END;
    END;

    PROCEDURE CleanupPreviousInboxEntries@1160040006(CurrMilInbox@1160040000 : Record 6086353);
    VAR
      MilInbox@1160040001 : Record 6086353;
    BEGIN
      //FORMER ENTRIES MIGHT BE IN ERRORS WHICH WERE ALREADY CORRECTED.
      MilInbox.SETCURRENTKEY("Mileage GUID");
      MilInbox.SETRANGE("Mileage GUID",CurrMilInbox."Mileage GUID");
      MilInbox.SETFILTER("Entry No.",'<%1',CurrMilInbox."Entry No.");
      MilInbox.SETFILTER(Status,'%1|%2',MilInbox.Status::Pending,MilInbox.Status::Error);
      MilInbox.SETRANGE("Mileage Completed",FALSE);
      MilInbox.MODIFYALL(Status,MilInbox.Status::Accepted);
    END;

    BEGIN
    END.
  }
}

