OBJECT Report 11020259 Import Shpmnt Package Tracking
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 110=m;
    CaptionML=[DEU=Lieferungspaketimport Nachverfolgung;
               ENU=Import Shipment Package Tracking;
               NLD=Importeer traceringsnummer verzending;
               NOR=Importer Pakkesporing for Forsendelse];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  FileMgt@1100528704 : Codeunit 419;
                  ImportFile@1100528703 : File;
                  ServerFileName@1100528702 : Text[1024];
                  LineNo@1100528705 : Integer;
                  Line@1100528701 : Text[1024];
                  SplittedArr@1100528700 : ARRAY [10] OF Text[250];
                BEGIN
                  IF FileName = '' THEN
                    CurrReport.QUIT;

                  InitErrorMessages;

                  ServerFileName := FileMgt.UploadFileSilent(FileName);

                  ImportFile.TEXTMODE := TRUE;
                  ImportFile.WRITEMODE := FALSE;
                  ImportFile.OPEN(ServerFileName);

                  WHILE ImportFile.POS < ImportFile.LEN DO BEGIN
                    LineNo := LineNo + 1;

                    ImportFile.READ(Line);

                    CASE TRUE OF
                      (LineNo > 1) AND (ShippingAgent = ShippingAgent::GLS):
                        BEGIN
                          StringFunctions.SplitString(Line, ';', SplittedArr);
                          IF NOT UpdatePackageTracking(SplittedArr[3], SplittedArr[1]) THEN
                            StoreErrorMessage(STRSUBSTNO(Text004, SplittedArr[3]));
                        END;
                    END;
                  END;

                  ImportFile.CLOSE;

                  ERASE(ServerFileName);
                END;

    OnPostReport=BEGIN
                   ShowErrorMessages;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100528701;;Container;
                  ContainerType=ContentArea }

      { 1100528702;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Opties;
                             NLD=Options;
                             NOR=Alternativer];
                  GroupType=Group }

      { 1100528700;2;Field  ;
                  CaptionML=[DEU=Dateiname;
                             ENU=File Name;
                             NLD=Bestandsnaam;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=FileName;
                  OnAssistEdit=VAR
                                 FileManagement@1100528400 : Codeunit 419;
                                 FilterString@1100528401 : Text;
                               BEGIN
                                 FilterString := FileManagement.GetToFilterText('', '.csv');
                                 FileName := FileManagement.OpenFileDialog('', FileName, FilterString);
                               END;
                                }

      { 1100528703;2;Field  ;
                  CaptionML=[DEU=Zusteller;
                             ENU=Shipping Agent;
                             NLD=Vrachtvervoerder;
                             NOR=Transport›r;
                             SVE=Spedit”r];
                  OptionCaptionML=[DEU=GLS;
                                   ENU=GLS;
                                   NLD=GLS;
                                   NOR=GLS;
                                   SVE=GLS];
                  SourceExpr=ShippingAgent }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      FileName@1100528701 : Text[250];
      StringFunctions@1100528700 : Codeunit 11012273;
      ShippingAgent@1100528703 : 'GLS';
      Text004@1100528709 : TextConst 'DEU=Unbekannte Referenz %1.;ENU=Unknown reference %1.;NLD=Onbekende referentie %1.;NOR=Ukjent referanse %1';

    PROCEDURE UpdatePackageTracking@1100528700(DocumentNo@1100528701 : Code[20];PackageTrackingNo@1100528702 : Text[30]) : Boolean;
    VAR
      SalesShipmentHeader@1100528700 : Record 110;
    BEGIN
      IF SalesShipmentHeader.GET(DocumentNo) THEN BEGIN
        SalesShipmentHeader."Package Tracking No." := PackageTrackingNo;
        SalesShipmentHeader.MODIFY;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE InitErrorMessages@1100528701();
    VAR
      Error@1100528700 : Record 11012051;
    BEGIN
      Error.SETRANGE("User ID", USERID);
      Error.SETRANGE("Source Type", Error."Source Type"::"Read Documents");
      Error.DELETEALL;
    END;

    LOCAL PROCEDURE StoreErrorMessage@5(ErrorMessage@11012001 : Text[250]);
    VAR
      Error@1210190000 : Record 11012051;
      LineNo@11012002 : Integer;
    BEGIN
      Error.SETRANGE("User ID", USERID);
      Error.SETRANGE("Source Type", Error."Source Type"::"Read Documents");
      IF Error.FINDLAST THEN
        LineNo := Error."Line No." + 1
      ELSE
        LineNo := 1;

      Error.INIT;
      Error."User ID" := USERID;
      Error."Source Type" := Error."Source Type"::"Read Documents";
      Error."Line No." := LineNo;
      Error."Error message" := ErrorMessage;
      Error.INSERT;
    END;

    PROCEDURE ShowErrorMessages@1100528702();
    VAR
      Error@1100528700 : Record 11012051;
    BEGIN
      Error.SETRANGE("User ID", USERID);
      Error.SETRANGE("Source Type", Error."Source Type"::"Read Documents");
      IF Error.FINDFIRST THEN BEGIN
        COMMIT;
        PAGE.RUNMODAL(PAGE::Errors, Error);
      END;
    END;

    BEGIN
    {
      Attention:
      At the request of a customer SplittedArr[8] is replaced by SplittedArr[1]. As a result of this parcel number(1st column in CSV-file)
      will be taken in stead of Track ID (8th column in CSV-file).
    }
    END.
  }
  RDLDATA
  {
  }
}

