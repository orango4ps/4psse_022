OBJECT Report 11012135 WKA - Data Request Process
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=öberpr. Steuer- und Sozialversicherungspf - Datenanfragenvorgang;
               ENU=WKA - Data Request Process;
               NLD=WKA - Gegevensaanvraag proces;
               NOR=WKA Õ behandling av dataforespõrsel;
               SVE=WKA Õ behandling av datafîrfrÜgan];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   SetApplyVendorFilterToOtherCompaniesEditable;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1100525000;;DataItem;                  ;
               DataItemTable=Table2000000006;
               DataItemTableView=SORTING(Name);
               OnPreDataItem=BEGIN
                               CompanyFilters.COPYFILTERS(Company);
                               SETRANGE(Name, COMPANYNAME);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF ApplyVendorFilterToOtherCompanies THEN BEGIN
                                    Company2.COPYFILTERS(CompanyFilters);
                                    Company2.FINDSET;
                                    REPEAT
                                      TmpCompany3.Name := Company2.Name;
                                      TmpCompany3.INSERT;
                                    UNTIL Company2.NEXT = 0;
                                  END;

                                  IF NOT TmpCompany3.GET(COMPANYNAME) THEN BEGIN
                                    TmpCompany3.Name := COMPANYNAME;
                                    TmpCompany3.INSERT;
                                  END;
                                END;

               ReqFilterFields=Name }

    { 3182;1   ;DataItem;                    ;
               DataItemTable=Table23;
               DataItemTableView=SORTING(No.)
                                 WHERE(WKA Check Not Active=CONST(No));
               OnPreDataItem=VAR
                               Vendor2@1100525000 : Record 23;
                             BEGIN
                               VendorFilters.COPYFILTERS(Vendor);
                               Vendor.RESET;

                               IF TmpCompany3.FINDSET THEN BEGIN
                                 REPEAT
                                   Vendor2.CHANGECOMPANY(TmpCompany3.Name);
                                   Vendor2.COPYFILTERS(VendorFilters);
                                   IF Vendor2.FINDSET THEN
                                     REPEAT
                                       IF (Vendor2."Subcontractor Type" IN [Vendor2."Subcontractor Type"::Subcontractor,Vendor2."Subcontractor Type"::"Self Employed",Vendor2."Subcontractor Type"::"Employment Agency"])  OR Vendor2.ACL THEN BEGIN
                                         IF Vendor.GET(Vendor2."No.") THEN
                                           Vendor.MARK(TRUE);
                                       END;
                                     UNTIL Vendor2.NEXT = 0
                                 UNTIL TmpCompany3.NEXT = 0;
                                 MARKEDONLY(TRUE);
                               END;
                             END;

               OnAfterGetRecord=VAR
                                  Vendor2@1100528402 : Record 23;
                                  TmpReportSelections@1100525003 : TEMPORARY Record 77;
                                  TempEmailAttachment@1100528403 : TEMPORARY Record 11229294;
                                  DocumentMailing@1100528405 : Codeunit 260;
                                  DocumentPrint@1100525002 : Codeunit 229;
                                  DataPrintedForVendor@1100528404 : Boolean;
                                  MailTemplateNo@1100525004 : Integer;
                                BEGIN
                                  MailTemplateNo := 90;
                                  Vendor2.COPY(Vendor);
                                  Vendor2.SETRECFILTER;
                                  IF ("WKA Data Request by E-mail") AND ("E-Mail for WKA Data Request" <> '') THEN BEGIN
                                    DocumentPrint.GetReportSelectionForEmailAttachmentUsageVendor(
                                      6005, '', FALSE, TmpReportSelections);
                                    StoreReportParameters(TmpReportSelections."Report ID");
                                    IF TmpReportSelections.SaveAnyRecordReportAsPdf(
                                      Vendor2, MailTemplateNo, Vendor2."Global Dimension 1 Code",
                                      TmpReportSelections."Report ID", FALSE, FALSE, Vendor2.TABLECAPTION, Vendor2."No.", TempEmailAttachment)
                                    THEN
                                      DataPrintedForVendor := TRUE
                                    ELSE
                                      DataPrintedForVendor := FALSE;
                                    ReportParameterManagement.FlushReportParameters(TmpReportSelections."Report ID");
                                    IF NOT DataPrintedForVendor THEN
                                      CurrReport.SKIP;

                                    TmpReportSelections.AddStandardAttachments(
                                      TempEmailAttachment,
                                      MailTemplateNo,
                                      Vendor2."Global Dimension 1 Code",
                                      Vendor."Language Code");

                                    COMMIT;
                                    DocumentMailing.SetTempEmailAttachments(
                                      TempEmailAttachment);
                                    IF TempEmailAttachment.FINDFIRST THEN;
                                    DocumentMailing.SetGlobalLanguage(Vendor."Language Code");
                                    DocumentMailing.EmailFile4PS(
                                      TempEmailAttachment."Attachment Server File Path",
                                      '',
                                      '',
                                      Vendor."No.",
                                      Vendor."E-Mail for WKA Data Request",
                                      MailTemplateNo,
                                      Vendor."Global Dimension 1 Code",
                                      Vendor."Language Code",
                                      Vendor,
                                      '',
                                      Text000,
                                      TRUE);
                                    DocumentMailing.ResetGlobalLanguage;
                                  END ELSE BEGIN
                                    DocumentPrint.GetReportSelectionForPrintUsageVendor(
                                      6005, '', FALSE, TmpReportSelections);
                                    StoreReportParameters(TmpReportSelections."Report ID");
                                    REPORT.RUNMODAL(TmpReportSelections."Report ID", FALSE, FALSE, Vendor2);
                                    ReportParameterManagement.FlushReportParameters(TmpReportSelections."Report ID");
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Vendor.COPYFILTERS(VendorFilters);
                              END;

               ReqFilterFields=No.,Blocked,Balance }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               DueDate := WORKDATE;
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100485000;2;Field  ;
                  CaptionML=[DEU=FÑlligkeitsdatum;
                             ENU=Due Date;
                             NLD=Vervaldatum;
                             NOR=Forfallsdato;
                             SVE=Fîrfallodatum];
                  ToolTipML=[DEU=Hier kînnen Sie das F†lligkeitsdatum auswÑhlen. Alle öberprÅfungen der Steuer- und Sozialverplichungen, mit einem Ñlteren FÑlligkeitsdatum als hier ausgewÑlht, werden gedruckt.;
                             ENU=You can select a Due Date here. All WKA Checks with a Due Date prior to the selected Due Date are printed.;
                             NLD=Hier kunt u de vervaldatum selecteren. Alle WKA -controles met een oudere vervaldatum dan de hier geselecteerde worden afgedrukt.;
                             NOR=Du kan velge en forfallsdato her. Alle WKA-kontroller med en forfallsdato som inntreffer tidligere enn valgt forfallsdato skrives ut.;
                             SVE=Du kan vÑlja ett fîrfallodatum hÑr. All WKA-kontroller med ett fîrfallodatum som infaller tidigare Ñn valt fîrfallodatum skrivs ut.];
                  SourceExpr=DueDate }

      { 1100485002;2;Field  ;
                  CaptionML=[DEU=Ignorieren FÑlligkeitsdatum Ñlter als;
                             ENU=Ignore Due Date Older Than;
                             NLD=Negeer vervaldatum ouder dan;
                             NOR=Ignorer forfallsdato eldre enn;
                             SVE=Ignorera fîrfallodatum Ñldre Ñn];
                  SourceExpr=IgnoreDueDateOlderThan }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=Kreditorenfilter in anderen Unternehmen anwenden;
                             ENU=Apply Vendor Filter in other Companies;
                             NLD=Leveranciersfilter toepassen in overige bedrijven];
                  SourceExpr=ApplyVendorFilterToOtherCompanies;
                  Editable=ApplyVendorFilterToOtherCompaniesEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Company2@1100525005 : Record 2000000006;
      TmpCompany3@1100525000 : TEMPORARY Record 2000000006;
      VendorFilters@1100525002 : Record 23;
      CompanyFilters@1100525004 : Record 2000000006;
      ReportParameterManagement@1100528503 : Codeunit 11012356;
      DueDate@1100485002 : Date;
      IgnoreDueDateOlderThan@1100485003 : Date;
      ApplyVendorFilterToOtherCompanies@1100525001 : Boolean INDATASET;
      ApplyVendorFilterToOtherCompaniesEditable@1100525003 : Boolean INDATASET;
      Text000@1100525006 : TextConst 'DEU=WKA-Datenanfrage;ENU=WKA Data Request;NLD=WKA Gegevens Aanvraag;NOR=WKA-dataforespõrsel';

    LOCAL PROCEDURE StoreReportParameters@1100528400(ReportID@1100525000 : Integer);
    BEGIN
      ReportParameterManagement.StoreReportParameter(
        ReportID, 'DueDate', DueDate, 0);
      ReportParameterManagement.StoreReportParameter(
        ReportID, 'IgnoreDueDateOlderThan', IgnoreDueDateOlderThan, 0);
    END;

    PROCEDURE SetApplyVendorFilterToOtherCompaniesEditable@1100525004();
    VAR
      MarketingSetup@1100525000 : Record 5079;
    BEGIN
      MarketingSetup.GET;
      ApplyVendorFilterToOtherCompaniesEditable := MarketingSetup.Vendors;
    END;

    BEGIN
    {
      4PS01 JD 9 jan 2007 call 7371, execute WKA check using due date
    }
    END.
  }
  RDLDATA
  {
  }
}

