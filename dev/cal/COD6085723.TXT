OBJECT Codeunit 6085723 Purch. Alloc.-Reverse
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.5.00,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=6085730;
    Permissions=TableData 6085730=im,
                TableData 6085731=i;
    OnRun=VAR
            NewPurchAllocHeader@161024014 : Record 6085730;
            PurchAllocLine@161024013 : Record 6085731;
            NewPurchAllocLine@161024012 : Record 6085731;
            PurchHeader@161024016 : Record 38;
            DCSetup@1160040002 : Record 6085573;
            PostingDate@161024015 : Date;
          BEGIN
            CLEARALL;

            DCSetup.GET;

            PurchAllocHeader.COPY(Rec);

            WITH PurchAllocHeader DO BEGIN
              TESTFIELD(Status,Status::Posted);

              IF DCSetup."Rev. Purch.Alloc. Posting Date" =
                DCSetup."Rev. Purch.Alloc. Posting Date"::"Original purch. allocation posting date"
              THEN BEGIN
                IF GenJnlCheckLine.DateNotAllowed("Posting Date") THEN BEGIN
                  PurchHeader.GET("Document Type","Document No.");
                  IF GenJnlCheckLine.DateNotAllowed(PurchHeader."Posting Date") THEN
                    PurchHeader.FIELDERROR("Posting Date",Text003)
                  ELSE
                    PostingDate := PurchHeader."Posting Date";
                END ELSE
                  PostingDate := "Posting Date";
              END ELSE BEGIN
                PurchHeader.GET("Document Type","Document No.");
                IF GenJnlCheckLine.DateNotAllowed(PurchHeader."Posting Date") THEN
                  PurchHeader.FIELDERROR("Posting Date",Text003)
                ELSE
                  PostingDate := PurchHeader."Posting Date";
              END;

              NewPurchAllocHeader := PurchAllocHeader;
              NewPurchAllocHeader."No." := '';
              NewPurchAllocHeader."Posting Date" := PostingDate;
              NewPurchAllocHeader.Status := NewPurchAllocHeader.Status::Open;
              NewPurchAllocHeader."Applies-to Doc. No." := PurchAllocHeader."No.";
              NewPurchAllocHeader.SetSkipCopyDim(TRUE);
              NewPurchAllocHeader.INSERT(TRUE);

              PurchAllocLine.SETRANGE("Document No.",PurchAllocHeader."No.");
              IF PurchAllocLine.FINDSET THEN
                REPEAT
                  NewPurchAllocLine := PurchAllocLine;
                  NewPurchAllocLine."Document No." := NewPurchAllocHeader."No.";
                  NewPurchAllocLine.Quantity := - PurchAllocLine.Quantity; //**4PS
                  NewPurchAllocLine.VALIDATE(Amount,-PurchAllocLine.Amount);
                  NewPurchAllocLine.VALIDATE("Amount Including VAT",-PurchAllocLine."Amount Including VAT");
                  NewPurchAllocLine.INSERT;

                  NewPurchAllocLine.TESTFIELD(Amount,-PurchAllocLine.Amount);
                  NewPurchAllocLine.TESTFIELD("Amount Including VAT",-PurchAllocLine."Amount Including VAT");
                UNTIL PurchAllocLine.NEXT = 0;

              CODEUNIT.RUN(CODEUNIT::"Purch. Alloc.-Post",NewPurchAllocHeader);
              NewPurchAllocHeader.Status := Status::Reversed;
              NewPurchAllocHeader.MODIFY;

              "Applies-to Doc. No." := NewPurchAllocHeader."No.";
              Status := Status::Reversed;
              MODIFY;
            END;

            Rec := PurchAllocHeader;
          END;

  }
  CODE
  {
    VAR
      PurchAllocHeader@1070000 : Record 6085730;
      Text003@161024012 : TextConst 'DEU=liegt nicht innerhalb des zugelassenen Buchungszeitraums.;ENU=is not within your range of allowed posting dates;NLD=is geen geldige boekingsdatum. De datum valt buiten het toegestane bereik.;NOR=er ikke innenfor tillatte bokf›ringsdatoer;SVE=„r inte i ditt till†tna intervall f”r bokf”ringsdatum';
      GenJnlCheckLine@161024013 : Codeunit 11;

    BEGIN
    END.
  }
}

