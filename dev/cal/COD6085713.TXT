OBJECT Codeunit 6085713 Purch./Sales Doc.-Val. G/L Acc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    TableNo=6085593;
    OnRun=VAR
            GLAccount@161024012 : Record 15;
            Document@161024016 : Record 6085590;
            Template@161024017 : Record 6085579;
            DataTransl@161024013 : Record 6085584;
            TemplField@161024014 : Record 6085580;
            CaptureMgt@1160040000 : Codeunit 6085576;
          BEGIN
            IF NOT "Updated By User" THEN
              EXIT;

            IF "Value (Text)" = '' THEN
              EXIT;

            IF NOT GLAccount.GET("Value (Text)") THEN BEGIN
              GLAccount.SETCURRENTKEY("Search Name");
              GLAccount.SETFILTER("Search Name",'@' + CaptureMgt.ReplaceIllegalFilterCharacters("Value (Text)",FALSE,0) + '*');
              IF GLAccount.FINDFIRST THEN BEGIN
                "Value (Text)" := GLAccount."No.";
              END ELSE
                GLAccount.GET("Value (Text)");
            END;
            GLAccount.TESTFIELD("Direct Posting");

            TemplField.SETRANGE("Template No.","Template No.");
            TemplField.SETRANGE(Type,TemplField.Type::Header);
            TemplField.SETRANGE("G/L Account Field Code",Code);
            IF NOT TemplField.FINDFIRST THEN
              ERROR(Text003,TemplField.FIELDCAPTION("G/L Account Field Code"));

            IF DataTransl.GET(TemplField."Template No.",TemplField.Type,TemplField.Code) THEN BEGIN
              IF (DataTransl."Translate to Type" = DataTransl."Translate to Type"::"G/L Account") AND
                (DataTransl."Translate to No." = GLAccount."No.")
              THEN
                EXIT;
            END;

            IF Code = 'GLACCOUNTNO' THEN BEGIN
              Document.GET("Document No.");
              Template.GET(Document."Template No.");
              IF (Template."Purch. Inv. Reg. Step 1" = Template."Purch. Inv. Reg. Step 1"::"Match & Update Order") OR
                (Template."Purch. Cr.Memo Reg. Step 1" = Template."Purch. Cr.Memo Reg. Step 1"::"Match & Update Return Order")
              THEN
                EXIT;
            END;

            IF NOT CONFIRM(Text001,TRUE,GLAccount.TABLECAPTION,GLAccount."No.",GLAccount.Name,TemplField."Field Name") THEN
              EXIT;

            REPEAT
              DataTransl.SETRANGE("Template No.",TemplField."Template No.");
              DataTransl.SETRANGE(Type,TemplField.Type);
              DataTransl.SETRANGE("Field Code",TemplField.Code);

              IF NOT DataTransl.FINDFIRST THEN BEGIN
                DataTransl.INIT;
                DataTransl."Template No." := TemplField."Template No.";
                DataTransl.Type := TemplField.Type;
                DataTransl."Field Code" := TemplField.Code;
                DataTransl.INSERT(TRUE);
              END;
              DataTransl.VALIDATE("Translate to Type",DataTransl."Translate to Type"::"G/L Account");
              DataTransl.VALIDATE("Translate to No.","Value (Text)");
              DataTransl.MODIFY(TRUE);
            UNTIL TemplField.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      Text001@161024012 : TextConst 'DEU=Mîchten Sie %1 %2 (%3) als Standardkonto fÅr ''%4''einstellen?;ENU=Do you want to configure %1 %2 (%3) as the default account for ''%4''?;NLD=Wilt u %1 %2 (%3) configureren als de standaardrekening voor ''%4''?';
      Text003@161024014 : TextConst 'DEU=Dieses Kontofeld ist nicht mit einem Betragsfeld verknÅpft.\\Dieser Link wird erstellt, indem ''%1'' im Betragsvorlagenfeld ausgewÑhlt wird.;ENU=This Account-field is not linked to an Amount-field.\\This link is created by selection ''%1'' on the Amount Template Field.;NLD=Dit rekeningveld is niet gekoppeld aan een bedragveld.\\Deze koppeling komt tot stand door in het bedragsjabloonveld ''%1'' te selecteren.';

    BEGIN
    END.
  }
}

