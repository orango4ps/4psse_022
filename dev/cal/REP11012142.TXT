OBJECT Report 11012142 Update Budget Rates
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aktualisieren Budgettarife;
               ENU=Update Budget Rates;
               NLD=Actualiseren begrotingstarieven;
               NOR=Aktualiser budsjettsats;
               SVE=F”rverkliga budgetavgifter];
    ProcessingOnly=Yes;
    OnPostReport=VAR
                   lBudgetSummaryRec@1100485000 : Record 11012064;
                 BEGIN
                   TempExtRec.SETRANGE("Budget Fixed", FALSE);
                   IF TempExtRec.FINDSET(FALSE,FALSE) THEN
                     REPEAT
                       lBudgetSummaryRec.RESET;
                       lBudgetSummaryRec.SETRANGE("Project No.", TempExtRec."Project No.");
                       lBudgetSummaryRec.SETRANGE("Contract No.", TempExtRec."Contract No.");
                       lBudgetSummaryRec.SETFILTER("Plot No.", '%1', '');
                       IF lBudgetSummaryRec.FINDFIRST THEN BEGIN
                         lBudgetSummaryRec.ReCalcSummaryLines('');
                         lBudgetSummaryRec.UpdateBudgetLines(FALSE,'');
                       END;
                     UNTIL TempExtRec.NEXT =0;

                   TempProjRec.SETRANGE("Budget Fixed", FALSE);
                   IF TempProjRec.FINDSET(FALSE,FALSE) THEN
                     REPEAT
                       lBudgetSummaryRec.RESET;
                       lBudgetSummaryRec.SETRANGE("Project No.", TempProjRec."No.");
                       lBudgetSummaryRec.SETFILTER("Contract No.", '%1', '');
                       lBudgetSummaryRec.SETFILTER("Plot No.", '%1', '');
                       IF lBudgetSummaryRec.FINDFIRST THEN BEGIN
                         lBudgetSummaryRec.ReCalcSummaryLines('');
                         lBudgetSummaryRec.UpdateBudgetLines(FALSE,'');
                       END;
                     UNTIL TempProjRec.NEXT =0;
                 END;

  }
  DATASET
  {
    { 5115;    ;DataItem;                    ;
               DataItemTable=Table11012001;
               DataItemTableView=SORTING(Project No.,Adjustment,Extension Contract,Option,Line No.)
                                 WHERE(Plot No.=CONST(),
                                       Option=CONST());
               OnPreDataItem=BEGIN
                               Status.OPEN(Text001);
                               NoOfLines := COUNT;
                               StatusCounter := 0;
                               //"Budget Line".SETFILTER("Plot No.", '%1', '') ;  act. plot-options via R11012532 (see WHERE in DataItemTableView)
                             END;

               OnAfterGetRecord=BEGIN
                                  StatusCounter := StatusCounter+1;
                                  Status.UPDATE(1,ROUND(StatusCounter/NoOfLines*10000,1));

                                  IF "Rate Code" <> '' THEN BEGIN
                                    IF ("Project No." <> ProjRec."No.") THEN BEGIN
                                      ProjRec.GET("Project No.");
                                      IF UpdateVersionDate AND (NOT ProjRec."Budget Fixed") THEN BEGIN
                                        ProjRec."Version Date" := "Version Date";
                                        ProjRec.MODIFY;
                                      END;
                                      TempProjRec := ProjRec;
                                      IF TempProjRec.INSERT THEN;
                                    END;

                                    IF (Adjustment <> '') AND
                                       (("Project No." <> AdjRec."Project No.") OR (Adjustment <> AdjRec."No."))
                                    THEN BEGIN
                                      AdjRec.GET("Project No.", Adjustment);
                                      IF UpdateVersionDate AND (NOT AdjRec."Budget Correction Fixed") THEN BEGIN
                                        AdjRec."Version Date" := "Version Date";
                                        AdjRec.MODIFY;
                                      END;
                                    END;

                                    IF ("Extension Contract" <> '') AND
                                       (("Project No." <> ExtRec."Project No.") OR ("Extension Contract" <> ExtRec."Contract No."))
                                    THEN BEGIN
                                      ExtRec.GET("Project No.", "Extension Contract");
                                      IF UpdateVersionDate AND (NOT ExtRec."Budget Fixed") THEN BEGIN
                                        ExtRec."Version Date" := "Version Date";
                                        ExtRec.MODIFY;
                                      END;
                                      TempExtRec := ExtRec;
                                      IF TempExtRec.INSERT THEN;
                                    END;

                                    IF NOT SkipFixedBudget(Adjustment, "Extension Contract") THEN BEGIN
                                      IF UpdateVersionDate THEN
                                        "Version Date" := TODAY;
                                      FillElementPhaseCodeBasedOnElement;
                                      VALIDATE("Rate Code");
                                      MODIFY(TRUE);
                                    END;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Status.CLOSE;
                              END;

               ReqFilterFields=Project No.,Adjustment,Extension Contract,Option,Version Date }

    { 8711;    ;DataItem;                    ;
               DataItemTable=Table11012064;
               DataItemTableView=SORTING(Project No.,Contract No.,Plot No.,House Model,Main Group,Group,Sub Group,Option,Row No.);
               OnPreDataItem=BEGIN
                               Status.OPEN(Text002);

                               IF "Budget Line".GETFILTER(Adjustment) <> '' THEN
                                 IF "Budget Line".GETRANGEMIN(Adjustment) > '' THEN
                                   CurrReport.BREAK;

                               "Budget Line".COPYFILTER("Project No.", "Budget Summary Sheet"."Project No.");
                               "Budget Line".COPYFILTER("Extension Contract", "Budget Summary Sheet"."Contract No.");
                               "Budget Line".SETFILTER("Plot No.", '%1', '');
                               "Budget Line".SETFILTER("House Model", '%1', '');
                               "Budget Line".SETFILTER("Main Group", '%1', '');
                               "Budget Line".SETFILTER(Group, '%1', '');
                               "Budget Line".SETFILTER("Sub Group", '%1', '');
                               "Budget Line".SETFILTER(Option, '%1', '');
                               "Budget Line".COPYFILTER("Cost Type", "Budget Summary Sheet"."Cost Type");
                               "Budget Line".COPYFILTER("Cost Object", "Budget Summary Sheet"."Cost Object");
                               "Budget Line".COPYFILTER("Cost Component", "Budget Summary Sheet"."Cost Component");
                               "Budget Line".COPYFILTER("Version Date", "Budget Summary Sheet"."Version Date");
                               "Budget Line".COPYFILTER(Element, "Budget Summary Sheet".Element);

                               NoOfLines := COUNT;
                               StatusCounter := 0;

                               ProjRec.INIT;
                               ProjRec."No." := '';

                               ExtRec.INIT;
                               ExtRec."Project No." := '';
                               ExtRec."Contract No." := '';

                               OptRec.INIT;
                               OptRec."Project No." := '';
                               OptRec."Plot No." := '';
                               OptRec."House Model" := '';
                               OptRec."Main Group" := '';
                               OptRec.Group := '';
                               OptRec."Sub Group" := '';
                               OptRec.Option := '';
                             END;

               OnAfterGetRecord=BEGIN
                                  StatusCounter := StatusCounter+1;
                                  Status.UPDATE(1,ROUND(StatusCounter/NoOfLines*10000,1));

                                  IF TempProjRec.GET("Project No.") THEN BEGIN
                                    //dit is een poging om selectie op andere punten dan de gecopieerde filters af te vangen

                                    IF "Rate Code" <> '' THEN BEGIN
                                      IF ("Project No." <> ProjRec."No.") THEN BEGIN
                                        ProjRec.GET("Project No.");
                                        IF UpdateVersionDate AND (NOT ProjRec."Budget Fixed") THEN BEGIN
                                          ProjRec."Version Date" := "Version Date";
                                          ProjRec.MODIFY;
                                        END;
                                        TempProjRec := ProjRec;
                                        IF TempProjRec.INSERT THEN;
                                      END;

                                      IF ("Contract No." <> '') AND
                                         (("Project No." <> ExtRec."Project No.") OR ("Contract No." <> ExtRec."Contract No."))
                                      THEN BEGIN
                                        ExtRec.GET("Project No.", "Contract No.");
                                        IF UpdateVersionDate AND (NOT ExtRec."Budget Fixed") THEN BEGIN
                                          ExtRec."Version Date" := "Version Date";
                                          ExtRec.MODIFY;
                                        END;
                                        TempExtRec := ExtRec;
                                        IF TempExtRec.INSERT THEN;
                                      END;

                                      IF NOT SkipFixedBudget('', "Contract No.") THEN BEGIN
                                        IF UpdateVersionDate THEN
                                          "Version Date" := TODAY;
                                        VALIDATE("Rate Code");
                                        MODIFY(TRUE);
                                      END;
                                    END;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Status.CLOSE;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100485004;2;Field  ;
                  CaptionML=[DEU=Aktualisieren Versionsdatum;
                             ENU=Update Version Date;
                             NLD=Bijwerken versiedatum;
                             NOR=Oppdater versjonsdato;
                             SVE=Uppdatera versionsdatum];
                  SourceExpr=UpdateVersionDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      UpdateVersionDate@1100485002 : Boolean;
      ProjRec@1100485006 : Record 11072003;
      ExtRec@1100485003 : Record 11012004;
      AdjRec@1100485004 : Record 11012003;
      OptRec@1100485005 : Record 11012502;
      Status@1100485009 : Dialog;
      NoOfLines@1100485008 : Integer;
      StatusCounter@1100485007 : Integer;
      Text001@1100485012 : TextConst 'DEU=Aktualisieren Budget @1@@@@@@@@@@@@@@@@@@;ENU=Update Job Budget @1@@@@@@@@@@@@@@@@@@;NLD=Bijwerken werkbegroting @1@@@@@@@@@@@@@@@@@@;NOR=Oppdater jobbudsjett @1@@@@@@@@@@@@@@@@@@;SVE=Uppdatera jobbudget @1@@@@@@@@@@@@@@@@@@';
      Text002@1100485000 : TextConst 'DEU=Aktualisieren Endblatt @1@@@@@@@@@@@@@@@@@@;ENU=Update Summary Sheet @1@@@@@@@@@@@@@@@@@@;NLD=Bijwerken staartblad @1@@@@@@@@@@@@@@@@@@;NOR=Oppdater sammendragsark @1@@@@@@@@@@@@@@@@@@;SVE=Uppdatera sammandragsark @1@@@@@@@@@@@@@@@@@@';
      TempProjRec@1100485011 : TEMPORARY Record 11072003;
      TempExtRec@1100485010 : TEMPORARY Record 11012004;

    PROCEDURE SkipFixedBudget@1100525000(Adjustm@1100525000 : Code[20];ExtContr@1100525001 : Code[20]) : Boolean;
    BEGIN
      IF ((Adjustm = '') AND (ExtContr = '') AND ProjRec."Budget Fixed") OR
         ((Adjustm <> '') AND AdjRec."Budget Correction Fixed") OR
         ((ExtContr <> '') AND ExtRec."Budget Fixed")
      THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

