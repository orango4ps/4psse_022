OBJECT XMLport 11012393 Import TRAD-Budget (TXT 5.00)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import IBIS-Trad TXT Budget (5.00);
               SVE=Import IBIS-Trad TXT Budget (5.00)];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnPreXMLport=BEGIN
                   ProjSetup.GET;
                   CreateRateTable;
                   SumReached := FALSE;

                   LineCounter := 0;

                   BudgetRec.SETRANGE("Project No.", ProjectCde);
                   BudgetRec.SETRANGE("Extension Contract", ExtensionCde);
                   BudgetRec.SETRANGE(Adjustment,  AdjustmentCde);
                   BudgetRec.SETRANGE(Option, OptionCde);
                   BudgetRec.SETRANGE("Settlement Quantity Code", SettlementQuantityCde);
                   IF BudgetRec.FINDLAST THEN
                     LineCounter := BudgetRec."Line No.";

                   IF (ProjectCde <> '') THEN  //C038256
                     ProjRec.GET(ProjectCde);
                 END;

    OnPostXMLport=VAR
                    lvSumSheetRec@1100525000 : Record 11012064;
                  BEGIN
                    WITH "Budget Line" DO BEGIN

                    IF OptionCde <> '' THEN BEGIN
                      BudgetRec.SETRANGE("Project No.", ProjectCde);
                      BudgetRec.SETRANGE(Adjustment, '');
                      BudgetRec.SETRANGE("Extension Contract", '');
                      BudgetRec.SETRANGE(Option, OptionCde);
                      BudgetRec.SETRANGE("Settlement Quantity Code", '');
                      BudgetRec.SETRANGE("Extension Contract Status", "Extension Contract Status"::Expired);
                      BudgetRec.DELETEALL(TRUE);
                    END;

                    IF SettlementQuantityCde <> '' THEN BEGIN
                      BudgetRec.SETRANGE("Project No.", ProjectCde);
                      BudgetRec.SETRANGE(Adjustment, '');
                      BudgetRec.SETRANGE("Extension Contract", '');
                      BudgetRec.SETRANGE(Option, '');
                      BudgetRec.SETRANGE("Settlement Quantity Code", SettlementQuantityCde);
                      BudgetRec.SETRANGE("Extension Contract Status", "Extension Contract Status"::Expired);
                      BudgetRec.DELETEALL(TRUE);
                    END;

                    lvSumSheetRec."Project No." := ProjectCde;
                    lvSumSheetRec."Contract No." := ExtensionCde;
                    lvSumSheetRec."Plot No." := PlotCde;
                    lvSumSheetRec."House Model" := HouseModelCde;
                    lvSumSheetRec."Main Group" := MainGroupCde;
                    lvSumSheetRec.Group := GroupCde;
                    lvSumSheetRec."Sub Group" := SubGroupCde;
                    lvSumSheetRec.Option := OptionCde;
                    lvSumSheetRec."Settlement Quantity Code" := SettlementQuantityCde;
                    IF OptionCde <> '' THEN BEGIN
                      lvSumSheetRec.ReCalcOptionSummaryLines();
                      lvSumSheetRec.UpdateOptBudgetLines();
                    END ELSE BEGIN
                      IF SettlementQuantityCde <> '' THEN BEGIN
                        lvSumSheetRec.ReCalcSettlQuanSummaryLines();
                        lvSumSheetRec.UpdateSettlQuanBudgLines();
                      END ELSE BEGIN
                        lvSumSheetRec.ReCalcSummaryLines(AdjustmentCde);
                        lvSumSheetRec.UpdateBudgetLines(TRUE,AdjustmentCde);
                      END;
                    END;

                    CheckUpdateOption();
                    CheckUpdateSettlQuantity();

                    END;
                  END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{D83C7E5F-3E9F-4810-9DE9-1C89EBA7CC78}];  ;Root                ;Element ;Text     }

    { [{9B61C620-C498-44F7-AB10-16730623E1FD}];1 ;BudgetLine          ;Element ;Table   ;
                                                  SourceTable=Table11012001;
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Budget Line".INIT;
                                                                              CLEAR(ElementCde);
                                                                              CLEAR(StuurCde);
                                                                              CLEAR(QuantityTxt);
                                                                              CLEAR(NormTxt);
                                                                              CLEAR(MatPriceTxt);
                                                                              CLEAR(PlntPriceTxt);
                                                                              CLEAR(SubPriceTxt);
                                                                              CLEAR(AdmiGenCde);
                                                                              CLEAR(AdmiLaborCde);
                                                                              CLEAR(AdmiMatCde);
                                                                              CLEAR(AdmiPlntCde);
                                                                              CLEAR(AdmiSubCde);
                                                                              CLEAR(FactorTxt);
                                                                              CLEAR(TenderCde);
                                                                              CLEAR(FactorLabTxt);
                                                                              CLEAR(FactorMatTxt);
                                                                              CLEAR(FactorPlntTxt);
                                                                              CLEAR(FactorSubTxt);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 WITH "Budget Line" DO BEGIN

                                                                                 IF TestSummaryStart(StuurCde) THEN
                                                                                   SumReached := TRUE;
                                                                                 IF TestSummaryEnd(StuurCde) THEN
                                                                                   SumReached := FALSE;
                                                                                 IF SumReached THEN
                                                                                   currXMLport.SKIP;

                                                                                 LineCreated := FALSE;
                                                                                 LineCounter := LineCounter + 10000;
                                                                                 "Line No." := LineCounter;
                                                                                 "Project No." := ProjectCde;
                                                                                 FillMainProject;
                                                                                 Element := '';
                                                                                 Chapter := '';
                                                                                 Paragraph := '';
                                                                                 "Element Phase Code" := '';
                                                                                 "Cost Object" := '';
                                                                                 "Extension Contract" := ExtensionCde;
                                                                                 "Cost Type":= "Cost Type"::Sundry;
                                                                                 Adjustment := AdjustmentCde;
                                                                                 Option := OptionCde;
                                                                                 "Settlement Quantity Code" := SettlementQuantityCde;
                                                                                 "Time Quantity" := 0;
                                                                                 Quantity := 0;
                                                                                 "Price (LCY)" := 0;
                                                                                 Norm := 0;
                                                                                 "Rate (LCY)" := 0;
                                                                                 "Amount (LCY)" := 0;
                                                                                 Hours := 0;
                                                                                 CLEAR("Option Line Type");
                                                                                 "Department Code" := ProjRec."Global Dimension 1 Code";

                                                                                 RecRef.GETTABLE("Budget Line");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 GetVersionDate;
                                                                                 UpdateExtContractStatus;

                                                                                 IF Option <> '' THEN BEGIN
                                                                                   "Extension Contract Status" := "Extension Contract Status"::Expired;
                                                                                   OptionRec.SETRANGE("Project No.","Project No.");
                                                                                   OptionRec.SETRANGE("Plot No.","Plot No.");
                                                                                   OptionRec.SETRANGE("House Model",HouseModelCde);
                                                                                   OptionRec.SETRANGE("Main Group",MainGroupCde);
                                                                                   OptionRec.SETRANGE(Group,GroupCde);
                                                                                   OptionRec.SETRANGE("Sub Group",SubGroupCde);
                                                                                   OptionRec.SETRANGE(Option,Option);
                                                                                   IF OptionRec.FINDFIRST THEN
                                                                                     "Version Date" := OptionRec."Version Date";
                                                                                 END;

                                                                                 IF "Settlement Quantity Code" <> '' THEN BEGIN
                                                                                   "Extension Contract Status" := "Extension Contract Status"::Expired;
                                                                                   SettlementQuantityRec.SETRANGE("Project No.","Project No.");
                                                                                   SettlementQuantityRec.SETRANGE(Code, "Settlement Quantity Code");
                                                                                   IF SettlementQuantityRec.FINDFIRST THEN
                                                                                     "Version Date" := SettlementQuantityRec."Version Date";
                                                                                 END;

                                                                                 CASE ProjSetup."Fill Element with (TRAD)" OF
                                                                                   ProjSetup."Fill Element with (TRAD)"::"Alternative Code":FillElement(AdmiGenCde);
                                                                                   ProjSetup."Fill Element with (TRAD)"::"Middel Code":FillElement(ElementCde);
                                                                                   ProjSetup."Fill Element with (TRAD)"::"Tender Code":FillElement(TenderCde);
                                                                                 END;
                                                                                 FillElementPhaseCodeBasedOnElement;
                                                                                 ConvertAmounts;

                                                                                 IF TestNormalLine(StuurCde) THEN BEGIN
                                                                                   IF FactorTxt <> '' THEN BEGIN
                                                                                     TmpProjRateRec.GET(ProjectCde,FactorTxt,0D);
                                                                                     Quantity := QuantityDec * TmpProjRateRec."Rate (LCY)";
                                                                                   END ELSE
                                                                                     Quantity := QuantityDec;
                                                                                   "Time Quantity" := 1;

                                                                                   IF "Unit of Measure" <> '' THEN
                                                                                     IF NOT UnitRec.GET("Unit of Measure") THEN
                                                                                       StoreMessage(Description, STRSUBSTNO(Text999, FIELDCAPTION("Unit of Measure"), "Unit of Measure"));

                                                                                   InsertLines;
                                                                                 END ELSE BEGIN
                                                                                   IF TestTextLine(StuurCde) THEN
                                                                                     InsertTextLines
                                                                                   ELSE
                                                                                     IF TestStartTotalLine(StuurCde) THEN
                                                                                       InsertTextLines;
                                                                                 END;

                                                                                 currXMLport.SKIP;

                                                                                 END;
                                                                               END;
                                                                                }

    { [{6CB3DABC-027E-4C9C-9112-40F8B8FB3B46}];2 ;Filler1             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{159DA4B9-190F-40A5-B57B-B6F16CDC598C}];2 ;ElementCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  ElementCde := COPYSTR(ElementCde,1,20);
                                                                                END;

                                                  Width=22 }

    { [{15C2B593-6949-4E7B-9822-ED61C6760A78}];2 ;StuurCde            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{40F80AA8-EFB6-41B8-A02D-1A04BD88F2B0}];2 ;Description         ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=Budget Line::Description;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{7F6A2CAF-BFC2-4430-92A9-B5C2CBFCBDA8}];2 ;QuantityTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{2B0D4DD2-70DC-4FE6-8897-5F218F8F10A1}];2 ;UnitOfMeasure       ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Budget Line::Unit of Measure;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{D5031E37-CA40-474F-B541-CB9F404BE3D9}];2 ;NormTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{95D4C864-6B21-4E4D-B65D-861D2E289912}];2 ;MatPriceTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{34E4F36F-5965-4EF7-9A03-EEF349799A76}];2 ;PlntPriceTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{F122D1FB-694A-4A44-BCA3-82E693DB84D6}];2 ;SubPriceTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{94E1AF6F-30E0-4210-929F-CE288E5D31AA}];2 ;Filler2             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{7FCEEE9C-DA8E-4B4D-AF17-2E15595EBAB0}];2 ;RateCode            ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Budget Line::Rate Code;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{2D7E364E-50AC-44EF-BDAB-B01211148D58}];2 ;Filler3             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{BFFBF916-69D4-4AA8-A251-A7AC0025FBA1}];2 ;Filler4             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{543E1898-27C3-4513-8496-0442550DE28C}];2 ;AdmiGenCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{D8CB8D8B-7E62-482F-9800-C10C6699ECC5}];2 ;Filler5             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{41B0F400-8D8B-4105-98DC-7828F62582FE}];2 ;Filler6             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{170DC36D-CC52-4847-BC7C-6128A62DDDB7}];2 ;AdmiLaborCde        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{94E76E16-B539-480B-81C5-1F7631E1CE12}];2 ;AdmiMatCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{12F5FBB5-6F5F-436C-83E1-4ECC1DA2D78B}];2 ;AdmiPlntCde         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{1B1FC5F8-1F7F-4C82-B7ED-3F94AA0DD390}];2 ;AdmiSubCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{1AF1E1B4-1AF7-414B-973F-876634DFF04B}];2 ;FactorTxt           ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{C907F6C0-3AE1-469D-80AF-4BC447A05293}];2 ;TenderCde           ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  TenderCde := COPYSTR(TenderCde,1,20);
                                                                                END;

                                                  Width=22 }

    { [{19CE38C2-5B4A-4C0E-92CE-1FEFAD0E123A}];2 ;Filler7             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{51549CDF-C2B0-4674-BE66-0F00B7DEAD10}];2 ;Filler8             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{5727CF9E-E7A9-4B8A-BF5F-3863264D8653}];2 ;FactorLabTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{EBEA17DD-DB6E-423B-86D8-67C89F9DCAA3}];2 ;FactorMatTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{85D4E87B-CF24-4C58-930A-6097B5FAEFD2}];2 ;FactorPlntTxt       ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{4EAB1EEE-D95D-479A-87CD-661115142A86}];2 ;FactorSubTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{A42CEBFC-8629-4723-BB95-5B5D603FA034}];2 ;Filler9             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{5D0D13B6-94E6-43BB-BD74-19F5153B860B}];2 ;Filler10            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=13 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      ProjSetup@1100525049 : Record 315;
      ProjRec@1100525048 : Record 11072003;
      ElemRec@1100525047 : Record 11012010;
      BudgetRec@1100525046 : Record 11012001;
      OptionRec@1100525045 : Record 11012502;
      OptionBudgetRec@1100525044 : Record 11012503;
      DimValRec@1100525043 : Record 349;
      TmpProjRateRec@1100525042 : TEMPORARY Record 11020231;
      UnitRec@1100525041 : Record 204;
      RecRef@1100525032 : RecordRef;
      ProjectCde@1100525025 : Code[20];
      AdjustmentCde@1100525024 : Code[10];
      ExtensionCde@1100525023 : Code[10];
      HouseModelCde@1100525022 : Code[20];
      MainGroupCde@1100525021 : Code[20];
      GroupCde@1100525020 : Code[20];
      SubGroupCde@1100525019 : Code[20];
      OptionCde@1100525018 : Code[20];
      LineCounter@1100525017 : Integer;
      LineCreated@1100525016 : Boolean;
      gElementOptout@1100525015 : Boolean;
      gFolder@1100525014 : Text[250];
      TradBudgetTxt@1100525013 : Text[100];
      PPriceDec@1100525008 : Decimal;
      MPriceDec@1100525007 : Decimal;
      OPriceDec@1100525006 : Decimal;
      QuantityDec@1100525005 : Decimal;
      NormDec@1100525004 : Decimal;
      DimMgt@1100525003 : Codeunit 408;
      XMLportStatusDialogMgt@1100525033 : Codeunit 11130006;
      SumReached@1100525002 : Boolean;
      PlotCde@1100525001 : Code[20];
      gVersion@1100525000 : Text[10];
      Text001@1100525031 : TextConst 'ENU=Line not coded for cost type %1;SVE=Rad inte kodad f”r kostnadstyp %1';
      Text004@1100525030 : TextConst 'ENU=Cost Object %1 not present for Cost Type %2;SVE=Kostnadsobjekt %1 finns inte f”r kostnadstypen %2';
      Text005@1100525029 : TextConst 'ENU=*#+;SVE=*#+';
      Text006@1100525028 : TextConst 'ENU=?$ABCDEFWXYZGMNOQRSTUV;SVE=?$ABCDEFWXYZGMNOQRSTUV';
      Text008@1100525027 : TextConst 'ENU=/;SVE=/';
      Text009@1100525026 : TextConst 'ENU=.!,;SVE=.!,';
      Text010@1100525012 : TextConst 'ENU=\;SVE=\';
      Text011@1100525011 : TextConst 'ENU=Cost type %1 does not match Cost Object %2;SVE=Kostnadstypen %1 matchar inte kostnadsobjektet %2';
      Text012@1100525010 : TextConst 'ENU=No ELement present (Element is mandatory);SVE=Det finns inget element (element „r obligatoriskt)';
      Text013@1210190000 : TextConst 'ENU=Cost Object %1 is blocked;SVE=Kostnadsobjekt %1 „r blockerad';
      Text999@1100525009 : TextConst 'ENU=%1 %2 not present;SVE=%1 %2 finns inte';
      SettlementQuantityCde@1100528201 : Code[20];
      FromSettlementQuantity@1100528200 : Boolean;
      SettlementQuantityRec@1100528202 : Record 11072243;
      SettlementQuantityBudgetRec@1100528203 : Record 11072244;

    PROCEDURE DetermineSelection@1(Project1Cde@11012000 : Code[20];Adjustment1Cde@11012001 : Code[10];Extension1Cde@11012002 : Code[10];HouseModel1Cde@1210190004 : Code[20];MainGroup1Cde@1210190003 : Code[20];Group1Cde@1210190002 : Code[20];SubGroup1Cde@1210190001 : Code[20];Option1Cde@11012003 : Code[20];lFolder@1100485001 : Text[250];lTradBudgetTxt@1210190000 : Text[100];IPlotCde@1100485000 : Code[20];IVersion@1100485002 : Text[10];IElementOptout@1100485003 : Boolean);
    BEGIN
      ProjectCde := Project1Cde;
      AdjustmentCde := Adjustment1Cde;
      ExtensionCde := Extension1Cde;
      PlotCde := IPlotCde;
      HouseModelCde := HouseModel1Cde;
      MainGroupCde := MainGroup1Cde;
      GroupCde := Group1Cde;
      SubGroupCde := SubGroup1Cde;
      OptionCde := Option1Cde;
      gFolder := lFolder;
      TradBudgetTxt := lTradBudgetTxt;
      gVersion := IVersion;
      gElementOptout := IElementOptout;
    END;

    PROCEDURE CreateRateTable@1210190000();
    VAR
      TempBlob@1100525001 : Record 99008535;
      FileMgt@1100525000 : Codeunit 419;
      InStreamStrm@1210190001 : InStream;
      Txt@1210190002 : Text[250];
      RateTxt@1210190003 : Text[17];
    BEGIN
      FileMgt.BLOBImport4PS(TempBlob, gFolder + 'f' + TradBudgetTxt + '.txt', FALSE);
      TempBlob.Blob.CREATEINSTREAM(InStreamStrm);

      WHILE NOT (InStreamStrm.EOS()) DO BEGIN
        InStreamStrm.READTEXT(Txt);
        TmpProjRateRec.INIT;
        TmpProjRateRec."Project No." := ProjectCde;
        TmpProjRateRec."Rate Code" := COPYSTR(Txt,2,2);
        TmpProjRateRec."Starting Date" := 0D;
        RateTxt := COPYSTR(Txt,43,17);
        TmpProjRateRec."Rate (LCY)" := Alphanumeric(RateTxt);
        TmpProjRateRec.INSERT;
      END;
    END;

    PROCEDURE Alphanumeric@2(Text@11012001 : Text[250]) Result@11012000 : Decimal;
    VAR
      dummy@11012002 : Decimal;
    BEGIN
      Text := CONVERTSTR(Text, '.', ',');
      IF DELCHR(Text, '=', ' -,0123456789') <> '' THEN EXIT(0);
      IF DELCHR(Text, '=', ' ') = '' THEN EXIT(0);
      EVALUATE(dummy, Text);
      EXIT(dummy);
    END;

    PROCEDURE ConvertAmounts@1210190002();
    BEGIN
      QuantityDec := Alphanumeric(QuantityTxt);
      NormDec   := Alphanumeric(NormTxt);
      MPriceDec := Alphanumeric(MatPriceTxt);
      PPriceDec := Alphanumeric(PlntPriceTxt);
      OPriceDec := Alphanumeric(SubPriceTxt);
    END;

    PROCEDURE FillElement@1210190001(lElement@1210190000 : Code[20]) : Decimal;
    VAR
      lvProjRec@1100485000 : Record 11072003;
      pos1@1210190001 : Integer;
      ElementFormat@1210190003 : Codeunit 11012022;
      lChapterCde@1210190005 : Code[20];
      lParGraphCde@1210190006 : Code[20];
      lLevel@1210190007 : Integer;
    BEGIN
      IF gElementOptout THEN
        lElement := '';

      IF lElement <> '' THEN BEGIN
        pos1 := STRPOS(lElement, '.');
        IF (pos1 = 2) OR ((pos1 = 0) AND (STRLEN(lElement) = 1)) THEN
          lElement := '0' + lElement;
        ElementFormat.FormatElemExtended(2,lElement,lChapterCde,lParGraphCde,lLevel);
        CASE lLevel OF
          1:BEGIN
              IF NOT ElemRec.GET(ProjectCde,lChapterCde) THEN
                InsertElement(lChapterCde);
            END;
          2:BEGIN
              IF NOT ElemRec.GET(ProjectCde,lChapterCde) THEN
                InsertElement(lChapterCde);
              IF NOT ElemRec.GET(ProjectCde,lParGraphCde) THEN
                InsertElement(lParGraphCde);
            END;
          3:BEGIN
              IF NOT ElemRec.GET(ProjectCde,lChapterCde) THEN
                InsertElement(lChapterCde);
              IF NOT ElemRec.GET(ProjectCde,lParGraphCde) THEN
                InsertElement(lParGraphCde);
              IF NOT ElemRec.GET(ProjectCde,lElement) THEN
                InsertElement(lElement);
             END;
        END;
        "Budget Line".Element := lElement;
        "Budget Line".Chapter := lChapterCde;
        "Budget Line".Paragraph := lParGraphCde;
      END ELSE BEGIN
        IF ProjectCde <> '' THEN BEGIN
          lvProjRec.GET(ProjectCde);
          IF lvProjRec."Posting Element Mandatory" THEN
            StoreMessage("Budget Line".Description,Text012);
        END;
      END;
    END;

    PROCEDURE InsertElement@1210190012(lElementCde@1210190000 : Code[20]);
    BEGIN
      IF ProjectCde <> '' THEN BEGIN
        ElemRec.VALIDATE("Project No.", ProjectCde);
        ElemRec.VALIDATE(Element,lElementCde);
        ElemRec.Description := COPYSTR("Budget Line".Description, 1, MAXSTRLEN(ElemRec.Description));
        ElemRec.Quantity  := 1;
        ElemRec.INSERT;
      END;
    END;

    PROCEDURE InsertLines@1210190003();
    VAR
      BudgetRateRec@1210190000 : Record 11012000;
    BEGIN
      WITH "Budget Line" DO BEGIN
        IF NormDec <> 0 THEN BEGIN
          "Cost Type" := "Cost Type"::Labor;
          GetDefaultPurchAction;
          TmpProjRateRec.GET(ProjectCde, "Rate Code", 0D);
          IF "Rate Code" <> '' THEN BEGIN
            BudgetRateRec.SETRANGE(Code, "Rate Code");
            BudgetRateRec.SETRANGE("Starting Date", 0D, TODAY);
            BudgetRateRec.SETFILTER("Ending Date",'%1|>=%2',0D, TODAY);
            IF BudgetRateRec.ISEMPTY THEN
              StoreMessage(Description, STRSUBSTNO(Text999, FIELDCAPTION("Rate Code"), "Rate Code"));
          END;
          "Rate (LCY)" := TmpProjRateRec."Rate (LCY)";
          IF FactorLabTxt <> '' THEN BEGIN
            TmpProjRateRec.GET(ProjectCde, FactorLabTxt, 0D);
            "Rate (LCY)" := "Rate (LCY)" * TmpProjRateRec."Rate (LCY)";
          END;
          Norm := NormDec;
          CalcTotals;
          "Cost Object" := FillCostObject("Cost Type");
          "Cost Component" := DefaultCostComponent("Cost Object", "Project No.");
          FillDefaultFscType();
          IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN
            INSERT
          ELSE
            AssignOptionBudget;
          IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN UpdateCostControlStatus(0);
          LineCreated := TRUE;
        END;

        IF MPriceDec <> 0 THEN BEGIN
          IF LineCreated THEN BEGIN
            LineCounter := LineCounter + 10000;
            "Line No." := LineCounter;
          END;
          "Cost Type" := "Cost Type"::Material;
          GetDefaultPurchAction;
          "Rate (LCY)" := 0;
          Hours := 0;
          "Price (LCY)" := MPriceDec;
          IF FactorMatTxt <> '' THEN BEGIN
            TmpProjRateRec.GET(ProjectCde, FactorMatTxt, 0D);
            "Price (LCY)" := "Price (LCY)" * TmpProjRateRec."Rate (LCY)";
          END;
          "Rate Code" := '';
          Norm := 0;
          CalcTotals;
          "Cost Object" := FillCostObject("Cost Type");
          "Cost Component" := DefaultCostComponent("Cost Object", "Project No.");
          FillDefaultFscType();
          IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN
            INSERT
          ELSE
            AssignOptionBudget;
          IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN UpdateCostControlStatus(0);
          LineCreated := TRUE;
        END;

        IF PPriceDec <> 0 THEN BEGIN
          IF LineCreated THEN BEGIN
            LineCounter := LineCounter + 10000;
            "Line No." := LineCounter;
          END;
          "Cost Type" := "Cost Type"::Plant;
          GetDefaultPurchAction;
          "Rate (LCY)" := 0;
          Hours := 0;
          "Price (LCY)" := PPriceDec;
          IF FactorPlntTxt <> '' THEN BEGIN
            TmpProjRateRec.GET(ProjectCde, FactorPlntTxt, 0D);
            "Price (LCY)" := "Price (LCY)" * TmpProjRateRec."Rate (LCY)";
          END;
          "Rate Code" := '';
          Norm := 0;
          CalcTotals;
          "Cost Object" := FillCostObject("Cost Type");
          "Cost Component" := DefaultCostComponent("Cost Object", "Project No.");
          FillDefaultFscType();
          IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN
            INSERT
          ELSE
            AssignOptionBudget;
          IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN UpdateCostControlStatus(0);
          LineCreated := TRUE;
        END;

        IF OPriceDec <> 0 THEN BEGIN
          IF LineCreated THEN BEGIN
            LineCounter := LineCounter + 10000;
            "Line No." := LineCounter;
          END;
          "Cost Type" := "Cost Type"::Subcontracting;
          GetDefaultPurchAction;
          "Rate (LCY)" := 0;
          Hours := 0;
          "Price (LCY)" := OPriceDec;
          IF FactorSubTxt <> '' THEN BEGIN
            TmpProjRateRec.GET(ProjectCde, FactorSubTxt, 0D);
            "Price (LCY)" := "Price (LCY)" * TmpProjRateRec."Rate (LCY)";
          END;
          "Rate Code" := '';
          Norm := 0;
          CalcTotals;
          "Cost Object" := FillCostObject("Cost Type");
          "Cost Component" := DefaultCostComponent("Cost Object", "Project No.");
          FillDefaultFscType();  //DP00129
          IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN
            INSERT
          ELSE
            AssignOptionBudget;
          IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN UpdateCostControlStatus(0);
          LineCreated := TRUE;
        END;
      END;
    END;

    PROCEDURE InsertTextLines@1210190010();
    BEGIN
      LineCounter := LineCounter + 10000;

      WITH "Budget Line" DO BEGIN
        "Line No." := LineCounter;
        "Cost Type" := "Cost Type"::Sundry;
        GetDefaultPurchAction;
        "Rate (LCY)" := 0;
        Hours := 0;
        "Price (LCY)" := 0;
        "Rate Code" := '';
        Norm := 0;
        CalcTotals;
        "Cost Object" := '';
        "Cost Component" := '';
        IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN
          INSERT
        ELSE
          AssignOptionBudget;
      END;
    END;

    PROCEDURE AssignOptionBudget@3();
    VAR
      LastLino@11012000 : Integer;
    BEGIN
      IF SettlementQuantityCde <> '' THEN BEGIN
        AssignSettlQuantityBudget;
        EXIT;
      END;

      BudgetRec.COPY("Budget Line");

      OptionBudgetRec.SETRANGE("Project No.",ProjectCde);
      IF PlotCde <> '' THEN
        OptionBudgetRec.SETRANGE("Plot No.",PlotCde)
      ELSE
        OptionBudgetRec.SETFILTER("Plot No.",'%1','');
      OptionBudgetRec.SETRANGE("House Model",HouseModelCde);
      OptionBudgetRec.SETRANGE("Main Group",MainGroupCde);
      OptionBudgetRec.SETRANGE(Group,GroupCde);
      OptionBudgetRec.SETRANGE("Sub Group",SubGroupCde);
      OptionBudgetRec.SETRANGE(Option,OptionCde);
      IF NOT OptionBudgetRec.FINDLAST THEN
        LastLino := 10000
      ELSE
        LastLino := OptionBudgetRec."Line No." + 10000;

      OptionBudgetRec.INIT;
      OptionBudgetRec."Project No." := BudgetRec."Project No.";
      OptionBudgetRec."Plot No." := PlotCde;
      OptionBudgetRec."House Model" := HouseModelCde;
      OptionBudgetRec."Main Group" := MainGroupCde;
      OptionBudgetRec.Group := GroupCde;
      OptionBudgetRec."Sub Group" := SubGroupCde;
      OptionBudgetRec.Option := BudgetRec.Option;
      OptionBudgetRec.Element := BudgetRec.Element;
      OptionBudgetRec."Cost Object" := BudgetRec."Cost Object";
      OptionBudgetRec."Cost Component" := BudgetRec."Cost Component";
      OptionBudgetRec.Description := BudgetRec.Description;
      OptionBudgetRec.Quantity := BudgetRec.Quantity;
      OptionBudgetRec."Unit of Measure" := BudgetRec."Unit of Measure";
      OptionBudgetRec.Norm := BudgetRec.Norm;
      OptionBudgetRec."Rate Code" := BudgetRec."Rate Code";
      OptionBudgetRec.Rate := BudgetRec."Rate (LCY)";
      OptionBudgetRec.Hours := BudgetRec.Hours;
      OptionBudgetRec.Amount := BudgetRec."Amount (LCY)";
      OptionBudgetRec."Line No." := LastLino;
      OptionBudgetRec."Total Amount" := BudgetRec."Total Amount";
      OptionBudgetRec."Total Hours" := BudgetRec."Total Hours";
      OptionBudgetRec."Cost Type" := BudgetRec."Cost Type";
      OptionBudgetRec.Price := BudgetRec."Price (LCY)";
      OptionBudgetRec."Time Quantity" := BudgetRec."Time Quantity";
      OptionBudgetRec."Unit of Time" := BudgetRec."Unit of Time";
      OptionBudgetRec.Chapter := BudgetRec.Chapter;
      OptionBudgetRec.Paragraph := BudgetRec.Paragraph;

      OptionBudgetRec.CheckInsertSummarySheet(FALSE);  //* Must be done before INSERT, because of test 'only when first'.
      OptionBudgetRec.INSERT;
    END;

    PROCEDURE StoreMessage@5(lvDesc@11012000 : Text[100];lvMess@11012001 : Text[250]);
    VAR
      ErrorRec@1210190000 : Record 11012051;
      lvError@11012002 : Integer;
    BEGIN
      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"Import Budget");
        IF FINDLAST THEN
          lvError := "Line No." + 1
        ELSE
          lvError := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"Import Budget";
        "Project No." := ProjRec."No.";
        "Line No." := lvError;
        "Error message" := lvMess;
        Description := lvDesc;
        INSERT;
      END;
    END;

    PROCEDURE FillCostObject@1210190020(CostTypeOpt@1210190000 : 'Labor,Material,Subcontracting,Plant') lCostObject@1210190001 : Code[20];
    VAR
      lAdmicde@1210190002 : Code[20];
    BEGIN
      CASE ProjSetup."Fill Cost Object with (TRAD)" OF
        ProjSetup."Fill Cost Object with (TRAD)"::"Alternative Code":
          EXIT(GetCostObject(AdmiGenCde,CostTypeOpt));
        ProjSetup."Fill Cost Object with (TRAD)"::"Admi Code":
          BEGIN
            CASE CostTypeOpt OF
              CostTypeOpt::Labor:          lAdmicde := AdmiLaborCde;
              CostTypeOpt::Material:       lAdmicde := AdmiMatCde;
              CostTypeOpt::Subcontracting: lAdmicde := AdmiSubCde;
              CostTypeOpt::Plant:          lAdmicde := AdmiPlntCde;
            END;
            EXIT(GetCostObject(lAdmicde,CostTypeOpt));
          END;
        ProjSetup."Fill Cost Object with (TRAD)"::"Middel Code":
           EXIT(GetCostObject(ElementCde,CostTypeOpt));
        ProjSetup."Fill Cost Object with (TRAD)"::"Tender Code":
           EXIT(GetCostObject(TenderCde,CostTypeOpt));
      END;
    END;

    PROCEDURE GetCostObject@1210190026(ladmicde@1210190000 : Code[20];lCostTypeOpt@1210190001 : 'Labor,Material,Subcontracting,Plant') Result@1210190002 : Code[20];
    BEGIN
      IF ladmicde = '' THEN BEGIN
        StoreMessage("Budget Line".Description, STRSUBSTNO(Text001,"Budget Line"."Cost Type"));
        EXIT('');
      END ELSE BEGIN
        DimMgt.GetDimValueRec(2, ladmicde, DimValRec, FALSE,'');
        IF DimValRec.Code <> '' THEN BEGIN
          IF DimValRec."Cost Type" = lCostTypeOpt THEN BEGIN
            IF DimValRec.Blocked THEN BEGIN  //C-026977
              StoreMessage("Budget Line".Description, STRSUBSTNO(Text013,ladmicde));
              EXIT('');
            END;
            EXIT(ladmicde)
          END ELSE BEGIN
            StoreMessage("Budget Line".Description, STRSUBSTNO(Text011,"Budget Line"."Cost Type",ladmicde));
            EXIT('');
          END;
        END ELSE BEGIN
            StoreMessage("Budget Line".Description, STRSUBSTNO(Text004,ladmicde,"Budget Line"."Cost Type"));
            EXIT('');
        END;
      END;
    END;

    PROCEDURE TestStartTotalLine@1210190004(Text@1210190000 : Text[250]) Result@1210190001 : Boolean;
    BEGIN
      EXIT(DELCHR(Text,'=',Text005)='');
    END;

    PROCEDURE TestNormalLine@1210190005(text@1210190000 : Text[250]) result@1210190001 : Boolean;
    BEGIN
      IF text = '' THEN
        EXIT(TRUE)
      ELSE
        EXIT(DELCHR(text,'=',Text006)='');
    END;

    PROCEDURE TestTextLine@1210190006(text@1210190000 : Text[250]) result@1210190001 : Boolean;
    BEGIN
      EXIT(DELCHR(text,'=',Text009)='');
    END;

    PROCEDURE TestSummaryStart@1210190008(Text@1210190000 : Text[250]) Result@1210190001 : Boolean;
    BEGIN
      IF Text = Text008 THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE TestSummaryEnd@1210190009(Text@1210190000 : Text[250]) Result@1210190001 : Boolean;
    BEGIN
      IF Text = Text010 THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE CheckUpdateOption@1100485000();
    VAR
      lvDummyOptionSurchRec@1100485001 : Record 11012512;
      lvBuyerMgtCU@1100485000 : Codeunit 11012500;
    BEGIN
      IF OptionCde = '' THEN
        EXIT;

      lvBuyerMgtCU.BmCalcSurchargesUpdateOption(
        ProjectCde, PlotCde, HouseModelCde, MainGroupCde, GroupCde, SubGroupCde, OptionCde, FALSE, lvDummyOptionSurchRec);
    END;

    PROCEDURE DefaultCostComponent@1100485001(lvCostObject@1100485000 : Code[20];lvProject@1100485001 : Code[20]) lvCostComponent : Code[20];
    VAR
      lvDimValRec@1100485002 : Record 349;
    BEGIN
      lvCostComponent := '';
      IF lvCostObject <> '' THEN BEGIN
        DimMgt.GetDimValueRec(2, lvCostObject, lvDimValRec, FALSE, lvProject);
        IF NOT lvDimValRec.Blocked THEN
          lvCostComponent := lvDimValRec."Cost Component";
      END;
    END;

    PROCEDURE SetDefaultsSettlQuantity@1100528203(ISettlementQuantityCde@11012000 : Code[20]);
    BEGIN
      IF SettlementQuantityRec.WRITEPERMISSION AND SettlementQuantityBudgetRec.WRITEPERMISSION THEN BEGIN
        FromSettlementQuantity := TRUE;
        SettlementQuantityCde := ISettlementQuantityCde;
      END ELSE BEGIN
        FromSettlementQuantity := FALSE;
        SettlementQuantityCde := '';
      END;
    END;

    PROCEDURE CheckUpdateSettlQuantity@1100528200();
    VAR
      lvDummySettlementQuantitySurcharge@1100485001 : Record 11072247;
      lvSettlementQuantityManagement@1100485000 : Codeunit 11071690;
    BEGIN
      IF SettlementQuantityCde = '' THEN
        EXIT;

      lvSettlementQuantityManagement.MMRCalcSurchargesUpdateSettlQuan(
        ProjectCde, SettlementQuantityCde, '' ,FALSE,lvDummySettlementQuantitySurcharge)
    END;

    PROCEDURE AssignSettlQuantityBudget@1100528205();
    VAR
      LastLino@11012000 : Integer;
    BEGIN
      BudgetRec.COPY("Budget Line");

      SettlementQuantityBudgetRec.SETRANGE("Project No.",ProjectCde);
      SettlementQuantityBudgetRec.SETRANGE("Settlement Quantity Code", SettlementQuantityCde);
      IF NOT SettlementQuantityBudgetRec.FINDLAST THEN
        LastLino := 10000
      ELSE
        LastLino := SettlementQuantityBudgetRec."Line No." + 10000;

      SettlementQuantityBudgetRec.INIT;
      SettlementQuantityBudgetRec."Project No." := BudgetRec."Project No.";
      SettlementQuantityBudgetRec."Settlement Quantity Code":= BudgetRec."Settlement Quantity Code";
      SettlementQuantityBudgetRec."Line No." := LastLino;
      SettlementQuantityBudgetRec.Element := BudgetRec.Element;
      SettlementQuantityBudgetRec."Cost Type" := BudgetRec."Cost Type";
      SettlementQuantityBudgetRec."Cost Object" := BudgetRec."Cost Object";
      SettlementQuantityBudgetRec.Description := BudgetRec.Description;
      SettlementQuantityBudgetRec."Cost Component" := BudgetRec."Cost Component";
      SettlementQuantityBudgetRec.Quantity := BudgetRec.Quantity;
      SettlementQuantityBudgetRec."Unit of Measure" := BudgetRec."Unit of Measure";
      SettlementQuantityBudgetRec.Norm := BudgetRec.Norm;
      SettlementQuantityBudgetRec."Rate Code" := BudgetRec."Rate Code";
      SettlementQuantityBudgetRec.Rate := BudgetRec."Rate (LCY)";
      SettlementQuantityBudgetRec.Hours := BudgetRec.Hours;
      SettlementQuantityBudgetRec.Amount := BudgetRec."Amount (LCY)";
      SettlementQuantityBudgetRec."Total Amount" := BudgetRec."Total Amount";
      SettlementQuantityBudgetRec."Total Hours" := BudgetRec."Total Hours";
      SettlementQuantityBudgetRec.Price := BudgetRec."Price (LCY)";
      SettlementQuantityBudgetRec."Time Quantity" := BudgetRec."Time Quantity";
      SettlementQuantityBudgetRec."Unit of Time" := BudgetRec."Unit of Time";
      SettlementQuantityBudgetRec.Chapter := BudgetRec.Chapter;
      SettlementQuantityBudgetRec.Paragraph := BudgetRec.Paragraph;

      SettlementQuantityBudgetRec.CheckInsertSummarySheet(FALSE);  //* Must be done before INSERT, because of test 'only when first'.
      SettlementQuantityBudgetRec.INSERT;
    END;

    BEGIN
    END.
  }
}

