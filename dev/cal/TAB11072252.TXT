OBJECT Table 11072252 Plot SQ Recording Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               //IF PlotSQRecording.GET("Project No.","Plot No.","Recording Seq. No.") THEN BEGIN
               //  VALIDATE("Recording Date", PlotSQRecording."Recording Date");
               //  VALIDATE(Phase, PlotSQRecording.Type);
               //END;
               "Cost Plus Entry Created" := FALSE;
             END;

    OnModify=BEGIN
               CalculateFields;
             END;

    OnDelete=BEGIN
               TESTFIELD("Cost Plus Entry Created", FALSE);
             END;

    OnRename=BEGIN
               IF (NOT RenameIsAllowed) THEN
                 ERROR(Text000, TABLECAPTION);
             END;

    CaptionML=ENU=SQ Recording Line;
    LookupPageID=Page11229515;
  }
  FIELDS
  {
    { 1   ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 10  ;   ;Plot No.            ;Code20        ;TableRelation=Plot."Plot No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Plot No.;
                                                              NOR=Tegningnr;
                                                              SVE=Ritningnr];
                                                   NotBlank=Yes }
    { 15  ;   ;Recording Seq. No.  ;Integer       ;CaptionML=ENU=Recording Seq. No.;
                                                   Editable=No }
    { 20  ;   ;House Model         ;Code20        ;TableRelation="House Model".Model WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=House Model;
                                                              NOR=Husmodell;
                                                              SVE=Husmodell] }
    { 30  ;   ;Unit                ;Code20        ;TableRelation="Units per House Model".Unit WHERE (Project No.=FIELD(Project No.),
                                                                                                     Model=FIELD(House Model));
                                                   CaptionML=[ENU=Unit;
                                                              NOR=Enhet;
                                                              SVE=Enhet] }
    { 35  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 40  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=Settlement,Option,Other;
                                                   OptionString=Settlement,Option,Other }
    { 50  ;   ;Main Group          ;Code20        ;TableRelation="Option Main Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Main Group;
                                                              NOR=Hovedgruppe;
                                                              SVE=Huvudgrupp] }
    { 60  ;   ;Group               ;Code20        ;TableRelation="Option Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                            Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Group;
                                                              NOR=Gruppe;
                                                              SVE=Grupp] }
    { 70  ;   ;Sub Group           ;Code20        ;TableRelation="Option Sub Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Sub Group;
                                                              NOR=Undergruppe;
                                                              SVE=Undergrupp] }
    { 80  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Settlement)) "Settlement Quantity".Code WHERE (Project No.=FIELD(Project No.))
                                                                 ELSE IF (Type=CONST(Option)) Option.Option WHERE (Project No.=FIELD(Project No.),
                                                                                                                   Main Group=FIELD(Main Group),
                                                                                                                   Group=FIELD(Group),
                                                                                                                   Sub Group=FIELD(Sub Group),
                                                                                                                   Plot No.=FILTER(''));
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 100 ;   ;Recording Date      ;Date          ;CaptionML=ENU=Recording Date/Time;
                                                   Editable=No }
    { 110 ;   ;Delta Recording Quantity;Decimal   ;OnValidate=BEGIN
                                                                IF NOT gConversion THEN
                                                                  TESTFIELD("Cost Plus Entry Created", FALSE);
                                                                CalculateFields;
                                                                IF NOT gConversion THEN
                                                                  CheckCreatePlotOption;
                                                              END;

                                                   CaptionML=ENU=Delta Recording Quantity;
                                                   DecimalPlaces=0:2 }
    { 130 ;   ;Recording Value     ;Decimal       ;CaptionML=ENU=Recording Value;
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 155 ;   ;Previous Recording Quantity;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plot SQ Recording Line"."Delta Recording Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                              Plot No.=FIELD(Plot No.),
                                                                                                                              House Model=FIELD(House Model),
                                                                                                                              Unit=FIELD(Unit),
                                                                                                                              Line No.=FIELD(Line No.),
                                                                                                                              Recording Date=FIELD(Prev. Date Filter),
                                                                                                                              Phase=FILTER(Warm Recording|Production)));
                                                   CaptionML=ENU=Previous Recording Quantity;
                                                   DecimalPlaces=0:2;
                                                   Editable=No }
    { 160 ;   ;Previous Recording Value;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plot SQ Recording Line"."Recording Value" WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Plot No.=FIELD(Plot No.),
                                                                                                                     House Model=FIELD(House Model),
                                                                                                                     Unit=FIELD(Unit),
                                                                                                                     Line No.=FIELD(Line No.),
                                                                                                                     Recording Date=FIELD(Prev. Date Filter),
                                                                                                                     Phase=FILTER(Warm Recording|Production)));
                                                   CaptionML=ENU=Previous Recording Value;
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 170 ;   ;Previous Recording Date;Date       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plot SQ Recording Line"."Recording Date" WHERE (Project No.=FIELD(Project No.),
                                                                                                                       Plot No.=FIELD(Plot No.),
                                                                                                                       House Model=FIELD(House Model),
                                                                                                                       Unit=FIELD(Unit),
                                                                                                                       Line No.=FIELD(Line No.),
                                                                                                                       Phase=FIELD(Phase),
                                                                                                                       Recording Date=FIELD(Prev. Date Filter)));
                                                   CaptionML=ENU=Previous Recording Date;
                                                   Editable=No }
    { 185 ;   ;Prev. Date Filter   ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Prev. Date Filter;
                                                   Editable=No }
    { 190 ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              NOR=Dato filter;
                                                              SVE=Datumfilter];
                                                   Editable=No }
    { 200 ;   ;Additional Info     ;Text50        ;CaptionML=[ENU=Additional Info;
                                                              NOR=Tilleggsinfo;
                                                              SVE=Ytterligare info] }
    { 210 ;   ;Bill-to Customer No.;Code20        ;CaptionML=[ENU=Bill-to Customer No.;
                                                              NOR=Faktura til-kundenr.;
                                                              SVE=Faktureringskundnr];
                                                   Editable=No }
    { 220 ;   ;Cost Plus Entry Created;Boolean    ;CaptionML=[ENU=Cost Plus Entry Created;
                                                              NOR=Selvkosttransaksjon er opprettet;
                                                              SVE=Redovisad sj„lvkostnad har skapats] }
    { 230 ;   ;Creation Date Cost Plus Entry;Date ;CaptionML=[ENU=Creation Date Cost Plus Entry;
                                                              NOR=Opprettelsesdato for selvkosttransaksjon;
                                                              SVE=Skapandedatum f”r redovisad sj„lvkostnad];
                                                   Editable=No }
    { 240 ;   ;Cost Plus Line No.  ;Integer       ;CaptionML=[ENU=Cost Plus Line No.;
                                                              NOR=Radnr for selvkost;
                                                              SVE=Radnr f”r redovisag sj„lvkostnad];
                                                   Editable=No }
    { 250 ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 270 ;   ;Production Status   ;Option        ;CaptionML=[ENU=Production Status;
                                                              NOR=Produksjonsstatus;
                                                              SVE=Produktionsstatus];
                                                   OptionCaptionML=[ENU=Proposal,Offered,Approved,Expired;
                                                                    NOR=Forslag,Tilbudt,Godkjent,Forfalt;
                                                                    SVE=F”rslag,Erbjuden,Godk„nd,F”rfallen];
                                                   OptionString=Proposal,Offered,Approved,Expired;
                                                   Editable=No }
    { 330 ;   ;Phase               ;Option        ;CaptionML=ENU=Fase;
                                                   OptionCaptionML=ENU=Cold Recording,Warm Recording,Production;
                                                   OptionString=Cold Recording,Warm Recording,Production }
    { 500 ;   ;Description         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Settlement Quantity".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                               House Model=FIELD(House Model),
                                                                                                               Code=FIELD(Code)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 510 ;   ;Unit of Measure     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Settlement Quantity"."Unit of Measure" WHERE (Project No.=FIELD(Project No.),
                                                                                                                     House Model=FIELD(House Model),
                                                                                                                     Code=FIELD(Code)));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=M†ttenhet];
                                                   Editable=No }
    { 520 ;   ;Unit Price          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Settlement Quantity"."Settlement Price" WHERE (Project No.=FIELD(Project No.),
                                                                                                                      House Model=FIELD(House Model),
                                                                                                                      Code=FIELD(Code)));
                                                   CaptionML=[ENU=Unit Price;
                                                              NOR=Salgspris;
                                                              SVE=A-pris];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 600 ;   ;Cold Recording Quantity;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plot SQ Recording Line"."Delta Recording Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                              Plot No.=FIELD(Plot No.),
                                                                                                                              House Model=FIELD(House Model),
                                                                                                                              Unit=FIELD(Unit),
                                                                                                                              Line No.=FIELD(Line No.),
                                                                                                                              Recording Date=FIELD(Date Filter),
                                                                                                                              Phase=FILTER(Cold Recording)));
                                                   CaptionML=ENU=Cold Recording Quantity;
                                                   DecimalPlaces=0:2;
                                                   Editable=No }
    { 610 ;   ;Warm Recording Quantity;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plot SQ Recording Line"."Delta Recording Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                              Plot No.=FIELD(Plot No.),
                                                                                                                              House Model=FIELD(House Model),
                                                                                                                              Unit=FIELD(Unit),
                                                                                                                              Line No.=FIELD(Line No.),
                                                                                                                              Recording Date=FIELD(Date Filter),
                                                                                                                              Phase=FILTER(Warm Recording)));
                                                   CaptionML=ENU=Warm Recording Quantity;
                                                   DecimalPlaces=0:2;
                                                   Editable=No }
    { 620 ;   ;Production Recording Quantity;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Plot SQ Recording Line"."Delta Recording Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                              Plot No.=FIELD(Plot No.),
                                                                                                                              House Model=FIELD(House Model),
                                                                                                                              Unit=FIELD(Unit),
                                                                                                                              Line No.=FIELD(Line No.),
                                                                                                                              Recording Date=FIELD(Date Filter),
                                                                                                                              Phase=FILTER(Warm Recording|Production)));
                                                   CaptionML=[ENU=Production Recording Quantity;
                                                              SVE=Realiseringssantal];
                                                   DecimalPlaces=0:2;
                                                   Editable=No }
    { 630 ;   ;First Recording Seq. No.;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Min("Plot SQ Recording Line"."Recording Seq. No." WHERE (Project No.=FIELD(Project No.),
                                                                                                                        Plot No.=FIELD(Plot No.),
                                                                                                                        House Model=FIELD(House Model),
                                                                                                                        Unit=FIELD(Unit),
                                                                                                                        Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=First Recording Seq. No.;
                                                   Editable=No }
    { 640 ;   ;Last Recording Seq. No.;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Max("Plot SQ Recording Line"."Recording Seq. No." WHERE (Project No.=FIELD(Project No.),
                                                                                                                        Plot No.=FIELD(Plot No.),
                                                                                                                        House Model=FIELD(House Model),
                                                                                                                        Unit=FIELD(Unit),
                                                                                                                        Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Last Recording Seq. No.;
                                                   Editable=No }
    { 700 ;   ;RecordID Cost Plus Line;RecordID   ;CaptionML=ENU=RecordID Cost Plus Line }
  }
  KEYS
  {
    {    ;Project No.,Plot No.,Recording Seq. No.,House Model,Unit,Line No. }
    {    ;Project No.,Plot No.,Recording Seq. No.,House Model,Unit,Type,Main Group,Group,Sub Group,Code;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Project No.,Plot No.,House Model,Unit,Type,Main Group,Group,Sub Group,Code,Recording Date }
    {    ;Project No.,RecordID Cost Plus Line      }
    {    ;Project No.,Plot No.,House Model,Unit,Line No.,Phase,Recording Date;
                                                   SumIndexFields=Delta Recording Quantity,Recording Value }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=Rename %1  is not allowed.';
      RenameIsAllowed@1100528300 : Boolean;
      Text002@1100528201 : TextConst 'ENU=Project Option not found.';
      Text009@1100528200 : TextConst 'ENU=The status is %1, you can''t change the quantity;NOR=Status er %1, du kan ikke endre antallet;SVE=Status „r %1, du kan inte „ndra antalet';
      gConversion@1100528202 : Boolean;

    PROCEDURE CalculateFields@1100528600();
    BEGIN
      CALCFIELDS("Unit Price");
      "Recording Value" := "Delta Recording Quantity" * "Unit Price";
      IF GetPreviousDatefilter THEN BEGIN
        CALCFIELDS("Previous Recording Quantity", "Previous Recording Value", "Previous Recording Date")
      END;
    END;

    PROCEDURE GetPreviousDatefilter@1100528200() : Boolean;
    VAR
      PlotSQRecording@1100528200 : Record 11072251;
    BEGIN
      PlotSQRecording.SETRANGE("Project No.", "Project No.");
      PlotSQRecording.SETFILTER("Plot No.", '%1', '');
      PlotSQRecording.SETFILTER("Recording Date", '<%1', "Recording Date");
      IF NOT PlotSQRecording.ISEMPTY THEN BEGIN
        PlotSQRecording.FINDLAST;
        SETFILTER("Prev. Date Filter", '..%1', PlotSQRecording."Recording Date");
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE GetPreviousProdCumulative@1100528602() : Decimal;
    BEGIN
      SETFILTER("Prev. Date Filter", '<%1', "Recording Date");
      CALCFIELDS("Previous Recording Quantity");
      EXIT("Previous Recording Quantity");
    END;

    PROCEDURE GetPreviousColdCumulative@1100528203() : Decimal;
    VAR
      PlotSQRecordingLine@1100528200 : Record 11072252;
    BEGIN
      PlotSQRecordingLine.COPY(Rec);
      PlotSQRecordingLine.SETFILTER("Date Filter", '<%1', PlotSQRecordingLine."Recording Date");
      PlotSQRecordingLine.CALCFIELDS("Cold Recording Quantity");
      EXIT(PlotSQRecordingLine."Cold Recording Quantity");
    END;

    PROCEDURE SetRenameIsAllowed@1100528300(NewRenameIsAllowed@1100528300 : Boolean);
    BEGIN
      RenameIsAllowed := NewRenameIsAllowed;
    END;

    PROCEDURE CheckCreatePlotOption@1100528201();
    VAR
      lvOption@1100528201 : Record 11012502;
      lvPlotSQRecordingLine@1100528202 : Record 11072252;
      lvPlotRecordingCrossList@1100528200 : Record 11072253;
      lvTotal@1100528203 : Decimal;
      lvPlot@1100528204 : Record 11012500;
    BEGIN
      IF Type <> Type::Option THEN
        EXIT;

      lvOption.SETRANGE("Project No.", "Project No.");
      lvOption.SETRANGE("Plot No.", "Plot No.");
      lvOption.SETRANGE("House Model", "House Model");
      lvOption.SETRANGE("Main Group", "Main Group");
      lvOption.SETRANGE(Group, Group);
      lvOption.SETRANGE("Sub Group", "Sub Group");
      lvOption.SETRANGE(Option, Code);

      IF NOT lvOption.FINDFIRST THEN
        CopyJobOptionToPlotOption(Rec);

      lvPlot.GET("Project No.","Plot No.");

      lvOption.FINDFIRST;

      IF (lvOption.Status >= lvOption.Status::Order) AND (lvOption."Plot No." <> '') AND (lvOption.Status <> lvOption.Status::Expired) THEN
        ERROR(Text009,lvOption.Status);  //* Do not delete this test,  has consequences for project budget (note: test already exist).

      IF lvPlot."MMR Phase" <> lvPlot."MMR Phase"::PreRecording THEN BEGIN
        lvTotal := "Delta Recording Quantity" + GetPreviousProdCumulative; // because new value is not in the database yet.
      END ELSE BEGIN
        lvTotal := "Delta Recording Quantity" + GetPreviousColdCumulative; // because  new value is not in the database yet.
      END;

      lvPlotRecordingCrossList.SETRANGE("Project No.", lvOption."Project No.");
      lvPlotRecordingCrossList.SETRANGE("House Model", lvOption."House Model");
      lvPlotRecordingCrossList.SETRANGE("Main Group", lvOption."Main Group");
      lvPlotRecordingCrossList.SETRANGE(Group, lvOption.Group);
      lvPlotRecordingCrossList.SETRANGE("Sub Group", lvOption."Sub Group");
      lvPlotRecordingCrossList.SETRANGE(Code, lvOption.Option);
      //lvPlotRecordingCrossList.SETFILTER("Line No.", '<>%1',"Line No."); //Not equal to current line
      IF lvPlotRecordingCrossList.FINDSET THEN BEGIN
        REPEAT
          IF NOT ((lvPlotRecordingCrossList."Line No."="Line No.") AND (lvPlotRecordingCrossList.Unit=Unit)) THEN BEGIN
            lvPlotSQRecordingLine.SETRANGE("Project No.", lvPlotRecordingCrossList."Project No.");
            lvPlotSQRecordingLine.SETRANGE("Plot No.",  "Plot No.");
            lvPlotSQRecordingLine.SETRANGE("House Model", lvPlotRecordingCrossList."House Model");
            lvPlotSQRecordingLine.SETRANGE(Unit, lvPlotRecordingCrossList.Unit);
            lvPlotSQRecordingLine.SETRANGE("Line No.",  lvPlotRecordingCrossList."Line No.");
            lvPlotSQRecordingLine.SETFILTER("Date Filter", '..%1', "Recording Date");
            IF lvPlotSQRecordingLine.FINDLAST THEN BEGIN
              IF lvPlot."MMR Phase" <> lvPlot."MMR Phase"::PreRecording THEN BEGIN
                lvPlotSQRecordingLine.CALCFIELDS("Production Recording Quantity");
                lvTotal := lvTotal +  lvPlotSQRecordingLine."Production Recording Quantity";
              END ELSE BEGIN
                lvPlotSQRecordingLine.CALCFIELDS("Cold Recording Quantity");
                lvTotal := lvTotal +  lvPlotSQRecordingLine."Cold Recording Quantity";
              END;
            END;
          END;
        UNTIL lvPlotRecordingCrossList.NEXT = 0;
      END;

      IF lvOption.Quantities THEN BEGIN
        lvOption.Quantity := lvTotal;
        lvOption.DetermineTotal(FALSE);
        IF lvOption.Quantity  <> 0 THEN BEGIN
          IF (lvOption.Status < lvOption.Status::Order) OR (lvOption.Status = lvOption.Status::Expired)  THEN
            lvOption.VALIDATE(Status, lvOption.Status::Offered);
        END ELSE BEGIN
          lvOption.VALIDATE(Status, lvOption.Status::Expired);
        END;
        lvOption.MODIFY(TRUE);
      END ELSE BEGIN
        IF lvTotal > 0 THEN
          lvTotal := 1
        ELSE
          lvTotal := 0;
        lvOption.Quantity := lvTotal;
        lvOption.DetermineTotal(FALSE);
        IF lvOption.Quantity  <> 0 THEN BEGIN
          IF (lvOption.Status < lvOption.Status::Order) OR (lvOption.Status = lvOption.Status::Expired)  THEN
            lvOption.VALIDATE(Status, lvOption.Status::Offered);
        END ELSE BEGIN
          lvOption.VALIDATE(Status, lvOption.Status::Expired);
        END;
        lvOption.MODIFY(TRUE);

      END;
    END;

    PROCEDURE ShowDocuments@1100485003();
    VAR
      RecRef@1100525000 : RecordRef;
      DocumentFilters@1100525001 : Codeunit 11012432;
    BEGIN
      IF "Plot No." = '' THEN
        EXIT;

      //IF NOT FIND('=') THEN
      //  EXIT;

      RecRef.GETTABLE(Rec);

      DocumentFilters.CreateTempFilterAndExecute1(RecRef, '', FALSE, TRUE);
    END;

    LOCAL PROCEDURE CopyJobOptionToPlotOption@1100528202(PlotSQRecordingLine@1100528200 : Record 11072252);
    VAR
      lvOptionRec@1100528201 : Record 11012502;
      BuyersManagement@1100528202 : Codeunit 11012500;
      CopyLevel@1100528203 : 'Standard,Project,Plot';
      OptionCode@1100528204 : Code[20];
    BEGIN
      lvOptionRec.SETRANGE("Project No.", PlotSQRecordingLine."Project No.");
      lvOptionRec.SETFILTER("Plot No.", '%1', '');
      lvOptionRec.SETRANGE("House Model", PlotSQRecordingLine."House Model");
      lvOptionRec.SETRANGE("Main Group",PlotSQRecordingLine."Main Group");
      lvOptionRec.SETRANGE(Group, PlotSQRecordingLine.Group);
      lvOptionRec.SETRANGE("Sub Group", PlotSQRecordingLine."Sub Group");
      lvOptionRec.SETRANGE(Option, PlotSQRecordingLine.Code);

      IF NOT lvOptionRec.FINDSET(FALSE, FALSE) THEN
        ERROR(Text002);

      REPEAT;
        //Problem with '(' and ')' in Option (is used as a filter argument) handled by conversion to '?'   CALL T005792 Maintenance and Renovation
        OptionCode := CONVERTSTR(lvOptionRec.Option, '(', '?');
        OptionCode := CONVERTSTR(OptionCode, ')', '?');
        //

        BuyersManagement.BmCopyOptions(
          CopyLevel::Project, CopyLevel::Plot,
          PlotSQRecordingLine."Project No.", PlotSQRecordingLine."Project No.",
          '', PlotSQRecordingLine."Plot No.",
          PlotSQRecordingLine."House Model", PlotSQRecordingLine."House Model",
          PlotSQRecordingLine."Main Group", '',
          PlotSQRecordingLine.Group, '',
          PlotSQRecordingLine."Sub Group", '',
          OptionCode, '', TRUE);

      UNTIL NEXT = 0;
    END;

    PROCEDURE SetConversionStatus@1100528204(Conversion@1100528200 : Boolean);
    BEGIN
      gConversion := Conversion;
    END;

    BEGIN
    END.
  }
}

