OBJECT Codeunit 2002 Cortana Tracing
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CortanaCategoryTxt@1000 : TextConst '@@@=Locked;ENU=AL Cortana;NOR=AL Cortana';
      TraceImageAnalysisSuccessTagsTxt@1001 : TextConst '@@@=Locked;ENU="Number of Image Analysis calls: %1;Limit: %2;Period type: %3;Execution time: %4 ms;Confidence: %5.";NOR="Number of Image Analysis calls: %1;Limit: %2;Period type: %3;Execution time: %4 ms;Confidence: %5."';
      AnalysisStartTime@1002 : DateTime;
      TraceImageAnalysisSuccessTxt@1003 : TextConst '@@@=Locked;ENU="Number of Image Analysis calls: %1;Limit: %2;Period type: %3;Execution time: %4 ms.";NOR="Number of Image Analysis calls: %1;Limit: %2;Period type: %3;Execution time: %4 ms."';

    [EventSubscriber(Codeunit,2020,OnBeforeImageAnalysis)]
    LOCAL PROCEDURE TraceImageAnalysisStart@2(VAR Sender@1000 : Codeunit 2020);
    BEGIN
      AnalysisStartTime := CURRENTDATETIME;
    END;

    [EventSubscriber(Codeunit,2020,OnAfterImageAnalysis)]
    LOCAL PROCEDURE TraceImageAnalysisEnd@1(VAR Sender@1000 : Codeunit 2020;ImageAnalysisResult@1001 : Codeunit 2021);
    VAR
      CortanaIntelligenceUsage@1010 : Record 2003;
      LastError@1006 : Text;
      IsUsageLimitError@1007 : Boolean;
      LimitType@1004 : 'Year,Month,Day,Hour';
      LimitValue@1003 : Integer;
      Message@1008 : Text;
      AnalysisDuration@1002 : Integer;
      AnalysisType@1009 : 'Tags,Faces,Color';
      NoOfCalls@1011 : Integer;
    BEGIN
      IF Sender.GetLastError(LastError,IsUsageLimitError) THEN
        SENDTRACETAG('000015X',CortanaCategoryTxt,VERBOSITY::Error,LastError,DATACLASSIFICATION::SystemMetadata)
      ELSE BEGIN
        Sender.GetLimitParams(LimitType,LimitValue);

        AnalysisDuration := CURRENTDATETIME - AnalysisStartTime;
        ImageAnalysisResult.GetLatestAnalysisType(AnalysisType);
        NoOfCalls := CortanaIntelligenceUsage.GetTotalProcessingTime(CortanaIntelligenceUsage.Service::"Computer Vision");
        IF AnalysisType = AnalysisType::Tags THEN
          Message := STRSUBSTNO(TraceImageAnalysisSuccessTagsTxt,
              NoOfCalls,LimitValue,LimitType,AnalysisDuration,
              ImageAnalysisResult.TagConfidence(1))
        ELSE
          Message := STRSUBSTNO(TraceImageAnalysisSuccessTxt,
              NoOfCalls,LimitValue,LimitType,AnalysisDuration);

        SENDTRACETAG('000015Y',CortanaCategoryTxt,VERBOSITY::Normal,Message,DATACLASSIFICATION::SystemMetadata);
      END;
    END;

    BEGIN
    END.
  }
}

