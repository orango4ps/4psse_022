OBJECT Codeunit 11229308 Company Event Handling
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Table,2000000006,OnAfterDeleteEvent,"",Skip)]
    LOCAL PROCEDURE OnAfterDeleteCompany@1100529200(VAR Rec@1100529200 : Record 2000000006;RunTrigger@1100529201 : Boolean);
    VAR
      CompanyRelationsCentralMgtRec@1100529203 : Record 11012059;
      CompanyData@1100529202 : Record 11020674;
    BEGIN
      IF Rec.ISTEMPORARY OR NOT RunTrigger THEN
        EXIT;

      CompanyRelationsCentralMgtRec.SETRANGE(Company, Rec.Name);
      CompanyRelationsCentralMgtRec.DELETEALL;

      CompanyData.SETRANGE("Company Name",Rec.Name);
      CompanyData.DELETEALL;

      DeleteWorkorders(Rec);
      DeleteResources(Rec);
      DeleteResourceGroups(Rec);
    END;

    [EventSubscriber(Table,2000000006,OnAfterRenameEvent,"",Skip)]
    LOCAL PROCEDURE OnAfterRenameCompany@1100529209(VAR Rec@1100529200 : Record 2000000006;VAR xRec@1100529201 : Record 2000000006;RunTrigger@1100529204 : Boolean);
    VAR
      IntercompanyRelation@1100529206 : Record 11012057;
      IntercompanyRelation2@1100529205 : Record 11012057;
      IntercompanyEntry@1100529203 : Record 11012058;
      IntercompanyEntry2@1100529202 : Record 11012058;
    BEGIN
      IF Rec.ISTEMPORARY OR NOT RunTrigger THEN
        EXIT;

      IntercompanyRelation.RESET;
      IntercompanyRelation.SETRANGE("Supplying Company", xRec.Name);
      IF IntercompanyRelation.FIND('-') THEN BEGIN
        REPEAT
          IntercompanyRelation2 := IntercompanyRelation;
          IntercompanyRelation2."Supplying Company" := Rec.Name;
          IntercompanyRelation2.INSERT;
        UNTIL IntercompanyRelation.NEXT = 0;
        IntercompanyRelation.DELETEALL;
      END;

      IntercompanyRelation.RESET;
      IntercompanyRelation.SETRANGE("Receiving Company", xRec.Name);
      IF IntercompanyRelation.FIND('-') THEN BEGIN
        REPEAT
          IntercompanyRelation2 := IntercompanyRelation;
          IntercompanyRelation2."Receiving Company" := Rec.Name;
          IntercompanyRelation2.INSERT;
        UNTIL IntercompanyRelation.NEXT = 0;
        IntercompanyRelation.DELETEALL;
      END;

      IntercompanyEntry.RESET;
      IntercompanyEntry.SETRANGE("Supplying Company", xRec.Name);
      IF IntercompanyEntry.FIND('-') THEN BEGIN
        REPEAT
          IntercompanyEntry2 := IntercompanyEntry;
          IntercompanyEntry2."Supplying Company" := Rec.Name;
          IntercompanyEntry2.MODIFY;
        UNTIL IntercompanyEntry.NEXT = 0;
      END;

      IntercompanyEntry.RESET;
      IntercompanyEntry.SETRANGE("Receiving Company", xRec.Name);
      IF IntercompanyEntry.FIND('-') THEN BEGIN
        REPEAT
          IntercompanyEntry2 := IntercompanyEntry;
          IntercompanyEntry2."Receiving Company" := Rec.Name;
          IntercompanyEntry2.MODIFY;
        UNTIL IntercompanyEntry.NEXT = 0;
      END;

      IntercompanyEntry.RESET;
      IntercompanyEntry.SETRANGE("Post in Company", xRec.Name);
      IF IntercompanyEntry.FIND('-') THEN BEGIN
        REPEAT
          IntercompanyEntry2 := IntercompanyEntry;
          IntercompanyEntry2."Post in Company" := Rec.Name;
          IntercompanyEntry2.MODIFY;
        UNTIL IntercompanyEntry.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE DeleteWorkorders@1100528000(Company@1100529200 : Record 2000000006);
    VAR
      WorkOrderSignature@1100528003 : Record 11229280;
      WorkOrderRefPoint@1100528002 : Record 11229282;
      WorkOrder@1100528001 : Record 11229279;
      MobileDecentralWorkOrder@1100528000 : Record 11125649;
    BEGIN
      WorkOrder.SETRANGE("Source Company", Company.Name);
      IF WorkOrder.FINDSET THEN BEGIN
        MobileDecentralWorkOrder.SETCURRENTKEY("Work Order No.");
        REPEAT
          WorkOrderSignature.SETRANGE("Work Order No.", WorkOrder."No.");
          WorkOrderSignature.DELETEALL;
          //
          WorkOrderRefPoint.SETRANGE("Work Order No.", WorkOrder."No.");
          WorkOrderRefPoint.DELETEALL;
          //
          MobileDecentralWorkOrder.SETRANGE("Work Order No.", WorkOrder."No.");
          MobileDecentralWorkOrder.DELETEALL;
          //
          WorkOrder.DELETE(FALSE);
        UNTIL WorkOrder.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE DeleteResources@1100528001(Company@1100529200 : Record 2000000006);
    VAR
      Resource@1100528005 : Record 11229278;
      WorkOrder@1100528004 : Record 11229279;
      DeletedWorkOrderCache@1100528003 : Record 11229281;
      MobileDecentralWorkOrder@1100528002 : Record 11125649;
      ResourceDiscipline@1100528001 : Record 11229286;
      ResourcePlanData@1100529201 : Record 11229287;
      VisibleResourceWOP@1100528000 : Record 11229285;
    BEGIN
      Resource.SETRANGE(Company, Company.Name);
      IF Resource.FINDSET THEN BEGIN
        WorkOrder.SETCURRENTKEY("Resource No.");
        VisibleResourceWOP.SETCURRENTKEY("Resource No.");
        REPEAT
          WorkOrder.SETRANGE("Resource No.", Resource."No.");
          WorkOrder.MODIFYALL("Resource No.", '');
          //
          DeletedWorkOrderCache.SETRANGE("Resource No.", Resource."No.");
          DeletedWorkOrderCache.DELETEALL;
          //
          MobileDecentralWorkOrder.SETRANGE("Resource No. (Employee)", Resource."No.");
          MobileDecentralWorkOrder.DELETEALL;
          //
          ResourceDiscipline.SETRANGE("Resource No.", Resource."No.");
          IF ResourceDiscipline.FINDSET THEN
            REPEAT
              ResourcePlanData.SETRANGE("Resource No.",ResourceDiscipline."Resource No.");
              ResourcePlanData.SETRANGE(Discipline, ResourceDiscipline.Discipline);
              ResourcePlanData.DELETEALL;
              ResourceDiscipline.DELETE;
            UNTIL ResourceDiscipline.NEXT = 0;
          //
          VisibleResourceWOP.SETRANGE("Resource No.", Resource."No.");
          VisibleResourceWOP.DELETEALL;
          //
          Resource.DELETE(FALSE);
        UNTIL Resource.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE DeleteResourceGroups@1100529000(Company@1100529200 : Record 2000000006);
    VAR
      ResourceGroup@1100528005 : Record 11229384;
      VisibleResourceGroup@1100528000 : Record 11229385;
    BEGIN
      ResourceGroup.SETRANGE(Company, Company.Name);
      IF ResourceGroup.FINDSET THEN BEGIN
        VisibleResourceGroup.SETCURRENTKEY("Resource Group No.");
        REPEAT
          VisibleResourceGroup.SETRANGE("Resource Group No.", ResourceGroup."No.");
          VisibleResourceGroup.DELETEALL;
          //
          ResourceGroup.DELETE(FALSE);
        UNTIL ResourceGroup.NEXT = 0;
      END;
      VisibleResourceGroup.SETCURRENTKEY(Company);
      VisibleResourceGroup.SETRANGE(Company, Company.Name);
      VisibleResourceGroup.DELETEALL;
    END;

    BEGIN
    END.
  }
}

