OBJECT Table 11229446 Project Element Phase
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckNoOfProjectElementPhases;
             END;

    OnDelete=VAR
               ProjectElementPhaseLabel@1100528400 : Record 11229463;
             BEGIN
               CheckIfPhaseIsNotUsedInBudgetLines;
               CheckIfPhaseIsNotUsedInProjectElements;
               ProjectElementPhaseLabel.SETRANGE("Project No.", "Project No.");
               ProjectElementPhaseLabel.SETRANGE("Phase Code", Code);
               ProjectElementPhaseLabel.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Project Element Phase;
    LookupPageID=Page11129702;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Code                ;Code10        ;TableRelation="Base Element Phase".Code;
                                                   OnValidate=BEGIN
                                                                FillDefaultValuesOfDescriptionAndSequenceNo;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Code;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Sequence No.        ;Integer       ;OnValidate=BEGIN
                                                                CheckIfPhaseIsNotUsedInBudgetLines;
                                                              END;

                                                   CaptionML=[ENU=Sequence No.;
                                                              SVE=Sekvensnr] }
  }
  KEYS
  {
    {    ;Project No.,Code                        ;Clustered=Yes }
    {    ;Sequence No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528400 : TextConst 'ENU=The maximum number of %1s is %2.';

    LOCAL PROCEDURE CheckNoOfProjectElementPhases@1100528400();
    VAR
      ProjectElementPhase@1100528400 : Record 11229446;
    BEGIN
      ProjectElementPhase.SETRANGE("Project No.", "Project No.");
      IF COUNT >= 10 THEN
        ERROR(Text000, TABLECAPTION, 10);
    END;

    PROCEDURE LookupProjectElementPhaseCode@1100528405(VAR ProjectElement@1100528404 : Record 11012010;ProjectNo@1100528403 : Code[20];VAR ProjectElementPhaseCode@1100528402 : Code[10]);
    VAR
      ProjectElementPhase@1100528401 : Record 11229446;
      ProjectElementPhases@1100528400 : Page 11129702;
    BEGIN
      ProjectElementPhases.LOOKUPMODE := TRUE;
      ProjectElementPhase.SETRANGE("Project No.", ProjectNo);
      ProjectElementPhases.SETTABLEVIEW(ProjectElementPhase);
      IF ProjectElementPhases.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ProjectElementPhases.GETRECORD(ProjectElementPhase);
        CheckIfProjectElementsCanBeMovedToPhase(ProjectElement, ProjectNo, ProjectElementPhase.Code);
        ProjectElementPhaseCode := ProjectElementPhase.Code;
      END;
    END;

    PROCEDURE CheckIfProjectElementsCanBeMovedToPhase@1100528410(VAR ProjectElement@1100528404 : Record 11012010;ProjectNo@1100528403 : Code[20];ProjectElementPhaseCode@1100528400 : Code[10]);
    VAR
      ProjectElementPhase@1100528401 : Record 11229446;
      BudgetLine@1100528402 : Record 11012001;
    BEGIN
      ProjectElementPhase.GET(ProjectNo, ProjectElementPhaseCode);
      IF ProjectElement.FINDSET THEN
        REPEAT
          IF ProjectElement."Phase Code" = ProjectElementPhaseCode THEN
            ProjectElement.FIELDERROR("Phase Code");
          BudgetLine.SETRANGE("Project No.", ProjectNo);
          BudgetLine.SETRANGE(Element, ProjectElement.Element);
          IF BudgetLine.FINDSET THEN
            REPEAT
              IF BudgetLine."Element Phase Code" = ProjectElementPhaseCode THEN
                BudgetLine.FIELDERROR("Element Phase Code");
            UNTIL BudgetLine.NEXT = 0;
        UNTIL ProjectElement.NEXT = 0;
    END;

    LOCAL PROCEDURE FillDefaultValuesOfDescriptionAndSequenceNo@1100528401();
    VAR
      BaseElementPhase@1100528400 : Record 11229465;
    BEGIN
      IF BaseElementPhase.GET(Code) THEN BEGIN
        Description := BaseElementPhase.Description;
        "Sequence No." := BaseElementPhase."Sequence No.";
      END;
    END;

    LOCAL PROCEDURE CheckIfPhaseIsNotUsedInBudgetLines@1100528402();
    VAR
      BudgetLine@1100528400 : Record 11012001;
    BEGIN
      BudgetLine.SETRANGE("Project No.", "Project No.");
      BudgetLine.SETRANGE("Element Phase Code", Code);
      IF BudgetLine.FINDFIRST THEN
        BudgetLine.FIELDERROR("Element Phase Code");
    END;

    LOCAL PROCEDURE CheckIfPhaseIsNotUsedInProjectElements@1100528403();
    VAR
      ProjectElement@1100528400 : Record 11012010;
    BEGIN
      ProjectElement.SETRANGE("Project No.", "Project No.");
      ProjectElement.SETRANGE("Phase Code", Code);
      IF ProjectElement.FINDFIRST THEN
        ProjectElement.FIELDERROR("Phase Code");
    END;

    BEGIN
    END.
  }
}

