OBJECT Table 11012911 Project Development Phase
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CheckIfMultipleCurrentPhases;
             END;

    CaptionML=[ENU=Project Development Phase;
               NOR=Prosjektets utviklingsfase;
               SVE=Projektutvecklingsfas];
    LookupPageID=Page11012914;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation="Project Development";
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Phase               ;Code10        ;TableRelation="Standard Development Phase";
                                                   CaptionML=[ENU=Phase;
                                                              NOR=Fase;
                                                              SVE=Fas];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Development Phase".Description WHERE (Phase=FIELD(Phase)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 40  ;   ;Score Percentage    ;Decimal       ;CaptionML=[ENU=Score Percentage;
                                                              NOR=Poengprosent;
                                                              SVE=Po„ngprocent];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 50  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 60  ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 70  ;   ;Actual Phase        ;Boolean       ;OnValidate=BEGIN
                                                                IF "Actual Phase" THEN
                                                                  UpdateProjectDevelopmentWithPhase("Project No.", Phase);
                                                              END;

                                                   CaptionML=ENU=Actual Phase }
  }
  KEYS
  {
    {    ;Project No.,Phase                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'ENU=There can only be one actual phase per project.';

    LOCAL PROCEDURE CheckIfMultipleCurrentPhases@1100525000();
    VAR
      ProjectDevelopmentPhase@1100525000 : Record 11012911;
    BEGIN
      IF xRec."Actual Phase" <> Rec."Actual Phase" THEN
        IF Rec."Actual Phase" THEN BEGIN
          ProjectDevelopmentPhase.RESET;
          ProjectDevelopmentPhase.SETRANGE("Project No.", "Project No.");
          ProjectDevelopmentPhase.SETRANGE("Actual Phase", TRUE);
          IF ProjectDevelopmentPhase.FINDSET THEN
            REPEAT
              IF ProjectDevelopmentPhase.Phase <> Phase THEN
                FIELDERROR("Actual Phase", Text001);
            UNTIL ProjectDevelopmentPhase.NEXT = 0;
        END;
    END;

    PROCEDURE UpdateActualPhase@1100525001(ProjectNo@1100525002 : Code[20];Phase@1100525000 : Code[10]);
    VAR
      ProjectDevelopmentPhase@1100525001 : Record 11012911;
    BEGIN
      ProjectDevelopmentPhase.RESET;
      ProjectDevelopmentPhase.SETRANGE("Project No.", ProjectNo);
      IF ProjectDevelopmentPhase.FINDSET(TRUE) THEN
        REPEAT
          ProjectDevelopmentPhase."Actual Phase" := FALSE;
          ProjectDevelopmentPhase.MODIFY;
        UNTIL ProjectDevelopmentPhase.NEXT = 0;
      IF ProjectDevelopmentPhase.GET(ProjectNo, Phase) THEN BEGIN
        ProjectDevelopmentPhase."Actual Phase" := TRUE;
        ProjectDevelopmentPhase.MODIFY;
      END;
    END;

    LOCAL PROCEDURE UpdateProjectDevelopmentWithPhase@1100525002(ProjectNo@1100525001 : Code[20];Phase@1100525002 : Code[10]);
    VAR
      ProjectDevelopment@1100525000 : Record 11012915;
    BEGIN
      IF ProjectDevelopment.GET(ProjectNo) THEN BEGIN
        ProjectDevelopment."Actual Phase" := Phase;
        ProjectDevelopment.CALCFIELDS("Actual Phase Description");
        ProjectDevelopment.MODIFY;
      END;
    END;

    PROCEDURE GetActualPhase@1100525003(ProjectNo@1100525000 : Code[20]) : Code[10];
    VAR
      ProjectDevelopmentPhase@1100525001 : Record 11012911;
    BEGIN
      ProjectDevelopmentPhase.RESET;
      ProjectDevelopmentPhase.SETRANGE("Project No.", ProjectNo);
      ProjectDevelopmentPhase.SETRANGE("Actual Phase", TRUE);
      IF ProjectDevelopmentPhase.FINDFIRST THEN
        EXIT(ProjectDevelopmentPhase.Phase);
    END;

    BEGIN
    END.
  }
}

