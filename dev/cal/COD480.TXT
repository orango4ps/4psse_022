OBJECT Codeunit 480 Get Shortcut Dimension Values
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      TempDimSetEntry@1001 : TEMPORARY Record 480;
      DimensionSetEntry@1002 : Record 480;
      HasGotGLSetup@1004 : Boolean;
      WhenGotGLSetup@1005 : DateTime;
      GLSetupShortcutDimCode@1003 : ARRAY [8] OF Code[20];
      ReceivingCompany@1100525000 : Text[80];

    [External]
    PROCEDURE GetShortcutDimensions@2(DimSetID@1001 : Integer;VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    VAR
      i@1002 : Integer;
    BEGIN
      CLEAR(ShortcutDimCode);
      IF DimSetID = 0 THEN
        EXIT;
      GetGLSetup;
      FOR i := 1 TO 8 DO
        IF GLSetupShortcutDimCode[i] <> '' THEN
          ShortcutDimCode[i] := GetDimSetEntry(DimSetID,GLSetupShortcutDimCode[i]);
    END;

    LOCAL PROCEDURE GetDimSetEntry@4(DimSetID@1001 : Integer;DimCode@1002 : Code[20]) : Code[20];
    BEGIN
      IF ReceivingCompany = '' THEN
        DimensionSetEntry.CHANGECOMPANY(COMPANYNAME)
      ELSE
        DimensionSetEntry.CHANGECOMPANY(ReceivingCompany);

      IF TempDimSetEntry.GET(DimSetID,DimCode) THEN
        EXIT(TempDimSetEntry."Dimension Value Code");
      TempDimSetEntry.INIT;
      IF DimensionSetEntry.GET(DimSetID,DimCode) THEN
        TempDimSetEntry := DimensionSetEntry
      ELSE BEGIN
        TempDimSetEntry."Dimension Set ID" := DimSetID;
        TempDimSetEntry."Dimension Code" := DimCode;
      END;
      TempDimSetEntry.INSERT;
      EXIT(TempDimSetEntry."Dimension Value Code");
    END;

    LOCAL PROCEDURE GetGLSetup@52();
    BEGIN
      IF WhenGotGLSetup = 0DT THEN
        WhenGotGLSetup := CURRENTDATETIME;

      IF HasGotGLSetup AND
         (((ReceivingCompany <> '') AND (GLSetup.CURRENTCOMPANY <> ReceivingCompany)) OR
          ((ReceivingCompany = '') AND (GLSetup.CURRENTCOMPANY <> COMPANYNAME)))
      THEN
        HasGotGLSetup := FALSE;

      IF CURRENTDATETIME > WhenGotGLSetup + 60000 THEN
        HasGotGLSetup := FALSE;
      IF HasGotGLSetup THEN
        EXIT
      ELSE BEGIN
        IF ReceivingCompany = '' THEN
          GLSetup.CHANGECOMPANY(COMPANYNAME)
        ELSE
          GLSetup.CHANGECOMPANY(ReceivingCompany);
      END;

      GLSetup.GET;
      GLSetupShortcutDimCode[1] := GLSetup."Shortcut Dimension 1 Code";
      GLSetupShortcutDimCode[2] := GLSetup."Shortcut Dimension 2 Code";
      GLSetupShortcutDimCode[3] := GLSetup."Shortcut Dimension 3 Code";
      GLSetupShortcutDimCode[4] := GLSetup."Shortcut Dimension 4 Code";
      GLSetupShortcutDimCode[5] := GLSetup."Shortcut Dimension 5 Code";
      GLSetupShortcutDimCode[6] := GLSetup."Shortcut Dimension 6 Code";
      GLSetupShortcutDimCode[7] := GLSetup."Shortcut Dimension 7 Code";
      GLSetupShortcutDimCode[8] := GLSetup."Shortcut Dimension 8 Code";
      HasGotGLSetup := TRUE;
      WhenGotGLSetup := CURRENTDATETIME;
    END;

    [EventSubscriber(Table,98,OnAfterModifyEvent)]
    LOCAL PROCEDURE OnGLSetupModify@1(VAR Rec@1000 : Record 98;VAR xRec@1001 : Record 98;RunTrigger@1002 : Boolean);
    BEGIN
      HasGotGLSetup := FALSE;
    END;

    PROCEDURE SetCompany@1100485005(Company@1100485000 : Text[80]);
    BEGIN
      //**4PS
      ReceivingCompany := Company;
    END;

    BEGIN
    END.
  }
}

