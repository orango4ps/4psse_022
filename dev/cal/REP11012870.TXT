OBJECT Report 11012870 Update Maint. Sales Rates
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aktualisieren Objekttarife;
               ENU=Update Maintenance Sales Rates;
               NLD=Bijwerken objecttarieven;
               NOR=Oppdater salgsrater for vedlikehold;
               SVE=Uppdatera s„ljavgifter f”r underh†ll];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NewReferenceDate = 0D THEN
                    NewReferenceDate := TODAY();
                END;

  }
  DATASET
  {
    { 2743;    ;DataItem;                    ;
               DataItemTable=Table11020341;
               OnPreDataItem=VAR
                               CentralDataManagement@1100528600 : Codeunit 11012010;
                             BEGIN
                               IF ("Object Group Type" = "Object Group Type"::Standard) AND ("Customer No." = '') THEN
                                 IF NOT CentralDataManagement.ObjectSalesRateFormEditable THEN
                                   CurrReport.BREAK;

                               CreateNewObjectRates("Maintenance Sales Rate", NewReferenceDate, ObjectRatePerc, RoundRateAmnt);
                               CurrReport.BREAK;
                             END;

               ReqFilterFields=Service Package,Object Group,Object Group Type,Customer No.,Reference Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Aktualisieren mit Prozents„tzen;
                             ENU=Update With Percentage;
                             NLD=Bijwerken met percentage;
                             NOR=Oppdater med prosentsats;
                             SVE=Uppdatera med procentsats];
                  SourceExpr=ObjectRatePerc }

      { 1100525003;2;Field  ;
                  CaptionML=[DEU=Rundungsbetrag;
                             ENU=Round Amount;
                             NLD=Afrondingsbedrag;
                             NOR=Avrundet bel›p;
                             SVE=Avrundat belopp];
                  SourceExpr=RoundRateAmnt }

      { 1100525004;2;Field  ;
                  CaptionML=[DEU=Neues Indexierungsdatum;
                             ENU=New Index Date;
                             NLD=Nieuwe indexeringsdatum;
                             NOR=Ny indeksdato;
                             SVE=Nytt indexdatum];
                  SourceExpr=NewReferenceDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      NewReferenceDate@1100526200 : Date;
      ObjectRatePerc@1100526201 : Decimal;
      RoundRateAmnt@1100526202 : Decimal;

    LOCAL PROCEDURE CreateNewObjectRates@1100526200(VAR IMaintenanceSalesRate@1100526203 : Record 11020341;NewReferenceDate@1100526202 : Date;ObjectRatePerc@1100526201 : Decimal;RoundRateAmnt@1100526200 : Decimal);
    VAR
      MaintenanceSalesRate@1100526205 : Record 11020341;
      LastMaintenanceSalesRate@1100526206 : Record 11020341;
      TempObjectRate@1100526204 : TEMPORARY Record 11020341;
    BEGIN
      MaintenanceSalesRate.COPY(IMaintenanceSalesRate);
      MaintenanceSalesRate.SETCURRENTKEY("Service Package", "Object Group", "Object Group Type",
        "Customer No.");
      MaintenanceSalesRate.ASCENDING(FALSE);
      IF NOT MaintenanceSalesRate.FIND('-') THEN
        EXIT;

      REPEAT
        IF (LastMaintenanceSalesRate."Service Package" <> MaintenanceSalesRate."Service Package") OR
          (LastMaintenanceSalesRate."Object Group" <> MaintenanceSalesRate."Object Group") OR
          (LastMaintenanceSalesRate."Object Group Type" <> MaintenanceSalesRate."Object Group Type") OR
          (LastMaintenanceSalesRate."Customer No." <>   LastMaintenanceSalesRate."Customer No.") THEN BEGIN

          CreateNewObjectRateInTempTable(MaintenanceSalesRate, TempObjectRate, NewReferenceDate, ObjectRatePerc, RoundRateAmnt);
          LastMaintenanceSalesRate := MaintenanceSalesRate;
        END;
      UNTIL MaintenanceSalesRate.NEXT = 0;

      CopyRatesFromTempToRealTable(TempObjectRate);
    END;

    LOCAL PROCEDURE CreateNewObjectRateInTempTable@1100526201(ObjectRate@1100526204 : Record 11020341;VAR TempObjectRate@1100526203 : Record 11020341;NewReferenceDate@1100526202 : Date;ObjectRatePerc@1100526201 : Decimal;RoundRateAmnt@1100526200 : Decimal);
    BEGIN
      TempObjectRate := ObjectRate;
      TempObjectRate."Sales Rate" := TempObjectRate."Sales Rate" * (1 + ObjectRatePerc / 100);
      IF RoundRateAmnt <> 0 THEN
        TempObjectRate."Sales Rate" := ROUND(TempObjectRate."Sales Rate", RoundRateAmnt)
      ELSE
        TempObjectRate."Sales Rate" := ROUND(TempObjectRate."Sales Rate", 0.01);
      TempObjectRate."Reference Date" := NewReferenceDate;
      TempObjectRate.INSERT;
    END;

    LOCAL PROCEDURE CopyRatesFromTempToRealTable@1100526202(VAR TempObjectRate@1100526200 : Record 11020341);
    VAR
      ObjectRate@1100526201 : Record 11020341;
    BEGIN
      IF TempObjectRate.FINDSET THEN
        REPEAT
           IF ObjectRate.GET(TempObjectRate."Service Package", TempObjectRate."Object Group", TempObjectRate."Object Group Type",
             TempObjectRate."Customer No.", TempObjectRate."Reference Date") THEN BEGIN
             ObjectRate."Sales Rate" := TempObjectRate."Sales Rate";
             ObjectRate.MODIFY(TRUE);
           END ELSE BEGIN
             ObjectRate := TempObjectRate;
             ObjectRate.INSERT(TRUE);
           END;
        UNTIL TempObjectRate.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

