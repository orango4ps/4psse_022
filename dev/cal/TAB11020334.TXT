OBJECT Table 11020334 Standard Object
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               ServSetup.GET;
               IF "No." = '' THEN BEGIN
                 ServSetup.TESTFIELD("Standard Object Nos.");
                 NoSeriesMgt.InitSeries(ServSetup."Standard Object Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
               TESTFIELD("No.");

               "Last Date Modified" := TODAY;

               CentralDataManagement.EditStandardObject(Rec, 1, xRec);

               "Tender Code System 1" := ServSetup."Tender Code System 1";
               "Tender Code System 2" := ServSetup."Tender Code System 2";
               "Copy Maintenance Schemes" := TRUE;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;

               CentralDataManagement.EditStandardObject(Rec, 2, xRec);
             END;

    OnDelete=VAR
               StdObjectMaintenanceScheme@1100528600 : Record 11071703;
               MaintenanceSchemeVersion@1100528601 : Record 11071743;
               ServiceInspectionVersion@1100528603 : Record 11071750;
               ObjectSpecificationLine@1100528604 : Record 11071781;
               ObjectMaterialInventory@1100528605 : Record 11071712;
               ObjectConsumptionComponent@1100528800 : Record 11071969;
               Item@1100528602 : Record 27;
             BEGIN
               CommLineRec.SETRANGE("Table Name", CommLineRec."Table Name"::"Standard Object");
               CommLineRec.SETRANGE("No.", "No.");
               CommLineRec.DELETEALL;

               TestNotInUse;

               StdObjectMaintenanceScheme.SETRANGE("Object No.", "No.");
               StdObjectMaintenanceScheme.DELETEALL(TRUE);

               MaintenanceSchemeVersion.SETFILTER("Line Type", '%1|%2|%3',
                 MaintenanceSchemeVersion."Line Type"::"Standard Object",
                 MaintenanceSchemeVersion."Line Type"::"Standard Object Deinstallation",
                 MaintenanceSchemeVersion."Line Type"::"Standard Object Installation");
               MaintenanceSchemeVersion.SETRANGE(Code, "No.");
               MaintenanceSchemeVersion.DELETEALL(TRUE);

               ServiceInspectionVersion.SETFILTER("Inspection Type", '%1|%2',
                 ServiceInspectionVersion."Inspection Type"::"Standard Object",
                 ServiceInspectionVersion."Inspection Type"::"Standard Object (Calls)");
               ServiceInspectionVersion.SETRANGE(Code, "No.");
               ServiceInspectionVersion.DELETEALL(TRUE);

               ObjectSpecificationLine.SETRANGE("Specification Type", ObjectSpecificationLine."Specification Type"::"Standard Object");
               ObjectSpecificationLine.SETRANGE(Code, "No.");
               ObjectSpecificationLine.DELETEALL(TRUE);

               ObjectMaterialInventory.SETRANGE(Type, ObjectMaterialInventory.Type::"Standard Object");
               ObjectMaterialInventory.SETRANGE(Code, "No.");
               ObjectMaterialInventory.DELETEALL(TRUE);

               CentralDataManagement.EditStandardObject(Rec, 4, xRec);

               ObjectConsumptionComponent.SETRANGE("Standard Object", "No.");
               ObjectConsumptionComponent.DELETEALL(TRUE);

               Item.SETRANGE("Standard Object No.", "No.");
               Item.MODIFYALL("Standard Object No.", '', TRUE);

               ExchangeMOB('D');
             END;

    OnRename=BEGIN
               ExchangeMOB('R');
             END;

    CaptionML=[ENU=Standard Object;
               NOR=Standardobjekt;
               SVE=Standardobjekt:];
    LookupPageID=Page11072547;
    DrillDownPageID=Page11072547;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=VAR
                                                                MainRec@1100528600 : Record 11020334;
                                                              BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  MainRec := xRec;
                                                                  IF MainRec.FIND THEN
                                                                    CentralDataManagement.EditStandardObject(Rec, 3, xRec);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 20  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 25  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 30  ;   ;Object Group        ;Code10        ;TableRelation="Service Group".No. WHERE (Customer No.=CONST());
                                                   CaptionML=[ENU=Object Group;
                                                              NOR=Objektgruppe;
                                                              SVE=Objektgrupp] }
    { 31  ;   ;Object Group Description;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Group".Description WHERE (No.=FIELD(Object Group),
                                                                                                         Customer No.=CONST()));
                                                   CaptionML=[ENU=Object Group Description;
                                                              NOR=Objektgruppe, beskrivelse;
                                                              SVE=Objektgrupp, beskrivning];
                                                   Editable=No }
    { 40  ;   ;Text                ;Text250       ;CaptionML=[ENU=Text;
                                                              NOR=Tekst;
                                                              SVE=Ben„mning] }
    { 45  ;   ;Type Code           ;Code20        ;TableRelation="Service Object Type".Code WHERE (Brand Code=FIELD(Brand Code),
                                                                                                   Model Code=FIELD(Model Code));
                                                   OnValidate=VAR
                                                                ServiceSetup@1100528601 : Record 11012800;
                                                                ServiceObject@1100528600 : Record 11071691;
                                                              BEGIN
                                                                CALCFIELDS("Type Description");
                                                                IF xRec."Type Code" <> "Type Code" THEN BEGIN
                                                                  ServiceSetup.GET;
                                                                  IF ServiceSetup."Sync. Object Brand/Model/Type" THEN BEGIN
                                                                    ServiceObject.SETCURRENTKEY("Standard Object No.");
                                                                    ServiceObject.SETRANGE("Standard Object No.", "No.");
                                                                    ServiceObject.MODIFYALL("Type Code", "Type Code", TRUE);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp] }
    { 51  ;   ;Type Description    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Type".Description WHERE (Brand Code=FIELD(Brand Code),
                                                                                                               Model Code=FIELD(Model Code),
                                                                                                               Code=FIELD(Type Code)));
                                                   CaptionML=[ENU=Type Description;
                                                              NOR=Typebeskrivelse;
                                                              SVE=Typbeskrivning];
                                                   Editable=No }
    { 55  ;   ;Brand Code          ;Code20        ;TableRelation="Service Object Brand";
                                                   OnValidate=VAR
                                                                ServiceSetup@1100528600 : Record 11012800;
                                                                ServiceObject@1100528601 : Record 11071691;
                                                              BEGIN
                                                                IF xRec."Brand Code" <> "Brand Code" THEN
                                                                  VALIDATE("Model Code", '');
                                                                CALCFIELDS("Brand Description");
                                                                IF xRec."Brand Code" <> "Brand Code" THEN BEGIN
                                                                  ServiceSetup.GET;
                                                                  IF ServiceSetup."Sync. Object Brand/Model/Type" THEN BEGIN
                                                                    ServiceObject.SETCURRENTKEY("Standard Object No.");
                                                                    ServiceObject.SETRANGE("Standard Object No.", "No.");
                                                                    ServiceObject.MODIFYALL("Brand Code", "Brand Code", TRUE);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Brand;
                                                              NOR=Varemerke;
                                                              SVE=M„rke] }
    { 61  ;   ;Brand Description   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Brand".Description WHERE (Code=FIELD(Brand Code)));
                                                   CaptionML=[ENU=Brand Description;
                                                              SVE=M„rke];
                                                   Editable=No }
    { 65  ;   ;Model Code          ;Code20        ;TableRelation="Service Object Model".Code WHERE (Brand Code=FIELD(Brand Code));
                                                   OnValidate=VAR
                                                                ServiceSetup@1100528600 : Record 11012800;
                                                                ServiceObject@1100528601 : Record 11071691;
                                                              BEGIN
                                                                IF xRec."Model Code" <> "Model Code" THEN
                                                                  VALIDATE("Type Code", '');
                                                                CALCFIELDS("Model Description");
                                                                IF xRec."Model Code" <> "Model Code" THEN BEGIN
                                                                  ServiceSetup.GET;
                                                                  IF ServiceSetup."Sync. Object Brand/Model/Type" THEN BEGIN
                                                                    ServiceObject.SETCURRENTKEY("Standard Object No.");
                                                                    ServiceObject.SETRANGE("Standard Object No.", "No.");
                                                                    ServiceObject.MODIFYALL("Model Code", "Model Code", TRUE);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Model;
                                                              NOR=Modell;
                                                              SVE=Modell] }
    { 71  ;   ;Model Description   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Model".Description WHERE (Brand Code=FIELD(Brand Code),
                                                                                                                Code=FIELD(Model Code)));
                                                   CaptionML=[ENU=Model Description;
                                                              NOR=Modellbeskrivelse;
                                                              SVE=Modell];
                                                   Editable=No }
    { 80  ;   ;Warranty Code       ;Code10        ;TableRelation="Service Warranty";
                                                   CaptionML=[ENU=Warranty Code;
                                                              NOR=Garantikode;
                                                              SVE=Garantikod] }
    { 90  ;   ;Theoretical Life Span;DateFormula  ;CaptionML=[ENU=Theoretical Life Span;
                                                              NOR=Teoretisk levetid;
                                                              SVE=Teoretisk livsl„ngd] }
    { 100 ;   ;Capacity            ;Decimal       ;CaptionML=[ENU=Capacity;
                                                              NOR=Kapasitet;
                                                              SVE=Kapacitet];
                                                   BlankZero=Yes }
    { 105 ;   ;Capacity Unit       ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Capacity Unit;
                                                              NOR=Kapasitetsenhet;
                                                              SVE=Kapacitetsenhet] }
    { 130 ;   ;Maintenance Period  ;DateFormula   ;FieldClass=Normal;
                                                   CaptionML=[ENU=Maintenance Period;
                                                              NOR=Vedlikeholdsperiode;
                                                              SVE=Underh†llsperiod] }
    { 150 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serier];
                                                   Editable=No }
    { 151 ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(Standard Object),
                                                                                           No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              NOR=Kommentar;
                                                              SVE=Kommentar];
                                                   Editable=No }
    { 170 ;   ;Default Service Package OBS.;Code10;TableRelation="Service Package".Code WHERE (Select for Maint. Sales Rate=CONST(Yes));
                                                   CaptionML=ENU=Default Service Package OBSOLETE;
                                                   Description=OBSOLETE }
    { 180 ;   ;Service Package Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Package".Description WHERE (Code=FIELD(Default Service Package OBS.)));
                                                   CaptionML=[ENU=Service Package Description;
                                                              NOR=Servicepakke, beskrivelse;
                                                              SVE=Servicepaket, beskrivning];
                                                   Editable=No }
    { 190 ;   ;Expired             ;Boolean       ;OnValidate=VAR
                                                                DefMaintSchServObject@1100528601 : Record 11071703;
                                                                MaintenanceSchemeObjects@1100528600 : Record 11071745;
                                                              BEGIN
                                                                IF (CurrFieldNo = FIELDNO(Expired)) AND NOT Expired THEN BEGIN
                                                                  DefMaintSchServObject.SETRANGE("Object No.", "No.");
                                                                  IF DefMaintSchServObject.FINDSET THEN
                                                                    REPEAT
                                                                      IF DefMaintSchServObject."Maintenance Scheme" <> '' THEN BEGIN
                                                                        MaintenanceSchemeObjects.GET(DefMaintSchServObject."Maintenance Scheme");
                                                                        MaintenanceSchemeObjects.TESTFIELD(Expired, FALSE);
                                                                      END;
                                                                    UNTIL DefMaintSchServObject.NEXT = 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Expired;
                                                              NOR=Utg†tt;
                                                              SVE=Utg†ngen] }
    { 200 ;   ;Checklist URL       ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Checklist URL;
                                                              NOR=Sjekkliste for URL;
                                                              SVE=Kontrollista f”r URL] }
    { 210 ;   ;Discipline          ;Code10        ;TableRelation=Discipline;
                                                   OnValidate=BEGIN
                                                                ApplySMAgreements;
                                                              END;

                                                   CaptionML=[ENU=Discipline;
                                                              NOR=Disiplin;
                                                              SVE=Disciplin] }
    { 220 ;   ;Trade Speciality    ;Code10        ;TableRelation="Trade Speciality".Code WHERE (Discipline=FIELD(Discipline));
                                                   OnValidate=BEGIN
                                                                ApplySMAgreements;
                                                              END;

                                                   CaptionML=[ENU=Trade Speciality;
                                                              NOR=SNI-nummer;
                                                              SVE=SNI-nummer] }
    { 230 ;   ;Tender Code System 1;Code10        ;TableRelation="Coding System"."Coding System";
                                                   OnValidate=BEGIN
                                                                IF "Tender Code System 1" <> xRec."Tender Code System 1" THEN
                                                                  "Tender Code 1" := '';
                                                              END;

                                                   CaptionML=[ENU=Tender Code System 1;
                                                              NOR=Tilbudskodesystem 1;
                                                              SVE=Standardandbudkodsystem];
                                                   Editable=No }
    { 240 ;   ;Tender Code 1       ;Code20        ;TableRelation="Code per Coding System".Code WHERE (Coding System=FIELD(Tender Code System 1));
                                                   CaptionML=[ENU=Tender Code 1;
                                                              NOR=Tilbudskode 1;
                                                              SVE=Anbudskod] }
    { 260 ;   ;Tender Code System 2;Code10        ;TableRelation="Coding System"."Coding System";
                                                   OnValidate=BEGIN
                                                                IF "Tender Code System 2" <> xRec."Tender Code System 2" THEN
                                                                  "Tender Code 2" := '';
                                                              END;

                                                   CaptionML=[ENU=Tender Code System 2;
                                                              NOR=Tilbudskodesystem 2;
                                                              SVE=Anbudskodsystem 2];
                                                   Editable=No }
    { 270 ;   ;Tender Code 2       ;Code20        ;TableRelation="Code per Coding System".Code WHERE (Coding System=FIELD(Tender Code System 2));
                                                   CaptionML=[ENU=Tender Code 2;
                                                              NOR=Tilbudskode 2;
                                                              SVE=Anbudskod 2] }
    { 280 ;   ;NEN2767 Element Code;Code20        ;TableRelation="NEN2767 Element".No.;
                                                   OnValidate=BEGIN
                                                                IF "NEN2767 Element Code" <> '' THEN
                                                                  NEN2767Management.TestNEN2767Licensed;

                                                                IF GUIALLOWED THEN
                                                                  IF CurrFieldNo = FIELDNO("NEN2767 Element Code") THEN
                                                                    IF xRec."NEN2767 Element Code" <> "NEN2767 Element Code" THEN
                                                                      IF CONFIRM(STRSUBSTNO(Text003, FIELDCAPTION("NEN2767 Element Code"))) THEN
                                                                        UpdateNEN2767OnObjects;
                                                              END;

                                                   CaptionML=[ENU=NEN2767 Element;
                                                              NOR=NEN2767-element;
                                                              SVE=NEN2767 Element] }
    { 290 ;   ;NEN2767 Element Description;Text100;FieldClass=FlowField;
                                                   CalcFormula=Lookup("NEN2767 Element".Description WHERE (No.=FIELD(NEN2767 Element Code)));
                                                   CaptionML=ENU=NEN2767 Element Description;
                                                   Editable=No }
    { 300 ;   ;Default Cond. Meas. Method OLD;Option;
                                                   OnValidate=BEGIN
                                                                NEN2767Management.TestNEN2767Licensed;
                                                              END;

                                                   CaptionML=ENU=Standard Condition Measurement Method OLD;
                                                   OptionCaptionML=[ENU=Scale,Repair Cost;
                                                                    NOR=Skala,Reparasjonskost;
                                                                    SVE=Skala,Reperationskostnad];
                                                   OptionString=Scale,Repair Cost;
                                                   Description=OLD }
    { 310 ;   ;Copy Maintenance Schemes;Boolean   ;CaptionML=[ENU=Copy Maintenance Schemes;
                                                              NOR=Kopier vedlikeholdsskjemaer] }
    { 320 ;   ;Subcontracting      ;Boolean       ;CaptionML=[ENU=Subcontracting;
                                                              NOR=Underentrepen›r;
                                                              SVE=Underentreprenad] }
    { 330 ;   ;Base Inspection List Code;Code20   ;TableRelation="Base Service Inspection List";
                                                   OnValidate=VAR
                                                                StdObjectMaintenanceScheme@1100528600 : Record 11071703;
                                                              BEGIN
                                                                IF "Base Inspection List Code" <> '' THEN BEGIN
                                                                  StdObjectMaintenanceScheme.SETRANGE("Object No.", "No.");
                                                                  IF NOT StdObjectMaintenanceScheme.ISEMPTY THEN
                                                                    ERROR(Text001, FIELDCAPTION("Base Inspection List Code"), StdObjectMaintenanceScheme.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Base Inspection List;
                                                              NOR=Basisinspeksjonsliste;
                                                              SVE=Basinspektionslista] }
    { 340 ;   ;Hide in Service Portal;Boolean     ;CaptionML=[ENU=Hide in Service Portal;
                                                              SVE=D”lj i serviceportal] }
    { 350 ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=VAR
                                                                Item@1100528600 : Record 27;
                                                                UpdateDescription@1100528601 : Boolean;
                                                              BEGIN
                                                                IF Item.GET("Item No.") THEN BEGIN
                                                                  UpdateDescription := TRUE;
                                                                  IF ((Description <> '') AND (Description <> Item.Description)) OR
                                                                     (("Description 2" <> '') AND ("Description 2" <> Item."Description 2"))
                                                                  THEN
                                                                    UpdateDescription := CONFIRM(Text002, TRUE, Item.Description, Item."Description 2");
                                                                  IF UpdateDescription THEN BEGIN
                                                                    VALIDATE(Description, Item.Description);
                                                                    VALIDATE("Description 2", Item."Description 2");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr] }
    { 360 ;   ;Quantity Based on   ;Option        ;CaptionML=[ENU=Quantity Based on;
                                                              SVE=Antal baserat p†];
                                                   OptionCaptionML=ENU=Service Object,Contract Object;
                                                   OptionString=Service Object,Contract Object }
    { 370 ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Oppdateringsdato;
                                                              SVE=Uppdateringsdatum] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Brand Code,Model Code,Type Code          }
    {    ;Object Group                             }
    {    ;Tender Code 1                            }
    {    ;Tender Code 2                            }
    {    ;Item No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServSetup@1100525000 : Record 11012800;
      CommLineRec@1100525005 : Record 97;
      NoSeriesMgt@1100525001 : Codeunit 396;
      CentralDataManagement@1100528600 : Codeunit 11012010;
      Text000@1100528601 : TextConst 'ENU=Standard Object %1 in use at table %2;NOR=Standardobjekt %1 i bruk i tabell %2';
      NEN2767Management@1100528602 : Codeunit 11012863;
      Text001@1100528603 : TextConst 'ENU=Field %1 can only be used when no %2 lines are present';
      Text002@1100528604 : TextConst 'ENU=Replace existing description by %1/%2?;SVE=Ers„tt befintlig beskrivning med %1/%2';
      Text003@1100528605 : TextConst 'ENU=Also update %1 on objects with this field empty?';

    PROCEDURE AssistEdit@2(OldObjectRec@11012000 : Record 11020334) : Boolean;
    VAR
      ObjectRec@1100525000 : Record 11020334;
    BEGIN
      WITH ObjectRec DO BEGIN
        ObjectRec := Rec;
        ServSetup.GET;
        ServSetup.TESTFIELD("Standard Object Nos.");
        IF NoSeriesMgt.SelectSeries(ServSetup."Standard Object Nos.",OldObjectRec."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := ObjectRec;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE TestNotInUse@1100528600();
    VAR
      ServiceObject@1100528600 : Record 11071691;
      ServSurvLocationObject@1100528601 : Record 11071813;
      ObjectModification@1100528602 : Record 11071700;
    BEGIN
      ServiceObject.SETRANGE("Standard Object No.", "No.");
      IF NOT ServiceObject.ISEMPTY THEN
        ERROR(Text000, "No.", ServiceObject.TABLECAPTION);

      ServSurvLocationObject.SETRANGE("Standard Object No.", "No.");
      IF NOT ServSurvLocationObject.ISEMPTY THEN
        ERROR(Text000, "No.", ServSurvLocationObject.TABLECAPTION);

      ObjectModification.SETRANGE("Standard Object No.", "No.");
      IF NOT ObjectModification.ISEMPTY THEN
        ERROR(Text000, "No.", ObjectModification.TABLECAPTION);
    END;

    PROCEDURE ApplySMAgreements@1100528606();
    VAR
      ServiceObject@1100528603 : Record 11071691;
      ServiceContract@1100528602 : Record 11012812;
      ContractObject@1100528601 : Record 11071702;
      MaintenanceManagement@1100528600 : Codeunit 11012829;
    BEGIN
      IF MODIFY(TRUE) THEN; // Needed for HandleSMAgreement();
      ServiceObject.SETCURRENTKEY("Standard Object No.");
      ServiceObject.SETRANGE("Standard Object No.", "No.");
      IF ServiceObject.FINDSET THEN
        REPEAT
          MaintenanceManagement.GetActualContractDSForObject(ServiceObject."No.", TODAY, ServiceContract);
          IF ServiceContract.FINDSET THEN
            REPEAT
              IF ContractObject.GET(ServiceContract."No.", ServiceObject."No.") THEN BEGIN
                ContractObject.HandleSMAgreement(TRUE, TRUE);
                ContractObject.MODIFY(TRUE);
              END;
            UNTIL ServiceContract.NEXT = 0;
        UNTIL ServiceObject.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateNEN2767OnObjects@1100528601();
    VAR
      ServiceObject@1100528600 : Record 11071691;
    BEGIN
      ServiceObject.SETCURRENTKEY("Standard Object No.");
      ServiceObject.SETRANGE("Standard Object No.", "No.");
      ServiceObject.MODIFYALL("NEN2767 Element Code", "NEN2767 Element Code", TRUE);
    END;

    LOCAL PROCEDURE ExchangeMOB@1100528703(Mode@1100528702 : Code[1]);
    VAR
      JobsSetup@1100528700 : Record 315;
      DeletedDataMgtMobile@1100528701 : Codeunit 11012440;
    BEGIN
      IF NOT JobsSetup.IsFSAActive THEN
        EXIT;

      IF (Mode = 'D') OR (Mode = 'R') THEN
        DeletedDataMgtMobile.StoreDeletedStandardObject(Rec);
    END;

    BEGIN
    END.
  }
}

