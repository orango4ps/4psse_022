OBJECT Page 11127295 Copy Gen. Checklist Templ. V.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Generische Checklistenvorlagenversion kopieren;
               ENU=Copy Generic Checklist Template Version;
               NLD=Generieke checklijstsjabloonversie kopi‰ren];
    PageType=StandardDialog;
    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK, ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1100526000;;Container;
                ContainerType=ContentArea }

    { 1100526001;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopi‰ren van] }

    { 1100526003;2;Field  ;
                Name=SourceTemplateCode;
                CaptionML=[DEU=Checklistenvorlage;
                           ENU=Checklist Template Code;
                           NLD=Checklijsttsjabloon];
                SourceExpr=SourceTemplateCode;
                TableRelation="Gen. Checklist Templ.".Code;
                OnLookup=VAR
                           GenChecklistTemplVersion@1100526000 : Record 11125894;
                         BEGIN
                           IF GenChecklistTemplVersion.GET(SourceTemplateCode, SourceTemplateVersion) THEN;
                           IF PAGE.RUNMODAL(0, GenChecklistTemplVersion) = ACTION::LookupOK THEN BEGIN
                             SourceTemplateCode := GenChecklistTemplVersion."Checklist Template Code";
                             SourceTemplateVersion := GenChecklistTemplVersion.Version;
                           END;
                         END;
                          }

    { 1100526004;2;Field  ;
                Name=SourceTemplateVersion;
                CaptionML=[DEU=Version;
                           ENU=Version;
                           NLD=Versie];
                SourceExpr=SourceTemplateVersion;
                TableRelation="Gen. Checklist Templ. Version".Version }

    { 1100526002;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopi‰ren naar] }

    { 1100526006;2;Field  ;
                Name=TargetTemplateCode;
                CaptionML=[DEU=Checklistenvorlage;
                           ENU=Checklist Template Code;
                           NLD=Checklijsttsjabloon];
                SourceExpr=TargetTemplateCode;
                TableRelation="Gen. Checklist Templ.".Code }

    { 1100526005;2;Field  ;
                Name=TargetTemplateVersion;
                CaptionML=[DEU=Version;
                           ENU=Version;
                           NLD=Versie];
                SourceExpr=TargetTemplateVersion }

  }
  CODE
  {
    VAR
      CloseActionOK@1100526000 : Boolean;
      SourceTemplateCode@1100526001 : Code[20];
      SourceTemplateVersion@1100526002 : Integer;
      TargetTemplateCode@1100526004 : Code[20];
      TargetTemplateVersion@1100526003 : Integer;
      Text001@1100526005 : TextConst 'DEU=%1 %2 existiert nicht.;ENU=%1 %2 does not exist.;NLD=%1 %2 bestaat niet.';
      Text002@1100526006 : TextConst 'DEU=%1 %2 ist bereits vorhanden.;ENU=%1 %2 already exists.;NLD=%1 %2 bestaat reeds.';

    LOCAL PROCEDURE StdDialogConfirm@1100528402() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      CopyTemplate;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    LOCAL PROCEDURE CopyTemplate@1100526000();
    VAR
      SourceGenChecklistTemplVersion@1100526000 : Record 11125894;
      SourceGenChecklistTemplLine@1100526002 : Record 11125895;
      TargetGenChecklistTemplVersion@1100526001 : Record 11125894;
      TargetGenChecklistTemplLine@1100526003 : Record 11125895;
    BEGIN
      ValidateSelection();

      SourceGenChecklistTemplVersion.SETRANGE("Checklist Template Code", SourceTemplateCode);
      SourceGenChecklistTemplVersion.SETRANGE(Version, SourceTemplateVersion);
      IF SourceGenChecklistTemplVersion.FINDFIRST THEN BEGIN
        TargetGenChecklistTemplVersion.TRANSFERFIELDS(SourceGenChecklistTemplVersion);
        TargetGenChecklistTemplVersion."Checklist Template Code" := TargetTemplateCode;
        TargetGenChecklistTemplVersion.Version := TargetTemplateVersion;
        TargetGenChecklistTemplVersion.INSERT;
        SourceGenChecklistTemplLine.SETRANGE("Checklist Template Code", SourceGenChecklistTemplVersion."Checklist Template Code");
        SourceGenChecklistTemplLine.SETRANGE("Checklist Template Version", SourceGenChecklistTemplVersion.Version);
        IF SourceGenChecklistTemplLine.FINDSET THEN
          REPEAT
            TargetGenChecklistTemplLine.TRANSFERFIELDS(SourceGenChecklistTemplLine);
            TargetGenChecklistTemplLine."Checklist Template Code" := TargetGenChecklistTemplVersion."Checklist Template Code";
            TargetGenChecklistTemplLine."Checklist Template Version" := TargetGenChecklistTemplVersion.Version;
            TargetGenChecklistTemplLine.INSERT;
          UNTIL SourceGenChecklistTemplLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE ValidateSelection@1100526001();
    VAR
      GenChecklistTemplVersion@1100526000 : Record 11125894;
    BEGIN
      IF NOT GenChecklistTemplVersion.GET(SourceTemplateCode, SourceTemplateVersion) THEN
        ERROR(Text001, STRSUBSTNO('%1 %2', SourceTemplateCode, SourceTemplateVersion), GenChecklistTemplVersion.TABLECAPTION);

      IF GenChecklistTemplVersion.GET(TargetTemplateCode, TargetTemplateVersion) THEN
        ERROR(Text002, STRSUBSTNO('%1 %2', TargetTemplateCode, TargetTemplateVersion), GenChecklistTemplVersion.TABLECAPTION);
    END;

    PROCEDURE SetSource@1100526002(GenChecklistTemplVersion@1100526000 : Record 11125894);
    BEGIN
      SourceTemplateCode := GenChecklistTemplVersion."Checklist Template Code";
      SourceTemplateVersion := GenChecklistTemplVersion.Version;
    END;

    BEGIN
    END.
  }
}

