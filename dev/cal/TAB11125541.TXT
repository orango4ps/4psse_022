OBJECT Table 11125541 Plant Need Line (SMF)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    OnInsert=VAR
               PlantNeed@1100409000 : Record 11012579;
             BEGIN
               IF Type = Type::Item THEN
                 TESTFIELD("Item No.")
               ELSE
                 TESTFIELD("Plant Type");
               "Copied Quantity" := 0;
               "Transf. from Estimate" := '';
               "Blocked for Modify" := FALSE;

               PlantNeed.CHANGECOMPANY("Plant Company");
               PlantNeed.INIT;
               PlantNeed.Location := "Plant Location";
               PlantNeed.CheckDepartmentCodeFilled("Plant Company");
               PlantNeed."Line No." := "Line No.";
               PlantNeed.FillDefaultFromDepot;
               FillPlantNeedFields(PlantNeed);
               PlantNeed.INSERT(FALSE);  // FALSE!
             END;

    OnModify=VAR
               PlantNeed@1100409000 : Record 11012579;
             BEGIN
               TESTFIELD("Blocked for Modify", FALSE);
               PlantNeed.CHANGECOMPANY("Plant Company");
               PlantNeed.GET("Plant Location", "Line No.");
               FillPlantNeedFields(PlantNeed);
               PlantNeed.MODIFY(FALSE);  // FALSE!
             END;

    OnDelete=VAR
               PlantNeed@1210190000 : Record 11012579;
             BEGIN
               TESTFIELD("Blocked for Modify", FALSE);
               PlantNeed.CHANGECOMPANY("Plant Company");
               IF PlantNeed.GET("Plant Location", "Line No.") THEN BEGIN
                 PlantNeed.ResetCopiedNeedPlantOrderLines("Plant Company");
                 PlantNeed.DELETE(FALSE);  // FALSE!
               END;
             END;

    CaptionML=[ENU=Plant Need Line (Site Manager Form);
               NOR=Maskinbehovrad (Byggelederskjema)];
  }
  FIELDS
  {
    { 10  ;   ;Plant Location      ;Code20        ;CaptionML=[ENU=Plant Location;
                                                              NOR=Maskinplassering;
                                                              SVE=Maskinutplacering] }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 25  ;   ;Plant Company       ;Text30        ;TableRelation=Company;
                                                   CaptionML=[ENU=Plant Company;
                                                              NOR=Maskinselskap;
                                                              SVE=Maskinf”retag] }
    { 26  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Plant:
                                                                    BEGIN
                                                                      TESTFIELD("Item No.", '');
                                                                    END;
                                                                  Type::Item:
                                                                    BEGIN
                                                                      TESTFIELD("Plant Type", '');
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Plant,Item;
                                                                    NOR=Maskin,Artikkel;
                                                                    SVE=Maskin,Artikel];
                                                   OptionString=Plant,Item }
    { 30  ;   ;Plant Type          ;Code20        ;OnValidate=VAR
                                                                PlantType@1100409000 : Record 11012551;
                                                              BEGIN
                                                                IF "Plant Type" <> '' THEN
                                                                  TESTFIELD(Type, Type::Plant);
                                                                TESTFIELD("Plant Location");
                                                                TESTFIELD("Plant Company");
                                                                TESTFIELD(Finished, FALSE);
                                                                TESTFIELD("Blocked for Modify", FALSE);
                                                                IF "Plant Type" <> '' THEN BEGIN
                                                                  PlantType.CHANGECOMPANY("Plant Company");
                                                                  PlantType.GET("Plant Type");
                                                                  FillPlantTypeRelatedFields(PlantType);
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupPlantType(TRUE);
                                                            END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   NotBlank=Yes }
    { 31  ;   ;Plant Description   ;Text100       ;CaptionML=[ENU=Plant/Item Description;
                                                              NOR=Maskin/artikkel beskrivelse;
                                                              SVE=Maskin/artikel beskrivning];
                                                   Editable=No }
    { 32  ;   ;Unit of Measure     ;Code10        ;CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=M†ttenhet];
                                                   Editable=No }
    { 33  ;   ;Cost Object (Plant) ;Code20        ;CaptionML=[ENU=Cost Object (Plant);
                                                              NOR=Kostnadsobjekt (Maskin);
                                                              SVE=Kostnadsobjekt (Maskin)];
                                                   Editable=No }
    { 34  ;   ;Bulk                ;Boolean       ;CaptionML=[ENU=Bulk;
                                                              NOR=Bulk;
                                                              SVE=Bulk];
                                                   Editable=No }
    { 35  ;   ;Item No.            ;Code20        ;OnValidate=VAR
                                                                Item@1100529000 : Record 27;
                                                              BEGIN
                                                                IF "Item No." <> '' THEN
                                                                  TESTFIELD(Type, Type::Item);
                                                                TESTFIELD("Plant Location");
                                                                TESTFIELD("Plant Company");
                                                                TESTFIELD(Finished, FALSE);
                                                                TESTFIELD("Blocked for Modify", FALSE);
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  Item.CHANGECOMPANY("Plant Company");
                                                                  Item.GET("Item No.");
                                                                  FillItemRelatedFields(Item);
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupItemNo(TRUE);
                                                            END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr] }
    { 40  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF "Starting Date" <> xRec."Starting Date" THEN BEGIN
                                                                  TESTFIELD(Finished, FALSE);
                                                                  TESTFIELD("Blocked for Modify", FALSE);
                                                                END;
                                                                IF ("Ending Date" <> 0D) AND ("Starting Date" <> xRec."Starting Date") THEN BEGIN
                                                                  IF CONFIRM(Text001,TRUE) THEN
                                                                    "Ending Date" := "Ending Date" + ("Starting Date" - xRec."Starting Date");
                                                                END;
                                                                VALIDATE("Ending Date")
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum];
                                                   NotBlank=Yes }
    { 50  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF "Ending Date" <> xRec."Ending Date" THEN BEGIN
                                                                  TESTFIELD(Finished, FALSE);
                                                                  TESTFIELD("Blocked for Modify", FALSE);
                                                                END;
                                                                IF ("Ending Date" <> 0D) AND ("Ending Date" < "Starting Date") THEN
                                                                  "Ending Date" := "Starting Date";
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 60  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF Quantity <> xRec.Quantity THEN BEGIN
                                                                  TESTFIELD(Finished, FALSE);
                                                                  TESTFIELD("Blocked for Modify", FALSE);
                                                                  "Quantity To Copy" := Quantity;
                                                                  "Delete Remainder" := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 70  ;   ;Copied Quantity     ;Decimal       ;CaptionML=[ENU=Copied Quantity (PO);
                                                              NOR=Kopiert antall (PO);
                                                              SVE=Kopierat antal (PO)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 80  ;   ;Quantity To Copy    ;Decimal       ;OnValidate=BEGIN
                                                                IF "Quantity To Copy" <> xRec."Quantity To Copy" THEN BEGIN
                                                                  TESTFIELD(Finished, FALSE);
                                                                  TESTFIELD("Blocked for Modify", FALSE);
                                                                  IF NOT "Line Exists in Plant Need" THEN  //* For new lines always filled with Quantity
                                                                    TESTFIELD("Quantity To Copy", Quantity);
                                                                  IF "Quantity To Copy" = 0 THEN BEGIN
                                                                    Finished := TRUE;
                                                                    "Delete Remainder" := FALSE;
                                                                  END ELSE BEGIN
                                                                    IF "Quantity To Copy" < (Quantity - "Copied Quantity") THEN
                                                                      "Delete Remainder" := TRUE;
                                                                    IF "Quantity To Copy" >= (Quantity - "Copied Quantity") THEN
                                                                      "Delete Remainder" := FALSE;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=To Copy;
                                                              NOR=Til kopi;
                                                              SVE=Till kopia];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 85  ;   ;Delete Remainder    ;Boolean       ;OnValidate=BEGIN
                                                                IF "Delete Remainder" <> xRec."Delete Remainder" THEN BEGIN
                                                                  TESTFIELD(Finished, FALSE);
                                                                  TESTFIELD("Blocked for Modify", FALSE);
                                                                  IF NOT "Line Exists in Plant Need" THEN  //* Not done for new lines
                                                                    TESTFIELD("Delete Remainder", FALSE);
                                                                  IF "Delete Remainder" THEN BEGIN
                                                                    IF "Quantity To Copy" >= (Quantity - "Copied Quantity") THEN
                                                                      TESTFIELD("Delete Remainder", FALSE);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Delete Remainder;
                                                              NOR=Fjerne Resten;
                                                              SVE=Ta bort resten] }
    { 90  ;   ;Finished            ;Boolean       ;OnValidate=BEGIN
                                                                IF Finished <> xRec.Finished THEN BEGIN
                                                                  TESTFIELD("Blocked for Modify", FALSE);
                                                                  IF NOT "Line Exists in Plant Need" THEN  //* Not done for new lines
                                                                    TESTFIELD(Finished, FALSE);
                                                                  IF Finished THEN BEGIN
                                                                    "Quantity To Copy" := 0;
                                                                    "Delete Remainder" := FALSE;
                                                                  END ELSE BEGIN
                                                                    "Quantity To Copy" := Quantity - "Copied Quantity";
                                                                    IF "Quantity To Copy" < 0 THEN
                                                                      "Quantity To Copy" := 0;
                                                                    "Delete Remainder" := FALSE;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Finished;
                                                              NOR=Ferdig;
                                                              SVE=Avslutad] }
    { 120 ;   ;Transf. from Estimate;Code20       ;CaptionML=[ENU=Transf. from Estimate;
                                                              NOR=Overf›r fra Kalkyle;
                                                              SVE=™verf. fr†n kalkyl];
                                                   Editable=No }
    { 130 ;   ;Blocked for Modify  ;Boolean       ;CaptionML=[ENU=Blocked for Modify;
                                                              NOR=Blokkert for endring];
                                                   Description=Modify/Delete;
                                                   Editable=No }
    { 140 ;   ;Line Exists in Plant Need;Boolean  ;CaptionML=[ENU=Line Exist in Plant Need;
                                                              NOR=Rad finnes i maskinbehov];
                                                   Description=At the moment the tmp table is build;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Plant Location,Line No.                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=Also shift the End Dat;NOR=Endre ogs† Sluttdato;SVE=Žndra „ven slutdatum';

    PROCEDURE InitRecord@1100409004();
    BEGIN
      "Copied Quantity" := 0;
      Finished := FALSE;
      "Delete Remainder":= FALSE;
      "Transf. from Estimate" := '';
      "Blocked for Modify" := FALSE;
      "Line Exists in Plant Need" := FALSE;
    END;

    PROCEDURE LookupPlantType@1100525001(EditableMode@1100529000 : Boolean);
    VAR
      PlantType@1100525000 : Record 11012551;
    BEGIN
      TESTFIELD(Type, Type::Plant);
      TESTFIELD("Plant Location");
      TESTFIELD("Plant Company");

      PlantType.CHANGECOMPANY("Plant Company");
      PlantType.Code := "Plant Type";

      IF Finished OR "Blocked for Modify" OR (NOT EditableMode) THEN
        PAGE.RUNMODAL(0, PlantType)
      ELSE BEGIN
        IF PAGE.RUNMODAL(0, PlantType) = ACTION::LookupOK THEN
          VALIDATE("Plant Type", PlantType.Code);
      END;
    END;

    PROCEDURE LookupItemNo@1100529000(EditableMode@1100529000 : Boolean);
    VAR
      Item@1100525000 : Record 27;
    BEGIN
      TESTFIELD(Type, Type::Item);
      TESTFIELD("Plant Location");
      TESTFIELD("Plant Company");

      Item.CHANGECOMPANY("Plant Company");
      Item."No." := "Item No.";

      IF Finished OR "Blocked for Modify" OR (NOT EditableMode) THEN
        PAGE.RUNMODAL(0, Item)
      ELSE BEGIN
        IF PAGE.RUNMODAL(0, Item) = ACTION::LookupOK THEN
          VALIDATE("Item No.", Item."No.");
      END;
    END;

    PROCEDURE BuildTmpTableFromPlantNeed@1100409001(VAR TmpPlantNeedLineSMF@1100409000 : TEMPORARY Record 11125541;PlantCompany@1100409001 : Text[30];PlantLocation@1100409002 : Code[20]);
    VAR
      PlantNeed@1100409003 : Record 11012579;
      PlantType@1100409004 : Record 11012551;
      Item@1100529000 : Record 27;
    BEGIN
      //* NOTE: TmpPlantNeedLineSMF must be Temporary in the calling process!
      TmpPlantNeedLineSMF.RESET;
      TmpPlantNeedLineSMF.DELETEALL;
      IF (PlantCompany <> '') AND (PlantLocation <> '') THEN BEGIN
        PlantNeed.CHANGECOMPANY(PlantCompany);
        PlantNeed.SETRANGE(Location, PlantLocation);
        PlantNeed.SETRANGE("Document No.", ''); //Skip Plant Need from granule 'Sales Rental Contract' (not allowed on Request)
        IF PlantNeed.FINDSET THEN BEGIN
          REPEAT
            TmpPlantNeedLineSMF.INIT;
            TmpPlantNeedLineSMF."Plant Location" := PlantLocation;
            TmpPlantNeedLineSMF."Plant Company" := PlantCompany;
            TmpPlantNeedLineSMF."Line No." := PlantNeed."Line No.";
            TmpPlantNeedLineSMF.Type := PlantNeed.Type;
            IF PlantNeed.Type = PlantNeed.Type::Item THEN
              TmpPlantNeedLineSMF."Item No." := PlantNeed."Item No."
            ELSE
              TmpPlantNeedLineSMF."Plant Type" := PlantNeed."Plant Type";
            TmpPlantNeedLineSMF."Starting Date" := PlantNeed."Starting Date";
            TmpPlantNeedLineSMF."Ending Date" := PlantNeed."Ending Date";
            TmpPlantNeedLineSMF.Quantity := PlantNeed.Quantity;
            TmpPlantNeedLineSMF."Quantity To Copy" := PlantNeed."Quantity To Copy";
            TmpPlantNeedLineSMF."Delete Remainder" := PlantNeed."Delete Remainder";
            TmpPlantNeedLineSMF.Finished := PlantNeed.Finished;
            TmpPlantNeedLineSMF."Transf. from Estimate" := PlantNeed."Transf. from Estimate";
            IF TmpPlantNeedLineSMF."Plant Type" <> '' THEN BEGIN
              PlantType.CHANGECOMPANY(PlantCompany);
              IF PlantType.GET(TmpPlantNeedLineSMF."Plant Type") THEN
                TmpPlantNeedLineSMF.FillPlantTypeRelatedFields(PlantType);
            END;
            IF TmpPlantNeedLineSMF."Item No." <> '' THEN BEGIN
              Item.CHANGECOMPANY(PlantCompany);
              IF Item.GET(TmpPlantNeedLineSMF."Item No.") THEN
                TmpPlantNeedLineSMF.FillItemRelatedFields(Item);
            END;
            TmpPlantNeedLineSMF."Copied Quantity" := PlantNeed.QuantityCopiedLineTotal(PlantCompany);
            TmpPlantNeedLineSMF."Blocked for Modify" := TmpPlantNeedLineSMF.Finished OR ("Copied Quantity" <> 0);
            TmpPlantNeedLineSMF."Line Exists in Plant Need" := TRUE;
            TmpPlantNeedLineSMF.INSERT;
          UNTIL PlantNeed.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE FillPlantTypeRelatedFields@1100409005(PlantType@1100409000 : Record 11012551);
    BEGIN
      "Plant Description" := PlantType.Description;
      "Unit of Measure" := PlantType."Unit of Measure";
      "Cost Object (Plant)" := PlantType."Cost Object";
      Bulk := PlantType.Bulk;
    END;

    PROCEDURE FillItemRelatedFields@1100529001(Item@1100409000 : Record 27);
    BEGIN
      "Plant Description" := Item.Description;
      "Unit of Measure" := Item."Sales Unit of Measure";
      "Cost Object (Plant)" := '';
      Bulk := FALSE;
    END;

    PROCEDURE FillPlantNeedFields@1100409000(VAR PlantNeed@1100409000 : Record 11012579);
    BEGIN
      PlantNeed.Type := Type;
      PlantNeed."Plant Type" := "Plant Type";
      PlantNeed."Item No." := "Item No.";
      PlantNeed."Starting Date" := "Starting Date";
      PlantNeed."Ending Date" := "Ending Date";
      PlantNeed.Quantity := Quantity;
      PlantNeed."Quantity To Copy" := "Quantity To Copy";
      PlantNeed."Delete Remainder" := "Delete Remainder";
      PlantNeed.Finished := Finished;
    END;

    PROCEDURE CopyLinesToPlantRequestSMF@1100525000(VAR TmpPlantNeedLineSMF@1100525000 : TEMPORARY Record 11125541;PlantRequestNo@1100525001 : Code[20]);
    VAR
      PlantRequestSMF@1100525002 : Record 11125529;
      PlantReqLineSMF@1100525003 : Record 11125530;
      NextLineNo@1100409000 : Integer;
    BEGIN
      IF NOT TmpPlantNeedLineSMF.FINDSET(FALSE,FALSE) THEN
        EXIT;

      PlantRequestSMF.GET(PlantRequestNo);

      PlantReqLineSMF.SETRANGE("Request No.", PlantRequestNo);
      IF PlantReqLineSMF.FINDLAST THEN
        NextLineNo :=  PlantReqLineSMF."Line No." + 10000
      ELSE
        NextLineNo := 10000;

      REPEAT
        IF (NOT TmpPlantNeedLineSMF.Finished) AND (TmpPlantNeedLineSMF."Quantity To Copy" > 0) THEN BEGIN
          PlantReqLineSMF.INIT;
          PlantReqLineSMF."Request No." := PlantRequestNo;
          PlantReqLineSMF.InitRecord;
          PlantReqLineSMF."Line No." := NextLineNo;
          IF TmpPlantNeedLineSMF.Type = TmpPlantNeedLineSMF.Type::Item THEN BEGIN
            PlantReqLineSMF.Type := PlantReqLineSMF.Type::Item;
            PlantReqLineSMF."Plant/Item" := TmpPlantNeedLineSMF."Item No.";
          END ELSE BEGIN
            PlantReqLineSMF.Type := PlantReqLineSMF.Type::Plant;
            PlantReqLineSMF."Plant/Item" := TmpPlantNeedLineSMF."Plant Type";
          END;
          PlantReqLineSMF.Description := TmpPlantNeedLineSMF."Plant Description";
          PlantReqLineSMF.Bulk := TmpPlantNeedLineSMF.Bulk;
          PlantReqLineSMF."Expected return on" := TmpPlantNeedLineSMF."Ending Date";
          IF (PlantReqLineSMF."Expected return on" <> 0D) AND
             (PlantReqLineSMF."Expected return on" < PlantRequestSMF."Transfer Date")
          THEN
            PlantReqLineSMF."Expected return on" := PlantRequestSMF."Transfer Date";
          PlantReqLineSMF.Quantity := TmpPlantNeedLineSMF."Quantity To Copy";
          PlantReqLineSMF."Location Copied Need" := TmpPlantNeedLineSMF."Plant Location";
          PlantReqLineSMF."Line No. Copied Need" := TmpPlantNeedLineSMF."Line No.";
          PlantReqLineSMF.INSERT;
          NextLineNo :=NextLineNo + 10000;
        END;
      UNTIL (TmpPlantNeedLineSMF.NEXT = 0);
      COMMIT;
    END;

    BEGIN
    {
      This table may only be used as temporary!
    }
    END.
  }
}

