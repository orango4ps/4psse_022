OBJECT Codeunit 5707 TransferOrder-Post + Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=5740;
    OnRun=BEGIN
            TransHeader.COPY(Rec);
            Code;
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Receive;NOR=&Lever,&Motta;SVE=&Leverera,&Inleverera';
      TransHeader@1001 : Record 5740;

    LOCAL PROCEDURE Code@2();
    VAR
      TransLine@1000 : Record 5741;
      TransferPostShipment@1002 : Codeunit 5704;
      TransferPostReceipt@1003 : Codeunit 5705;
      DefaultNumber@1001 : Integer;
      Selection@1004 : ' ,Shipment,Receipt';
      IsHandled@1005 : Boolean;
    BEGIN
      OnBeforePost(TransHeader,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH TransHeader DO BEGIN
        TransLine.SETRANGE("Document No.","No.");
        IF TransLine.FIND('-') THEN
          REPEAT
            IF (TransLine."Quantity Shipped" < TransLine.Quantity) AND
               (DefaultNumber = 0)
            THEN
              DefaultNumber := Selection::Shipment;
            IF (TransLine."Quantity Received" < TransLine.Quantity) AND
               (DefaultNumber = 0)
            THEN
              DefaultNumber := Selection::Receipt;
          UNTIL (TransLine.NEXT = 0) OR (DefaultNumber > 0);
        IF "Direct Transfer" THEN BEGIN
          TransferPostShipment.RUN(TransHeader);
          TransferPostReceipt.RUN(TransHeader);
          PrintReport(TransHeader,Selection::Receipt);
        END ELSE BEGIN
          IF DefaultNumber = 0 THEN
            DefaultNumber := Selection::Shipment;
          Selection := STRMENU(Text000,DefaultNumber);
          CASE Selection OF
            0:
              EXIT;
            Selection::Shipment:
              TransferPostShipment.RUN(TransHeader);
            Selection::Receipt:
              TransferPostReceipt.RUN(TransHeader);
          END;
          PrintReport(TransHeader,Selection);
        END;
      END;

      OnAfterPost(TransHeader);
    END;

    [External]
    PROCEDURE PrintReport@1(TransHeaderSource@1001 : Record 5740;Selection@1000 : ' ,Shipment,Receipt');
    BEGIN
      WITH TransHeaderSource DO
        CASE Selection OF
          Selection::Shipment:
            PrintShipment("Last Shipment No.");
          Selection::Receipt:
            PrintReceipt("Last Receipt No.");
        END;
    END;

    LOCAL PROCEDURE PrintShipment@6(DocNo@1000 : Code[20]);
    VAR
      TransShptHeader@1001 : Record 5744;
    BEGIN
      IF TransShptHeader.GET(DocNo) THEN BEGIN
        TransShptHeader.SETRECFILTER;
        //TransShptHeader.PrintRecords(FALSE); //**4PS.o
        TransShptHeader.PrintRecords(FALSE,FALSE); //**4PS.n
      END;
    END;

    LOCAL PROCEDURE PrintReceipt@7(DocNo@1000 : Code[20]);
    VAR
      TransRcptHeader@1001 : Record 5746;
    BEGIN
      IF TransRcptHeader.GET(DocNo) THEN BEGIN
        TransRcptHeader.SETRECFILTER;
        TransRcptHeader.PrintRecords(FALSE);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPost@3(VAR TransHeader@1000 : Record 5740);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePost@5(VAR TransHeader@1000 : Record 5740;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

