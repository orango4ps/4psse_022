OBJECT Table 840 Cash Flow Forecast
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 842=rimd,
                TableData 847=rimd;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 CFSetup.GET;
                 CFSetup.TESTFIELD("Cash Flow Forecast No. Series");
                 NoSeriesMgt.InitSeries(CFSetup."Cash Flow Forecast No. Series",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Creation Date" := WORKDATE;
               "Created By" := USERID;
               "Manual Payments From" := WORKDATE;
               "G/L Budget From" := WORKDATE;
             END;

    OnDelete=BEGIN
               IF GetShowInChart THEN
                 CFSetup.SetChartRoleCenterCFNo('');

               CFAccountComment.RESET;
               CFAccountComment.SETRANGE("Table Name",CFAccountComment."Table Name"::"Cash Flow Forecast");
               CFAccountComment.SETRANGE("No.","No.");
               CFAccountComment.DELETEALL;

               CFForecastEntry.RESET;
               CFForecastEntry.SETCURRENTKEY("Cash Flow Forecast No.");
               CFForecastEntry.SETRANGE("Cash Flow Forecast No.","No.");
               CFForecastEntry.DELETEALL;
             END;

    CaptionML=[ENU=Cash Flow Forecast;
               NOR=Kontantstr›mprognose;
               SVE=Kassafl”desprognos];
    LookupPageID=Page849;
    DrillDownPageID=Page849;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Name;
                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 2   ;   ;Search Name         ;Code100       ;CaptionML=[ENU=Search Name;
                                                              NOR=S›kenavn;
                                                              SVE=S”knamn] }
    { 3   ;   ;Description         ;Text100       ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(xRec.Description)) OR ("Search Name" = '') THEN
                                                                  "Search Name" := Description;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 5   ;   ;Consider Discount   ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Consider Discount" THEN
                                                                  "Consider Pmt. Disc. Tol. Date" := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Consider Discount;
                                                              NOR=Vurder rabatt;
                                                              SVE=Beakta rabatt] }
    { 6   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettet den;
                                                              SVE=Uppl„ggningsdatum] }
    { 7   ;   ;Created By          ;Code50        ;DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Created By;
                                                              NOR=Opprettet av;
                                                              SVE=Skapad av] }
    { 8   ;   ;Manual Payments To  ;Date          ;CaptionML=[ENU=Manual Payments To;
                                                              NOR=Manuelle betalinger til;
                                                              SVE=Manuella betalningar till] }
    { 9   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Cash Flow Account Comment" WHERE (Table Name=CONST(Cash Flow Forecast),
                                                                                                        No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 10  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 11  ;   ;Manual Payments From;Date          ;CaptionML=[ENU=Manual Payments From;
                                                              NOR=Manuelle betalinger fra;
                                                              SVE=Manuella betalningar fr†n] }
    { 12  ;   ;G/L Budget From     ;Date          ;AccessByPermission=TableData 95=R;
                                                   CaptionML=[ENU=G/L Budget From;
                                                              NOR=Budsjett fra;
                                                              SVE=Redovisningsbudget fr†n] }
    { 13  ;   ;G/L Budget To       ;Date          ;AccessByPermission=TableData 95=R;
                                                   CaptionML=[ENU=G/L Budget To;
                                                              NOR=Budsjett til;
                                                              SVE=Redovisningsbudget till] }
    { 14  ;   ;Consider CF Payment Terms;Boolean  ;CaptionML=[ENU=Consider CF Payment Terms;
                                                              NOR=Vurder KS-betalingsbetingelser;
                                                              SVE=Beakta BK-betalningsvillkor] }
    { 15  ;   ;Consider Pmt. Disc. Tol. Date;Boolean;
                                                   CaptionML=[ENU=Consider Pmt. Disc. Tol. Date;
                                                              NOR=Vurder dato for betalingsrabattoleranse;
                                                              SVE=Beakta datum f”r kassarabattolerans] }
    { 16  ;   ;Consider Pmt. Tol. Amount;Boolean  ;CaptionML=[ENU=Consider Pmt. Tol. Amount;
                                                              NOR=Vurder betalingstoleransebel›p;
                                                              SVE=Beakta betalningstoleransbelopp] }
    { 17  ;   ;Account No. Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Account No. Filter;
                                                              NOR=Kontonummerfilter;
                                                              SVE=Filter f”r kontonummer] }
    { 18  ;   ;Source Type Filter  ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Source Type Filter;
                                                              NOR=Kildetypefilter;
                                                              SVE=Ursprungstypsfilter];
                                                   OptionCaptionML=[ENU=" ,Receivables,Payables,Liquid Funds,Cash Flow Manual Expense,Cash Flow Manual Revenue,Sales Order,Purchase Order,Fixed Assets Budget,Fixed Assets Disposal,Service Orders,G/L Budget,,,Job,Tax,Cortana Intelligence";
                                                                    NOR=" ,Kundekonto,Leverand›rkonto,Likvide midler,Kontantstr›m manuelle utgifter,Kontantstr›m manuelle inntekter,Ordre,Bestilling,Aktivabudsjett,Aktivasalg,Serviceordrer,Budsjett,,,Prosjekt,Mva,Cortana Intelligence";
                                                                    SVE=" ,Kundreskontra,Leverant”rsreskontra,Likviditetsfond,Manuell kostnad f”r kassafl”de,Manuell int„kt f”r kassafl”de,F”rs„ljningsorder,Ink”psorder,Budget f”r anl„ggningstillg†ngar,Avyttring av anl„ggningstillg†ngar,Serviceorder,Redovisningsbudget,,,Projekt,Skatt,Cortana Intelligence"];
                                                   OptionString=[ ,Receivables,Payables,Liquid Funds,Cash Flow Manual Expense,Cash Flow Manual Revenue,Sales Order,Purchase Order,Fixed Assets Budget,Fixed Assets Disposal,Service Orders,G/L Budget,,,Job,Tax,Cortana Intelligence] }
    { 19  ;   ;Cash Flow Date Filter;Date         ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Cash Flow Date Filter;
                                                              NOR=Datofilter for kontantstr›m;
                                                              SVE=Datumfilter f”r kassafl”de] }
    { 20  ;   ;Amount (LCY)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cash Flow Forecast Entry"."Amount (LCY)" WHERE (Cash Flow Forecast No.=FIELD(No.),
                                                                                                                    Cash Flow Date=FIELD(Cash Flow Date Filter),
                                                                                                                    Source Type=FIELD(Source Type Filter),
                                                                                                                    Cash Flow Account No.=FIELD(Account No. Filter),
                                                                                                                    Positive=FIELD(Positive Filter)));
                                                   CaptionML=[ENU=Amount (LCY);
                                                              NOR=Bel›p (NOK);
                                                              SVE=Belopp (BVA)] }
    { 21  ;   ;Positive Filter     ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Positive Filter;
                                                              NOR=Positivt filter;
                                                              SVE=Positivt filter] }
    { 22  ;   ;Overdue CF Dates to Work Date;Boolean;
                                                   CaptionML=[ENU=Overdue CF Dates to Work Date;
                                                              NOR=Forfalte kontantstr›mdatoer til arbeidsdato;
                                                              SVE=F”rfallna kassafl”desdatum till arbetsdatum] }
    { 23  ;   ;Default G/L Budget Name;Code10     ;TableRelation="G/L Budget Name";
                                                   CaptionML=[ENU=Default G/L Budget Name;
                                                              NOR=Standard finansbudsjettnavn] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Description 2,Creation Date }
  }
  CODE
  {
    VAR
      CFSetup@1000 : Record 843;
      CashFlowForecast@1001 : Record 840;
      CFAccountComment@1002 : Record 842;
      CFForecastEntry@1003 : Record 847;
      NoSeriesMgt@1005 : Codeunit 396;

    [External]
    PROCEDURE AssistEdit@2(OldCashFlowForecast@1000 : Record 840) : Boolean;
    BEGIN
      WITH CashFlowForecast DO BEGIN
        CashFlowForecast := Rec;
        CFSetup.GET;
        CFSetup.TESTFIELD("Cash Flow Forecast No. Series");
        IF NoSeriesMgt.SelectSeries(CFSetup."Cash Flow Forecast No. Series",OldCashFlowForecast."No. Series","No. Series") THEN BEGIN
          CFSetup.GET;
          CFSetup.TESTFIELD("Cash Flow Forecast No. Series");
          NoSeriesMgt.SetSeries("No.");
          Rec := CashFlowForecast;
          EXIT(TRUE);
        END;
      END;
    END;

    [External]
    PROCEDURE DrillDown@17();
    VAR
      CFForecastEntry@1000 : Record 847;
    BEGIN
      CFForecastEntry.DrillDownOnEntries(Rec);
    END;

    [External]
    PROCEDURE CalcAmountForPosNeg@18(PositiveAmount@1000 : Boolean) : Decimal;
    BEGIN
      SETRANGE("Positive Filter",PositiveAmount);
      EXIT(CalcAmount);
    END;

    [External]
    PROCEDURE DrillDownPosNegEntries@16(PositiveAmount@1000 : Boolean);
    BEGIN
      SETRANGE("Positive Filter",PositiveAmount);
      DrillDown;
    END;

    [External]
    PROCEDURE CalcAmountForAccountNo@12(AccountNo@1000 : Code[20]) : Decimal;
    BEGIN
      SetAccountNoFilter(AccountNo);
      EXIT(CalcAmount);
    END;

    [External]
    PROCEDURE SetAccountNoFilter@13(AccountNo@1000 : Code[20]);
    BEGIN
      IF AccountNo = '' THEN
        SETRANGE("Account No. Filter")
      ELSE
        SETRANGE("Account No. Filter",AccountNo);
    END;

    [External]
    PROCEDURE DrillDownEntriesForAccNo@15(AccountNo@1000 : Code[20]);
    BEGIN
      SetAccountNoFilter(AccountNo);
      DrillDown;
    END;

    [External]
    PROCEDURE CalcAmountFromSource@4(SourceType@1001 : Option) : Decimal;
    BEGIN
      SetSourceTypeFilter(SourceType);
      EXIT(CalcAmount);
    END;

    [External]
    PROCEDURE SetSourceTypeFilter@7(SourceType@1000 : Option);
    BEGIN
      IF SourceType = 0 THEN
        SETRANGE("Source Type Filter")
      ELSE
        SETRANGE("Source Type Filter",SourceType);
    END;

    [External]
    PROCEDURE DrillDownEntriesFromSource@5(SourceType@1000 : Option);
    BEGIN
      SetSourceTypeFilter(SourceType);
      DrillDown;
    END;

    [External]
    PROCEDURE CalcAmount@14() : Decimal;
    BEGIN
      CALCFIELDS("Amount (LCY)");
      EXIT("Amount (LCY)");
    END;

    [External]
    PROCEDURE SetCashFlowDateFilter@6(FromDate@1000 : Date;ToDate@1001 : Date);
    BEGIN
      IF (FromDate = 0D) AND (ToDate = 0D) THEN
        SETRANGE("Cash Flow Date Filter")
      ELSE
        IF ToDate = 0D THEN
          SETFILTER("Cash Flow Date Filter",'%1..',FromDate)
        ELSE
          SETRANGE("Cash Flow Date Filter",FromDate,ToDate);
    END;

    [External]
    PROCEDURE PrintRecords@1();
    VAR
      CashFlowForecast@1001 : Record 840;
      CFReportSelection@1002 : Record 856;
    BEGIN
      WITH CashFlowForecast DO BEGIN
        COPY(Rec);
        CFReportSelection.SETFILTER("Report ID",'<>0');
        IF CFReportSelection.FINDSET THEN
          REPEAT
            REPORT.RUNMODAL(CFReportSelection."Report ID",TRUE,FALSE,CashFlowForecast);
          UNTIL CFReportSelection.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE LookupCashFlowFilter@9(VAR Text@1000 : Text) : Boolean;
    VAR
      CashFlowForecastList@1002 : Page 849;
    BEGIN
      CashFlowForecastList.LOOKUPMODE(TRUE);
      IF CashFlowForecastList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Text := CashFlowForecastList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    [External]
    PROCEDURE CalculateAllAmounts@3(FromDate@1003 : Date;ToDate@1002 : Date;VAR Amounts@1010 : ARRAY [14] OF Decimal;VAR TotalAmount@1001 : Decimal);
    VAR
      SourceType@1000 : Option;
    BEGIN
      CLEAR(Amounts);
      SetCashFlowDateFilter(FromDate,ToDate);
      FOR SourceType := 1 TO ARRAYLEN(Amounts) DO
        Amounts[SourceType] := CalcAmountFromSource(SourceType);
      TotalAmount := CalcAmountFromSource(0);
    END;

    [External]
    PROCEDURE ValidateShowInChart@8(ShowInChart@1000 : Boolean) : Boolean;
    VAR
      NewCashFlowNo@1003 : Code[20];
    BEGIN
      IF ShowInChart THEN
        NewCashFlowNo := "No."
      ELSE
        NewCashFlowNo := '';

      CFSetup.GET;
      CFSetup.VALIDATE("CF No. on Chart in Role Center",NewCashFlowNo);
      CFSetup.MODIFY;
      EXIT(GetShowInChart);
    END;

    [External]
    PROCEDURE GetShowInChart@10() : Boolean;
    VAR
      ChartRoleCenterCFNo@1000 : Code[20];
    BEGIN
      ChartRoleCenterCFNo := CFSetup.GetChartRoleCenterCFNo;
      IF ChartRoleCenterCFNo = '' THEN
        EXIT(FALSE);

      EXIT("No." = ChartRoleCenterCFNo);
    END;

    [External]
    PROCEDURE GetEntryDate@11(Which@1001 : 'First,Last') : Date;
    VAR
      CFForecastEntry@1000 : Record 847;
    BEGIN
      CFForecastEntry.SETCURRENTKEY("Cash Flow Forecast No.","Cash Flow Date");
      CFForecastEntry.SETRANGE("Cash Flow Forecast No.","No.");
      CASE Which OF
        Which::First:
          IF NOT CFForecastEntry.FINDFIRST THEN
            EXIT(0D);
        Which::Last:
          IF NOT CFForecastEntry.FINDLAST THEN
            EXIT(0D);
      END;
      EXIT(CFForecastEntry."Cash Flow Date");
    END;

    BEGIN
    END.
  }
}

