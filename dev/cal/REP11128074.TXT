OBJECT Report 11128074 Receive BG Autogiro D
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE7.02.01 (12047969);
  }
  PROPERTIES
  {
    CaptionML=[ENU=Receive BG Auto Giro D;
               SVE=èterredov. BG Autogiro D];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF gBankAccountNo = '' THEN
                    ERROR(PEB009);
                  gTextFile.TEXTMODE := TRUE;
                  IF gFileMgt.CanRunDotNetOnClient THEN
                    gServerFileName := gFileMgt.UploadFileSilent(gFileName)
                  ELSE
                    gServerFileName := gFileMgt.UploadFile(PEB008, gFileName);

                  IF gServerFileName = '' THEN
                    CurrReport.QUIT;

                  gTextFile.OPEN(gServerFileName);
                END;

    OnPostReport=BEGIN
                   gTextFile.CLOSE;
                   ERASE(gServerFileName);
                 END;

  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               gGenJnlLine2.SETRANGE("Journal Template Name",gJnlTemplateName);
                               gGenJnlLine2.SETRANGE("Journal Batch Name",gJnlBatchName);
                               IF gGenJnlLine2.FIND('+') THEN
                                 gNextLineNo := gGenJnlLine2."Line No."
                               ELSE
                                 gNextLineNo := 0;
                               gWindow.OPEN(PEB001);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF gTextFile.LEN = gTextFile.POS THEN
                                    CurrReport.BREAK;
                                  gTextFile.READ(gTextLine);
                                  gType := COPYSTR(gTextLine,1,2);
                                  CASE gType OF
                                    '82','32':
                                          BEGIN // Debitering = 82 Kreditering = 32
                                            gGenJnlLine.INIT;
                                            gGenJnlLine."Journal Template Name" := gJnlTemplateName;
                                            gGenJnlLine."Journal Batch Name" := gJnlBatchName;
                                            gNextLineNo := gNextLineNo + 10000;
                                            gGenJnlLine."Line No." := gNextLineNo;
                                            CLEAR(gPostingDate);
                                            IF EVALUATE(gPostingDate,COPYSTR(gTextLine,3,8)) THEN;
                                            gGenJnlLine.VALIDATE("Posting Date",gPostingDate);
                                            gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::Customer);
                                            gGenJnlLine.VALIDATE("Document No.",gDocNo);
                                            gApplyToDocNo := COPYSTR(gTextLine,54,16);
                                            gWindow.UPDATE(1,gApplyToDocNo);
                                            IF InvoiceExist THEN
                                              gGenJnlLine.VALIDATE("Applies-to Doc. No.", gApplyToDocNo)
                                            ELSE
                                              IF CreditMemoExist THEN
                                                gGenJnlLine.VALIDATE("Applies-to Doc. No.", gApplyToDocNo)
                                              ELSE
                                                gGenJnlLine.VALIDATE("Applies-to Doc. No.", '');
                                            gAmountText := COPYSTR(gTextLine,32,12);
                                            gTransferAmount := 0;
                                            IF EVALUATE(gTransferAmount, gAmountText) THEN;
                                            gTransferAmount := ROUND(gTransferAmount / 100);
                                            IF gType = '32' THEN
                                              gTransferAmount := -gTransferAmount;
                                            gGenJnlLine.VALIDATE(Amount, -gTransferAmount);
                                            IF COPYSTR(gTextLine,80,1) = '1' THEN
                                              gGenJnlLine.Description := gApplyToDocNo+' '+PEB002;
                                            IF COPYSTR(gTextLine,80,1) = '2' THEN
                                              gGenJnlLine.Description := gApplyToDocNo+' '+PEB003;
                                            IF COPYSTR(gTextLine,80,1) = '9' THEN
                                              gGenJnlLine.Description := gApplyToDocNo+' '+PEB004;
                                            IF gGenJnlLine."Account No." = '' THEN
                                              gGenJnlLine.Description := PEB005;
                                            IF (COPYSTR(gTextLine,80,1) = '1') OR
                                               (COPYSTR(gTextLine,80,1) = '2') OR
                                               (COPYSTR(gTextLine,80,1) = '9') THEN
                                              gNotCorrectAmount := gNotCorrectAmount + gGenJnlLine.Amount;
                                            gGenJnlLine.INSERT;
                                          END;
                                    '09': BEGIN // TOTALPOST PER DATAMEDIA
                                            gGenJnlLine.INIT;
                                            gGenJnlLine."Journal Template Name" := gJnlTemplateName;
                                            gGenJnlLine."Journal Batch Name" := gJnlBatchName;
                                            gNextLineNo := gNextLineNo + 10000;
                                            gGenJnlLine."Line No." := gNextLineNo;
                                            gGenJnlLine.VALIDATE("Posting Date", gPostingDate);
                                            gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"G/L Account");
                                            gGenJnlLine.VALIDATE("Document No.",gDocNo);

                                            CASE gAccountType OF
                                              gAccountType::"G/L Account":
                                                gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"G/L Account");
                                              gAccountType::"Bank Account":
                                                gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"Bank Account");
                                            END;
                                            gGenJnlLine.VALIDATE("Account No.", gBankAccountNo);

                                            gGenJnlLine."Document Type" := gGenJnlLine."Document Type"::Payment;
                                            gGenJnlLine.VALIDATE("Applies-to Doc. No.", '');
                                            gAmountTextCredit := COPYSTR(gTextLine,29,12);
                                            gAmountText := COPYSTR(gTextLine,57,12);
                                            gTransferAmountCredit := 0;
                                            gTransferAmount := 0;
                                            EVALUATE(gTransferAmountCredit,gAmountTextCredit);
                                            EVALUATE(gTransferAmount, gAmountText);
                                            gTransferAmount := ROUND(gTransferAmount / 100) - ROUND(gTransferAmountCredit / 100);
                                            gTransferAmount := gTransferAmount + gNotCorrectAmount;
                                            gGenJnlLine.VALIDATE(Amount,gTransferAmount);
                                            gGenJnlLine.VALIDATE(Description,PEB006);
                                            gGenJnlLine.INSERT;
                                          END;
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   gDocNo := gGenJnlLine."Document No.";
                   gFileNameVisible := gFileMgt.CanRunDotNetOnClient;
                 END;

    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=Cont;
                  ContainerType=ContentArea }

      { 1100570000;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 12047959;2;Field    ;
                  CaptionML=[ENU=File Name;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=gFileName;
                  Visible=gFileNameVisible;
                  OnAssistEdit=BEGIN
                                 gFileName := gFileMgt.OpenFileDialog(PEB008,gFileName,PEB007);
                               END;
                                }

      { 1000000002;2;Field  ;
                  CaptionML=[ENU=Document No.;
                             NOR=Dokumentnr.;
                             SVE=Verifikationsnr];
                  SourceExpr=gDocNo }

      { 12047960;2;Field    ;
                  CaptionML=[ENU=Account Type;
                             NOR=Kontotype;
                             SVE=Kontotyp];
                  OptionCaptionML=[ENU=G/L Account,,,Bank Account;
                                   NOR=Hovedbokskonto,,,Bankkonto;
                                   SVE=Redov.konto,,,Bankkonto];
                  SourceExpr=gAccountType;
                  OnValidate=BEGIN
                               CLEAR(gBankAccountNo);
                             END;
                              }

      { 12047958;2;Field    ;
                  CaptionML=[ENU=Account No;
                             NOR=Nr;
                             SVE=Kontonr];
                  SourceExpr=gBankAccountNo;
                  OnValidate=BEGIN
                               IF gBankAccountNo = '' THEN
                                 EXIT;
                               CASE gAccountType OF
                                 gAccountType::"G/L Account":
                                   BEGIN
                                     gGLAccount.GET(gBankAccountNo);
                                   END;
                                 gAccountType::"Bank Account":
                                   BEGIN
                                     gBankAccount.GET(gBankAccountNo);
                                   END;
                               END;
                             END;

                  OnLookup=BEGIN
                             CASE gAccountType OF
                               gAccountType::"G/L Account":
                                 BEGIN
                                   IF PAGE.RUNMODAL(0,gGLAccount) = ACTION::LookupOK THEN
                                     gBankAccountNo := gGLAccount."No.";
                                 END;
                               gAccountType::"Bank Account":
                                 BEGIN
                                   IF PAGE.RUNMODAL(0,gBankAccount) = ACTION::LookupOK THEN
                                     gBankAccountNo := gBankAccount."No.";
                                 END;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gFileName@12047960 : Text;
      gTextFile@1000 : File;
      gTextLine@1002 : Text[250];
      gType@1003 : Text[2];
      gPostingDate@1004 : Date;
      gDocNo@1005 : Code[20];
      gGenJnlLine@1006 : Record 81;
      gTransferAmount@1007 : Decimal;
      gTransferAmountCredit@1008 : Decimal;
      gNextLineNo@1009 : Integer;
      gApplyToDocNo@1011 : Code[20];
      gAmountText@1012 : Text[30];
      gAmountTextCredit@1013 : Text[30];
      gJnlTemplateName@1014 : Code[10];
      gJnlBatchName@1015 : Code[10];
      gGenJnlLine2@1016 : Record 81;
      gWindow@1017 : Dialog;
      gNotCorrectAmount@1018 : Decimal;
      PEB001@1021 : TextConst 'ENU=Document No. #1########;SVE=Dokumentnr #1########';
      PEB002@1022 : TextConst 'ENU=FEL! TÑckning saknas;SVE=FEL! TÑckning saknas';
      PEB003@1023 : TextConst 'ENU=FEL! AG-koppling saknas;SVE=FEL! AG-koppling saknas';
      PEB004@1024 : TextConst 'ENU=FEL! Fîrnyad tÑckning;SVE=FEL! Fîrnyad tÑckning';
      PEB005@1025 : TextConst 'ENU=FEL! Fakturan/kreditfakturan Ñr redan reglerad.;SVE=FEL! Fakturan/kreditfakturan Ñr redan reglerad.';
      PEB006@1026 : TextConst 'ENU=Auto Giro Payment;SVE=Autogirobetalning';
      PEB007@1100570002 : TextConst 'ENU=All files|*.*|TXT-files (*.txt)|*.txt|WRI-files (*.wri)|*.wri;SVE=Alla filer|*.*|TXT-filer (*.txt)|*.txt|WRI-filer (*.wri)|*.wri';
      PEB008@1100570001 : TextConst 'ENU=BG Autogiro;SVE=BG Autogiro';
      gServerFileName@12047958 : Text;
      gFileMgt@12047959 : Codeunit 419;
      gBankAccountNo@12047964 : Code[20];
      gGLAccount@12047963 : Record 15;
      gBankAccount@12047962 : Record 270;
      gAccountType@12047961 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';
      PEB009@12047965 : TextConst 'ENU=Account No must not be blank;SVE=Kontonr mÜste vara ifyllt';
      gFileNameVisible@12047966 : Boolean INDATASET;

    PROCEDURE SetGenJnlLine@1(NewGLJnlLine@1000 : Record 81);
    BEGIN
      gGenJnlLine := NewGLJnlLine;
      gJnlTemplateName := gGenJnlLine."Journal Template Name";
      gJnlBatchName := gGenJnlLine."Journal Batch Name";
    END;

    PROCEDURE InvoiceExist@2() : Boolean;
    VAR
      CustLedgerEntry@1000 : Record 21;
    BEGIN
      CustLedgerEntry.SETCURRENTKEY("Document No.");
      CustLedgerEntry.SETRANGE("Document Type", CustLedgerEntry."Document Type"::Invoice);
      CustLedgerEntry.SETRANGE("Document No.",gApplyToDocNo);
      CustLedgerEntry.SETRANGE(Open,TRUE);
      IF CustLedgerEntry.FIND('-') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE CreditMemoExist@5() : Boolean;
    VAR
      CustLedgerEntry@1000 : Record 21;
    BEGIN
      CustLedgerEntry.SETCURRENTKEY("Document No.");
      CustLedgerEntry.SETRANGE("Document Type", CustLedgerEntry."Document Type"::"Credit Memo");
      CustLedgerEntry.SETRANGE("Document No.",gApplyToDocNo);
      CustLedgerEntry.SETRANGE(Open,TRUE);
      IF CustLedgerEntry.FIND('-') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0006 Autogiro
      PEB
      PEB *** End ***
    }
    END.
  }
  RDLDATA
  {
  }
}

