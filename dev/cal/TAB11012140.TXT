OBJECT Table 11012140 Document Data Item
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               DataItem2@1210190000 : Record 11012140;
             BEGIN
               //Create link with previous line
               IF "Line No." > 0 THEN BEGIN
                 DataItem2 := Rec;
                 DataItem2.SETRANGE("Document Group","Document Group");
                 DataItem2.SETRANGE(Document,Document);
                 IF DataItem2.FIND('<') THEN BEGIN
                   Indent := DataItem2.Indent + 1;
                   "Parent Line No." := DataItem2."Line No.";
                   "Parent Table No." := DataItem2."Table No.";
                 END;
               END;
             END;

    OnDelete=VAR
               DataItem2@1210190000 : Record 11012140;
               DataItem3@1210190001 : Record 11012140;
               lDataItemLink@1210190002 : Record 11012141;
             BEGIN
               DeleteDetails("Document Group",Document,"Line No.");
               //Create link between next line and previous line.

               //First search basis to which subsequent level should be linked.
               DataItem3 := Rec;
               DataItem3.SETRANGE("Document Group","Document Group");
               DataItem3.SETRANGE(Document,Document);
               DataItem3.SETRANGE("Line No.", "Parent Line No.");
               DataItem3.SETRANGE(Indent,Indent - 1);
               IF NOT DataItem3.FIND('-') THEN BEGIN
                 DataItem3."Line No." := 0;
                 DataItem3."Table No." := 0;
                 DataItem3.Indent := -1;
               END;

               //Search lines of which link must be updated, and delete dataitemlink fields
               DataItem2 := Rec;
               DataItem2.SETRANGE("Document Group","Document Group");
               DataItem2.SETRANGE(Document,Document);
               DataItem2.SETRANGE("Parent Line No.", "Line No.");
               DataItem2.SETFILTER("Line No.",'>%1', "Line No.");
               DataItem2.SETRANGE(Indent,Indent + 1);
               IF DataItem2.FIND('-') THEN BEGIN
                 REPEAT
                   DataItem2."Parent Line No."  := DataItem3."Line No.";
                   DataItem2."Parent Table No." := DataItem3."Table No.";
                   DataItem2.Indent := DataItem3.Indent + 1;
                   DataItem2.MODIFY;
                   //Cleanup dataitem-links.
                   lDataItemLink.SETRANGE("Document Group",DataItem2."Document Group");
                   lDataItemLink.SETRANGE(Document,DataItem2.Document);
                   lDataItemLink.SETRANGE("Data Item Line No.",DataItem2."Line No.");
                   lDataItemLink.DELETEALL;
                 UNTIL DataItem2.NEXT = 0;
               END;
             END;

    CaptionML=[ENU=Document Data Item;
               NOR=Dokumentdataartikkel;
               SVE=Dokumentdataartikel];
    LookupPageID=Page11012783;
  }
  FIELDS
  {
    { 1   ;   ;Document Group      ;Code10        ;TableRelation="Document Group";
                                                   CaptionML=[ENU=Document Group;
                                                              NOR=Dokumentgruppen finnes ikke;
                                                              SVE=Dokumentgrupp] }
    { 2   ;   ;Document            ;Code20        ;TableRelation="Document Template"."Template Code" WHERE (Document Group=FIELD(Document Group));
                                                   CaptionML=[ENU=Document;
                                                              NOR=Dokument;
                                                              SVE=Dokument] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 4   ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Table Name");

                                                                IF (xRec."Table No." <> 0) AND (xRec."Table No." <> "Table No.") THEN BEGIN

                                                                  DeleteDetails("Document Group",Document,"Line No.");

                                                                  DataItem.SETRANGE("Document Group","Document Group");
                                                                  DataItem.SETRANGE(Document,Document);
                                                                  DataItem.SETFILTER(Indent,'%1..',1);
                                                                  DataItem.SETRANGE("Parent Line No.","Line No.");
                                                                  IF DataItem.FIND('-') THEN
                                                                    REPEAT
                                                                      DataItem."Parent Table No." := "Table No.";
                                                                      DataItem.MODIFY;
                                                                      DeleteDetails(DataItem."Document Group",DataItem.Document,DataItem."Line No.");
                                                                    UNTIL DataItem.NEXT = 0;

                                                                END;

                                                                DocMgt.FillKeyTable(Rec);
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr] }
    { 5   ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              NOR=Tabellnavn;
                                                              SVE=Tabellnamn];
                                                   Editable=No }
    { 6   ;   ;Indent              ;Integer       ;CaptionML=[ENU=Indent;
                                                              NOR=Rykk inn;
                                                              SVE=Indrag] }
    { 7   ;   ;Parent Line No.     ;Integer       ;CaptionML=[ENU=Parent Line No.;
                                                              NOR=Overordnet radnr.;
                                                              SVE=™verordnat radnr] }
    { 8   ;   ;Parent Table No.    ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Parent Table No.;
                                                              NOR=Overordnet tabell nummer;
                                                              SVE=Nr f”r ”verordnad tabell] }
    { 9   ;   ;Link                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Document Data Item Link" WHERE (Document Group=FIELD(Document Group),
                                                                                                      Document=FIELD(Document),
                                                                                                      Data Item Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Link;
                                                              NOR=Lenke;
                                                              SVE=L„nk];
                                                   Editable=No }
    { 10  ;   ;Filter              ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Document Data Item Filter" WHERE (Document Group=FIELD(Document Group),
                                                                                                        Document=FIELD(Document),
                                                                                                        Data Item Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Filter;
                                                              NOR=Filter;
                                                              SVE=Urval];
                                                   Editable=No }
    { 11  ;   ;Fields              ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Document Data Item Field" WHERE (Document Group=FIELD(Document Group),
                                                                                                       Document=FIELD(Document),
                                                                                                       Data Item Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Fields;
                                                              NOR=Felt;
                                                              SVE=F„lt];
                                                   Editable=No }
    { 12  ;   ;Table Caption       ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Caption;
                                                              NOR=Tabelltittel;
                                                              SVE=Tabellrubrik];
                                                   Editable=No }
    { 13  ;   ;Word Document Table No.;Integer    ;CaptionML=[ENU=Word Document Table No.;
                                                              NOR=Tabellnr. i Word-dokument;
                                                              SVE=Tabellnr i Word-dokument] }
    { 15  ;   ;Sort Order          ;Option        ;CaptionML=[ENU=Sort Order;
                                                              NOR=Sorteringsrekkef›lge;
                                                              SVE=Sorteringsordning];
                                                   OptionCaptionML=[ENU=Ascending,Descending;
                                                                    NOR=Stigende,Synkende;
                                                                    SVE=Stigande,Fallande];
                                                   OptionString=Up,Down }
    { 16  ;   ;Key                 ;Integer       ;TableRelation="Document Keys"."Index No." WHERE (Table No.=FIELD(Table No.));
                                                   CaptionML=[ENU=Key;
                                                              NOR=N›kkel;
                                                              SVE=Nyckel] }
  }
  KEYS
  {
    {    ;Document Group,Document,Line No.        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DataItem@1000000002 : Record 11012140;
      DataItemLink@1000000000 : Record 11012141;
      DataItemFilter@1000000003 : Record 11012143;
      DataItemFields@1000000001 : Record 11012142;
      DocMgt@1000000004 : Codeunit 11012406;

    PROCEDURE DeleteDetails@1000000000(iDocumentGroup@1000000000 : Code[10];iDocument@1210190000 : Code[20];iLineNo@1000000001 : Integer);
    BEGIN
      DataItemLink.SETRANGE("Document Group",iDocumentGroup);
      DataItemLink.SETRANGE(Document,iDocument);
      DataItemLink.SETRANGE("Data Item Line No.",iLineNo);
      DataItemLink.DELETEALL;

      DataItemFilter.SETRANGE("Document Group",iDocumentGroup);
      DataItemFilter.SETRANGE(Document,iDocument);
      DataItemFilter.SETRANGE("Data Item Line No.",iLineNo);
      DataItemFilter.DELETEALL;

      DataItemFields.SETRANGE("Document Group",iDocumentGroup);
      DataItemFields.SETRANGE(Document,iDocument);
      DataItemFields.SETRANGE("Data Item Line No.",iLineNo);
      DataItemFields.DELETEALL;
    END;

    BEGIN
    END.
  }
}

