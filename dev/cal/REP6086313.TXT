OBJECT Report 6086313 Batch Post Mileage
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW17.00.00.3.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Batch-Erstellung Buchungsvorschlag;
               ENU=Batch Post Mileage;
               NLD=Batchboeken kilometrage];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   EMSetup.GET;
                 END;

    OnPreReport=BEGIN
                  IF ReplacePostingDate AND (PostingDatePolicy = PostingDatePolicy::"Specific Date") AND (PostingDateReq = 0D) THEN
                    ERROR(Text000);

                  IF Group THEN BEGIN
                    IF NOT ReplacePostingDate THEN
                      ERROR(ReplacePostingDateTxt);

                    IF NOT (EMSetup."Enable Settlement" AND EMSetup."Post Bank Trans. on Import") THEN
                      ERROR(GroupingNotAllowed);
                  END;
                END;

    OnPostReport=BEGIN
                   MileagePost.ForcePIApproval;

                   IF Group THEN
                     MileagePost.PostSettlement;

                   Window.CLOSE;

                   IF CounterOK <> 0 THEN
                     MESSAGE(Text004,CounterOK,Counter);
                 END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table6086338;
               DataItemTableView=SORTING(Posted,Continia User ID,Status);
               OnPreDataItem=BEGIN
                               SETRANGE(Posted,FALSE);
                               SETRANGE(Status,Status::Released);

                               CounterTotal := COUNT;

                               Window.OPEN(Text001 + Text002 + Text003);

                               IF Group THEN
                                 Mileage.SETCURRENTKEY("Continia User ID",Status,Posted);
                             END;

               OnAfterGetRecord=VAR
                                  Mileage2@1001 : Record 6086338;
                                BEGIN
                                  Counter := Counter + 1;

                                  CODEUNIT.RUN(CODEUNIT::"Mileage-Validate",Mileage);
                                  IF Mileage.HasErrorComment(FALSE,FALSE) THEN
                                    CurrReport.SKIP;

                                  Window.UPDATE(1,ROUND(Counter / CounterTotal * 10000,1));

                                  COMMIT;

                                  MileagePost.SetPostingDate(ReplacePostingDate,PostingDatePolicy,PostingDateReq);
                                  Mileage2 := Mileage;
                                  IF MileageCheck.RUN(Mileage2) THEN BEGIN
                                    TempMileageToPost.COPY(Mileage2);
                                    TempMileageToPost.INSERT;

                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               ReqFilterFields=Continia User ID,Registration Date,Entry No. }

    { 2   ;0   ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               CounterTotal := TempMileageToPost.COUNT;
                               SETRANGE(Number,1,CounterTotal);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Number = 1 THEN
                                    TempMileageToPost.FINDFIRST
                                  ELSE
                                    TempMileageToPost.NEXT;

                                  Mileage.GET(TempMileageToPost."Entry No.");
                                  Window.UPDATE(2,ROUND(Number / CounterTotal * 10000,1));

                                  MileagePost.SetPostingDate(ReplacePostingDate,PostingDatePolicy,PostingDateReq);
                                  MileagePost.SetGroup(Group);
                                  MileagePost.RunWithoutCheck(Mileage);

                                  CounterOK := CounterOK + 1;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      CaptionML=[DEU=Batch-Erstellung Buchungsvorschlag;
                 ENU=Batch Post Mileage;
                 NLD=Batchboeken kilometrage];
      OnOpenPage=BEGIN
                   PostingDateEditable := PostingDatePolicy = PostingDatePolicy::"Specific Date";
                   PostingDatePolicyEditable :=  ReplacePostingDate;
                 END;

    }
    CONTROLS
    {
      { 5   ;0   ;Container ;
                  ContainerType=ContentArea }

      { 4   ;1   ;Group     ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=Group by Settlement;
                  CaptionML=[DEU="";
                             ENU=Group by Settlement];
                  SourceExpr=Group;
                  OnValidate=BEGIN

                               IF Group THEN
                                 ReplacePostingDate := TRUE
                               ELSE
                                 ReplacePostingDate := FALSE;
                             END;
                              }

      { 3   ;2   ;Field     ;
                  Name=Replace Posting Date;
                  CaptionML=[DEU=Buchungsdatum ersetzen;
                             ENU=Replace Posting Date;
                             NLD=Vervang boekingsdatum;
                             NOR=Erstatt bokfõringsdato;
                             SVE=ErsÑtt bokfîringsdatum];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               PostingDatePolicyEditable :=  ReplacePostingDate;
                               CLEAR(PostingDatePolicy);
                               CLEAR(PostingDateReq);
                             END;
                              }

      { 6   ;2   ;Field     ;
                  Name=Posting Date Policy;
                  CaptionML=[DEU=Buchungsdatumpolitik;
                             ENU=Posting Date Policy;
                             NLD=Boekingsdatumbeleid];
                  OptionCaptionML=[DEU=Erste Ausgabe/Kilometerleistungsdatum,Letzte Ausgabe/Kilometerleistungsdatum,Spezifisches Datum;
                                   ENU=First Expense/Mileage Date,Last Expense/Mileage Date,Specific Date;
                                   NLD=Eerste uitgave/kilometragedatum,Laatste uitgave/kilometragedatum,Specifieke datum];
                  SourceExpr=PostingDatePolicy;
                  Editable=PostingDatePolicyEditable;
                  OnValidate=BEGIN
                               IF PostingDatePolicy <> PostingDatePolicy::"Specific Date" THEN
                                 CLEAR(PostingDateReq);

                               PostingDateEditable := PostingDatePolicy = PostingDatePolicy::"Specific Date";
                             END;
                              }

      { 2   ;2   ;Field     ;
                  Name=Posting Date;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  SourceExpr=PostingDateReq;
                  Editable=PostingDateEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempMileageToPost@1000 : TEMPORARY Record 6086338;
      EMSetup@1016 : Record 6086300;
      MileagePost@1010 : Codeunit 6086350;
      MileageCheck@1009 : Codeunit 6086344;
      Window@1008 : Dialog;
      Counter@1007 : Integer;
      CounterOK@1006 : Integer;
      CounterTotal@1005 : Integer;
      PostingDateReq@1004 : Date;
      ReplacePostingDate@1003 : Boolean;
      ReplaceDocumentDate@1002 : Boolean;
      Text000@1015 : TextConst 'DEU=Bitte das Buchungsdatum eingeben.;ENU=Please enter the posting date.;NLD=Geef de boekingsdatum in a.u.b.;NOR=Angi bokfõringsdatoen.;SVE=Skriv in bokfîringsdatum.';
      Text001@1014 : TextConst 'DEU=KilometerleistungsbuchungsvorschlÑge werden erstellt...;ENU=Posting Mileage...;NLD=Kilometrage boeken...';
      Text002@1013 : TextConst 'DEU=\PrÅfung der Zeilen @1@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=\Checking lines @1@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=\Regels controleren @1@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text003@1012 : TextConst 'DEU=\Buchung der Zeilen @2@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=\Posting lines  @2@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=\Boeken regels      @2@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text004@1011 : TextConst 'DEU=Buchungsvorschlag fÅr %1 Kilometerleistungen von insgesamt %2 wurde erstellt.;ENU=%1 mileage out of a total of %2 have been posted.;NLD=%1 van in totaal %2 kilometrages zijn geboekt.';
      Group@1001 : Boolean;
      PostingDatePolicy@1160040009 : 'First Expense/Mileage Date,Last Expense/Mileage Date,Specific Date';
      ReplacePostingDateTxt@1018 : TextConst 'DEU=Das Buchungsdatum muss beim Gruppieren ersetzt werden.;ENU=The posting date needs to be replaced when grouping.;NLD=De boekingsdatum moet worden vervangen bij het groeperen.';
      GroupingNotAllowed@1019 : TextConst 'DEU=Gruppierung ist nur relevant beim Gebrauch von Ausgleichen.;ENU=Settlements have not been enabled in the Expense Management Setup.;NLD=Groepering is alleen relevant bij het gebruik van afrekeningen.';
      PostingDateEditable@1020 : Boolean INDATASET;
      PostingDatePolicyEditable@6086300 : Boolean INDATASET;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

