OBJECT Codeunit 11012854 Send SMTP SO Status Mail
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11020337;
    OnRun=VAR
            ServiceSetup@1100528602 : Record 11012800;
            ServiceOrderEMailAttachm@1100528600 : Record 11071933;
            SMTPMail@1100528601 : Codeunit 400;
            MailTemplateManagement@1100528301 : Codeunit 11012622;
            MailBodyIST@1100528604 : InStream;
            MailLineTXT@1100528603 : Text[1024];
            FromName@1100528400 : Text[50];
            FromEMail@1100528401 : Text[80];
            MailTemplateNo@1100528300 : Integer;
          BEGIN
            ServiceSetup.GET;  //db, 14-11-12: C003685
            MailTemplateNo := 21;
            MailTemplateManagement.GetApplicableMailTemplateFromValues(MailTemplateNo, "Shortcut Dimension1 Code", TRUE, FromName, FromEMail);

            Status := Status::Processing;
            SMTPMail.CreateMessage(
              FromName,
              FromEMail,
              '',
              Subject,
              '',
              TRUE);
            SMTPMail.AddRecipients("To Address");
            IF "Copy-to Address" <> '' THEN
              SMTPMail.AddBCC("Copy-to Address");

            ServiceOrderEMailAttachm.SETRANGE("Queue Entry No.", "Entry No.");
            IF ServiceOrderEMailAttachm.FINDSET THEN
              REPEAT
                IF GUIALLOWED THEN
                  SMTPMail.AddAttachmentFromClient(ServiceOrderEMailAttachm.Filename,'')
                ELSE
                  SMTPMail.AddAttachment(ServiceOrderEMailAttachm.Filename,''); //Called by NAS
              UNTIL ServiceOrderEMailAttachm.NEXT = 0;

            CALCFIELDS(Body);
            Body.CREATEINSTREAM(MailBodyIST);
            WHILE NOT MailBodyIST.EOS DO BEGIN
              MailBodyIST.READTEXT(MailLineTXT);
              SMTPMail.AppendBody(MailLineTXT);
            END;
            IF SMTPMail.RUN THEN BEGIN
              Status := Status::Send;
              "Sending Date" := TODAY();
              "Sending Time" := TIME();
              "Error Message" := '';
            END ELSE BEGIN
              Status := Status::Error;
              "Error Message" := COPYSTR(GETLASTERRORTEXT, 1, 250);
            END;
          END;

  }
  CODE
  {

    BEGIN
    END.
  }
}

