OBJECT Table 725 Custom Address Format
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               CustomAddressFormatLine@1000 : Record 726;
             BEGIN
               CustomAddressFormatLine.RESET;
               CustomAddressFormatLine.SETRANGE("Country/Region Code","Country/Region Code");
               CustomAddressFormatLine.SETRANGE("Line No.","Line No.");
               CustomAddressFormatLine.DELETEALL;
             END;

    CaptionML=[ENU=Custom Address Format;
               NOR=Egendefinert adresseformat;
               SVE=Anpassat adressformat];
  }
  FIELDS
  {
    { 1   ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              NOR=Lands-/omr†dekode;
                                                              SVE=Lands-/regionkod] }
    { 2   ;   ;Line Position       ;Option        ;CaptionML=[ENU=Line Position;
                                                              NOR=Linjeposisjon];
                                                   OptionCaptionML=[ENU=1,2,3,4,5,6,7,8;
                                                                    NOR=1,2,3,4,5,6,7,8];
                                                   OptionString=1,2,3,4,5,6,7,8 }
    { 3   ;   ;Line Format         ;Text80        ;CaptionML=[ENU=Line Format;
                                                              NOR=Linjeformat] }
    { 4   ;   ;Line No.            ;Integer       ;AutoIncrement=Yes;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 5   ;   ;Field ID            ;Integer       ;TableRelation=Field.No. WHERE (TableNo=CONST(79),
                                                                                  No.=FILTER(2|3|4|5|6|30|31|36|51));
                                                   OnValidate=VAR
                                                                Field@1000 : Record 2000000041;
                                                              BEGIN
                                                                IF "Field ID" <> xRec."Field ID" THEN
                                                                  UpdateRelatedCustomerAddressFormatLines;

                                                                IF Field.GET(DATABASE::"Company Information","Field ID") THEN
                                                                  "Line Format" := STRSUBSTNO('[%1]',Field.FieldName)
                                                                ELSE
                                                                  "Line Format" := '';
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Field ID;
                                                              NOR=Felt-ID;
                                                              SVE=F„lt-ID] }
  }
  KEYS
  {
    {    ;Country/Region Code,Line No.            ;Clustered=Yes }
    {    ;Country/Region Code,Line Position        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE BuildAddressFormat@1();
    VAR
      CustomAddressFormatLine@1000 : Record 726;
    BEGIN
      "Line Format" := '';

      CustomAddressFormatLine.RESET;
      CustomAddressFormatLine.SETCURRENTKEY("Country/Region Code","Line No.","Field Position");
      CustomAddressFormatLine.SETRANGE("Country/Region Code","Country/Region Code");
      CustomAddressFormatLine.SETRANGE("Line No.","Line No.");
      CustomAddressFormatLine.SETFILTER("Field Name",'<>%1','');
      IF CustomAddressFormatLine.FINDSET THEN
        REPEAT
          "Line Format" += STRSUBSTNO('[%1]',CustomAddressFormatLine."Field Name");
          IF CustomAddressFormatLine.Separator <> '' THEN
            "Line Format" += CustomAddressFormatLine.Separator;
          "Line Format" += ' ';
        UNTIL CustomAddressFormatLine.NEXT = 0;

      IF CustomAddressFormatLine.COUNT > 1 THEN
        "Field ID" := 0;
    END;

    [External]
    PROCEDURE UseCounty@2(CountryCode@1002 : Code[10]) : Boolean;
    VAR
      CustomAddressFormatLine@1000 : Record 726;
      CompanyInformation@1001 : Record 79;
    BEGIN
      CustomAddressFormatLine.RESET;
      CustomAddressFormatLine.SETRANGE("Country/Region Code",CountryCode);
      CustomAddressFormatLine.SETRANGE("Field ID",CompanyInformation.FIELDNO(County));
      EXIT(NOT CustomAddressFormatLine.ISEMPTY);
    END;

    [External]
    PROCEDURE ShowCustomAddressFormatLines@3();
    VAR
      CustomAddressFormatLine@1001 : Record 726;
      CustomAddressFormatLines@1000 : Page 726;
    BEGIN
      TESTFIELD("Field ID",0);
      TESTFIELD("Line No.");
      TESTFIELD("Country/Region Code");
      CLEAR(CustomAddressFormatLines);
      CustomAddressFormatLine.RESET;
      CustomAddressFormatLine.SETRANGE("Country/Region Code","Country/Region Code");
      CustomAddressFormatLine.SETRANGE("Line No.","Line No.");
      CustomAddressFormatLine.SETCURRENTKEY("Country/Region Code","Line No.","Field Position");
      CustomAddressFormatLines.SETTABLEVIEW(CustomAddressFormatLine);
      CustomAddressFormatLines.RUNMODAL;

      BuildAddressFormat;
    END;

    LOCAL PROCEDURE UpdateRelatedCustomerAddressFormatLines@4();
    VAR
      CustomAddressFormatLine@1000 : Record 726;
      CountryRegion@1001 : Record 9;
    BEGIN
      CustomAddressFormatLine.SETRANGE("Country/Region Code","Country/Region Code");
      CustomAddressFormatLine.SETRANGE("Line No.","Line No.");
      CustomAddressFormatLine.DELETEALL;

      IF "Field ID" <> 0 THEN
        CountryRegion.CreateAddressFormatLine("Country/Region Code",1,"Field ID","Line No.");
    END;

    [External]
    PROCEDURE MoveLine@5(MoveBy@1000 : Integer);
    VAR
      CustomAddressFormat@1001 : Record 725;
    BEGIN
      CustomAddressFormat.SETRANGE("Country/Region Code","Country/Region Code");
      CustomAddressFormat.SETRANGE("Line Position","Line Position" + MoveBy);
      IF CustomAddressFormat.FINDFIRST THEN BEGIN
        CustomAddressFormat."Line Position" -= MoveBy;
        CustomAddressFormat.MODIFY;
        "Line Position" += MoveBy;
        MODIFY;
      END;
    END;

    [External]
    PROCEDURE LookupField@6();
    VAR
      CompanyInformation@1002 : Record 79;
      Field@1001 : Record 2000000041;
      FieldsLookup@1000 : Page 9806;
    BEGIN
      Field.SETFILTER(
        "No.",
        STRSUBSTNO(
          '%1|%2|%3|%4|%5|%6|%7|%8|%9',
          CompanyInformation.FIELDNO(Name),
          CompanyInformation.FIELDNO("Name 2"),
          CompanyInformation.FIELDNO(Address),
          CompanyInformation.FIELDNO("Address 2"),
          CompanyInformation.FIELDNO("Contact Person"),
          CompanyInformation.FIELDNO(City),
          CompanyInformation.FIELDNO("Post Code"),
          CompanyInformation.FIELDNO(County),
          CompanyInformation.FIELDNO("Country/Region Code")));
      Field.SETRANGE(TableNo,79);
      FieldsLookup.SETTABLEVIEW(Field);
      FieldsLookup.LOOKUPMODE(TRUE);
      IF FieldsLookup.RUNMODAL = ACTION::LookupOK THEN BEGIN
        FieldsLookup.GETRECORD(Field);
        VALIDATE("Field ID",Field."No.");
      END;
    END;

    BEGIN
    END.
  }
}

