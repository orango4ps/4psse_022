OBJECT Codeunit 5509 Graph Mgt - Inc Doc Attachment
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    Permissions=TableData 112=rm,
                TableData 114=rm,
                TableData 122=rm,
                TableData 124=rm,
                TableData 17=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      AttachmentCategoryLbl@1010 : TextConst '@@@={Locked};ENU=AL Attachment;NOR=AL Attachment';
      NoPermissionErr@1011 : TextConst '@@@="{Locked}; %1=table number that caused the error";ENU=No permission to update a related document (table %1).;NOR=No permission to update a related document (table %1).';

    [External]
    PROCEDURE UpdateIntegrationRecords@11(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      DummyIncomingDocumentAttachment@1002 : Record 133;
      GraphMgtGeneralTools@1001 : Codeunit 5465;
      RecRef@1019 : RecordRef;
    BEGIN
      RecRef.OPEN(DATABASE::"Incoming Document Attachment");
      GraphMgtGeneralTools.UpdateIntegrationRecords(RecRef,DummyIncomingDocumentAttachment.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@16();
    BEGIN
      UpdateIntegrationRecords(FALSE);
    END;

    [EventSubscriber(Table,133,OnAfterInsertEvent)]
    LOCAL PROCEDURE HandleOnAfterInsert@46(VAR Rec@1000 : Record 133;RunTrigger@1001 : Boolean);
    BEGIN
      UpdateRelatedDocument(Rec);
    END;

    [EventSubscriber(Table,133,OnAfterModifyEvent)]
    LOCAL PROCEDURE HandleOnAfterUpdate@47(VAR Rec@1000 : Record 133;VAR xRec@1001 : Record 133;RunTrigger@1002 : Boolean);
    BEGIN
      UpdateRelatedDocument(Rec);
    END;

    [EventSubscriber(Table,133,OnAfterRenameEvent)]
    LOCAL PROCEDURE HandleOnAfterRename@50(VAR Rec@1000 : Record 133;VAR xRec@1001 : Record 133;RunTrigger@1002 : Boolean);
    BEGIN
      UpdateRelatedDocument(Rec);
    END;

    [EventSubscriber(Table,133,OnAfterDeleteEvent)]
    LOCAL PROCEDURE HandleOnAfterDelete@51(VAR Rec@1000 : Record 133;RunTrigger@1001 : Boolean);
    BEGIN
      UpdateRelatedDocument(Rec);
    END;

    LOCAL PROCEDURE UpdateRelatedDocument@2(VAR IncomingDocumentAttachment@1000 : Record 133);
    VAR
      IncomingDocument@1002 : Record 130;
      GraphMgtGeneralTools@1005 : Codeunit 5465;
      IntegrationManagement@1001 : Codeunit 5150;
      DataTypeManagement@1006 : Codeunit 701;
      RelatedDocument@1003 : Variant;
      RelatedRecRef@1004 : RecordRef;
    BEGIN
      IF IncomingDocumentAttachment.ISTEMPORARY OR (NOT GraphMgtGeneralTools.IsApiEnabled) THEN
        EXIT;

      IF NOT IncomingDocument.GET(IncomingDocumentAttachment."Incoming Document Entry No.") THEN
        EXIT;

      IF NOT IncomingDocument.GetRecord(RelatedDocument) THEN
        EXIT;

      IF NOT DataTypeManagement.GetRecordRef(RelatedDocument,RelatedRecRef) THEN
        EXIT;

      IF NOT RelatedRecRef.WRITEPERMISSION THEN BEGIN
        SENDTRACETAG('00006WH',
          AttachmentCategoryLbl,
          VERBOSITY::Error,
          STRSUBSTNO(NoPermissionErr,RelatedRecRef.NUMBER),
          DATACLASSIFICATION::SystemMetadata);
        EXIT;
      END;

      IF NOT IntegrationManagement.IsIntegrationRecord(RelatedRecRef.NUMBER) THEN
        EXIT;

      RelatedRecRef.MODIFY;
    END;

    BEGIN
    END.
  }
}

