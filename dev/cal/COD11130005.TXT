OBJECT Codeunit 11130005 DateSelectionManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetDate@1100525001(VAR Date2@1100525002 : Date);
    BEGIN
      DateSelection(Date2);
    END;

    PROCEDURE GetWeek@1100525002(VAR WeekNo@1100525002 : Integer);
    VAR
      DummyYearNo@1100525000 : Integer;
    BEGIN
      GetWeekYear(WeekNo, DummyYearNo);
    END;

    PROCEDURE GetWeekYear@1100525007(VAR WeekNo@1100525002 : Integer;VAR YearNo@1100525004 : Integer);
    VAR
      StartDate@1100525001 : Date;
    BEGIN
      IF WeekNo IN [1..53] THEN BEGIN
        IF YearNo > 0 THEN
          StartDate := DWY2DATE(1, WeekNo, YearNo)
        ELSE
          StartDate := DWY2DATE(1, WeekNo);
      END;
      DateSelection(StartDate);
      IF StartDate <> 0D THEN BEGIN
        WeekNo := DATE2DWY(StartDate, 2);
        YearNo := DATE2DWY(StartDate, 3);
      END;
    END;

    PROCEDURE GetPeriod@1100525003(VAR PeriodString@1100525002 : Text[250]);
    VAR
      PeriodStart@1100525001 : Date;
      PeriodEnd@1100525000 : Date;
    BEGIN
      ConvertPeriodStringToDates(PeriodString, PeriodStart, PeriodEnd);
      PeriodSelection(PeriodStart, PeriodEnd);
      ConvertDatesToPeriodString(PeriodStart, PeriodEnd, PeriodString);
    END;

    LOCAL PROCEDURE DateSelection@1100525008(VAR Date2@1100525003 : Date);
    VAR
      PeriodSelectionPage@1100525000 : Page 11130228;
    BEGIN
      PeriodSelectionPage.SetDateSelection(Date2);
      IF PeriodSelectionPage.RUNMODAL <> ACTION::OK THEN
        ERROR('');
      PeriodSelectionPage.GetDateSelection(Date2);
    END;

    LOCAL PROCEDURE PeriodSelection@1100525004(VAR PeriodStart@1100525003 : Date;VAR PeriodEnd@1100525002 : Date);
    VAR
      PeriodSelectionPage@1100525000 : Page 11130228;
    BEGIN
      PeriodSelectionPage.SetPeriodSelection(PeriodStart, PeriodEnd);
      IF PeriodSelectionPage.RUNMODAL <> ACTION::OK THEN
        ERROR('');
      PeriodSelectionPage.GetPeriodSelection(PeriodStart, PeriodEnd);
    END;

    PROCEDURE ConvertDatesToPeriodString@1100525005(PeriodStart@1100525000 : Date;PeriodEnd@1100525001 : Date;VAR PeriodString@1100525002 : Text[250]);
    BEGIN
      CASE TRUE OF
          PeriodStart = 0D :
            PeriodString := STRSUBSTNO('%1%2', PeriodSeparator, PeriodEnd);
          PeriodEnd = 0D :
            PeriodString := STRSUBSTNO('%1%2', PeriodStart, PeriodSeparator);
          ELSE
            PeriodString := STRSUBSTNO('%1%2%3', PeriodStart, PeriodSeparator, PeriodEnd);
      END;
    END;

    PROCEDURE ConvertPeriodStringToDates@1100525006(PeriodString@1100525002 : Text[250];VAR PeriodStart@1100525001 : Date;VAR PeriodEnd@1100525000 : Date);
    VAR
      i@1100525003 : Integer;
    BEGIN
      i := STRPOS(PeriodString, PeriodSeparator);

      IF i > 0 THEN BEGIN
        IF NOT EVALUATE(PeriodStart, COPYSTR(PeriodString, 1, i - 1)) THEN
          PeriodStart := 0D;
        IF NOT EVALUATE(PeriodEnd, COPYSTR(PeriodString, i + STRLEN(PeriodSeparator))) THEN
          PeriodEnd := 0D;
      END ELSE BEGIN
        IF EVALUATE(PeriodStart, PeriodString) THEN;
        PeriodEnd := PeriodStart;
      END;
    END;

    LOCAL PROCEDURE PeriodSeparator@1100525000() : Text[2];
    BEGIN
      EXIT('..');
    END;

    BEGIN
    END.
  }
}

