OBJECT Codeunit 343 Dimension CaptionClass Mgmt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      GLSetupRead@1001 : Boolean;

    [EventSubscriber(Codeunit,42,OnResolveCaptionClass,"",Skip,Skip)]
    LOCAL PROCEDURE ResolveCaptionClass@3(CaptionArea@1001 : Text;CaptionExpr@1002 : Text;Language@1000 : Integer;VAR Caption@1006 : Text;VAR Resolved@1003 : Boolean);
    BEGIN
      IF CaptionArea = '1' THEN BEGIN
        Caption := DimCaptionClassTranslate(Language,CaptionExpr);
        Resolved := TRUE;
      END;
    END;

    LOCAL PROCEDURE DimCaptionClassTranslate@2(Language@1000 : Integer;CaptionExpr@1001 : Text) : Text;
    VAR
      Dim@1002 : Record 348;
      DimCaptionType@1003 : Text[80];
      DimCaptionRef@1004 : Text[80];
      DimOptionalParam1@1005 : Text[80];
      DimOptionalParam2@1006 : Text[80];
      CommaPosition@1007 : Integer;
    BEGIN
      // DIMCAPTIONTYPE
      // <DataType>   := [SubString]
      // <Length>     <= 10
      // <DataValue>  := 1..6
      // 1 to retrieve Code Caption of Global Dimension
      // 2 to retrieve Code Caption of Shortcut Dimension
      // 3 to retrieve Filter Caption of Global Dimension
      // 4 to retrieve Filter Caption of Shortcut Dimension
      // 5 to retrieve Code Caption of any kind of Dimensions
      // 6 to retrieve Filter Caption of any kind of Dimensions

      // DIMCAPTIONREF
      // <DataType>   := [SubString]
      // <Length>     <= 10
      // <DataValue>  :=
      // IF (<DIMCAPTIONTYPE> = 1) 1..2,<DIMOPTIONALPARAM1>,<DIMOPTIONALPARAM2>
      // IF (<DIMCAPTIONTYPE> = 2) 1..8,<DIMOPTIONALPARAM1>,<DIMOPTIONALPARAM2>
      // IF (<DIMCAPTIONTYPE> = 3) 1..2,<DIMOPTIONALPARAM1>,<DIMOPTIONALPARAM2>
      // IF (<DIMCAPTIONTYPE> = 4) 1..8,<DIMOPTIONALPARAM1>,<DIMOPTIONALPARAM2>
      // IF (<DIMCAPTIONTYPE> = 5) [Table]Dimension.[Field]Code,<DIMOPTIONALPARAM1>,<DIMOPTIONALPARAM2>
      // IF (<DIMCAPTIONTYPE> = 6) [Table]Dimension.[Field]Code,<DIMOPTIONALPARAM1>,<DIMOPTIONALPARAM2>

      // DIMOPTIONALPARAM1
      // <DataType>   := [SubString]
      // <Length>     <= 30
      // <DataValue>  := [String]
      // a string added before the dimension name

      // DIMOPTIONALPARAM2
      // <DataType>   := [SubString]
      // <Length>     <= 30
      // <DataValue>  := [String]
      // a string added after the dimension name

      IF NOT GetGLSetup THEN
        EXIT('');

      CommaPosition := STRPOS(CaptionExpr,',');
      IF CommaPosition > 0 THEN BEGIN
        DimCaptionType := COPYSTR(CaptionExpr,1,CommaPosition - 1);
        DimCaptionRef := COPYSTR(CaptionExpr,CommaPosition + 1);
        CommaPosition := STRPOS(DimCaptionRef,',');
        IF CommaPosition > 0 THEN BEGIN
          DimOptionalParam1 := COPYSTR(DimCaptionRef,CommaPosition + 1);
          DimCaptionRef := COPYSTR(DimCaptionRef,1,CommaPosition - 1);
          CommaPosition := STRPOS(DimOptionalParam1,',');
          IF CommaPosition > 0 THEN BEGIN
            DimOptionalParam2 := COPYSTR(DimOptionalParam1,CommaPosition + 1);
            DimOptionalParam1 := COPYSTR(DimOptionalParam1,1,CommaPosition - 1);
          END ELSE
            DimOptionalParam2 := '';
        END ELSE BEGIN
          DimOptionalParam1 := '';
          DimOptionalParam2 := '';
        END;
        CASE DimCaptionType OF
          '1':  // Code Caption - Global Dimension using No. as Reference
            CASE DimCaptionRef OF
              '1':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Global Dimension 1 Code",
                    GLSetup.FIELDCAPTION("Global Dimension 1 Code")));
              '2':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Global Dimension 2 Code",
                    GLSetup.FIELDCAPTION("Global Dimension 2 Code")));
            END;
          '2':  // Code Caption - Shortcut Dimension using No. as Reference
            CASE DimCaptionRef OF
              '1':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 1 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 1 Code")));
              '2':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 2 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 2 Code")));
              '3':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 3 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 3 Code")));
              '4':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 4 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 4 Code")));
              '5':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 5 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 5 Code")));
              '6':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 6 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 6 Code")));
              '7':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 7 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 7 Code")));
              '8':
                EXIT(
                  CodeCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 8 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 8 Code")));
            END;
          '3':  // Filter Caption - Global Dimension using No. as Reference
            CASE DimCaptionRef OF
              '1':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Global Dimension 1 Code",
                    GLSetup.FIELDCAPTION("Global Dimension 1 Code")));
              '2':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Global Dimension 2 Code",
                    GLSetup.FIELDCAPTION("Global Dimension 2 Code")));
            END;
          '4':  // Filter Caption - Shortcut Dimension using No. as Reference
            CASE DimCaptionRef OF
              '1':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 1 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 1 Code")));
              '2':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 2 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 2 Code")));
              '3':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 3 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 3 Code")));
              '4':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 4 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 4 Code")));
              '5':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 5 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 5 Code")));
              '6':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 6 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 6 Code")));
              '7':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 7 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 7 Code")));
              '8':
                EXIT(
                  FilterCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Shortcut Dimension 8 Code",
                    GLSetup.FIELDCAPTION("Shortcut Dimension 8 Code")));
            END;
          '5':  // Code Caption - using Dimension Code as Reference
            BEGIN
              IF Dim.GET(DimCaptionRef) THEN
                EXIT(DimOptionalParam1 + Dim.GetMLCodeCaption(Language) + DimOptionalParam2);
              EXIT(DimOptionalParam1);
            END;
          '6':  // Filter Caption - using Dimension Code as Reference
            BEGIN
              IF Dim.GET(DimCaptionRef) THEN
                EXIT(DimOptionalParam1 + Dim.GetMLFilterCaption(Language) + DimOptionalParam2);
              EXIT(DimOptionalParam1);
            END;
          //**4PS.sn
          '11012000':  // Name Caption - Global Dimension using No. as Reference
            CASE DimCaptionRef OF
              '1':
                EXIT(
                  DimNameCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Global Dimension 1 Code",
                    GLSetup.FIELDCAPTION("Global Dimension 1 Code")));
              '2':
                EXIT(
                  DimNameCaption(
                    Language,DimOptionalParam1,DimOptionalParam2,
                    GLSetup."Global Dimension 2 Code",
                    GLSetup.FIELDCAPTION("Global Dimension 2 Code")));
            END;
          //**4PS.en
        END;
      END;
      EXIT('');
    END;

    LOCAL PROCEDURE CodeCaption@5(Language@1002 : Integer;DimOptionalParam1@1003 : Text;DimOptionalParam2@1004 : Text;DimCode@1005 : Code[20];DimFieldCaption@1006 : Text[1024]) : Text;
    VAR
      Dim@1001 : Record 348;
    BEGIN
      IF Dim.GET(DimCode) THEN
        EXIT(DimOptionalParam1 + Dim.GetMLCodeCaption(Language) + DimOptionalParam2);
      EXIT(
        DimOptionalParam1 +
        DimFieldCaption +
        DimOptionalParam2);
    END;

    LOCAL PROCEDURE FilterCaption@6(Language@1002 : Integer;DimOptionalParam1@1003 : Text;DimOptionalParam2@1004 : Text;DimCode@1005 : Code[20];DimFieldCaption@1006 : Text[1024]) : Text;
    VAR
      Dim@1001 : Record 348;
    BEGIN
      IF Dim.GET(DimCode) THEN
        EXIT(DimOptionalParam1 + Dim.GetMLFilterCaption(Language) + DimOptionalParam2);
      EXIT(
        DimOptionalParam1 +
        DimFieldCaption +
        DimOptionalParam2);
    END;

    LOCAL PROCEDURE GetGLSetup@4() : Boolean;
    BEGIN
      IF NOT GLSetupRead THEN
        GLSetupRead := GLSetup.GET;
      EXIT(GLSetupRead);
    END;

    LOCAL PROCEDURE DimNameCaption@1100525002(Language@1002 : Integer;DimOptionalParam1@1003 : Text[80];DimOptionalParam2@1004 : Text[80];DimCode@1005 : Code[20];DimFieldCaption@1006 : Text[1024]) : Text[80];
    VAR
      Dim@1001 : Record 348;
    BEGIN
      //**4PS
      IF Dim.GET(DimCode) THEN
        EXIT(DimOptionalParam1 + Dim.GetMLName(Language) + DimOptionalParam2);
      EXIT(
        DimOptionalParam1 +
        DimFieldCaption +
        DimOptionalParam2);
    END;

    BEGIN
    END.
  }
}

