OBJECT Report 11125707 Gen. Prj. Act. Cap. Plan. Mes.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projektaktivit„t-Kapazit„tsplanungsmeldungen;
               ENU=Generate Project Activity Capacity Planning Messages;
               NLD=Genereren projectactiviteit-capaciteitsplanningsboodschappen;
               NOR=Generer kapasitetsplanleggingsbeskjeder for prosjektaktivitet;
               SVE=Generera meddelanden om kapacitetsplanering f”r projektaktivitet];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PlanMessCounter := 0;
                 END;

    OnPreReport=BEGIN
                  ValidatePeriodStart();
                  ValidateDuration();

                  CalendarCode := ProjActCapComMgt.GetCalendarCode();

                  DateTimeRun := CURRENTDATETIME;

                  Window.OPEN(STRSUBSTNO('%1 #3##########\%2 #4##########',
                    ProjActRec.FIELDCAPTION("Project No."), ProjActRec.FIELDCAPTION(Code)));
                END;

    OnPostReport=BEGIN
                   Window.CLOSE;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               DataItemTableView=SORTING(No.)
                                 WHERE(Project Status=FILTER(Preparation|Production));
               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(3, "No.");
                                  Window.UPDATE(4, '');

                                  PlanMessRec.RESET;
                                  PlanMessRec.SETRANGE("Project No.", "No.");
                                  PlanMessRec.DELETEALL;
                                  NextPlanMessLineNo := 1;

                                  ProjActRec.RESET;
                                  ProjActRec.SETCURRENTKEY("Project No.", Code);
                                  ProjActRec.SETRANGE("Project No.", "No.");
                                  IF ProjActRec.FINDSET(FALSE,FALSE) THEN BEGIN
                                    REPEAT
                                      Window.UPDATE(4, ProjActRec.Code);
                                      CheckActivity(ProjActRec."Project No.", ProjActRec.Code);
                                      CheckActivityRequestPlanned(ProjActRec."Project No.", ProjActRec.Code);
                                    UNTIL ProjActRec.NEXT = 0;
                                  END;

                                  Window.UPDATE(4, '');
                                  CheckExceedCapacity("No.");

                                  COMMIT;
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               EVALUATE(Duration, '<3M>');
             END;

      OnOpenPage=BEGIN
                   IF PeriodStart < TODAY THEN
                     PeriodStart := TODAY;
                 END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Startdatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  NotBlank=Yes;
                  SourceExpr=PeriodStart;
                  OnValidate=BEGIN
                               ValidatePeriodStart();
                             END;
                              }

      { 1100525003;2;Field  ;
                  CaptionML=[DEU=Dauer;
                             ENU=Duration;
                             NLD=Tijdsduur;
                             NOR=Varighet;
                             SVE=Duration];
                  NotBlank=Yes;
                  SourceExpr=Duration;
                  OnValidate=BEGIN
                               ValidateDuration();
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PlanMessRec@1100525008 : Record 11125712;
      ProjActRec@1100525013 : Record 11012431;
      ReqActCapRec@1100525006 : Record 11125708;
      ReqActCapByDayRec@1100525016 : Record 11125709;
      AssignActCapByDayRec@1100525002 : Record 11125711;
      ProjActCapComMgt@1100525003 : Codeunit 11125706;
      Window@1100525007 : Dialog;
      PeriodStart@1100525000 : Date;
      PeriodEnd@1100525001 : Date;
      DateTimeRun@1100525022 : DateTime;
      Duration@1100525024 : DateFormula;
      NextPlanMessLineNo@1100525011 : Integer;
      PlanMessCounter@1100525021 : Integer;
      CalendarCode@1100525004 : Code[10];
      PlanMessage@1100525009 : Text[100];
      Text001@1100525014 : TextConst 'DEU=Startdatum darf nicht leer sein.;ENU=Starting Date may not be empty.;NLD=Startdatum mag niet leeg zijn.;NOR=Startdato kan ikke v‘re tom;SVE=Startdatumet f†r inte vara tomt.';
      Text002@1100525015 : TextConst 'DEU=Enddatum darf nicht kleiner als Startdatum sein.;ENU=Ending Date may not be less than Starting Date.;NLD=Einddatum mag niet kleiner zijn dan startdatum.;NOR=Sluttdato kan ikke v‘re tidligere enn startdato;SVE=Slutdatum f†r inte infalla f”re startdatum.';
      Text010@1100525012 : TextConst 'DEU=%1 geplant %2: Summe geplant (%3) gr”sser als verfgbar (%4);ENU=%1 Planned %2: Total planned (%3) greater than Available (%4);NLD=%1 Gepland %2: Totaal gepland (%3) groter dan beschikbaar (%4);NOR=%1 Planlagt %2: Totalt planlagt (%3) st›rre enn tilgjengelig (%4);SVE=%1 planerad %2: Totalt planerad (%3) „r st”rre „n tillg„ngligt (%4)';
      Text011@1100525017 : TextConst 'DEU=%1..%2: Gefragt (%3) ungleich geplant (%4);ENU=%1..%2: Requested (%3) unequal to Planned (%4);NLD=%1..%2: Gevraagd (%3) ongelijk aan gepland (%4);NOR=%1..%2: Anmodet (%3) ikke likt planlagt (%4);SVE=%1..%2: Det som har beg„rts (%3) „r inte lika med det som planerats (%4)';
      Text012@1100525018 : TextConst 'DEU=Disziplin %1 der Teamart nicht geplant (Anfrage %2..%3);ENU=Discipline %1 of Team Type not planned (request %2..%3);NLD=Discipline %1 van ploegsoort niet gepland (aanvraag %2..%3);NOR=Disiplin %1 for lagtype ike planlagt (foresp›rsel %2..%3);SVE=Disciplin %1 av teamtyp „r inte planerad (f”rfr†gan %2..%3)';
      Text013@1100525019 : TextConst 'DEU=Keine gefragte Kapazit„t vorhanden;ENU=No requested capacity present;NLD=Geen gevraagde capaciteit aanwezig;NOR=Ingen anmodet kapasitet tilstede;SVE=Den beg„rda kapaciteten finns inte';
      Text014@1100525020 : TextConst 'DEU=Keine geplante Kapazit„t vorhanden;ENU=No planned capacity present;NLD=Geen geplande capaciteit aanwezig;NOR=Ingen planlagt kapasitet tilstede;SVE=Det finns ingen planerad kapacitet';
      Text015@1100525023 : TextConst 'DEU=%1 (%2) gr”sser als %3 (%4);ENU=%1 (%2) greater than %3 (%4);NLD=%1 (%2) groter dan %3 (%4);NOR=%1 (%2) st›rre enn %3 (%4);SVE=%1 (%2) „r st”rre „n %3 (%4).';

    PROCEDURE ValidatePeriodStart@1100525006();
    BEGIN
      IF PeriodStart = 0D THEN
        ERROR(Text001);
    END;

    PROCEDURE ValidateDuration@1100525007();
    BEGIN
      PeriodEnd := CALCDATE(Duration, PeriodStart);
      IF PeriodEnd < PeriodStart THEN
        ERROR(Text002);
    END;

    PROCEDURE CheckActivity@1100525005(ProjNo@1100525001 : Code[20];Activity@1100525000 : Code[10]);
    BEGIN
      ProjActRec.CALCFIELDS("Budgetted Hours", "Requested Hours", "Assigned Hours");

      IF ProjActRec."Planned Hours" > ProjActRec."Budgetted Hours" THEN BEGIN
        PlanMessage := STRSUBSTNO(Text015,
          ProjActRec.FIELDCAPTION("Planned Hours"), ProjActRec."Planned Hours",
          ProjActRec.FIELDCAPTION("Budgetted Hours"), ProjActRec."Budgetted Hours");
        InsertPlanMessage(ProjNo, PlanMessage, PlanMessRec.Process::Activity, 0, '', '', Activity);
      END;

      IF ProjActRec."Requested Hours" > ProjActRec."Planned Hours" THEN BEGIN
        PlanMessage := STRSUBSTNO(Text015,
          ProjActRec.FIELDCAPTION("Requested Hours"), ProjActRec."Requested Hours",
          ProjActRec.FIELDCAPTION("Planned Hours"), ProjActRec."Planned Hours");
        InsertPlanMessage(ProjNo, PlanMessage, PlanMessRec.Process::Request, 0, '', '', Activity);
      END;

      IF ProjActRec."Assigned Hours" > ProjActRec."Requested Hours" THEN BEGIN
        PlanMessage := STRSUBSTNO(Text015,
          ProjActRec.FIELDCAPTION("Assigned Hours"), ProjActRec."Assigned Hours",
          ProjActRec.FIELDCAPTION("Requested Hours"), ProjActRec."Requested Hours");
        InsertPlanMessage(ProjNo, PlanMessage, PlanMessRec.Process::Assign, 0, '', '', Activity);
      END;
    END;

    PROCEDURE CheckActivityRequestPlanned@1100525002(ProjNo@1100525000 : Code[20];Activity@1100525001 : Code[10]);
    BEGIN
      //* Requested on activity (period independent)
      ReqActCapByDayRec.RESET;
      ReqActCapByDayRec.SETCURRENTKEY("Project No.", "Activity Code", "Resource Type", Date);
      ReqActCapByDayRec.SETRANGE("Project No.", ProjNo);
      ReqActCapByDayRec.SETRANGE("Activity Code", Activity);
      ReqActCapByDayRec.SETRANGE("Resource Type", ReqActCapByDayRec."Resource Type"::Discipline);
      IF ReqActCapByDayRec.ISEMPTY THEN BEGIN
        InsertPlanMessage(ProjNo, Text013, PlanMessRec.Process::Request, 0, '', '', Activity);
        EXIT;
      END;

      //* Assigned (planned) on activity (period independent)
      AssignActCapByDayRec.RESET;
      AssignActCapByDayRec.SETCURRENTKEY("Project No.", "Activity Code", "Resource Type", Date);
      AssignActCapByDayRec.SETRANGE("Project No.", ProjNo);
      AssignActCapByDayRec.SETRANGE("Activity Code", Activity);
      AssignActCapByDayRec.SETRANGE("Resource Type", AssignActCapByDayRec."Resource Type"::Employee);
      IF AssignActCapByDayRec.ISEMPTY THEN BEGIN
        InsertPlanMessage(ProjNo, Text014, PlanMessRec.Process::Assign, 0, '', '', Activity);
        EXIT;
      END;

      //* Requested on activity in period
      ReqActCapByDayRec.SETRANGE(Date, PeriodStart, PeriodEnd);
      ReqActCapByDayRec.CALCSUMS(Hours);

      //* Assigned (planned) on activity in period
      AssignActCapByDayRec.SETRANGE(Date, PeriodStart, PeriodEnd);
      AssignActCapByDayRec.CALCSUMS(Hours);

      IF ReqActCapByDayRec.Hours <> AssignActCapByDayRec.Hours THEN BEGIN
        PlanMessage := STRSUBSTNO(Text011, PeriodStart, PeriodEnd, ReqActCapByDayRec.Hours, AssignActCapByDayRec.Hours);
        InsertPlanMessage(ProjNo, PlanMessage, PlanMessRec.Process::Assign, 0, '', '', Activity);
      END;

      CheckTeamTypeDiscipline(ProjNo, Activity);  //* Called here because should only be checked if assigned on activity
    END;

    PROCEDURE CheckTeamTypeDiscipline@1100525003(ProjNo@1100525000 : Code[20];Activity@1100525001 : Code[10]);
    VAR
      TeamTypeResourceRec@1100525002 : Record 11125707;
    BEGIN
      ReqActCapRec.RESET;
      ReqActCapRec.SETCURRENTKEY("Project No.", "Activity Code", "Resource Type", "Resource Code");
      ReqActCapRec.SETRANGE("Project No.", ProjNo);
      ReqActCapRec.SETRANGE("Activity Code", Activity);
      ReqActCapRec.SETRANGE("Resource Type", ReqActCapRec."Resource Type"::TeamType);
      ReqActCapRec.SETFILTER("Resource Code", '<>%1', '');
      ReqActCapRec.SETFILTER("Starting Date", '<=%1', PeriodEnd);
      ReqActCapRec.SETFILTER("Ending Date", '>=%1', PeriodStart);
      ReqActCapRec.SETFILTER("Plan Hours", '>%1', 0);
      IF ReqActCapRec.FINDSET(FALSE,FALSE) THEN BEGIN
        AssignActCapByDayRec.RESET;
        AssignActCapByDayRec.SETRANGE("Project No.", ReqActCapRec."Project No.");
        AssignActCapByDayRec.SETRANGE("Activity Code", ReqActCapRec."Activity Code");
        AssignActCapByDayRec.SETRANGE("Line No. Request", ReqActCapRec."Line No.");
        AssignActCapByDayRec.SETRANGE(Date, PeriodStart, PeriodEnd);
        AssignActCapByDayRec.SETRANGE("Resource Type", AssignActCapByDayRec."Resource Type"::Employee);
        REPEAT
          TeamTypeResourceRec.SETRANGE("Team Type Code", ReqActCapRec."Resource Code");
          TeamTypeResourceRec.SETRANGE("Resource Type", TeamTypeResourceRec."Resource Type"::Discipline);
          TeamTypeResourceRec.SETFILTER("Resource Code", '<>%1', '');
          TeamTypeResourceRec.SETFILTER(Quantity, '>%1', 0);
          TeamTypeResourceRec.SETFILTER(Percentage, '>%1', 0);
          IF TeamTypeResourceRec.FINDSET(FALSE,FALSE) THEN BEGIN
            REPEAT
              AssignActCapByDayRec.SETRANGE(Discipline, TeamTypeResourceRec."Resource Code");
              IF AssignActCapByDayRec.ISEMPTY THEN BEGIN
                PlanMessage := STRSUBSTNO(Text012,
                  TeamTypeResourceRec."Resource Code", ReqActCapRec."Starting Date", ReqActCapRec."Ending Date");
                InsertPlanMessage(ProjNo, PlanMessage, PlanMessRec.Process::Assign,
                  PlanMessRec."Resource Type"::TeamType, TeamTypeResourceRec."Team Type Code", '', Activity);
              END;
            UNTIL TeamTypeResourceRec.NEXT = 0;
          END;
        UNTIL ReqActCapRec.NEXT = 0;
      END;
    END;

    PROCEDURE CheckExceedCapacity@1100525000(ProjNo@1100525003 : Code[20]);
    VAR
      TmpResourceDateRec@1100525000 : TEMPORARY Record 11012188;
      JobsSetup@1100528400 : Record 315;
      ProjectTimeTable@1100528401 : Record 11020684;
      PlanDate@1100525007 : Date;
      ResourceType@1100525004 : Option;
      DateIsWorkingDate@1100525002 : Boolean;
      HoursAvailable@1100525001 : Integer;
      ResourceCode@1100525005 : Code[20];
      PlantNo@1100525006 : Code[10];
      ProjResPlanHrs@1100525008 : Decimal;
      ResourcePlanHrs@1100525009 : Decimal;
    BEGIN
      JobsSetup.GET;
      JobsSetup.TESTFIELD("Project Time Table");
      ProjectTimeTable.GET(JobsSetup."Project Time Table");

      AssignActCapByDayRec.RESET;
      AssignActCapByDayRec.SETCURRENTKEY("Project No.", "Resource Type", "Resource Code", "Plant No.", Date);
      AssignActCapByDayRec.SETRANGE("Project No.", ProjNo);
      AssignActCapByDayRec.SETRANGE(Date, PeriodStart, PeriodEnd);
      IF AssignActCapByDayRec.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          //* Insert a (one) record by resource/date, and count the hours
          IF NOT TmpResourceDateRec.GET(
            AssignActCapByDayRec."Resource Type", '',
            AssignActCapByDayRec."Resource Code", '',
            AssignActCapByDayRec."Plant No.", AssignActCapByDayRec.Date)
          THEN BEGIN
            TmpResourceDateRec."Record Type" := AssignActCapByDayRec."Resource Type";
            TmpResourceDateRec."Rate Group" := '';
            TmpResourceDateRec."Estimate No." := AssignActCapByDayRec."Resource Code";
            TmpResourceDateRec."Service Location No." := '';
            TmpResourceDateRec."Rate Code" := AssignActCapByDayRec."Plant No.";
            TmpResourceDateRec."Starting Date" := AssignActCapByDayRec.Date;
            TmpResourceDateRec."Cost Rate" := AssignActCapByDayRec.Hours;
            TmpResourceDateRec.INSERT;
          END ELSE BEGIN
            TmpResourceDateRec."Cost Rate" := TmpResourceDateRec."Cost Rate" + AssignActCapByDayRec.Hours;
            TmpResourceDateRec.MODIFY;
          END;
        UNTIL AssignActCapByDayRec.NEXT = 0;
      END;

      TmpResourceDateRec.RESET;
      IF TmpResourceDateRec.FINDSET(FALSE,FALSE) THEN BEGIN
        AssignActCapByDayRec.RESET;
        AssignActCapByDayRec.SETCURRENTKEY("Resource Type", "Resource Code", "Plant No.", Date);
        REPEAT
          ResourceType := TmpResourceDateRec."Record Type";
          ResourceCode := TmpResourceDateRec."Estimate No.";
          PlantNo := TmpResourceDateRec."Rate Code";
          PlanDate := TmpResourceDateRec."Starting Date";
          ProjResPlanHrs := TmpResourceDateRec."Cost Rate";

          //* Assigned (planned) on resource/date
          AssignActCapByDayRec.SETRANGE("Resource Type", ResourceType);
          AssignActCapByDayRec.SETRANGE("Resource Code", ResourceCode);
          AssignActCapByDayRec.SETRANGE("Plant No.", PlantNo);
          AssignActCapByDayRec.SETRANGE(Date, PlanDate);
          AssignActCapByDayRec.CALCSUMS(Hours);
          ResourcePlanHrs := AssignActCapByDayRec.Hours;

          //* Available for resource/date
          HoursAvailable := 0;
          DateIsWorkingDate := TRUE;
          IF CalendarCode <> '' THEN
            DateIsWorkingDate := ProjActCapComMgt.DateIsWorkingDate(CalendarCode, PlanDate);
          IF DateIsWorkingDate THEN BEGIN
            IF ResourceType <> AssignActCapByDayRec."Resource Type"::Plant THEN
              HoursAvailable := ProjActCapComMgt.CalcAvailableEmplHoursOnDate(ResourceCode, PlanDate)
            ELSE BEGIN
              IF ProjActCapComMgt.CheckPlantNoAvailableOnDate(ResourceCode, PlantNo, PlanDate) THEN
                HoursAvailable := HoursAvailable + ProjectTimeTable.GetHoursByDay(PlanDate);
            END;
          END;

          IF ResourcePlanHrs > HoursAvailable THEN BEGIN
            PlanMessage := STRSUBSTNO(Text010, PlanDate, ProjResPlanHrs, ResourcePlanHrs, HoursAvailable);
            IF ResourceType <> AssignActCapByDayRec."Resource Type"::Plant THEN
              InsertPlanMessage(ProjNo, PlanMessage, PlanMessRec.Process::Assign,
                PlanMessRec."Resource Type"::Employee, ResourceCode, '', '')
            ELSE
              InsertPlanMessage(ProjNo, PlanMessage, PlanMessRec.Process::Assign,
                PlanMessRec."Resource Type"::Plant, ResourceCode, PlantNo, '');
          END;
        UNTIL TmpResourceDateRec.NEXT = 0;
      END;
    END;

    PROCEDURE InsertPlanMessage@1100525001(ProjectNo@1100525000 : Code[20];MessDescription@1100525004 : Text[100];Process@1100525006 : Option;ResourceType@1100525001 : Option;ResourceCode@1100525002 : Code[20];PlantNo@1100525003 : Code[10];Activity@1100525005 : Code[10]);
    BEGIN
      PlanMessRec.INIT;
      PlanMessRec."Project No." := ProjectNo;
      PlanMessRec."Line No." := NextPlanMessLineNo;
      PlanMessRec.Description := MessDescription;
      PlanMessRec.Process := Process;
      PlanMessRec."Resource Type" := ResourceType;
      PlanMessRec."Resource Code" := ResourceCode;
      PlanMessRec."Plant No." := PlantNo;
      PlanMessRec."Activity Code" := Activity;
      PlanMessRec."Created on" := DateTimeRun;
      PlanMessRec.INSERT;
      NextPlanMessLineNo := NextPlanMessLineNo + 1;
      PlanMessCounter := PlanMessCounter + 1;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

